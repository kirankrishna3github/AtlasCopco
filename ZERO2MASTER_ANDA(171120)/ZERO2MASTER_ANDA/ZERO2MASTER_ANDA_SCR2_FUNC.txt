*&---------------------------------------------------------------------*
*&  Include           ZERO2MASTER_ANDA_SCR2_FUNC
*&---------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  APPEND_08b1_p1
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM append_08b1_p1 .
  TYPES: BEGIN OF l_typ_tfdir,
           funcname TYPE tfdir-funcname,
           pname    TYPE tfdir-pname,
         END OF l_typ_tfdir.

  DATA: l_rec_tfdir TYPE l_typ_tfdir,
        lv_line     TYPE i,
        l_tab_tfdir TYPE TABLE OF l_typ_tfdir.

  TYPES: BEGIN OF l_typ_trdir,
           name     TYPE trdir-name,
           funcname TYPE tfdir-funcname,
         END OF l_typ_trdir.

  DATA: l_rec_trdir TYPE l_typ_trdir,
        l_tab_trdir TYPE TABLE OF l_typ_trdir.

*  CHECK sy-uname = 'TEST'.
  IF 08b1_cb1 IS NOT INITIAL.
    CASE gc_x.
      WHEN 08b1_rb1.
        SELECT name
          INTO CORRESPONDING FIELDS OF TABLE l_tab_trdir
          FROM trdir
         WHERE ( cnam = 08b1_p0 OR unam = 08b1_p0 )
           AND subc = '1'.    "Executable
        IF sy-subrc = 0.
          LOOP AT l_tab_trdir INTO l_rec_trdir.
            IF l_rec_trdir-name(1) = '/'.
              CONTINUE.
            ENDIF.
            IF  NOT ( ( l_rec_trdir-name(1) = 'Y' )
                OR ( l_rec_trdir-name(1) = 'Z' ) ).
              CONTINUE.
            ENDIF.
            08b1_p1-sign = 'I'.
            08b1_p1-option = 'EQ'.
            08b1_p1-low =  l_rec_trdir-name.
            APPEND 08b1_p1.
          ENDLOOP.
        ENDIF.
      WHEN 08b1_rb2.
        SELECT funcname
               pname
          INTO CORRESPONDING FIELDS OF TABLE l_tab_tfdir
          FROM tfdir
          INNER JOIN trdir
            ON tfdir~pname = trdir~name
          WHERE (   trdir~cnam = 08b1_p0
                  OR trdir~unam = 08b1_p0 )
          AND   subc = '1'.    "Executable
        IF sy-subrc = 0.
          LOOP AT l_tab_tfdir INTO l_rec_tfdir.
            08b1_p1-sign = 'I'.
            08b1_p1-option = 'EQ'.
            08b1_p1-low =  l_rec_tfdir-funcname.
            APPEND 08b1_p1.
          ENDLOOP.
        ELSE.
          MESSAGE ID '00' TYPE 'E' NUMBER '001'
             WITH 'Program ID is invalid'(x03).
          EXIT.
        ENDIF.
      WHEN 08b1_rb3.
   "check vseoclass, refer to YCL_CWU_UTILITY=>SET_OBJECTLIST_SUB1_08B

      WHEN OTHERS.
    ENDCASE.

    DELETE ADJACENT DUPLICATES FROM 08b1_p1.

  ENDIF.
ENDFORM.                    " APPEND_08b1_p1
*&---------------------------------------------------------------------*
*&      Form  F_F4_HELP_08B1_P1
*&---------------------------------------------------------------------*
*       F4 help on object select
*----------------------------------------------------------------------*
FORM f_f4_help_08b1_p1.
  TYPES BEGIN OF ltyp_dynpfields.
          INCLUDE TYPE dynpread.
  TYPES END OF ltyp_dynpfields.

  DATA: ls_dynpfields TYPE ltyp_dynpfields,
        lt_dynpfields TYPE TABLE OF ltyp_dynpfields.

  DATA ls_object_type TYPE seu_obj.
*-----------------------------------------------------------------------
  CASE  gc_x.
    WHEN 08b1_rb1.
      ls_object_type = 'PROG'.
    WHEN 08b1_rb2.
      ls_object_type = 'FF'.
    WHEN 08b1_rb3.
      ls_object_type = 'CLAS'.
    WHEN OTHERS.
  ENDCASE.
  IF gv_option_ind = 'L'.
    ls_dynpfields-fieldname = '08B1_P1-LOW'.
  ELSE.
    ls_dynpfields-fieldname = '08B1_P1-HIGH'.
  ENDIF.
  APPEND ls_dynpfields TO lt_dynpfields.

  CALL FUNCTION 'DYNP_VALUES_READ'
    EXPORTING
        dyname               = sy-repid
        dynumb               = sy-dynnr
    TABLES
        dynpfields           = lt_dynpfields
    EXCEPTIONS
        OTHERS.

  READ TABLE lt_dynpfields INDEX 1 INTO ls_dynpfields.

  IF gv_option_ind = 'L'.
    CALL FUNCTION 'REPOSITORY_INFO_SYSTEM_F4'
      EXPORTING
        object_type          = ls_object_type
        object_name          = ls_dynpfields-fieldvalue
      IMPORTING
        object_name_selected = 08b1_p1-low
      EXCEPTIONS
        cancel               = 1
        wrong_type           = 2
        OTHERS               = 3.
  ELSE.
    CALL FUNCTION 'REPOSITORY_INFO_SYSTEM_F4'
      EXPORTING
        object_type          = ls_object_type
        object_name          = ls_dynpfields-fieldvalue
      IMPORTING
        object_name_selected = 08b1_p1-high
      EXCEPTIONS
        cancel               = 1
        wrong_type           = 2
        OTHERS               = 3.
  ENDIF.

ENDFORM.                    " F_F4_HELP_08B1_P1
*&---------------------------------------------------------------------*
*&      Form  f_f4_help_filepath
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM f_f4_help_filepath .
  FIELD-SYMBOLS: <lfs_scrname> TYPE ANY.
  DATA: lt_filetable TYPE filetable,
        lv_rc         TYPE i,
        lv_title      TYPE string,
        lv_extend     TYPE string,
        lv_folder     TYPE string,
        lv_dir        TYPE string,
        lv_def_file   TYPE string .

  CASE gv_f4_ind.

    WHEN 'F'.
      lv_dir = gv_defpath_08b.
      lv_title = 'Select Download File'(t71).
      lv_extend = 'All Files (*.*)'(t70).
      CASE gv_screenname.
        WHEN '08B4_P1'.
          lv_def_file = 'Step_count.csv'(t58).
        WHEN '00A_P1'.
        WHEN OTHERS.
      ENDCASE.
      CALL METHOD cl_gui_frontend_services=>file_open_dialog
        EXPORTING
          window_title            = lv_title
          default_extension       = lv_extend
          default_filename        = lv_def_file
          initial_directory       = lv_dir
        CHANGING
          file_table              = lt_filetable
          rc                      = lv_rc
        EXCEPTIONS
          file_open_dialog_failed = 1
          cntl_error              = 2
          error_no_gui            = 3
          not_supported_by_gui    = 4
          OTHERS                  = 5.
      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                   WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      ELSE.
        ASSIGN (gv_screenname) TO <lfs_scrname>.
        READ TABLE lt_filetable INTO <lfs_scrname> INDEX 1.
      ENDIF.
    WHEN 'D'.

      ASSIGN (gv_screenname) TO <lfs_scrname>.
      CALL METHOD cl_gui_frontend_services=>directory_get_current
        CHANGING
          current_directory            = lv_dir
        EXCEPTIONS
          directory_get_current_failed = 1
          cntl_error                   = 2
          error_no_gui                 = 3
          not_supported_by_gui         = 4
          OTHERS                       = 5.
      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                   WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      ENDIF.
      lv_title = 'Select a directory'(t79).
      CALL METHOD cl_gui_frontend_services=>directory_browse
        EXPORTING
          window_title         = lv_title
          initial_folder       = lv_dir
        CHANGING
          selected_folder      = lv_folder
        EXCEPTIONS
          cntl_error           = 1
          error_no_gui         = 2
          not_supported_by_gui = 3
          OTHERS               = 4.
      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                   WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      ENDIF.
      <lfs_scrname> = lv_folder.
    WHEN OTHERS.
  ENDCASE.


ENDFORM.                    " f_f4_help_filepath

*&---------------------------------------------------------------------*
*&      Form  FRM_04A1_SCREEN
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM frm_04a1_screen .
  IF 04a_rb3 = gc_x.
    LOOP AT SCREEN.
      IF screen-name = '04A_P4'.
        screen-active = '1'.
        screen-invisible = '0'.
        MODIFY SCREEN.
      ENDIF.
    ENDLOOP.
  ELSE.
    LOOP AT SCREEN.
      IF screen-name = '04A_P4'.
        screen-active = '0'.
        screen-invisible = '1'.
        MODIFY SCREEN.
      ENDIF.
    ENDLOOP.
  ENDIF.
ENDFORM.                    " FRM_04A1_SCREEN
