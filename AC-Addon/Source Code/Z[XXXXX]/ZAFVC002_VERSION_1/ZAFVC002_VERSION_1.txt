*&---------------------------------------------------------------------
*& Title: Correction report update confirmation counter on AFVC
*&---------------------------------------------------------------------
 REPORT ZAFVC002_VERSION_1.
*----------------------------------------------------------------------
*
* OSS-Note 335559
*
* Correction Report wrong confirmation counter on AFVC
*
* 1. read order header caufv
* 2. read order operation AFVC
* 3. read confirmations AFRU
* 4. quantity of confirmations <> confirmation counter on AFVC-RMZ
*    4.1. write protocol
*    4.2. if not test run update AFVC-RMZHL
*
*----------------------------------------------------------------------
 TABLES: CAUFV, AFVC, AFRU, *AFVC.

 DATA: YES VALUE 'X',
       NO  VALUE SPACE,
       COUNTER TYPE I,                        "counter for DESCRIBE
       FIRST VALUE 'X',                       "first run
       COMMENT(30) TYPE C,                    " comment for screen log
       COUNTER_AFRU_RMZHL TYPE I,             " select by group
       RUECK LIKE AFRU-RUECK.                 " select by grounp


 DATA: BEGIN OF CAUFV_ITAB OCCURS 10.         " oders
         INCLUDE STRUCTURE CAUFV.
 DATA: END OF CAUFV_ITAB.

 DATA: BEGIN OF AFVC_ITAB OCCURS 10.             " operations
         INCLUDE STRUCTURE AFVC.
 DATA: END OF AFVC_ITAB.

 DATA: BEGIN OF AFRU_ITAB OCCURS 10.             " confirmations
         INCLUDE STRUCTURE AFRU.
 DATA: END OF AFRU_ITAB.

 DATA: BEGIN OF AFRU_TEMP OCCURS 10,
         RUECK TYPE AFRU-RUECK,
         RMZHL TYPE AFRU-RMZHL,
       END OF AFRU_TEMP.

 PARAMETER:  TEST DEFAULT YES.              "Testrun


 SELECT-OPTIONS: ORDER_NO FOR CAUFV-AUFNR DEFAULT '?'." order

*  * Select all orders from VIAUKST for date/order-no.
 SELECT * FROM CAUFV INTO TABLE CAUFV_ITAB
          WHERE AUFNR IN ORDER_NO.

 DESCRIBE TABLE CAUFV_ITAB LINES COUNTER.
 IF COUNTER < 1.                                " No orders at all
   WRITE / 'No orders selected'.
 ELSE.
   PERFORM SELECT_AFVC.
 ENDIF.

 IF FIRST EQ YES.
   WRITE / 'No incorrect records found'.
 ENDIF.

*---------------------------------------------------------------------
* select_afvc    Select operations to orders
*---------------------------------------------------------------------
 FORM SELECT_AFVC.

   SORT CAUFV_ITAB.
   LOOP AT CAUFV_ITAB.

     REFRESH AFVC_ITAB.

* Select all operations for order

     SELECT * FROM AFVC INTO TABLE AFVC_ITAB
              WHERE AUFPL EQ CAUFV_ITAB-AUFPL.

     DESCRIBE TABLE AFVC_ITAB LINES COUNTER.
     IF COUNTER < 1.
       WRITE / 'No operations selected'.      " No operations at all
     ELSE.
       PERFORM SELECT_AFRU.
     ENDIF.

   ENDLOOP.

 ENDFORM.                    "SELECT_AFVC
*---------------------------------------------------------------------
* select_afru    Select confirmations to order operations
*---------------------------------------------------------------------
 FORM SELECT_AFRU.

   SORT AFVC_ITAB.
   LOOP AT AFVC_ITAB.

     CLEAR: RUECK, COUNTER_AFRU_RMZHL.

*    SELECT RUECK COUNT(*)
*       INTO (RUECK, COUNTER_AFRU_RMZHL)
*       FROM AFRU
*       WHERE RUECK EQ AFVC_ITAB-RUECK
*       GROUP BY RUECK.
*    ENDSELECT.
     SELECT RUECK RMZHL
        INTO TABLE AFRU_TEMP
        FROM AFRU
        WHERE RUECK EQ AFVC_ITAB-RUECK
        ORDER BY RUECK RMZHL DESCENDING.
     IF SY-SUBRC NE 0.
       CONTINUE.
     ENDIF.

     READ TABLE AFRU_TEMP INDEX 1.
     RUECK               = AFRU_TEMP-RUECK .
     COUNTER_AFRU_RMZHL  = AFRU_TEMP-RMZHL.

     PERFORM CHECK_DATA.
   ENDLOOP.

 ENDFORM.                    "SELECT_AFVC
*---------------------------------------------------------------------
* check_data     check counter on afvc with summary of afru
*---------------------------------------------------------------------
 FORM CHECK_DATA.

   IF NOT AFVC_ITAB-RMZHL EQ COUNTER_AFRU_RMZHL.
     PERFORM PROCESS_AFVC.
   ENDIF.

 ENDFORM.                    "SELECT_AFVC
*---------------------------------------------------------------------
* process_afvc   generate new conf. no.
* update operation with new conf.no.
* write log record on screen
*---------------------------------------------------------------------
 FORM PROCESS_AFVC.

   IF FIRST EQ YES.                           "write header only once
     WRITE AT /1 'order no.'.                   "order no
     WRITE AT 17 'operation'.                   "operation
     WRITE AT 27 'confirmation'.                "confirmation no
     WRITE AT 40 'afvc-rmzhl'.                "confirmation counter afvc
     WRITE AT 55 'counter afru'.                "confirmation sum of afr
     WRITE AT 70 'comment'.                     "comment line
     FIRST = NO.
   ENDIF.
   WRITE AT /1  CAUFV_ITAB-AUFNR.         " order no
   WRITE AT 17 AFVC_ITAB-VORNR.               "operation
   WRITE AT 27 AFVC_ITAB-RUECK.               "confirmation no
   WRITE AT 40 AFVC_ITAB-RMZHL.               "confirmation counter afvc
   WRITE AT 55 COUNTER_AFRU_RMZHL.            "confirmation sum of afru

   IF TEST = YES.
     WRITE AT 70 'test run'.
   ELSE.
     PERFORM UPDATE_AFVC.
   ENDIF.

 ENDFORM.                    "SELECT_AFVC
*----------------------------------------------------------------------
* update_afvc    update operation with new conf. counter
*----------------------------------------------------------------------
 FORM UPDATE_AFVC.
   MOVE COUNTER_AFRU_RMZHL TO AFVC_ITAB-RMZHL.
   MOVE AFVC_ITAB TO AFVC.
   UPDATE AFVC.
   IF SY-SUBRC NE 0.
     WRITE AT 70: 'Error Update AFVC'.
   ELSE.
     WRITE AT 70: 'update AFVC-RMZHL with counter AFRU'.
   ENDIF.

 ENDFORM.                    "SELECT_AFVC
