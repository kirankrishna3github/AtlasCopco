*&---------------------------------------------------------------------*
*& Report  ZCWU_UPLOAD_DOWN
*&---------------------------------------------------------------------*
*  Download and Upload your ABAP program from/to your local harddisk.
*  Allows individual or mass transfering of ABAP program.
*  Before using, create a folder C:\Anda\ABAP\
*
REPORT zcwu_upload_down
        NO STANDARD PAGE HEADING
        LINE-SIZE  255.
*----------------------------------------------------------------------
* Declare Database Objects
*----------------------------------------------------------------------
TABLES:
  dokil,
  trdir.

*----------------------------------------------------------------------
* Constants
*----------------------------------------------------------------------
CONSTANTS:
  gc_trdir_identifier(72)  TYPE c VALUE '%&%& RDIR',
  gc_report_identifier(72) TYPE c VALUE '%&%& REPO',
  gc_text_identifier(72)   TYPE c VALUE '%&%& TEXP',
  gc_thead_identifier(72)  TYPE c VALUE '%&%& HEAD',
  gc_doc_identifier(72)    TYPE c VALUE '%&%& DOKL',
  gc_trdir_short(4)        TYPE c VALUE 'RDIR',
  gc_report_short(4)       TYPE c VALUE 'REPO',
  gc_text_short(4)         TYPE c VALUE 'TEXP',
  gc_thead_short(4)        TYPE c VALUE 'HEAD',
  gc_doc_short(4)          TYPE c VALUE 'DOKP'.

*----------------------------------------------------------------------
* Declare TYPES
*----------------------------------------------------------------------
TYPES:
  BEGIN OF ty_source_line,
     line(255)  TYPE c,
  END OF ty_source_line,

  BEGIN OF ty_directory,
    name LIKE trdir-name,
    desc(72) TYPE c,
    savename LIKE rlgrap-filename,
  END OF ty_directory.

*----------------------------------------------------------------------
* Declare Data
*----------------------------------------------------------------------
DATA: gt_source TYPE STANDARD TABLE OF ty_source_line,
      gt_trdir TYPE STANDARD TABLE OF trdir,
      gt_file TYPE STANDARD TABLE OF ty_source_line,
      gt_documentation TYPE STANDARD TABLE OF ty_source_line,
      gt_directory TYPE STANDARD TABLE OF ty_directory,
      gt_texts TYPE STANDARD TABLE OF textpool.

DATA: gs_thead TYPE thead,
      gs_directory TYPE ty_directory.

*----------------------------------------------------------------------
* Selection Screen
*----------------------------------------------------------------------
*-- Options for upload/download of programs
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-udl.

PARAMETERS:
  rb_down RADIOBUTTON GROUP udl DEFAULT 'X'.       " Download reports

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-dir.
SELECT-OPTIONS:
  s_name  FOR trdir-name,              " Program Name
  s_subc  FOR trdir-subc               " Program Type
          DEFAULT 'F' OPTION EQ SIGN E," Exclude Functions by default
  s_cnam  FOR trdir-cnam               " Created by
          DEFAULT sy-uname,
  s_unam  FOR trdir-unam,              " Last Changed by
  s_cdat  FOR trdir-cdat,              " Creation date
  s_udat  FOR trdir-udat.              " Last update date
SELECTION-SCREEN END OF BLOCK b2.

*-- Options for uploading programs
PARAMETERS:
  rb_up   RADIOBUTTON GROUP udl.       " Upload reports

SELECTION-SCREEN BEGIN OF BLOCK b3 WITH FRAME TITLE text-upl.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(29) text-sng.
*SUBC
PARAMETERS:
  rb_file RADIOBUTTON GROUP how DEFAULT 'X'.
SELECTION-SCREEN COMMENT 33(42) text-fna.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(29) text-nth.
PARAMETERS:
  rb_list RADIOBUTTON GROUP how.
SELECTION-SCREEN COMMENT 33(42) text-lst.
SELECTION-SCREEN END OF LINE.
PARAMETERS: p_ptype TYPE subc DEFAULT '1'.

SELECTION-SCREEN END OF BLOCK b3.

SELECTION-SCREEN END OF BLOCK b1.

*-- Options for up/downloading programs
SELECTION-SCREEN BEGIN OF BLOCK b4 WITH FRAME TITLE text-fil.
PARAMETERS:
  rb_dos  RADIOBUTTON GROUP fil DEFAULT 'X', " Save to local
  rb_unix RADIOBUTTON GROUP fil,       " Save to UNIX
  p_path  LIKE rlgrap-filename         " Path to save files to
        DEFAULT 'C:\Anda\ABAP\'.
SELECTION-SCREEN END OF BLOCK b4.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR s_name-low.
  CALL FUNCTION 'F4_PROGRAM'
    EXPORTING
      object             = s_name-low
      suppress_selection = 'X'
    IMPORTING
      RESULT             = s_name-low
    EXCEPTIONS
      OTHERS             = 1.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR s_name-high.
  CALL FUNCTION 'F4_PROGRAM'
    EXPORTING
      object             = s_name-high
      suppress_selection = 'X'
    IMPORTING
      RESULT             = s_name-high
    EXCEPTIONS
      OTHERS             = 1.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR s_unam-low.
  PERFORM get_name USING 'S_UNAM-LOW'
                CHANGING s_unam-low.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR s_unam-high.
  PERFORM get_name USING 'S_UNAM-HIGH'
                CHANGING s_unam-high.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR s_cnam-low.
  PERFORM get_name USING 'S_CNAM-LOW'
                CHANGING s_cnam-low.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR s_cnam-high.
  PERFORM get_name USING 'S_CNAM-HIGH'
                CHANGING s_cnam-high.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_path.
  PERFORM file_f4_help.

TOP-OF-PAGE.
  IF rb_list = 'X'.
    FORMAT COLOR COL_HEADING.

    NEW-LINE.
    WRITE: AT 3 text-h01,
           AT 15 text-h03.
    FORMAT COLOR OFF.
  ENDIF.

AT LINE-SELECTION.
  CHECK rb_list = 'X'.                 " only do in list mode
  READ LINE sy-curow FIELD VALUE gs_directory-savename.

*-- Read file into an internal table
  PERFORM read_report_from_disk TABLES gt_file
                                USING  gs_directory-savename.
*-- Split table into TADIR entry, report lines, and report text
  PERFORM split_incoming_file TABLES gt_file
                                     gt_source
                                     gt_texts
                                     gt_documentation
                            CHANGING trdir
                                     gs_thead.
*-- Save all of the data
  PERFORM insert_new_report TABLES gt_source
                                   gt_texts
                                   gt_documentation
                            USING  trdir
                                   gs_thead.

*----------------------------------------------------------------------
* Start of processing
*----------------------------------------------------------------------
START-OF-SELECTION.
  FORMAT COLOR COL_NORMAL.

  IF rb_down = 'X'.
    PERFORM download_reports.
  ELSEIF rb_up = 'X'.
    PERFORM upload_reports.
  ENDIF.

END-OF-SELECTION.

  IF rb_down = 'X'.
    CONCATENATE p_path
                'directory.txt'
      INTO p_path.
    PERFORM save_table_to_file TABLES gt_directory
                               USING  p_path.
  ENDIF.

*---------------------------------------------------------------------*
*       FORM UPLOAD_REPORTS                                           *
*---------------------------------------------------------------------*
FORM upload_reports.

*-- Can upload a reports entered in selection criteria or
*-- select from a list.  List can be from index.txt in same directory
*-- (created by the download) or by reading the first line of each file
*-- in the directory.

  IF rb_file = 'X'. " Upload single program from a file
*-- Read file into an internal table
    PERFORM read_report_from_disk TABLES gt_file
                                  USING  p_path.
*-- Split table into TADIR entry, report lines, and report text
    PERFORM split_incoming_file TABLES gt_file
                                       gt_source
                                       gt_texts
                                       gt_documentation
                              CHANGING trdir
                                       gs_thead.
*-- Save all of the data
    PERFORM insert_new_report TABLES gt_source
                                     gt_texts
                                     gt_documentation
                              USING  trdir
                                     gs_thead.

  ELSEIF rb_list = 'X'. " Show list for user to choose from
*-- get list of report names/descriptions from directory text
    CONCATENATE p_path
                'directory.txt'
    INTO p_path.

    PERFORM read_report_from_disk TABLES gt_directory
                                  USING  p_path.

    SORT gt_directory.

*-- Write out list of report names/descriptions
    LOOP AT gt_directory INTO gs_directory.
      WRITE:
        / gs_directory-name UNDER text-h01,
          gs_directory-desc UNDER text-h03,
          gs_directory-savename.

    ENDLOOP.
*-- Process user selections for reports to upload.
  ENDIF.

ENDFORM.                               " upload_reports
*---------------------------------------------------------------------*
*       FORM DOWNLOAD_REPORTS                                         *
*---------------------------------------------------------------------*
*       From the user selections, get all programs that meet the      *
*       criteria, and save them in ftab_program_directory.            *
*       Also save the report to disk.                                 *
*---------------------------------------------------------------------*
FORM download_reports.

  DATA:
    lv_full_filename LIKE rlgrap-filename,
    ls_souceline     TYPE ty_source_line,
    ls_program_file  TYPE ty_source_line,
    ls_document      TYPE ty_source_line,
    ls_textpool      TYPE textpool,
    ls_trdir         TYPE trdir.

*-- The table is put into an internal table because the program will
*-- abend if multiple transfers to a dataset occur within a SELECT/
*-- ENDSELCT (tested on 3.1H)

  SELECT * FROM  trdir
         INTO TABLE gt_trdir
         WHERE  name  IN s_name
         AND    subc  IN s_subc
         AND    cnam  IN s_cnam
         AND    unam  IN s_unam
         AND    cdat  IN s_cdat
         AND    udat  IN s_udat.

  LOOP AT gt_trdir INTO ls_trdir.

*-- Clear out text and source code tables
    CLEAR:
      gt_file,
      gt_source,
      gt_texts,
      gt_documentation.

    REFRESH:
      gt_file,
      gt_source,
      gt_texts,
      gt_documentation.

*-- Get the report
    READ REPORT ls_trdir-name
       INTO gt_source.

*-- Get the text for the report
    READ TEXTPOOL ls_trdir-name INTO gt_texts.

*-- Get the documentation for the report
    CLEAR dokil.
    SELECT * UP TO 1 ROWS FROM dokil
           WHERE  id          = 'RE'
           AND    object      = ls_trdir-name
           AND    langu       = sy-langu
           AND    typ         = 'E'
           ORDER BY version DESCENDING.
    ENDSELECT.
*-- Documentation exists for this object
    IF sy-subrc = 0.
      CALL FUNCTION 'DOCU_READ'
        EXPORTING
          id      = dokil-id
          langu   = dokil-langu
          object  = dokil-object
          typ     = dokil-typ
          version = dokil-version
        IMPORTING
          head    = gs_thead
        TABLES
          line    = gt_documentation
        EXCEPTIONS
          OTHERS  = 1.

    ENDIF.

*-- Put the report code and texts into a single file

*-- Put the identifier line in so that the start of the TRDIR line
*-- is marked
    CONCATENATE gc_trdir_identifier
    ls_trdir-name
    INTO ls_program_file-line.
    APPEND ls_program_file TO gt_file.

*-- Add the TRDIR line
    ls_program_file-line = ls_trdir.
    APPEND ls_program_file TO gt_file.

*-- Put the identifier line in so that the start of the report code
*-- is marked
    CONCATENATE gc_report_identifier
                ls_trdir-name
      INTO ls_program_file-line.
    APPEND ls_program_file TO gt_file.

*-- Add the report code
    LOOP AT gt_source INTO ls_souceline.
      ls_program_file = ls_souceline.
      APPEND ls_program_file TO gt_file.
    ENDLOOP.

*-- Put the identifier line in so that the start of the report text
*-- is marked
    CONCATENATE gc_text_identifier
                ls_trdir-name
      INTO ls_program_file.
    APPEND ls_program_file TO gt_file.

*-- Add the report texts
    LOOP AT gt_texts INTO ls_textpool.
      ls_program_file = ls_textpool.
      APPEND ls_program_file TO gt_file.
    ENDLOOP.

*-- Put the identifier line in so that the start of the THEAD record
*-- is marked
    CONCATENATE gc_thead_identifier
                ls_trdir-name
      INTO ls_program_file-line.
    APPEND ls_program_file TO gt_file.

    ls_program_file = gs_thead.
    APPEND ls_program_file TO gt_file.

*-- Put the identifier line in so that the start of the report
*-- documentation is marked
    CONCATENATE gc_doc_identifier
                ls_trdir-name
      INTO ls_program_file-line.
    APPEND ls_program_file TO gt_file.

*-- Add the report documentation
    LOOP AT gt_documentation INTO ls_document.
      ls_program_file = ls_document.
      APPEND ls_program_file TO gt_file.
    ENDLOOP.

*-- Make the fully pathed filename that report will be saved to
    CONCATENATE p_path
                ls_trdir-name
                '.txt'
      INTO lv_full_filename.

    PERFORM save_table_to_file TABLES gt_file
                               USING  lv_full_filename.

*-- Write out message with Program Name/Description
    READ TABLE gt_texts INTO ls_textpool WITH KEY id = 'R'.
    IF sy-subrc = 0.
      gs_directory-name = ls_trdir-name.
      gs_directory-desc = ls_textpool-entry.
      gs_directory-savename = lv_full_filename.
      APPEND gs_directory TO gt_directory.

      WRITE: / ls_trdir-name,
               ls_textpool-entry(65) COLOR COL_HEADING.
    ELSE.
      gs_directory-name = ls_trdir-name.
      gs_directory-desc = 'No description available'.
      gs_directory-savename = lv_full_filename.
      APPEND gs_directory TO gt_directory.

      WRITE: / ls_trdir-name.
    ENDIF.
  ENDLOOP.
ENDFORM.                               " BUILD_PROGRAM_DIRECTORY
*---------------------------------------------------------------------*
*       FORM SAVE_TABLE_TO_FILE                                       *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
*  -->  FTAB_TABLE                                                    *
*  -->  F_FILENAME                                                    *
*---------------------------------------------------------------------*
FORM save_table_to_file TABLES ftab_table
                        USING  f_filename.

  IF rb_dos = 'X'.                  " Save file to presentation server
    CALL FUNCTION 'WS_DOWNLOAD'
      EXPORTING
        filename = f_filename
        filetype = 'ASC'
      TABLES
        data_tab = ftab_table
      EXCEPTIONS
        OTHERS   = 4.

    IF sy-subrc NE 0.
      WRITE: / 'Error opening dataset' COLOR COL_NEGATIVE,
               f_filename COLOR COL_NEGATIVE.
    ENDIF.
  ELSE.                                " Save file to application serve
    OPEN DATASET f_filename FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
    IF sy-subrc = 0.
      LOOP AT ftab_table.
        TRANSFER ftab_table TO f_filename.
        IF sy-subrc NE 0.
          WRITE: / 'Error writing record to file;' COLOR COL_NEGATIVE,
                   f_filename COLOR COL_NEGATIVE.
        ENDIF.
      ENDLOOP.
    ELSE.
      WRITE: / 'Error opening dataset' COLOR COL_NEGATIVE,
               f_filename COLOR COL_NEGATIVE.
    ENDIF.
  ENDIF.                               " End RB_DOS
ENDFORM.                               " SAVE_PROGRAM
*---------------------------------------------------------------------*
*       FORM READ_REPORT_FROM_DISK                                    *
*---------------------------------------------------------------------*
*       Read report into internal table.  Can read from local or      *
*       remote computer                                               *
*---------------------------------------------------------------------*
FORM read_report_from_disk TABLES ftab_table
                           USING  f_filename.

  DATA:
     lv_message(128) TYPE c.

  CLEAR   ftab_table.
  REFRESH ftab_table.

  IF rb_dos = 'X'.
    TRANSLATE f_filename USING '/\'.   " correct slash for Dos PC file
    CALL FUNCTION 'WS_UPLOAD'
      EXPORTING
        filename            = f_filename
        filetype            = 'ASC'
      TABLES
        data_tab            = ftab_table
      EXCEPTIONS
        conversion_error    = 1
        file_open_error     = 2
        file_read_error     = 3
        invalid_table_width = 4
        invalid_type        = 5
        no_batch            = 6
        unknown_error       = 7
        OTHERS              = 8.
    IF sy-subrc >< 0.
      WRITE: / 'Error reading file from local PC' COLOR COL_NEGATIVE.
    ENDIF.
  ELSEIF rb_unix = 'X'.
    TRANSLATE f_filename USING '\/'.   " correct slash for unix
    OPEN DATASET f_filename FOR INPUT MESSAGE lv_message IN TEXT MODE
     ENCODING DEFAULT.
    IF sy-subrc = 0.
      DO.
        READ DATASET f_filename INTO ftab_table.
        IF sy-subrc = 0.
          APPEND ftab_table.
        ELSE.
          EXIT.
        ENDIF.
      ENDDO.
      CLOSE DATASET f_filename.
    ELSE.
      WRITE: / 'Error reading file from remote computer'
                      COLOR COL_NEGATIVE,
             / lv_message,
             / f_filename.
      sy-subrc = 4.
    ENDIF.
  ENDIF.


ENDFORM.                               " READ_REPORT_FROM_DISK

*---------------------------------------------------------------------*
*       FORM SPLIT_INCOMING_FILE                                      *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
*  -->  FTAB_PROGRAM_FILE                                             *
*  -->  FTAB_PROGRAM_SOURCE                                           *
*  -->  `                                                             *
*  -->  FTAB_PROGRAM_TEXTS                                            *
*---------------------------------------------------------------------*
FORM split_incoming_file TABLES ftab_program_file
                                ftab_program_source
                                ftab_program_texts
                                ftab_program_documentation
                       CHANGING fstr_trdir
                                fstr_thead.

  DATA:
    lv_datatype(4) TYPE c,             " Type of data, REPO, TEXP, RDIR
    lv_program_file TYPE ty_source_line,
    ls_progfile TYPE ty_source_line.

  LOOP AT ftab_program_file INTO ls_progfile.
    lv_program_file = ls_progfile.
    CASE lv_program_file(9).
      WHEN gc_trdir_identifier.
        lv_datatype = gc_trdir_short.
      WHEN gc_report_identifier.
        lv_datatype = gc_report_short.
      WHEN gc_text_identifier.
        lv_datatype = gc_text_short.
      WHEN gc_doc_identifier.
        lv_datatype = gc_doc_short.
      WHEN gc_thead_identifier.
        lv_datatype = gc_thead_short.
      WHEN OTHERS. " Actual contents of report, trdir, or text
        CASE lv_datatype.
          WHEN gc_trdir_short.
            fstr_trdir = ftab_program_file.
          WHEN gc_report_short.
            ftab_program_source = ftab_program_file.
            APPEND ftab_program_source.
          WHEN gc_text_short.
            ftab_program_texts = ftab_program_file.
            APPEND ftab_program_texts.
          WHEN gc_thead_short.
            fstr_thead = ftab_program_file.
          WHEN gc_doc_short.
            ftab_program_documentation = ftab_program_file.
            APPEND ftab_program_documentation.
        ENDCASE.
    ENDCASE.
  ENDLOOP.
ENDFORM.                               " SPLIT_INCOMING_FILE
*---------------------------------------------------------------------*
*       FORM INSERT_NEW_REPORT                                        *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
*  -->  FTAB_PROGRAM_SOURCE                                           *
*  -->  FTAB_PROGRAM_TEXTS                                            *
*  -->  F_TRDIR                                                       *
*---------------------------------------------------------------------*
FORM insert_new_report TABLES ftab_program_source
                              ftab_program_texts
                              ftab_program_documentation
                       USING  fstr_trdir TYPE trdir
                              fstr_thead TYPE thead.
  DATA:
    lv_obj_name LIKE e071-obj_name,
    lv_mess     TYPE char100,
    lv_word      TYPE char100,
    lv_line     TYPE i,
    lv_line2(40)     TYPE c,
    lv_answer(1)     TYPE c.

*-- read trdir to see if the report already exists, if it does, prompt
*-- user to overwrite or abort.
  SELECT SINGLE * FROM trdir WHERE name = fstr_trdir-name.
  IF sy-subrc = 0.                     " Already exists
    CONCATENATE 'want to overwrite report'
                fstr_trdir-name
      INTO lv_line2 SEPARATED BY space.

    CONCATENATE lv_line2
                '?'
      INTO lv_line2.

    CALL FUNCTION 'POPUP_TO_CONFIRM_STEP'
      EXPORTING
        defaultoption  = 'N'
        textline1      = 'The selected report already exists, do you'
        textline2      = lv_line2
        titel          = 'Report already exists'
        cancel_display = space
      IMPORTING
        answer         = lv_answer
      EXCEPTIONS
        OTHERS         = 1.
  ELSE.
    lv_answer = 'J'.
  ENDIF.

  IF lv_answer = 'J'.
*-- Create the TADIR entry.  (TRDIR entry created by INSERT REPORT)
    lv_obj_name = trdir-name.

    CALL FUNCTION 'TR_TADIR_POPUP_ENTRY_E071'
      EXPORTING
        wi_e071_pgmid     = 'R3TR'
        wi_e071_object    = 'PROG'
        wi_e071_obj_name  = lv_obj_name
        wi_tadir_devclass = '$TMP'
      EXCEPTIONS
        exit              = 3
        OTHERS            = 4.

    IF sy-subrc = 0.
      SYNTAX-CHECK FOR  ftab_program_source MESSAGE lv_mess
       LINE lv_line WORD lv_word PROGRAM fstr_trdir-name.
*-- Create Report
      INSERT REPORT fstr_trdir-name FROM ftab_program_source PROGRAM TYPE p_ptype
       UNICODE ENABLING 'X'.
*-- Create Texts
      INSERT textpool fstr_trdir-name FROM ftab_program_texts
             LANGUAGE sy-langu.
*-- Save Documentation
      CALL FUNCTION 'DOCU_UPDATE'
        EXPORTING
          head    = fstr_thead
          state   = 'A'
          typ     = 'E'
          version = '1'
        TABLES
          line    = ftab_program_documentation
        EXCEPTIONS
          OTHERS  = 1.

    ELSE.
      WRITE: / 'Error updating the TADIR entry' COLOR COL_NEGATIVE,
               'Program' COLOR COL_NEGATIVE INTENSIFIED OFF,
               fstr_trdir-name, 'was not loaded into SAP.'
                  COLOR COL_NEGATIVE INTENSIFIED OFF.
    ENDIF.
  ELSE.
    WRITE: / fstr_trdir-name COLOR COL_NEGATIVE,
             'was not uploaded into SAP.  Action cancelled by user'
                 COLOR COL_NEGATIVE INTENSIFIED OFF.
  ENDIF.
ENDFORM.                               " INSERT_NEW_REPORT
*---------------------------------------------------------------------*
*       FORM GET_NAME                                                 *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
*  -->  VALUE(F_FIELD)                                                *
*  -->  F_NAME                                                        *
*---------------------------------------------------------------------*
FORM get_name USING value(f_field)
           CHANGING f_name.

  DATA: ltab_fields LIKE dynpread OCCURS 0 WITH HEADER LINE,
        lv_prog LIKE d020s-prog,
        lv_dnum LIKE d020s-dnum.

  TRANSLATE f_field TO UPPER CASE.

  REFRESH ltab_fields.
  ltab_fields-fieldname = f_field.
  APPEND ltab_fields.
  lv_prog =  sy-repid .
  lv_dnum =  sy-dynnr .
  CALL FUNCTION 'DYNP_VALUES_READ'
    EXPORTING
      dyname     = lv_prog
      dynumb     = lv_dnum
    TABLES
      dynpfields = ltab_fields
    EXCEPTIONS
      OTHERS     = 01.
  READ TABLE ltab_fields INDEX 1.
  IF sy-subrc EQ 0.
    f_name = ltab_fields-fieldvalue.
    REFRESH ltab_fields.
  ENDIF.

  CALL FUNCTION 'F4_USER'
    EXPORTING
      object = f_name
    IMPORTING
      RESULT = f_name.

ENDFORM.                               " GET_NAME

*&---------------------------------------------------------------------*
*&      Form  FILE_F4_HELP
*&---------------------------------------------------------------------*
*       File choose help
*----------------------------------------------------------------------*
FORM file_f4_help .

  DATA: lt_filetable  TYPE filetable,
         ls_filetable  TYPE file_table,
         lv_rc         TYPE i.

  CALL METHOD cl_gui_frontend_services=>file_open_dialog
*    EXPORTING
*      window_title            =
*      default_extension       =
*      default_filename        =
*      file_filter             =
*      with_encoding           =
*      initial_directory       =
*      multiselection          =
    CHANGING
      file_table              = lt_filetable
      rc                      = lv_rc
*      user_action             =
*      file_encoding           =
    EXCEPTIONS
      file_open_dialog_failed = 1
      cntl_error              = 2
      error_no_gui            = 3
      not_supported_by_gui    = 4
      OTHERS                  = 5
          .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    READ TABLE lt_filetable INTO ls_filetable INDEX 1.
    IF sy-subrc = 0.
      p_path = ls_filetable-filename.
    ENDIF.
  ENDIF.
ENDFORM.                    " FILE_F4_HELP

*TEXPZKBPROGS
* IDIR     File Download Options (File Selection)
* IFIL     File Options
* IFNA     Enter filename below (under File Options)
* IH01     Prog Name
* IH03     Program Description
* ISNG     Upload a single file
* IUDL     Upload to SAP/Download from SAP
* IUPL     File Upload Options
* R        Backup/Restore program source code with texts
* P_PATH          Path to save programs to
* RB_DOS          Files on local computer
* RB_DOWN         Download Programs
* RB_FILE         Upload a single file
* RB_LIST         Select program(s) from a list
* RB_UNIX         Files on remote computer
* RB_UP           Upload Programs to SAP
* S_CDAT           Date Created
* S_CNAM          Created by UserID
* S_NAME          Program Name
* S_SUBC          Program Type
* S_UDAT          Date Changed
* S_UNAM          Last Changed by UserID
*HEADZKBPROGS
* DOKU      ZHRBDC54
*DOKLZKBPROGS


*--- End of Program
*Text symbol text£º
*DIR:File Download Options (File Selection)
*FIL:File Options
*FNA:Enter filename below (under File Options)
*H01:Prog Name
*H03:Program Description
*LST:Select program(s) from a list
*SNG:Upload a single file
*UDL:Upload to SAP/Download from SAP

*UPL:File Upload Options
*Selection text£º
*P_PATH:        Path to save programs to
*P_PTYPE:D       .
*RB_DOS:        Files on local computer
*RB_DOWN:        Download Programs
*RB_FILE:        Upload a single file
*RB_LIST:        Select program(s) from a list
*RB_UNIX:        Files on remote computer
*RB_UP:        Upload Programs to SAP
*S_CDAT:        Date Created
*S_CNAM:        Created by UserID
*S_NAME:        Program Name
*S_SUBC:        Program Type
*S_UDAT:        Date Changed
*S_UNAM:        Last Changed by UserID
