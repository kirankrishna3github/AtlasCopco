

*&---------------------------------------------------------------------*
*  标题 销售发票数据下载（SAP->金税）
*&---------------------------------------------------------------------*
* 报表 ID  ZCFIR014         种类　报表
*&---------------------------------------------------------------------*
*   处理概要:
*      1）根据屏幕参数取出系统销售发票的抬头和明细数据
*      2）查询addon表-发票状态明细表，提取未开票的数据。
*      3) 编辑未开票数据
*      4) 编辑出力信息并下载到本地
*      5) 更新addon表-发票状态明细表的已下载数据的状态
* 注：如果整张发票金额大于屏幕金额，没有做处理，
*     会计回答：在流程上控制，系统外对应，做sap发票时做成。
*&---------------------------------------------------------------------*
*   执行条件
*          有会计相关权限
*   运行后的处理
*          打印,下载
*&---------------------------------------------------------------------*
*   作  成  方  法           新建
*   作成理由   新建
*                  作成者   NEC-DEV07    作成日   2009/11/26
*&---------------------------------------------------------------------*
*  变更履历               变更者                       变更日
*  №  XXXXXX             XXXXXX                       YYYY/MM/DD
*  涓内容    XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
*              XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
*  №  001                NEC-DEV12                    2010/01/27
*  涓内容    物料描述逻辑变更
*  №  002                NEC-DEV12                    2010/02/08
*  涓内容    发票日期取出变更
*  №  003                NEC-DEV07                    2010/04/28
*  涓内容    发票数量取整
*&---------------------------------------------------------------------*
REPORT  ZCFIR014 NO STANDARD PAGE HEADING
                 MESSAGE-ID ZCFI1.

*&---------------------------------------------------------------------*
*& 包含
*&---------------------------------------------------------------------*
*CHEN INCLUDE ZCFII001.
TYPE-POOLS: SLIS.

*&---------------------------------------------------------------------*
*& 声明类型
*&---------------------------------------------------------------------*
DATA: WA_TABLE TYPE VBRK,
      WA_CHAR  TYPE C.

*&---------------------------------------------------------------------*
*& 选择画面
*&---------------------------------------------------------------------*
* 输出选项
SELECTION-SCREEN: BEGIN OF BLOCK B_LK1 WITH FRAME TITLE TEXT-001.
* 公司代码
PARAMETERS: P_BUKRS LIKE WA_TABLE-BUKRS OBLIGATORY MEMORY ID BUK.
* 开票日期
SELECT-OPTIONS: P_FKDAT FOR WA_TABLE-FKDAT.
* 发票凭证
SELECT-OPTIONS: P_VBELN FOR WA_TABLE-VBELN.
* 客户
SELECT-OPTIONS: P_KUNRG FOR WA_TABLE-KUNRG.
* 状态
SELECT-OPTIONS: P_STATUS FOR WA_CHAR.
SELECTION-SCREEN: END OF BLOCK B_LK1.

SELECTION-SCREEN: BEGIN OF BLOCK B_LK2 WITH FRAME TITLE TEXT-002.
* 客户
SELECT-OPTIONS: P_KUNNR FOR WA_TABLE-KUNRG.
SELECTION-SCREEN: END OF BLOCK B_LK2.
SELECTION-SCREEN: BEGIN OF BLOCK B_LK3 WITH FRAME TITLE TEXT-003.
* Q
PARAMETERS P_DMBTR TYPE KZWI1 OBLIGATORY DEFAULT '1000000'.
SELECTION-SCREEN: END OF BLOCK B_LK3.

*&---------------------------------------------------------------------*
*& 常量定义
*&---------------------------------------------------------------------*
CONSTANTS:
  CNS_X           TYPE C VALUE 'X',           "X
  CNS_CNY(3)      TYPE C VALUE 'CNY',         "CNY
  CNS_ZCFD(4)     TYPE C VALUE 'ZCFD',        "ZCFD
  CNS_ZCRD(4)     TYPE C VALUE 'ZCRD',        "ZCRD
  CNS_ZDFF(4)     TYPE C VALUE 'ZDFF',        "ZDFF
  CNS_ZDFR(4)     TYPE C VALUE 'ZDFR',        "ZDFR
  CNS_ZDCR(4)     TYPE C VALUE 'ZDCR',        "ZDCR
  CNS_ZDDR(4)     TYPE C VALUE 'ZDDR',        "ZDDR
  CNS_ZDFD(4)     TYPE C VALUE 'ZDFD',        "ZDFD
  CNS_ZDKA(4)     TYPE C VALUE 'ZDKA',        "ZDKA
  CNS_ZDKB(4)     TYPE C VALUE 'ZDKB',        "ZDKB
  CNS_ZDOR(4)     TYPE C VALUE 'ZDOR',        "ZDOR
  CNS_ZDRD(4)     TYPE C VALUE 'ZDRD',        "ZDRD
  CNS_ZDRE(4)     TYPE C VALUE 'ZDRE',        "ZDRE
  CNS_MWST(4)     TYPE C VALUE 'MWST',        "MWST
  CNS_4001(4)     TYPE C VALUE '4001',        "4001
  CNS_4002(4)     TYPE C VALUE '4002',        "4002
  CNS_4005(4)     TYPE C VALUE '4005',        "4005
  CNS_BOX(3)      TYPE C VALUE 'BOX',         "BOX
  CNS_DB(4)       TYPE C VALUE '&IC1',        "&IC1
  CNS_SELALL(6)   TYPE C VALUE 'SELALL',      "SELALL
  CNS_DESAL(5)    TYPE C VALUE 'DESAL',       "DESAL
  CNS_VF03(4)     TYPE C VALUE 'VF03',        "VF03
  CNS_SAVE(4)     TYPE C VALUE 'SAVE',        "SAVE
  CNS_9001(4)     TYPE C VALUE '9001',        "9001
  CNS_DEL(3)      TYPE C VALUE 'DEL',         "DEL
  CNS_VF(2)       TYPE C VALUE 'VF',          "VF
  CNS_EXEC(9)     TYPE C VALUE 'EXCUTE_SP',   "EXEC
  CNS_SEL(4)      TYPE C VALUE 'SEL',         "SEL
  CNS_CHAR        TYPE SLIS_FIELDCAT_ALV-DATATYPE
                  VALUE 'CHAR',               "CHAR
  CNS_CURR        TYPE SLIS_FIELDCAT_ALV-DATATYPE
                  VALUE 'CURR',               "CURR
  CNS_NUMC        TYPE SLIS_FIELDCAT_ALV-DATATYPE
                  VALUE 'NUMC',               "NUMC
  CNS_QUAN        TYPE SLIS_FIELDCAT_ALV-DATATYPE
                  VALUE 'QUAN',               "QUAN
  CNS_STA         TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'STA',                "STA
  CNS_MSG         TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'MSG',                "MSG
  CNS_VTEXT       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'VTEXT',              "VTEXT
  CNS_VBELN       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'VBELN',              "VBELN
  CNS_FKDAT       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'FKDAT',              "FKDAT
  CNS_NAME1       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'NAME1',              "NAME1
  CNS_MATNR       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'MATNR',              "MATNR
  CNS_ARKTX       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'ARKTX',              "ARKTX
  CNS_VRKME       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'VRKME',              "VRKME
  CNS_ZCSD1       TYPE DD03P-FIELDNAME
                  VALUE 'ZZDANWEI',           "ZZDANWEI
  CNS_007         TYPE DD03P-TABNAME
                  VALUE 'ZCSDT007',           "ZCSDT007
  CNS_FKIMG       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'FKIMG',              "FKIMG
  CNS_KZWI1       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'KZWI1',              "KZWI1
  CNS_WAERK       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'WAERK',              "WAERK
  CNS_SHUI        TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'SHUI',               "SHUI
  CNS_KUNAG       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'KUNAG',              "KUNAG
  CNS_STCEG       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'STCEG',              "STCEG
  CNS_PRICE       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'PRICE',              "PRICE
* ---- INSERT BY NEC-DEV07 20100108 START
  CNS_BEIZ        TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'BEIZ',               "BEIZ
  CNS_DATA_CHANGE TYPE SLIS_FORMNAME
                  VALUE 'DATA_CHANGE',        "DATA_CHANGE
* ---- INSERT BY NEC-DEV07 20100108 END
  CNS_SFAKN       TYPE SLIS_FIELDCAT_ALV-FIELDNAME
                  VALUE 'SFAKN',              "SFAKN
  CNS_BUKRSC(7)   TYPE C VALUE 'P_BUKRS',     "P_BUKRS
  CNS_BIS         TYPE C VALUE '\',           "\
  CNS_01(3)       TYPE C VALUE '_01',         "_01
  CNS_02(3)       TYPE C VALUE '_02',         "_02
  CNS_EXT(4)      TYPE C VALUE '.TXT',        ".TXT
  CNS_MASK        TYPE CHAR255 VALUE 'X',     "X
  CNS_C           TYPE C VALUE 'C',           "C
  CNS_R           TYPE C VALUE 'R',           "R
  CNS_I           TYPE C VALUE 'I',           "I
  CNS_EQ(2)       TYPE C VALUE 'EQ',          "EQ
  CNS_DIV(2)      TYPE C VALUE '~~',          "~~
  CNS_E           TYPE C VALUE 'E',           "E
  CNS_M           TYPE C VALUE 'M',           "M
  CNS_N           TYPE C VALUE 'N',           "N
  CNS_1           TYPE C VALUE '1',           "1
  CNS_O           TYPE C VALUE 'O',           "O
  CNS_S           TYPE C VALUE 'S',           "S
  CNS_9           TYPE C VALUE '9',           "9
  CNS_COMMAND     TYPE SLIS_FORMNAME
                  VALUE 'USER_COMMAND',       "'USER_COMMAND'
  CNS_PF_STATUS   TYPE SLIS_FORMNAME
                  VALUE 'ALV_SET_STATUS'.     "ALV_SET_STATUS

*&---------------------------------------------------------------------*
*& 类型定义
*&---------------------------------------------------------------------*
TYPES: BEGIN OF T_VBAK,
         AUART TYPE AUART,                    "销售类型
         VBELN TYPE VBELN_VF,                 "销售
       END OF T_VBAK.
TYPES: BEGIN OF T_VBAP,
         VBELN TYPE VBELN,                    "销售凭证
         POSNR TYPE POSNR,                    "项目
         MATNR TYPE MATNR,                    "物料号码
         ARKTX TYPE ARKTX,                    "项目短文本
       END OF T_VBAP.
TYPES: BEGIN OF T_KNA1,
         KUNNR TYPE KUNNR,                    "客户
         NAME1 TYPE NAME1,                    "名称
         STCEG TYPE STCEG,                    "税号
         STRAS TYPE STRAS,                    "住宅号及街道
         TELF1 TYPE TELF1,                    "第一个电话号
       END OF T_KNA1.
TYPES: BEGIN OF T_BANK,
         KUNNR TYPE KUNNR,                    "客户
         BANKA TYPE BANKA,                    "银行名称
         BRNCH TYPE BRNCH,                    "分行
       END OF T_BANK.
TYPES: BEGIN OF T_VBRK,
         VBELN TYPE VBELN_VF,                 "开票凭证
         FKART TYPE FKART,                    "开票类型
         KUNAG TYPE KUNAG,                    "售达方
         WAERK TYPE WAERK,                    "凭证货币
         VKORG TYPE VKORG,                    "销售组织
         KNUMV TYPE KNUMV,                    "单据条件数
         FKDAT TYPE FKDAT,                    "出具发票日期
         RFBSK TYPE RFBSK,                    "转到会计的状态
         XBLNR TYPE XBLNR,                    "参考凭证编号
         FKSTO TYPE FKSTO,                    "出具发票凭证被取消
         SFAKN TYPE SFAKN,                    "已取消的出具发票
         VBTYP TYPE VBTYP,                    "SD 凭证类别
         NETWR TYPE NETWR,                    "净价值
         BUKRS TYPE BUKRS,                    "公司代码
         MWSBK TYPE MWSBP,                    "凭证货币税额
       END OF T_VBRK.
TYPES: BEGIN OF T_VBRP,
         VBELN TYPE VBELN_VF,                 "开票凭证
         POSNR TYPE POSNR,                    "项目
         FKIMG TYPE FKIMG,                    "实际已开发票量
         VRKME TYPE VRKME,                    "销售单位
         MATNR TYPE MATNR,                    "物料号码
         NETWR TYPE NETWR,                    "净价值
         VGBEL TYPE VGBEL,                    "参考凭证
         MWSBP TYPE MWSBP,                    "税额
         KZWI1 TYPE KZWI1,                    "小计1
         AUBEL TYPE VBELN_VA,                 "销售凭证
         AUPOS TYPE POSNR,                    "销售单据项目
       END OF T_VBRP.
TYPES: BEGIN OF T_DATA,
         BOX(1) TYPE C,
         STA   TYPE CHAR10,                   "状态
         ZSTAUS TYPE CHAR1,                   "下载状态
         RFBSK TYPE RFBSK,                    "转到会计的状态
         VKORG TYPE VKORG,                    "销售组织
         FKART TYPE FKART,                    "开票类型
         VTEXT TYPE VTEXT,                    "描述
         VBELN TYPE VBELN_VF,                 "开票凭证
         VBELV TYPE VBELN ,                   "销售凭证
         FKDAT TYPE FKDAT,                    "出具发票日期
         NAME1 TYPE CHAR100,                  "客户名称
         MATNR TYPE MATNR,                    "物料号码
         ARKTX TYPE CHAR100,                  "项目短文本
         VRKME TYPE ZCSDT007-ZZDANWEI,        "销售单位
         DW    TYPE VRKME,                    "销售单位
         DMBTR(16) TYPE P DECIMALS 6,         "含税单价
         FKIMG(16) TYPE P DECIMALS 6,         "实际已开发票量
         KZWI1 TYPE KZWI1,                    "含税金额
         WAERK TYPE WAERK,                    "凭证货币
         MWSBP TYPE MWSBP,                    "税额
         KBETR TYPE KBETR,                    "税率
         SHUI  TYPE CHAR40,                   "税率
         NETWR TYPE NETWR,                    "净价值
         KUNAG TYPE KUNAG,                    "售达方
         STCEG TYPE STCEG,                    "税号 1
         PRICE TYPE CHAR40,                   "单价
         SFAKN TYPE SFAKN,                    "被取消
* ---- MODIFY BY NEC-DEV07 20100108 START
*         ADDR  TYPE CHAR80,                   "购方地址电话
         ADDR  TYPE CHAR100,                  "购方地址电话
* ---- MODIFY BY NEC-DEV07 20100108 END
         BANK  TYPE CHAR100,                  "银行
* ---- MODIFY BY NEC-DEV07 20100108 START
*         BEIZ  TYPE CHAR100,                  "备注
         BEIZ(160) TYPE C,                    "备注
* ---- MODIFY BY NEC-DEV07 20100108 END
         UNAME TYPE CHAR08,                   "复核人
         REV   TYPE CHAR08,                   "收款人
         PRD   TYPE CHAR060,                  "清单行商品名称
         DATE  TYPE CHAR14,                   "单据日期
         ACC   TYPE CHAR80,                   "销方银行账号
         TEL   TYPE CHAR80,                   "销方地址电话
         LINE  TYPE I,                        "行号
         TEXT1 TYPE CHAR01,                   "TEMP
         JIA1  TYPE CHAR40,                   "净价值
         JIA2  TYPE CHAR40,                   "税率
         JIA3  TYPE CHAR40,                   "税额
         JIA4  TYPE CHAR40,                   "单价
         SHU   TYPE CHAR40,                   "实际已开发票量
         ERRFG TYPE CHAR01,                   "下载错误标志
         MSG   TYPE CHAR100,                  "错误日志
         VBTYP TYPE VBTYP,                    "SD 凭证类别
         FLG   TYPE C,                        "增值税标记
       END OF T_DATA.
TYPES: BEGIN OF T_DOWNLOAD,
         LINE(1000) TYPE C,
         FLG(1) TYPE C,
       END OF T_DOWNLOAD.
*&---------------------------------------------------------------------*
*& 内部表定义
*&---------------------------------------------------------------------*
DATA: IT_VBRK    TYPE TABLE OF T_VBRK,
      IT_VBAK    TYPE TABLE OF T_VBAK,
      IT_VBAP    TYPE TABLE OF T_VBAP,
      IT_KNA1    TYPE TABLE OF T_KNA1,
      IT_ZCSDT6  TYPE TABLE OF ZCSDT006,
* ---- INSERT BY NEC-DEV07 20100108 START
      IT_ZCSDT8  TYPE TABLE OF ZCSDT008,
* ---- INSERT BY NEC-DEV07 20100108 END
      IT_TVFKT   TYPE TABLE OF TVFKT,
      IT_VBRP    TYPE TABLE OF T_VBRP,
      IT_DATA    TYPE TABLE OF T_DATA,
      IT_BAKUP   TYPE TABLE OF T_DATA,
      IT_ZCSDT5  TYPE TABLE OF ZCSDT005,
      IT_INSERT  TYPE TABLE OF ZCSDT005,
      IT_OUTPUT  TYPE TABLE OF T_DOWNLOAD,
      IT_BANK    TYPE TABLE OF T_BANK,
      IT_MSG     TYPE TABLE OF T_DATA,
      IT_DOWN    TYPE TABLE OF T_DATA,
      IT_FCAT    TYPE SLIS_T_FIELDCAT_ALV.    "ａｌｖ字段输出属性
*&---------------------------------------------------------------------*
*& 变量定义
*&---------------------------------------------------------------------*
* 工作区
DATA: WA_VBRK    LIKE LINE OF IT_VBRK,
      WA_VBAK    LIKE LINE OF IT_VBAK,
      WA_VBRP    LIKE LINE OF IT_VBRP,
      WA_VBAP    LIKE LINE OF IT_VBAP,
      WA_KNA1    LIKE LINE OF IT_KNA1,
      WA_ZCSDT6  LIKE LINE OF IT_ZCSDT6,
* ---- INSERT BY NEC-DEV07 20100108 START
      WA_ZCSDT8  LIKE LINE OF IT_ZCSDT8,
* ---- INSERT BY NEC-DEV07 20100108 END
      WA_TVFKT   LIKE LINE OF IT_TVFKT,
      WA_DATA    LIKE LINE OF IT_DATA,
      WA_MSG     LIKE LINE OF IT_MSG,
      WA_DOWN    LIKE LINE OF IT_DOWN,
      WA_BANK    LIKE LINE OF IT_BANK,
      WA_OUTPUT  LIKE LINE OF IT_OUTPUT,
      WA_ZCSDT5  LIKE LINE OF IT_ZCSDT5.

* 全局变量
DATA: FLG_STOP TYPE C,
      W_NAME TYPE STRING,
      W_NAME1 TYPE STRING,
      W_NAME2 TYPE STRING,
      W_BUTXT  TYPE BUTXT.
*&---------------------------------------------------------------------*
*& 屏幕检查
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.
* 检查公司代码
* PERFORM CHECK_BUKRS USING P_BUKRS CNS_BUKRSC
* CHANGING W_BUTXT.
* 检查权限
* chen PERFORM AUTHORITY_CHECK USING P_BUKRS CNS_BUKRSC.

*&---------------------------------------------------------------------*
*& 运行程序
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* 主处理
  PERFORM MAIN_PROC.
END-OF-SELECTION.
  PERFORM UNLOCK_ZCSDT005.
*&---------------------------------------------------------------------*
*&      Form  MAIN_PROC
*&---------------------------------------------------------------------*
*       主处理
*----------------------------------------------------------------------*
FORM MAIN_PROC.

* 清全局变量
  PERFORM CLEAR_ALL.
* 取得发票对象
  PERFORM GET_VBRK.
  CHECK FLG_STOP = SPACE.
* 筛选销售凭证类型
  PERFORM SELECT_AUART.
* 取得发票状态明细表
  PERFORM GET_ZCSDT005.
* 取得发票明细
  PERFORM GET_VBRP.
* 取得其他数据
  PERFORM GET_OTHER.
* 编辑数据
  PERFORM EDIT_DATA.
* ALV 显示
  PERFORM ALV_FRM.

ENDFORM.                    "MAIN_PROC
*&---------------------------------------------------------------------*
*&      Form  CLEAR_ALL
*&---------------------------------------------------------------------*
*       清全局变量
*----------------------------------------------------------------------*
FORM CLEAR_ALL .

  REFRESH:IT_VBRK,
          IT_VBAK,
          IT_VBAP,
          IT_KNA1,
          IT_ZCSDT6,
          IT_TVFKT,
          IT_VBRP,
          IT_DATA,
          IT_BAKUP,
          IT_ZCSDT5,
          IT_INSERT,
          IT_OUTPUT,
          IT_BANK,
          IT_MSG,
          IT_DOWN,
          IT_FCAT.
  CLEAR: WA_VBRK,
         WA_VBAK,
         WA_VBRP,
         WA_VBAP,
         WA_KNA1,
         WA_ZCSDT6,
         WA_TVFKT,
         WA_DATA,
         WA_MSG,
         WA_DOWN,
         WA_BANK,
         WA_OUTPUT,
         WA_ZCSDT5,
         FLG_STOP.


ENDFORM.                    " CLEAR_ALL
*&---------------------------------------------------------------------*
*&      Form  GET_VBRK
*&---------------------------------------------------------------------*
*       取得发票对象
*----------------------------------------------------------------------*
FORM GET_VBRK .

  SELECT VBELN                                "开票凭证
         FKART                                "开票类型
         KUNAG                                "售达方
         WAERK                                "凭证货币
         VKORG                                "销售组织
         KNUMV                                "单据条件数
* ---- MODIFY BY NEC-DEV12 20100208 START
*         FKDAT                                "出具发票日期
         ERDAT AS FKDAT                       "出具发票日期
* ---- MODIFY BY NEC-DEV12 20100208 END
         RFBSK                                "转到会计的状态
         XBLNR                                "参考凭证编号
         FKSTO                                "出具发票凭证被取消
         SFAKN                                "已取消的出具发票
         VBTYP                                "SD 凭证类别
         NETWR                                "净价值
         BUKRS                                "公司代码
         MWSBK                                "凭证货币税额
    INTO TABLE IT_VBRK
    FROM VBRK
* ---- MODIFY BY NEC-DEV12 20100208 START
*   WHERE FKDAT IN P_FKDAT                     "出具发票日期
   WHERE ERDAT IN P_FKDAT                     "出具发票日期
* ---- MODIFY BY NEC-DEV12 20100208 END
     AND VBELN IN P_VBELN                     "开票凭证
     AND BUKRS = P_BUKRS                      "公司代码
     AND KUNRG IN P_KUNRG                     "售达方
     AND WAERK = CNS_CNY                      "凭证货币
     AND ( RFBSK = CNS_C                      "转到会计的状态
      OR   RFBSK = CNS_E )
     AND ( VBTYP = CNS_M                      "SD 凭证类别
      OR   VBTYP = CNS_N
      OR   VBTYP = CNS_O
      OR   VBTYP = CNS_S ).
  IF SY-SUBRC <> 0.
    FLG_STOP = CNS_X.
* 没有符合条件的对象数据。
    MESSAGE S014.
  ENDIF.

ENDFORM.                    " GET_VBRK
*&---------------------------------------------------------------------*
*&      Form  SELECT_AUART
*&---------------------------------------------------------------------*
*       筛选销售凭证类型
*----------------------------------------------------------------------*
FORM SELECT_AUART .

* 删除不需要的发票
  LOOP AT IT_VBRK INTO WA_VBRK.
    CLEAR: WA_VBAK.
    SELECT SINGLE AUART                       "销售凭证类型
           VBELN                              "销售凭证
      INTO WA_VBAK
      FROM VBAK
     WHERE VBELN = WA_VBRK-XBLNR.             "销售凭证
    IF SY-SUBRC = 0.
* 例外的销售类型
      IF WA_VBAK-AUART = CNS_ZCFD OR
         WA_VBAK-AUART = CNS_ZCRD OR
         WA_VBAK-AUART = CNS_ZDFF OR
         WA_VBAK-AUART = CNS_ZDFR OR
         WA_VBAK-AUART = CNS_ZDCR OR
         WA_VBAK-AUART = CNS_ZDDR OR
         WA_VBAK-AUART = CNS_ZDFD OR
         WA_VBAK-AUART = CNS_ZDKA OR
         WA_VBAK-AUART = CNS_ZDKB OR
         WA_VBAK-AUART = CNS_ZDOR OR
         WA_VBAK-AUART = CNS_ZDRD OR
         WA_VBAK-AUART = CNS_ZDRE.
        DELETE IT_VBRK.
      ENDIF.
    ENDIF.
  ENDLOOP.
  IF IT_VBRK[] IS INITIAL.
    FLG_STOP = CNS_X.
* 没有符合条件的对象数据。
    MESSAGE S014.
  ENDIF.

ENDFORM.                    " SELECT_AUART
*&---------------------------------------------------------------------*
*&      Form  EDIT_DATA
*&---------------------------------------------------------------------*
*       编辑数据
*----------------------------------------------------------------------*
FORM EDIT_DATA .

  DATA: LC_NUM TYPE P DECIMALS 3.

  SORT IT_VBRK BY VBELN.
  SORT IT_KNA1 BY KUNNR.
  SORT IT_VBAP BY VBELN POSNR.
  SORT IT_ZCSDT5 BY VBELN MATNR.
  SORT IT_BANK BY KUNNR .
  LOOP AT IT_VBRP INTO WA_VBRP.
    CLEAR:WA_VBRK,
          WA_DATA,
          WA_ZCSDT5.
    CLEAR:WA_VBAP.
    READ TABLE IT_VBAP INTO WA_VBAP
      WITH KEY VBELN = WA_VBRP-AUBEL          "销售单据
               POSNR = WA_VBRP-AUPOS          "销售单据项目
      BINARY SEARCH.
    WA_DATA-ARKTX = WA_VBAP-ARKTX.            "项目短文本
    WA_DATA-DW = WA_VBRP-VRKME.               "销售单位

* ---- INSERT BY NEC-DEV12 20100127 START
    READ TABLE IT_VBRK INTO WA_VBRK
      WITH KEY VBELN = WA_VBRP-VBELN
      BINARY SEARCH.
* ---- INSERT BY NEC-DEV12 20100127 END

    IF P_KUNNR[] IS NOT INITIAL.
      CLEAR: WA_ZCSDT6.
      READ TABLE IT_ZCSDT6 INTO WA_ZCSDT6
        WITH KEY KUNRG = WA_VBRK-KUNAG        "售达方
                 MATNR = WA_VBRP-MATNR        "物料号码
        BINARY SEARCH.
      IF WA_ZCSDT6-ARKTX <> SPACE.
        WA_DATA-ARKTX = WA_ZCSDT6-ARKTX.      "项目短文本
      ENDIF.
    ENDIF.

* ---- DELETE BY NEC-DEV12 20100127 START
*    READ TABLE IT_VBRK INTO WA_VBRK
*      WITH KEY VBELN = WA_VBRP-VBELN
*      BINARY SEARCH.
* ---- DELETE BY NEC-DEV12 20100127 END

    READ TABLE IT_ZCSDT5 INTO WA_ZCSDT5
      WITH KEY VBELN = WA_VBRP-VBELN          "开票凭证
               MATNR = WA_VBRP-MATNR          "物料
      BINARY SEARCH.
* 状态
    IF SY-SUBRC = 0.
      WA_DATA-VRKME = WA_ZCSDT5-VRKME.        "销售单位
      WA_DATA-ARKTX = WA_ZCSDT5-ARKTX.
      WA_DATA-BEIZ = WA_ZCSDT5-BEIZ.
      WA_DATA-ZSTAUS = WA_ZCSDT5-ZSTAUS.
* 状态选择
      PERFORM SEL_STATUS USING WA_ZCSDT5-ZSTAUS
                         CHANGING WA_DATA-STA.
    ELSE.
      IF P_STATUS[] IS NOT INITIAL.
        CONTINUE.
      ENDIF.
      WA_DATA-STA = TEXT-004.
      WA_DATA-VRKME = WA_VBRP-VRKME.          "销售单位
    ENDIF.
    WA_DATA-FKART = WA_VBRK-FKART.            "开票类型
    CLEAR:WA_TVFKT.
    READ TABLE IT_TVFKT INTO WA_TVFKT
      WITH KEY FKART = WA_VBRK-FKART.         "开票类型
    WA_DATA-VTEXT = WA_TVFKT-VTEXT.           "描述
    WA_DATA-VKORG = WA_VBRK-VKORG.            "销售组织
    WA_DATA-RFBSK = WA_VBRK-RFBSK.            "转到会计的状态
    WA_DATA-VBELN = WA_VBRK-VBELN.            "开票凭证
    WA_DATA-FKDAT = WA_VBRK-FKDAT.            "出具发票日期
    CLEAR:WA_KNA1.
    READ TABLE IT_KNA1 INTO WA_KNA1
      WITH KEY KUNNR = WA_VBRK-KUNAG          "售达方
      BINARY SEARCH.
    WA_DATA-NAME1 = WA_KNA1-NAME1.            "客户名称
* ---- MODIFY BY NEC-DEV07 20100108 START
*    WA_DATA-STCEG = WA_KNA1-STCEG.            "税号
*    CONCATENATE WA_KNA1-STRAS                 "住宅号及街道
*                WA_KNA1-TELF1                 "第一个电话号
*           INTO WA_DATA-ADDR.                 "购方地址电话
    CLEAR:WA_ZCSDT8.
    READ TABLE IT_ZCSDT8 INTO WA_ZCSDT8
      WITH KEY KUNNR = WA_VBRK-KUNAG.         "售达方
    WA_DATA-STCEG = WA_ZCSDT8-ZZZHUCESH.      "注册税号
    WA_DATA-ADDR = WA_ZCSDT8-ZZZHUCEDZ.       "注册地址
    WA_DATA-BANK = WA_ZCSDT8-ZZBANK.          "银行账号
* ---- MODIFY BY NEC-DEV07 20100108 END
    WA_DATA-KUNAG = WA_VBRK-KUNAG.            "售达方
    WA_DATA-MATNR = WA_VBRP-MATNR.            "物料号码
    WA_DATA-VBELV = WA_VBRP-AUBEL.            "销售单据
* ---- INSERT BY NEC-DEV07 20100428 START
    LC_NUM = FRAC( WA_VBRP-FKIMG ).
    IF LC_NUM >='0.9' OR
       LC_NUM < '0.1'.
      IF LC_NUM >='0.9' .
        WA_VBRP-FKIMG = WA_VBRP-FKIMG + 1.
      ENDIF.
      CALL FUNCTION 'ROUND'
        EXPORTING
          DECIMALS            = 0
          INPUT               = WA_VBRP-FKIMG
          SIGN                = '-'
        IMPORTING
          OUTPUT              = WA_VBRP-FKIMG
        EXCEPTIONS
          INPUT_INVALID       = 1
          OVERFLOW            = 2
          TYPE_INVALID        = 3
          OTHERS              = 4
                .
      IF SY-SUBRC <> 0.
      ENDIF.
    ENDIF.
* ---- INSERT BY NEC-DEV07 20100428 END
    IF WA_VBRK-VBTYP = CNS_N OR
       WA_VBRK-VBTYP = CNS_O.
      WA_DATA-NETWR = - WA_VBRP-NETWR.        "净价值
      WA_DATA-MWSBP = - WA_VBRP-MWSBP.        "税额
      WA_DATA-FKIMG = - WA_VBRP-FKIMG.        "数量
      WA_DATA-KZWI1 = - WA_VBRP-KZWI1.        "小计1
    ELSE.
      WA_DATA-NETWR = WA_VBRP-NETWR.          "净价值
      WA_DATA-MWSBP = WA_VBRP-MWSBP.          "税额
      WA_DATA-FKIMG = WA_VBRP-FKIMG.          "数量
      WA_DATA-KZWI1 = WA_VBRP-KZWI1.          "小计1
    ENDIF.
    WA_DATA-DMBTR = WA_VBRP-KZWI1
                  / WA_DATA-FKIMG.            "含税单价
    WA_DATA-DMBTR = ABS( WA_DATA-DMBTR ).
    WRITE WA_DATA-DMBTR TO WA_DATA-PRICE NO-GROUPING.
    SELECT KBETR                              "税率
      INTO WA_DATA-KBETR
      FROM KONV
           UP TO 1 ROWS
     WHERE KNUMV = WA_VBRK-KNUMV              "单据条件
       AND KPOSN = WA_VBRP-POSNR              "项目
       AND KSCHL = CNS_MWST.                  "条件类型
    ENDSELECT.
    WA_DATA-KBETR = WA_DATA-KBETR / 1000.
    WRITE WA_DATA-KBETR TO WA_DATA-SHUI
      CURRENCY CNS_CNY
      NO-GROUPING.
    WA_DATA-WAERK = WA_VBRK-WAERK.            "凭证货币
    WA_DATA-SFAKN = WA_VBRK-SFAKN.            "被取消
* ---- DELETE BY NEC-DEV07 20100108 START
*    CLEAR:WA_BANK.
*    READ TABLE IT_BANK INTO WA_BANK
*      WITH KEY KUNNR = WA_VBRK-KUNAG          "售达方
*      BINARY SEARCH.
*    CONCATENATE WA_BANK-BANKA
*                WA_BANK-BRNCH
*           INTO WA_DATA-BANK.                 "银行
* ---- DELETE BY NEC-DEV07 20100108 END
    WA_DATA-VBTYP = WA_VBRK-VBTYP.
    COLLECT WA_DATA INTO IT_DATA.
  ENDLOOP.


ENDFORM.                    " EDIT_DATA
*&---------------------------------------------------------------------*
*&      Form  GET_ZCSDT005
*&---------------------------------------------------------------------*
*       取得发票状态明细表
*----------------------------------------------------------------------*
FORM GET_ZCSDT005 .

  SELECT *
    INTO TABLE IT_ZCSDT5
    FROM ZCSDT005
   WHERE FKDAT IN P_FKDAT                     "出具发票日期
     AND VBELN IN P_VBELN                     "开票凭证
     AND BUKRS = P_BUKRS                      "公司代码
     AND KUNAG IN P_KUNRG                     "售达方
     AND WAERK = CNS_CNY                      "凭证货币
     AND ZSTAUS IN P_STATUS                   "状态
     AND ZSTAUS <> CNS_9.

  SORT IT_ZCSDT5 BY VBELN MATNR.

ENDFORM.                    " GET_ZCSDT005
*&---------------------------------------------------------------------*
*&      Form  GET_VBRP
*&---------------------------------------------------------------------*
*       取得发票明细
*----------------------------------------------------------------------*
FORM GET_VBRP .

  SELECT VBELN                                "开票凭证
         POSNR                                "项目
         FKIMG                                "实际已开发票量
         VRKME                                "销售单位
         MATNR                                "物料号码
         NETWR                                "净价值
         VGBEL                                "参考凭证
         MWSBP                                "税额
         KZWI1                                "小计1
         AUBEL                                "销售凭证
         AUPOS                                "销售单据项目
    INTO TABLE IT_VBRP
    FROM VBRP
         FOR ALL ENTRIES IN IT_VBRK
   WHERE VBELN = IT_VBRK-VBELN.               "开票凭证

  DELETE IT_VBRP WHERE KZWI1 = SPACE.
  SORT IT_VBRP BY VBELN POSNR.

ENDFORM.                    " GET_VBRP
*&---------------------------------------------------------------------*
*&      Form  GET_OTHER
*&---------------------------------------------------------------------*
*       取得其他数据
*----------------------------------------------------------------------*
FORM GET_OTHER .

* 客户
  SELECT KUNNR                                "售达方
         NAME1                                "名称
         STCEG                                "税号
         STRAS                                "住宅号及街道
         TELF1                                "第一个电话号
    INTO TABLE IT_KNA1
    FROM KNA1
         FOR ALL ENTRIES IN IT_VBRK
   WHERE KUNNR = IT_VBRK-KUNAG.               "客户
* ---- INSERT BY NEC-DEV07 20100108 START
  REFRESH: IT_ZCSDT8.
  SELECT *
    INTO TABLE IT_ZCSDT8
    FROM ZCSDT008
         FOR ALL ENTRIES IN IT_VBRK
   WHERE KUNNR = IT_VBRK-KUNAG.               "客户
* ---- INSERT BY NEC-DEV07 20100108 END
* 物料
  SELECT VBELN                                "销售凭证
         POSNR                                "销售单据项目
         MATNR                                "物料编号
         ARKTX                                "项目短文本
    INTO TABLE IT_VBAP
    FROM VBAP
         FOR ALL ENTRIES IN IT_VBRP
   WHERE VBELN = IT_VBRP-AUBEL                "销售凭证
     AND POSNR = IT_VBRP-AUPOS.               "销售单据项目
  IF P_KUNNR[] IS NOT INITIAL.
    SELECT *
      INTO TABLE IT_ZCSDT6
      FROM ZCSDT006
     WHERE KUNRG IN P_KUNNR.                  "客户
    SORT IT_ZCSDT6 BY KUNRG MATNR.
  ENDIF.
* 开票文本
  SELECT *
    INTO TABLE IT_TVFKT
    FROM TVFKT
   WHERE SPRAS = SY-LANGU.                    "语言
* ---- DELETE BY NEC-DEV07 20100108 START
* 银行数据
*  SELECT A~KUNNR                              "客户
*         B~BANKA                              "银行
*         B~BRNCH                              "分行
*    INTO TABLE IT_BANK
*    FROM KNBK AS A
*         INNER JOIN BNKA AS B
*      ON ( A~BANKS = B~BANKS
*     AND   A~BANKL = B~BANKL )
*         FOR ALL ENTRIES IN IT_VBRK
*   WHERE A~KUNNR = IT_VBRK-KUNAG.             "客户
* ---- DELETE BY NEC-DEV07 20100108 END

ENDFORM.                    " GET_OTHER
*&---------------------------------------------------------------------*
*&      Form  ALV_FRM
*&---------------------------------------------------------------------*
*       ALV 显示
*----------------------------------------------------------------------*
FORM ALV_FRM .

* ALV FIELDCAT
  PERFORM SET_FIELDCAT.
* ALV 输出
  PERFORM ALV_OUTPUT.

ENDFORM.                    " ALV_FRM
*&---------------------------------------------------------------------*
*&      Form  ALV_OUTPUT
*&---------------------------------------------------------------------*
*       ALV 输出
*----------------------------------------------------------------------*
FORM ALV_OUTPUT.

  DATA: LC_REPID  LIKE SYST-REPID,            "Temp ABAP program
        LC_LAYOUT TYPE SLIS_LAYOUT_ALV.
* ---- INSERT BY NEC-DEV07 20100108 START
  DATA: LR_EVENTS TYPE SLIS_ALV_EVENT,
        LT_EVENTS LIKE TABLE OF LR_EVENTS.

  LR_EVENTS-NAME = SLIS_EV_DATA_CHANGED.
  LR_EVENTS-FORM = CNS_DATA_CHANGE.
  APPEND LR_EVENTS TO LT_EVENTS.
* ---- INSERT BY NEC-DEV07 20100108 END

  LC_REPID  = SY-REPID.                       "ABAP program

  LC_LAYOUT-COLWIDTH_OPTIMIZE = CNS_X.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      I_CALLBACK_PROGRAM       = LC_REPID     "Temp ABAP program
      I_CALLBACK_USER_COMMAND  = CNS_COMMAND
* ---- INSERT BY NEC-DEV07 20100108 START
      IT_EVENTS                = LT_EVENTS
* ---- INSERT BY NEC-DEV07 20100108 END
      I_CALLBACK_PF_STATUS_SET = CNS_PF_STATUS
      IT_FIELDCAT              = IT_FCAT      "Fieldcat
      IS_LAYOUT                = LC_LAYOUT
    TABLES
      T_OUTTAB                 = IT_DATA.

ENDFORM.                    "ALV_OUTPUT
*&---------------------------------------------------------------------*
*&      Form  ALV_SET_STATUS
*&---------------------------------------------------------------------*
*       SET PF-STATUS
*----------------------------------------------------------------------*
*    --->I_EXTAB TOOLBAR命令
*----------------------------------------------------------------------*
FORM ALV_SET_STATUS USING I_EXTAB TYPE SLIS_T_EXTAB.        "#EC CALLED

  SET PF-STATUS '9000'.

ENDFORM.                    " ALV_SET_STATUS
*&---------------------------------------------------------------------*
*&      Form  USER_COMMAND
*&---------------------------------------------------------------------*
*      -->I_R_UCOMM  COMMAND CODE
*      -->I_RS_SELFIELD FIELDCAT
*----------------------------------------------------------------------*
FORM USER_COMMAND USING I_UCOMM LIKE SY-UCOMM
                        I_SELFIELD TYPE SLIS_SELFIELD.      "#EC CALLED

  DATA: LC_GRID1  TYPE REF TO CL_GUI_ALV_GRID,
        LC_VALID  TYPE C,
        LR_STBL TYPE LVC_S_STBL.

* ALV 中的值放到内部表中
  CALL FUNCTION 'GET_GLOBALS_FROM_SLVC_FULLSCR'
   IMPORTING
     E_GRID           =  LC_GRID1
            .
  CASE I_UCOMM.
* 全选
    WHEN CNS_SELALL.
      CLEAR: WA_DATA.
      WA_DATA-BOX = CNS_X.
      MODIFY IT_DATA FROM WA_DATA TRANSPORTING BOX
        WHERE WAERK = CNS_CNY.
* 取消全选
    WHEN CNS_DESAL.
      CLEAR: WA_DATA.
      WA_DATA-BOX = SPACE.
      MODIFY IT_DATA FROM WA_DATA TRANSPORTING BOX
        WHERE WAERK = CNS_CNY.
* 双击或选择
    WHEN CNS_DB.
      IF I_SELFIELD-FIELDNAME = CNS_VBELN.
        SET PARAMETER ID CNS_VF FIELD I_SELFIELD-VALUE.
        CALL TRANSACTION CNS_VF03 AND SKIP FIRST SCREEN.
      ENDIF.
* 选择同一凭证号
      IF I_SELFIELD-FIELDNAME = CNS_BOX.
        CLEAR:WA_DATA.
        READ TABLE IT_DATA INTO WA_DATA INDEX I_SELFIELD-TABINDEX.
        IF WA_DATA-BOX = CNS_X.
          WA_DATA-BOX = SPACE.
        ELSE.
          WA_DATA-BOX = CNS_X.
        ENDIF.
        MODIFY IT_DATA FROM WA_DATA TRANSPORTING BOX
          WHERE VBELN = WA_DATA-VBELN.
      ENDIF.
    WHEN CNS_EXEC.
* 下载数据
      PERFORM EDIT_DOWNLOAD.
    WHEN CNS_SEL.
* 选中未开票
      CLEAR: WA_DATA.
      WA_DATA-BOX = CNS_X.
      MODIFY IT_DATA FROM WA_DATA TRANSPORTING BOX
                    WHERE ZSTAUS = SPACE.
      WA_DATA-BOX = SPACE.
      MODIFY IT_DATA FROM WA_DATA TRANSPORTING BOX
                    WHERE ZSTAUS <> SPACE.
* 作废发票
    WHEN CNS_DEL.
* 作废发票
      PERFORM DEL_DOC.
    WHEN CNS_SAVE.
      CALL METHOD LC_GRID1->CHECK_CHANGED_DATA
        IMPORTING E_VALID = LC_VALID.
* 保存数据
      PERFORM SAVE_EDIT_DATA.
    WHEN OTHERS.
  ENDCASE.
  LR_STBL-ROW = CNS_X.
  LR_STBL-COL = CNS_X.
  CALL METHOD LC_GRID1->REFRESH_TABLE_DISPLAY
    EXPORTING
      IS_STABLE      = LR_STBL
          .

ENDFORM.                    "USER_COMMAND
*&---------------------------------------------------------------------*
*&      Form  SET_FIELDCAT
*&---------------------------------------------------------------------*
*       SET FIELDCAT
*----------------------------------------------------------------------*
FORM SET_FIELDCAT.

  DATA: LC_FCAT TYPE SLIS_FIELDCAT_ALV.

  REFRESH IT_FCAT.
  CLEAR LC_FCAT.
  LC_FCAT-CHECKBOX  = CNS_X.
  LC_FCAT-FIELDNAME = CNS_BOX.                "BOX
  LC_FCAT-SELTEXT_L = TEXT-H00.               "状态
  LC_FCAT-HOTSPOT   = CNS_X.
  LC_FCAT-DATATYPE  = CNS_CHAR.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_STA.                "STA
  LC_FCAT-SELTEXT_L = TEXT-H01.               "状态
  LC_FCAT-DATATYPE  = CNS_CHAR.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_VTEXT.              "VTEXT
  LC_FCAT-SELTEXT_L = TEXT-H02.               "发票类型
  LC_FCAT-DATATYPE  = CNS_CHAR.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_VBELN.              "VBELN
  LC_FCAT-SELTEXT_L = TEXT-H03.               "发票号
  LC_FCAT-HOTSPOT = CNS_X.
  LC_FCAT-DATATYPE  = CNS_CHAR.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_FKDAT.              "FKDAT
  LC_FCAT-SELTEXT_L = TEXT-H04.               "发票日期
  LC_FCAT-DATATYPE  = CNS_CHAR.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_NAME1.              "NAME1
  LC_FCAT-SELTEXT_L = TEXT-H05.               "客户名称
  LC_FCAT-DATATYPE  = CNS_CHAR.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_MATNR.              "MATNR
  LC_FCAT-SELTEXT_L = TEXT-H06.               "商品代码
  LC_FCAT-DATATYPE  = CNS_CHAR.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_ARKTX.              "ARKTX
  LC_FCAT-SELTEXT_L = TEXT-H07.               "商品名
  LC_FCAT-OUTPUTLEN = 100.
  LC_FCAT-LOWERCASE = CNS_X.
  LC_FCAT-EDIT = CNS_X.
  LC_FCAT-DATATYPE  = CNS_CHAR.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_VRKME.              "VRKME
  LC_FCAT-SELTEXT_L = TEXT-H08.               "计量单位
  LC_FCAT-REF_TABNAME = CNS_007.
  LC_FCAT-REF_FIELDNAME = CNS_ZCSD1.
  LC_FCAT-DATATYPE  = CNS_CHAR.
  LC_FCAT-EDIT = CNS_X.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_PRICE.              "PRICE
  LC_FCAT-SELTEXT_L = TEXT-H09.               "含税单价
  LC_FCAT-JUST = CNS_R.
  LC_FCAT-DATATYPE  = CNS_CHAR.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_FKIMG.              "FKIMG
  LC_FCAT-SELTEXT_L = TEXT-H10.               "数量
  LC_FCAT-DATATYPE  = CNS_QUAN.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_KZWI1.              "KZWI1
  LC_FCAT-SELTEXT_L = TEXT-H11.               "含税金额
  LC_FCAT-DATATYPE  = CNS_CURR.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_WAERK.              "WAERK
  LC_FCAT-SELTEXT_L = TEXT-H12.               "货币
  LC_FCAT-DATATYPE  = CNS_CHAR.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_SHUI.               "SHUI
  LC_FCAT-SELTEXT_L = TEXT-H13.               "税率
  LC_FCAT-DATATYPE  = CNS_NUMC.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_KUNAG.              "KUNAG
  LC_FCAT-SELTEXT_L = TEXT-H14.               "客户代码
  LC_FCAT-DATATYPE  = CNS_CHAR.
  APPEND LC_FCAT TO IT_FCAT.

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_STCEG.              "STCEG
  LC_FCAT-SELTEXT_L = TEXT-H15.               "税码
  LC_FCAT-DATATYPE  = CNS_CHAR.
  APPEND LC_FCAT TO IT_FCAT.

* ---- INSERT BY NEC-DEV07 20100108 START
  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_BEIZ.               "BEIZ
  LC_FCAT-SELTEXT_L = TEXT-H18.               "备注
  LC_FCAT-DATATYPE  = CNS_CHAR.
  LC_FCAT-EDIT = CNS_X.
  LC_FCAT-LOWERCASE = CNS_X.
  LC_FCAT-OUTPUTLEN = 128.
  APPEND LC_FCAT TO IT_FCAT.
* ---- INSERT BY NEC-DEV07 20100108 END

  CLEAR LC_FCAT.
  LC_FCAT-FIELDNAME = CNS_SFAKN.              "SFAKN
  LC_FCAT-SELTEXT_L = TEXT-H16.               "取消
  LC_FCAT-DATATYPE  = CNS_CHAR.
  APPEND LC_FCAT TO IT_FCAT.

ENDFORM.                    "SET_FIELDCAT
*&---------------------------------------------------------------------*
*&      Form  EDIT_DOWNLOAD
*&---------------------------------------------------------------------*
*       下载数据
*----------------------------------------------------------------------*
FORM EDIT_DOWNLOAD .

  FLG_STOP = SPACE.
* 验证数据
  PERFORM CHECK_DATA_DANWEI.
  CHECK FLG_STOP = SPACE.
* 取得备注信息
  PERFORM GET_TEXT_DATA.
  CHECK FLG_STOP = SPACE.
* 下载数据
  PERFORM DOWNLOAD_DATA_DIV.
  CHECK FLG_STOP = SPACE.
* 更新add-on表
  PERFORM UPDATE_ZCSDT5.
* 输出日志
  PERFORM OUTPUT_MSG.

ENDFORM.                    " EDIT_DOWNLOAD
*&---------------------------------------------------------------------*
*&      Form  GET_TEXT
*&---------------------------------------------------------------------*
*       取得备注信息
*----------------------------------------------------------------------*
FORM GET_TEXT_DATA .

  DATA: RG_VBELN TYPE RANGE OF VBELN_VF,
        LR_VBELN LIKE LINE OF RG_VBELN,
        LC_NUM   TYPE I,
        LC_FLAG  TYPE C,
        LC_VBELV TYPE VBELN,
        LC_VBELN1 TYPE VBELN,
        LC_VBELN TYPE VBELN.

  REFRESH:IT_DOWN,
          IT_MSG,
          IT_INSERT,
          IT_BAKUP,
          IT_OUTPUT.
  IT_BAKUP[] = IT_DATA[].
  CLEAR:LR_VBELN.
  LR_VBELN-SIGN = CNS_I.
  LR_VBELN-OPTION = CNS_EQ.
  LOOP AT IT_DATA INTO WA_DATA
                 WHERE BOX = CNS_X.
    LR_VBELN-LOW = WA_DATA-VBELN.
    COLLECT LR_VBELN INTO RG_VBELN.
  ENDLOOP.
  IF SY-SUBRC <> 0.
* 请至少选择一条数据。
    MESSAGE S022.
    FLG_STOP = CNS_X.
    EXIT.
  ENDIF.
  SORT IT_DATA BY VBELN MATNR.
  CLEAR:LC_VBELN,
        LC_FLAG.
  LOOP AT IT_DATA INTO WA_DATA
                 WHERE VBELN IN RG_VBELN.
* 冲销 贷项
    IF WA_DATA-VBTYP = CNS_N OR
       WA_DATA-VBTYP = CNS_O.
      IF WA_DATA-VBTYP = CNS_N.
        LC_VBELN = WA_DATA-SFAKN.
      ELSE.
        SELECT VBELV                          "销售单
          INTO LC_VBELV
          FROM VBFA
               UP TO 1 ROWS
         WHERE VBELN = WA_DATA-VBELN          "发票
           AND VBTYP_V = CNS_C.               "类型
        ENDSELECT.
        SELECT VBELN                          "发票
          INTO LC_VBELN
               UP TO 1 ROWS
          FROM VBFA
         WHERE VBELV = LC_VBELV               "销售单
           AND VBTYP_V = CNS_M.               "类型
        ENDSELECT.
      ENDIF.
* 取得备注
      PERFORM GET_BEIZHU USING LC_VBELN.
    ELSE.
* ---- DELETE BY NEC-DEV07 20100108 START
*      CONCATENATE TEXT-008
*                  WA_DATA-VBELN
*             INTO WA_DATA-BEIZ
*        SEPARATED BY SPACE.
* ---- DELETE BY NEC-DEV07 20100108 END
    ENDIF.
* 检查税号
    PERFORM CHECK_TAX_NUM.
    IF LC_VBELN1 <> WA_DATA-VBELN.
      LC_NUM = 0.
    ENDIF.
* 已开票数据
    IF WA_DATA-ZSTAUS <> SPACE.
      LC_FLAG = WA_DATA-ZSTAUS.
    ENDIF.
    LC_NUM = LC_NUM + 1.
* 如果是新增数据
* 拆分金额
    PERFORM SPLIT_AMOUNT CHANGING LC_NUM.
    LC_VBELN1 = WA_DATA-VBELN.
* 状态选择
    PERFORM SEL_STATUS USING WA_DATA-ZSTAUS
                    CHANGING WA_DATA-STA.
    MODIFY IT_DATA FROM WA_DATA
      TRANSPORTING BEIZ
                   MSG
                   ZSTAUS
                   STA
                   FLG.
  ENDLOOP.
* 确认下载
  IF LC_FLAG <> SPACE.
    PERFORM COMFIRM_DIALOG USING LC_FLAG.
  ENDIF.
  CHECK FLG_STOP = SPACE.
  APPEND LINES OF IT_MSG TO IT_DOWN.
* 取得下载路径
  PERFORM GET_FILE_NAME.
  CLEAR:W_NAME1,
        W_NAME2.
  CONCATENATE W_NAME
              CNS_BIS
              SY-DATUM
              SY-UZEIT
              CNS_01
              CNS_EXT
         INTO W_NAME1.
  CONCATENATE W_NAME
              CNS_BIS
              SY-DATUM
              SY-UZEIT
              CNS_02
              CNS_EXT
         INTO W_NAME2.
  IF W_NAME IS INITIAL.
    FLG_STOP = CNS_X.
    IT_DATA[] = IT_BAKUP[].
    EXIT.
  ENDIF.
* 锁表
  PERFORM LOCK_ZCSDT005.
  IF FLG_STOP = CNS_X.
    EXIT.
  ENDIF.
* 取得最新add-on 表状态
  REFRESH: IT_ZCSDT5.
  PERFORM GET_ZCSDT005.
* 赋值下载与更新数据
  PERFORM UPDATE_AND_DOWNLOAD.

ENDFORM.                    " GET_TEXT
*&---------------------------------------------------------------------*
*&      Form  GET_BEIZHU
*&---------------------------------------------------------------------*
*       取得备注
*----------------------------------------------------------------------*
FORM GET_BEIZHU USING I_VBELN TYPE VBELN.

  DATA: LC_ZSFPXH TYPE ZCSDT005-ZZSFPXH,
        LC_ZSFPHM TYPE ZCSDT005-ZZSFPHM,
        LC_ZZFLAG TYPE ZCSDT008-ZZFLAG.

  SELECT ZZSFPXH                              "金穗发票类别代码
         ZZSFPHM                              "金穗发票号码
    INTO (LC_ZSFPXH,
          LC_ZSFPHM)
    FROM ZCSDT005
         UP TO 1 ROWS
   WHERE VBELN = I_VBELN.                     "开票凭证
  ENDSELECT.
  IF SY-SUBRC <> 0.
    WA_DATA-ERRFG = CNS_1.
    WA_DATA-MSG = TEXT-012.
  ELSE.
    SELECT SINGLE ZZFLAG                      "专用增税标记
      INTO LC_ZZFLAG
      FROM ZCSDT008
     WHERE KUNNR = WA_DATA-KUNAG.             "客户
    IF LC_ZZFLAG = SPACE.
      CONCATENATE TEXT-006
                  LC_ZSFPXH
                  TEXT-007
                  LC_ZSFPHM
                  TEXT-009
* ---- MODIFY BY NEC-DEV07 20100108 START
*                  TEXT-008
*                  WA_DATA-VBELN
                  WA_DATA-BEIZ
* ---- MODIFY BY NEC-DEV07 20100108 END
             INTO WA_DATA-BEIZ.
    ELSE.
       CONCATENATE TEXT-011
                   TEXT-009
* ---- MODIFY BY NEC-DEV07 20100108 START
*                   TEXT-008
*                   WA_DATA-VBELN
                   WA_DATA-BEIZ
* ---- MODIFY BY NEC-DEV07 20100108 END
              INTO WA_DATA-BEIZ.

    ENDIF.
  ENDIF.


ENDFORM.                    " GET_BEIZHU
*&---------------------------------------------------------------------*
*&      Form  CHECK_TAX_NUM
*&---------------------------------------------------------------------*
*       检查税号
*----------------------------------------------------------------------*
FORM CHECK_TAX_NUM .

  DATA: LC_CHAR TYPE I,
        LC_TYPE TYPE DD01V-DATATYPE.

  CLEAR:WA_ZCSDT8.
  READ TABLE IT_ZCSDT8 INTO WA_ZCSDT8
    WITH KEY KUNNR = WA_DATA-KUNAG.
  IF WA_ZCSDT8-ZZFLAG <> SPACE.
    WA_DATA-FLG = CNS_X.
  ELSE.
    CLEAR:WA_DATA-FLG.
  ENDIF.
  CHECK WA_ZCSDT8-ZZFLAG <> SPACE.
  LC_CHAR = STRLEN( WA_DATA-STCEG ).
  IF LC_CHAR <> 15.
    WA_DATA-ERRFG = 3.
    WA_DATA-MSG = TEXT-013.
    EXIT.
  ENDIF.
  CALL FUNCTION 'NUMERIC_CHECK'
    EXPORTING
      STRING_IN = WA_DATA-STCEG+0(1)
    IMPORTING
      HTYPE     = LC_TYPE.
  IF LC_TYPE <> CNS_NUMC.
    WA_DATA-ERRFG = 3.
    WA_DATA-MSG = TEXT-013.
  ENDIF.

ENDFORM.                    " CHECK_TAX_NUM
*&---------------------------------------------------------------------*
*&      Form  DOWNLOAD_DATA
*&---------------------------------------------------------------------*
*       下载数据
*----------------------------------------------------------------------*
FORM DOWNLOAD_DATA .

* 下载文件
  CALL FUNCTION 'GUI_DOWNLOAD'
    EXPORTING
      FILENAME                = W_NAME        "文件路径
      COL_SELECT              = CNS_X
      COL_SELECT_MASK         = CNS_MASK
      TRUNC_TRAILING_BLANKS   = CNS_X         "去除空格
    TABLES
      DATA_TAB                = IT_OUTPUT     "数据下载
    EXCEPTIONS
      FILE_WRITE_ERROR        = 1
      NO_BATCH                = 2
      GUI_REFUSE_FILETRANSFER = 3
      INVALID_TYPE            = 4
      NO_AUTHORITY            = 5
      UNKNOWN_ERROR           = 6
      HEADER_NOT_ALLOWED      = 7
      SEPARATOR_NOT_ALLOWED   = 8
      FILESIZE_NOT_ALLOWED    = 9
      HEADER_TOO_LONG         = 10
      DP_ERROR_CREATE         = 11
      DP_ERROR_SEND           = 12
      DP_ERROR_WRITE          = 13
      UNKNOWN_DP_ERROR        = 14
      ACCESS_DENIED           = 15
      DP_OUT_OF_MEMORY        = 16
      DISK_FULL               = 17
      DP_TIMEOUT              = 18
      FILE_NOT_FOUND          = 19
      DATAPROVIDER_EXCEPTION  = 20
      CONTROL_FLUSH_ERROR     = 21
      OTHERS                  = 22.
  IF SY-SUBRC = 0.
*下载成功
    MESSAGE S015.
  ENDIF.
  IF SY-SUBRC <> 0.
    FLG_STOP = CNS_X.
    MESSAGE ID SY-MSGID TYPE CNS_S NUMBER SY-MSGNO
            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.


ENDFORM.                    " DOWNLOAD_DATA
*&---------------------------------------------------------------------*
*&      Form  OUTPUT_MSG
*&---------------------------------------------------------------------*
*       输出日志
*----------------------------------------------------------------------*
FORM OUTPUT_MSG .

  DATA: LR_FCAT TYPE LVC_S_FCAT,
        LT_FCAT TYPE LVC_T_FCAT,
        LR_STBL TYPE LVC_S_STBL,
        LC_GRID1  TYPE REF TO CL_GUI_ALV_GRID.
  CHECK IT_MSG[] IS NOT INITIAL.
  CALL FUNCTION 'GET_GLOBALS_FROM_SLVC_FULLSCR'
       IMPORTING
         E_GRID           =  LC_GRID1
                .
* MODIFY THE FIELDCATALOG WITH DROPDOWN LIST
  CALL METHOD LC_GRID1->GET_FRONTEND_FIELDCATALOG
    IMPORTING
      ET_FIELDCATALOG = LT_FCAT[]
      .
* ---- INSERT BY NEC-DEV07 02100108 START
  READ TABLE LT_FCAT INTO LR_FCAT
    WITH KEY FIELDNAME = CNS_MSG.
  CHECK SY-SUBRC <> 0.
* ---- INSERT BY NEC-DEV07 02100108 END
  CLEAR LR_FCAT.
  LR_FCAT-FIELDNAME = CNS_MSG.                "FKIMG
  LR_FCAT-SCRTEXT_L = TEXT-H17.               "消息
  LR_FCAT-DATATYPE  = CNS_CHAR.
  INSERT LR_FCAT INTO LT_FCAT INDEX 1.
  CALL METHOD LC_GRID1->SET_FRONTEND_FIELDCATALOG
    EXPORTING
      IT_FIELDCATALOG = LT_FCAT[]
      .
  LR_STBL-ROW = CNS_X.
  LR_STBL-COL = CNS_X.
  CALL METHOD LC_GRID1->REFRESH_TABLE_DISPLAY
    EXPORTING
      IS_STABLE      = LR_STBL
    .

ENDFORM.                    " OUTPUT_MSG
*&---------------------------------------------------------------------*
*&      Form  SPLIT_AMOUNT
*&---------------------------------------------------------------------*
*       拆分金额
*----------------------------------------------------------------------*
FORM SPLIT_AMOUNT CHANGING O_NUM TYPE I.

  DATA: LC_AMT TYPE KZWI1,
        LC_BK1 TYPE KZWI1,
        LC_BK2 LIKE WA_DATA-FKIMG,
        LC_BK3 TYPE KZWI1,
        LC_BK4 TYPE KZWI1,
        LC_INDEX TYPE I.

  LC_AMT = WA_DATA-KZWI1.
  LC_BK1 = WA_DATA-KZWI1.
  LC_BK2 = WA_DATA-FKIMG.
  LC_BK3 = WA_DATA-NETWR.
  LC_BK4 = WA_DATA-MWSBP.

  LC_INDEX = 0.
* 拆分金额
  WHILE LC_AMT >= P_DMBTR.
    LC_INDEX = LC_INDEX + 1.
    WA_DATA-KZWI1 = WA_DATA-KZWI1 / 2.
    WA_DATA-FKIMG = WA_DATA-FKIMG / 2.
    WA_DATA-NETWR = WA_DATA-NETWR / 2.
    WA_DATA-MWSBP = WA_DATA-MWSBP / 2.
    LC_AMT = WA_DATA-KZWI1.
  ENDWHILE.
  LC_INDEX = 2 ** LC_INDEX  - 1.
  DO LC_INDEX TIMES.
    WA_DATA-LINE = O_NUM.
    IF WA_DATA-ERRFG = SPACE.
      WA_DATA-ZSTAUS = 1.
      APPEND WA_DATA TO IT_DOWN.
    ELSE.
      WA_DATA-ZSTAUS = 2.
      APPEND WA_DATA TO IT_MSG.
    ENDIF.
    O_NUM = O_NUM + 1.
  ENDDO.
* 保证没有丢失
  WA_DATA-KZWI1 = LC_BK1 - WA_DATA-KZWI1 * LC_INDEX.
  WA_DATA-FKIMG = LC_BK2 - WA_DATA-FKIMG * LC_INDEX.
  WA_DATA-NETWR = LC_BK3 - WA_DATA-NETWR * LC_INDEX.
  WA_DATA-MWSBP = LC_BK4 - WA_DATA-MWSBP * LC_INDEX.
  WA_DATA-LINE = O_NUM.
  IF WA_DATA-ERRFG = SPACE.
    WA_DATA-ZSTAUS = 1.
    APPEND WA_DATA TO IT_DOWN.
  ELSE.
    WA_DATA-ZSTAUS = 2.
    APPEND WA_DATA TO IT_MSG.
  ENDIF.

ENDFORM.                    " SPLIT_AMOUNT
*&---------------------------------------------------------------------*
*&      Form  CHECK_DATA_DANWEI
*&---------------------------------------------------------------------*
*       验证数据
*----------------------------------------------------------------------*
FORM CHECK_DATA_DANWEI .

  DATA: LC_GRID1  TYPE REF TO CL_GUI_ALV_GRID,
        LC_VALID TYPE C.

* ALV 中的值放到内部表中
  CALL FUNCTION 'GET_GLOBALS_FROM_SLVC_FULLSCR'
   IMPORTING
     E_GRID           =  LC_GRID1
            .
  CALL METHOD LC_GRID1->CHECK_CHANGED_DATA
    IMPORTING E_VALID = LC_VALID.
  IF LC_VALID <> CNS_X.
    FLG_STOP = CNS_X.
  ENDIF.

ENDFORM.                    " CHECK_DATA_DANWEI
*&---------------------------------------------------------------------*
*&      Form  UPDATE_ZCSDT5
*&---------------------------------------------------------------------*
*       更新add-on表
*----------------------------------------------------------------------*
FORM UPDATE_ZCSDT5 .

  IF IT_INSERT[] IS NOT INITIAL.
    INSERT ZCSDT005 FROM TABLE IT_INSERT ACCEPTING DUPLICATE KEYS.
    IF SY-SUBRC <> 0.
*   更新发票状态明细表失败
      MESSAGE S048.
      FLG_STOP = CNS_X.
      ROLLBACK WORK.
    ENDIF.
  ENDIF.
  COMMIT WORK.
* 解锁
  PERFORM UNLOCK_ZCSDT005.

ENDFORM.                    " UPDATE_ZCSDT5
*&---------------------------------------------------------------------*
*&      Form  LOCK_ZCSDT005
*&---------------------------------------------------------------------*
*       锁表ZCSDT005
*----------------------------------------------------------------------*
FORM LOCK_ZCSDT005.

  DO 10 TIMES.
    CALL FUNCTION 'ENQUEUE_EZCSDT005'
      EXPORTING
        MODE_ZCSDT005         = CNS_E
      EXCEPTIONS
        FOREIGN_LOCK         = 1
        SYSTEM_FAILURE       = 2
        OTHERS               = 3
                .
    IF SY-SUBRC <> 0.
      WAIT UP TO 1 SECONDS.
    ENDIF.
  ENDDO.
* LOCK表失败
  IF SY-SUBRC <> 0.
    FLG_STOP = CNS_X.
    MESSAGE ID SY-MSGID TYPE CNS_S NUMBER SY-MSGNO
          WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

ENDFORM.                    " LOCK_ZCSDT005
*&---------------------------------------------------------------------*
*&      Form  UNLOCK_ZCSDT005
*&---------------------------------------------------------------------*
*       解锁表ZCSDT005
*----------------------------------------------------------------------*
FORM UNLOCK_ZCSDT005.

  CALL FUNCTION 'DEQUEUE_EZCSDT005'
    EXPORTING
      MODE_ZCSDT005       = CNS_E
                .

ENDFORM.                    " UNLOCK_ZCSDT005
*&---------------------------------------------------------------------*
*&      Form  ASSIGN_ZCSDT5_INSERT
*&---------------------------------------------------------------------*
*        赋值新增发票状态明细表
*----------------------------------------------------------------------*
FORM ASSIGN_ZCSDT5_INSERT .

  READ TABLE IT_ZCSDT5 INTO WA_ZCSDT5
    WITH KEY VBELN = WA_DATA-VBELN            "开票凭证
    BINARY SEARCH.
  IF SY-SUBRC = 0.
    IF WA_DATA-ERRFG = SPACE AND
       WA_ZCSDT5-ZSTAUS = 2.
      UPDATE ZCSDT005 SET ZSTAUS = 1
                          VRKME = WA_DATA-VRKME"单位
                          BEIZ  = WA_DATA-BEIZ
                          ARKTX = WA_DATA-ARKTX
                          ZZVELNA = SY-UNAME  "修改者
                          ZZDDATE = SY-DATUM  "下载日期
        WHERE VBELN = WA_DATA-VBELN           "发票
          AND MATNR = WA_DATA-MATNR           "物料代码
          AND BUKRS = P_BUKRS.                "公司代码
    ELSE.
      UPDATE ZCSDT005 SET ZSTAUS = WA_DATA-ZSTAUS
                          VRKME = WA_DATA-VRKME"单位
                          BEIZ  = WA_DATA-BEIZ
                          ARKTX = WA_DATA-ARKTX
                          ZZVELNA = SY-UNAME  "修改者
                          ZZDDATE = SY-DATUM  "下载日期
        WHERE VBELN = WA_DATA-VBELN           "发票
          AND MATNR = WA_DATA-MATNR           "物料代码
          AND BUKRS = P_BUKRS.                "公司代码
    ENDIF.
    EXIT.
  ENDIF.
  CLEAR: WA_ZCSDT5.
  WA_ZCSDT5-BUKRS = P_BUKRS.                  "公司代码
  WA_ZCSDT5-VBELN = WA_DATA-VBELN.            "开票凭证
  WA_ZCSDT5-POSNR = WA_DATA-LINE.             "出具发票项目
  WA_ZCSDT5-ZSTAUS = WA_DATA-ZSTAUS.          "上传下载状态
  WA_ZCSDT5-VKORG = WA_DATA-VKORG.            "销售组织
  WA_ZCSDT5-KUNAG = WA_DATA-KUNAG.            "售达方
  WA_ZCSDT5-FKDAT = WA_DATA-FKDAT.            "出具发票日期
  WA_ZCSDT5-MATNR = WA_DATA-MATNR.            "物料代码
  WA_ZCSDT5-ARKTX = WA_DATA-ARKTX.            "描述
  WA_ZCSDT5-RFBSK = WA_DATA-RFBSK.            "转到会计的状态
  WA_ZCSDT5-NETWR = WA_DATA-NETWR.            "净价值
  WA_ZCSDT5-FKIMG = WA_DATA-FKIMG.            "数量
  WA_ZCSDT5-VRKME = WA_DATA-VRKME.            "单位
  WA_ZCSDT5-ARKTX = WA_DATA-ARKTX.            "文本
  WA_ZCSDT5-BEIZ  = WA_DATA-BEIZ.             "备注
  WA_ZCSDT5-ZZDANJIA = WA_DATA-PRICE.         "单价
  WA_ZCSDT5-MWSBK = WA_DATA-MWSBP.            "税额
  WA_ZCSDT5-WAERK = WA_DATA-WAERK.            "SD 凭证货币
  WA_ZCSDT5-ZSONO = WA_DATA-VBELV.            "销售和分销凭证号
  WA_ZCSDT5-ERNAM = SY-UNAME.                 "创建对象的人员名称
  WA_ZCSDT5-ERDAT = SY-DATUM.                 "创建日期
  WA_ZCSDT5-ERZET = SY-UZEIT.                 "输入时间
  WA_ZCSDT5-ZZDDATE = SY-DATUM.               "下载日期
  WA_ZCSDT5-ZZVELNA = SY-UNAME.               "更新者
  APPEND WA_ZCSDT5 TO IT_INSERT.

ENDFORM.                    " ASSIGN_ZCSDT5_INSERT
*&---------------------------------------------------------------------*
*&      Form  COMFIRM_DIALOG
*&---------------------------------------------------------------------*
*       确认下载
*----------------------------------------------------------------------*
FORM COMFIRM_DIALOG USING I_FLAG TYPE C.

  DATA: LC_ANS TYPE C,
        LC_TEXT(80) TYPE C.

* 已下载
  IF I_FLAG = 1.
    LC_TEXT = TEXT-D04.
  ENDIF.
* 下载错误
  IF I_FLAG = 2.
    LC_TEXT = TEXT-D07.
  ENDIF.
* 导入成功的数据
  IF I_FLAG = 3.
    LC_TEXT = TEXT-D08.
  ENDIF.
* 发票作废
  IF I_FLAG = 4.
    LC_TEXT = TEXT-D09.
  ENDIF.
  CALL FUNCTION 'POPUP_TO_CONFIRM_WITH_MESSAGE'"#EC *
    EXPORTING
      DEFAULTOPTION        = CNS_N            "返回值
      DIAGNOSETEXT1        = LC_TEXT          "对话语句
      TEXTLINE1            = TEXT-D05         "文本内容
      TITEL                = TEXT-D06         "确认框
      CANCEL_DISPLAY       = SPACE
    IMPORTING
      ANSWER               = LC_ANS           "返回值
           .
  IF LC_ANS = CNS_N.
    FLG_STOP = CNS_X.
    IT_DATA[] = IT_BAKUP[].
  ENDIF.

ENDFORM.                    " COMFIRM_DIALOG
*&---------------------------------------------------------------------*
*&      Form  UPDATE_AND_DOWNLOAD
*&---------------------------------------------------------------------*
*       赋值下载与更新数据
*----------------------------------------------------------------------*
FORM UPDATE_AND_DOWNLOAD .

  DATA: LC_VBELN   TYPE VBELN,
        LC_NUM     TYPE I,
        LC_NUMC(4) TYPE C.

  SORT IT_DOWN BY FLG
                  VBELN
                  LINE DESCENDING
                  MATNR.
  SORT IT_MSG BY VBELN.
  REFRESH: IT_OUTPUT.
  CLEAR:LC_VBELN.
  CLEAR:WA_OUTPUT.
  CONCATENATE TEXT-014
              TEXT-015
              TEXT-016
         INTO WA_OUTPUT-LINE
    SEPARATED BY CNS_DIV.
  APPEND WA_OUTPUT TO IT_OUTPUT.
  LOOP AT IT_DOWN INTO WA_DATA.
* 赋值新增发票状态明细表
    PERFORM ASSIGN_ZCSDT5_INSERT.
    READ TABLE IT_MSG INTO WA_MSG
      WITH KEY VBELN = WA_DATA-VBELN
      BINARY SEARCH.
    IF SY-SUBRC = 0.
      CONTINUE.
    ENDIF.
    IF WA_DATA-VBELN <> LC_VBELN.
      CLEAR:WA_OUTPUT.
* 抬头编辑
      LC_NUMC = WA_DATA-LINE.
      CONDENSE LC_NUMC.
      WA_DATA-DATE = WA_DATA-FKDAT.
      IF P_BUKRS = CNS_4002.
        WA_DATA-ACC = TEXT-024.
        WA_DATA-TEL = TEXT-025.
      ENDIF.
      IF P_BUKRS = CNS_4001.
        WA_DATA-ACC = TEXT-024.
      ENDIF.
      IF P_BUKRS = CNS_4005.
        WA_DATA-ACC = TEXT-024.
      ENDIF.
      CONCATENATE WA_DATA-VBELN               "单据号
                  LC_NUMC                     "商品行数
                  WA_DATA-NAME1               "购方名称
                  WA_DATA-STCEG               "购方税号
                  WA_DATA-ADDR                "购方地址电话
                  WA_DATA-BANK                "购方银行帐号
                  WA_DATA-BEIZ                "备注
                  WA_DATA-UNAME               "复核人
                  WA_DATA-REV                 "收款人
                  WA_DATA-PRD                 "清单行商品名称
                  WA_DATA-DATE                "单据日期
                  WA_DATA-ACC                 "销方银行账号
                  WA_DATA-TEL                 "销方地址电话
             INTO WA_OUTPUT-LINE
        SEPARATED BY CNS_DIV.
      WA_OUTPUT-FLG = WA_DATA-FLG.
      APPEND WA_OUTPUT TO IT_OUTPUT.
    ENDIF.
    CLEAR:WA_OUTPUT.
    WRITE WA_DATA-FKIMG TO WA_DATA-SHU        "实际已开发票量
      NO-GROUPING.
    WRITE WA_DATA-KZWI1 TO WA_DATA-JIA1       "含税金额
      CURRENCY CNS_CNY
      NO-GROUPING.
    WRITE WA_DATA-MWSBP TO WA_DATA-JIA3       "税额
      CURRENCY CNS_CNY
      NO-GROUPING.
    CONDENSE:  WA_DATA-SHU NO-GAPS,
               WA_DATA-JIA1 NO-GAPS,
               WA_DATA-PRICE NO-GAPS,
               WA_DATA-JIA3 NO-GAPS,
               WA_DATA-SHUI NO-GAPS.
    CONCATENATE WA_DATA-ARKTX                 "货物名称
                WA_DATA-VRKME                 "计量单位
                WA_DATA-MATNR                 "规格
                WA_DATA-SHU                   "数量
                WA_DATA-JIA1                  "含税金额
                WA_DATA-SHUI                  "税率
                CNS_9001                      "商品税目
                WA_DATA-TEXT1                 "折扣金额
* ---- MODIFY BY NEC-DEV07 20100114 START
*                WA_DATA-JIA3                  "税额
                WA_DATA-TEXT1                 "税额
* ---- MODIFY BY NEC-DEV07 20100114 END
                WA_DATA-TEXT1                 "折扣税额
                WA_DATA-TEXT1                 "折扣率
* ---- MODIFY BY NEC-DEV07 20100114 START
*                WA_DATA-PRICE                 "单价
                WA_DATA-TEXT1                 "单价
* ---- MODIFY BY NEC-DEV07 20100114 END
                CNS_1                         "价格方式
           INTO WA_OUTPUT-LINE
      SEPARATED BY CNS_DIV.
    WA_OUTPUT-FLG = WA_DATA-FLG.
    APPEND WA_OUTPUT TO IT_OUTPUT.
    LC_VBELN = WA_DATA-VBELN.
  ENDLOOP.
  DESCRIBE TABLE IT_OUTPUT LINES LC_NUM.
  IF LC_NUM = 1.
    FLG_STOP = CNS_X.
* 更新add-on
    PERFORM UPDATE_ZCSDT5 .
* 输出日志
    PERFORM OUTPUT_MSG.
  ENDIF.

ENDFORM.                    " UPDATE_AND_DOWNLOAD
*&---------------------------------------------------------------------*
*&      Form  SEL_STATUS
*&---------------------------------------------------------------------*
*       状态选择
*----------------------------------------------------------------------*
FORM SEL_STATUS  USING    I_STAUS TYPE c
                 CHANGING O_STA TYPE CHAR10.

  CASE I_STAUS.
* 未下载
    WHEN SPACE.
      O_STA = TEXT-004.
* 已下载
    WHEN 1.
      O_STA = TEXT-005.
* 下载错误
    WHEN 2.
      O_STA = TEXT-023.
* 导入成功的数据
    WHEN 3.
      O_STA = TEXT-026.
* 发票作废
    WHEN 4.
      O_STA = TEXT-027.
    WHEN OTHERS.
  ENDCASE.

ENDFORM.                    " SEL_STATUS
*&---------------------------------------------------------------------*
*&      Form  DEL_DOC
*&---------------------------------------------------------------------*
*       作废发票
*----------------------------------------------------------------------*
FORM DEL_DOC .

* 确认作废发票
  PERFORM COMFIRM_DOC.
  CHECK FLG_STOP = SPACE.
* 编辑作废发票内容
  PERFORM EDIT_DEL_DOC.
  CHECK FLG_STOP = SPACE.
* 下载数据
  PERFORM DOWNLOAD_DATA.

ENDFORM.                    " DEL_DOC
*&---------------------------------------------------------------------*
*&      Form  COMFIRM_DOC
*&---------------------------------------------------------------------*
*       确认作废发票
*----------------------------------------------------------------------*
FORM COMFIRM_DOC .

  DATA: LC_ANS TYPE C.

  FLG_STOP = SPACE.
  LOOP AT IT_DATA INTO WA_DATA
                 WHERE BOX = CNS_X.
    EXIT.
  ENDLOOP.
  IF SY-SUBRC <> 0.
* 请至少选择一条数据。
    MESSAGE S022.
    FLG_STOP = CNS_X.
    EXIT.
  ENDIF.
  IT_BAKUP[] = IT_DATA[].
  CALL FUNCTION 'POPUP_TO_CONFIRM_WITH_MESSAGE'"#EC *
    EXPORTING
      DEFAULTOPTION        = CNS_N            "返回值
      DIAGNOSETEXT1        = TEXT-D11         "对话语句
      TEXTLINE1            = TEXT-D10         "文本内容
      TITEL                = TEXT-D06         "确认框
      CANCEL_DISPLAY       = SPACE
    IMPORTING
      ANSWER               = LC_ANS           "返回值
           .
  IF LC_ANS = CNS_N.
    FLG_STOP = CNS_X.
  ENDIF.

ENDFORM.                    " COMFIRM_DOC
*&---------------------------------------------------------------------*
*&      Form  EDIT_DEL_DOC
*&---------------------------------------------------------------------*
*       编辑作废发票内容
*----------------------------------------------------------------------*
FORM EDIT_DEL_DOC .

  DATA: LT_DATA LIKE IT_DATA,
        LC_FILE TYPE RLGRAP-FILENAME.

* 取得下载路径
*chen PERFORM HELP_DIS CHANGING LC_FILE.
  W_NAME = LC_FILE.
  IF W_NAME IS INITIAL.
    FLG_STOP = CNS_X.
    IT_DATA[] = IT_BAKUP[].
    EXIT.
  ENDIF.
  REFRESH:IT_OUTPUT.
  CLEAR:WA_OUTPUT.
* 抬头
  CONCATENATE TEXT-018
              TEXT-019
              TEXT-016
         INTO WA_OUTPUT-LINE
   SEPARATED BY CNS_DIV.
  APPEND WA_OUTPUT TO IT_OUTPUT.
  LT_DATA[] = IT_DATA[].
  WA_DATA-STA = TEXT-027.
  WA_DATA-ZSTAUS = 4.
  MODIFY IT_DATA FROM WA_DATA
                 TRANSPORTING STA
                              ZSTAUS
                WHERE BOX = CNS_X.
  DELETE LT_DATA WHERE BOX = SPACE.
  SORT LT_DATA BY VBELN.
  DELETE ADJACENT DUPLICATES FROM LT_DATA
                        COMPARING VBELN.
  LOOP AT LT_DATA INTO WA_DATA.
    CLEAR:WA_OUTPUT.
    UPDATE ZCSDT005 SET ZSTAUS = 4
                        ZZVELNA = SY-UNAME  "修改者
                        ZZDDATE = SY-DATUM  "下载日期
      WHERE VBELN = WA_DATA-VBELN           "发票
        AND BUKRS = P_BUKRS.                "公司代码
    WA_OUTPUT-LINE = WA_DATA-VBELN.
    APPEND WA_OUTPUT TO IT_OUTPUT.
  ENDLOOP.
  COMMIT WORK.

ENDFORM.                    " EDIT_DEL_DOC
* ---- INSERT BY NEC-DEV07 20100108 START
*&---------------------------------------------------------------------*
*&      Form  DATA_CHANGE
*&---------------------------------------------------------------------*
*      -->RR_DATA_CHANGED TYPE REF TO
*                                 CL_ALV_CHANGED_DATA_PROTOCOL
*----------------------------------------------------------------------*
FORM DATA_CHANGE USING RR_DATA_CHANGED TYPE REF TO
                         CL_ALV_CHANGED_DATA_PROTOCOL.

  DATA: LR_MOD_CELLS TYPE LVC_S_MODI.

  LOOP AT RR_DATA_CHANGED->MT_GOOD_CELLS INTO LR_MOD_CELLS.
    CASE LR_MOD_CELLS-FIELDNAME.
      WHEN CNS_BEIZ.
        IF LR_MOD_CELLS-VALUE <> SPACE.
          CLEAR:WA_DATA.
          READ TABLE IT_DATA INTO WA_DATA INDEX LR_MOD_CELLS-ROW_ID.
          WA_DATA-BEIZ = LR_MOD_CELLS-VALUE.
          MODIFY IT_DATA FROM WA_DATA
            TRANSPORTING BEIZ
            WHERE VBELN = WA_DATA-VBELN.
        ENDIF.
      WHEN OTHERS.
    ENDCASE.
  ENDLOOP.

ENDFORM.
* ---- INSERT BY NEC-DEV07 20100108 END
*&---------------------------------------------------------------------*
*&      Form  DOWNLOAD_DATA_DIV
*&---------------------------------------------------------------------*
*       下载数据
*----------------------------------------------------------------------*
FORM DOWNLOAD_DATA_DIV .

  DATA: LT_DOWN LIKE IT_OUTPUT,
        LC_NUM TYPE I.

  LT_DOWN[] = IT_OUTPUT[].
  DELETE IT_OUTPUT WHERE FLG = CNS_X.
  LC_NUM = 0.
  DESCRIBE TABLE IT_OUTPUT LINES LC_NUM.
  IF LC_NUM > 1.
    W_NAME = W_NAME2.
    PERFORM DOWNLOAD_DATA.
  ENDIF.
  IT_OUTPUT[] = LT_DOWN[].
  WA_OUTPUT-FLG = CNS_X.
  MODIFY IT_OUTPUT FROM WA_OUTPUT INDEX 1
    TRANSPORTING FLG.
  DELETE IT_OUTPUT WHERE FLG = SPACE.
  LC_NUM = 0.
  DESCRIBE TABLE IT_OUTPUT LINES LC_NUM.
  IF LC_NUM > 1.
    W_NAME = W_NAME1.
    PERFORM DOWNLOAD_DATA.
  ENDIF.

ENDFORM.                    " DOWNLOAD_DATA_DIV
*&---------------------------------------------------------------------*
*&      Form  GET_FILE_NAME
*&---------------------------------------------------------------------*
*       获得下载文件名
*----------------------------------------------------------------------*
FORM GET_FILE_NAME.

  DATA LC_PATH TYPE STRING.
* 下载路径选择
  CALL METHOD CL_GUI_FRONTEND_SERVICES=>DIRECTORY_BROWSE
    CHANGING
      SELECTED_FOLDER      = LC_PATH          "路径
    EXCEPTIONS
      CNTL_ERROR           = 1
      ERROR_NO_GUI         = 2
      NOT_SUPPORTED_BY_GUI = 3
      OTHERS               = 4.
  IF SY-SUBRC <> 0.
    MESSAGE ID SY-MSGID TYPE CNS_S NUMBER SY-MSGNO
               WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.
  W_NAME = LC_PATH.

ENDFORM.                    " GET_FILENAME
*&---------------------------------------------------------------------*
*&      Form  SAVE_EDIT_DATA
*&---------------------------------------------------------------------*
*       保存数据
*----------------------------------------------------------------------*
FORM SAVE_EDIT_DATA .

  DATA: LC_NUM   TYPE I,
        LC_VBELN TYPE VBELN.

  REFRESH: IT_INSERT,
           IT_ZCSDT5.

  SORT IT_DATA BY VBELN MATNR.
  FLG_STOP = SPACE.
* 锁表
  PERFORM LOCK_ZCSDT005.
  IF FLG_STOP = CNS_X.
    EXIT.
  ENDIF.
* 取最新数据
  PERFORM GET_ZCSDT005.
  LC_NUM = 0.
  LOOP AT IT_DATA INTO WA_DATA.
    IF LC_VBELN <> WA_DATA-VBELN.
      LC_NUM = 0.
    ENDIF.
    LC_NUM = LC_NUM + 1.
    WA_DATA-LINE = LC_NUM.
* 赋值更新
    PERFORM ASSIGN_ZCSDT5_INSERT .
    LC_VBELN = WA_DATA-VBELN.
  ENDLOOP.
* 插入表
  PERFORM UPDATE_ZCSDT5 .
  MESSAGE S052(ZCFI1).

ENDFORM.                    " SAVE_EDIT_DATA

*Text symbol text：
*H00:状态
*H01:状态
*H02:发票类型
*H03:发票号
*H04:发票日期
*H05:客户名称
*H06:商品代码
*H07:商品名
*H08:计量单位
*H09:含税单价
*H10:数量
*H11:含税金额
*H12:货币
*H13:税率
*H14:客户代码
*H15:税码
*H16:取消
*H17:消息
*H18:备注
