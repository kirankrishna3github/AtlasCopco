*&---------------------------------------------------------------------*
*& Report  Z_SAP_SER02_OBJK_CORRECT                                    *
*&                                                                     *
*&---------------------------------------------------------------------*
* This report finds records in table SER02 which have no affiliated    *
* OBJK records.                                                        *
* If ptest is initial the records found are deleted from table SER02   *
* A list of the deleted records is created.                            *
* Where do SER02's without affiliated OBJK's come from?                *
* - They were created when technical objects were deleted from the     *
*   object list of SD documents (orders, contracts, ...) before the    *
*   implementation of notes 359535 and 569269 or with a support        *
*   package level less than SAPKH46B48 or SAPKH46C39 or SAPKH47005.    *
*----------------------------------------------------------------------*
REPORT  z_sap_ser02_objk_correct.

DATA: count_ser02 TYPE i.
DATA: BEGIN OF lt_ser02 OCCURS 0,
        obknr   LIKE ser02-obknr,
        sdaufnr LIKE vbap-vbeln,
        posnr   LIKE vbap-posnr,
      END OF lt_ser02.

FIELD-SYMBOLS: <lt_ser02> LIKE lt_ser02.

PARAMETERS ptest AS CHECKBOX DEFAULT 'X'.
************************************************************************
START-OF-SELECTION.

* get ser02's with no corresponding objk's
  SELECT * FROM ser02 AS s
    INTO CORRESPONDING FIELDS OF TABLE lt_ser02
    WHERE NOT EXISTS
      ( SELECT * FROM objk
          WHERE obknr = s~obknr ).

* list output
  IF lt_ser02[] IS INITIAL.
    WRITE: / 'SER02 and OBJK consistent, ok'.
  ELSE.
    CLEAR count_ser02.
    LOOP AT lt_ser02
            ASSIGNING <lt_ser02>.
      count_ser02 = count_ser02 + 1.
      WRITE: / <lt_ser02>-sdaufnr,
               <lt_ser02>-posnr,
               <lt_ser02>-obknr.
    ENDLOOP.
    WRITE: / 'Number of SER02 entries without affiliated OBJK entries',
              count_ser02.
  ENDIF.

***********************************************************************
* Update
  IF ptest IS INITIAL.
* no mismatches between SER02 and OBJK, for all SER02 entry there is an
* affiliated OBJK entry -> no update
    IF lt_ser02[] IS INITIAL.
      WRITE: / 'No records deleted from SER02'.
* there are SER02 entries for which there is no affiliated OBJK entry
* -> update necessary
    ELSE.
      CLEAR count_ser02.
      WRITE: / 'Records deleted from SER02:'.
      LOOP AT lt_ser02
              ASSIGNING <lt_ser02>.
* Delete SER02 entries
        DELETE FROM ser02
                    WHERE obknr = <lt_ser02>-obknr.
        count_ser02 = count_ser02 + 1.
      ENDLOOP.
      WRITE: / 'Number of SER02 entries without affiliated OBJK entries',
                count_ser02.
    ENDIF.
  ENDIF.
***********************************************************************
***********************************************************************
*&---------------------------------------------------------------------*
*& Report  Z_SAP_SER02_OBJK_CORRECT                                    *
*&                                                                     *
*&---------------------------------------------------------------------*
*&                                                                     *
*&                                                                     *

*&---------------------------------------------------------------------*
*Selection text£º
*PTEST:        TEST?
