*&---------------------------------------------------------------------*
*& Report  Z_CORRECT_DATE_VALUES
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*& Report  Z_CORRECT_DATE_VALUES
*& version 4.3.2011
*&---------------------------------------------------------------------*
*& This report contains correction for Equipments date values
*& Correction is relevant for EQUI fields : ansdt, auldt, inbdt
*& Report corrects only those values which are SPACE and replaces it
*& with initial date values
*&
*& This report solves issue with SPACE data values which are newly
*& checked after notes 1432502, 1530245
*&---------------------------------------------------------------------*
*& W A R N I N G:

*& - Please reduce the amount of equipments to test/correct by using
*&   the parameter s_equi. This would reduce the runtime of the
*&   report.
*& - Please start this report in the test-mode first
*&   (p_test is marked), to make sure, that the correction, the
*&   report suggested is correct.
*& - If start the report make sure, that noboy is on work with the
*&   system due to equipments, because the report doesn't include
*&   any enque/deque-techniques!
*&---------------------------------------------------------------------*

REPORT  Z_CORRECT_DATE_VALUES.

*    definition of tables
TABLES: equi.
CONSTANTS: const_x TYPE c VALUE 'X'.
CONSTANTS: lc_initial_date LIKE sy-datum VALUE '00000000'.

* selection screen
SELECT-OPTIONS: s_equi FOR equi-equnr.
PARAMETERS p_test TYPE c DEFAULT 'X'.

DATA: lt_equi TYPE TABLE OF equi,
      lt_equi_final TYPE TABLE OF equi,
      ls_equi TYPE equi.

CLEAR lt_equi.
CLEAR lt_equi_final.

* equipments select
SELECT * FROM equi INTO TABLE lt_equi WHERE equnr IN s_equi and
      ( ansdt = '' OR auldt = '' OR inbdt = '' ).

IF sy-subrc = 0.

  LOOP AT lt_equi INTO ls_equi.
      IF p_test = space.
*       Acquisition date
        IF ls_equi-ansdt = ''.
          ls_equi-ansdt = lc_initial_date.
        ENDIF.

*       First delivery date
        IF ls_equi-auldt = ''.
          ls_equi-auldt = lc_initial_date.
        ENDIF.

*       Start-up date
        IF ls_equi-inbdt = ''.
          ls_equi-inbdt = lc_initial_date.
        ENDIF.

        INSERT ls_equi INTO TABLE lt_equi_final.

      ELSE.
*       Acquisition date
        IF ls_equi-ansdt = ''.
          WRITE: / 'Equipment ', ls_equi-equnr,
           ' has Acquisition date SPACE - needs to be corrected.'.
        ENDIF.

*       First delivery date
        IF ls_equi-auldt = ''.
          WRITE: / 'Equipment ', ls_equi-equnr,
           ' has First delivery date SPACE - needs to be corrected.'.
        ENDIF.

*       Start-up date
        IF ls_equi-inbdt = ''.
          WRITE: / 'Equipment ', ls_equi-equnr,
          ' has Start-up Date SPACE - needs to be corrected.'.
        ENDIF.

      ENDIF.
  ENDLOOP.

* DB update if not TEST mode
  IF p_test = space.

      UPDATE equi FROM TABLE lt_equi_final.

      IF sy-subrc is initial.
        COMMIT WORK.
        LOOP AT lt_equi_final INTO ls_equi.
          WRITE: / 'Equipment ', ls_equi-equnr,
                    ' date values modified.'.
        ENDLOOP.
      ELSE.
        ROLLBACK WORK.
          WRITE: / 'Equipments update was NOT successful, run report again'.
      ENDIF.

  ENDIF.

ELSE.
  WRITE: / 'No Equipments with date values SPACE exist - no correction needed'.

ENDIF.

*Selection text£º
*P_TEST:        Test
*S_EQUI:D       .
