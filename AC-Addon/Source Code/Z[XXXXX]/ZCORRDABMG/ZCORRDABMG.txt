REPORT zcorrdabmg.

TABLES: ekko, ekpo, eket, ekes.

DATA: t_ekpo LIKE ekpo OCCURS 0 WITH HEADER LINE.
DATA:    BEGIN OF ett OCCURS 50.
        INCLUDE STRUCTURE beket.
DATA:    END OF ett.
DATA: BEGIN OF yeket OCCURS 20.
        INCLUDE STRUCTURE ueket                         .
DATA: END OF yeket                          .
DATA: BEGIN OF t_eket OCCURS 20.
        INCLUDE STRUCTURE eket                         .
DATA: END OF t_eket                          .

DATA: ekesok.              "filled from Import-parameter E_EKESOK
DATA: h_funkt(2).          "function parameter for confirmation program

DATA:  BEGIN OF l_ekpodata OCCURS 0,   "Schedul. Agreements
          ebeln LIKE ekpo-ebeln,
          ebelp LIKE ekpo-ebelp,
       END OF l_ekpodata.

DATA:  BEGIN OF f_ekpodata OCCURS 0,   "Purchase Orders
          ebeln LIKE ekpo-ebeln,
          ebelp LIKE ekpo-ebelp,
       END OF f_ekpodata.

* select-options: s_ebeln  for ekes-ebeln.
SELECT-OPTIONS: po_nr FOR ekpo-ebeln.
SELECT-OPTIONS: po_item FOR ekpo-ebelp.
PARAMETERS onlylst AS CHECKBOX DEFAULT 'X'. "Only list, no batch Input
PARAMETERS:
      group1(12) DEFAULT 'znv_sched.agr.' OBLIGATORY,
      group2(12) DEFAULT 'znv_POs' OBLIGATORY,
      user(12)     OBLIGATORY,         "user for start session in batch
      keep AS CHECKBOX,                "' ' = delete session if finished
                                       "'X' = keep   session if finished
      holddate LIKE sy-datum.

*     Batchinputdata of single transaction
DATA: BEGIN OF bdcdata OCCURS 0.
        INCLUDE STRUCTURE bdcdata.
DATA: END OF bdcdata.

*----------------------------------------------------------------------*
*   create batchinput session                                          *
*----------------------------------------------------------------------*
FORM open_group USING group.
  SKIP.
  WRITE: /(20) 'Create group'(i01), group.
  SKIP.
* open batchinput group.
  CALL FUNCTION 'BDC_OPEN_GROUP'
    EXPORTING
      client   = sy-mandt
      group    = group
      user     = user
      keep     = keep
      holddate = holddate.
  WRITE: /(30) 'BDC_OPEN_GROUP'(i02),
          (12) 'returncode:'(i05),
               sy-subrc.
ENDFORM.                    "OPEN_GROUP

*----------------------------------------------------------------------*
*   end batchinput session                                             *
*----------------------------------------------------------------------*
FORM close_group.
* close batchinput group
  CALL FUNCTION 'BDC_CLOSE_GROUP'.
  WRITE: /(30) 'BDC_CLOSE_GROUP'(i04),
          (12) 'returncode:'(i05),
               sy-subrc.
ENDFORM.                    "CLOSE_GROUP

*----------------------------------------------------------------------*
*        start new transaction                                         *
*----------------------------------------------------------------------*
FORM bdc_transaction USING tcode.
  CALL FUNCTION 'BDC_INSERT'
    EXPORTING
      tcode     = tcode
    TABLES
      dynprotab = bdcdata.
  WRITE: /(25) 'BDC_INSERT'(i03),
               tcode,
          (12) 'returncode:'(i05),
               sy-subrc.
ENDFORM.                    "BDC_TRANSACTION

*----------------------------------------------------------------------*
*        Start new screen                                              *
*----------------------------------------------------------------------*
FORM bdc_dynpro USING program dynpro.
  CLEAR bdcdata.
  bdcdata-program  = program.
  bdcdata-dynpro   = dynpro.
  bdcdata-dynbegin = 'X'.
  APPEND bdcdata.
ENDFORM.                    "BDC_DYNPRO

*----------------------------------------------------------------------*
*        Insert field                                                  *
*----------------------------------------------------------------------*
FORM bdc_field USING fnam fval.
  CLEAR bdcdata.
  bdcdata-fnam = fnam.
  bdcdata-fval = fval.
  APPEND bdcdata.
ENDFORM.                    "BDC_FIELD

*---------------------------------------------------------------------*
*       FORM ENTER_DOCUMENT_L                                         *
*---------------------------------------------------------------------*
FORM enter_document_l USING ebeln LIKE ekpo-ebeln.   "sched. agr.

  DATA: fval LIKE bdcdata-fval.

  fval = ebeln.

  PERFORM bdc_dynpro      USING 'SAPMM06E' '0205'.
  PERFORM bdc_field       USING 'BDC_OKCODE'
                                '/00'.
  PERFORM bdc_field       USING 'BDC_CURSOR'
                                'RM06E-EVRTN'.
  PERFORM bdc_field       USING 'RM06E-EVRTN'
                                fval.
ENDFORM.                    "ENTER_DOCUMENT_L
*---------------------------------------------------------------------*
*       FORM SELECT_ITEM_L                                            *
*---------------------------------------------------------------------*
FORM select_item_l    USING item LIKE ekpo-ebelp.

  DATA: fval LIKE bdcdata-fval.

  fval = item.

  PERFORM bdc_dynpro      USING 'SAPMM06E' '0222'.
  PERFORM bdc_field       USING 'BDC_OKCODE'
                                '/00'.
  PERFORM bdc_field       USING 'BDC_CURSOR'
                                'RM06E-EBELP'.
  PERFORM bdc_field       USING 'RM06E-EBELP'
                                fval.
  PERFORM bdc_field       USING 'RM06E-TCSELFLAG(01)'
                                ''.    "'X'.
  PERFORM bdc_dynpro      USING 'SAPMM06E' '0222'.
  PERFORM bdc_field       USING 'BDC_OKCODE'
                                '=BSTA'.
  PERFORM bdc_field       USING 'BDC_CURSOR'
                                'RM06E-EVRTP(01)'.
  PERFORM bdc_field       USING 'RM06E-TCSELFLAG(01)'
                                 'X'.
ENDFORM.                    "SELECT_ITEM_L

*---------------------------------------------------------------------*
*     FORM REDISTRIBUTION                                             *
*---------------------------------------------------------------------*
FORM redistribution.

  PERFORM bdc_dynpro      USING 'SAPLEINB' '0200'.
  PERFORM bdc_field       USING 'BDC_OKCODE'
                                '=NEUV'.
  PERFORM bdc_field       USING 'BDC_CURSOR'
                                'RM06E-LPEIN(01)'.
  PERFORM bdc_dynpro      USING 'SAPLEINB' '0200'.
  PERFORM bdc_field       USING 'BDC_OKCODE'
                                '=BACK'.
  PERFORM bdc_field       USING 'BDC_CURSOR'
                                'RM06E-LPEIN(01)'.

ENDFORM.                    "REDISTRIBUTION

*----------------------------------------------------------------------*
*   form save_document_l                                               *
*----------------------------------------------------------------------*
FORM save_document_l.

  PERFORM bdc_dynpro      USING 'SAPMM06E' '0222'.
  PERFORM bdc_field       USING 'BDC_OKCODE'
                                '=BU'.
  PERFORM bdc_field       USING 'BDC_CURSOR'
                                 'RM06E-TCSELFLAG(01)'.

ENDFORM.                    "SAVE_DOCUMENT_L

*---------------------------------------------------------------------*
*       FORM DO_TRANSACTION_ME38                                      *
*---------------------------------------------------------------------*
FORM do_transaction_me38.

  PERFORM bdc_transaction USING 'ME38'.

ENDFORM.                    "DO_TRANSACTION_ME38

*---------------------------------------------------------------------*
*       FORM REPAIR_L                                                 *
*---------------------------------------------------------------------*
FORM repair_l USING docnr LIKE ekpo-ebeln.

  LOOP AT l_ekpodata WHERE ebeln = docnr.

    REFRESH bdcdata.

    PERFORM enter_document_l USING docnr.

    SKIP.

    WRITE: /(32) 'REPAIRING',
            (10) 'document:', 44 docnr .
    WRITE: /38   'item:', 44  l_ekpodata-ebelp.

    PERFORM select_item_l           USING l_ekpodata-ebelp.
    PERFORM redistribution.
    PERFORM save_document_l.
    PERFORM do_transaction_me38.

  ENDLOOP.

  SKIP.

ENDFORM.                    "REPAIR_L

*---------------------------------------------------------------------*
*       FORM ENTER_DOCUMENT_F                                         *
*---------------------------------------------------------------------*
FORM enter_document_f USING ebeln LIKE ekpo-ebeln.          "POs

  DATA: fval LIKE bdcdata-fval.

  fval = ebeln.

  PERFORM bdc_dynpro      USING 'SAPMM06E' '0105'.
  PERFORM bdc_field       USING 'BDC_OKCODE'
                                '/00'.
  PERFORM bdc_field       USING 'BDC_CURSOR'
                                'RM06E-BSTNR'.
  PERFORM bdc_field       USING 'RM06E-BSTNR'
                                fval.
ENDFORM.                    "ENTER_DOCUMENT_F
*---------------------------------------------------------------------*
*       FORM SELECT_ITEM_F                                            *
*---------------------------------------------------------------------*
FORM select_item_f    USING item LIKE ekpo-ebelp.

  DATA: fval LIKE bdcdata-fval.

  fval = item.

  PERFORM bdc_dynpro      USING 'SAPMM06E' '0120'.
  PERFORM bdc_field       USING 'BDC_OKCODE'
                                '/00'.
  PERFORM bdc_field       USING 'BDC_CURSOR'
                                'RM06E-EBELP'.
  PERFORM bdc_field       USING 'RM06E-EBELP'
                                fval.
  PERFORM bdc_field       USING 'RM06E-TCSELFLAG(01)'
                                ''.    "'X'.
  PERFORM bdc_dynpro      USING 'SAPMM06E' '0120'.
  PERFORM bdc_field       USING 'BDC_OKCODE'
                                '=BSTA'.
  PERFORM bdc_field       USING 'BDC_CURSOR'
                                'RM06E-BSTPO(01)'.
  PERFORM bdc_field       USING 'RM06E-TCSELFLAG(01)'
                                 'X'.
ENDFORM.                    "SELECT_ITEM_F

*----------------------------------------------------------------------*
*   form save_document_f                                               *
*----------------------------------------------------------------------*
FORM save_document_f.

  PERFORM bdc_dynpro      USING 'SAPMM06E' '0120'.
  PERFORM bdc_field       USING 'BDC_OKCODE'
                                '=BU'.
  PERFORM bdc_field       USING 'BDC_CURSOR'
                                 'RM06E-BSTPO(01)'.

ENDFORM.                    "SAVE_DOCUMENT_F

*---------------------------------------------------------------------*
*       FORM DO_TRANSACTION_ME22                                      *
*---------------------------------------------------------------------*
FORM do_transaction_me22.

  PERFORM bdc_transaction USING 'ME22'.

ENDFORM.                    "DO_TRANSACTION_ME22

*---------------------------------------------------------------------*
*       FORM REPAIR_F                                                 *
*---------------------------------------------------------------------*
FORM repair_f USING docnr LIKE ekpo-ebeln.

  LOOP AT f_ekpodata WHERE ebeln = docnr.

    REFRESH bdcdata.

    PERFORM enter_document_f USING docnr.

    SKIP.

    WRITE: /(32) 'REPAIRING',
            (10) 'document:', 44 docnr .
    WRITE: /38   'item:', 44 f_ekpodata-ebelp.

    PERFORM select_item_f   USING f_ekpodata-ebelp.
    PERFORM redistribution.
    PERFORM save_document_f.
    PERFORM do_transaction_me22.

  ENDLOOP.

  SKIP.

ENDFORM.                    "REPAIR_F




*----------- START-OF-SELECTION --------------------------------------*
START-OF-SELECTION.

  h_funkt = 'P1'.
  REFRESH: f_ekpodata, l_ekpodata.
  REFRESH t_ekpo.
  SELECT * FROM ekpo INTO TABLE t_ekpo WHERE
                          ebeln IN po_nr   AND
                          ebelp IN po_item AND
                          bstae NE space   AND
                          elikz EQ space   AND
                          loekz EQ space.
  LOOP AT t_ekpo.
    MOVE t_ekpo TO ekpo.
*check: confirmaton records exists !
    SELECT SINGLE * FROM ekes WHERE ebeln EQ ekpo-ebeln
                              AND   ebelp EQ ekpo-ebelp.
    IF sy-subrc = 0.
* check: candidate for redistribution
      REFRESH ett.
      REFRESH t_eket.
      SELECT * FROM eket INTO TABLE t_eket
                       WHERE ebeln = ekpo-ebeln
                        AND  ebelp = ekpo-ebelp.
      LOOP AT t_eket.
        MOVE-CORRESPONDING t_eket TO ett.
        APPEND ett.
      ENDLOOP.
      CLEAR sy-msgid.
      CLEAR sy-msgno.
      CALL FUNCTION 'ME_CONFIRMATIONS_REFRESH'.
      CALL FUNCTION 'ME_CONFIRMATION_MAINTAIN'
        EXPORTING
          i_bstae  = ekpo-bstae
          i_ebeln  = ekpo-ebeln
          i_ebelp  = ekpo-ebelp
          i_funkt  = h_funkt
          i_werks  = ekpo-werks
        IMPORTING
          e_ekesok = ekesok
        TABLES
          xeket    = ett
          yeket    = yeket.
* record for redistribution is found
      IF ekesok  = 'Z'.
        CASE ekpo-bstyp.
          WHEN 'F'.
            f_ekpodata-ebeln = ekpo-ebeln.
            f_ekpodata-ebelp = ekpo-ebelp.
            APPEND f_ekpodata.
          WHEN 'L'.
            l_ekpodata-ebeln = ekpo-ebeln.
            l_ekpodata-ebelp = ekpo-ebelp.
            APPEND l_ekpodata.
          WHEN OTHERS.
            WRITE: / 'Wrong BSTYP',
                   ekpo-bstyp, ekpo-ebeln, ekpo-ebelp.
        ENDCASE.
      ENDIF.
    ENDIF.
  ENDLOOP.
  DATA h_counter LIKE sy-tabix.

  DESCRIBE TABLE l_ekpodata LINES h_counter.
  WRITE: / h_counter ,'Schedule Agreement Items found'.
  LOOP AT l_ekpodata.
    WRITE: / l_ekpodata-ebeln, l_ekpodata-ebelp.
  ENDLOOP.
  DESCRIBE TABLE f_ekpodata LINES h_counter.
  WRITE: / h_counter ,'Purchase Order Items found'.
  LOOP AT f_ekpodata.
    WRITE: / f_ekpodata-ebeln, f_ekpodata-ebelp.
  ENDLOOP.
  CHECK onlylst IS INITIAL.


  CLEAR l_ekpodata.
  SORT l_ekpodata.
  PERFORM open_group USING group1.
  LOOP AT l_ekpodata.
    AT NEW ebeln.
      PERFORM repair_l USING l_ekpodata-ebeln.
    ENDAT.
  ENDLOOP.
  PERFORM close_group.

  CLEAR f_ekpodata.
  SORT f_ekpodata.
  PERFORM open_group USING group2.
  LOOP AT f_ekpodata.
    AT NEW ebeln.
      PERFORM repair_f USING f_ekpodata-ebeln.
    ENDAT.
  ENDLOOP.
  PERFORM close_group.

*Selection text£º
*ONLYLST:        Only list, no BDC
