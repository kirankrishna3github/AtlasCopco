*&---------------------------------------------------------------------*
*& Report  Z312788                                                     *
*&                                                                     *
*&---------------------------------------------------------------------*
*&                                                                     *
*&                                                                     *
*&---------------------------------------------------------------------*

REPORT Z312788 MESSAGE-ID VZ.

parameters: test default 'X' as checkbox,
            tbprefix(4) type c default space.

types: begin of dpc,
         objnr like ad01dli-objnr,
         dlinr1 like ad01dli-dlinr,
         dlinr2 like ad01dli-dlinr,
         fl_dlief2 type c,
         fl_dlisf2 type c,
         fl_vpkdl2 type c,
       end of dpc.

constants c_tbl_vpkdli like ad01srctab-tabname
          value 'VPKDLI'.

data g_svtbl_ad01dli like ad01srctab-tabname.
data g_svtbl_ad01dlief like ad01srctab-tabname.
data g_svtbl_ad01dlisf like ad01srctab-tabname.

data: begin of t_objnr,
        objnr like ad01dli-objnr,
      end of t_objnr.
data lt_objnr like table of t_objnr.
data l_low type i value 1.
data l_high type i value 100.
data lt_dli type table of ad01dli.
data wt_objnr like table of t_objnr.
data l_tabix_v like sy-tabix.
data l_attri type ad01attri.
data l_v_attri type ad01attri.
data lt_dpc type standard table of dpc.
data l_dpc like line of lt_dpc.
data l_counter type i.
data lt_ad01dlief type table of ad01dlief.
data lt_ad01dlisf type table of ad01dlisf.
data l_ad01dli type ad01dli.
data lt_ad01dlief_merge type table of ad01dlief.
data lt_ad01dlisf_merge type table of ad01dlisf.
data lt_ad01dli_merge type table of ad01dli.
data l_old_dlinr like ad01dli-dlinr.
data l_comm_counter type p.
data l_dlinr_count like ad01dli-dlinr value is initial.
data l_write_objnr type boole_d.
data l_write_dlinr type boole_d.
field-symbols <dli> like line of lt_dli.
field-symbols <v_dli> like line of lt_dli.

initialization.
  test = 'X'.

start-of-selection.
  if not tbprefix is initial.
    concatenate tbprefix 'AD01DLI'
                into g_svtbl_ad01dli.
    concatenate tbprefix 'AD01DLIEF'
                into g_svtbl_ad01dlief.
    concatenate tbprefix 'AD01DLISF'
                into g_svtbl_ad01dlisf.
  endif.
* Read all objects in AD01DLI
  select distinct objnr
         from ad01dli
         into table lt_objnr.
  do.
* Process 100 records in each loop
    refresh lt_dli.
    refresh wt_objnr.
    append lines of lt_objnr
           from l_low to l_high
           to wt_objnr.
    if wt_objnr[] is initial.
      exit.
    endif.
    delete lt_objnr
           from l_low to l_high.
* Read dynamic items for objects
    select * from ad01dli
           into table lt_dli
           for all entries in wt_objnr
           where objnr = wt_objnr-objnr.
* Compare dynamic items
    sort lt_dli by objnr dlisrc dlinr.
    loop at lt_dli assigning <dli>.
      move-corresponding <dli> to l_attri.
      clear l_dpc.
      loop at lt_dli assigning <v_dli>
              from 2.
        l_tabix_v = sy-tabix.
* Next object or source
        if <dli>-objnr <> <v_dli>-objnr
           or <dli>-dlisrc <> <v_dli>-dlisrc.
          exit.
        endif.
        move-corresponding <v_dli> to l_v_attri.
        if <dli>-dlisrc = <v_dli>-dlisrc
           and l_attri = l_v_attri.
* Dynamic items characteristics are equal
          if l_dpc-dlinr1 is initial.
* Insert DLI to replacement table
            add 1 to l_dlinr_count.
            l_dpc-dlinr1 = l_dlinr_count.   " temp. new dlinr
            l_dpc-dlinr2 = <dli>-dlinr.
            l_dpc-objnr = <dli>-objnr.
* Check existency of dependent records for DLI 1
            select count(*) from ad01dlief
                   where dlinr = l_dpc-dlinr1.
            if sy-subrc = 4.
              l_dpc-fl_dlief2 = '0'.
            endif.
            select count(*) from ad01dlisf
                   where dlinr = l_dpc-dlinr1.
            if sy-subrc = 4.
              l_dpc-fl_dlisf2 = '0'.
            endif.
            if     sy-saprl <> '45B'
               and sy-saprl <> '45A'.
              select count(*)
                     from (c_tbl_vpkdli)
                     where dlinr = l_dpc-dlinr1.
              if sy-subrc = 4.
                l_dpc-fl_vpkdl2 = '0'.
              endif.
            else.
              l_dpc-fl_vpkdl2 = '0'.
            endif.
            collect l_dpc into lt_dpc.
          endif.
* Add DLI2 to replacement table
          l_dpc-dlinr2 = <v_dli>-dlinr.
* Check existency of dependent records for DLI 2
          select count(*) from ad01dlief
                        where dlinr = l_dpc-dlinr2.
          if sy-subrc = 4.
            l_dpc-fl_dlief2 = '0'.
          endif.
          select count(*) from ad01dlisf
                 where dlinr = l_dpc-dlinr2.
          if sy-subrc = 4.
            l_dpc-fl_dlisf2 = '0'.
          endif.
          if     sy-saprl <> '45B'
             and sy-saprl <> '45A'.
            select count(*)
                   from (c_tbl_vpkdli)
                   where dlinr = l_dpc-dlinr2.
            if sy-subrc = 4.
              l_dpc-fl_vpkdl2 = '0'.
            endif.
          else.
            l_dpc-fl_vpkdl2 = '0'.
          endif.
* Add entry to log table
          collect l_dpc into lt_dpc.
          delete lt_dli index l_tabix_v.
        endif.
      endloop.                         "lt_dli
      delete lt_dli index 1.
    endloop.                           "lt_dli
  enddo.

*---------------------------------*
* List output and data correction *
*---------------------------------*

* List output
  describe table lt_dpc lines l_counter.
  write:  'Number of double dynamic items', l_counter.
  skip 1.
  l_old_dlinr = 1.
  loop at lt_dpc into l_dpc.
    at new objnr.
      l_write_objnr = 'X'.
    endat.
    at new dlinr1.
      l_write_dlinr = 'X'.
    endat.
    if test is initial.
      if l_dpc-dlinr1 <> l_old_dlinr.
*     Merge dynamic items
        perform merge
                tables lt_ad01dlief_merge
                       lt_ad01dlisf_merge
                       lt_ad01dli_merge.
        l_old_dlinr = l_dpc-dlinr1.
        refresh lt_ad01dlief_merge.
        refresh lt_ad01dlisf_merge.
        refresh lt_ad01dli_merge.
        add 1 to l_comm_counter.
        if l_comm_counter >= 10.
          commit work.
          write: /10 '-> COMMIT WORK'.
          clear l_comm_counter.
        endif.
      endif.
    endif.
    if l_write_objnr = 'X'.
      write: / l_dpc-objnr.
      clear l_write_objnr.
    endif.
    if l_write_dlinr = 'X'.
      write: /10 l_dpc-dlinr1.
      clear l_write_dlinr.
    endif.
    write: / l_dpc-dlinr2 under l_dpc-dlinr1,
           (2) l_dpc-fl_dlief2, (2) l_dpc-fl_dlisf2,
           (2) l_dpc-fl_vpkdl2.
    if test is initial.
* Delete dynamic item
      perform delete
              tables   lt_ad01dlief
                       lt_ad01dlisf
              using    l_dpc-dlinr2
              changing l_ad01dli.
      append lines of lt_ad01dlief to lt_ad01dlief_merge.
      append lines of lt_ad01dlisf to lt_ad01dlisf_merge.
      append l_ad01dli to lt_ad01dli_merge.
    endif.
  endloop.
  if test is initial.
    perform merge
            tables lt_ad01dlief_merge
                   lt_ad01dlisf_merge
                   lt_ad01dli_merge.
    commit work.
    write: /10 '-> COMMIT WORK'.
  endif.
* Page heading
top-of-page.
  if test = ' '.
    write: 'Productive run;'.
    write: 'Delete double dynamic items; RRB flow merged'.
  else.
    write: 'Test without update of databse'.
  endif.
  skip 1.

*---------------------------------------------------------------------*
*       FORM merge                                                    *
*---------------------------------------------------------------------*
form merge
     tables it_ad01dlief structure ad01dlief
            it_ad01dlisf structure ad01dlisf
            it_ad01dli structure ad01dli.
  data l_count type i.
  data l_dlinr_new like ad01dli-dlinr.
  data lt_ad01dlief_new type table of ad01dlief.
  data lt_ad01dlisf_new type table of ad01dlisf.
  data l_ad01dli_new type ad01dli.
  data l_ad01dlief_new type ad01dlief.
  data l_ad01dlisf_new type ad01dlisf.
* Check for flow
  describe table it_ad01dlisf lines l_count.
  if l_count = 0.
    describe table it_ad01dlief lines l_count.
    if l_count <> 0.
      message x000 with 'INCONSISTENT DATA'.
    endif.
*   Nothing to do
    exit.
  endif.
  sort it_ad01dli by dlinr.
  sort it_ad01dlisf by dlius dlinr.
  sort it_ad01dlief by dlius dlinr vbeln vbpos.
* Get the next DLi number
  PERFORM GET_NEXT_DLINR
          changing l_dlinr_new.
* Update DLINR
  read table it_ad01dli
       into l_ad01dli_new
       index 1.
  if sy-subrc ne 0.
    message x000 with 'INCONSISTENT DATA'.
  endif.
  l_ad01dli_new-dlinr = l_dlinr_new.
  loop at it_ad01dlisf into l_ad01dlisf_new.
    l_ad01dlisf_new-dlinr = l_dlinr_new.
    collect l_ad01dlisf_new into lt_ad01dlisf_new.
  endloop.
  loop at it_ad01dlief into l_ad01dlief_new.
    l_ad01dlief_new-dlinr = l_dlinr_new.
    collect l_ad01dlief_new into lt_ad01dlief_new.
  endloop.
  insert ad01dli from l_ad01dli_new.
  if sy-subrc ne 0.
    message x000 with 'Error insert AD01DLI; DLI' l_dlinr_new.
  endif.
  insert ad01dlief from table lt_ad01dlief_new.
  if sy-subrc ne 0.
    message x000 with 'Error insert AD01DLIEF; DLI' l_dlinr_new.
  endif.
  insert ad01dlisf from table lt_ad01dlisf_new.
  if sy-subrc ne 0.
    message x000 with 'Error insert AD01DLISF; DLI' l_dlinr_new.
  endif.
  write: /10 l_dlinr_new, ' inserted.'.
endform.                               "MERGE

*---------------------------------------------------------------------*
*       FORM delete                                                   *
*---------------------------------------------------------------------*
form delete
     tables   et_ad01dlief structure ad01dlief
              et_ad01dlisf structure ad01dlisf
     using    p_dlinr like ad01dli-dlinr
     changing e_ad01dli like ad01dli.
* Delete table AD01DLI
  select single *
         from ad01dli
         into e_ad01dli
         where dlinr = p_dlinr.
  if sy-subrc ne 0.
    message x000 with 'DLI' p_dlinr 'not found in AD01DLI'.
  endif.
  if not tbprefix is initial.
    insert (g_svtbl_ad01dli) from e_ad01dli.
    if sy-subrc ne 0.
      message x000 with 'Error insert ' g_svtbl_ad01dli
                        ', DLI:' p_dlinr.
    endif.
  endif.
  delete from ad01dli
         where dlinr = p_dlinr.
  if sy-subrc ne 0.
    message x000 with 'Error delete AD01DLI; DLI' p_dlinr.
  endif.
* Delete table AD01DLIEF
  select * from ad01dlief
         into table et_ad01dlief
         where dlinr = p_dlinr.
  if sy-subrc = 0.
    if not tbprefix is initial.
      insert (g_svtbl_ad01dlief) from table et_ad01dlief.
      if sy-subrc ne 0.
        message x000 with 'Error insert ' g_svtbl_ad01dlief
                          ', DLI:' p_dlinr.
      endif.
    endif.
    delete from ad01dlief
           where dlinr = p_dlinr.
    if sy-subrc ne 0.
      message x000 with 'Error delete AD01DLIEF; DLI' p_dlinr.
    endif.
  endif.
* Delete table AD01DLISF
  select * from ad01dlisf
         into table et_ad01dlisf
         where dlinr = p_dlinr.
  if sy-subrc = 0.
    if not tbprefix is initial.
      insert (g_svtbl_ad01dlisf) from table et_ad01dlisf.
      if sy-subrc ne 0.
        message x000 with 'Error insert ' g_svtbl_ad01dlisf
                          ', DLI:' p_dlinr.
      endif.
    endif.
    delete from ad01dlisf
           where dlinr = p_dlinr.
    if sy-subrc ne 0.
      message x000 with 'Error delete AD01DLISF; DLI' p_dlinr.
    endif.
  endif.
* Delete table AD01DLISF
  if     sy-saprl <> '45B'
     and sy-saprl <> '45A'.
    select count(*) from (c_tbl_vpkdli)
           where dlinr = p_dlinr.
    if sy-subrc = 0.
      delete from (c_tbl_vpkdli)
             where dlinr = p_dlinr.
      if sy-subrc ne 0.
        message x000 with 'Error delete ' c_tbl_vpkdli '; DLI' p_dlinr.
      endif.
    endif.
  endif.
* Finished
  write: /10 p_dlinr, ' deleted'.
endform.                               "DELETE

*---------------------------------------------------------------------*
*       FORM get_next_dlinr                                           *
*---------------------------------------------------------------------*
form get_next_dlinr
          changing e_dlinr type ad01dlinr.
constants: nr_object like nriv-object value 'AD01DLINR',
           nr_rangenr like nriv-nrrangenr value '01'.
* Get a number
  call function 'NUMBER_GET_NEXT'
       exporting
            nr_range_nr             = nr_rangenr
            object                  = nr_object
       importing
            number                  = e_dlinr
       exceptions
            interval_not_found      = 1
            number_range_not_intern = 2
            object_not_found        = 3
            quantity_is_0           = 4
            quantity_is_not_1       = 5
            interval_overflow       = 6
            others                  = 7.
  IF SY-SUBRC <> 0.
     MESSAGE A000(VZ) WITH 'NUMER RANGE_ERROR'.
  ENDIF.
endform.                               " GET_NEXT_DLINR

*Selection text£º
*TEST:        TEST
