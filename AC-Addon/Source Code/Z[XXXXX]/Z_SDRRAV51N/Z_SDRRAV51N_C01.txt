*----------------------------------------------------------------------*
*  Include           SDRRAV51N_C01
*----------------------------------------------------------------------*
*----------------------------------------------------------------------*
*       CLASS lcl_event_receiver_top DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
class lcl_event_receiver_top definition.
  public section.
    methods:
      handle_toolbar for event toolbar of cl_gui_alv_grid
                     importing e_object e_interactive,

      handle_user_command for event user_command of cl_gui_alv_grid
                     importing e_ucomm,

      on_double_click for event double_click of cl_gui_alv_grid
                     importing e_row e_column,

     on_hotspot_click   for event hotspot_click of cl_gui_alv_grid
                     importing e_row_id e_column_id es_row_no.

endclass.                              "lcl_event_receiver DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcl_event_receiver_buttom DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
class lcl_event_receiver_buttom definition.
  public section.
    methods:

     on_hotspot_click   for event hotspot_click of cl_gui_alv_grid
                     importing e_row_id e_column_id es_row_no.

endclass.                    "lcl_event_receiver DEFINITION

*---------------------------------------------------------------------*
*       CLASS lcl_event_receiver_top IMPLEMENTATION
*---------------------------------------------------------------------*
*
*---------------------------------------------------------------------*
class lcl_event_receiver_top implementation.

*---------------------------------------------------------------------*
*      METHOD HANDLE_TOOLBAR
*---------------------------------------------------------------------*
  method handle_toolbar.

    data: lvs_toolbar  type stb_button.

*   append a separator to normal toolbar
    clear lvs_toolbar.
    move 3                 to lvs_toolbar-butn_type.
    append lvs_toolbar     to e_object->mt_toolbar.
*   append SHOW_ITEMS
    clear lvs_toolbar.
    move gcf_ucomm_show_it to lvs_toolbar-function.
    move icon_assign       to lvs_toolbar-icon.
    move text-003          to lvs_toolbar-quickinfo.
    move ' '               to lvs_toolbar-disabled.
    append lvs_toolbar     to e_object->mt_toolbar.
*   append a separator to normal toolbar
    clear lvs_toolbar.
    move 3                 to lvs_toolbar-butn_type.
    append lvs_toolbar     to e_object->mt_toolbar.
*   append SHOW_FI
    clear lvs_toolbar.
    move gcf_ucomm_show_fi to lvs_toolbar-function.
    move icon_financing    to lvs_toolbar-icon.
    move text-004          to lvs_toolbar-quickinfo.
    move text-005          to lvs_toolbar-text.
    move ' '               to lvs_toolbar-disabled.
    append lvs_toolbar     to e_object->mt_toolbar.
*   append SHOW_FI_MULTI
    clear lvs_toolbar.
    move gcf_ucomm_show_itmu to lvs_toolbar-function.
    move icon_display        to lvs_toolbar-icon.
    move text-007            to lvs_toolbar-quickinfo.
    move text-007            to lvs_toolbar-text.
    move ' '                 to lvs_toolbar-disabled.
    append lvs_toolbar       to e_object->mt_toolbar.
  endmethod.                    "handle_toolbar

*----------------------------------------------------------------------*
*     METHDOD HANDLE_USER_COMMAND
*----------------------------------------------------------------------*
  method handle_user_command.

    data: lvs_row   type lvc_s_row,
          lvt_row   type lvc_t_row,
          lvf_lines type i.

    case e_ucomm.

      when gcf_ucomm_show_itmu.

        call method go_alv_grid_top->get_selected_rows
          importing
            et_index_rows = lvt_row.

        perform show_items_multi tables lvt_row.

      when gcf_ucomm_show_it.

        call method go_alv_grid_top->get_selected_rows
          importing
            et_index_rows = lvt_row.

        describe table lvt_row lines lvf_lines.
        if lvf_lines <> 1.
          message s019.
          exit.
        endif.

        read table lvt_row into lvs_row index 1.

        perform show_items using lvs_row-index.

      when gcf_ucomm_show_fi.

        perform show_fi_documents.

    endcase.

  endmethod.                    "handle_user_command IMPLEMENTATION

*----------------------------------------------------------------------*
*       METHOD ON_DOUBLE_CLICK
*----------------------------------------------------------------------*
  method on_double_click.

    perform show_items using e_row-index.

  endmethod.                    "on_double_click

*----------------------------------------------------------------------*
*       METHOD ON_HOTSPOT_CLICK
*----------------------------------------------------------------------*
  method on_hotspot_click.

    data: lvs_header like line of gvt_header.

    read table gvt_header index e_row_id-index into lvs_header.

    case e_column_id-fieldname.
      when gcf_vbeln_v.
        call function 'RV_CALL_DISPLAY_TRANSACTION'
          exporting
            posnr = lvs_header-posnr_v
            vbeln = lvs_header-vbeln_v.

    endcase.
  endmethod.                    "on_hotspot_click
endclass.                    "lcl_event_receiver IMPLEMENTATION

*----------------------------------------------------------------------*
*       CLASS lcl_event_receiver_buttom IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
class lcl_event_receiver_buttom implementation.

*----------------------------------------------------------------------*
*       METHOD ON_HOTSPOT_CLICK
*----------------------------------------------------------------------*
  method on_hotspot_click.

*   Internal range tables
    data: lrt_samnr type range of sammg,
          lrt_ernam type range of ernam.

*   Internal work area
    data: lvs_item  like line of gvt_item,
          lrs_samnr like line of lrt_samnr.

*   Internal fields
    data: lvf_vbeln type vbreve-vbeln,
          lvf_vbtyp type vbuk-vbtyp,
          lvf_bukrs type vbreve-bukrs,
          lvf_gjahr type bkpf-gjahr,
          lvf_appli type char2.

*   BAdI references
    data: lvr_badi type ref to badi_sd_rev_rec_podev_disp.

    read table gvt_item_lines index e_row_id-index into lvs_item.

    case e_column_id-fieldname.

      when gcf_vbeln_n.
*       Display subsequent document
        call function 'RV_CALL_DISPLAY_TRANSACTION'
          exporting
            posnr = lvs_item-posnr_n
            vbeln = lvs_item-vbeln_n.

      when gcf_sammg.
*       Display the collective run log,  V.21
        authority-check object 'S_TCODE'
                 id 'TCD' field 'V.21'.
        if sy-subrc ne 0.
          message s172(00) with 'V.21'.
          exit.
        endif.

        lrs_samnr-sign   = gcf_sign_i.
        lrs_samnr-option = gcf_options_eq.
        lrs_samnr-low    = lvs_item-sammg.
        append lrs_samnr to lrt_samnr.

        submit sdsampro with samnr in lrt_samnr          "#EC CI_SUBMIT
                        with smart  = gcf_rev_rec_smart
                        with ernam in lrt_ernam
                        with werror = gcf_true
                        with oerror = gcf_true
                        and return.

      when gcf_vbeln.
*       Display document number
        call function 'RV_CALL_DISPLAY_TRANSACTION'
          exporting
            posnr = lvs_item-posnr
            vbeln = lvs_item-vbeln.

      when gcf_revevdocn.

        check: not lvs_item-revevdocn is initial.

*       Display event document
        case lvs_item-revevtyp.

          when gcf_revevtyp_a.
*           Invoice receipt
            lvf_vbeln = lvs_item-revevdocn.
            lvf_bukrs = lvs_item-bukrs.
            lvf_gjahr = lvs_item-revevdat(4).
            lvf_vbtyp = gcf_revevtyp_a.
            lvf_appli = gcf_appli_mm.

*           Display document
            call function 'RV_CALL_DISPLAY_TRANSACTION'
              exporting
                bukrs    = lvf_bukrs
                gjahr    = lvf_gjahr
                vbeln    = lvf_vbeln
                vbtyp    = lvf_vbtyp
                fi_appli = lvf_appli.

          when gcf_revevtyp_b or
               gcf_revevtyp_p.
*           Customer acceptance date - Display sales document
*           Standard POD             - Display delivery document
            call function 'RV_CALL_DISPLAY_TRANSACTION'
              exporting
                posnr = lvs_item-revevdocni
                vbeln = lvs_item-revevdocn.

          when gcf_revevtyp_x or
               gcf_revevtyp_y or
               gcf_revevtyp_z.

***************************************************
*** CALL BAdI for customer specific events (POD) ***
****************************************************
            try.
*               Get BAdI
                get badi lvr_badi
                  filters
                    revenue_event_type = lvs_item-revevtyp.
*               Call BAdI for customer event
                call badi lvr_badi->display_podev_document
                  exporting
                    iv_revevdocn  = lvs_item-revevdocn
                    iv_revevdocni = lvs_item-revevdocni.

              catch cx_badi_not_implemented.            "#EC NO_HANDLER
            endtry.

        endcase.

    endcase.

  endmethod.                    "on_hotspot_click
endclass.                    "lcl_event_receiver IMPLEMENTATION
