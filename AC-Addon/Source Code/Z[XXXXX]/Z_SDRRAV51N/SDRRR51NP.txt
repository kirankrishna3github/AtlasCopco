*----------------------------------------------------------------------*
*  INCLUDE SDRRR51NP  ---   Subroutines
*  ************************************
*
* perform prepare_fieldcat.
* perform prepare_layout.
* perform process.
*
*----------------------------------------------------------------------*

*----------------------------------------------------------------------*
*       Form  prepare_fieldcat
*----------------------------------------------------------------------*
form prepare_fieldcat
  using
        pif_level      type char1
  changing
        pct_fieldcat   type lvc_t_fcat.

  field-symbols:
        <lvs_fieldcat> type lvc_s_fcat.

  clear pct_fieldcat.

* prepare fieldcatalog
  if pif_level = gcf_headerkey.
    call function 'LVC_FIELDCATALOG_MERGE'
      exporting
        i_structure_name = gcf_vbrav51hd_s
      changing
        ct_fieldcat      = pct_fieldcat.

    loop at pct_fieldcat assigning <lvs_fieldcat>.

      case <lvs_fieldcat>-fieldname.
        when gcf_vbeln.
          <lvs_fieldcat>-key        = gcf_true.
        when gcf_posnr.
          <lvs_fieldcat>-key        = gcf_true.
        when gcf_bdjpoper.
          <lvs_fieldcat>-edit_mask  = '____/___'.
        when gcf_vbeln_v.
          <lvs_fieldcat>-hotspot    = gcf_true.
      endcase.
    endloop.

  elseif pif_level = gcf_itemkey.
    call function 'LVC_FIELDCATALOG_MERGE'
      exporting
        i_structure_name = gcf_vbrav51it_s
      changing
        ct_fieldcat      = pct_fieldcat.

    loop at pct_fieldcat assigning <lvs_fieldcat>.

      case <lvs_fieldcat>-fieldname.
        when gcf_vbeln.
          <lvs_fieldcat>-key        = gcf_true.
          <lvs_fieldcat>-hotspot    = gcf_true.
        when gcf_posnr.
          <lvs_fieldcat>-key        = gcf_true.
        when gcf_bdjpoper.
          <lvs_fieldcat>-edit_mask  = '____/___'.
        when gcf_vbeln_n.
          <lvs_fieldcat>-hotspot    = gcf_true.
        when gcf_sammg.
          <lvs_fieldcat>-hotspot    = gcf_true.
        when gcf_revevdocn.
          <lvs_fieldcat>-hotspot    = gcf_true.
        when gcf_revpoblck.
          <lvs_fieldcat>-tech       = gcf_true.
        when gcf_icon_poblck.
          <lvs_fieldcat>-outputlen  = gcf_call_5.
          <lvs_fieldcat>-coltext    = text-008.
          <lvs_fieldcat>-tooltip    = text-002.
          <lvs_fieldcat>-seltext    = text-002.
      endcase.
    endloop.
  endif.

endform.                    " prepare_fieldcat

*----------------------------------------------------------------------*
*       Form  prepare_layout
*----------------------------------------------------------------------*
form prepare_layout using    pif_level type char1
                    changing pcs_layout type lvc_s_layo.

  clear: pcs_layout.

  if pif_level eq gcf_headerkey.
    pcs_layout-grid_title = text-006.
    pcs_layout-sel_mode   = 'A'.
  elseif pif_level eq gcf_itemkey.
    pcs_layout-grid_title = text-003.
  endif.

endform.                    "prepare_layout

*----------------------------------------------------------------------*
*       Form  process
*----------------------------------------------------------------------*
form process.

* Select control lines from database
  select * from vbrevk into table gvt_vbrevk where vbeln in svbeln and
                                                   posnr in sposnr and
                                                   bukrs in sbukrs and
                                                   auart in sauart and
                                                   kunag in skunag
                                             order by primary key.

  if sy-subrc eq 0.

*   Read detail lines from database
    perform read_detail_lines using gvt_vbrevk[]
                           changing gvt_vbreve[]
                                    gvt_vbrevr[]
                                    gvt_control[]
                                    gvf_subrc_vbreve
                                    gvf_subrc_vbrevr.

*   Create report lines for ALV
    perform create_report_lines using gvt_vbreve[]
                                      gvt_vbrevr[]
                                      gvf_subrc_vbreve
                                      gvf_subrc_vbrevr
                             changing gvt_vbrevk[]
                                      gvt_header[]
                                      gvt_item[].


  endif.

endform.                    " process
