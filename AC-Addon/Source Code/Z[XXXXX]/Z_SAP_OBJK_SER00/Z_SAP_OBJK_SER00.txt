*&---------------------------------------------------------------------*
*& Report  Z_SAP_OBJK_SER00                                            *
*&                                                                     *
*&---------------------------------------------------------------------*
* This report finds records in table SER00 which have no affiliated    *
* OBJK records.                                                        *
* If ptest is initial the records found are deleted from table SER00   *
* and from table SER00. A list of the deleted records is created.      *
* Where do SER00's without affiliated OBJK's come from?                *
* - They were created when technical objects were deleted from the     *
*   object list of SD documents (orders, contracts, ...) before the    *
*   implementation of notes 359535 and 569269 or with a support        *
*   package level less than SAPKH46B48 or SAPKH46C39 or SAPKH47005.    *
*----------------------------------------------------------------------*
REPORT  Z_SAP_OBJK_SER00.

DATA: count_ser00 TYPE i.
DATA: BEGIN OF lt_ser00 OCCURS 0,
        obknr   LIKE objk-obknr,
      END OF lt_ser00.

FIELD-SYMBOLS: <lt_ser00> LIKE lt_ser00.

PARAMETERS ptest AS CHECKBOX DEFAULT 'X'.
************************************************************************
START-OF-SELECTION.

* get ser00's with no corresponding objk's
  SELECT * FROM ser00 AS s
    INTO CORRESPONDING FIELDS OF TABLE lt_ser00
    WHERE NOT EXISTS
      ( SELECT * FROM objk
          WHERE obknr = s~obknr ).

* list output
  IF lt_ser00[] IS INITIAL.
    WRITE: / 'SER00 and OBJK consistent, ok'.
  ELSE.
    CLEAR count_ser00.
    WRITE: / 'Number of SER00 entries without affiliated OBJK entries:'.
    LOOP AT lt_ser00
            ASSIGNING <lt_ser00>.
      count_ser00 = count_ser00 + 1.
      WRITE: / <lt_ser00>-obknr.
    ENDLOOP.
    WRITE: / 'Number of SER00 entries without affiliated OBJK entries',
              count_ser00.
  ENDIF.

***********************************************************************
* Update
  IF ptest IS INITIAL.
* no mismatches between SER00 and OBJK, for all SER00 entry there is an
* affiliated OBJK entry -> no update
    IF lt_ser00[] IS INITIAL.
      WRITE: / 'No records deleted from SER00'.
* there are SER00 entries for which there is no affiliated OBJK entry
* -> update necessary
    ELSE.
      CLEAR count_ser00.
      WRITE: / 'Records deleted from SER00:'.
      LOOP AT lt_ser00
              ASSIGNING <lt_ser00>.
* Delete SER00 entries
        DELETE FROM ser00
                    WHERE obknr = <lt_ser00>-obknr.
        count_ser00 = count_ser00 + 1.
      ENDLOOP.
      WRITE: / count_ser00.
    ENDIF.
  ENDIF.
***********************************************************************
*&---------------------------------------------------------------------*
*& Report  Z_SAP_OBJK_SER00                                            *
*&                                                                     *
*&---------------------------------------------------------------------*
*&                                                                     *
*&                                                                     *
*&---------------------------------------------------------------------*

                     .

*Selection text£º
*PTEST:        TEST?
