*----------------------------------------------------------------------*
*  INCLUDE SDRRR01F  ---   Subroutines
*  ************************************
*
*  perform fetch_ctrl_for_cancelation.
*  perform fetch_docs_with_collrun_number.
*  perform fetch_log_system.
*  perform fetch_revrec_data.
*
*----------------------------------------------------------------------*

*----------------------------------------------------------------------*
*       Form  FETCH_CTRL_FOR_CANCELATION
*----------------------------------------------------------------------*
*       Fetch relevant control lines for cancelation
*----------------------------------------------------------------------*
*  --> PIT_VBSS        Sales document numbers of collective process
*  --> PIT_BUKRS       Selected company codes
*  --> PIT_AUART       Selected document types
*  --> PIT_POSNR       Selected document item numbers
*  --> PIT_KUNAG       Selected customer numbers
*  <-> PCT_VBELN       Selected document numbers
*  <-> PCT_VBREVK      Fetched control lines
*  <-> PCF_SUBRC       Error mark
*----------------------------------------------------------------------*
form fetch_ctrl_for_cancelation
                          using pit_vbss   type gvt_strucst_vbss
                                pit_bukrs  type gvt_strucst_range04
                                pit_auart  type gvt_strucst_range04
                                pit_posnr  type gvt_strucst_range06
                                pit_kunag  type gvt_strucst_range10
                       changing pct_vbeln  type gvt_strucst_range10
                                pct_vbrevk type gvt_strucst_vbrevk
                                pcf_subrc  like sy-subrc.

* Internal ranges
  data: lvs_vbeln type gvs_struc_range10.

* Internal fields
  data: lvf_vbss_lines like sy-tfill,
        lvf_numf       type f,
        lvf_num        like sy-index,
        lvf_tabix_from like sy-tabix value 1,
        lvf_tabix_to   like sy-tabix value gcf_sammg_package.

* Field symbols
  field-symbols: <lvs_vbss> type vbss.

* Determine lines counter
  describe table pit_vbss lines lvf_vbss_lines.

* Set loop counter
  if lvf_vbss_lines eq 0.
    lvf_num = 1.
  else.
    lvf_numf = lvf_vbss_lines / gcf_sammg_package.
    lvf_num  = abs( lvf_numf ).
    if lvf_num lt lvf_numf.
      lvf_num = lvf_num + 1.
    endif.
*   Set range data
    lvs_vbeln-sign   = gcf_sign_i.
    lvs_vbeln-option = gcf_options_eq.
  endif.

  do lvf_num times.

    if lvf_vbss_lines gt 0.
*     Initialize selection table
      refresh: pct_vbeln.
    endif.

*   Insert document numbers in selection table
    loop at pit_vbss assigning <lvs_vbss> from lvf_tabix_from
                                            to lvf_tabix_to.
      lvs_vbeln-low  = <lvs_vbss>-vbeln.
*     Append document number
      append lvs_vbeln to pct_vbeln.
    endloop.

    if sy-index lt lvf_num.
*     Set loop tabix
      lvf_tabix_from = lvf_tabix_from + gcf_sammg_package.
      lvf_tabix_to   = lvf_tabix_to   + gcf_sammg_package.
      if lvf_tabix_to gt lvf_vbss_lines.
        lvf_tabix_to = lvf_vbss_lines.
      endif.
    endif.

*   Fetch relevant control lines for cancelation
    select * appending table pct_vbrevk from vbrevk
             where bukrs in pit_bukrs          and
                   auart in pit_auart          and
                   vbeln in pct_vbeln          and
                   posnr in pit_posnr          and
                   kunag in pit_kunag
             order by primary key.

    if not sy-subrc  is initial and
           pcf_subrc is initial.
*     Set error mark
      pcf_subrc = sy-subrc.
    endif.

  enddo.

  if not pcf_subrc    is initial and
     not pct_vbrevk[] is initial.
*   Initialize error mark because control lines were found
    clear: pcf_subrc.
  endif.

endform.                                " FETCH_CTRL_FOR_CANCELATION

*----------------------------------------------------------------------*
*      Form  FETCH_DOCS_COLLRUN_NUMBER
*----------------------------------------------------------------------*
*      Check revenue lines: Open posting period
*                           Check/create header keys
*----------------------------------------------------------------------*
*  --> PIF_SAMMG_SEL   Selected collective run number
*  --> PIT_VBELN       Selected document numbers
*  <-> PCT_VBSS        Sales document numbers of collective process
*  <-> PCT_VBFS        Error log
*----------------------------------------------------------------------*
form fetch_docs_with_collrun_number
                       using pif_sammg_sel like vbreve-sammg
                             pit_vbeln     type gvt_strucst_range10
                    changing pct_vbss      type gvt_strucst_vbss
                             pct_vbfs      type gvt_strucst_vbfs.

* Selected collective run number and no sales document numbers ?
  check: not pif_sammg_sel is initial and
             pit_vbeln[]   is initial.

* Fetch sales document numbers of an collective run process
  select * into table pct_vbss from vbss where sammg = pif_sammg_sel
                               order by primary key.

  if not sy-subrc is initial.
*   Write error log because no sales doc number in VBSS
    perform create_error_log using pif_sammg_sel
                                   space
                                   gcf_vfrr
                                   gcf_msgid_e
                                   gcf_msgno_021
                                   pif_sammg_sel
                                   space
                                   space
                                   space
                          changing pct_vbfs[].
  endif.

endform.                                  " FETCH_DOCS_COLLRUN_NUMBER

*----------------------------------------------------------------------*
*      Form  FETCH_LOG_SYSTEM
*----------------------------------------------------------------------*
*      Fetch logical system
*----------------------------------------------------------------------*
*  <-> PCF_LOGSYS   Relevant logical system
*----------------------------------------------------------------------*
form fetch_log_system changing pcf_logsys like t000-logsys.

* Set logical system name
  select single logsys from t000 into pcf_logsys
                       where mandt = sy-mandt.

endform.                                  " FETCH_LOG_SYSTEM

*----------------------------------------------------------------------*
*       Form  FETCH_REVREC_DATA
*----------------------------------------------------------------------*
*       Fetch revenue recogntion data
*----------------------------------------------------------------------*
*  --> PIT_VBSS        Sales document numbers of collective process
*  --> PIT_BUKRS       Selected company codes
*  --> PIT_AUART       Selected document types
*  --> PIT_POSNR       Selected document item numbers
*  --> PIT_VBELNN      Selected subsequent document numbers
*  --> PIT_POSNRN      Selected subsequent document item numbers
*  --> PIT_KUNAG       Selected customer numbers
*  --> PIF_GJAHR_L     Selected fiscal year        (low)
*  --> PIF_POPER_L     Selected fiscal year period (low)
*  --> PIF_GJAHR_H     Selected fiscal year        (high)
*  --> PIF_POPER_H     Selected fiscal year period (high)
*  --> PIF_POSTDAT     Selected posting date
*  --> PIF_RLKZ        Selected revenue lines which can be realized
*  --> PIF_BLKZ        Selected revenue lines which are blocked
*  --> PIF_SAMMG_SEL   Selected collective run number
*  --> PIF_REVFUNC     Recognition/Cancelation flag
*  <-> PCT_VBELN       Selected document numbers
*  <-> PCT_VBREVEAV    Relevant revenue lines
*  <-> PCT_VBREVKAV    Control line infos
*  <-> PCT_VBFS        Error log
*  <-> PCF_LOGSYS      Relevant logical system
*  <-> PCF_BDJPOPER_L  Relevant posting period (low)
*  <-> PCF_BDJPOPER_H  Relevant posting period (high)
*  <-> PCF_MASSAGE     Message flag
*----------------------------------------------------------------------*
form fetch_revrec_data
                 using pit_vbss        type gvt_strucst_vbss
                       pit_bukrs       type gvt_strucst_range04
                       pit_auart       type gvt_strucst_range04
                       pit_posnr       type gvt_strucst_range06
                       pit_vbelnn      type gvt_strucst_range10
                       pit_posnrn      type gvt_strucst_range06
                       pit_kunag       type gvt_strucst_range10
                       pif_gjahr_l     like t009b-bdatj
                       pif_poper_l     like t009b-poper
                       pif_gjahr_h     like t009b-bdatj
                       pif_poper_h     like t009b-poper
                       pif_postdat     like sy-datum
                       pif_rlkz        like vbreve-revpoblck
                       pif_blkz        like vbreve-revpoblck
                       pif_sammg_sel   like vbreve-sammg
                       pif_revfunc     type char1
              changing pct_vbeln       type gvt_strucst_range10
                       pct_vbreveav    type gvt_strucst_vbreveav
                       pct_vbrevkav    type gvt_strucst_vbrav51hd
                       pct_vbfs        type gvt_strucst_vbfs
                       pcf_logsys      like t000-logsys
                       pcf_bdjpoper_l  like vbreve-bdjpoper
                       pcf_bdjpoper_h  like vbreve-bdjpoper
                       pcf_message     type char1.

* Internal tables
  data: lvt_vbrevk    type gvt_strucst_vbrevk,    " Control lines
        lvt_control   type gvt_strucst_control,   " Collect contr.lines
        lvt_peropen   type gvt_strucso_peropen,   " Open periods
        lvt_head_keys type gvt_strucst_head_keys, " Header keys
        lvt_refamt    type gvt_strucst_refamt.    " Ref.line amount

* Internal ranges
  ranges: lvt_bdjpoper  for vbreve-bdjpoper,       " Relevant periods
          lvt_sammg_sel for vbreve-sammg,          " Collective run no
          lvt_revpoblck for vbreve-revpoblck.      " Revenue post.block

* Internal work areas
  data: lvs_bdjpoper_l type gvs_struc_bdjpoper,
        lvs_bdjpoper_h type gvs_struc_bdjpoper.

* Internal fields
  data: lvf_rrsta        like vbreve-rrsta,
        lvf_revfix       like vbreve-revfix,
        lvf_vbrevk_subrc like sy-subrc.

* Set relevant selection status
  if pif_revfunc eq gcf_recognition.

*   Fill range of revenue posting block info
    if pif_rlkz eq gcf_rlkz_yes.
      if pif_blkz eq gcf_blkz_no.
        lvt_revpoblck-option = gcf_options_ne.
        lvt_revpoblck-low    = gcf_blkz_yes.
      endif.
    else.
      if pif_blkz eq gcf_blkz_yes.
        lvt_revpoblck-option = gcf_options_eq.
        lvt_revpoblck-low    = gcf_blkz_yes.
      endif.
    endif.

    if not lvt_revpoblck is initial.
      lvt_revpoblck-sign = gcf_sign_i.
*     Append revenue posting block info
      append lvt_revpoblck.
    endif.

*   Status for recognition
    lvf_rrsta  = gcf_rrsta_aktiv.

  else.
*   Status for cancalation
    lvf_rrsta = gcf_rrsta_ready.
*   Migrated lines can be canceled
    lvf_revfix = gcf_revfix_e.
  endif.

* Fetch logical system
  perform fetch_log_system changing pcf_logsys.

* Fill range of posting period
  lvs_bdjpoper_l-bdatj = pif_gjahr_l.
  lvs_bdjpoper_l-poper = pif_poper_l.
  lvs_bdjpoper_h-bdatj = pif_gjahr_h.
  lvs_bdjpoper_h-poper = pif_poper_h.

* Set posting period
  if not ( lvs_bdjpoper_l is initial and
           lvs_bdjpoper_h is initial ).

    if lvs_bdjpoper_h is initial.
      lvt_bdjpoper-option = gcf_options_eq.
    else.
      lvt_bdjpoper-option = gcf_options_bt.
    endif.

    lvt_bdjpoper-sign = gcf_sign_i.
    lvt_bdjpoper-low  = lvs_bdjpoper_l.
    lvt_bdjpoper-high = lvs_bdjpoper_h.

    append lvt_bdjpoper.

  endif.

* Fill range of collective run number
  if not pif_sammg_sel is initial.

*   Set collective run number
    lvt_sammg_sel-option = gcf_options_eq.
    lvt_sammg_sel-sign   = gcf_sign_i.
    lvt_sammg_sel-low    = pif_sammg_sel.

    append lvt_sammg_sel.

  endif.

  if lvs_bdjpoper_l is initial and      " If lvs_bdjpoper_l is ...
     lvs_bdjpoper_h is initial and
     pif_sammg_sel  is initial.
*   Set message flag
    pcf_message = gc_charx.
*   No period selected
    message s009.
  else.

*   Set highest posting period
    if not lvs_bdjpoper_l is initial and
           lvs_bdjpoper_h is initial.
      lvs_bdjpoper_h = lvs_bdjpoper_l.
    endif.

*   Set period fields
    pcf_bdjpoper_l = lvs_bdjpoper_l.
    pcf_bdjpoper_h = lvs_bdjpoper_h.

    if pif_revfunc eq gcf_recognition.
*     Fetch relevant control lines for recognition
      select * into table lvt_vbrevk from vbrevk
               where bukrs in pit_bukrs          and
                     rrsta eq gcf_rrsta_aktiv    and
                     auart in pit_auart          and
                     vbeln in pct_vbeln          and
                     posnr in pit_posnr          and
                     kunag in pit_kunag
               order by primary key.
*     Set error mark
      lvf_vbrevk_subrc = sy-subrc.
    else.
*     Fetch relevant control lines for cancelation
      perform fetch_ctrl_for_cancelation using pit_vbss[]
                                               pit_bukrs[]
                                               pit_auart[]
                                               pit_posnr[]
                                               pit_kunag[]
                                      changing pct_vbeln[]
                                               lvt_vbrevk[]
                                               lvf_vbrevk_subrc.
    endif.

    if lvf_vbrevk_subrc is initial.      " If lvf_vbrevk_subrc is ...

*     Collect control lines with posting period check
      perform collect_control_lines using lvt_vbrevk[]
                                          pif_postdat
                                 changing lvt_control[]
                                          lvt_peropen[]
                                          pct_vbfs[].

*     Check enties in control table
      check: not lvt_control[] is initial.

*     Fetch relevant revenue lines
      select * into corresponding fields of table pct_vbreveav
                          from vbreve
                          for all entries in lvt_control
                          where vbeln     =  lvt_control-vbeln and
                                posnr     =  lvt_control-posnr and
                                bdjpoper  in lvt_bdjpoper      and
                                vbeln_n   in pit_vbelnn        and
                                posnr_n   in pit_posnrn        and
                                sammg     in lvt_sammg_sel     and
                              ( revpoblck in lvt_revpoblck or
                                revpoblck is null )            and
                                rrsta     =  lvf_rrsta         and
                              ( revfix    =  space      or
                                revfix    =  lvf_revfix or
                                revfix    is null )
                          order by primary key.

      if sy-subrc is initial.             " If sy-subrc is ...

*       Check revenue lines
        perform check_revenue_lines using lvt_vbrevk[]
                                          lvt_peropen[]
                                          pif_revfunc
                                 changing pct_vbreveav[]
                                          lvt_head_keys[].

*       Fetch relevant reference lines (only WRBTR) with index table
        select * into corresponding fields of table lvt_refamt
                            from vbrevr
                            for all entries in lvt_head_keys
                            where vbeln = lvt_head_keys-vbeln and
                                  posnr = lvt_head_keys-posnr
                            order by primary key.

*       Create conrol line infos for ALV
        perform create_ctrl_info using lvt_head_keys[]
                                       lvt_refamt[]
                              changing lvt_vbrevk[]
                                       pct_vbrevkav[].

      endif.                             " If sy-subrc is ...
    endif.                               " If lvf_vbrevk_subrc is ...
  endif.                                 " If lvs_bdjpoper_l is ...

* Customer function changing the work list
  call function 'OUTBOUND_CALL_00503108_E'
    exporting
      fif_revfunc  = pif_revfunc
    tables
      fit_vbreveav = pct_vbreveav
      fit_vbrevkav = pct_vbrevkav.

endform.                                  " FETCH_REVREC_DATA
