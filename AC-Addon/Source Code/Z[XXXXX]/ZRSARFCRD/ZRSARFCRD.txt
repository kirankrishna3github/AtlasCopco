REPORT rsarfcrd NO STANDARD PAGE HEADING
                LINE-SIZE 265
                MESSAGE-ID sr.
INCLUDE <sys000>.
TABLES : arfcsstate, arfcsdata, t100, rfcdes.

*  Text-Konstanten
DATA : recorded LIKE arfcsstate-arfcstate VALUE 'RECORDED',
       cpicerr  LIKE arfcsstate-arfcstate VALUE 'CPICERR',
       sysfail  LIKE arfcsstate-arfcstate VALUE 'SYSFAIL',
       confail  LIKE arfcsstate-arfcstate VALUE 'CONFAIL',
       executed LIKE arfcsstate-arfcstate VALUE 'EXECUTED',
       mailed   LIKE arfcsstate-arfcstate VALUE 'MAILED',
       read     LIKE arfcsstate-arfcstate VALUE 'READ',
       vbrecord LIKE arfcsstate-arfcstate VALUE 'VBRECORD',
       vberror  LIKE arfcsstate-arfcstate VALUE 'VBERROR',
       sended   LIKE arfcsstate-arfcstate VALUE 'SENDED',
       debug    LIKE arfcsstate-arfcstate VALUE 'DEBUG',
       vxrecord LIKE arfcsstate-arfcstate VALUE 'VXRECORD',
       sysload  LIKE arfcsstate-arfcstate VALUE 'SYSLOAD',
       retry    LIKE arfcsstate-arfcstate VALUE 'RETRY',
       aretry   LIKE arfcsstate-arfcstate VALUE 'ARETRY',
       anoretry LIKE arfcsstate-arfcstate VALUE 'ANORETRY'.

* Local data
DATA: BEGIN OF arfcistate OCCURS 50.
        INCLUDE STRUCTURE arfcsstate.
DATA: END OF arfcistate.

DATA: BEGIN OF acttid.
        INCLUDE STRUCTURE arfctid.
DATA: END OF acttid.

DATA: nfill      TYPE i,
      valid_line VALUE 'X',
      globalline LIKE sy-linno,
      globalfield(100),
      sl_entry(96),
      answer,
      dbg.

DATA: rfcmess(72),
      retcode     TYPE c,
      p_title(60) TYPE c,
      subrc       LIKE sy-subrc,
      rmqdest     LIKE rfcdes-rfcdest,
      fields      LIKE sval OCCURS 0 WITH HEADER LINE.

DATA: size TYPE p DECIMALS 2,
      trys TYPE i.

FIELD-SYMBOLS <m>.

SELECT-OPTIONS zeitraum FOR arfcsstate-arfcdatum
                            DEFAULT sy-datum TO sy-datum
                            OPTION BT.
SELECT-OPTIONS benutzer FOR arfcsstate-arfcuser
                            DEFAULT sy-uname
                            OPTION EQ.
SELECT-OPTIONS function FOR arfcsstate-arfcfnam
                            DEFAULT '*'
                            OPTION CP.
SELECT-OPTIONS dest FOR arfcsstate-arfcdest
                            DEFAULT '*'
                            OPTION CP.
SELECT-OPTIONS status FOR arfcsstate-arfcstate
                            DEFAULT '*'
                            OPTION CP.
SELECT-OPTIONS trfcipid FOR arfcsstate-arfcipid
                            DEFAULT '*'
                            OPTION CP
                            NO-DISPLAY.
SELECT-OPTIONS trfcpid FOR arfcsstate-arfcpid
                            DEFAULT '*'
                            OPTION CP
                            NO-DISPLAY.
SELECT-OPTIONS trfctime FOR arfcsstate-arfctime
                            DEFAULT '*'
                            OPTION CP
                            NO-DISPLAY.
SELECT-OPTIONS trfctcnt FOR arfcsstate-arfctidcnt
                            DEFAULT '*'
                            OPTION CP
                            NO-DISPLAY.
SELECT-OPTIONS trfclcnt FOR arfcsstate-arfcluwcnt
                            DEFAULT '*'
                            OPTION CP
                            NO-DISPLAY.
TRANSLATE benutzer USING '*%+_'.

SET PF-STATUS 'LISTE'.
*perform top_of_page.
PERFORM show_file.

*---------------------------------------------------------------------*
*       FORM SHOW_FILE                                                *
*---------------------------------------------------------------------*
FORM show_file.

  valid_line = 'X'.

*  if benutzer = space or benutzer = '*'.
*    select * from arfcsstate into table arfcistate
*             where  arfcdatum in zeitraum.
*  else.
*    select * from arfcsstate into table arfcistate
*             where  arfcuser like benutzer
*             and    arfcdatum in zeitraum.
*  endif.

* Problem: When a select-options table (RSPARAM format) is passed to
* this report, then the select-options table header is filled with
* the specifications from the selection table. When a range table is
* passed in, then the range table entries are entered in the select-
* options table, but the header line contains the defaults.  Further,
* should an empty range table be passed in, then the table has a length
* of zero, but the defaults are present in the header and are used
* by the select. Because of these peculiarities, we must check for
* the presence of a line in the select option table for the first part
* of a TRFC TID. This allows differentiation of the empty range-table
* situation from all others.  An error should probably be raised.
* is a default or not. An RC 4 indicates an empty range table.
  READ TABLE trfcipid INDEX 1.
  CASE sy-subrc.
    WHEN 0.    " Select-options table contained a record other than the
      " defaults.  This means that arfcsstate tid specifications
      " have been passed in and the select should use them.
      IF trfcipid-low <> '*'.
        SELECT * FROM arfcsstate INTO TABLE arfcistate
          WHERE arfcipid   IN trfcipid
            AND arfcpid    IN trfcpid
            AND arfctime   IN trfctime
            AND arfctidcnt IN trfctcnt
            AND arfcluwcnt IN trfclcnt
            AND arfcreturn  = space.
      ELSE.
        SELECT * FROM arfcsstate INTO TABLE arfcistate
          WHERE arfcuser   IN benutzer
            AND arfcdest   IN dest
            AND arfcfnam   IN function
            AND arfcstate  IN status
            AND arfcdatum  IN zeitraum
            AND arfcreturn  = space.
      ENDIF.
    WHEN 4.    " Empty range tables passed - only header of select table
      " is filled, with the defaults for the select-option.
      " In this case, use select form without tid.
      SELECT * FROM arfcsstate INTO TABLE arfcistate
        WHERE arfcipid   IN trfcipid
          AND arfcpid    IN trfcpid
          AND arfctime   IN trfctime
          AND arfctidcnt IN trfctcnt
          AND arfcluwcnt IN trfclcnt
          AND arfcreturn  = space.
  ENDCASE.

  subrc = sy-subrc.
  IF subrc <> 0.
    SKIP 1.
    FORMAT COLOR COL_HEADING INTENSIFIED OFF.
    WRITE: / sy-uline(65).
    WRITE: /: '|', (61) 'Es wurde nichts selektiert.'(009), '|'.
    WRITE: / sy-uline(65).
    EXIT.
  ENDIF.

  SORT arfcistate BY arfcipid arfcpid arfctime arfctidcnt arfcdest
                     arfcluwcnt  arfcuser  arfcdatum.

* qRFC-LUWs entfernen
  IF subrc = 0.
    LOOP AT arfcistate.
      %_reserv = arfcistate-arfcreserv.
      IF %_reserv-qrfcflag <> space.
        DELETE arfcistate.
        CONTINUE.
      ENDIF.
    ENDLOOP.

    DESCRIBE TABLE arfcistate LINES nfill.
    IF nfill = 0.
      SKIP 1.
      FORMAT COLOR COL_HEADING INTENSIFIED OFF.
      WRITE: / sy-uline(65).
      WRITE: /: '|', (61) 'Es wurde nichts selektiert.'(009), '|'.
      WRITE: / sy-uline(65).
      EXIT.
    ENDIF.
  ENDIF.

  PERFORM top_of_page.
  FORMAT COLOR COL_NORMAL  INTENSIFIED OFF.
  LOOP AT arfcistate.
    acttid = arfcistate(24).
    MOVE-CORRESPONDING arfcistate TO arfcsstate.      "Wegen F1
    WRITE : sy-vline NO-GAP ,
            (12) arfcsstate-arfcuser COLOR COL_KEY NO-GAP,
            sy-vline NO-GAP , (30) arfcsstate-arfcfnam NO-GAP,
            sy-vline NO-GAP , (32) arfcsstate-arfcdest NO-GAP,
            sy-vline NO-GAP.
    HIDE: acttid,
          arfcsstate-arfcdest, arfcsstate-arfcfnam ,
          arfcsstate-arfcluwcnt, arfcsstate-arfcreserv.
    IF  arfcistate-arfcdatum >< space.
      WRITE  (10) arfcsstate-arfcdatum NO-GAP.
      HIDE arfcsstate-arfcdatum.
    ENDIF.
    WRITE  sy-vline NO-GAP.
    IF arfcsstate-arfcuzeit >< space.
      WRITE  (8) arfcsstate-arfcuzeit NO-GAP.
    ENDIF.
    WRITE  sy-vline NO-GAP.
    IF arfcsstate-arfcmsg <> space.
      IF arfcsstate-arfcstate = sysfail  OR
         arfcsstate-arfcstate = cpicerr  OR
         arfcsstate-arfcstate = anoretry OR
         arfcsstate-arfcstate = aretry   OR
         arfcsstate-arfcstate = vberror  OR
         arfcsstate-arfcstate = retry.
        WRITE : (50) arfcsstate-arfcmsg COLOR COL_NEGATIVE NO-GAP.
      ELSE.
        WRITE : (50) arfcsstate-arfcmsg NO-GAP.
      ENDIF.
    ELSE.
      PERFORM langtext USING arfcsstate-arfcstate.  " setzt <M> !
      WRITE (50) <m> NO-GAP.
    ENDIF.
    HIDE: arfcsstate-arfcstate, arfcsstate-arfcmsg.
    WRITE : sy-vline NO-GAP,   (24) acttid NO-GAP.
    HIDE  acttid.
*         --------- BIS HIERHER ---------

    WRITE :  sy-vline NO-GAP,   (8) arfcsstate-arfcrhost NO-GAP.
    WRITE :  sy-vline NO-GAP,   (20) arfcsstate-arfctcode NO-GAP.
    %_reserv = arfcsstate-arfcreserv.
    WRITE : sy-vline NO-GAP,   (40) %_reserv-cprog NO-GAP.
    WRITE :  sy-vline NO-GAP,  (3)  %_reserv-mandt NO-GAP.
    WRITE :  sy-vline NO-GAP,  (4) arfcsstate-arfcretrys NO-GAP.
    POSITION sy-linsz. WRITE sy-vline NO-GAP.
    HIDE valid_line.
    AT END OF arfctidcnt.              "LUW unterstreichen
      ULINE.
    ENDAT.
  ENDLOOP.
  SCROLL LIST INDEX 1 TO PAGE sy-cpage LINE sy-staro.
  SCROLL LIST INDEX 1 TO COLUMN sy-staco.
  SET CURSOR FIELD globalfield LINE globalline.
ENDFORM.                    "show_file

*---------------------------------------------------------------------*
*       FORM EXECUTE_LINE                                             *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
FORM execute_line.

  IF acttid IS INITIAL.
    MESSAGE e015.
  ENDIF.
  %_reserv = arfcsstate-arfcreserv.    "Cast auf Struktur %_RESERV
  IF %_reserv-mandt <> sy-mandt.
    MESSAGE e040 WITH %_reserv-mandt.
  ENDIF.

  IF arfcsstate-arfcstate <> executed.
    IF dbg <> space.
      SELECT SINGLE * FROM rfcdes
        WHERE rfcdest = arfcsstate-arfcdest.
      IF ( sy-subrc = 0 AND rfcdes-rfctype = 'T' ).
        MESSAGE e361(sy) WITH rfcdes-rfctype.
      ENDIF.
    ENDIF.
    SUBMIT rsarfcse AND RETURN WITH tid = acttid
                               WITH forced = 'X'.
  ELSEIF valid_line = space.
    MESSAGE e015.
  ELSE.
    MESSAGE e016.
  ENDIF.
ENDFORM.                    "execute_line

*---------------------------------------------------------------------*
*       FORM top_of_page                                              *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
FORM top_of_page.
  ULINE.
  FORMAT COLOR COL_HEADING.
  WRITE : sy-vline NO-GAP, (12)  'Aufrufer'(001) NO-GAP,
          sy-vline NO-GAP, (30)  'Funktionsbaustein'(002) NO-GAP,
          sy-vline NO-GAP, (32)  'Zielsystem'(003) NO-GAP,
          sy-vline NO-GAP, (10)   'Datum'(004) NO-GAP,
          sy-vline NO-GAP, (8)   'Zeit'(005) NO-GAP,
          sy-vline NO-GAP, (50)  'Statustext'(006) NO-GAP,
          sy-vline NO-GAP, (24)  'TransaktionsID'(007) NO-GAP,
          sy-vline NO-GAP, (8)   'Host'(016) NO-GAP,
          sy-vline NO-GAP, (20)  'Tktn.'(017) NO-GAP,
          sy-vline NO-GAP, (40)  'Programm'(018) NO-GAP,
          sy-vline NO-GAP, (3)   'Mndt'(012) NO-GAP,
          sy-vline NO-GAP, (4)   'Wdhl'(008) NO-GAP.

  POSITION sy-linsz. WRITE sy-vline NO-GAP.
  ULINE.
  FORMAT COLOR OFF.
ENDFORM.                    "top_of_page


AT USER-COMMAND.
  READ LINE sy-lilli INDEX sy-lsind OF PAGE sy-cpage.
  CASE sy-ucomm.

** Dateigr##e
    WHEN 'INFO'.
      CALL SCREEN 100 STARTING AT 10 5 ENDING AT 70 10.

** Systemeinstellung
    WHEN 'SETI'.
      CALL SCREEN 100 STARTING AT 10 5 ENDING AT 70 10.

** Gesamtgr##e ARFCSDATA
    WHEN 'SIZE'.
      CALL SCREEN 100 STARTING AT 10 5 ENDING AT 70 10.

** Abbrechen
    WHEN 'RW'.
      LEAVE TO TRANSACTION ' '.

** Navigieren je nach Feldposition
    WHEN 'SELC'.
      PERFORM navigate.
      CLEAR arfcsstate.

** Auffrischen
    WHEN 'REFR'.
      PERFORM redisplay.

** Zeile l#schen
    WHEN 'CLEA'.
      IF valid_line = space.
        MESSAGE e015.
      ENDIF.
*      IF sy-lisel+1(12) <> sy-uname.
*        MESSAGE e019 WITH  sy-lisel+1(12).
*      ENDIF.
      GET CURSOR FIELD globalfield LINE globalline.
      CLEAR sl_entry.
      CONCATENATE sy-uname '&' acttid INTO sl_entry.
      CALL 'C_WRITE_SYSLOG_ENTRY' ID 'TYP'  FIELD space
                                  ID 'KEY'  FIELD 'Q20'
                                  ID 'DATA' FIELD sl_entry.
      SUBMIT rsarfcdl WITH tid = acttid AND RETURN.
      IF arfcsstate-arfcstate = aretry OR
         arfcsstate-arfcstate = anoretry.
        CALL FUNCTION 'TRFC_DELETE_RETURN_DATA'
          EXPORTING
            tid = acttid.
      ENDIF.
      IF sy-lsind = 1. sy-lsind = sy-lsind - 1. ENDIF.
      PERFORM redisplay.

      CLEAR acttid.

** Eintr#ge l#schen
    WHEN 'REOR'.
      SUBMIT rsarfcer VIA SELECTION-SCREEN AND RETURN.

** Protokolldatei l#schen
    WHEN 'DELA'.
      IF sy-uname <> 'DDIC'.
        MESSAGE e021.
        EXIT.
      ENDIF.
      CALL FUNCTION 'POPUP_TO_CONFIRM_STEP'
        EXPORTING
          defaultoption = 'N'
          textline1     = 'Protokolldatei l#schen ?'(010)
          textline2     = ' '
          titel         = 'L#schen'(011)
        IMPORTING
          answer        = answer.
      IF answer = 'J'.
        acttid = '*'.
        SUBMIT rsarfcdl WITH tid = acttid AND RETURN.
        IF sy-lsind = 1. sy-lsind = sy-lsind - 1. ENDIF.
        PERFORM redisplay.

        CLEAR acttid.
      ENDIF.

** Ausf¨¹hren
    WHEN 'EXEC'.
      dbg = space.
      EXPORT dbg TO MEMORY  ID '%_arfc_debug'.
      PERFORM execute_line.
      PERFORM redisplay.

** Ausf¨¹hren mit Breakpoint.
    WHEN 'DEXE'.
      dbg = 'X'.
      EXPORT dbg TO MEMORY  ID '%_arfc_debug'.
      PERFORM execute_line.
      PERFORM redisplay.

** Batchjob anzeigen
    WHEN 'BATC'.
      DATA: job LIKE btcselect.
      CONCATENATE 'ARFC:' acttid INTO job-jobname.
      job-username =  sy-lisel+1(12).
      PERFORM batch_display USING job.

* Status SENDED zur¨¹cksetzen
    WHEN 'RSET'.
      IF acttid IS INITIAL.
        MESSAGE e015.
      ENDIF.
      GET CURSOR FIELD globalfield LINE globalline.
      IF arfcsstate-arfcstate = executed.
        MESSAGE e042.
      ELSEIF arfcsstate-arfcstate <> recorded.
        PERFORM reset_luw_state.
        PERFORM redisplay.
      ENDIF.

* Aufr#umerrich
    WHEN 'RCVR'.
      SUBMIT rsarfcex VIA SELECTION-SCREEN AND RETURN.

* qRFC-Administration
    WHEN 'DMQE'.
      SUBMIT rsqevtma AND RETURN.
      PERFORM redisplay.

* QOUT-Scheduler
    WHEN 'DMQS'.
      SUBMIT rsqowkma AND RETURN.
      PERFORM redisplay.

** Aufruf Remote SM58
    WHEN 'RSYS'.
      p_title = 'Remote System'(065).
      PERFORM get_remote_system_param USING    p_title
                                      CHANGING retcode rmqdest.

* Weitermachen, falls Eingabe ok ist.
      IF retcode = space.
        CALL FUNCTION 'ABAP4_CALL_TRANSACTION'
          DESTINATION rmqdest
          EXPORTING
            tcode                   = 'SM58'
          EXCEPTIONS
            system_failure          = 1  MESSAGE rfcmess
            communication_failure   = 2  MESSAGE rfcmess
            call_transaction_denied = 3
            tcode_invalid           = 4
            OTHERS                  = 5.
        subrc = sy-subrc.
        IF subrc <> 0.
          IF subrc = 1 OR subrc = 2.
            MESSAGE i053 WITH rfcmess rfcmess+50.
          ELSEIF subrc = 3.
            MESSAGE i000 WITH 'Keine Berechtigung f¨¹r SM58'(067).
          ELSEIF subrc = 4.
            MESSAGE i000 WITH 'SM58 im Zielsystem nicht vorhanden'(068).
          ELSE.
            MESSAGE i000 WITH
                         'Unerwarteter Fehler beim Start SM58'(069).
          ENDIF.
        ENDIF.
      ENDIF.

  ENDCASE.
  IF sy-lsind = 1. sy-lsind = sy-lsind - 1. ENDIF.
  CLEAR acttid.
  CLEAR arfcsstate.
  CLEAR sy-ucomm.
  CLEAR valid_line.


*---------------------------------------------------------------------*
*       FORM get_remote_system_param
*---------------------------------------------------------------------*
FORM get_remote_system_param USING  p_title TYPE any
                           CHANGING retcode TYPE c
                                    remsyst LIKE rfcdes-rfcdest.

  GET CURSOR FIELD globalfield LINE globalline.
  CLEAR fields.
  REFRESH fields.
  fields-tabname = 'RFCDES'.
  fields-fieldname = 'RFCDEST'.
  fields-field_attr = '00'.
  APPEND fields.
  CALL FUNCTION 'POPUP_GET_VALUES'
    EXPORTING
      popup_title = p_title
    IMPORTING
      returncode  = retcode
    TABLES
      fields      = fields
    EXCEPTIONS
      OTHERS      = 0.
  IF retcode <> 'A'.
    retcode = space.
    READ TABLE fields INDEX 1.
    remsyst = fields-value(24).
    IF remsyst = space.
      retcode = 'X'.
      MESSAGE i000 WITH 'Ung¨¹ltige Destination'(066).
    ENDIF.
  ELSE.
    retcode = 'X'.
  ENDIF.

ENDFORM.                    "get_remote_system_param


*---------------------------------------------------------------------*
*       FORM REDISPLAY
*---------------------------------------------------------------------*
FORM redisplay.
*  perform top_of_page.
  PERFORM show_file.
ENDFORM.                    "redisplay


*---------------------------------------------------------------------*
*       FORM NAVIGATE                                                 *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
FORM navigate.
  DATA: feldname(50), val(100).
  DATA: rfc LIKE rfcdes.
  DATA: BEGIN OF vbkeytab OCCURS 0.
          INCLUDE STRUCTURE vbselkey.
  DATA: END OF vbkeytab.

  GET CURSOR FIELD feldname VALUE val.
  CASE feldname.
* Destinationenpflege
    WHEN 'ARFCSSTATE-ARFCDEST'.
      SELECT SINGLE * FROM rfcdes INTO rfc
        WHERE rfcdest = arfcsstate-arfcdest.
      IF sy-subrc <> 0.
        rfc-rfcdest = arfcsstate-arfcdest.
      ENDIF.
      CALL FUNCTION 'RFCDES_DISPLAY'
        EXPORTING
          rfcdes_export   = rfc
          authority_check = 'X'
          editable        = space.

* FB anzeigen
    WHEN 'ARFCSSTATE-ARFCFNAM'.
      CALL FUNCTION 'RS_FUNCTION_SHOW'
        EXPORTING
          funcname = arfcsstate-arfcfnam
        EXCEPTIONS
          OTHERS   = 1.
      IF sy-subrc = 1.
        MESSAGE w362(sy).
      ELSE.
        CALL FUNCTION 'RS_NAVIGATION_MONITOR'.
      ENDIF.

* Feld mit Errormessages und Status
    WHEN 'ARFCSSTATE-ARFCMSG'.
      IF val CS 'ERROR '  AND
         NOT ( val CS 'RfcAbort' ) AND
         NOT ( val CS 'CPI-C ERROR ' ).
        DATA:  pos TYPE i, ag(2), nr(3).
        pos = sy-fdpos + 6.            " strlen 'ERROR '
        SHIFT val BY pos PLACES.
        ag = val.
        SHIFT val BY 2 PLACES.
        nr = val.
        CALL FUNCTION 'RS_EDTR_SHOW_MESSAGE'
          EXPORTING
            message_id = ag
            message_nr = nr
          EXCEPTIONS
            OTHERS     = 1.

        IF sy-subrc = 0.
          EXIT.
        ENDIF.
      ENDIF.
      CASE arfcsstate-arfcstate.
        WHEN cpicerr.
          IF val = space.
            MESSAGE i027.
          ELSE.
            %_reserv = arfcsstate-arfcreserv.
            MESSAGE i053 WITH val %_reserv-arfcmsg2.
          ENDIF.
        WHEN  sysfail.
          IF val = space.
            MESSAGE i028.
          ELSE.
            %_reserv = arfcsstate-arfcreserv.
            MESSAGE i053 WITH val %_reserv-arfcmsg2.
          ENDIF.
        WHEN vberror.
* Absprung in die Verbuchung zum Aufgetretenen Fehler
          %_reserv = arfcsstate-arfcreserv.
          vbkeytab = %_reserv-vberrkey.
          APPEND vbkeytab.
          EXPORT
             vbkeytab TO MEMORY ID 'CALL_SM13_TAB'.
          CALL TRANSACTION 'SM13'.
        WHEN confail.
          MESSAGE i029.
        WHEN executed.
          MESSAGE i030.
        WHEN mailed.
          MESSAGE i031.
        WHEN read.
          MESSAGE i032.
        WHEN recorded.
          MESSAGE i033.
        WHEN vbrecord.
          MESSAGE i034.
        WHEN vxrecord.
          MESSAGE i036.
        WHEN sended.
          MESSAGE i038.
        WHEN debug.
          MESSAGE i039.
        WHEN sysload.
          MESSAGE i052.
        WHEN retry.
          MESSAGE i500(sy).
        WHEN aretry.
          SUBMIT rstrfcds AND RETURN WITH tid  = acttid
                                     WITH fnum = 0.
        WHEN anoretry.
          SUBMIT rstrfcds AND RETURN WITH tid  = acttid
                                     WITH fnum = 0.
        WHEN OTHERS.
          MESSAGE i035.
      ENDCASE.

    WHEN 'T100-TEXT'.
      CASE arfcsstate-arfcstate.
        WHEN cpicerr.
          IF val = space.
            MESSAGE i027.
          ELSE.
            %_reserv = arfcsstate-arfcreserv.
            MESSAGE i053 WITH val %_reserv-arfcmsg2.
          ENDIF.
        WHEN  sysfail.
          IF val = space.
            MESSAGE i028.
          ELSE.
            %_reserv = arfcsstate-arfcreserv.
            MESSAGE i053 WITH val %_reserv-arfcmsg2.
          ENDIF.
        WHEN confail.
          MESSAGE i029.
        WHEN executed.
          MESSAGE i030.
        WHEN mailed.
          MESSAGE i031.
        WHEN read.
          MESSAGE i032.
        WHEN recorded.
          MESSAGE i033.
        WHEN vbrecord.
          MESSAGE i034.
        WHEN vxrecord.
          MESSAGE i036.
        WHEN sended.
          MESSAGE i038.
        WHEN debug.
          MESSAGE i039.
        WHEN sysload.
          MESSAGE i052.
        WHEN retry.
          MESSAGE i500(sy).
        WHEN aretry.
          SUBMIT rstrfcds AND RETURN WITH tid  = acttid
                                     WITH fnum = 0.
        WHEN anoretry.
          SUBMIT rstrfcds AND RETURN WITH tid  = acttid
                                     WITH fnum = 0.
        WHEN OTHERS.
          MESSAGE i035.
      ENDCASE.

* Anzeige LUW-Daten
    WHEN OTHERS.
      IF valid_line = space  OR  acttid IS INITIAL.
        MESSAGE e015.
      ENDIF.
      SUBMIT rstrfcdt AND RETURN WITH tid = acttid
                                 WITH fnum = arfcsstate-arfcluwcnt.
  ENDCASE.
ENDFORM.                               " NAVIGATE

*&---------------------------------------------------------------------*
*&      Form  LANGTEXT
*&---------------------------------------------------------------------*
FORM langtext USING status TYPE any.

  CASE status.
    WHEN cpicerr.
      PERFORM get_t100 USING  'SR' '027' cpicerr.
    WHEN  sysfail.
      PERFORM get_t100 USING  'SR' '028' sysfail.
    WHEN confail.
      PERFORM get_t100 USING  'SR' '029' confail.
    WHEN executed.
      PERFORM get_t100 USING  'SR' '030' executed.
    WHEN mailed.
      PERFORM get_t100 USING  'SR' '031' mailed.
    WHEN read.
      PERFORM get_t100 USING  'SR' '032' read.
    WHEN recorded.
      PERFORM get_t100 USING  'SR' '033' recorded.
    WHEN vbrecord.
      PERFORM get_t100 USING  'SR' '034' vbrecord.
    WHEN vberror.
      PERFORM get_t100 USING  'SR' '079' vberror.
    WHEN vxrecord.
      PERFORM get_t100 USING  'SR' '036' vxrecord.
    WHEN sended.
      PERFORM get_t100 USING  'SR' '038' sended.
    WHEN debug.
      PERFORM get_t100 USING  'SR' '039' debug.
    WHEN sysload.
      PERFORM get_t100 USING  'SR' '052' sysload.
    WHEN retry.
      PERFORM get_t100 USING  'SY' '500' retry.
    WHEN aretry.
      PERFORM get_t100 USING  'SY' '504' aretry.
    WHEN anoretry.
      PERFORM get_t100 USING  'SY' '505' anoretry.
    WHEN space.
      PERFORM get_t100 USING  'SR' '033' recorded.
    WHEN OTHERS.
      PERFORM get_t100 USING  'SR' '035' 'UNKNOWN STATE'.
  ENDCASE.
  ASSIGN t100-text TO <m>.
ENDFORM.                    "langtext

*---------------------------------------------------------------------*
*       FORM GET_T100                                                 *
*---------------------------------------------------------------------*
FORM get_t100 USING arbgb      LIKE t100-arbgb
                    nombre     LIKE t100-msgnr
                    statustext TYPE any.

  SELECT SINGLE * FROM  t100
    WHERE sprsl = sy-langu
      AND arbgb = arbgb
      AND msgnr = nombre.
  IF sy-subrc <> 0.
    CLEAR t100-text.
    t100-text = statustext.
  ENDIF.

ENDFORM.                    "get_t100


*---------------------------------------------------------------------*
*       FORM batch_display                                            *
*---------------------------------------------------------------------*
FORM batch_display USING job TYPE any.

  DATA: user_wants_to_exit.

  DATA: BEGIN OF jobsel_params.
          INCLUDE STRUCTURE btcselect.
  DATA: END OF jobsel_params.

  DATA BEGIN OF sel_joblist OCCURS 100.
          INCLUDE STRUCTURE tbtcjob.
  DATA END OF sel_joblist.

  jobsel_params = job.
  WHILE user_wants_to_exit EQ space.
*
*   Jobselektionswerte vom Benutzer entgegennehmen und Jobs selektieren
*
    CALL FUNCTION 'BP_JOB_SELECT'
      EXPORTING
        jobselect_dialog   = 'N'
        jobsel_param_in    = jobsel_params
      IMPORTING
        jobsel_param_out   = jobsel_params
      TABLES
        jobselect_joblist  = sel_joblist
      EXCEPTIONS
        no_jobs_found      = 1
        selection_canceled = 2
        OTHERS             = 99.

    CASE sy-subrc.
      WHEN 0.
        user_wants_to_exit = 'X'.      " Jobliste erfolgreich erstellt
      WHEN 1.
        MESSAGE s151(bt).
        user_wants_to_exit = 'X'.      "FEHLER in 30: Endlosloop !
      WHEN 2.
        user_wants_to_exit = 'X'.
        CHECK 1 EQ 0.                  " continue
      WHEN OTHERS.
        user_wants_to_exit = 'X'.
    ENDCASE.
*
*   Joblistenprozessor im Editmodus aufrufen.
*
    CALL FUNCTION 'BP_JOBLIST_PROCESSOR'
      EXPORTING
        joblist_opcode             = 21
        joblist_refr_param         = jobsel_params
      TABLES
        joblist                    = sel_joblist
      EXCEPTIONS
        joblist_processor_canceled = 1
        OTHERS                     = 99.

    CASE sy-subrc.
      WHEN 0.
        " normal beendet
      WHEN 1.
        user_wants_to_exit = 'X'.
      WHEN OTHERS.
        user_wants_to_exit = 'X'.
    ENDCASE.
  ENDWHILE.

ENDFORM.                    "batch_display

*---------------------------------------------------------------------*
*      Module  SUPPRESS  OUTPUT
*---------------------------------------------------------------------*
MODULE suppress OUTPUT.
  SET PF-STATUS '100'.
  CASE sy-xcode.
    WHEN 'INFO'.
      SET TITLEBAR  '100'.
    WHEN 'SETI'.
      SET TITLEBAR  '101'.
  ENDCASE.
  SUPPRESS DIALOG.
ENDMODULE.                             " SUPPRESS  OUTPUT

*---------------------------------------------------------------------*
*      Module  PRINT  INPUT
*---------------------------------------------------------------------*
MODULE print INPUT.

  trys = %_arfc_tries.

  LEAVE TO LIST-PROCESSING.
  CASE sy-xcode.
    WHEN 'INFO'.
      SELECT COUNT(*) FROM arfcsdata
                               WHERE  arfcipid = acttid-arfcipid
                               AND    arfcpid  = acttid-arfcpid
                               AND    arfctime = acttid-arfctime
                               AND    arfctidcnt = acttid-arfctidcnt.
      size = sy-dbcnt * '1.800'.
      WRITE: / '    Gr##e des zu versendenden Packets '(013).
      ULINE.
      WRITE: / '    ca. '(014),size ,'KBytes'(015).
    WHEN 'SIZE'.
      SELECT COUNT(*) FROM arfcsdata.
      size = sy-dbcnt * '1.800'.
      WRITE: / '    Gr##e der Datei ARFCSDATA '(030).
      ULINE.
      WRITE: / '    ca. '(014),size ,'KBytes'(015).
    WHEN 'SETI'.
      WRITE: / 'Standardeinstellung:             '(020).
      ULINE.
      SKIP 1.
      WRITE: / 'Verbindungsversuche[Anz.]:'(021), trys.
      WRITE: / 'Versuchsabstand[Min.]:    '(022), %_arfc_retry_later.
    WHEN 'BACK'.
      SET SCREEN 0. LEAVE SCREEN.
    WHEN 'RW'.
      SET SCREEN 0. LEAVE SCREEN.
  ENDCASE.
ENDMODULE.                    "print INPUT

*---------------------------------------------------------------------*
*       FORM reset_luw_state
*---------------------------------------------------------------------*
FORM reset_luw_state .

  DATA: answer    TYPE c,
        register  LIKE sy-subrc,
        titel(62) TYPE c,
        text1(62) TYPE c,
        text2(62) TYPE c,
        istate    LIKE arfcsstate OCCURS 0 WITH HEADER LINE.

* TIMEOUT-Pr¨¹fung notwendig bei ACTIVATE oder RESTART
  MOVE 'Diese LUW wird m#glicherweise noch ausgef¨¹hrt.'(023) TO text1.
  MOVE 'Status unbedingt zur¨¹cksetzen?'(026) TO text2.
  MOVE 'LUW-Status zur¨¹cksetzen'(027) TO titel.

  CALL FUNCTION 'POPUP_TO_CONFIRM_STEP'
    EXPORTING
      defaultoption = 'N'
      textline1     = text1
      textline2     = text2
      titel         = titel
    IMPORTING
      answer        = answer.
  IF answer = 'J'.

* Aktuelle STATE holen
    SELECT * FROM arfcsstate
      INTO TABLE istate
      WHERE arfcipid   = acttid-arfcipid
        AND arfcpid    = acttid-arfcpid
        AND arfctime   = acttid-arfctime
        AND arfctidcnt = acttid-arfctidcnt.
    CHECK sy-dbcnt <> 0.
    SORT istate BY arfcdest arfcluwcnt.

* Eintr#ge im Status EXECUTED l#schen
    READ TABLE istate INDEX 1.
    IF istate-arfcstate = executed.
      CALL FUNCTION 'TRFC_QOUT_DELETE_LUW'
        EXPORTING
          tid    = acttid
        EXCEPTIONS
          OTHERS = 0.
    ELSE.
* Status auf RECORDED zur¨¹cksetzen
      LOOP AT istate.
        istate-arfcstate = recorded.
        %_reserv = istate-arfcreserv.
        CLEAR: istate-arfcmsg, %_reserv-arfcmsg2.
        istate-arfcreserv = %_reserv.
        MODIFY istate.
      ENDLOOP.

* Evtl. Batchjob stornieren
      PERFORM delete_batchjob TABLES istate.

* Update Status
      UPDATE arfcsstate FROM TABLE istate.

      CALL FUNCTION 'DB_COMMIT'.

      CALL FUNCTION 'QDEST_CHECK_REGISTER'
        EXPORTING
          dest     = arfcsstate-arfcdest
        IMPORTING
          register = register.
      IF register <> 0.
        CALL FUNCTION 'QDEST_SCHEDULER_ACTIVATE'.
      ENDIF.
    ENDIF.
  ENDIF.

ENDFORM.                    "reset_luw_state

*---------------------------------------------------------------------*
*       FORM delete_batchjob
*---------------------------------------------------------------------*
FORM delete_batchjob TABLES istate STRUCTURE arfcsstate.

  DATA: jobname LIKE tbtco-jobname.

  DATA: jobtab LIKE tbtco OCCURS 0 WITH HEADER LINE.

* Pr¨¹fen, ob Batchjob vorhanden ist.
  IF sy-batch <> space. EXIT. ENDIF.
  READ TABLE istate INDEX 1.
  %_reserv = istate-arfcreserv.
  CHECK %_reserv-batchpla <> space.

* Wenn JA, Merker hierf¨¹r l#schen
  LOOP AT istate.
    %_reserv = istate-arfcreserv.
    %_reserv-batchpla = space.
    istate-arfcreserv = %_reserv.
    MODIFY istate.
  ENDLOOP.

* Batchjob stornieren
  READ TABLE istate INDEX 1.
  CONCATENATE 'ARFC:' istate-arfcipid istate-arfcpid
                      istate-arfctime istate-arfctidcnt
                 INTO jobname.
  SELECT * FROM tbtco
    INTO TABLE jobtab
    WHERE jobname = jobname.
  LOOP AT jobtab.
    CALL FUNCTION 'BP_JOB_DELETE'
      EXPORTING
        forcedmode = 'X'
        jobcount   = jobtab-jobcount
        jobname    = jobtab-jobname
      EXCEPTIONS
        OTHERS     = 0.
  ENDLOOP.

ENDFORM.                    "delete_batchjob

*Text symbol text£º
*001:Caller
*002:Function Module
*003:Target System
*004:Date
*005:Time
*006:Status Text
*007:Transaction ID
*008:Rpts
*009:Nothing was selected
*010:Delete log file?
*011:Delete
*012:Clnt
*013:    Size of packet to be sent
*014:  about
*015:KB
*016:Host
*017:Tctn
*018:Program
*020:Standard Setting:
*021:Connection Attempts [No.]:
*022:Attempt Interval [Mins]:
*023:This LUW is possibly still being executed.
*026:Definitely reset status?
*027:Reset LUW Status
*030:    Size of File ARFCSDATA
*065:Remote System
*066:Invalid destination
*067:No authorization for SM58
*068:SM58 does not exist in target

*069:Unexpected error when starting SM58
*Selection text£º
*BENUTZER:        User Name
*DEST:        TRFC Destination
*FUNCTION:        TRFC Function
*STATUS:        TRFC Status
*ZEITRAUM:        Display Period
