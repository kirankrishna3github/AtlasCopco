*&---------------------------------------------------------------------*
*& Report  Z_CSKB_OVERLAP
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*& Report  Z_CSKB_OVERLAP                                              *
*& correction report for overlapping cost element master records       *
*&---------------------------------------------------------------------*

REPORT  Z_CSKB_OVERLAP LINE-SIZE 250 NO STANDARD PAGE HEADING.

INCLUDE <icon>.
PARAMETERS: p_ktopl LIKE cska-ktopl MEMORY ID kpl,
            p_test LIKE kreo-testlauf DEFAULT 'X'.
DATA: gt_kokrs_cskb  LIKE cskb-kokrs  OCCURS 0 WITH HEADER LINE.
DATA: CURSORFIELD(20),
      gd_mindate TYPE date VALUE '19000101'.
DATA: end_of_line LIKE sy-subrc.
DATA: BEGIN OF gt_tab OCCURS 0,
        kokrs LIKE tka01-kokrs,
      END OF gt_tab.
DATA: gt_cskb LIKE cskb OCCURS 0 WITH HEADER LINE.
TYPES: BEGIN OF tgs_bug.
        INCLUDE STRUCTURE cskb.
TYPES:  flag TYPE flag.
TYPES: END OF tgs_bug.
TYPES: tgt_bug TYPE tgs_bug OCCURS 0.
DATA:  gt_bug TYPE tgt_bug,
       gt_del TYPE tgt_bug,
       gs_bug TYPE tgs_bug,
       gd_kst TYPE flag,
       gd_all TYPE sy-tabix,
       gd_del TYPE sy-tabix.

*********************TOP-OF-PAGE*******************


TOP-OF-PAGE.
  WRITE: / 'Run for: Test', p_test AS CHECKBOX INPUT OFF , ',' ,
           'inconsistencies for chart of accounts:',
            p_ktopl COLOR COL_HEADING INTENSIFIED ON.

  ULINE AT /(95).
  WRITE: / '   |',  'Cost elem.' COLOR COL_HEADING INTENSIFIED ON,
           '|', 'Valid-from' COLOR COL_HEADING INTENSIFIED ON, '-',
              'Valid to  ' COLOR COL_HEADING INTENSIFIED ON, '|',
              'CE category' COLOR COL_HEADING INTENSIFIED ON, '|',
              'Created on' COLOR COL_HEADING INTENSIFIED ON, '|',
              'Entered by  ' COLOR COL_HEADING INTENSIFIED ON, '|' ,
              'CO area' COLOR COL_HEADING INTENSIFIED ON, '|'.

*********************START-OF-SELECTION*******************
START-OF-SELECTION.
  PERFORM delete_space.

* find all CO areas from TKA01 with selected chart of accounts
  SELECT kokrs FROM tka01 INTO TABLE gt_tab WHERE ktopl = p_ktopl.
  IF NOT gt_tab[] IS INITIAL.
    SORT gt_tab[].
  ELSE.
    NEW-PAGE NO-TOPOFPAGE.
    WRITE: / 'no controlling areas with chart of accounts:',
              p_ktopl COLOR COL_HEADING INTENSIFIED ON.
    EXIT.
  ENDIF.

* find all CO areas with selected chart of accounts and existing
* cost elements
  SELECT DISTINCT kokrs FROM cskb INTO TABLE gt_kokrs_cskb
                                  FOR ALL ENTRIES IN gt_tab[]
                                  WHERE kokrs = gt_tab-kokrs.
  SORT gt_kokrs_cskb.

* find inconsistencies for each controlling area
  LOOP AT gt_kokrs_cskb.
    PERFORM find_overlaps USING gt_bug gt_kokrs_cskb.
  ENDLOOP.

  IF NOT gt_bug[] IS INITIAL.
    PERFORM overlapping_show USING gt_bug.
  ELSE.
    NEW-PAGE NO-TOPOFPAGE.
    WRITE: / 'no inconsistencies for chart of accounts:',
              p_ktopl COLOR COL_HEADING INTENSIFIED ON.
  ENDIF.
*&---------------------------------------------------------------------*
*&      Form  find_overlaps
*&---------------------------------------------------------------------*
FORM find_overlaps USING pt_bug TYPE tgt_bug
                         p_kokrs LIKE csks-kokrs.

  DATA: ls_bug  TYPE tgs_bug,
        lt_cskb TYPE tgt_bug,
        ls_save TYPE tgs_bug,
        lt_bug  TYPE tgt_bug.

  SELECT * FROM cskb INTO TABLE lt_cskb WHERE kokrs = p_kokrs
                                ORDER BY PRIMARY KEY.
  CHECK sy-subrc = 0.

* find overlaps
  READ TABLE lt_cskb INTO ls_save INDEX 1.
  IF ls_save-datab > ls_save-datbi OR         " datab > datbi
     ls_save-datab IS INITIAL      OR
     ls_save-datbi IS INITIAL      OR
     ls_save-datab < gd_mindate    OR
     ls_save-datbi < gd_mindate.
    APPEND ls_save TO lt_bug.
  ENDIF.
  LOOP AT lt_cskb INTO ls_bug FROM 2.
    IF ls_save-kstar = ls_bug-kstar AND
      ls_save-datbi >= ls_bug-datab.
      APPEND ls_save TO lt_bug.
      APPEND ls_bug  TO lt_bug.
    ENDIF.
    IF ls_bug-datab > ls_bug-datbi   OR        " datab > datbi
       ls_bug-datab   IS INITIAL     OR
       ls_bug-datbi   IS INITIAL     OR
       ls_bug-datab  < gd_mindate    OR
       ls_bug-datbi  < gd_mindate.

        APPEND ls_bug  TO lt_bug.
    ENDIF.
    ls_save = ls_bug.
  ENDLOOP.

  SORT lt_bug.
  DELETE ADJACENT DUPLICATES FROM lt_bug.
  CHECK NOT lt_bug[] IS INITIAL.
  APPEND LINES OF lt_bug TO pt_bug.

ENDFORM.                    " find_overlaps
*&---------------------------------------------------------------------*
*&      Form  overlapping_show
*&---------------------------------------------------------------------*
FORM overlapping_show USING pt_bug TYPE tgt_bug.

  DATA: ls_bug TYPE tgs_bug,
        ls_hlp TYPE tgs_bug,
        ls_sve TYPE tgs_bug.

  LOOP AT pt_bug INTO ls_bug.

    ls_hlp = ls_bug.
    AT NEW kstar .
      ULINE AT /(95).
      CLEAR ls_sve-kstar.
    ENDAT.

    IF ls_hlp-kstar <> ls_sve-kstar.
      FORMAT HOTSPOT.
      WRITE AT /1 icon_okay AS ICON
                  QUICKINFO 'Select an interval that you want delete'.
      FORMAT HOTSPOT OFF.

      WRITE: '|',  ls_hlp-kstar COLOR COL_NORMAL INTENSIFIED OFF,
             '|', ls_hlp-datab, '-', ls_hlp-datbi, '|     ',
              ls_hlp-katyp , '    |',
              ls_hlp-ersda UNDER 'Created on', '|',
              ls_hlp-usnam UNDER 'Entered by  ', '| ',
              ls_hlp-kokrs COLOR COL_NORMAL INTENSIFIED OFF, '  |'.
      ls_sve-kstar = ls_hlp-kstar.
      gs_bug = ls_hlp.
      HIDE: gs_bug-kokrs, gs_bug-kstar, gs_bug-datbi, gs_bug-flag.
      gd_kst = 'K'. HIDE gd_kst.
      CONTINUE.
    ENDIF.
    ls_sve-kstar = ls_hlp-kstar.
    FORMAT HOTSPOT.
    WRITE AT /1 icon_okay AS ICON
                QUICKINFO 'Select interval that you want delete'.
    FORMAT HOTSPOT OFF.
    WRITE: '|',  ls_hlp-kstar UNDER ls_hlp-kstar, '|',
             ls_hlp-datab UNDER ls_hlp-datab, '-',
             ls_hlp-datbi UNDER ls_hlp-datbi, '|     ',
             ls_hlp-katyp , '    |',
             ls_hlp-ersda UNDER 'Created on', '|',
             ls_hlp-usnam UNDER 'Entered by  ', '| ',
             ls_hlp-kokrs , '  |'.
    gs_bug = ls_hlp.
    HIDE: gs_bug-kokrs, gs_bug-kstar, gs_bug-datbi, gs_bug-flag.
    gd_kst = 'K'. HIDE gd_kst.
  ENDLOOP.
  ULINE AT /(95).
  CLEAR gs_bug.

ENDFORM.                    " overlapping_show

************************ user command *************************
AT USER-COMMAND.
  REFRESH gt_del.
  IF sy-ucomm = 'DELETE'.
    DO.
      CLEAR gd_kst.
      READ LINE sy-index.
      end_of_line = sy-subrc.
      IF gd_kst = 'K'.
        ADD 1 TO gd_all.
      ENDIF.
      IF sy-lisel(2) = '11'.
        ADD 1 TO gd_del.
        gs_bug-mandt = sy-mandt.
        APPEND gs_bug TO gt_del.
      ENDIF.
********************
      IF gd_all > 0 AND gd_kst IS INITIAL.
        IF gd_del = gd_all AND gd_del > 1 AND gd_all > 1 .
          DELETE gt_del WHERE kokrs = gs_bug-kokrs
                        AND   kstar = gs_bug-kstar.
         WRITE: / 'you cannot delete all records for one cost element:',
                   gs_bug-kokrs COLOR 1, '/', gs_bug-kstar COLOR 2.
          CLEAR: gd_del, gd_all.

        ELSEIF gd_del = gd_all AND gd_del = 1 AND gd_all = 1 .
          sy-lsind = 0.
          REFRESH gt_cskb.
          SELECT * FROM cskb INTO TABLE gt_cskb
                             WHERE kokrs = gs_bug-kokrs AND
                                   kstar = gs_bug-kstar.
          DESCRIBE TABLE gt_cskb LINES sy-tabix.
          IF sy-tabix = 1.                        "One entry in cskb
            READ TABLE gt_cskb INTO gs_bug INDEX 1.
            IF gs_bug-datab > gs_bug-datbi OR
               gs_bug-datab IS INITIAL      OR
               gs_bug-datbi IS INITIAL      OR
               gs_bug-datab < gd_mindate    OR
               gs_bug-datbi < gd_mindate.

            gs_bug-datab = '19000101'. gs_bug-datbi = '99991231'.
         WRITE: / 'you cannot delete all records for one cost element:',
            gs_bug-kstar COLOR 2, 'But because Valid-from > Valid to ,'.
         WRITE: / 'Valid-from will be updated with date = ',
                  gs_bug-datab COLOR 2,
                  'and Valid-to will be updated with date = ',
                  gs_bug-datbi COLOR 2.

              UPDATE cskb SET   datab = '19000101'      " datab > datbi
                                datbi = '99991231'
                          WHERE kokrs = gs_bug-kokrs
                          AND   kstar = gs_bug-kstar.
              DELETE gt_del WHERE kokrs = gs_bug-kokrs
                            AND   kstar = gs_bug-kstar.
              CLEAR: gd_del, gd_all.
            ENDIF.
          ELSEIF sy-tabix > 1.
            CLEAR: gd_del, gd_all.
          ENDIF.
        ELSE.
          CLEAR: gd_del, gd_all.
        ENDIF.
      ENDIF.
      CHECK end_of_line <> 0.
      EXIT.
    ENDDO.

    sy-lsind = 0.
    CHECK NOT gt_del[] IS INITIAL.
    DELETE cskb FROM TABLE gt_del.
    SORT gt_del.
    WRITE: / sy-dbcnt COLOR COL_POSITIVE ,
            'inconsistent records deleted'.
    COMMIT WORK.
  ENDIF.                                        .
*&---------------------------------------------------------------------*
*&      Form  delete_space
*&---------------------------------------------------------------------*
FORM delete_space .
  DELETE FROM cskb WHERE kstar = space.
  DELETE FROM cska WHERE kstar = space.
  DELETE FROM csku WHERE kstar = space.
ENDFORM.                    " delete_space

****************************AT LINE-SELECTION******************
AT LINE-SELECTION.
GET CURSOR FIELD CURSORFIELD.
  IF sy-pfkey = 'INLI' AND  sy-ucomm = 'PICK'
                       AND CURSORFIELD = 'ICON_OKAY'.
    sy-lsind = 1.

    IF sy-lisel(2) = '11' AND p_test IS INITIAL.
      sy-lisel(2) = '0V'.        " ICON_OKAY
    ELSEIF sy-lisel(2) = '0V' AND p_test IS INITIAL.
      sy-lisel(2) = '11'.        " ICON_DELETE
    ENDIF.

    MODIFY CURRENT LINE .
  ENDIF.

*Selection text£º
*P_KTOPL:D       .
*P_TEST:D       .
