*&---------------------------------------------------------------------*
*&  Include           ZSQLEXPLORERO01
*&---------------------------------------------------------------------*
************************************************************************
*^ Written By      : Tom Yang
*^ Date Written    : 2006/12/12
*^ Include Name    : ZSQLEXPLORERO01
*^ Used in Programs: <Programs referencing this include>
*^ Purpose         : To define parameters and varants
*
*^ Other           :
************************************************************************


************************************************************************
*&      Module  USER_COMMAND_0200  INPUT
************************************************************************
MODULE user_command_0200 INPUT.

  CASE gv_ucomm .

    WHEN 'CONF'.
      CASE 'X'.
        WHEN gv_case_01 .
          gv_case = 1 .
        WHEN gv_case_02 .
          gv_case = 2 .
        WHEN gv_case_03 .
          gv_case = 3 .
      ENDCASE .

      CASE 'X'.
        WHEN gv_label_01 .
          gv_label = 1 .
        WHEN gv_label_02 .
          gv_label = 2 .
      ENDCASE .

    WHEN 'CANC'.
  ENDCASE .

  CLEAR : gv_ucomm .

ENDMODULE.                 " USER_COMMAND_0200  INPUT

******************************************************
*&      Module  STATUS_0200  OUTPUT
******************************************************
MODULE status_0200 OUTPUT.

SET PF-STATUS ''.

ENDMODULE.                 " STATUS_0200  OUTPUT



******************************************************
*&      Form  Show_Message
******************************************************
FORM show_message USING p_message TYPE string .

  CALL FUNCTION 'POPUP_TO_INFORM'
    EXPORTING
      titel = gv_repid
      txt2  = space
      txt1  = p_message.

ENDFORM.                    "Show_Message


************************************************************************
*&      Module  USER_COMMAND_0100  INPUT
************************************************************************
MODULE user_command_0100 INPUT.
  DATA : lv_start    TYPE  i ,
         lv_end      TYPE  i ,
         lt_temp    TYPE  ty_t_text ,
         lv_number   TYPE  i .

*& Initial Serval Global Variant
  CLEAR : gt_exception. " lv_number .

*& Keep Start Time
  GET RUN TIME FIELD lv_start.


*& Processing User Command
  CASE gv_ucomm.

    WHEN 'EXIT' OR 'BACK' OR 'BREAK'.
      PERFORM exit_program.

    WHEN 'EXEC'.
      PERFORM save_text_to_table       USING go_editor1
                                    CHANGING gt_text.
      PERFORM execute_sql              USING gt_text
                                    CHANGING lv_number.
      gv_msg = 'Execute SQL ON & & & &'.
      REPLACE FIRST OCCURRENCE OF '&' in gv_msg WITH sy-DATLO.
      REPLACE FIRST OCCURRENCE OF '&' in gv_msg WITH sy-timlo.
      message s001(00) WITH gv_msg
        INTO gv_msg.
      PERFORM add_message_to_log.
    WHEN 'FORM'.
      PERFORM save_text_to_table       USING go_editor1
                                    CHANGING gt_text.

      PERFORM format_sql            CHANGING gt_text.

      PERFORM set_text_as_r3table      USING go_editor1
                                    CHANGING gt_text.

    WHEN 'BUDG'.
      PERFORM save_text_to_table       USING go_editor1
                                    CHANGING gt_text.

      PERFORM bud_sturcture            USING gt_text
                                    CHANGING lt_temp .

      PERFORM set_text_as_r3table      USING go_editor
                                    CHANGING lt_temp   .

    WHEN 'SAVE'.
      IF gv_file IS INITIAL .
        PERFORM get_filename CHANGING gv_file .
      ENDIF .

      CHECK gv_file IS NOT INITIAL AND sy-subrc = 0 .
      PERFORM save_as_local_file USING go_editor
                                       gv_file  .


    WHEN 'MOVE'.
      PERFORM move_splitter_postion    USING go_splitter1 .

    WHEN 'CONF'.
      CALL SCREEN 200 STARTING AT 30 5 .

    WHEN 'LOG'.
      PERFORM display_APP_LOG.

  ENDCASE.

*& Set Runtime
  GET RUN TIME FIELD lv_end.

  PERFORM set_runtime_and_lines USING lv_start
                                      lv_end
                                      lv_number.



  CLEAR : gv_ucomm .


ENDMODULE.                 " USER_COMMAND_0100  INPUT

************************************************************************
*&      Module  Initial_Screen  OUTPUT
************************************************************************
MODULE initial_screen OUTPUT.
  DATA : lv_orientation  TYPE  i  ,
         lv_mode         TYPE  i  .

  CHECK go_editor IS INITIAL.

  gv_repid = sy-repid.

*& Create Control Container
  PERFORM create_container_object USING go_container
                                        c_sql_editor.

  lv_orientation  = 1 .
  PERFORM create_splitter_object  USING go_splitter
                                        go_container
                                        lv_orientation .

  PERFORM create_editor_object    USING go_editor
                                        go_splitter->bottom_right_container .

  lv_orientation  = 0 .
  PERFORM create_splitter_object  USING go_splitter1
                                        go_splitter->top_left_container
                                        lv_orientation.


*& Create Text Editor
  PERFORM create_editor_object USING go_editor1
                                     go_splitter1->top_left_container .

*& Create ALV
  PERFORM create_alv_object    USING go_grid
                                     go_splitter1->bottom_right_container.


  IF sy-subrc NE 0.
    PERFORM show_message USING c_msg_te-m01 .
  ENDIF.

*& Set SQL Editor Support Highlight Comments
  PERFORM set_comment_mode USING go_editor1 .
  PERFORM set_comment_mode USING go_editor.

*& Set Editor Read Only
  lv_mode  =  1 .
  PERFORM set_readonly_mode USING go_editor
                                  lv_mode  .

ENDMODULE.                 " Initial_Screen  OUTPUT

************************************************************************
*&      Module  status_0100  OUTPUT
************************************************************************
MODULE status_0100 OUTPUT.

  SET PF-STATUS 'SCREEN100'.
  SET TITLEBAR  'TITLE100'.

ENDMODULE.                 " status_0100  OUTPUT
