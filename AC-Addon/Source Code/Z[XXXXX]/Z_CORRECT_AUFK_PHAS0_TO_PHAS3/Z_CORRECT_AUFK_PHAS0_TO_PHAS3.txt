REPORT  Z_CORRECT_AUFK_PHAS0_TO_PHAS3.

  TABLES: AUFK, AFIH, T003O.

  DATA: lt_aufk_upd LIKE STANDARD TABLE OF AUFK WITH HEADER LINE,
        ls_aufk_upd LIKE lt_aufk_upd,
        ls_aufk_old LIKE lt_aufk_upd.
  DATA: BEGIN OF lt_afih OCCURS 0,
           aufnr LIKE AFIH-aufnr,
           iphas LIKE AFIH-iphas,
        END OF lt_afih,
        ls_afih LIKE lt_afih.
  DATA: BEGIN OF lt_t003o OCCURS 0,
           auart LIKE T003O-auart,
        END OF lt_t003o.
  DATA: BEGIN OF l_nriv OCCURS 0.
              INCLUDE STRUCTURE NRIV.
  DATA: END OF l_nriv.
  DATA: dynpro_value TYPE TABLE OF dynpread,
        field_value  LIKE LINE  OF dynpro_value.
  DATA: l_numkr LIKE T003O-numkr.
  DATA: tmp_tabix LIKE SY-tabix.
  DATA: l_prog    LIKE SY-repid.
  DATA: l_dynp    LIKE SY-dynnr.
  DATA: counter_update  TYPE I,
        counter_skip    TYPE I VALUE 6.

  DATA: BEGIN OF ls_string,
          phas0 LIKE AUFK-phas0,
          phas1 LIKE AUFK-phas1,
          phas2 LIKE AUFK-phas2,
          phas3 LIKE AUFK-phas3,
        END OF ls_string,
        string TYPE STRING.
  DATA: l_len  TYPE I.

  SELECTION-SCREEN COMMENT /1(80) TEXT1.
  SELECTION-SCREEN COMMENT /1(80) TEXT2.
  SELECTION-SCREEN COMMENT /1(80) TEXT3.
  SELECTION-SCREEN COMMENT /1(80) TEXT4.

  SELECTION-SCREEN ULINE.
  SELECTION-SCREEN COMMENT /1(80) TEXT5.
  SELECTION-SCREEN COMMENT /1(80) TEXT6.
  SELECTION-SCREEN ULINE.

  PARAMETERS:     test AS CHECKBOX DEFAULT 'X'.

  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT (33) TEXT_1 FOR FIELD auart.
    SELECTION-SCREEN POSITION 38.
    PARAMETERS auart LIKE AUFK-AUART.
  SELECTION-SCREEN END OF LINE.

  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT (22) TEXT_2 FOR FIELD auart.
    SELECTION-SCREEN POSITION POS_LOW.
    SELECT-OPTIONS aufnr FOR AUFK-AUFNR.
  SELECTION-SCREEN END OF LINE.
  SELECTION-SCREEN COMMENT /1(80) TEXT_3.

  SELECTION-SCREEN SKIP 1.

  INITIALIZATION.
  TEXT1 = 'Dear Customer.'.
  TEXT2 = 'You are just running a correction report.'.
  TEXT3 = 'This report has been specifically developed for the '
          & 'symptom described in'.
  TEXT4 = 'note 1226354.'.
  TEXT5 = 'Please check wether this symptom actually applies to '
            & 'your case before starting.'.
  TEXT6 = 'In case of doubt please contact SAP.'.
  TEXT_1 = 'Enter Order-Type and press ENTER:'.
  TEXT_2 = 'Enter Order-Number(s):'.
  TEXT_3 = 'If BLANK, complete numberrange for specified Order-Type '
            & 'is set.'.

  START-OF-SELECTION.

* Display in result list, if Test- oder Update-Mode
  IF test IS INITIAL.
     WRITE: / 'Mode.........: Update-Run'.
  ELSE.
     WRITE: / 'Mode.........: Test-Run'.
  ENDIF.

* Skip to line 5,...line 4 reserved for number of found records
  SKIP TO LINE 5.
  ULINE.

* To reduce runtime, selection criterias must be exist
  IF auart IS INITIAL. "AND aufnr IS INITIAL.
     SKIP TO LINE 6.
     WRITE: / 'Please enter Order Type to reduce runtime.'.
     EXIT.
  ENDIF.

* BEGIN OF STEP 1:-----------------------------------------------------*
* Check if given orders from selection screen are in valid number range
* 1.1: read number range number from given AUART:
   SELECT SINGLE numkr FROM T003O INTO l_numkr
                      WHERE autyp = '30'
                      AND   auart = auart.
  IF SY-subrc IS INITIAL.
*    1.2: read number range (fromnumber, tonumber)
     SELECT * FROM NRIV INTO CORRESPONDING FIELDS OF TABLE l_nriv
                        WHERE object    = 'AUFTRAG'
                        AND   nrrangenr = l_numkr.
     IF l_nriv[] IS INITIAL.
        SKIP TO LINE 6.
        WRITE: / 'No numberrange defined for ordertype', auart.
        EXIT.
     ENDIF.
     READ TABLE l_nriv INDEX 1.

*    1.3  check, if given order numbers are in number range
     IF aufnr-low  IS INITIAL AND aufnr-high IS INITIAL.
*    ...take l_nriv-fromnumber and l_nriv-tonumber....
     ELSEIF aufnr-high IS INITIAL.
        SKIP TO LINE 6.
        LOOP AT aufnr.
             tmp_tabix = SY-tabix.
             IF NOT aufnr-low BETWEEN l_nriv-fromnumber
                              AND     l_nriv-tonumber.
                DELETE aufnr INDEX tmp_tabix.
                ADD 1 TO counter_skip.
                WRITE: / 'Ordernumber', AT 13 aufnr-low,
                         AT 26 'is not in numberrange of ordertype',
                         AT 61 auart, AT 66 '->No update possible'.
             ENDIF.
        ENDLOOP.
        IF aufnr[] IS INITIAL.
           EXIT.
        ENDIF.
     ELSEIF aufnr-low IS INITIAL.
        SKIP TO LINE 6.
        LOOP AT aufnr.
             tmp_tabix = SY-tabix.
             IF NOT aufnr-high BETWEEN l_nriv-fromnumber
                               AND     l_nriv-tonumber.
                DELETE aufnr INDEX tmp_tabix.
                ADD 1 TO counter_skip.
                WRITE: / 'Ordernumber', AT 13 aufnr-high,
                         AT 26 'is not in numberrange of ordertype',
                         AT 61 auart.
             ENDIF.
        ENDLOOP.
        IF aufnr[] IS INITIAL.
           EXIT.
        ENDIF.
     ELSE.
        SKIP TO LINE 6.
        LOOP AT aufnr.
             tmp_tabix = SY-tabix.
             IF NOT aufnr-low  BETWEEN l_nriv-fromnumber
                               AND l_nriv-tonumber
                OR
                NOT aufnr-high BETWEEN l_nriv-fromnumber
                               AND     l_nriv-tonumber.
                DELETE aufnr INDEX tmp_tabix.
                ADD 1 TO counter_skip.
                WRITE: / 'Orders', AT 10 aufnr-low,
                         AT 22 'to', AT 26 aufnr-high,
                         AT 38 'are not in numberrange of ordertype',
                         At 75 auart.
              ENDIF.
        ENDLOOP.
        IF aufnr[] IS INITIAL.
           EXIT.
        ENDIF.
     ENDIF.
  ELSE.
     SKIP TO LINE 6.
     WRITE: / 'Ordertype', auart, 'not of type MAINTENANCE.',
            / 'Please use F4 for valid values.'.
     EXIT.
  ENDIF.
* O.K., order numbers of selection screen are in valid number range
* END OF STEP 1--------------------------------------------------------*

* BEGIN OF STEP 2:-----------------------------------------------------*
* 2.1 Read orders from selection screen, but only AUTYP = '30'
* and fill UPDATE-table lt_aufk_upd
  IF aufnr[] IS INITIAL.
     SELECT * FROM AUFK INTO TABLE lt_aufk_upd
                             WHERE aufnr BETWEEN l_nriv-fromnumber
                             AND                 l_nriv-tonumber
                             AND   auart = auart
                             AND   autyp = '30'
                             AND   loekz = SPACE
                             AND ( phas0 <> SPACE
                                OR phas1 <> SPACE
                                OR phas2 <> SPACE
                                OR phas3 <> SPACE ).
  ELSE.
     SELECT * FROM AUFK INTO TABLE lt_aufk_upd
                             WHERE aufnr IN aufnr
                             AND   auart = auart
                             AND   autyp = '30'
                             AND   loekz = SPACE
                             AND ( phas0 <> SPACE
                                OR phas1 <> SPACE
                                OR phas2 <> SPACE
                                OR phas3 <> SPACE ).
  ENDIF.
  IF lt_aufk_upd[] IS INITIAL.
    WRITE: / 'No orders found.'(001).
    EXIT.
  ENDIF.
  SORT lt_aufk_upd BY aufnr.

* 2.2 select orders from AFIH
  SELECT aufnr iphas FROM AFIH INTO TABLE lt_afih
                     FOR ALL ENTRIES IN lt_aufk_upd
                     WHERE aufnr = lt_aufk_upd-aufnr.

  IF lt_afih[] IS INITIAL.
    WRITE: / 'No orders found.'(002).
    EXIT.
  ENDIF.

*  IF counter_skip > 6.
*     ADD 1 TO counter_skip.
*  ENDIF.
*  SKIP TO LINE counter_skip.

SKIP TO LINE 8.
* 2.3 Fill lt_aufk_upd-phas0 to lt_aufk_upd-phas3 from AFIH-iphas
  LOOP AT lt_aufk_upd INTO ls_aufk_upd.
       tmp_tabix = SY-tabix.
       MOVE-CORRESPONDING ls_aufk_upd TO ls_string.
       string = ls_string.
       CONDENSE string NO-GAPS.
       l_len = STRLEN( string ).
       CASE l_len.
         WHEN 1.
           DELETE lt_aufk_upd INDEX tmp_tabix.
           CONTINUE.
         WHEN 2 OR 3 OR 4.
           READ TABLE lt_afih WITH KEY aufnr = ls_aufk_upd-aufnr
                              INTO ls_afih.
           IF SY-subrc IS INITIAL.
              ls_aufk_old = ls_aufk_upd.
              CASE ls_afih-iphas.
                WHEN '0'.
                  ls_aufk_upd-phas0 = 'X'.
                  CLEAR: ls_aufk_upd-phas1,
                         ls_aufk_upd-phas2,
                         ls_aufk_upd-phas3,
                         ls_aufk_upd-idat1.
                WHEN '2'.
                  ls_aufk_upd-phas1 = 'X'.
                  CLEAR: ls_aufk_upd-phas0,
                         ls_aufk_upd-phas2,
                         ls_aufk_upd-phas3.
                WHEN '3'.
                  ls_aufk_upd-phas2 = 'X'.
                  CLEAR: ls_aufk_upd-phas0,
                         ls_aufk_upd-phas1,
                         ls_aufk_upd-phas3,
                         ls_aufk_upd-idat1.
                WHEN '6'.
                  ls_aufk_upd-phas3 = 'X'.
                  CLEAR: ls_aufk_upd-phas0,
                         ls_aufk_upd-phas1,
                         ls_aufk_upd-phas2,
                         ls_aufk_upd-idat1.
              ENDCASE.
              MODIFY lt_aufk_upd INDEX tmp_tabix FROM ls_aufk_upd.
              ADD 1 TO counter_update.
              WRITE: / ls_aufk_upd-aufnr,
                 AT 17 ls_aufk_old-phas0, AT 24 ls_aufk_old-phas1,
                 AT 31 ls_aufk_old-phas2, AT 38 ls_aufk_old-phas3,
                 AT 43 ls_aufk_old-idat1,
                 AT 57 ls_aufk_upd-phas0, AT 64 ls_aufk_upd-phas1,
                 AT 71 ls_aufk_upd-phas2, AT 78 ls_aufk_upd-phas3,
                 AT 83 ls_aufk_upd-idat1.
           ELSE.
             DELETE lt_aufk_upd INDEX tmp_tabix.
             CONTINUE.
           ENDIF.
       ENDCASE.
  ENDLOOP.

  IF lt_aufk_upd[] IS INITIAL.
    SKIP TO LINE 6.
    WRITE: / 'No records for update found in table AUFK',
           / 'Nothing to do...bye.'.
    EXIT.
  ENDIF.
* Now phas0 to phas3 are assigned to lt_aufk_upd
* END OF STEP 2--------------------------------------------------------*

* BEGIN OF STEP 3:-----------------------------------------------------*
* 3.1 Update AUFK and write result list
  IF test IS INITIAL.
     UPDATE AUFK FROM TABLE lt_aufk_upd.
  ENDIF.

* 3.2 Write number of updated records
  SKIP TO LINE 4.
  IF test IS INITIAL.
     WRITE: / 'Records successfully updated:', counter_update.
  ELSE.
     WRITE: / 'Records for update:', counter_update.
  ENDIF.
* END OF STEP 3--------------------------------------------------------*

  SKIP TO LINE 6.
  FORMAT COLOR COL_KEY.
  WRITE: / 'AUFNR',
     AT 15 'PHAS0', AT 22 'PHAS1', AT 29 'PHAS2', AT 36 'PHAS3',
     AT 43 'IDAT1_old',
     AT 55 'PHAS0', AT 62 'PHAS1', AT 69 'PHAS2', AT 76 'PHAS3',
     AT 83 'IDAT1_new '.
  SKIP TO LINE 7.
  WRITE:  AT 16 'old', AT 23 'old', AT 30 'old', AT 37 'old',
          AT 56 'new', AT 63 'new', AT 70 'new', AT 77 'new',
          AT 80 '             '.
  FORMAT COLOR COL_NORMAL.


*&---------------------------------------------------------------------*
* Provide F4-values
*&---------------------------------------------------------------------*
* F4-values for PM order types
  AT SELECTION-SCREEN ON VALUE-REQUEST FOR AUART.
  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
       EXPORTING
           retfield    = 'AUART'
           dynpprog    = l_prog
           dynpnr      = l_dynp
           dynprofield = 'AUART'
           value_org   = 'S'
       TABLES
           value_tab   = lt_t003o.

* F4-values for number ranges of given AUART
* a) low-value of given AUART
  AT SELECTION-SCREEN ON VALUE-REQUEST FOR aufnr-low.
  CALL FUNCTION 'DYNP_VALUES_READ'
       EXPORTING
           DYNAME                   = l_prog
           DYNUMB                   = l_dynp
      TABLES
           DYNPFIELDS               = dynpro_value.

  READ TABLE dynpro_value INDEX 1 INTO field_value.
  READ TABLE lt_t003o WITH KEY auart = field_value-fieldvalue.
  SELECT SINGLE numkr FROM T003O INTO l_numkr
                      WHERE autyp = '30'
                      AND   auart = lt_t003o-auart.

  SELECT * FROM NRIV INTO CORRESPONDING FIELDS OF TABLE l_nriv
                 WHERE object    = 'AUFTRAG'
                 AND   nrrangenr = l_numkr.
  IF NOT SY-subrc IS INITIAL.
     MESSAGE I870(NR).
     EXIT.
  ENDIF.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
       EXPORTING
           retfield    = 'FROMNUMBER'
           dynpprog    = l_prog
           dynpnr      = l_dynp
           dynprofield = 'AUART'
           value_org   = 'S'
       TABLES
           value_tab   = l_nriv.

* F4-values for number range of given AUART
* b) high-value of given AUART
  AT SELECTION-SCREEN ON VALUE-REQUEST FOR aufnr-high.
  CALL FUNCTION 'DYNP_VALUES_READ'
       EXPORTING
           DYNAME                   = l_prog
           DYNUMB                   = l_dynp
      TABLES
           DYNPFIELDS               = dynpro_value.

  READ TABLE dynpro_value INDEX 1 INTO field_value.
  READ TABLE lt_t003o WITH KEY auart = field_value-fieldvalue.
  SELECT SINGLE numkr FROM T003O INTO l_numkr
                      WHERE autyp = '30'
                      AND   auart = lt_t003o-auart.

  SELECT * FROM NRIV INTO CORRESPONDING FIELDS OF TABLE l_nriv
                 WHERE object    = 'AUFTRAG'
                 AND   nrrangenr = l_numkr.
  IF NOT SY-subrc IS INITIAL.
     MESSAGE I870(NR).
     EXIT.
  ENDIF.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
       EXPORTING
           retfield    = 'TONUMBER'
           dynpprog    = l_prog
           dynpnr      = l_dynp
           dynprofield = 'AUART'
           value_org   = 'S'
       TABLES
           value_tab   = l_nriv.

*&---------------------------------------------------------------------*
* Events for moment AT SELECTION SCREEN OUTPUT
*&---------------------------------------------------------------------*
  AT SELECTION-SCREEN OUTPUT.

* Read order types for PM-orders
  SELECT * FROM T003O INTO CORRESPONDING FIELDS OF TABLE lt_t003o
           WHERE autyp = '30'.
  IF NOT SY-subrc IS INITIAL.
     WRITE: / 'No orders of ordertype  MAINTENANCE  found.',
            / 'Nothing to do; ..... bye.'.
     EXIT.
  ENDIF.

* Values for F4-help
  field_value-fieldname = 'AUART'.
  APPEND field_value TO dynpro_value.
  l_prog = sy-repid.
  l_dynp = sy-dynnr.

* SET Flag TEST when back on selection screen
* Set always 'X'
  TEST = 'X'.


* END OF REPORT Z_CORRECT_AUFK_PHAS0_TO_PHAS3
*Selection text£º
*AUART:D       .
*AUFNR:D       .
*TEST:        TEST?
