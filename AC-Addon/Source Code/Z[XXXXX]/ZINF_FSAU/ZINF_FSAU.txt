*----------------------------------------------------------------------*
* PROGRAM TITLE   : ZINF_FSAU                                          *
* PROJECT         : SAP Tools                                          *
* MODULE          :                                                    *
* CREATED BY      : Reena Prabhakar         .                          *
* CREATION DATE   : 6.12.2004                                          *
*                                                                      *
*----------------------------------------------------------------------*
* DESCRIPTION     : Report program to generate a list business         *
*                   scenarios implemented in a particular instance     *
*                   of SAP                                             *
* DEPENDENCIES    :                                                    *
*                                                                      *
*----------------------------------------------------------------------*
* MODIFICATION HISTORY:                                                *
* DATE            |PROGRAMMER           |REQUEST#          |CORRECTION *
*----------------------------------------------------------------------*
* SY-DATUM        |SY-UNAME             |                              *
* 17.05.2007      |SAPTOOLS             |DEVK929355: Bug fix           *
*----------------------------------------------------------------------*
* 22.08.2007      |SAPTOOLS             |DEVK929782: Introduced new    *
*                 |                     | grouping of modules over     *
*                 |                     | submodules                   *
*----------------------------------------------------------------------*
* 19.09.2007      |SAPTOOLS             |DEVK929910: Bug fix           *
*----------------------------------------------------------------------*
* 27.09.2007      |SAPTOOLS             |DEVK929930: Background        *
*                 |                     | functionality                *
*----------------------------------------------------------------------*
* 02.07.2008      |SAPTOOLS             |DEVK930516: Increase the      *
*                 |                     | number of join conditons     *
*                 |                     | between two tables           *
*----------------------------------------------------------------------*
REPORT  z_mm NO STANDARD PAGE HEADING
        LINE-SIZE 1023
        LINE-COUNT 100(3)
        MESSAGE-ID z_fsau.

INCLUDE z_fsau_top.
INCLUDE z_fsau_forms.
*----------------------------------------------------------------------*
*               SELECTION-SCREEN 100                                   *
*----------------------------------------------------------------------*

* Begin of Block-1
SELECTION-SCREEN BEGIN OF BLOCK blk1 WITH FRAME TITLE text-001.
SELECT-OPTIONS so_date FOR sy-datum NO-EXTENSION.
SELECTION-SCREEN END OF BLOCK blk1.
* End of Block-1

* Begin of Block-2
SELECTION-SCREEN BEGIN OF BLOCK blk2 WITH FRAME TITLE text-005.
PARAMETERS: pa_utilb AS CHECKBOX.
SELECTION-SCREEN END OF BLOCK blk2.
* End of Block-2

* Begin of Block-3
SELECTION-SCREEN BEGIN OF BLOCK blk3 WITH FRAME.
PARAMETER pa_dwnld AS CHECKBOX DEFAULT 'X'.
SELECTION-SCREEN SKIP 1.
PARAMETER pa_brows TYPE rlgrap-filename VISIBLE LENGTH 30 DEFAULT
'C:\Business Profile.xls' .

PARAMETER pa_app LIKE rlgrap-filename
          DEFAULT '\\IDESDEV\AppFiles\Business Profiler.txt'.

SELECTION-SCREEN END OF BLOCK blk3.
* End of Block-3

* Begin of Block-4
SELECTION-SCREEN BEGIN OF BLOCK blk4 WITH FRAME TITLE text-002.
SELECTION-SCREEN SKIP 1.
PARAMETERS:  p_module TYPE zinf_fsau_submod-zmodule AS LISTBOX VISIBLE
LENGTH 25.
PARAMETERS : p_submod TYPE zinf_fsau_submod-submod.
SELECTION-SCREEN END OF BLOCK blk4.
SELECTION-SCREEN SKIP 1.
* End of Block-4

SELECTION-SCREEN SKIP 3.

* Define Pushbuttons for Table maintenance
SELECTION-SCREEN PUSHBUTTON /35(32) pb_maint USER-COMMAND table_maint.

*----------------------------------------------------------------------*
*                      AT SELECTION-SCREEN OUTPUT                      *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR pa_brows.
  CLEAR gd_downfile.
  PERFORM browse_file.
  MOVE gd_downfile TO pa_brows.

*AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_submod.
*
*  DATA: dynpro_values TYPE TABLE OF dynpread,
*        field_value LIKE LINE OF dynpro_values,
*        i_module TYPE zinf_fsau_submod-zmodule,
*        ret_tab TYPE ddshretval OCCURS 0 WITH HEADER LINE.
*
*  DATA: BEGIN OF it_value OCCURS 0,
*          submod TYPE zinf_fsau_submod-submod,
*        END OF it_value.
*
*  CLEAR: field_value, dynpro_values.
*  field_value-fieldname = 'P_MODULE'.
*  APPEND field_value TO dynpro_values.
*
*  CALL FUNCTION 'DYNP_VALUES_READ'
*    EXPORTING
*      dyname               = sy-repid
*      dynumb               = sy-dynnr
*      translate_to_upper   = 'X'
*    TABLES
*      dynpfields           = dynpro_values
*    EXCEPTIONS
*      invalid_abapworkarea = 1
*      invalid_dynprofield  = 2
*      invalid_dynproname   = 3
*      invalid_dynpronummer = 4
*      invalid_request      = 5
*      no_fielddescription  = 6
*      invalid_parameter    = 7
*      undefind_error       = 8
*      double_conversion    = 9
*      stepl_not_found      = 10
*      OTHERS               = 11.
*
*  READ TABLE dynpro_values INDEX 1 INTO field_value.
*  IF sy-subrc = 0.
*    i_module = field_value-fieldvalue.
*  ENDIF.
*  PERFORM submod_name USING i_module.
*
*  REFRESH it_value.
*  LOOP AT tb_submod INTO wa_submod.
*    it_value-submod = wa_submod-submod.
*    APPEND it_value.
*  ENDLOOP.
*
*  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
*    EXPORTING
*      retfield     = 'SUBMOD'
*      window_title = 'Select Item'
*      value_org    = 'S'
*    TABLES
*      value_tab    = it_value
*      return_tab   = ret_tab.
*  IF sy-subrc = 0.
*    p_submod =  ret_tab-fieldval.
*  ENDIF.

*----------------------------------------------------------------------*
*                   Initialization                                     *
*----------------------------------------------------------------------*
INITIALIZATION.
  MOVE 'Module Setup' TO pb_maint.
*----------------------------------------------------------------------*
*               At Selection Screen                                    *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN .

  CASE sy-ucomm.
    WHEN 'TABLE_MAINT'.
      CALL TRANSACTION 'ZINF_FSAU_SETUP3'.
    WHEN 'CRET' OR 'FC01' OR 'ONLI'.
      IF so_date-low IS INITIAL .
        MESSAGE 'Please Enter the Date Range' TYPE 'E'.
      ENDIF.
      IF  p_module IS INITIAL.
        MESSAGE 'Please Choose a Module' TYPE 'E'.
      ENDIF.
      IF  p_submod IS INITIAL.
        MESSAGE 'Please Choose a Submodule' TYPE 'E'.
      ENDIF.
      IF pa_dwnld = 'X'.
        IF sy-batch IS INITIAL AND pa_brows IS INITIAL.
          MESSAGE 'Please enter file name on presentation server'
          TYPE 'E'.
        ELSEIF sy-batch = 'X' AND pa_app IS INITIAL.
          MESSAGE 'Please enter file name on application server'
          TYPE 'E'.
        ENDIF.
      ENDIF.
      IF sy-ucomm = 'FC01'. "background processing
      ENDIF.
    WHEN OTHERS.
  ENDCASE.
*End of insert by Neelima

START-OF-SELECTION.
  submod = p_submod.
  module = p_module.
  PERFORM submod_name USING p_module.
* Obtain all the related table names into an internal table
  PERFORM table_name.
  DESCRIBE TABLE tb_tables LINES no_of_tables.
  IF no_of_tables IS INITIAL.
    MESSAGE i002(z_fsau).
    EXIT.
  ENDIF.

* Form the where clause to select records within the date range
  READ TABLE tb_submod WITH KEY submod = submod INTO wa_submod.

* Set a flag to know if the utility B has been chosen by the user or not
  IF pa_utilb = 'X'.
    flagb = 1.
  ENDIF.

* Obtain all the critical fields into an internal table
  PERFORM critical_fields.
  DESCRIBE TABLE tb_critical LINES no_of_tables.
  IF no_of_tables IS INITIAL.
    MESSAGE i006(z_fsau).
    EXIT.
  ENDIF.
  IF flagb = 1.
    READ TABLE tb_critical INTO wa_critical
    WITH KEY blank = 'M'.
    IF sy-subrc <> 0.
      MESSAGE i007(z_fsau).
      EXIT.
    ENDIF.
  ENDIF.

* Obtain the join condition from the table zinf_fsau_tabrel.
  PERFORM tab_rel.
  IF sy-subrc <> 0.
    MESSAGE i009(z_fsau).
    EXIT.
  ENDIF.

  LOOP AT tb_tabrel INTO wa_tabrel.
    wa_critical-tabname = wa_tabrel-ltab.
    wa_critical-fields = wa_tabrel-lfield.
    wa_critical-blank = 'J'.
    APPEND wa_critical TO tb_critical.
    CLEAR wa_critical.
    wa_critical-tabname = wa_tabrel-rtab.
    wa_critical-fields = wa_tabrel-rfield.
    wa_critical-blank = 'J'.
    APPEND wa_critical TO tb_critical.
    CLEAR wa_critical.
  ENDLOOP.

  REFRESH tb_list.
  tb_list[] = tb_critical[].
  SORT tb_list BY tabname fields.
  DELETE ADJACENT DUPLICATES FROM tb_list COMPARING tabname fields.

* Field 'Freq' required to maintain the Frequencies of each scenario in
*Util B
  ls_fieldcatalog-fieldname = 'FreqB'.
  APPEND ls_fieldcatalog TO lt_fieldcatalog.

* Field 'ScenarioB' required to maintain the scenario numbers of Util B
  ls_fieldcatalog-fieldname = 'ScenarioB'.
  ls_fieldcatalog-inttype = 'I'.
  APPEND ls_fieldcatalog TO lt_fieldcatalog.

* Move the contents to another internal table, since we will need even
**  the duplicate fields later
  tb1_critical[] = tb_critical[].
  DELETE tb1_critical WHERE blank = 'J'.

* Remove the duplicate fields i.e the fields which appear in both the
* utilities

  DATA: l_critical LIKE wa_critical.

  DESCRIBE TABLE tb_critical LINES max_rec.
  LOOP AT tb_critical INTO wa_critical.
    tab = sy-tabix + 1.
    DO.
      READ TABLE tb_critical INTO l_critical INDEX tab.
      IF sy-subrc NE 0.
        EXIT.
      ENDIF.
      IF l_critical-fields = wa_critical-fields.
        DELETE tb_critical INDEX tab.
        tab = tab - 1.
      ENDIF.
      tab = tab + 1.
    ENDDO.
  ENDLOOP.

  LOOP AT tb_critical INTO wa_critical.
* Append the fields required to the dynamic internal table
    ls_fieldcatalog-fieldname =  wa_critical-fields . "Fieldname
    SELECT * INTO TABLE tb_dd03l FROM dd03l WHERE fieldname =
    wa_critical-fields AND tabname = wa_critical-tabname
    AND as4local = 'A'.
    READ TABLE tb_dd03l INDEX 1.
    ls_fieldcatalog-inttype = tb_dd03l-inttype.
    ls_fieldcatalog-intlen  = tb_dd03l-intlen.
    IF tb_dd03l-inttype = 'N'.
      ls_fieldcatalog-intlen = 15.
    ENDIF.
    ls_fieldcatalog-tabname = wa_critical-tabname.
    APPEND ls_fieldcatalog TO lt_fieldcatalog.

* Form the lists of fields used for the selection query depending on the
*number of tables
*   PERFORM form_list.
  ENDLOOP.

* Field 'Scenario' required to maintain the scenario numbers
  ls_fieldcatalog-fieldname = 'Scenario'.
  ls_fieldcatalog-inttype = 'I'.
  APPEND ls_fieldcatalog TO lt_fieldcatalog.

* Field 'Freq' required to maintain the Frequencies of each scenario
  ls_fieldcatalog-fieldname = 'Freq'.
  ls_fieldcatalog-inttype = 'I'.
  APPEND ls_fieldcatalog TO lt_fieldcatalog.
  ASSIGN lt_data TO <fs_data>.

* Method to create a dynamic internal table
  CALL METHOD cl_alv_table_create=>create_dynamic_table
    EXPORTING
      it_fieldcatalog           = lt_fieldcatalog
    IMPORTING
      ep_table                  = <fs_data>
    EXCEPTIONS
      generate_subpool_dir_full = 1
      OTHERS                    = 2.
  IF sy-subrc <> 0.
  ENDIF.

*  So <FS_1> now points to the dynamic internal table.
  ASSIGN <fs_data>->* TO <fs_1>.

* Create a work area for the dynamic internal table.
  CREATE DATA new_line LIKE LINE OF <fs_1>.

* A field-symbol to access that work area
  ASSIGN new_line->* TO <fs_2>.

* Select all the records which satisfy the join condition from the
* tables
*  PERFORM join_tables.
  CREATE DATA line LIKE <fs_1>.
  ASSIGN line->* TO <fs_new>.

  SORT tb_tables BY sequence.
  LOOP AT tb_tables.

    wa_table = tb_tables.
    CLEAR: list_s, where_clause.

    LOOP AT tb_list INTO wa_critical
    WHERE tabname = wa_table-tabname.
      CONCATENATE list_s wa_critical-fields INTO list_s SEPARATED
      BY space.
    ENDLOOP.

    AT FIRST.
      CONCATENATE '''' so_date-low '''' INTO low.
      IF so_date-high IS INITIAL.
        MOVE so_date-low TO so_date-high.
      ENDIF.
      CONCATENATE '''' so_date-high '''' INTO high.
      CONCATENATE wa_submod-selfield 'BETWEEN ' low 'AND ' high INTO
      where_clause SEPARATED BY space.
      PERFORM filter_data USING wa_table-tabname CHANGING where_clause.
      SELECT (list_s) FROM (wa_table-tabname)
      INTO CORRESPONDING FIELDS OF TABLE <fs_1> WHERE (where_clause).
      IF sy-subrc <> 0.
        EXIT.
      ENDIF.
      CONTINUE.
    ENDAT.

    LOOP AT <fs_1> INTO <fs_2>.
      CLEAR l_one.
      LOOP AT tb_tabrel INTO wa_tabrel WHERE rtab = wa_table-tabname.
        ASSIGN COMPONENT wa_tabrel-lfield OF STRUCTURE <fs_2>
        TO <fs_val>.
        CONCATENATE '''' <fs_val> '''' INTO l_value.
        IF l_one IS INITIAL.
          CONCATENATE wa_tabrel-rfield 'EQ' l_value INTO where_clause
          SEPARATED BY space.
        ELSE.
          CONCATENATE where_clause 'AND' wa_tabrel-rfield 'EQ' l_value
          INTO where_clause SEPARATED BY space.
        ENDIF.
        l_one = 'X'.
      ENDLOOP.

      IF where_clause IS INITIAL.
        MESSAGE e208(00) WITH 'Missing Join Conditions'.
      ENDIF.
      PERFORM filter_data USING wa_table-tabname CHANGING where_clause.
      SELECT (list_s) FROM (wa_table-tabname)
      INTO CORRESPONDING FIELDS OF <fs_2> WHERE (where_clause).
        APPEND <fs_2> TO <fs_new>.
      ENDSELECT.
      IF sy-subrc NE 0.
        DELETE <fs_1>.
      ENDIF.
    ENDLOOP.
    REFRESH <fs_1>.
    <fs_1> = <fs_new>.
    REFRESH <fs_new>.

  ENDLOOP.
* If no records are selected raise a message
  IF <fs_1> IS INITIAL. " or p_submod = 'ACCOUNT RECEIVABLE'."PM
    MESSAGE i001(z_fsau).
    EXIT.
  ENDIF.

*Reading it again with for the primary key
  READ TABLE tb_tabrel INTO wa_tabrel INDEX 1.

* Form the lists of fields used for processing based on the utilities
*  PERFORM unique_list.

* Identify the unique records and assign the scenario number and
* frequency
  PERFORM unique_records.

* Display the output
  PERFORM display_output_a.
  IF flagb = 1.
    flagb2 = 1.
    REFRESH tb_index.

* Identify the unique records of utility B
    PERFORM unique_records.

    CALL METHOD cl_alv_table_create=>create_dynamic_table
      EXPORTING
        it_fieldcatalog           = lt_fieldcatalog
      IMPORTING
        ep_table                  = <fs_data>
      EXCEPTIONS
        generate_subpool_dir_full = 1
        OTHERS                    = 2.
    IF sy-subrc <> 0.
    ENDIF.
    ASSIGN <fs_data>->* TO <fs_master_final> .
    CREATE DATA new_master_line LIKE LINE OF <fs_master_final>.
    ASSIGN new_master_line->* TO <fs_master_final_wa>.

* Display utility B
    PERFORM display_heading_b.

* To identify the top 3 most frequently occuring unique set of records
    s = 1.
    k = 1.
    WHILE s <= max_scenario.
      LOOP AT <fs_1> ASSIGNING <fs_2> .
        ASSIGN COMPONENT 'SCENARIOB' OF STRUCTURE <fs_2> TO <field9>.
        IF <field9> = s.
          APPEND <fs_2> TO <fs_master_final>.
        ENDIF.
      ENDLOOP.
      SORT <fs_master_final> DESCENDING.
      FORMAT COLOR 3 ON.
      MOVE s TO k.
      CONCATENATE text-200 k INTO scenario SEPARATED BY ' '.
      WRITE:/ scenario.
      FORMAT COLOR OFF.

* Internal table <fs_master_final> contains all the records of a single
*scenario frm utility A
      READ TABLE <fs_master_final> INDEX 1 ASSIGNING
<fs_master_final_wa>.
      ASSIGN COMPONENT 'FREQB' OF STRUCTURE <fs_master_final_wa> TO
<field10>.
      MOVE <field10> TO max_scenario_b.
      i = 1.
      k = 1.
      LOOP AT <fs_master_final> ASSIGNING <fs_master_final_wa>.
        ASSIGN COMPONENT 'SCENARIOB' OF STRUCTURE <fs_master_final_wa>
TO <fs_scenario>.
        AT NEW <fs_scenario>.
          PERFORM display_output_b.
          i = i + 1.
          IF i > 3.
            EXIT.
          ENDIF.
        ENDAT.
      ENDLOOP.
      CLEAR <fs_master_final>.
      s = s + 1.
      k = s.
    ENDWHILE.
  ENDIF.

* To provide the download option on the output screen
*----------------------------------------------------------------------*
*                       Pf- Status                                     *
*----------------------------------------------------------------------*
  SET PF-STATUS 'MENUS'.
  IF pa_dwnld = 'X'. "OR sy-ucomm = 'FC01'.
    MOVE pa_brows TO gd_downfile.
    f_app = pa_app.
    PERFORM download.
  ENDIF.
*----------------------------------------------------------------------*
*                       At User Command                                *
*----------------------------------------------------------------------*
AT USER-COMMAND.
  IF sy-ucomm = 'DOWNLOAD'.
    PERFORM browse_file.
    IF NOT gd_downfile IS INITIAL.
      PERFORM download.
    ENDIF.
  ENDIF.
*----------------------------------------------------------------------*
*                      Top Of Page                                     *
*----------------------------------------------------------------------*
TOP-OF-PAGE.
  WRITE: / 'Report from  ', so_date-low ,'-', so_date-high,130 'Page',
sy-pagno.

*Text symbol text£º
*001:Please Enter the date range
*002:Select the Sub Module
*005:Master Data Scenario
*200:Scenario

*213:No. Of Occ
*Selection text£º
*PA_APP:        Application server file name
*PA_BROWS:        Presentation server file name
*PA_DWNLD:                   Download to File
*PA_UTILB:                   Display Master Data
*P_MODULE:D       .
*P_SUBMOD:D       .
*SO_DATE:        Date
