REPORT zimrg_display LINE-SIZE 160.

DATA:
  lt_imptt TYPE TABLE OF imptt,
  ls_imptt TYPE imptt,
  ls_imrg TYPE imrg,
  lv_point LIKE imrg-point,
  lv_backwards LIKE imptt-indrv,
  lv_readg_old TYPE imrg-cntrr,
  lv_readg_new TYPE imrg-cntrr,
  lv_error TYPE c.

SELECT-OPTIONS:
  pt_point FOR lv_point.

* Error legend
FORMAT COLOR COL_TOTAL.
WRITE: / '1) New counter <> old counter + difference'.
FORMAT COLOR COL_NEGATIVE.
WRITE: / '2) New counter < old counter'.
FORMAT COLOR COL_POSITIVE.
WRITE: / '3) 806624-Error', /.
FORMAT COLOR OFF.

* Get all points
SELECT *
  FROM imptt
  INTO TABLE lt_imptt
  WHERE point IN pt_point.

LOOP AT lt_imptt INTO ls_imptt.

* Header line
  WRITE: /,
         'MDOCM                POINT        IDATE      ITIME GENER',
         'READG            READGI  CNTRR            CNTRRI  CDIFF',
         '           CDIFFI IDIFF EXCHG TOTEX CANCL Error', sy-uline.

* Backwards?
  SELECT SINGLE indrv
    FROM imptt
    INTO lv_backwards
    WHERE point = ls_imptt-point.

* Get IMRG
  SELECT *
    FROM imrg
    INTO ls_imrg
    WHERE point = ls_imptt-point
    ORDER BY invts DESCENDING.

* Check for errors
    IF lv_backwards IS INITIAL.
*   Counter runs forward.
      lv_readg_new = lv_readg_old + ls_imrg-cdiff.
    ELSE.
*   Counter runs backwards.
      lv_readg_new = lv_readg_old - ls_imrg-cdiff.
    ENDIF.
    IF lv_readg_new <> ls_imrg-readg AND
       ls_imrg-totex IS INITIAL.
      lv_error = 1.
      FORMAT COLOR COL_TOTAL.
      IF ls_imrg-readg = ls_imrg-cdiff.
        lv_error = 3.
        FORMAT COLOR COL_POSITIVE.
      ENDIF.
    ENDIF.
    IF ( lv_backwards = ' ' AND lv_readg_old > ls_imrg-readg ) OR
       ( lv_backwards = 'X' AND lv_readg_old < ls_imrg-readg ) AND
       ls_imrg-totex IS INITIAL.
      FORMAT COLOR COL_NEGATIVE.
      lv_error = 2.
    ENDIF.
    IF lv_readg_old IS INITIAL OR ls_imrg-cancl = 'X'.
      FORMAT COLOR OFF.
      CLEAR lv_error.
    ENDIF.

* Write entry
    WRITE: / ls_imrg-mdocm,
           ls_imrg-point,
           ls_imrg-idate,
           ls_imrg-itime,
           ls_imrg-gener,
           ls_imrg-readg,
           ls_imrg-readgi,
           ls_imrg-cntrr,
           ls_imrg-cntrri,
           ls_imrg-cdiff,
           ls_imrg-cdiffi,' ',
           ls_imrg-idiff,'   ',
           ls_imrg-exchg,'   ',
           ls_imrg-totex,'   ',
           ls_imrg-cancl,'   ',
           lv_error.

    IF ls_imrg-cancl IS INITIAL.
      lv_readg_old = ls_imrg-readg.
    ENDIF.
    FORMAT COLOR OFF.
    CLEAR lv_error.

  ENDSELECT.

  CLEAR lv_readg_old.

ENDLOOP.

*Selection text£º
*PT_POINT:D       .
