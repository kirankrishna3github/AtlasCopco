*&---------------------------------------------------------------------*
*& Report  ZTEST_PICTURE
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

REPORT  ZTEST_PICTURE.

TYPE-POOLS: cndp.
DATA: ok_code TYPE syucomm,
container TYPE REF TO cl_gui_custom_container,
picture   TYPE REF TO cl_gui_picture,
url       TYPE cndp_url.


PARAMETERS: p_objid TYPE w3objid OBLIGATORY.

AT SELECTION-SCREEN.
  SELECT COUNT(*) FROM wwwparams
  WHERE objid = p_objid.
  IF sy-subrc <> 0.
    MESSAGE e001(00) WITH 'MIME Object not found'.
  ENDIF.


START-OF-SELECTION.

  IF container IS INITIAL.

    CREATE OBJECT container
      EXPORTING
        container_name              = 'CONT'
        repid                       = 'ZTEST_PICTURE'
        dynnr                       = '0100'
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5
        OTHERS                      = 6.
    IF sy-subrc <> 0.
      MESSAGE i001(00) WITH 'Error while creating container'.
      LEAVE LIST-PROCESSING.
    ENDIF.
  ENDIF.
  IF picture IS INITIAL.
    CREATE OBJECT picture
      EXPORTING
        parent = container
      EXCEPTIONS
        error  = 1
        OTHERS = 2.
    IF sy-subrc <> 0.
      MESSAGE i001(00) WITH 'Error while displaying picture'.
      LEAVE LIST-PROCESSING.
    ENDIF.
  ENDIF.
  IF picture IS NOT INITIAL.

    CALL FUNCTION 'DP_PUBLISH_WWW_URL'
      EXPORTING
        objid    = p_objid
        lifetime = cndp_lifetime_transaction
      IMPORTING
        url      = url
      EXCEPTIONS
        OTHERS   = 1.

    IF sy-subrc = 0.
      CALL METHOD picture->load_picture_from_url_async
        EXPORTING
          url = url.

      CALL METHOD picture->set_display_mode
        EXPORTING
          display_mode = cl_gui_picture=>display_mode_fit.
    ELSE.
      MESSAGE i001(00) WITH 'Error while load picture'.
      LEAVE LIST-PROCESSING.
    ENDIF.
  ENDIF.

  CALL SCREEN 0100.
*&---------------------------------------------------------------------*
*&      Module  STATUS_0100  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE status_0100 OUTPUT.
  SET PF-STATUS 'S0100'.
*  SET TITLEBAR 'xxx'.

ENDMODULE.                 " STATUS_0100  OUTPUT
*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_0100  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE user_command_0100 INPUT.
  CASE ok_code.
    WHEN 'BACK'.
      SET SCREEN 00.
      LEAVE SCREEN.
  ENDCASE.
ENDMODULE.                 " USER_COMMAND_0100  INPUT
