*&---------------------------------------------------------------------*
*& Report  YSE_FI_RECLASS_3115_3116
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& Reclass GPS code 3111 to 3115 & 3116 (SEED)                         *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 30.09.2011
*  Change Request Number : CR2012
*  Transport request Nr. : CD1K968166
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME           | CORRECTION NR. | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 27.03.2012 | J. Smets       | CD1K970961     | CR2012      *
*         | Add ProfitCenter                                           *
*----------------------------------------------------------------------*
* MOD-002 | 20.04.2012 | J. Smets       | CD1K971444     | CR2012      *
*         | Selection accounts via selection table                     *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_fi_reclass_3115_3116 NO STANDARD PAGE HEADING
                                 LINE-SIZE 100.

TABLES: bsis,
        bsas,
        yse_fi_sel_accnt.                                   "MOD-002

DATA: BEGIN OF gt_bsis  OCCURS 0,
         blart     TYPE blart,
         prctr     TYPE prctr,                              "MOD-001
         segment   TYPE fb_segment,
         belnr     TYPE belnr_d,
         gjahr     TYPE gjahr,
         buzei     TYPE buzei,
         budat     TYPE budat,
         dmbtr     TYPE dmbtr,
         shkzg     TYPE shkzg,
       END OF gt_bsis.

* Document data
DATA: BEGIN OF gt_doc  OCCURS 0,
         belnr       TYPE belnr_d,
         blart       TYPE blart,
         prctr       TYPE prctr,                            "MOD-001
         segment     TYPE fb_segment,
         value_lc    TYPE wogxxx,
      END OF gt_doc.

DATA: i_bdcdata LIKE bdcdata  OCCURS 0  WITH HEADER LINE,
      struct_bdcdata TYPE bdcdata,
      gt_err    LIKE bdcmsgcoll  OCCURS 0  WITH HEADER LINE.

*** MOD-002 * begin ***
RANGES: gr_hkont   FOR bsis-hkont.
*** MOD-002 * end ***

DATA: gv_waers_cc  TYPE waers,
      gv_date_eom  TYPE datum,
      gv_value_lc  TYPE wogxxx,
      gv_xintb     TYPE xfeld,
      gv_hkont     TYPE hkont,
      gv_msgtxt    LIKE t100-text,
      gv_errlst    TYPE xfeld     VALUE 'X',
      gv_detlst    TYPE xfeld     VALUE ' ',
      gv_mode(1)   TYPE c         VALUE 'N'.

CONSTANTS: gc_sa       TYPE blart       VALUE 'SA',
           gc_zz       TYPE blart       VALUE 'ZZ',
           gc_zs       TYPE mwskz       VALUE 'ZS',
           gc_seg1410  TYPE fb_segment  VALUE '0000001410'.

* Selections
SELECTION-SCREEN  BEGIN OF BLOCK sel  WITH FRAME  TITLE text-s01.
PARAMETERS: p_bukrs   TYPE bukrs  OBLIGATORY
                                  MEMORY ID buk.
SELECT-OPTIONS: s_budat  FOR bsis-budat  NO-EXTENSION.
SELECTION-SCREEN SKIP.
PARAMETERS: p_test AS CHECKBOX  DEFAULT 'X'.
SELECTION-SCREEN  END OF BLOCK sel.
* G/L Accounts
SELECTION-SCREEN  BEGIN OF BLOCK acc  WITH FRAME  TITLE text-s02.
*** MOD-002 * begin ***
*PARAMETERS:     p_acc1     TYPE hkont  DEFAULT '0002997901'  OBLIGATORY
*                                       MODIF ID nup,
*                p_acc2     TYPE hkont  DEFAULT '0001780002'  OBLIGATORY
*                                       MODIF ID nup,
*** MOD-002 * end ***
PARAMETERS:     p_acc3     TYPE hkont  DEFAULT '0003210911'  OBLIGATORY
                                       MODIF ID nup,
                p_acc4     TYPE hkont  DEFAULT '0003290901'  OBLIGATORY
                                       MODIF ID nup,
                p_acc5     TYPE hkont  DEFAULT '0003292901'  OBLIGATORY
                                       MODIF ID nup.
SELECTION-SCREEN  END OF BLOCK acc.


*----------------------------------------------------------------------*
* On the selection screen                                              *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-group1 = 'NUP'.
      screen-input = 0.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON p_bukrs.

  PERFORM  check_authorization.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*

INITIALIZATION.

  s_budat-low = sy-datum.
  s_budat-low+6(2) = '01'.
  CALL FUNCTION 'DATE_GET_MONTH_LASTDAY'
    EXPORTING
      i_date = s_budat-low
    IMPORTING
      e_date = gv_date_eom.
  s_budat-high = gv_date_eom.
  s_budat-option = 'BT'.
  s_budat-sign   = 'I'.
  APPEND s_budat.


*----------------------------------------------------------------------*
START-OF-SELECTION.

  PERFORM initial.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_bsis[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No documents selected'(e01).
    EXIT.
  ENDIF.

* Process data
  PERFORM process_data.

* List data
  PERFORM list_data.


*----------------------------------------------------------------------*
TOP-OF-PAGE.

  WRITE: /01 'Date:', sy-datum,
          25 'Reclass GPS code 3111 to 3115 & 3116 (SEED)',
          75 'Company:', p_bukrs,
          97(4) sy-pagno.
  ULINE.

  IF NOT gv_errlst IS INITIAL.
    WRITE: /01 'Errors'.
  ELSE.
    WRITE: /01 'Document',
            13 'Type',
            22 'Prof.Ctr',
            35 'Segment',
            48 '        Posted Value ',
            70 'Curr.'.
  ENDIF.

  ULINE.


*----------------------------------------------------------------------*
*       FORMS
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  AUTHORITY-CHECK OBJECT 'F_BKPF_BUK'
           ID 'ACTVT' DUMMY
           ID 'BUKRS' FIELD p_bukrs.

  IF sy-subrc = 4.
*     No authorisation to display data
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH p_bukrs.
    EXIT.
  ELSEIF sy-subrc <> 0.
*     Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
    EXIT.
  ENDIF.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  INITIAL
*&---------------------------------------------------------------------*
*       Initialization
*----------------------------------------------------------------------*
FORM initial .

* Company currency
  SELECT SINGLE waers INTO gv_waers_cc
         FROM t001
         WHERE bukrs = p_bukrs.
  IF sy-subrc NE 0.
  ENDIF.

* Posting date
  IF p_test IS INITIAL.
    READ TABLE s_budat INDEX 1.
    IF sy-subrc = 0.
      CALL FUNCTION 'DATE_GET_MONTH_LASTDAY'
        EXPORTING
          i_date = s_budat-high
        IMPORTING
          e_date = gv_date_eom.
    ENDIF.
  ENDIF.

ENDFORM.                    " INITIAL

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

*** MOD-002 * begin ***
  DATA:   lt_accnt   TYPE TABLE OF yse_fi_sel_accnt
                          WITH HEADER LINE.
  RANGES: lr_hkont   FOR bsis-hkont.

* Build range for selection of accounts
  SELECT * INTO TABLE lt_accnt
           FROM yse_fi_sel_accnt
           WHERE progname = sy-repid
             AND bukrs = p_bukrs.
  CHECK NOT lt_accnt[] IS INITIAL.
  CLEAR lr_hkont.
  LOOP AT lt_accnt.
    lr_hkont-low = lt_accnt-saknr.
    lr_hkont-option = 'EQ'.
    lr_hkont-sign   = 'I'.
    APPEND lr_hkont.
  ENDLOOP.
*** MOD-002 * end ***

* Open items (G/L accounts)
*  SELECT blart segment belnr                               "MOD-001
  SELECT blart prctr segment belnr                          "MOD-001
         gjahr buzei budat dmbtr shkzg
         INTO TABLE gt_bsis
         FROM bsis
         WHERE bukrs   =  p_bukrs
*           AND hkont   IN (p_acc1, p_acc2)                 "MOD-002
           AND hkont   IN lr_hkont                          "MOD-002
           AND budat   IN s_budat
*           AND blart   NE gc_sa
           AND NOT ( blart = gc_sa AND zuonr = 'LT/ST Split' )
           AND segment NE gc_seg1410.

* Cleared items (G/L accounts)
*  SELECT blart segment belnr                               "MOD-001
  SELECT blart prctr segment belnr                          "MOD-001
         gjahr buzei budat dmbtr shkzg
         APPENDING TABLE gt_bsis
         FROM bsas
         WHERE bukrs   =  p_bukrs
*           AND hkont   IN (p_acc1, p_acc2)                 "MOD-002
           AND hkont   IN lr_hkont                          "MOD-002
           AND budat   IN s_budat
*           AND blart   NE gc_sa
           AND NOT ( blart = gc_sa AND zuonr = 'LT/ST Split' )
           AND segment NE gc_seg1410.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  PROCESS_DATA
*&---------------------------------------------------------------------*
*       Process data
*----------------------------------------------------------------------*
FORM process_data .

  SORT gt_bsis BY blart
                  prctr                                     "MOD-001
                  segment
                  belnr.

  LOOP AT gt_bsis.

*   Begin
    AT FIRST.
*     Open accounts
      gv_xintb = ' '.
      gv_hkont = p_acc3.
      PERFORM call_fss0.
      gv_hkont = p_acc4.
      PERFORM call_fss0.
      gv_hkont = p_acc5.
      PERFORM call_fss0.
    ENDAT.

*   New segment
    AT NEW segment.
      CLEAR gv_value_lc.
    ENDAT.

*   Detail.
    IF gt_bsis-shkzg = 'S'.
      gv_value_lc = gv_value_lc + gt_bsis-dmbtr.
    ELSE.
      gv_value_lc = gv_value_lc - gt_bsis-dmbtr.
    ENDIF.

*   End of segment
    AT END OF segment.
      IF gv_value_lc NE 0.
        CLEAR gt_doc.
        IF p_test IS INITIAL.
          PERFORM post_reclass.
        ENDIF.
        gt_doc-blart    = gt_bsis-blart.
        gt_doc-prctr    = gt_bsis-prctr.                    "MOD-001
        gt_doc-segment  = gt_bsis-segment.
        gt_doc-value_lc = gv_value_lc.
        APPEND gt_doc.
      ENDIF.
    ENDAT.

*   End
    AT LAST.
*     Close accounts
      gv_xintb = 'X'.
      gv_hkont = p_acc3.
      PERFORM call_fss0.
      gv_hkont = p_acc4.
      PERFORM call_fss0.
      gv_hkont = p_acc5.
      PERFORM call_fss0.
    ENDAT.

  ENDLOOP.

ENDFORM.                    " PROCESS_DATA

*&---------------------------------------------------------------------*
*&      Form  CALL_FSS0
*&---------------------------------------------------------------------*
*       Prepare & Call transaction FSS0
*----------------------------------------------------------------------*
FORM call_fss0 .

  REFRESH: i_bdcdata, gt_err.

  PERFORM bdc_dynpro      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=ACC_MOD'.
  PERFORM bdc_field       USING 'GLACCOUNT_SCREEN_KEY-SAKNR' gv_hkont.
  PERFORM bdc_field       USING 'GLACCOUNT_SCREEN_KEY-BUKRS' p_bukrs.

  PERFORM bdc_dynpro      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=TAB02'.

  PERFORM bdc_dynpro      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=SAVE'.
  PERFORM bdc_field       USING 'GLACCOUNT_SCREEN_CCODE-XINTB' gv_xintb.

  CALL TRANSACTION 'FSS0' USING i_bdcdata
                          MODE gv_mode
                          UPDATE 'S'
                          MESSAGES INTO gt_err.

* Check for errors
  LOOP AT gt_err WHERE msgtyp = 'E'
                    OR msgtyp = 'A'.
    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*        SPRSL                   = SY-LANGU
        arbgb                   = gt_err-msgid
        msgnr                   = gt_err-msgnr
        msgv1                   = gt_err-msgv1
        msgv2                   = gt_err-msgv2
        msgv3                   = gt_err-msgv3
        msgv4                   = gt_err-msgv4
      IMPORTING
        msgtext                 = gv_msgtxt
      EXCEPTIONS
        message_not_found       = 1
        OTHERS                  = 2.

    WRITE: / gv_msgtxt.
  ENDLOOP.

ENDFORM.                    " CALL_FSS0

*----------------------------------------------------------------------*
*        Start new screen                                              *
*----------------------------------------------------------------------*
FORM bdc_dynpro USING program dynpro.

  CLEAR i_bdcdata.
  i_bdcdata-program  = program.
  i_bdcdata-dynpro   = dynpro.
  i_bdcdata-dynbegin = 'X'.
  APPEND i_bdcdata.

ENDFORM.                    "BDC_DYNPRO

*----------------------------------------------------------------------*
*        Insert field                                                  *
*----------------------------------------------------------------------*
FORM bdc_field USING fnam fval.

  CLEAR i_bdcdata.
  i_bdcdata-fnam = fnam.
  i_bdcdata-fval = fval.
  APPEND i_bdcdata.

ENDFORM.                    "bdc_field

*&---------------------------------------------------------------------*
*&      Form  POST_RECLASS
*&---------------------------------------------------------------------*
*       Reclass posting
*----------------------------------------------------------------------*
FORM post_reclass .

  DATA: lv_date(10)     TYPE c,
        lv_wrbtr_lc(16) TYPE c,
        lv_zuonr        TYPE dzuonr,
        lv_xblnr        TYPE xblnr,
        lv_hkont        TYPE hkont.


  REFRESH: i_bdcdata, gt_err.

  WRITE gv_date_eom TO lv_date.
  WRITE gv_value_lc TO lv_wrbtr_lc
                       CURRENCY gv_waers_cc NO-SIGN.

  IF gt_bsis-blart = gc_sa  OR
     gt_bsis-blart = gc_zz.
*   Reclass RR Value 3111_3116
    lv_hkont = p_acc5.
    lv_xblnr = 'Recl.3116'.
    lv_zuonr = 'Recl.RR.Val.3116'.
  ELSE.
*   Reclass Billing Value 3111_3115
    lv_hkont = p_acc4.
    lv_xblnr = 'Recl.3115'.
    lv_zuonr = 'Recl.Bill.Val.3115'.
  ENDIF.

* Header
  PERFORM bdc_dynpro      USING 'SAPMF05A' '0100'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '/00'.
  PERFORM bdc_field       USING 'BKPF-BLDAT' lv_date.
  PERFORM bdc_field       USING 'BKPF-BLART' gc_sa.
  PERFORM bdc_field       USING 'BKPF-BUKRS' p_bukrs.
  PERFORM bdc_field       USING 'BKPF-BUDAT' lv_date.
  PERFORM bdc_field       USING 'BKPF-WAERS' gv_waers_cc.
  PERFORM bdc_field       USING 'BKPF-XBLNR' lv_xblnr.
  IF gv_value_lc > 0.
    PERFORM bdc_field       USING 'RF05A-NEWBS' '40'.
  ELSE.
    PERFORM bdc_field       USING 'RF05A-NEWBS' '50'.
  ENDIF.
  PERFORM bdc_field       USING 'RF05A-NEWKO' p_acc3.

* Item 1
  PERFORM bdc_dynpro      USING 'SAPMF05A' '0300'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=ZK'.
  PERFORM bdc_field       USING 'BSEG-WRBTR' lv_wrbtr_lc.
  PERFORM bdc_field       USING 'BSEG-ZUONR' lv_zuonr.
  PERFORM bdc_field       USING 'DKACB-FMORE' 'X'.

* Item 1: more date
  PERFORM bdc_dynpro      USING 'SAPLKACB' '0002'.
  PERFORM bdc_field       USING 'BDC_OKCODE' 'ENTE'.
  PERFORM bdc_field       USING 'COBL-PRCTR' gt_bsis-prctr.    "MOD-001
  PERFORM bdc_field       USING 'COBL-SEGMENT' gt_bsis-segment.

* Item 2
  PERFORM bdc_dynpro      USING 'SAPMF05A' '0330'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '/00'.
  IF gv_value_lc > 0.
    PERFORM bdc_field       USING 'RF05A-NEWBS' '50'.
  ELSE.
    PERFORM bdc_field       USING 'RF05A-NEWBS' '40'.
  ENDIF.
  PERFORM bdc_field       USING 'RF05A-NEWKO' lv_hkont.

  PERFORM bdc_dynpro      USING 'SAPMF05A' '0300'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=ZK'.
  PERFORM bdc_field       USING 'BSEG-WRBTR' lv_wrbtr_lc.
  PERFORM bdc_field       USING 'BSEG-ZUONR' lv_zuonr.
  PERFORM bdc_field       USING 'DKACB-FMORE' 'X'.

* Item 2: more date
  PERFORM bdc_dynpro      USING 'SAPLKACB' '0002'.
  PERFORM bdc_field       USING 'BDC_OKCODE' 'ENTE'.
  PERFORM bdc_field       USING 'COBL-PRCTR' gt_bsis-prctr.    "MOD-001
  PERFORM bdc_field       USING 'COBL-SEGMENT' gt_bsis-segment.

* Save
  PERFORM bdc_dynpro      USING 'SAPMF05A' '0330'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=BU'.

  CALL TRANSACTION 'FB01' USING i_bdcdata
                          MODE gv_mode
                          UPDATE 'S'
                          MESSAGES INTO gt_err.

* Check for errors
  LOOP AT gt_err WHERE msgtyp = 'E'
                    OR msgtyp = 'A'.
    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*        SPRSL                   = SY-LANGU
        arbgb                   = gt_err-msgid
        msgnr                   = gt_err-msgnr
        msgv1                   = gt_err-msgv1
        msgv2                   = gt_err-msgv2
        msgv3                   = gt_err-msgv3
        msgv4                   = gt_err-msgv4
      IMPORTING
        msgtext                 = gv_msgtxt
      EXCEPTIONS
        message_not_found       = 1
        OTHERS                  = 2.

    WRITE: / gv_msgtxt.
  ENDLOOP.

* No errors
  IF sy-subrc NE 0.
*   Get document number
*    GET PARAMETER ID 'BLN' FIELD gt_doc-belnr.
    READ TABLE gt_err WITH KEY msgid = 'F5'
                               msgnr = '312'.
    IF sy-subrc NE 0.
      WRITE: / 'No posting made for', gt_bsis-blart, gt_bsis-segment,
                                      lv_zuonr,
                                      gv_value_lc, gv_waers_cc.
    ELSE.
      CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
        EXPORTING
          input  = gt_err-msgv1
        IMPORTING
          output = gt_doc-belnr.
    ENDIF.
  ENDIF.

ENDFORM.                    " POST_RECLASS

*&---------------------------------------------------------------------*
*&      Form  LIST_DATA
*&---------------------------------------------------------------------*
*       List data
*----------------------------------------------------------------------*
FORM list_data .

  CLEAR gv_errlst.
  NEW-PAGE.

  IF gt_doc[] IS INITIAL.
    WRITE: /01 'NO documents posted'.
  ELSE.
*   Posted documents
    LOOP AT gt_doc.
      WRITE: /01 gt_doc-belnr,
              13 gt_doc-blart,
              22 gt_doc-prctr,                              "MOD-001
              35 gt_doc-segment,
              48(20) gt_doc-value_lc CURRENCY gv_waers_cc,
              70 gv_waers_cc.
    ENDLOOP.
  ENDIF.

  ULINE.

ENDFORM.                    " LIST_DATA

*Text symbol text£º
*S01:Selection

*S02:G/L Accounts
*Selection text£º
*P_ACC3:        G/L Account
*P_ACC4:        G/L Account (Recl.3115)
*P_ACC5:        G/L Account (Recl.3116)
*P_BUKRS:D       .
*P_TEST:        Test run
*S_BUDAT:D       .
