*&---------------------------------------------------------------------*
*&  Include           LCORUFS3
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  INITIALIZE_TIME_ZONE
*&---------------------------------------------------------------------*
*       get time zone BAdIs and check customizing
*----------------------------------------------------------------------*
FORM initialize_time_zone.

*--- at first check if BAdI for specific list is implemented
  TRY.
      GET BADI gb_badi_time_zone_coru.

    CATCH cx_badi_not_implemented.
      gv_time_zone_active = tzs1_active-no.
      RETURN.
  ENDTRY.

  IF cl_badi_query=>number_of_implementations(
                                    gb_badi_time_zone_coru ) = 0.
    gv_time_zone_active = tzs1_active-no.
    RETURN.
  ENDIF.

**--- general part for all lists
  PERFORM initialize_time_zone_f90 USING tzs1_appl-pm_woc.
ENDFORM.                    " INITIALIZE_TIME_ZONE

*&---------------------------------------------------------------------*
*&      Form  CHANGE_CONF_LIST_DISP
*&---------------------------------------------------------------------*
*    EAM Time zone support : convert the out put data for
*    confirmation in user time zone
*----------------------------------------------------------------------*
*      <--cv_conf_dt  confirmation date
*      <--cv_conf_tm  confirmation time
*----------------------------------------------------------------------*
FORM change_conf_list_disp  CHANGING cv_conf_dt
                                     cv_conf_tm.
  BREAK-POINT ID eam_tzs_gen.

  IF gv_time_zone_active NE tzs1_active-yes.
*--- only if time zone support is active
    RETURN.
  ENDIF.

  CALL BADI gb_badi_time_zone_coru->convert_rmafru00_sys_to_sess
    CHANGING
      cv_conf_dt = cv_conf_dt
      cv_conf_tm = cv_conf_tm.

ENDFORM.                    " CHANGE_CONF_LIST_DISP

*&---------------------------------------------------------------------*
*&      Form  INITIALIZE_TIME_ZONE_F90
*&---------------------------------------------------------------------*
*       initialze time zone functionality and check customizing
*       Generic part for all lists
*----------------------------------------------------------------------*
*      -->IV_APPL  time zone application
*----------------------------------------------------------------------*
FORM initialize_time_zone_f90  USING iv_appl TYPE tz_d_appl.

  TRY.
      GET BADI gb_badi_time_zone_generic.

    CATCH cx_badi_not_implemented.
      gv_time_zone_active = tzs1_active-no.
      RETURN.
  ENDTRY.

  IF cl_badi_query=>number_of_implementations(
            gb_badi_time_zone_generic ) = 0.
    gv_time_zone_active = tzs1_active-no.
    RETURN.
  ENDIF.

*--- check if customizing is active
  CALL BADI gb_badi_time_zone_generic->check_customizing
    EXPORTING
      is_application        = iv_appl
    IMPORTING
      es_activated          = gv_time_zone_active
    EXCEPTIONS
      application_not_valid = 1
      OTHERS                = 2.
  IF sy-subrc <> 0.
    gv_time_zone_active = tzs1_active-no.
  ENDIF.

ENDFORM.                    " INITIALIZE_TIME_ZONE_F90
*&---------------------------------------------------------------------*
*&      Form  CREATE_TIME_ZONE_BUTTON_F90
*&---------------------------------------------------------------------*
*       create button for time zone change
*----------------------------------------------------------------------*
*      -->iv_ucomm function code for used button
*----------------------------------------------------------------------*
FORM create_time_zone_button_f90 USING iv_ucomm TYPE syucomm.

  CONSTANTS: lc_fieldname_function TYPE fieldname VALUE 'SSCRFIELDS-FUNCTXT_'.

  DATA: lt_exclude TYPE TABLE OF syucomm,
        ls_dyntxt  TYPE smp_dyntxt,
        lv_fieldname TYPE fieldname.

  FIELD-SYMBOLS: <lv_field> TYPE ANY.

  BREAK-POINT ID eam_tzs_pbo.

  IF gv_time_zone_active = tzs1_active-yes.
*--- time zone active -> set button
    CALL BADI gb_badi_time_zone_generic->create_button
      IMPORTING
        es_button = ls_dyntxt.

*--- find field for function code
    CONCATENATE lc_fieldname_function iv_ucomm+2(2) INTO lv_fieldname.

    ASSIGN (lv_fieldname) TO <lv_field>.

    <lv_field> = ls_dyntxt.

*--- set time zone button for list output too
    gs_time_zone_func_dyntxt = ls_dyntxt.
  ELSE.
*--- time zone not active -> hide button
    APPEND iv_ucomm TO lt_exclude.

    CALL FUNCTION 'RS_SET_SELSCREEN_STATUS'
      EXPORTING
        p_status  = sy-pfkey
      TABLES
        p_exclude = lt_exclude.
  ENDIF.

ENDFORM.                    " CREATE_TIME_ZONE_BUTTON_F90

*&---------------------------------------------------------------------*
*&      Form  TIME_ZONE_BUTTON_ON_LIST_FS3
*&---------------------------------------------------------------------*
* display a message, when time zone button is pressed in output list
*----------------------------------------------------------------------*
FORM time_zone_button_on_list_fs3.
 BREAK-POINT ID eam_tzs_gen.

  IF gv_time_zone_active NE tzs1_active-yes.
*--- only if time zone support is active
    RETURN.
  ENDIF.

  CALL BADI gb_badi_time_zone_generic->time_zone_button_on_list.

ENDFORM.                    " TIME_ZONE_BUTTON_ON_LIST_FS3
