*&---------------------------------------------------------------------*
*& Report  ZKEDRCHECK
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

REPORT  ZKEDRCHECK.

type-pools: kedr.

parameters: allclnts as checkbox default 'X',
            delsteps as checkbox.

tables: tkedrs, tkedrsf, tkedrst, trdir, tadir.

types: begin of t_del,
         tkedrs type tkedrs,
         message(60) type c,
       end of t_del.

data: tab_dels type table of t_del.
data: tab_tkedrs type table of tkedrs.
data: tab_tkedrst type table of tkedrst.
data: tab_tkedrsf type table of tkedrsf.


perform init using allclnts.
perform check_fields.
perform check_tables.
perform write_results.
if not delsteps is initial.
  perform delete_steps.
endif.

*---------------------------------------------------------------------*
*       FORM check_fields                                             *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
form check_fields.
  data: name type kedr_name.
  data: failed type kedr_flag.
  data: del type t_del.

  loop at tab_tkedrsf into tkedrsf.
    name-identifier = tkedrsf-identifier.
    name-tabname = tkedrsf-tabname.
    name-fieldname = tkedrsf-fieldname.
    perform name--check(saplkedr) using name failed.
    check not failed is initial.
    move-corresponding tkedrsf to del-tkedrs.
    del-message = 'Feld &1-&2 nicht vorhanden'(010).
    perform replace using '&1' name-tabname del-message.
    perform replace using '&2' name-fieldname del-message.
    append del to tab_dels.
  endloop.
endform.

*---------------------------------------------------------------------*
*       FORM check_tables                                             *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
form check_tables.
  data: del type t_del.

  loop at tab_tkedrs into tkedrs where method = 'DRULE'.
    select single * from tadir where pgmid = 'R3TR'
                                 and object = 'TABL'
                                 and obj_name = tkedrs-param_1.
    if sy-subrc <> 0.
      del-tkedrs = tkedrs.
      del-message = 'Tabelle &1 nicht vorhanden'(020).
      perform replace using '&1' tkedrs-param_1 del-message.
      append del to tab_dels.
    endif.
  endloop.
endform.

*---------------------------------------------------------------------*
*       FORM write_results                                            *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
form write_results.
  data: del type t_del.

  if tab_dels is initial.
    write 'Keine Inkonsistenzen festgestellt'(030).
  else.

    perform list_new.
    perform list_define using 'Clnt'    3  'KEY'.        "#EC NOTEXT
* perform list_define using 'Module'  6  'NORMAL'.     "EC NOTEXT
    perform list_define using 'Appl'    4  'KEY'.        "#EC NOTEXT
    perform list_define using 'Subc'    4  'KEY'.        "#EC NOTEXT
    perform list_define using 'ID'      4  'KEY'.        "#EC NOTEXT
    perform list_define using 'Env '    8  'KEY'.        "#EC NOTEXT
    perform list_define using 'Step'    4  'KEY'.        "#EC NOTEXT
    perform list_define using 'Text'    40 'NORMAL'.     "#EC NOTEXT
    perform list_define using 'Problem' 60 'BACKGROUND'. "#EC NOTEXT
    perform list_open.

    loop at tab_dels into del.
      perform list_write using del-tkedrs-mandt.
*   perform list_write using del-tkedrs-applname.
      perform list_write using del-tkedrs-applclass.
      perform list_write using del-tkedrs-subclass.
      perform list_write using del-tkedrs-kedrstratid.
      perform list_write using del-tkedrs-kedrenv.
      perform list_write using del-tkedrs-step_no.
      clear tkedrst.
      read table tab_tkedrst into tkedrst
                    with key mandt = del-tkedrs-mandt
                             applclass = del-tkedrs-applclass
                             subclass = del-tkedrs-subclass
                             kedrstratid = del-tkedrs-kedrstratid
                             kedrenv = del-tkedrs-kedrenv
                             step_no = del-tkedrs-step_no
                             lang = sy-langu.
      perform list_write using tkedrst-text.
      perform list_write using del-message.
    endloop.

    perform list_close.
  endif.
endform.
*---------------------------------------------------------------------*
*       FORM replace                                                  *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
*  -->  i_pattern                                                     *
*  -->  i_text                                                        *
*  -->  x_line                                                        *
*---------------------------------------------------------------------*
form replace using i_pattern type c
                   i_text type c
                   x_line type c.
  data: len type i.
  field-symbols: <text> type c.

  len = strlen( i_text ).
  assign i_text(len) to <text>.
  replace i_pattern with <text> into x_line.
endform.

*---------------------------------------------------------------------*
*       FORM delete_steps                                             *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
form delete_steps.
  data: del type t_del.

  sort tab_dels by tkedrs-mandt
                   tkedrs-applclass
                   tkedrs-subclass
                   tkedrs-kedrstratid
                   tkedrs-kedrenv
                   tkedrs-step_no.

  delete adjacent duplicates from tab_dels comparing
                         tkedrs-mandt
                         tkedrs-applclass
                         tkedrs-subclass
                         tkedrs-kedrstratid
                         tkedrs-kedrenv
                         tkedrs-step_no.

  loop at tab_dels into del.
    tkedrs = del-tkedrs.
    call function 'KEDR_STRATEGY_STEP_DELETE'
         exporting
              i_appl        = tkedrs-applclass
              i_subclass    = tkedrs-subclass
              i_strategy_id = tkedrs-kedrstratid
              i_env         = tkedrs-kedrenv
              i_step_no     = tkedrs-step_no
              i_client      = tkedrs-mandt.
  endloop.
endform.

*---------------------------------------------------------------------*
*       FORM init                                                     *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
*  -->  i_all_clients                                                 *
*---------------------------------------------------------------------*
form init using i_all_clients.
  if i_all_clients is initial.
    select * from tkedrs into table tab_tkedrs.
    select * from tkedrst into table tab_tkedrst.
    select * from tkedrsf into table tab_tkedrsf.
  else.
    select * from tkedrs client specified into table tab_tkedrs.
    select * from tkedrst client specified into table tab_tkedrst.
    select * from tkedrsf client specified into table tab_tkedrsf.
  endif.
  sort tab_tkedrsf by mandt applclass subclass
                      kedrenv kedrstratid step_no.
  sort tab_tkedrs by mandt applclass subclass
                     kedrenv kedrstratid step_no.
endform.

include rkeb0580.


*Selection text£º
*ALLCLNTS:        All clients
*DELSTEPS:        Delete inconsistent steps.
