*&---------------------------------------------------------------------*
*& Report  ZREP_OBJLIST
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

************************************************************************
* Correction report for defect Object List                              *
*                                                                      *
* All changes are done if report is started with flag PTEST = SPACE.   *
* No enqueues are performed - be sure that the order is free for       *
* update                                                               *
*                                                                      *
* The report corrects the following problems for order associated      *
* object list                                                          *
*                                                                      *
* - double entries for the same notification                           *
*   -> all lines except the first are deleted from the table OBJK      *
*                                                                      *
* - notifications in the object list which are not linked to the       *
*   order                                                              *
*   -> all lines which belongs to those notifications are deleted      *
*      from table OBJK                                                 *
*                                                                      *
* - notifications which belongs to the order but are not in the        *
*   object list                                                        *
*   -> for all those notifications an entry in table OBJK is created   *
*                                                                      *
************************************************************************

************************************************************************
* Correction report for defect Object List                              *
*                                                                      *
* All changes are done if report is started with flag PTEST = SPACE.   *
* No enqueues are performed - be sure that the order is free for       *
* update                                                               *
*                                                                      *
* The report corrects the following problems for order associated      *
* object list                                                          *
*                                                                      *
* - double entries for the same notification                           *
*   -> all lines except the first are deleted from the table OBJK      *
*                                                                      *
* - notifications in the object list which are not linked to the       *
*   order                                                              *
*   -> all lines which belongs to those notifications are deleted      *
*      from table OBJK                                                 *
*                                                                      *
* - notifications which belongs to the order but are not in the        *
*   object list                                                        *
*   -> for all those notifications an entry in table OBJK is created   *
*                                                                      *
************************************************************************

REPORT  ZREP_OBJLIST.

TABLES: qmel.
TABLES: afih.
TABLES: objk.

DATA iobjk LIKE objk OCCURS 0 WITH HEADER LINE.
DATA iobjk_del LIKE objk OCCURS 0 WITH HEADER LINE.
DATA iobjk_ins LIKE objk OCCURS 0 WITH HEADER LINE.
DATA wobjk LIKE objk OCCURS 0 WITH HEADER LINE.
DATA wobjk_del LIKE objk OCCURS 0 WITH HEADER LINE.
DATA wobjk_ins LIKE objk OCCURS 0 WITH HEADER LINE.
DATA iqmel LIKE qmel OCCURS 0 WITH HEADER LINE.

DATA l_ihnum LIKE objk-ihnum.
DATA l_obzae LIKE objk-obzae.
DATA l_obknr LIKE OBJK-OBKNR.
DATA l_lines TYPE i.

PARAMETERS paufnr LIKE afih-aufnr.
PARAMETERS ptest  AS CHECKBOX DEFAULT 'X'.

IF paufnr IS INITIAL.
  WRITE: 'Please enter an order number'.
  EXIT.
ENDIF.

SELECT SINGLE * FROM afih WHERE aufnr = paufnr.
CHECK sy-subrc IS INITIAL.

* get all the dependent objects
SELECT * FROM objk INTO TABLE iobjk WHERE obknr = afih-obknr.
SORT iobjk BY obknr obzae.
SELECT * FROM qmel INTO TABLE iqmel WHERE aufnr = paufnr.
SORT iqmel BY qmnum.

* get max obzae
DESCRIBE TABLE iobjk LINES l_lines.
IF NOT l_lines IS INITIAL.
  READ TABLE iobjk INDEX l_lines.
  l_obzae = iobjk-obzae.
ENDIF.

* test the object list entries
SORT iobjk BY ihnum.
IF NOT l_lines IS INITIAL.
  LOOP AT iobjk WHERE ihnum <> space.
    IF l_ihnum = iobjk-ihnum.
      WRITE: / 'Double entry for notif:           ', iobjk-ihnum.
      APPEND iobjk TO iobjk_del.
    elseif iobjk-ihnum+0(1) = '%'.
      WRITE: / 'Defect entry in object list:', iobjk-obknr, iobjk-obzae.
      APPEND iobjk TO iobjk_del.
    ELSE.
      l_ihnum = iobjk-ihnum.
      READ TABLE iqmel WITH KEY qmnum = iobjk-ihnum BINARY SEARCH.
      IF NOT sy-subrc IS INITIAL.
        WRITE: / 'Invalid notif in objectlist:      ', iobjk-ihnum.
        APPEND iobjk TO iobjk_del.
        if iobjk-ihnum = afih-qmnum.
          WRITE: / 'Invalid notif in header:          ', afih-qmnum.
          clear afih-qmnum.
        endif.
      ENDIF.
    ENDIF.
  ENDLOOP.

* test the order related notifications
  SORT iobjk BY ihnum.
  LOOP AT iqmel.
    READ TABLE iobjk WITH KEY ihnum = iqmel-qmnum BINARY SEARCH.
    IF NOT sy-subrc IS INITIAL.
      WRITE: / 'Correct notif not in objectlist:  ', iqmel-qmnum.
      CLEAR iobjk_ins.
      ADD 1 TO l_obzae.
      iobjk_ins-obknr = afih-obknr.
      iobjk_ins-obzae = l_obzae.
      iobjk_ins-ihnum = iqmel-qmnum.
      iobjk_ins-objvw = 'A'.
      iobjk_ins-datum = sy-datum.
      APPEND iobjk_ins.
    ENDIF.
  ENDLOOP.

ELSE.
* order header has notification, but the order has no object list
* -> a new object list is created
  CLEAR l_lines.
  CLEAR l_obzae.
  DESCRIBE TABLE iqmel LINES l_lines.
  IF l_lines = '1'.
    CALL FUNCTION 'OBJECTLIST_NUMBER'
         IMPORTING
              obknr = l_obknr.
    iobjk_ins-obknr = l_obknr.
    afih-obknr      = l_obknr.
    iobjk_ins-obzae = '1'.
    read table iqmel INDEX 1.
    iobjk_ins-ihnum = iqmel-qmnum.
    iobjk_ins-objvw = 'A'.
    iobjk_ins-datum = sy-datum.
    APPEND iobjk_ins.
  ELSEIF l_lines IS INITIAL.
    write: / 'no notification in QMEL with order', paufnr.
  ELSEIF l_lines > 1.
    write: / 'more than 1 notification in QMEL with order',
              paufnr, 'please contact SAP'.
  ENDIF.
ENDIF.


* do the update
IF ptest IS INITIAL.
  LOOP AT iobjk_ins.
    objk = iobjk_ins.
    INSERT objk.
  ENDLOOP.
  LOOP AT iobjk_del.
    objk = iobjk_del.
    DELETE objk.
  ENDLOOP.
  update afih.
  COMMIT WORK.
ENDIF.
