************************************************************************
* Correction report for defect Object Lists                            *
*                                                                      *
* All changes are done if report is started with flag UPDATE checked.  *
* No enqueues are performed - be sure that the order is free for       *
* update                                                               *
*                                                                      *
* The report corrects object list entries with OBZAE = 0. It finds     *
* the maximum OBZAE in this object list, adds 1 and moves this number  *
* to the initial OBZAE.                                                *
************************************************************************
REPORT  ZREP_OBJK_OBZAE.

PARAMETERS: UPDATE TYPE C DEFAULT ' '.

TABLES: OBJK.

DATA: BEGIN OF HEADER OCCURS 0,
      OBKNR LIKE OBJK-OBKNR,
      OBZAE LIKE OBJK-OBZAE.
DATA: END OF HEADER.

DATA: BEGIN OF OBJK_TAB OCCURS 20.
        INCLUDE STRUCTURE OBJK.
DATA: END OF OBJK_TAB.

DATA: BEGIN OF INSERT_TAB OCCURS 20.
        INCLUDE STRUCTURE OBJK.
DATA: END OF INSERT_TAB.

DATA: BEGIN OF DELETE_TAB OCCURS 20.
        INCLUDE STRUCTURE OBJK.
DATA: END OF DELETE_TAB.

DATA: LO TYPE I.
DATA: LI TYPE I.
DATA: LD TYPE I.
DATA: TEMP_OBZAE LIKE OBJK-OBZAE.

 SKIP 2.

CASE UPDATE.
 WHEN SPACE.
   WRITE : / '*** simulation ***'.
 WHEN OTHERS.
   WRITE : / '*** update ***'.
ENDCASE.

 SKIP 2.


* read object lists with obzae = 0.
SELECT  OBKNR OBZAE APPENDING CORRESPONDING FIELDS OF TABLE HEADER
        FROM OBJK WHERE OBZAE = 0.


LOOP AT HEADER.
  SELECT * FROM OBJK INTO TABLE OBJK_TAB
         WHERE OBKNR = HEADER-OBKNR.

  PERFORM GET_MAXOBZAE.

ENDLOOP.

PERFORM OBJK_TO_DB.


WRITE: / '********** END OF PROGRAM ************'.

*& --------------------------------------------------------------
*&      FORM  GET_MAXOBZAE
*& --------------------------------------------------------------
FORM GET_MAXOBZAE.

  SORT OBJK_TAB BY OBZAE DESCENDING.

  READ TABLE OBJK_TAB INDEX 1.
   IF SY-SUBRC NE 0.
     WRITE: / 'entry not found, please check table OBJK.'.
   ELSE.
     TEMP_OBZAE = OBJK_TAB-OBZAE + 1.
   ENDIF.

  DESCRIBE TABLE OBJK_TAB LINES LO.
  READ TABLE OBJK_TAB INDEX LO.
   IF SY-SUBRC NE 0.
     WRITE: / 'wrong entry, please check table OBJK'.
   ELSEIF OBJK_TAB-OBZAE NE 0.
     WRITE: / 'obzae 0 not found'.
   ELSE.
     MOVE-CORRESPONDING OBJK_TAB TO DELETE_TAB.
     APPEND DELETE_TAB.
     OBJK_TAB-OBZAE = TEMP_OBZAE.
     MOVE-CORRESPONDING OBJK_TAB TO INSERT_TAB.
     APPEND INSERT_TAB.
   ENDIF.


ENDFORM.                               " GET_MAXOBZAE

*& -----------------------------------------------------------------   *
*&      Form  OBJK_TO_DB
*&------------------------------------------------------------------  *
FORM OBJK_TO_DB.


* Simulation ?
  IF UPDATE IS INITIAL.
    LOOP AT INSERT_TAB.
      WRITE: / 'new    :', INSERT_TAB-OBKNR, INSERT_TAB-OBZAE,
                           INSERT_TAB-ILOAN, INSERT_TAB-SORTF.
    ENDLOOP.
    SKIP.
    DESCRIBE TABLE INSERT_TAB LINES LI.
    WRITE: / 'number of entries inserted:', LI.
    SKIP.

    LOOP AT DELETE_TAB.
      WRITE: / 'deleted:', DELETE_TAB-OBKNR, DELETE_TAB-OBZAE,
                           DELETE_TAB-ILOAN, DELETE_TAB-SORTF.
    ENDLOOP.
    SKIP.
    DESCRIBE TABLE DELETE_TAB LINES LD.
    WRITE: / 'number of entries deleted:', LD.

    SKIP.

* Update ?
  ELSEIF NOT UPDATE IS INITIAL.
    INSERT OBJK FROM TABLE INSERT_TAB.
      IF SY-SUBRC EQ 0.
        LOOP AT INSERT_TAB.
          WRITE: / 'new    :', INSERT_TAB-OBKNR, INSERT_TAB-OBZAE,
                               INSERT_TAB-ILOAN, INSERT_TAB-SORTF.
        ENDLOOP.
        SKIP.
        WRITE: / 'number of entries inserted:', SY-DBCNT.
        SKIP.
      ENDIF.

    DELETE OBJK FROM TABLE DELETE_TAB.
      IF SY-SUBRC EQ 0.
        LOOP AT DELETE_TAB.
          WRITE: / 'deleted:', DELETE_TAB-OBKNR, DELETE_TAB-OBZAE,
                               DELETE_TAB-ILOAN, DELETE_TAB-SORTF.
        ENDLOOP.
        SKIP.
        WRITE: / 'number of entries deleted:', SY-DBCNT.
      ENDIF.
    SKIP.

  ENDIF.


ENDFORM.                               " OBJK_TO_DB


**************************** END **************************************
*&---------------------------------------------------------------------*
*& Report  ZREP_OBJK_OBZAE
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*
