*&---------------------------------------------------------------------*
*& Report  ZSSF_TEST_PSE                                               *
*&                                                                     *
*&---------------------------------------------------------------------*
*&                                                                     *
*&                                                                     *
*&---------------------------------------------------------------------*

REPORT ZSSF_TEST_PSE LINE-SIZE 120.

DEFINE CHECKBOX.
  SELECTION-SCREEN BEGIN OF LINE.
  PARAMETER: &1 TYPE C AS CHECKBOX.
  SELECTION-SCREEN COMMENT 3(40) &2 FOR FIELD &1.
  SELECTION-SCREEN END OF LINE.
END-OF-DEFINITION.

* PSE parameter
SELECTION-SCREEN BEGIN OF BLOCK PSE WITH FRAME TITLE T_PSE.

SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (20) T_FILE FOR FIELD PA_PSE.
PARAMETERS PA_PSE TYPE SSFPARMS-PAB OBLIGATORY.
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (20) T_PIN FOR FIELD PA_PIN.
PARAMETERS PA_PIN TYPE SSFPARMS-PABPW.
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN END OF BLOCK PSE.

* Test parameter
SELECTION-SCREEN BEGIN OF BLOCK TEST WITH FRAME TITLE T_TEST.
CHECKBOX PA_SIG  T_SIG.
CHECKBOX PA_ENC  T_ENC.
SELECTION-SCREEN END OF BLOCK TEST.


INITIALIZATION.

  T_PSE  = 'PSE'(PSE).
  T_FILE = 'Filename'(FNM).
  T_PIN  = 'PIN'(PIN).
  T_TEST = 'Test'(TST).
  T_SIG  = 'Signature'(SIG)..
  T_ENC  = 'Encryption'(ENC).
  PA_PSE = 'SAPSYS.pse'.
  PA_SIG = 'X'.

START-OF-SELECTION.

  TYPES:
    CERTATTR(255)  TYPE C.

  DATA:
    G_STR          TYPE STRING,
    G_DIR_INSTANCE TYPE STRING,
    G_SUBJECT      TYPE CERTATTR,
    G_ALGID        TYPE CERTATTR.

*----------------------------------------------------------------------*
* Show input parameter
*----------------------------------------------------------------------*
  FORMAT COLOR COL_HEADING.
  WRITE: SY-ULINE, (130)  'Input parameter'(INP), SY-ULINE.

  FORMAT COLOR COL_BACKGROUND.
  WRITE: /(19) T_FILE, (100) PA_PSE.

  IF PA_PIN IS INITIAL.
    WRITE: /(19) T_PIN, '<no>'(NO_).
  ELSE.
    WRITE: /(19) T_PIN, '<yes>'(YES).
  ENDIF.

  WRITE: /(19) T_SIG, PA_SIG.
  WRITE: /(19) T_ENC, PA_ENC.

*-----------------------------------------------------------------------
* Read kernel release
*-----------------------------------------------------------------------
  PERFORM READ_KERNEL.

*----------------------------------------------------------------------*
* Profile parameter SSF/SEC
*----------------------------------------------------------------------*
  PERFORM READ_PROFILE.

*----------------------------------------------------------------------*
* Environment variables SECUDIR and USER
*----------------------------------------------------------------------*
  PERFORM READ_ENVIRONMENT.

*----------------------------------------------------------------------*
* Version SAPCrypto or SAPSeculib
*----------------------------------------------------------------------*
  PERFORM READ_SAPCRYPTO.

*----------------------------------------------------------------------*
* Access to PSE (credentials and certificates)
*----------------------------------------------------------------------*
  PERFORM READ_PSE
    CHANGING G_SUBJECT.

*----------------------------------------------------------------------*
* SSFA customizing
*----------------------------------------------------------------------*
  PERFORM READ_SSFARGS.

*----------------------------------------------------------------------*
* Signature
*----------------------------------------------------------------------*
  IF NOT PA_SIG IS INITIAL.
    PERFORM TEST_SIG.
  ENDIF.

*----------------------------------------------------------------------*
* Encryption
*----------------------------------------------------------------------*
  IF NOT PA_ENC IS INITIAL.
    PERFORM TEST_ENC.
  ENDIF.


*&---------------------------------------------------------------------*
*&      Form  read_kernel
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM READ_KERNEL.

  DATA:
    L_KERN_REL             LIKE THLINES-THLINE,
    L_KERN_DBLIB           LIKE THLINES-THLINE,
    L_KERN_COMP_ON         LIKE THLINES-THLINE,
    L_KERN_COMP_TIME       LIKE THLINES-THLINE,
    L_KERN_PATCHLEVEL      LIKE THLINES-THLINE,
    L_THLINE               TYPE THLINES,
    LT_SUPP_DB_SAP         TYPE TABLE OF THLINES,
    LT_SUPP_DB_VENDOR      TYPE TABLE OF THLINES,
    LT_SUPP_OP_SYS         TYPE TABLE OF THLINES,
    LT_PATCHCOMMENT        TYPE TABLE OF THLINES.

  FORMAT COLOR COL_HEADING.
  WRITE: SY-ULINE, (120) 'System/Kernel information'(SYS), SY-ULINE.

  CALL FUNCTION 'TH_SAPREL2'
    IMPORTING
      KERN_REL        = L_KERN_REL
      KERN_DBLIB      = L_KERN_DBLIB
      KERN_COMP_ON    = L_KERN_COMP_ON
      KERN_COMP_TIME  = L_KERN_COMP_TIME
      KERN_PATCHLEVEL = L_KERN_PATCHLEVEL
    TABLES
      SUPP_DB_SAP     = LT_SUPP_DB_SAP
      SUPP_DB_VENDOR  = LT_SUPP_DB_VENDOR
      SUPP_OP_SYS     = LT_SUPP_OP_SYS
      PATCHCOMMENT    = LT_PATCHCOMMENT.

  FORMAT COLOR COL_BACKGROUND.
  WRITE: /(19) 'Kernelpatch'(PAT),        (100) L_KERN_PATCHLEVEL.
  WRITE: /(19) 'Kernelrelease'(REL),      (100) L_KERN_REL.
  WRITE: /(19) 'DB Library'(DBL),         (100) L_KERN_DBLIB.

  LOOP AT LT_SUPP_DB_SAP INTO L_THLINE.
    WRITE: /(19) 'SAP DB Version'(SDB),   (100) L_THLINE.
  ENDLOOP.

  LOOP AT LT_SUPP_DB_VENDOR INTO L_THLINE.
    WRITE: /(19) 'Database'(DAB),         (100) L_THLINE.
  ENDLOOP.

  LOOP AT LT_SUPP_OP_SYS INTO L_THLINE.
    WRITE: /(19) 'Operating System'(OPS), (100) L_THLINE.
  ENDLOOP.

ENDFORM.                    " read_kernel

*&---------------------------------------------------------------------*
*&      Form  read_profile
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM READ_PROFILE.

  DATA: L_STATUS LIKE SY-INDEX,
        L_NAME(60),
        L_WERT(60),
        L_SSFNAME(60),
        L_SSFAPILIB(60),
        L_SECLIBSAPSECU(60),

        BEGIN OF L_USUB OCCURS 0,
          STATUS LIKE L_STATUS,
          PNAME LIKE L_NAME,
          USER_WERT LIKE L_WERT,
          DEFAULT_WERT LIKE L_WERT,
        END OF L_USUB,

        BEGIN OF L_SUB OCCURS 0,
          STATUS LIKE L_STATUS,
          PNAME LIKE L_NAME,
          USER_WERT LIKE L_WERT,
          DEFAULT_WERT LIKE L_WERT,
        END OF L_SUB.

  FORMAT COLOR COL_HEADING.
  WRITE: SY-ULINE, (120) 'Profile parameter'(PRO), SY-ULINE.

  FREE L_USUB.
  FREE L_SUB.

  CALL 'C_SAPGALLPARAM' ID 'PAR_USUB' FIELD L_USUB-*SYS*
                        ID 'PAR_SUB'  FIELD L_SUB-*SYS*.

  FORMAT COLOR COL_BACKGROUND.

  LOOP AT L_SUB WHERE PNAME(3) = 'ssf' OR
                      PNAME(4) = 'sec/' OR
                      PNAME    = 'DIR_INSTANCE'.
    WRITE: / L_SUB-PNAME(30),
             L_SUB-USER_WERT(44),
             L_SUB-DEFAULT_WERT(44).
    IF L_SUB-USER_WERT IS INITIAL.
      L_WERT = L_SUB-DEFAULT_WERT.
    ELSE.
      L_WERT = L_SUB-USER_WERT.
    ENDIF.
    IF L_SUB-PNAME = 'DIR_INSTANCE'.
      G_DIR_INSTANCE = L_WERT.
    ELSEIF L_SUB-PNAME = 'ssf/name'.
      L_SSFNAME = L_WERT.
    ELSEIF L_SUB-PNAME = 'ssf/ssfapi_lib'.
      L_SSFAPILIB = L_WERT.
    ELSEIF L_SUB-PNAME = 'sec/libsapsecu'.
      L_SECLIBSAPSECU = L_WERT.
    ENDIF.
  ENDLOOP.

  IF L_SECLIBSAPSECU IS INITIAL.
    FORMAT COLOR COL_TOTAL.
    WRITE /(120) 'WARNING: profile parameter sec/libsapsecu' &
                 ' not set'(WPP).
    WRITE /(120) 'Set parameter to path and filename of' &
                 ' SAPSeculib or SAPCryptolib'(SPP).
    RETURN.
  ENDIF.

  IF L_SSFNAME = 'SAPSECULIB' AND L_SSFAPILIB <> L_SECLIBSAPSECU.
    FORMAT COLOR COL_NEGATIVE.
    WRITE /(120) 'ERROR: profile parameter sec/libsapsecu and' &
                 ' ssf/ssfapi_lib are different'(EPP).
    WRITE /(120) 'Set both parameter to path and filename of' &
                 ' SAPSeculib or SAPCryptolib'(SBP).
  ENDIF.

ENDFORM.                    " read_profile

*&---------------------------------------------------------------------*
*&      Form  read_environment
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM READ_ENVIRONMENT.

  DATA: ENVIRONMENT   TYPE TABLE OF THENV,
        ENVIRONMENT_S TYPE THENV,
        VARIABLE      TYPE TEXT80,
        VALUE         TYPE TEXT80,
        LEN           TYPE I.

  CALL FUNCTION 'TH_ENVIRONMENT'
    TABLES
      ENVIRONMENT = ENVIRONMENT.

  FORMAT COLOR COL_HEADING.
  WRITE: SY-ULINE, (120) 'Environment variables'(ENV), SY-ULINE.

  FORMAT COLOR COL_BACKGROUND.
  LOOP AT ENVIRONMENT INTO ENVIRONMENT_S.
    SPLIT ENVIRONMENT_S-LINE AT '=' INTO VARIABLE VALUE.
    IF VARIABLE = 'SECUDIR' OR VARIABLE = 'USER'.
      WRITE: / VARIABLE(19), VALUE.
    ENDIF.
    IF VARIABLE = 'SECUDIR'.
      LEN = STRLEN( G_DIR_INSTANCE ).
      IF VALUE IS INITIAL OR
         ( LEN > 0 AND VALUE(LEN) <> G_DIR_INSTANCE ).
        FORMAT COLOR COL_NEGATIVE.
        WRITE /(120) 'ERROR: environment variable SECUDIR not set' &
                     ' or has wrong value'(EEV).
        WRITE /(120) 'Should be set to subdirectory sec of' &
                     ' DIR_INSTANCE (profile parameter)'(SEV).
        FORMAT COLOR COL_BACKGROUND.
      ENDIF.
    ENDIF.
  ENDLOOP.

ENDFORM.

*&---------------------------------------------------------------------*
*&      Form  read_sapcrypto
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM READ_SAPCRYPTO.

  DATA:
    L_TXT               TYPE STRING,
    L_VERSION_DATA(255) TYPE C,
    L_VERSION_DATA_L    TYPE I,
    L_SSFRC             LIKE SSFPARMS-SSFCRC.


  FORMAT COLOR COL_HEADING.
  WRITE: SY-ULINE, (120) 'Version of SAPSeculib or SAPCryptolib'(VRS),
         SY-ULINE.

  CALL FUNCTION 'SSF_KRN_VERSION'
    EXPORTING
      SSFTOOLKIT             = 'SAPSECULIB'
    IMPORTING
      OSTR_OUTPUT_DATA       = L_VERSION_DATA
      OSTR_OUTPUT_DATA_L     = L_VERSION_DATA_L
      CRC                    = L_SSFRC
    EXCEPTIONS
      SSF_KRN_ERROR          = 1
      SSF_KRN_NOOP           = 2
      SSF_KRN_OPINV          = 3
      SSF_KRN_INVALID_PAR    = 4
      SSF_KRN_INVALID_PARLEN = 5
      OTHERS                 = 6.
  IF SY-SUBRC <> 0.
    FORMAT COLOR COL_NEGATIVE.
    WRITE: / 'ERROR: SSF_KRN_VERSION failed, SY-SUBRC ='(EVS), SY-SUBRC.
    WRITE: / 'In transaction ST11 check the developer trace'(TRC).
  ELSEIF L_SSFRC <> 0.
    FORMAT COLOR COL_NEGATIVE.
    WRITE: / 'ERROR: Could not determine version, CRC ='(EVC), L_SSFRC.
    WRITE: / 'Check installation of SAPSeculib or SAPCryptolib'(INS).
  ELSE.
    FORMAT COLOR COL_BACKGROUND.
    WRITE: / L_VERSION_DATA.
    WRITE: / L_VERSION_DATA+120.
  ENDIF.

ENDFORM.                    " read_sapcrypto

*&---------------------------------------------------------------------*
*&      Form  read_pse
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM READ_PSE
  CHANGING P_SUBJECT TYPE CERTATTR.

  DATA:
    L_STR                       TYPE TEXT80,
    L_PROFILE                   TYPE SSFPARMS-PAB,
    L_PROFILEPW                 TYPE SSFPARMS-PABPW,
    L_CERTIFICATE               TYPE SSFCERT,
    L_SUBJECT                   TYPE CERTATTR,
    L_ISSUER                    TYPE CERTATTR,
    L_SERIALNO                  TYPE CERTATTR,
    L_VALIDFROM                 TYPE SSFUTC,
    L_VALIDTO                   TYPE SSFUTC,
    L_ALGID                     TYPE CERTATTR,
    L_FINGERPRINT               TYPE CERTATTR,
    L_SUMMARY                   TYPE CERTATTR,
    L_ALL                       TYPE CERTATTR,
    L_CERTLIST                  TYPE SSFCERTLIN,
    LT_CERTLIST                 TYPE TABLE OF SSFCERTLIN,
    L_DATEFROM                  TYPE D,
    L_TIMEFROM                  TYPE T,
    L_DATETO                    TYPE D,
    L_TIMETO                    TYPE T.

  FORMAT COLOR COL_HEADING.
  WRITE: SY-ULINE, (120) 'Overview of certificates from PSE'(CER),
         SY-ULINE.

  L_PROFILE = PA_PSE.
  L_PROFILEPW = PA_PIN.

* read own certificate from pse
  CALL FUNCTION 'SSFC_GETOWNCERTIFICATE'
    EXPORTING
      PROFILE               = L_PROFILE
      PROFILEPW             = L_PROFILEPW
    IMPORTING
      CERTIFICATE           = L_CERTIFICATE
    EXCEPTIONS
      SSF_KRN_ERROR         = 1
      SSF_KRN_NOMEMORY      = 2
      SSF_KRN_NOSSFLIB      = 3
      SSF_KRN_INVALID_PAR   = 4
      SSF_KRN_NOCERTIFICATE = 5
      OTHERS                = 6.

  IF SY-SUBRC <> 0.
    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
        WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4 INTO L_STR.
    CONCATENATE 'ERROR -' L_STR INTO L_STR SEPARATED BY SPACE.
    FORMAT COLOR COL_NEGATIVE.
    WRITE: /(120) L_STR.
    WRITE: /(120) 'Check the PSE-file (Transaction AL11)' &
                  ' and the corresponding credentials'(CPC),
                  SY-ULINE.
    RETURN.
  ENDIF.

* parse own certificate from pse
  CALL FUNCTION 'SSFC_PARSECERTIFICATE'
    EXPORTING
      CERTIFICATE         = L_CERTIFICATE
    IMPORTING
      SUBJECT             = P_SUBJECT
      ISSUER              = L_ISSUER
      SERIALNO            = L_SERIALNO
      VALIDFROM           = l_validfrom
      VALIDTO             = l_validto
      ALGID               = g_algid
      FINGERPRINT         = L_FINGERPRINT
      SUMMARY             = L_SUMMARY
      ALL                 = L_ALL
    EXCEPTIONS
      SSF_KRN_ERROR       = 1
      SSF_KRN_NOMEMORY    = 2
      SSF_KRN_NOSSFLIB    = 3
      SSF_KRN_INVALID_PAR = 4
      OTHERS              = 5.
  IF SY-SUBRC <> 0.
    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
       WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4 INTO L_STR.
    CONCATENATE 'ERROR -' L_STR INTO L_STR SEPARATED BY SPACE.
    FORMAT COLOR COL_NEGATIVE.
    WRITE: /(120) L_STR.
  ENDIF.

* write own certificate from pse
  FORMAT COLOR COL_BACKGROUND.
  WRITE: /(19) 'Subject (DN)'(SUB), (100) P_SUBJECT.
  WRITE: /(19) 'Issuer'(ISS),       (100) L_ISSUER.

  PERFORM UTC2DATETIME
    USING    L_VALIDFROM
    CHANGING L_DATEFROM L_TIMEFROM.
  PERFORM UTC2DATETIME
    USING    L_VALIDTO
    CHANGING L_DATETO L_TIMETO.

  WRITE: /(19) 'Validity'(VAL), L_DATEFROM DD/MM/YYYY,
                                L_TIMEFROM DD/MM/YYYY,
                                L_DATETO   DD/MM/YYYY,
                                L_TIMETO   DD/MM/YYYY.

  IF SY-DATUM > L_DATETO OR
   ( SY-DATUM = L_DATETO AND SY-UZEIT > L_TIMETO ).
    FORMAT COLOR COL_NEGATIVE.
    WRITE: /(120) 'ERROR: certificate is expired'(EXP).
    WRITE: /(120) 'Request a new certificate from your CA'(REQ).
    FORMAT COLOR COL_BACKGROUND.
  ENDIF.

  WRITE: /(19) 'Algorithm'(ALG), (100) g_algid.

* check credentials if PIN is supplied
  IF NOT L_PROFILEPW IS INITIAL.
    CALL FUNCTION 'SSFC_GETOWNCERTIFICATE'
      EXPORTING
        PROFILE               = L_PROFILE
        PROFILEPW             = SPACE
      IMPORTING
        CERTIFICATE           = L_CERTIFICATE
      EXCEPTIONS
        SSF_KRN_ERROR         = 1
        SSF_KRN_NOMEMORY      = 2
        SSF_KRN_NOSSFLIB      = 3
        SSF_KRN_INVALID_PAR   = 4
        SSF_KRN_NOCERTIFICATE = 5
        OTHERS                = 6.
    IF SY-SUBRC <> 0.
      FORMAT COLOR COL_TOTAL.
      CONCATENATE 'WARNING: No credentials available for'(NOC) L_PROFILE
             INTO L_STR SEPARATED BY SPACE.
      WRITE: /(120) L_STR.
      WRITE: /(120) 'Create credentials in transaction STRUST or with' &
                    ' the sapgenpse tool'(CCR).
      RETURN.
    ENDIF.
  ENDIF.

* read certificate list from pse
  FORMAT COLOR COL_BACKGROUND.
  WRITE: / 'Certificate list'(LST).
  CALL FUNCTION 'SSFC_GETCERTIFICATELIST'
    EXPORTING
      PROFILE               = L_PROFILE
      PROFILEPW             = L_PROFILEPW
    TABLES
      CERTIFICATELIST       = LT_CERTLIST
    EXCEPTIONS
      SSF_KRN_ERROR         = 1
      SSF_KRN_NOMEMORY      = 2
      SSF_KRN_NOSSFLIB      = 3
      SSF_KRN_INVALID_PAR   = 4
      SSF_KRN_NOCERTIFICATE = 5
      OTHERS                = 6.
  IF SY-SUBRC <> 0.
    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
        WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4 INTO L_STR.
    CONCATENATE 'ERROR -' L_STR INTO L_STR SEPARATED BY SPACE.
    FORMAT COLOR COL_NEGATIVE.
    WRITE: / L_STR.
    RETURN.
  ENDIF.

* parse certificate list from pse
  LOOP AT LT_CERTLIST INTO L_CERTLIST.
    CLEAR: L_SUBJECT, L_ISSUER, L_SERIALNO, L_VALIDFROM, L_VALIDTO,
           L_ALGID, L_FINGERPRINT, L_SUMMARY, L_ALL.
    CALL FUNCTION 'SSFC_PARSECERTIFICATE'
      EXPORTING
        CERTIFICATE         = L_CERTLIST-CERT
      IMPORTING
        SUBJECT             = L_SUBJECT
        ISSUER              = L_ISSUER
        SERIALNO            = L_SERIALNO
        VALIDFROM           = l_validfrom
        VALIDTO             = l_validto
        ALGID               = l_algid
        FINGERPRINT         = L_FINGERPRINT
        SUMMARY             = L_SUMMARY
        ALL                 = L_ALL
      EXCEPTIONS
        SSF_KRN_ERROR       = 1
        SSF_KRN_NOMEMORY    = 2
        SSF_KRN_NOSSFLIB    = 3
        SSF_KRN_INVALID_PAR = 4
        OTHERS              = 5.
    IF SY-SUBRC <> 0.
      MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
         WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4 INTO L_STR.
      CONCATENATE 'ERROR -' L_STR INTO L_STR SEPARATED BY SPACE.
      FORMAT COLOR COL_NEGATIVE.
      WRITE: / L_STR.
    ELSE.
      FORMAT COLOR COL_BACKGROUND.
      WRITE: /5(116) L_SUBJECT.
    ENDIF.
  ENDLOOP.

  FORMAT COLOR COL_BACKGROUND.
  WRITE: / SY-ULINE.

ENDFORM.                    " read_pse

*&---------------------------------------------------------------------*
*&      Form  READ_SSFARGS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM READ_SSFARGS.

  DATA: L_SSFARGS TYPE SSFARGS,
        L_STR     TYPE STRING.

  FORMAT COLOR COL_HEADING.
  WRITE: SY-ULINE, (120) 'SSF Customizing (Transaction SSFA)'(CUS),
         SY-ULINE.

  FORMAT COLOR COL_BACKGROUND.
  SELECT SINGLE * FROM SSFARGS
                  INTO L_SSFARGS
                  WHERE PROFILE = PA_PSE.

  IF SY-SUBRC = 0.
    CONCATENATE 'SSF application'(APP) L_SSFARGS-APPLIC
                'defined for filename'(DEF) PA_PSE
           INTO L_STR SEPARATED BY SPACE.
    WRITE: / L_STR.
    IF L_SSFARGS-PROFILEID IS INITIAL.
      WRITE: /(7) 'ID', '<empty>'(EMP).
    ELSE.
      WRITE: /(7) 'ID', L_SSFARGS-PROFILEID(112).
    ENDIF.
    IF NOT L_SSFARGS-PROFILEID IS INITIAL AND
       NOT G_SUBJECT IS INITIAL           AND
       L_SSFARGS-PROFILEID <> G_SUBJECT.
      FORMAT COLOR COL_NEGATIVE.
      WRITE: /(120) 'ERROR: table entry contains wrong ID'(WRG).
      CONCATENATE 'In transaction SSFA set SSF-Profile-ID'(SPI)
        G_SUBJECT INTO L_STR SEPARATED BY SPACE.
      WRITE: /(120) L_STR.
      FORMAT COLOR COL_BACKGROUND.
    ENDIF.
  ELSE.
    CONCATENATE 'No SSF application defined for filename'(NOA) PA_PSE
           INTO L_STR SEPARATED BY SPACE.
    WRITE: / L_STR.
  ENDIF.

ENDFORM.                    " read_ssfa

*&---------------------------------------------------------------------*
*&      Form  TEST_SIG
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM TEST_SIG.

  DATA: CRC                LIKE SSFPARMS-SSFCRC,
        OSTR_INPUT_DATA_L  LIKE SSFPARMS-INDATALEN,
        OSTR_INPUT_DATA    TYPE TABLE OF SSFBIN,
        OSTR_INPUT_DATA_S  TYPE SSFBIN,
        SIGNER             TYPE TABLE OF SSFINFO,
        SIGNER_S           TYPE SSFINFO,
        OSTR_SIGNED_DATA_L LIKE SSFPARMS-SIGDATALEN,
        OSTR_SIGNED_DATA   TYPE TABLE OF SSFBIN,
        MSG                TYPE TEXT80.

  FORMAT COLOR COL_HEADING.
  WRITE: SY-ULINE, (120) 'Test signature'(TSI), SY-ULINE.

  OSTR_INPUT_DATA_L = 12.
  OSTR_INPUT_DATA_S-BINDATA = '48656C6C6F20776F726C6421'. "Hello world!
  APPEND OSTR_INPUT_DATA_S TO OSTR_INPUT_DATA.

  SIGNER_S-ID = G_SUBJECT.
  SIGNER_S-PROFILE = PA_PSE.
  SIGNER_S-PASSWORD = PA_PIN.
  APPEND SIGNER_S TO SIGNER.

  CALL FUNCTION 'SSF_KRN_SIGN'
    EXPORTING
      SSFTOOLKIT                         = 'SAPSECULIB'
*     STR_FORMAT                         = 'PKCS7'
*     B_INC_CERTS                        = ' '
*     B_DETACHED                         = ' '
*     B_INENC                            = 'X'
*     IO_SPEC                            = 'T'
      OSTR_INPUT_DATA_L                  = OSTR_INPUT_DATA_L
*     STR_HASHALG                        =
    IMPORTING
      OSTR_SIGNED_DATA_L                 = OSTR_SIGNED_DATA_L
      CRC                                = CRC
    TABLES
      OSTR_INPUT_DATA                    = OSTR_INPUT_DATA
      SIGNER                             = SIGNER
      OSTR_SIGNED_DATA                   = OSTR_SIGNED_DATA
    EXCEPTIONS
      SSF_KRN_ERROR                      = 1
      SSF_KRN_NOOP                       = 2
      SSF_KRN_NOMEMORY                   = 3
      SSF_KRN_OPINV                      = 4
      SSF_KRN_NOSSFLIB                   = 5
      SSF_KRN_SIGNER_LIST_ERROR          = 6
      SSF_KRN_INPUT_DATA_ERROR           = 7
      SSF_KRN_INVALID_PAR                = 8
      SSF_KRN_INVALID_PARLEN             = 9
      SSF_FB_INPUT_PARAMETER_ERROR       = 10
      OTHERS                             = 11.
  IF SY-SUBRC = 0 AND CRC = 0.
    FORMAT COLOR COL_BACKGROUND.
    WRITE: /(120) 'Signature OK'(SOK).
  ELSE.
    IF CRC <> 0.
      READ TABLE SIGNER INDEX 1 INTO SIGNER_S.
      CALL FUNCTION 'SSF_ERRORMESSAGE'
        EXPORTING
          CRC       = CRC
          RESULT    = SIGNER_S-RESULT
        IMPORTING
          ERRORTEXT = MSG
        EXCEPTIONS
          ERROR     = 1
          OTHERS    = 2.
      IF SY-SUBRC = 0.
        CONCATENATE 'Signature ERROR -'(SER) MSG
               INTO MSG SEPARATED BY SPACE.
      ELSE.
        MSG = 'Signature ERROR - no message'(SNM).
      ENDIF.
    ELSE. "SY-SUBRC <> 0
      WRITE SY-SUBRC TO MSG LEFT-JUSTIFIED.
      CONCATENATE 'Signature SY-SUBRC ='(SRC) MSG
             INTO MSG SEPARATED BY SPACE.
    ENDIF.
    FORMAT COLOR COL_NEGATIVE.
    WRITE: /(120) MSG.
    RETURN.
  ENDIF.

  CLEAR: SIGNER, CRC.

  CALL FUNCTION 'SSF_KRN_VERIFY'
    EXPORTING
      SSFTOOLKIT                         = 'SAPSECULIB'
*     STR_FORMAT                         = 'PKCS7'
*     B_INC_CERTS                        = 'X'
*     B_INENC                            = 'X'
*     B_OUTDEC                           = 'X'
*     IO_SPEC                            = 'T'
      OSTR_SIGNED_DATA_L                 = OSTR_SIGNED_DATA_L
*     OSTR_INPUT_DATA_L                  = 0
      STR_PAB                            = PA_PSE
      STR_PAB_PASSWORD                   = PA_PIN
    IMPORTING
*     OSTR_OUTPUT_DATA_L                 =
      CRC                                = CRC
    TABLES
      OSTR_SIGNED_DATA                   = OSTR_SIGNED_DATA
*     OSTR_INPUT_DATA                    =
      SIGNER_RESULT_LIST                 = SIGNER
      OSTR_OUTPUT_DATA                   = OSTR_INPUT_DATA
*     CERTIFICATELIST                    =
    EXCEPTIONS
      SSF_KRN_ERROR                      = 1
      SSF_KRN_NOOP                       = 2
      SSF_KRN_NOMEMORY                   = 3
      SSF_KRN_OPINV                      = 4
      SSF_KRN_NOSSFLIB                   = 5
      SSF_KRN_INPUT_DATA_ERROR           = 6
      SSF_KRN_INVALID_PAR                = 7
      SSF_KRN_INVALID_PARLEN             = 8
      SSF_FB_INPUT_PARAMETER_ERROR       = 9
      OTHERS                             = 10.
  IF SY-SUBRC = 0 AND CRC = 0.
    FORMAT COLOR COL_BACKGROUND.
    WRITE: /(120) 'Verification OK'(VOK).
  ELSE.
    IF CRC <> 0.
      READ TABLE SIGNER INDEX 1 INTO SIGNER_S.
      CALL FUNCTION 'SSF_ERRORMESSAGE'
        EXPORTING
          CRC       = CRC
          RESULT    = SIGNER_S-RESULT
        IMPORTING
          ERRORTEXT = MSG
        EXCEPTIONS
          ERROR     = 1
          OTHERS    = 2.
      IF SY-SUBRC = 0.
        CONCATENATE 'Verification ERROR -'(VER) MSG
               INTO MSG SEPARATED BY SPACE.
      ELSE.
        MSG = 'Verification ERROR - no message'(VNM).
      ENDIF.
    ELSE. "SY-SUBRC <> 0
      WRITE SY-SUBRC TO MSG LEFT-JUSTIFIED.
      CONCATENATE 'Verification SY-SUBRC ='(VRC) MSG
             INTO MSG SEPARATED BY SPACE.
    ENDIF.
    FORMAT COLOR COL_NEGATIVE.
    WRITE: /(120) MSG.
  ENDIF.

ENDFORM.

*&---------------------------------------------------------------------*
*&      Form  TEST_ENC
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM TEST_ENC.

  DATA: CRC                   LIKE SSFPARMS-SSFCRC,
        OSTR_INPUT_DATA_L     LIKE SSFPARMS-INDATALEN,
        OSTR_INPUT_DATA       TYPE TABLE OF SSFBIN,
        OSTR_INPUT_DATA_S     TYPE SSFBIN,
        RECIPIENT             TYPE TABLE OF SSFINFO,
        RECIPIENT_S           TYPE SSFINFO,
        OSTR_ENVELOPED_DATA_L LIKE SSFPARMS-SIGDATALEN,
        OSTR_ENVELOPED_DATA   TYPE TABLE OF SSFBIN,
        MSG                   TYPE TEXT80.

  FORMAT COLOR COL_HEADING.
  WRITE: SY-ULINE, (120) 'Test encryption'(TEN), SY-ULINE.

  IF G_ALGID = 'id-dsa (OID OID 1.2.840.10040.4.1)'.
    FORMAT COLOR COL_NEGATIVE.
    WRITE: /(120) 'ERROR: certificate does not allow encryption'(ENE).
    WRITE: /(120) 'For encryption use a PSE with RSA algorithm'(RSA).
    RETURN.
  ENDIF.

  OSTR_INPUT_DATA_L = 12.
  OSTR_INPUT_DATA_S-BINDATA = '48656C6C6F20776F726C6421'. "Hello world!
  APPEND OSTR_INPUT_DATA_S TO OSTR_INPUT_DATA.

  RECIPIENT_S-ID = G_SUBJECT.
  APPEND RECIPIENT_S TO RECIPIENT.

  CALL FUNCTION 'SSF_KRN_ENVELOPE'
    EXPORTING
      SSFTOOLKIT                         = 'SAPSECULIB'
*     STR_FORMAT                         = 'PKCS7'
*     B_INENC                            = 'X'
*     IO_SPEC                            = 'T'
      OSTR_INPUT_DATA_L                  = OSTR_INPUT_DATA_L
      STR_PAB                            = PA_PSE
      STR_PAB_PASSWORD                   = PA_PIN
*     STR_SYM_ENCR_ALG                   =
    IMPORTING
      OSTR_ENVELOPED_DATA_L              = OSTR_ENVELOPED_DATA_L
      CRC                                = CRC
    TABLES
      OSTR_INPUT_DATA                    = OSTR_INPUT_DATA
      RECIPIENT_LIST                     = RECIPIENT
      OSTR_ENVELOPED_DATA                = OSTR_ENVELOPED_DATA
    EXCEPTIONS
      SSF_KRN_ERROR                      = 1
      SSF_KRN_NOOP                       = 2
      SSF_KRN_NOMEMORY                   = 3
      SSF_KRN_OPINV                      = 4
      SSF_KRN_NOSSFLIB                   = 5
      SSF_KRN_RECIPIENT_LIST_ERROR       = 6
      SSF_KRN_INPUT_DATA_ERROR           = 7
      SSF_KRN_INVALID_PAR                = 8
      SSF_KRN_INVALID_PARLEN             = 9
      SSF_FB_INPUT_PARAMETER_ERROR       = 10
      OTHERS                             = 11.
  IF SY-SUBRC = 0 AND CRC = 0.
    FORMAT COLOR COL_BACKGROUND.
    WRITE: /(120) 'Encryption OK'(EOK).
  ELSE.
    IF CRC <> 0.
      READ TABLE RECIPIENT INDEX 1 INTO RECIPIENT_S.
      CALL FUNCTION 'SSF_ERRORMESSAGE'
        EXPORTING
          CRC       = CRC
          RESULT    = RECIPIENT_S-RESULT
        IMPORTING
          ERRORTEXT = MSG
        EXCEPTIONS
          ERROR     = 1
          OTHERS    = 2.
      IF SY-SUBRC = 0.
        CONCATENATE 'Encryption ERROR -'(EER) MSG
               INTO MSG SEPARATED BY SPACE.
      ELSE.
        MSG = 'Encryption ERROR - no message'(ENM).
      ENDIF.
    ELSE. "SY-SUBRC <> 0
      WRITE SY-SUBRC TO MSG LEFT-JUSTIFIED.
      CONCATENATE 'Encryption SY-SUBRC ='(ERC) MSG
             INTO MSG SEPARATED BY SPACE.
    ENDIF.
    FORMAT COLOR COL_NEGATIVE.
    WRITE: /(120) MSG.
    RETURN.
  ENDIF.

  CLEAR: RECIPIENT, CRC.
  RECIPIENT_S-ID = G_SUBJECT.
  RECIPIENT_S-PROFILE = PA_PSE.
  RECIPIENT_S-PASSWORD = PA_PIN.
  APPEND RECIPIENT_S TO RECIPIENT.

  CALL FUNCTION 'SSF_KRN_DEVELOPE'
    EXPORTING
      SSFTOOLKIT                         = 'SAPSECULIB'
*     STR_FORMAT                         = 'PKCS7'
*     B_OUTDEC                           = 'X'
*     IO_SPEC                            = 'T'
      OSTR_ENVELOPED_DATA_L              = OSTR_ENVELOPED_DATA_L
    IMPORTING
*     OSTR_OUTPUT_DATA_L                 =
      CRC                                = CRC
    TABLES
      OSTR_ENVELOPED_DATA                = OSTR_ENVELOPED_DATA
      RECIPIENT                          = RECIPIENT
      OSTR_OUTPUT_DATA                   = OSTR_INPUT_DATA
    EXCEPTIONS
      SSF_KRN_ERROR                      = 1
      SSF_KRN_NOOP                       = 2
      SSF_KRN_NOMEMORY                   = 3
      SSF_KRN_OPINV                      = 4
      SSF_KRN_NOSSFLIB                   = 5
      SSF_KRN_RECIPIENT_ERROR            = 6
      SSF_KRN_INPUT_DATA_ERROR           = 7
      SSF_KRN_INVALID_PAR                = 8
      SSF_KRN_INVALID_PARLEN             = 9
      SSF_FB_INPUT_PARAMETER_ERROR       = 10
      OTHERS                             = 11.
  IF SY-SUBRC = 0 AND CRC = 0.
    FORMAT COLOR COL_BACKGROUND.
    WRITE: /(120) 'Decryption OK'(DOK).
  ELSE.
    IF CRC <> 0.
      READ TABLE RECIPIENT INDEX 1 INTO RECIPIENT_S.
      CALL FUNCTION 'SSF_ERRORMESSAGE'
        EXPORTING
          CRC       = CRC
          RESULT    = RECIPIENT_S-RESULT
        IMPORTING
          ERRORTEXT = MSG
        EXCEPTIONS
          ERROR     = 1
          OTHERS    = 2.
      IF SY-SUBRC = 0.
        CONCATENATE 'Decryption ERROR -'(DER) MSG
               INTO MSG SEPARATED BY SPACE.
      ELSE.
        MSG = 'Decryption ERROR - no message'(DNM).
      ENDIF.
    ELSE. "SY-SUBRC <> 0
      WRITE SY-SUBRC TO MSG LEFT-JUSTIFIED.
      CONCATENATE 'Decryption SY-SUBRC ='(DRC) MSG
             INTO MSG SEPARATED BY SPACE.
    ENDIF.
    FORMAT COLOR COL_NEGATIVE.
    WRITE: /(120) MSG.
  ENDIF.

ENDFORM.

*&---------------------------------------------------------------------*
*&      Form  UTC2DATETIME
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_UTC       text
*      <--P_DATE      date
*      <--P_TIME      time
*----------------------------------------------------------------------*
FORM UTC2DATETIME
     USING    P_UTC      TYPE CSEQUENCE
     CHANGING P_DATE     TYPE D
              P_TIME     TYPE T.

  DATA: LEN     TYPE I.
  DATA: YEAR    TYPE I.
  DATA: DATE(8) TYPE C.
  DATA: TIME(6) TYPE C.
  DATA: DUMMY   TYPE C.                                     "#EC NEEDED

  IF NOT P_UTC IS INITIAL.
    LEN = STRLEN( P_UTC ).
    IF LEN = 15.        "IETF RFC 3280, 4.1.2.5.2  GeneralizedTime
      DATE = P_UTC(8).
      TIME = P_UTC+8.
    ELSE.               "IETF RFC 3280, 4.1.2.5.1  UTCTime
      YEAR = P_UTC(2).
      IF YEAR GE 50.
        DATE(2) = '19'.
      ELSE.
        DATE(2) = '20'.
      ENDIF.
      DATE+2 = P_UTC(6).
      TIME   = P_UTC+6.
      FIND 'Z' IN TIME.
      IF SY-SUBRC = 0.
        SPLIT TIME AT 'Z' INTO TIME DUMMY.
        CONCATENATE TIME '000000' INTO TIME.
      ENDIF.
    ENDIF.
  ENDIF. "not p_utc is initial

  P_DATE = DATE.
  P_TIME = TIME.

ENDFORM.                    " UTC2DATETIME

*Selection text£º
*PA_ENC:        Encrytion
*PA_PIN:D       .
*PA_PSE:D       .
*PA_SIG:        Signature
