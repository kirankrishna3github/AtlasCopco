*&---------------------------------------------------------------------*
*&  Include           ZLM_ABAP_OO_VEHICLE
*&---------------------------------------------------------------------*
INTERFACE lintf_vehicle.
  METHODs: getmaxspeed EXPORTING ex_maxspeed TYPE i,
           getcurrentspeed EXPORTING ex_currentspeed TYPE i.
ENDINTERFACE.                    "lintf_vehicle

*----------------------------------------------------------------------*
*       CLASS lcl_vehicle DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_vehicle DEFINITION ABSTRACT.
  PUBLIC SECTION.

    INTERFACES: lintf_vehicle.

    METHODS: constructor IMPORTING im_maxspeed TYPE i
                                   im_currentspeed TYPE i,
*             getcurrentspeed EXPORTING ex_currentspeed TYPE i,
             setcurrentspeed IMPORTING im_currentspeed TYPE i,
*             getmaxspeed EXPORTING ex_maxspeed TYPE i,
             setmaxspeed IMPORTING im_maxspeed TYPE i,
             displayattributes,
             accelerate ABSTRACT RAISING zcx_lme_test.

    CLASS-METHODS: displaynbofvehicles RETURNING value(nbov) TYPE i.

  PROTECTED SECTION.
    CLASS-DATA: nbofvehicles TYPE i.

    DATA: maxspeed TYPE i,
          currentspeed TYPE i.

ENDCLASS.                    "lcl_vehicle DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcl_vehicle IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_vehicle IMPLEMENTATION.
  METHOD constructor.
    setmaxspeed( im_maxspeed ).
    setcurrentspeed( im_currentspeed ).
    nbofvehicles = nbofvehicles + 1.
  ENDMETHOD.                    "constructor

  METHOD lintf_vehicle~getcurrentspeed.
    ex_currentspeed = currentspeed.
  ENDMETHOD.                    "getCurrentSpeed

  METHOD setcurrentspeed.
    currentspeed = im_currentspeed.
  ENDMETHOD.                    "setCurrentSpeed

  METHOD lintf_vehicle~getmaxspeed.
    ex_maxspeed = maxspeed.
  ENDMETHOD.                    "getmaxSpeed

  METHOD setmaxspeed.
    maxspeed = im_maxspeed.
  ENDMETHOD.                    "setmaxSpeed

  METHOD displayattributes.
    WRITE: / 'Maxspeed :', maxspeed,
           / 'Currentspeed :', currentspeed.
  ENDMETHOD.                    "displayattributes

  METHOD displaynbofvehicles.
    WRITE: / 'Nb. of vehicles :', nbofvehicles.
  ENDMETHOD.                    "displaynbofvehicles

ENDCLASS.                    "lcl_vehicle IMPLEMENTATION

*----------------------------------------------------------------------*
*       CLASS lcl_car DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_car DEFINITION INHERITING FROM lcl_vehicle.
  PUBLIC SECTION.
    METHODS: constructor IMPORTING im_maxspeed TYPE i
                                   im_currentspeed TYPE i
                                   im_cartype TYPE c,
             getcartype EXPORTING ex_cartype TYPE c,
*             setcartype IMPORTING im_cartype TYPE c,
             displayattributes REDEFINITION,
             accelerate REDEFINITION.

  PRIVATE SECTION.
    DATA: cartype(10) TYPE c.
    METHODS setcartype IMPORTING im_cartype TYPE c.
ENDCLASS.                    "lcl_car DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcl_car IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_car IMPLEMENTATION.
  METHOD constructor.
    super->constructor( im_maxspeed = im_maxspeed
                        im_currentspeed = im_currentspeed ).
    setcartype( im_cartype ).
  ENDMETHOD.                    "constructor

  METHOD getcartype.
    ex_cartype = cartype.
  ENDMETHOD.                    "getcartype

  METHOD setcartype.
    cartype = im_cartype.
  ENDMETHOD.                    "setcartype

  METHOD accelerate.
    DATA: lv_currentspeed TYPE i,
          lv_maxspeed TYPE i.

    lintf_vehicle~getcurrentspeed( IMPORTING ex_currentspeed = lv_currentspeed ).
    lintf_vehicle~getmaxspeed( IMPORTING ex_maxspeed = lv_maxspeed ).
    IF lv_currentspeed > 0.
      lv_currentspeed = lv_currentspeed + 5.
    ELSE.
      lv_currentspeed = 1.
    ENDIF.

    IF lv_currentspeed > lv_maxspeed.
      "maximum speed is reached, do not increase speed anymore
      RAISE EXCEPTION TYPE zcx_lme_test
        EXPORTING
          textid = zcx_lme_test=>too_fast.
    ELSE.
      setcurrentspeed( lv_currentspeed ).
    ENDIF.
  ENDMETHOD.                    "accelarate

  METHOD displayattributes.
    super->displayattributes( ).
    WRITE: / 'Cartype', cartype.
  ENDMETHOD.                    "displayattributes
ENDCLASS.                    "lcl_car IMPLEMENTATION

*----------------------------------------------------------------------*
*       CLASS lcl_truck DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_truck DEFINITION INHERITING FROM lcl_vehicle.
  PUBLIC SECTION.
    METHODS: constructor IMPORTING im_maxspeed TYPE i
                                   im_currentspeed TYPE i
                                   im_maxcargo TYPE i,
             getmaxcargo EXPORTING ex_maxcargo TYPE i,
             setmaxcargo IMPORTING im_maxcargo TYPE i,
             displayattributes REDEFINITION,
             accelerate REDEFINITION.

  PRIVATE SECTION.
    DATA: maxcargo TYPE i.
ENDCLASS.                    "lcl_truck DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcl_truck IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_truck IMPLEMENTATION.
  METHOD constructor.
    super->constructor( im_maxspeed = im_maxspeed
                        im_currentspeed = im_currentspeed ).
    setmaxcargo( im_maxcargo ).
  ENDMETHOD.                    "constructor

  METHOD getmaxcargo.
    ex_maxcargo = maxcargo.
  ENDMETHOD.                    "getmaxcargo

  METHOD setmaxcargo.
    maxcargo = im_maxcargo.
  ENDMETHOD.                    "setmaxcargo

  METHOD accelerate.
    DATA: lv_currentspeed TYPE i,
          lv_maxspeed TYPE i.

    lintf_vehicle~getcurrentspeed( IMPORTING ex_currentspeed = lv_currentspeed ).
    lintf_vehicle~getmaxspeed( IMPORTING ex_maxspeed = lv_maxspeed ).
    IF lv_currentspeed > 0.
      lv_currentspeed = lv_currentspeed + 2.
    ELSE.
      lv_currentspeed = 1.
    ENDIF.

    IF lv_currentspeed > lv_maxspeed.
      "maximum speed is reached, do not increase speed anymore
    ELSE.
      setcurrentspeed( lv_currentspeed ).
    ENDIF.
  ENDMETHOD.                    "accelarate

  METHOD displayattributes.
    super->displayattributes( ).
    WRITE: / 'Maxcargo', maxcargo.
  ENDMETHOD.                    "displayattributes
ENDCLASS.                    "lcl_truck IMPLEMENTATION
