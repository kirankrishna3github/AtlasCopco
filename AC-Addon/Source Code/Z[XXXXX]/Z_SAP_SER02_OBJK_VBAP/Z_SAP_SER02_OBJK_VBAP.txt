***********************************************************************
*&---------------------------------------------------------------------*
*& Report  Z_SAP_SER02_OBJK_VBAP                                       *
*&                                                                     *
*&---------------------------------------------------------------------*
* This report finds records in table SER02 which have no belonging     *
* sales document item.                                                 *
* If ptest is initial the records found are deleted from table SER02   *
* A list of the deleted records is created.                            *
* Where do SER02's without belonging sales document items come from?   *
* - They were created when technical objects were deleted from the     *
*   object list of SD documents (orders, contracts, ...) before the    *
*   implementation of notes 359535 and 569269 or with a support        *
*   package level less than SAPKH46B48 or SAPKH46C39 or SAPKH47005.    *
*----------------------------------------------------------------------*
REPORT  z_sap_ser02_objk_vbap.

* global list of selected data
DATA: BEGIN OF GT_CONTRACTS OCCURS 0,
        OBKNR   LIKE SER02-OBKNR,
        ANZSN   LIKE SER02-ANZSN,
        SDAUFNR LIKE VBAP-VBELN,
        POSNR   LIKE VBAP-POSNR,
        OBJKS   LIKE SY-TABIX,
      END OF GT_CONTRACTS.

FIELD-SYMBOLS: <FS_CONTRACTS> LIKE GT_CONTRACTS.

PARAMETERS PTEST AS CHECKBOX DEFAULT 'X'.
*************************************************************
START-OF-SELECTION.

* get ser02s
  SELECT * FROM SER02 AS S
    INTO CORRESPONDING FIELDS OF TABLE GT_CONTRACTS
    WHERE NOT EXISTS
      ( SELECT * FROM VBAP
          WHERE VBELN = S~SDAUFNR
          AND   POSNR = S~POSNR ).
* count objks per ser02
  LOOP AT GT_CONTRACTS.
    SELECT COUNT(*) FROM OBJK INTO GT_CONTRACTS-OBJKS
      WHERE OBKNR = GT_CONTRACTS-OBKNR.
    MODIFY GT_CONTRACTS.
  ENDLOOP.

  IF GT_CONTRACTS[] IS INITIAL.
    WRITE: / 'No contracts with missing object lists found'.
  ELSE.
* output with hides for line selection
    LOOP AT GT_CONTRACTS.
      WRITE: / GT_CONTRACTS-SDAUFNR, GT_CONTRACTS-POSNR,
               GT_CONTRACTS-ANZSN, GT_CONTRACTS-OBKNR,
        '     Anzahl OBJK-Eintr#ge:', GT_CONTRACTS-OBJKS.
      HIDE: GT_CONTRACTS-SDAUFNR, GT_CONTRACTS-POSNR.
    ENDLOOP.
  ENDIF.

  IF PTEST IS INITIAL.
    PERFORM SER02_OBJK_DELETE.
  ENDIF.

AT LINE-SELECTION.

* try to display sd order
  SET PARAMETER ID 'AUN' FIELD GT_CONTRACTS-SDAUFNR.
  CALL TRANSACTION 'VA43' AND SKIP FIRST SCREEN.

***********************************************************
FORM SER02_OBJK_DELETE.

  LOOP AT GT_CONTRACTS
       ASSIGNING <FS_CONTRACTS>.
    DELETE FROM SER02 WHERE OBKNR = <FS_CONTRACTS>-OBKNR.
    IF NOT <FS_CONTRACTS>-OBJKS IS INITIAL.
      DELETE FROM OBJK  WHERE OBKNR = <FS_CONTRACTS>-OBKNR.
    ENDIF.
  ENDLOOP.

ENDFORM.
***************************************************************************
***************************************************************************
*&---------------------------------------------------------------------*
*& Report  Z_SAP_SER02_OBJK_VBAP                                       *
*&                                                                     *
*&---------------------------------------------------------------------*
*&                                                                     *
*&                                                                     *

*&---------------------------------------------------------------------*
*Selection text£º
*PTEST:        TEST?
