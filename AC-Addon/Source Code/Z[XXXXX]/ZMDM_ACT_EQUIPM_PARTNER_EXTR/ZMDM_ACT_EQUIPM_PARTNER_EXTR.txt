*&---------------------------------------------------------------------*
*& Report  ZMDM_ACT_EQUIPM_PARTNER_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Equipment Partners extraction (Individual)                   *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 19.12.2013
*  Change Request Number : CR1794
*  Transport request Nr. : CD1K979194
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                 | CD1K9.....    |             *
*----------------------------------------------------------------------*

************************************************************************

REPORT  zmdm_act_equipm_partner_extr.

TABLES: equi,
        equz,
        iloa,
        iflot,
        jest,
        ihpa,
        kna1,
        adrc.

TYPES: BEGIN OF ty_equi,
         equnr      TYPE equnr,
         objnr      TYPE j_objnr,
         tplnr      TYPE tplnr,
         tplma      TYPE tplnr,
      END OF ty_equi.

TYPES: BEGIN OF ty_dlfl,
         objnr      TYPE j_objnr,
      END OF ty_dlfl.

TYPES: BEGIN OF ty_iflo,
         tplnr      TYPE tplnr,
         objnr      TYPE j_objnr,
      END OF ty_iflo.

TYPES: BEGIN OF ty_ihpa,
         objnr      TYPE j_objnr,
         parvw      TYPE parvw,
         parnr      TYPE kunnr,
       END OF ty_ihpa.

TYPES: BEGIN OF ty_adrc,
         kunnr      TYPE kunnr,
         sort1      TYPE ad_sort1,
       END OF ty_adrc.

DATA: gt_equi  TYPE TABLE OF ty_equi
                    WITH HEADER LINE.

DATA: gt_dlfl  TYPE HASHED TABLE OF ty_dlfl
                    WITH UNIQUE KEY objnr
                    WITH HEADER LINE,
      gt_dlfli TYPE TABLE OF ty_dlfl
                    WITH HEADER LINE.

DATA: gt_iflo  TYPE HASHED TABLE OF ty_iflo
                    WITH UNIQUE KEY tplnr
                    WITH HEADER LINE,
      gt_ifloi TYPE TABLE OF ty_iflo
                    WITH HEADER LINE.

DATA: gt_ihpa  TYPE SORTED TABLE OF ty_ihpa
                    WITH NON-UNIQUE KEY objnr
                    WITH HEADER LINE.

DATA: gt_adrc  TYPE HASHED TABLE OF ty_adrc
                    WITH UNIQUE KEY kunnr
                    WITH HEADER LINE,
      gt_adrci TYPE TABLE OF ty_adrc
                    WITH HEADER LINE.

DATA: BEGIN OF gt_out  OCCURS 0,
        equnr       TYPE equnr,
        tab01(1)    TYPE c,
        parvw       TYPE parvw,
        tab02(1)    TYPE c,
        parnr       TYPE kunnr,
        tab03(1)    TYPE c,
        sort1       TYPE ad_sort1,
        tab04(1)    TYPE c,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        equnr       TYPE fieldname,
        tab01(1)    TYPE c,
        parvw       TYPE fieldname,
        tab02(1)    TYPE c,
        parnr       TYPE fieldname,
        tab03(1)    TYPE c,
        sort1       TYPE fieldname,
        tab04(1)    TYPE c,
      END OF gt_outh.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_ersda(14)     TYPE c,                      "YYYYMMDDHHMMSS
      gv_error         TYPE xfeld,
      gv_parnr         TYPE kunnr,
      gv_sort1         TYPE ad_sort1.

CONSTANTS: gc_stat_dl  TYPE j_status  VALUE 'I0076',
           gc_stat_in  TYPE j_status  VALUE 'I0320',
           gc_stat_scr TYPE j_status  VALUE 'E0005'.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
PARAMETERS: p_bukrs  LIKE iloa-bukrs  OBLIGATORY  MEMORY ID buk.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_parvw  FOR ihpa-parvw.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_equnr  FOR equi-equnr.
SELECTION-SCREEN  END OF BLOCK input.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Timestamp
  CONCATENATE sy-datum sy-uzeit INTO gv_ersda.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_equi[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No equipments selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.

* Remove duplicates
  SORT gt_out.
  DELETE ADJACENT DUPLICATES FROM gt_out.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Equipment time segment
  SELECT e~equnr e~objnr l~tplnr f~tplma
         INTO TABLE gt_equi
         FROM equi AS e
         INNER JOIN equz AS z
                    ON z~equnr = e~equnr
         INNER JOIN iloa AS l
                    ON l~iloan = z~iloan
         INNER JOIN iflot AS f
                    ON f~tplnr = l~tplnr
         WHERE e~equnr IN s_equnr
           AND z~datbi =  '99991231'
           AND l~bukrs =  p_bukrs.
  CHECK NOT gt_equi[] IS INITIAL.

* Check for deleted/scrapped equipments
  SELECT objnr
         INTO TABLE gt_dlfli
         FROM jest
         FOR ALL ENTRIES IN gt_equi
         WHERE objnr = gt_equi-objnr
           AND ( stat = gc_stat_dl   OR
                 stat = gc_stat_in   OR
                 stat = gc_stat_scr )
           AND inact = ' '.
  SORT gt_dlfli BY objnr.
  DELETE ADJACENT DUPLICATES FROM gt_dlfli
                                  COMPARING objnr.
  gt_dlfl[] = gt_dlfli[].
  FREE gt_dlfli.

  LOOP AT gt_equi.
    READ TABLE gt_dlfl WITH TABLE KEY objnr = gt_equi-objnr.
    IF sy-subrc = 0.
      DELETE gt_equi.
    ENDIF.
  ENDLOOP.
  FREE gt_dlfl.
  CHECK NOT gt_equi[] IS INITIAL.

* Function location data
  SELECT tplnr objnr
         INTO TABLE gt_ifloi
         FROM iflot
         FOR ALL ENTRIES IN gt_equi
         WHERE tplnr = gt_equi-tplnr
            OR tplnr = gt_equi-tplma.
  SORT gt_ifloi BY tplnr.
  DELETE ADJACENT DUPLICATES FROM gt_ifloi
                                  COMPARING tplnr.
  gt_iflo[] = gt_ifloi[].
  FREE gt_ifloi.

* Partner data
* Equipments
  SELECT objnr parvw parnr
         INTO TABLE gt_ihpa
         FROM ihpa
         FOR ALL ENTRIES IN gt_equi
         WHERE objnr    = gt_equi-objnr
           AND parvw   IN s_parvw
           AND kzloesch = ' '.
* Functional locations
  SELECT objnr parvw parnr
         APPENDING TABLE gt_ihpa
         FROM ihpa
         FOR ALL ENTRIES IN gt_iflo
         WHERE objnr    = gt_iflo-objnr
           AND parvw   IN s_parvw
           AND kzloesch = ' '.

* Search terms
  SELECT k~kunnr a~sort1
         INTO TABLE gt_adrci
         FROM kna1 AS k
         INNER JOIN adrc AS a
                    ON a~addrnumber = k~adrnr
         FOR ALL ENTRIES IN gt_ihpa
         WHERE k~kunnr = gt_ihpa-parnr
           AND a~sort1 NE ' '.
  SORT gt_adrci BY kunnr.
  DELETE ADJACENT DUPLICATES FROM gt_adrci
                                  COMPARING kunnr.
  gt_adrc[] = gt_adrci[].
  FREE gt_adrci.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_equi.

    CLEAR: gt_out.

    MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03,
                 gt_out-tab04.

    gt_out-equnr = gt_equi-equnr.

*   Equipment
    PERFORM get_partner USING gt_equi-objnr.

*   Functional location
    READ TABLE gt_iflo
               WITH TABLE KEY tplnr = gt_equi-tplnr.
    IF sy-subrc = 0.
      PERFORM get_partner USING gt_iflo-objnr.
    ENDIF.

*   Superior functional location
    READ TABLE gt_iflo
               WITH TABLE KEY tplnr = gt_equi-tplma.
    IF sy-subrc = 0.
      PERFORM get_partner USING gt_iflo-objnr.
    ENDIF.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  GET_PARTNER
*&---------------------------------------------------------------------*
*       Get partner from equipment or functional mocation
*----------------------------------------------------------------------*
*      -->P_OBJNR : Object number
*----------------------------------------------------------------------*
FORM get_partner  USING    p_objnr TYPE j_objnr.

  LOOP AT gt_ihpa WHERE objnr = p_objnr.

    gt_out-parvw = gt_ihpa-parvw.
    gt_out-parnr = gt_ihpa-parnr.

*   Search Term
    PERFORM get_sort1.

    APPEND gt_out.

  ENDLOOP.

ENDFORM.                    " GET_PARTNER

*&---------------------------------------------------------------------*
*&      Form  GET_SORT1
*&---------------------------------------------------------------------*
*       Get search term for partner
*----------------------------------------------------------------------*
FORM get_sort1 .

  CHECK NOT gt_ihpa-parnr IS INITIAL.

  READ TABLE gt_adrc WITH TABLE KEY kunnr = gt_ihpa-parnr.

  IF sy-subrc = 0.
    gt_out-sort1 = gt_adrc-sort1.
  ELSE.
    CLEAR gt_out-sort1.
  ENDIF.

ENDFORM.                    " GET_SORT1

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

  CONCATENATE 'EQUIPMENT' 'PARTNER' 'INDIV' p_bukrs gv_ersda
             INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM fill_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

* Fill output
  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  FILL_HEADER
*&---------------------------------------------------------------------*
*       Fill header
*----------------------------------------------------------------------*
FORM fill_header .

  CLEAR: gt_outh.
  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03,
               gt_outh-tab04.

  gt_outh-equnr  = 'EQUNR'.
  gt_outh-parvw  = 'PARVW'.
  gt_outh-parnr  = 'PARNR'.
  gt_outh-sort1  = 'SORT1'.
  APPEND gt_outh.

  gt_outh-equnr  = 'Equipment'.
  gt_outh-parvw  = 'Function'.
  gt_outh-parnr  = 'Partner'.
  gt_outh-sort1  = 'Search Term'.
  APPEND gt_outh.

ENDFORM.                    " FILL_HEADER

*Text symbol text£º
*E01:No equipments selected
*E02:Could not open output file
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_BUKRS:D       .
*S_EQUNR:D       .
*S_PARVW:D       .
