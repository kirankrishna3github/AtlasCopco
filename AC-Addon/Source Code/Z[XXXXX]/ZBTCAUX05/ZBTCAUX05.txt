*&---------------------------------------------------------------------*
*& Report  BTCAUX05
*&
*&---------------------------------------------------------------------*
*& This report finds and lists job that have created speficied
*& spool requests
*&---------------------------------------------------------------------*

REPORT  btcaux05 LINE-SIZE 255 MESSAGE-ID po.

INCLUDE lbtchdef.

TYPE-POOLS: slis, sp01r.

TYPES: BEGIN OF job,
       rqident TYPE tsp01-rqident,
       jobname TYPE tbtcjob-jobname,
       jobcount TYPE tbtcjob-jobcount,
       sdluname TYPE tbtcjob-sdluname,
       authckman TYPE tbtcjob-authckman,
       strtdate TYPE tbtcjob-strtdate,
       strttime TYPE tbtcjob-strttime,
       authcknam TYPE tbtcstep-authcknam,
       stepcount TYPE tbtcp-stepcount,
       program TYPE tbtcstep-program,
       rqdest TYPE tsp01-rqdest,
       rqtitle TYPE tsp01-rqtitle,
       selected(1),
       END OF job.

DATA: rqident TYPE tsp01_sp0r-rqid_char.
DATA: rq0name TYPE tsp01sys-rq0name.
DATA: rq1name TYPE tsp01sys-rq1name.
DATA: rq2name TYPE tsp01sys-rq2name.
DATA: rqowner TYPE tsp01sys-rqowner.
DATA: credate TYPE rspotype-credate.
DATA: rqclient TYPE tsp01sys-rqclient.
DATA: rqdestl TYPE tsp01_sp0r-rqdestl.
DATA: rqtitle TYPE tsp01sys-rqtitle.
DATA: requests TYPE TABLE OF tsp01sys.
DATA: step TYPE tbtcp.
DATA: steps TYPE TABLE OF tbtcp.
DATA: listident TYPE tbtcp-listident.
DATA: request TYPE tsp01sys.
DATA: spoolreq TYPE tsp01-rqident.
DATA: wa_job TYPE job.
DATA: jobs TYPE TABLE OF job WITH KEY rqident.
DATA: r_lines TYPE i.

SELECTION-SCREEN BEGIN OF BLOCK a1 WITH FRAME TITLE text-001.
PARAMETERS: all RADIOBUTTON GROUP mode DEFAULT 'X' USER-COMMAND radio,
sing RADIOBUTTON GROUP mode.

SELECTION-SCREEN BEGIN OF BLOCK a.

SELECT-OPTIONS s_rqiden FOR rqident NO INTERVALS
                        MODIF ID ab.
SELECT-OPTIONS s_rq0nam FOR rq0name NO INTERVALS
                        MODIF ID ab.
SELECT-OPTIONS s_rq1nam FOR rq1name NO INTERVALS
                        MODIF ID ab.
SELECT-OPTIONS s_rq2nam FOR rq2name NO INTERVALS
                        MODIF ID ab.
SELECT-OPTIONS s_rqowne FOR rqowner DEFAULT sy-uname
                        NO INTERVALS MODIF ID ab.
SELECT-OPTIONS s_rqcred FOR credate DEFAULT sy-datlo TO
                                     sy-datlo MODIF ID ab.
SELECT-OPTIONS s_rqclie FOR rqclient DEFAULT sy-mandt
                        NO INTERVALS MODIF ID ab.
SELECT-OPTIONS l_rqdest FOR rqdestl NO INTERVALS
                        MODIF ID ab.
SELECT-OPTIONS s_rqtitl FOR rqtitle NO INTERVALS
                        MODIF ID ab.
SELECTION-SCREEN END OF BLOCK a.
SELECTION-SCREEN END OF BLOCK a1.

AT SELECTION-SCREEN.

  LOOP AT s_rqiden.
    IF s_rqiden-low CN '0123456789 '.
      MESSAGE e033(sp01r) WITH s_rqiden-low.  "#EC *
    ELSEIF s_rqiden-high CN '0123456789 '.
      MESSAGE e033(sp01r) WITH s_rqiden-high.
    ENDIF.
  ENDLOOP.

* If spoolnumer is given clear all other selections
  IF NOT s_rqiden IS INITIAL.
    REFRESH: s_rq0nam, s_rq1nam, s_rq2nam, s_rqcred,
             l_rqdest, s_rqtitl, s_rqowne, s_rqclie.
  ENDIF.

AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF ( sing = 'X' AND screen-group1 = 'AB' )
    AND ( screen-group3 = 'LOW' OR SCREEN-GROUP3 = 'HGH' ).
      screen-active = '1'.
      screen-input = '1'.
    ELSEIF ( all = 'X' AND screen-group1 = 'AB' ).
      screen-active = '1'.
      screen-input = '0'.
    ENDIF.
    MODIFY SCREEN.
  ENDLOOP.

START-OF-SELECTION.

  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      text = 'Selecting Jobs...'(002).                   ""#EC

  IF sing IS NOT INITIAL.

    CALL FUNCTION 'RSPO_L_RSELECT_SPOOLREQS'
      EXPORTING
        rqnone             = 'X'
        rqproc             = 'X'
        rqsucc             = 'X'
        rqerr              = 'X'
*   ACCESS             = '    '
*   PROTO              = ' '
*   NONEX_DEV          = ' '
* IMPORTING
*   RQ_NO_ACCESS       =
*   RQ_ACCESS          =
     TABLES
       s_rqiden           = s_rqiden
       s_rqclie           = s_rqclie
       s_rq0nam           = s_rq0nam
       s_rq1nam           = s_rq1nam
       s_rq2nam           = s_rq2nam
       s_rqowne           = s_rqowne
*   S_RQ1DIS           =
*   S_RQ2DIS           =
*   S_RQFIN            =
*   S_RQCOPI           =
*   S_RQPRIO           =
*   S_RQPAPE           =
*   S_RQPJRE           =
       s_rqtitl           = s_rqtitl
*   S_RQRECE           =
*   S_RQDIVI           =
*   S_RQAUTH           =
*   S_RQTLAN           =
*   S_RQTNUM           =
       s_rqcred           = s_rqcred
*   S_RQDELD           =
       s_rqdestl          = l_rqdest
*   S_RQPAGE           =
*   S_RQSAPT           =
*   S_RQUNXT           =
*   S_RQADESL          =
*   S_RQSYST           =
       result_tsp01       = requests
 EXCEPTIONS
   error              = 1
   OTHERS             = 2
              .
    IF sy-subrc <> 0.
* MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*         WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    ENDIF.

    DESCRIBE TABLE requests LINES r_lines.
    IF r_lines = 0.
      MESSAGE i475.
      EXIT.
    ENDIF.

    LOOP AT requests INTO request.

      MOVE request-rqident TO listident.

      SELECT * FROM tbtcp INTO TABLE steps WHERE listident = listident.

      LOOP AT steps INTO step.
      IF sy-subrc = 0 AND ( step-authcknam = request-rqowner ).
        PERFORM fill_job TABLES jobs USING step request.
      ENDIF.
      ENDLOOP.

      CLEAR: step, listident, request, wa_job.
      REFRESH steps.

    ENDLOOP.

  ELSE.

    SELECT * FROM tbtcp INTO step.

      IF step-listident IS NOT INITIAL.
        spoolreq = step-listident.

        SELECT SINGLE * FROM tsp01 INTO request
        WHERE rqident = spoolreq.
        IF sy-subrc = 0 AND ( step-authcknam = request-rqowner ).
          PERFORM fill_job TABLES jobs USING step request.
        ENDIF.

      ENDIF.

    ENDSELECT.

  ENDIF.

  PERFORM output_list TABLES jobs.

*&---------------------------------------------------------------------*
*&      Form  COMPARE_TIMESTAMPS
*&---------------------------------------------------------------------*
*  In this routine, the spool creation time, which is stored in UTC
*  time, is converted to local server time and compared with the end
*  time of the batch job. If the spool creation time is younger than
*  the job end time, the spool request cannot belong to this particular
*  job.
*----------------------------------------------------------------------*
*      -->P_REQUEST_RQCRETIME  text
*      -->P_WA_TBTCO_ENDDATE  text
*      -->P_WA_TBTCO_ENDTIME  text
*      <--P_RC  text
*----------------------------------------------------------------------*
FORM compare_timestamps  USING p_request_rqcretime TYPE tsp01-rqcretime
                               p_wa_tbtco_enddate TYPE tbtco-enddate
                               p_wa_tbtco_endtime TYPE tbtco-endtime
                         CHANGING p_rc.

  DATA: spool_timestamp TYPE timestamp.
  DATA: diff TYPE int4.
  DATA: tz TYPE timezone.
  DATA: batch_timestamp TYPE timestamp.

  CLEAR p_rc.
  spool_timestamp = p_request_rqcretime(14).

  CALL FUNCTION 'GET_SYSTEM_TIMEZONE'
    IMPORTING
      timezone            = tz
    EXCEPTIONS
      customizing_missing = 1
      OTHERS              = 2.
  IF sy-subrc <> 0.
    EXIT.
  ENDIF.

  CONVERT DATE p_wa_tbtco_enddate TIME p_wa_tbtco_endtime INTO TIME
  STAMP batch_timestamp TIME ZONE tz.

  IF sy-subrc = 0.
    IF spool_timestamp > batch_timestamp.
      p_rc = 1.
    ENDIF.
  ENDIF.

ENDFORM.                    " COMPARE_TIMESTAMPS

*&---------------------------------------------------------------------*
*&      Form  OUTPUT_LIST
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_JOBS  text
*----------------------------------------------------------------------*
FORM output_list  TABLES   p_jobs LIKE jobs.

  DATA: my_program TYPE sy-repid.
  DATA: grid_title(70).
  DATA: gt_fieldcat TYPE slis_t_fieldcat_alv,
        gs_layout   TYPE slis_layout_alv,
        gt_exclude TYPE slis_t_extab,
        alv_print TYPE slis_print_alv.
  DATA: j_lines TYPE i.

  DESCRIBE TABLE p_jobs LINES j_lines.
  IF j_lines = 0.
    MESSAGE i145(bt).
    LEAVE PROGRAM.
  ENDIF.

  SORT p_jobs.

  PERFORM get_fieldcat USING gt_fieldcat[].
  PERFORM build_layout USING gs_layout.
  PERFORM set_excludes USING gt_exclude[].

  grid_title = sy-title.

  alv_print-no_new_page = 'X'.
  alv_print-no_print_listinfos = 'X'.

  my_program = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = my_program
      i_callback_user_command = 'USER_COMMAND'
*     i_structure_name        =
      i_grid_title            = grid_title
      is_layout               = gs_layout
      it_fieldcat             = gt_fieldcat[]
      it_excluding            = gt_exclude[]
      i_default               = 'X'
      i_save                  = 'A'
      is_print                = alv_print
    TABLES
      t_outtab                = p_jobs
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.
  IF sy-subrc <> 0.
* MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*         WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.


ENDFORM.                    " OUTPUT_LIST


*&---------------------------------------------------------------------*
*&      Form  build_layout
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_GS_LAYOUT  text
*----------------------------------------------------------------------*
FORM build_layout  USING  gs_layout TYPE slis_layout_alv.

  gs_layout-window_titlebar = sy-title.
  gs_layout-no_totalline = 'X'.
  gs_layout-cell_merge = 'N'.
  gs_layout-colwidth_optimize = 'X'.
  gs_layout-box_fieldname = 'SELECTED'.

ENDFORM.                    " build_layout
*&---------------------------------------------------------------------*
*&      Form  set_excludes
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_GT_EXCLUDE[]  text
*----------------------------------------------------------------------*
FORM set_excludes  USING   gt_exclude TYPE slis_t_extab.

  DATA: BEGIN OF ls_exclude,
  fcode LIKE rsmpe-func,
  END OF ls_exclude.

  CLEAR ls_exclude.
  ls_exclude-fcode = '&VEXCEL'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&VLOTUS'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&VCRYSTAL'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&ABC'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&GRAPH'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&URL_COPY_TO_CLIPBOARD'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&XINT'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&CRDATA'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&CRDESIG'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&CRTEMPL'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&AQW'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&XML'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&HTML'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&XXL'.
  APPEND ls_exclude TO gt_exclude.
  CLEAR ls_exclude.
  ls_exclude-fcode = '&UMC'.
  APPEND ls_exclude TO gt_exclude.

ENDFORM.                    " set_excludes

*&---------------------------------------------------------------------*
*&      Form  get_fieldcat
*&---------------------------------------------------------------------*
FORM get_fieldcat  USING    gt_fieldcat TYPE slis_t_fieldcat_alv.

  DATA: ls_fieldcat TYPE slis_fieldcat_alv.

  CLEAR ls_fieldcat.
  ls_fieldcat-fieldname = 'RQIDENT'.
  ls_fieldcat-ref_tabname = 'TSP01_SP0R'.
  ls_fieldcat-key       = 'X'.
  ls_fieldcat-no_zero   = 'X'.
  APPEND ls_fieldcat TO gt_fieldcat.

  CLEAR ls_fieldcat-key.
  CLEAR ls_fieldcat-no_zero.

  ls_fieldcat-ref_tabname = 'TBTCJOB'.
  ls_fieldcat-fieldname = 'JOBNAME'.
  APPEND ls_fieldcat TO gt_fieldcat.

  ls_fieldcat-fieldname = 'JOBCOUNT'.
  APPEND ls_fieldcat TO gt_fieldcat.

  ls_fieldcat-fieldname = 'SDLUNAME'.
  APPEND ls_fieldcat TO gt_fieldcat.

  ls_fieldcat-fieldname = 'AUTHCKMAN'.
  APPEND ls_fieldcat TO gt_fieldcat.

  ls_fieldcat-fieldname = 'STRTDATE'.
  APPEND ls_fieldcat TO gt_fieldcat.

  ls_fieldcat-fieldname = 'STRTTIME'.
  APPEND ls_fieldcat TO gt_fieldcat.

  ls_fieldcat-ref_tabname = 'TBTCSTEP'.
  ls_fieldcat-fieldname = 'AUTHCKNAM'.
  APPEND ls_fieldcat TO gt_fieldcat.

  ls_fieldcat-ref_tabname = 'TBTCP'.
  ls_fieldcat-fieldname = 'STEPCOUNT'.
  APPEND ls_fieldcat TO gt_fieldcat.

  ls_fieldcat-ref_tabname = 'TBTCSTEP'.
  ls_fieldcat-fieldname = 'PROGRAM'.
  APPEND ls_fieldcat TO gt_fieldcat.

  ls_fieldcat-ref_tabname = 'TSP01_SP0R'.
  ls_fieldcat-fieldname = 'RQDEST'.
  APPEND ls_fieldcat TO gt_fieldcat.

  ls_fieldcat-fieldname = 'RQTITLE'.
  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " get_fieldcat
*&---------------------------------------------------------------------*
*&      Form  FILL_JOB
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_JOBS  text
*      -->P_STEP  text
*      -->P_REQUEST  text
*----------------------------------------------------------------------*
FORM fill_job  TABLES   p_jobs LIKE jobs
               USING    p_step STRUCTURE tbtcp
                        p_request STRUCTURE tsp01.

  DATA: wa_tbtco TYPE tbtco.
  DATA: rc TYPE sy-subrc.

  SELECT SINGLE * FROM tbtco INTO wa_tbtco
  WHERE jobname = p_step-jobname AND jobcount = p_step-jobcount.
  IF sy-subrc = 0.
    PERFORM compare_timestamps USING p_request-rqcretime
                                     wa_tbtco-enddate
                                     wa_tbtco-endtime
                               CHANGING rc.
    IF rc = 0.
      MOVE-CORRESPONDING wa_tbtco TO wa_job.
      MOVE-CORRESPONDING p_step TO wa_job.
      MOVE p_step-progname TO wa_job-program.
      MOVE-CORRESPONDING p_request TO wa_job.
      APPEND wa_job TO p_jobs.
    ENDIF.
  ENDIF.

ENDFORM.                    " FILL_JOB

*&---------------------------------------------------------------------*
*&      Form  USER_COMMAND
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*
*
*
*----------------------------------------------------------------------*

FORM user_command                                           "#EC CALLED
      USING r_ucomm     LIKE sy-ucomm
            rs_selfield TYPE slis_selfield.

  DATA: idtab TYPE sp01r_id_list.
  DATA: idlist TYPE sp01r_id.
  DATA: single_job TYPE job.
  DATA: disp_jobs TYPE TABLE OF tbtcjob.
  DATA: jobhead TYPE tbtcjob.

  READ TABLE jobs INDEX rs_selfield-tabindex INTO single_job.
  IF sy-subrc = 0.

    CASE r_ucomm.

      WHEN '&IC1'.                       "when double click
        CASE rs_selfield-fieldname.
          WHEN 'RQIDENT'.
            MOVE single_job-rqident TO idlist-id.
            MOVE sy-sysid TO idlist-sysid.
            APPEND idlist TO idtab.

            CALL FUNCTION 'RSPO_RID_SPOOLREQ_LIST'
              EXPORTING
                id_list = idtab
*               SUMMARY = ' '
              EXCEPTIONS
                error   = 1
                OTHERS  = 2.
            IF sy-subrc <> 0.
              MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                      WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
            ENDIF.

          WHEN OTHERS.
            CALL FUNCTION 'BP_JOB_READ'
              EXPORTING
                job_read_jobcount     = single_job-jobcount
                job_read_jobname      = single_job-jobname
                job_read_opcode       = btc_read_jobhead_only
              IMPORTING
                job_read_jobhead      = jobhead
              EXCEPTIONS
                invalid_opcode        = 1
                job_doesnt_exist      = 2
                job_doesnt_have_steps = 3
                OTHERS                = 4.
            IF sy-subrc <> 0.
              MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                      WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
            ENDIF.

            APPEND jobhead TO disp_jobs.

            CALL FUNCTION 'BP_JOB_MANAGEMENT'
              EXPORTING
                jobselect_dialog        = btc_no
                joblist_opcode          = btc_joblist_edit
*               JOBSEL_PARAM_INPUT      =
*               LIST_TITLE              =
              TABLES
                joblist                 = disp_jobs
              EXCEPTIONS
                unknown_selection_error = 1
                invalid_opcode          = 2
                invalid_dialog_type     = 3
                refresh_list_required   = 4
                empty_list              = 5
                OTHERS                  = 6.
            IF sy-subrc <> 0.
              MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                      WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
            ENDIF.

        ENDCASE.

      WHEN OTHERS.

    ENDCASE.

  ENDIF.

ENDFORM.                    "user_command

*Text symbol text£º
*001:Show creating jobs

*002:Selecting jobs...
*Selection text£º
*ALL:        For all spool requests
*L_RQDEST:D       .
*SING:        For specified spool requests
*S_RQ0NAM:D       .
*S_RQ1NAM:D       .
*S_RQ2NAM:D       .
*S_RQCLIE:D       .
*S_RQCRED:D       .
*S_RQIDEN:D       .
*S_RQOWNE:D       .

*S_RQTITL:D       .
*List Title: Titlebar£º
*:Select jobs to spool requests
