*&---------------------------------------------------------------------*
*& Report  Z_MASS_JOB_CHANGE_V3_700
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

* 15.12.2011   d023157
* Now jobs in status Z (suspended) are changed as well. they were not taken
* into account before.


REPORT  Z_MASS_JOB_CHANGE.

****Include****
CLASS lcx_sbti_mass_job_change DEFINITION
  INHERITING FROM cx_sbti_exception
  FINAL
  CREATE PUBLIC.

  PUBLIC SECTION.

    CONSTANTS:
      BEGIN OF cx_incorrect_time,
        msgid TYPE symsgid VALUE '00',
        msgno TYPE symsgno VALUE '069',
        attr1 TYPE scx_attrname VALUE '',
        attr2 TYPE scx_attrname VALUE '',
        attr3 TYPE scx_attrname VALUE '',
        attr4 TYPE scx_attrname VALUE '',
      END OF cx_incorrect_time.
    CONSTANTS:
      BEGIN OF cx_invalid_group,
        msgid TYPE symsgid VALUE 'BT',
        msgno TYPE symsgno VALUE '662',
        attr1 TYPE scx_attrname VALUE 'TARGET',
        attr2 TYPE scx_attrname VALUE '',
        attr3 TYPE scx_attrname VALUE '',
        attr4 TYPE scx_attrname VALUE '',
      END OF cx_invalid_group.
    CONSTANTS:
      BEGIN OF cx_invalid_host,
        msgid TYPE symsgid VALUE 'BT',
        msgno TYPE symsgno VALUE '047',
        attr1 TYPE scx_attrname VALUE 'TARGET',
        attr2 TYPE scx_attrname VALUE '',
        attr3 TYPE scx_attrname VALUE '',
        attr4 TYPE scx_attrname VALUE '',
      END OF cx_invalid_host.
    CONSTANTS:
      BEGIN OF cx_invalid_printer,
        msgid TYPE symsgid VALUE 'PO',
        msgno TYPE symsgno VALUE '366',
        attr1 TYPE scx_attrname VALUE 'PDEST',
        attr2 TYPE scx_attrname VALUE '',
        attr3 TYPE scx_attrname VALUE '',
        attr4 TYPE scx_attrname VALUE '',
      END OF cx_invalid_printer.
    CONSTANTS:
      BEGIN OF cx_no_authority,
        msgid TYPE symsgid VALUE 'BT',
        msgno TYPE symsgno VALUE '791',
        attr1 TYPE scx_attrname VALUE '',
        attr2 TYPE scx_attrname VALUE '',
        attr3 TYPE scx_attrname VALUE '',
        attr4 TYPE scx_attrname VALUE '',
      END OF cx_no_authority.
    CONSTANTS:
      BEGIN OF cx_no_report,
        msgid TYPE symsgid VALUE 'BT',
        msgno TYPE symsgno VALUE '074',
        attr1 TYPE scx_attrname VALUE 'REPORT',
        attr2 TYPE scx_attrname VALUE '',
        attr3 TYPE scx_attrname VALUE '',
        attr4 TYPE scx_attrname VALUE '',
      END OF cx_no_report.
    CONSTANTS:
      BEGIN OF cx_no_step_user,
        msgid TYPE symsgid VALUE 'BT',
        msgno TYPE symsgno VALUE '069',
        attr1 TYPE scx_attrname VALUE '',
        attr2 TYPE scx_attrname VALUE '',
        attr3 TYPE scx_attrname VALUE '',
        attr4 TYPE scx_attrname VALUE '',
      END OF cx_no_step_user.
    CONSTANTS:
      BEGIN OF cx_no_variant,
        msgid TYPE symsgid VALUE 'BT',
        msgno TYPE symsgno VALUE '075',
        attr1 TYPE scx_attrname VALUE 'VARIANT',
        attr2 TYPE scx_attrname VALUE '',
        attr3 TYPE scx_attrname VALUE '',
        attr4 TYPE scx_attrname VALUE '',
      END OF cx_no_variant.
    CONSTANTS:
      BEGIN OF cx_report_not_to_schedule,
        msgid TYPE symsgid VALUE 'BT',
        msgno TYPE symsgno VALUE '077',
        attr1 TYPE scx_attrname VALUE 'REPORT',
        attr2 TYPE scx_attrname VALUE '',
        attr3 TYPE scx_attrname VALUE '',
        attr4 TYPE scx_attrname VALUE '',
      END OF cx_report_not_to_schedule.
    CONSTANTS:
      BEGIN OF cx_step_user_not_found,
        msgid TYPE symsgid VALUE 'BT',
        msgno TYPE symsgno VALUE '071',
        attr1 TYPE scx_attrname VALUE 'USER',
        attr2 TYPE scx_attrname VALUE '',
        attr3 TYPE scx_attrname VALUE '',
        attr4 TYPE scx_attrname VALUE '',
      END OF cx_step_user_not_found.
    CONSTANTS:
      BEGIN OF cx_th_server_list_na,
        msgid TYPE symsgid VALUE 'BT',
        msgno TYPE symsgno VALUE '254',
        attr1 TYPE scx_attrname VALUE '',
        attr2 TYPE scx_attrname VALUE '',
        attr3 TYPE scx_attrname VALUE '',
        attr4 TYPE scx_attrname VALUE '',
      END OF cx_th_server_list_na.
    DATA jobcount TYPE btcjobcnt.                           "#EC NEEDED
    DATA jobname TYPE btcjob.                               "#EC NEEDED
    DATA pdest TYPE sypdest.                                "#EC NEEDED
    DATA report TYPE btcprog.                               "#EC NEEDED
    DATA target TYPE char20.                                "#EC NEEDED
    DATA user TYPE uname.                                   "#EC NEEDED
    DATA variant TYPE btcvariant.                           "#EC NEEDED

    METHODS constructor
      IMPORTING
        !textid LIKE if_t100_message=>t100key OPTIONAL
        !previous LIKE previous OPTIONAL
        !jobname TYPE btcjob OPTIONAL
        !jobcount TYPE btcjobcnt OPTIONAL
        !report TYPE btcprog OPTIONAL
        !variant TYPE btcvariant OPTIONAL
        !user TYPE uname OPTIONAL
        !target TYPE char20 OPTIONAL
        !pdest TYPE sypdest OPTIONAL.
  PROTECTED SECTION.
  PRIVATE SECTION.
ENDCLASS.                    "lcx_sbti_mass_job_change DEFINITION

*----------------------------------------------------------------------*
*       CLASS lcx_sbti_mass_job_change IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcx_sbti_mass_job_change IMPLEMENTATION.
*---------------------------------------------------------------------*
*                       REDIFINITIONS
*---------------------------------------------------------------------*
*
*                                                                     *
*---------------------------------------------------------------------*
*                       IMPLEMENTATION
*---------------------------------------------------------------------*
*
*                                                                     *

  METHOD constructor.
    CALL METHOD super->constructor
      EXPORTING
        previous = previous.
    me->jobname = jobname.
    me->jobcount = jobcount.
    me->report = report.
    me->variant = variant.
    me->user = user.
    me->target = target.
    me->pdest = pdest.
    CLEAR me->textid.
    IF textid IS INITIAL.
      if_t100_message~t100key = if_t100_message=>default_textid.
    ELSE.
      if_t100_message~t100key = textid.
    ENDIF.
  ENDMETHOD.                    "CONSTRUCTOR
ENDCLASS.                    "lcx_sbti_mass_job_change IMPLEMENTATION



*----------------------------------------------------------------------*
*       CLASS LCL_SBTI_JOB_FILTER_DEFAULT DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_sbti_job_filter_default DEFINITION
  CREATE PUBLIC .

  PUBLIC SECTION.
*"* public components of class LCL_SBTI_JOB_FILTER_DEFAULT
*"* do not include other source files here!!!

    TYPES:
      ty_jobname_range TYPE RANGE OF btcjob .
    TYPES:
      ty_username_range TYPE RANGE OF uname .

    CLASS-METHODS class_constructor .
    METHODS constructor
      IMPORTING
        !iv_from TYPE timestamp OPTIONAL
        !it_job_name TYPE ty_jobname_range OPTIONAL
        !it_job_owner TYPE ty_username_range OPTIONAL
        !iv_to TYPE timestamp OPTIONAL
      RAISING
        lcx_sbti_mass_job_change .
    METHODS get_select_syntax
      EXPORTING
        !ev_where_clause TYPE string
        !ev_column_syntax TYPE string
        !ev_dbtab_syntax TYPE string .
    METHODS get_filter_text
      RETURNING
        value(ev_text) TYPE string .
  PROTECTED SECTION.
*"* protected components of class LCL_SBTI_JOB_FILTER_DEFAULT
*"* do not include other source files here!!!
  PRIVATE SECTION.
*"* private components of class LCL_SBTI_JOB_FILTER_DEFAULT
*"* do not include other source files here!!!

    DATA mv_from TYPE timestamp .
    DATA mv_to TYPE timestamp .
    DATA mt_job_owner TYPE ty_username_range .
    DATA mt_job_name TYPE ty_jobname_range .
    CLASS-DATA mv_sys_timezone TYPE timezone .
    DATA mv_filter_text TYPE string .
    DATA mv_where_clause TYPE string .
    DATA mv_column_syntax TYPE string .
    DATA mv_dbtab_syntax TYPE string .

    METHODS selopt_to_whereclause
      IMPORTING
        !it_selopt TYPE STANDARD TABLE
        !iv_field_name TYPE char40
      RETURNING
        value(ev_where_clause) TYPE string .
ENDCLASS.                    "LCL_SBTI_JOB_FILTER_DEFAULT DEFINITION



*----------------------------------------------------------------------*
*       CLASS LCL_SBTI_JOB_FILTER_DEFAULT IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_sbti_job_filter_default IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Static Public Method LCL_SBTI_JOB_FILTER_DEFAULT=>CLASS_CONSTRUCTOR
* +-------------------------------------------------------------------------------------------------+
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD class_constructor.
* Get the system timezone?
    CALL FUNCTION 'GET_SYSTEM_TIMEZONE'
      IMPORTING
        timezone            = mv_sys_timezone
      EXCEPTIONS
        customizing_missing = 1
        OTHERS              = 2.
* System Timezone not customized? Use UTC
    IF sy-subrc <> 0.
      mv_sys_timezone = 'UTC'.
    ENDIF.

  ENDMETHOD.                    "class_constructor


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method LCL_SBTI_JOB_FILTER_DEFAULT->CONSTRUCTOR
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_FROM                        TYPE        TIMESTAMP(optional)
* | [--->] IT_JOB_NAME                    TYPE        TY_JOBNAME_RANGE(optional)
* | [--->] IT_JOB_OWNER                   TYPE        TY_USERNAME_RANGE(optional)
* | [--->] IV_TO                          TYPE        TIMESTAMP(optional)
* | [!CX!] lcx_sbti_mass_job_change
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD constructor.
    me->mv_from = iv_from.
    me->mv_to = iv_to.
    me->mt_job_owner = it_job_owner.
    me->mt_job_name = it_job_name.

    DATA: lv_date TYPE datum,
          lv_time TYPE uzeit,                               "#EC NEEDED
          lv_where_clause TYPE string.

* Column syntax
    mv_column_syntax = `tbtco~jobname tbtco~jobcount`.

* DB tables syntax
    mv_dbtab_syntax = `tbtco as tbtco`.

* Where clause
* Only released or scheduled jobs in current client
    CONCATENATE mv_where_clause
  `( tbtco~status = 'P' or tbtco~status = 'S' or tbtco~status = 'Z' ) and tbtco~authckman = '` sy-mandt `'` "#EC NOTEXT
           INTO mv_where_clause.

    CONCATENATE `STATUS = P or STATUS = S or STATUS = Z; AUTHCKMAN = ` sy-mandt "#EC NOTEXT
           INTO mv_filter_text.
* From
    IF NOT mv_from IS INITIAL.
* Time zone conversion UTC to SYS
      CONVERT TIME STAMP mv_from
        TIME ZONE mv_sys_timezone
        INTO DATE lv_date TIME lv_time.
      IF sy-subrc NE 0.
* Raise exception
        RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
          EXPORTING
            textid = lcx_sbti_mass_job_change=>cx_incorrect_time.
      ENDIF.
      CONCATENATE mv_where_clause ` and `
                  `tbtco~sdldate >= '` lv_date `'`
*                `tbtco~sdlstrttm > '` lv_time `'`
             INTO mv_where_clause.
      CONCATENATE mv_filter_text `; SDLDATE>=` lv_date
        INTO mv_filter_text.
    ENDIF.
* To
    IF NOT mv_to IS INITIAL.
* Time zone conversion UTC to SYS
      CONVERT TIME STAMP mv_to
        TIME ZONE mv_sys_timezone
        INTO DATE lv_date TIME lv_time.
      IF sy-subrc NE 0.
* Raise exception
        RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
          EXPORTING
            textid = lcx_sbti_mass_job_change=>cx_incorrect_time.
      ENDIF.
      CONCATENATE mv_where_clause ` and `
                  `tbtco~sdldate <= '` lv_date `'`
*                `tbtco~sdlstrttm < '` lv_time `'`
             INTO mv_where_clause.
      CONCATENATE mv_filter_text `; SDLDATE<=` lv_date
        INTO mv_filter_text.
    ENDIF.
* Job ownwer
    IF NOT mt_job_owner IS INITIAL.
      lv_where_clause = me->selopt_to_whereclause(
        it_selopt       = mt_job_owner
        iv_field_name   = 'tbtco~sdluname'
           ).
      IF NOT lv_where_clause IS INITIAL.
        CONCATENATE mv_where_clause ` and `
                    lv_where_clause
               INTO mv_where_clause.

        CONCATENATE mv_filter_text `; ` lv_where_clause
          INTO mv_filter_text.
      ENDIF.
    ENDIF.
* Job name
    IF NOT mt_job_name IS INITIAL.
      lv_where_clause = me->selopt_to_whereclause(
            it_selopt       = mt_job_name
            iv_field_name   = 'tbtco~jobname'
               ).
      IF NOT lv_where_clause IS INITIAL.
        CONCATENATE mv_where_clause ` and `
                    lv_where_clause
               INTO mv_where_clause.

        CONCATENATE mv_filter_text `; ` lv_where_clause
          INTO mv_filter_text.
      ENDIF.
    ENDIF.


  ENDMETHOD.                    "constructor


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method LCL_SBTI_JOB_FILTER_DEFAULT->GET_FILTER_TEXT
* +-------------------------------------------------------------------------------------------------+
* | [<-()] EV_TEXT                        TYPE        STRING
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD get_filter_text.
    ev_text = mv_filter_text.
  ENDMETHOD.                    "get_filter_text


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method LCL_SBTI_JOB_FILTER_DEFAULT->GET_SELECT_SYNTAX
* +-------------------------------------------------------------------------------------------------+
* | [<---] EV_WHERE_CLAUSE                TYPE        STRING
* | [<---] EV_COLUMN_SYNTAX               TYPE        STRING
* | [<---] EV_DBTAB_SYNTAX                TYPE        STRING
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD get_select_syntax.
    ev_where_clause = mv_where_clause.
    ev_column_syntax = mv_column_syntax.
    ev_dbtab_syntax = mv_dbtab_syntax.
  ENDMETHOD.                    "get_select_syntax


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method LCL_SBTI_JOB_FILTER_DEFAULT->SELOPT_TO_WHERECLAUSE
* +-------------------------------------------------------------------------------------------------+
* | [--->] IT_SELOPT                      TYPE        TABLE
* | [--->] IV_FIELD_NAME                  TYPE        CHAR40
* | [<-()] EV_WHERE_CLAUSE                TYPE        STRING
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD selopt_to_whereclause.
    FIELD-SYMBOLS <fs_inp> TYPE ANY.
    DATA: ls_f4_selopt TYPE ddshselopt,
          lt_f4_selopt TYPE STANDARD TABLE OF ddshselopt.

    LOOP AT it_selopt ASSIGNING <fs_inp>.
      MOVE-CORRESPONDING <fs_inp> TO ls_f4_selopt.
      ls_f4_selopt-shlpfield = iv_field_name.
      APPEND ls_f4_selopt TO lt_f4_selopt.
    ENDLOOP.

    CALL FUNCTION 'F4_CONV_SELOPT_TO_WHERECLAUSE'
      IMPORTING
        where_clause = ev_where_clause
      TABLES
        selopt_tab   = lt_f4_selopt.


  ENDMETHOD.                    "selopt_to_whereclause
ENDCLASS.                    "LCL_SBTI_JOB_FILTER_DEFAULT IMPLEMENTATION


*----------------------------------------------------------------------*
*       CLASS lcl_sbti_mass_job_change DEFINITION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_sbti_mass_job_change DEFINITION
  CREATE PUBLIC
  FINAL.

  PUBLIC SECTION.
*"* public components of class LCL_SBTI_MASS_JOB_CHANGE
*"* do not include other source files here!!!

    TYPES:
      BEGIN OF lt_s_error_log,
        jobname TYPE btcjob,
        jobcount TYPE btcjobcnt,
        status(1)  TYPE c,
        rc TYPE int1,
      END OF lt_s_error_log .
    TYPES:
      lt_t_error_log TYPE STANDARD TABLE OF lt_s_error_log .
    TYPES:
      BEGIN OF lt_s_error_header,
         timestamp TYPE timestamp,
         client TYPE mandt,
         user TYPE uname,
         mode(1) TYPE c,
         test_run TYPE boolean,
         filter TYPE string,
         old_params TYPE string,
         new_params TYPE string,
         changed_jobs TYPE i,
         errors TYPE i,
       END OF lt_s_error_header .

    CONSTANTS mc_trg_mode_group_group TYPE i VALUE 3.       "#EC NOTEXT
    CONSTANTS mc_trg_mode_group_host TYPE i VALUE 4.        "#EC NOTEXT
    CONSTANTS mc_trg_mode_host_group TYPE i VALUE 2.        "#EC NOTEXT
    CONSTANTS mc_trg_mode_host_host TYPE i VALUE 1.         "#EC NOTEXT

    METHODS change_exec_target
      IMPORTING
        !iv_old_target TYPE char20
        !iv_new_target TYPE char20
        !iv_mode TYPE i
      RETURNING
        value(ev_total_matches) TYPE int4
      RAISING
        lcx_sbti_mass_job_change .
    TYPE-POOLS abap .
    METHODS change_printer
      IMPORTING
        !iv_old_printer TYPE sypdest
        !iv_new_printer TYPE sypdest
        !iv_no_dialog TYPE boolean DEFAULT abap_true
      RETURNING
        value(ev_total_matches) TYPE int4
      RAISING
        lcx_sbti_mass_job_change .
    METHODS change_report_variant
      IMPORTING
        !iv_old_report TYPE btcprog
        !iv_old_variant TYPE btcvariant OPTIONAL
        !iv_new_report TYPE btcprog
        !iv_new_variant TYPE btcvariant OPTIONAL
      RETURNING
        value(ev_total_matches) TYPE int4
      RAISING
        lcx_sbti_mass_job_change .
*    METHODS change_spool_recipient
*      RAISING
*        lcx_sbti_mass_job_change .
    METHODS change_user
      IMPORTING
        !iv_old_user TYPE uname
        !iv_change_step_user TYPE boolean
        !iv_change_owner TYPE boolean
        !iv_new_user TYPE uname
      RETURNING
        value(ev_total_matches) TYPE int4
      RAISING
        lcx_sbti_mass_job_change .
    METHODS constructor
      IMPORTING
        !ir_filter TYPE REF TO lcl_sbti_job_filter_default
        !iv_portion TYPE int4 DEFAULT 1000
        !iv_do_commit TYPE boolean DEFAULT abap_true
        !iv_test_mode TYPE boolean DEFAULT abap_true
      RAISING
        lcx_sbti_mass_job_change .
    METHODS log_get
      EXPORTING
        !es_header TYPE lt_s_error_header
        !et_records TYPE lt_t_error_log .
  PRIVATE SECTION.
*"* private components of class LCL_SBTI_MASS_JOB_CHANGE
*"* do not include other source files here!!!

    CONSTANTS mc_max_iterations TYPE int4 VALUE 100.        "#EC NOTEXT
*    CONSTANTS mc_mode_change_client TYPE c VALUE 'C'.       "#EC NOTEXT
    CONSTANTS mc_mode_change_report TYPE c VALUE 'R'.       "#EC NOTEXT
    CONSTANTS mc_mode_change_target TYPE c VALUE 'T'.       "#EC NOTEXT
    CONSTANTS mc_mode_change_user TYPE c VALUE 'U'.         "#EC NOTEXT
    CONSTANTS mc_mode_change_variant TYPE c VALUE 'V'.      "#EC NOTEXT
    CONSTANTS mc_rc_cannt_update_tbtco TYPE int1 VALUE 4.   "#EC NOTEXT
    CONSTANTS mc_rc_cannt_update_tbtcp TYPE int1 VALUE 5.   "#EC NOTEXT
*    CONSTANTS mc_rc_cannt_update_tbtcs TYPE int1 VALUE 6.   "#EC NOTEXT
    CONSTANTS mc_rc_cant_enqueue TYPE int1 VALUE 1.         "#EC NOTEXT
    CONSTANTS mc_rc_incorrect_status TYPE int1 VALUE 3.     "#EC NOTEXT
    CONSTANTS mc_rc_not_exists TYPE int1 VALUE 2.           "#EC NOTEXT
    DATA mr_filter TYPE REF TO lcl_sbti_job_filter_default .
    DATA ms_log_header TYPE lt_s_error_header .
    DATA mt_log_records TYPE lt_t_error_log .
    DATA mv_do_commit TYPE boolean .
    DATA mv_portion TYPE int4 .
    DATA mv_test_mode TYPE boolean .
    CONSTANTS mc_rc_cannt_save_pripar TYPE int1 VALUE 7.    "#EC NOTEXT
    CONSTANTS mc_rc_cannt_load_pripar TYPE int1 VALUE 8.    "#EC NOTEXT
    CONSTANTS mc_rc_cannt_update_pripar TYPE int1 VALUE 9.  "#EC NOTEXT

    METHODS check_target
      IMPORTING
        !iv_mode TYPE i
        !iv_target TYPE char20
      RETURNING
        value(ev_target) TYPE char40
      RAISING
        lcx_sbti_mass_job_change .
    METHODS dequeue
      IMPORTING
        !iv_jobname TYPE btcjob
        !iv_jobcount TYPE btcjobcnt .
    METHODS enqueue
      IMPORTING
        !iv_jobname TYPE btcjob
        !iv_jobcount TYPE btcjobcnt
      RETURNING
        value(ev_rc) TYPE int1 .
    METHODS get_srvgrp_id
      IMPORTING
        value(iv_grp) TYPE bpsrvgrp
      RETURNING
        value(ev_id) TYPE csmsysguid
      RAISING
        lcx_sbti_mass_job_change .
    METHODS log_job_change
      IMPORTING
        !iv_jobname TYPE btcjob
        !iv_jobcount TYPE btcjobcnt
        !iv_status TYPE c
        !iv_rc TYPE int1 .
    METHODS log_save .
    METHODS log_test_run
      IMPORTING
        !it_selected_jobs TYPE btc_t_joblist .
    METHODS update_exec_target
      IMPORTING
        !it_selected_jobs TYPE btc_t_joblist
        !iv_new_target TYPE char40
        !iv_mode TYPE i
      RAISING
        lcx_sbti_mass_job_change .
    METHODS update_report_variant
      IMPORTING
        !it_selected_jobs TYPE btc_t_joblist
        !iv_old_report TYPE btcprog
        !iv_old_variant TYPE btcvariant OPTIONAL
        !iv_new_report TYPE btcprog
        !iv_new_variant TYPE btcvariant OPTIONAL
      RAISING
        lcx_sbti_mass_job_change .
    METHODS update_user
      IMPORTING
        !it_selected_jobs TYPE btc_t_joblist
        !iv_old_user TYPE uname
        !iv_new_user TYPE uname
        !iv_change_step_user TYPE boolean
        !iv_change_owner TYPE boolean
      RAISING
        lcx_sbti_mass_job_change .
    METHODS update_printer
      IMPORTING
        !it_selected_jobs TYPE btc_t_joblist
        !iv_old_key TYPE syprkey
        !iv_new_printer TYPE sypdest
        !iv_no_dialog TYPE boolean
      RAISING
        lcx_sbti_mass_job_change .
ENDCLASS.                    "LCL_SBTI_MASS_JOB_CHANGE DEFINITION



*----------------------------------------------------------------------*
*       CLASS LCL_SBTI_MASS_JOB_CHANGE IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS lcl_sbti_mass_job_change IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method LCL_SBTI_MASS_JOB_CHANGE->CHANGE_EXEC_TARGET
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_OLD_TARGET                  TYPE        CHAR20
* | [--->] IV_NEW_TARGET                  TYPE        CHAR20
* | [--->] IV_MODE                        TYPE        I
* | [<-()] EV_TOTAL_MATCHES               TYPE        INT4
* | [!CX!] lcx_sbti_mass_job_change
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD change_exec_target.
    DATA: lv_where_clause TYPE string,
          lv_column_syntax TYPE string,
          lv_dbtab_syntax TYPE string,
          lt_selected_jobs TYPE btc_t_joblist.
    DATA: lv_target(40) TYPE c.


* Check input parameters
    IF  iv_mode NE mc_trg_mode_host_host   AND
        iv_mode NE mc_trg_mode_host_group  AND
        iv_mode NE mc_trg_mode_group_group AND
        iv_mode NE mc_trg_mode_group_host.
      EXIT.
    ENDIF.
* Check new target
    lv_target =  me->check_target(
                  iv_mode   = iv_mode
                  iv_target = iv_new_target
                     ).

* Log header
    GET TIME STAMP FIELD ms_log_header-timestamp.
    ms_log_header-user = sy-uname.
    ms_log_header-client = sy-mandt.
    ms_log_header-test_run = mv_test_mode.
    ms_log_header-filter = mr_filter->get_filter_text( ).
    IF  iv_mode = mc_trg_mode_group_host  OR
        iv_mode = mc_trg_mode_group_group.
      CONCATENATE 'TGTSRVGRP=' iv_old_target
       INTO ms_log_header-old_params.
    ELSE.
      CONCATENATE 'EXECSERVER=' iv_old_target
       INTO ms_log_header-old_params.
    ENDIF.
    IF  iv_mode = mc_trg_mode_host_group  OR
        iv_mode = mc_trg_mode_group_group.
      CONCATENATE 'TGTSRVGRP=' iv_new_target
       INTO ms_log_header-new_params.
    ELSE.
      CONCATENATE 'EXECSERVER=' iv_new_target
       INTO ms_log_header-new_params.
    ENDIF.

* Get select syntax
    mr_filter->get_select_syntax(
      IMPORTING
        ev_where_clause  = lv_where_clause
        ev_column_syntax = lv_column_syntax
        ev_dbtab_syntax  = lv_dbtab_syntax
           ).

* Update column syntax
* nothing to do here

* Update DB tables syntax
* nothing to do here

* Update Where clause
    ms_log_header-mode = mc_mode_change_target.
    IF iv_old_target IS INITIAL.
      CONCATENATE lv_where_clause
          ` and tbtco~tgtsrvgrp = '' and tbtco~execserver = ''` INTO lv_where_clause.
    ELSE.
      IF  iv_mode = mc_trg_mode_group_host  OR
          iv_mode = mc_trg_mode_group_group.
        DATA: lv_grpid(40) TYPE c.
        lv_grpid = me->get_srvgrp_id( iv_old_target ).
        CONCATENATE lv_where_clause ` and `
                      `tbtco~tgtsrvgrp = '` lv_grpid `'` INTO lv_where_clause.
      ELSE.
        CONCATENATE lv_where_clause ` and `
                      `tbtco~execserver = '` iv_old_target `'` INTO lv_where_clause.
      ENDIF.
    ENDIF.

    DO mc_max_iterations TIMES.
* Do select
      SELECT (lv_column_syntax) FROM (lv_dbtab_syntax)
           INTO CORRESPONDING FIELDS OF TABLE lt_selected_jobs
            UP TO mv_portion ROWS
            WHERE (lv_where_clause).

      DELETE ADJACENT DUPLICATES FROM lt_selected_jobs COMPARING jobname jobcount.

      IF mv_test_mode = abap_true.
        SELECT COUNT(*) FROM (lv_dbtab_syntax)
              INTO ev_total_matches
              WHERE (lv_where_clause).

        me->log_test_run( lt_selected_jobs  ).

        EXIT.
      ENDIF.

* Do update
      me->update_exec_target(
          it_selected_jobs = lt_selected_jobs
          iv_new_target    = lv_target
          iv_mode = iv_mode

             ).

      IF LINES( lt_selected_jobs ) < mv_portion.
        EXIT.
      ENDIF.
    ENDDO.

* Save header in trace
    me->log_save( ).

  ENDMETHOD.                    "change_exec_target


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method LCL_SBTI_MASS_JOB_CHANGE->CHANGE_PRINTER
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_OLD_PRINTER                 TYPE        SYPDEST
* | [--->] IV_NEW_PRINTER                 TYPE        SYPDEST
* | [--->] IV_NO_DIALOG                   TYPE        BOOLEAN (default =ABAP_TRUE)
* | [<-()] EV_TOTAL_MATCHES               TYPE        INT4
* | [!CX!] lcx_sbti_mass_job_change
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD change_printer.
    DATA: lv_where_clause TYPE string,
          lv_column_syntax TYPE string,
          lv_dbtab_syntax TYPE string,
          lt_selected_jobs TYPE btc_t_joblist.

    DATA: lv_pripar_key TYPE syprkey,
          lv_total_matches TYPE i,
          lv_padest TYPE sypdest,
          lt_pripar_keys TYPE STANDARD TABLE OF syprkey.

* Check input parameters
    IF iv_new_printer IS INITIAL.
      RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
        EXPORTING
          textid = lcx_sbti_mass_job_change=>cx_invalid_printer.
    ENDIF.
* Check printer name
    SELECT SINGLE padest FROM tsp03 INTO lv_padest
      WHERE padest = iv_new_printer.
    IF sy-subrc NE 0.
      RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
        EXPORTING
          textid = lcx_sbti_mass_job_change=>cx_invalid_printer
          pdest  = iv_new_printer.
    ENDIF.

* Search print parameters for a printer name
    SELECT key1 key2 FROM tpri_par INTO (lv_pripar_key+0(12), lv_pripar_key+12(4))
      WHERE appl  = 'B' AND
            pdest = iv_old_printer.
      APPEND lv_pripar_key TO lt_pripar_keys.
    ENDSELECT.


* Log header
    GET TIME STAMP FIELD ms_log_header-timestamp.
    ms_log_header-user = sy-uname.
    ms_log_header-client = sy-mandt.
    ms_log_header-test_run = mv_test_mode.
    ms_log_header-filter = mr_filter->get_filter_text( ).
    CONCATENATE 'PRINTER=' iv_old_printer ';'
     INTO ms_log_header-old_params.
    CONCATENATE 'PRINTER=' iv_new_printer ';'
     INTO ms_log_header-new_params.


* Loop
    LOOP AT lt_pripar_keys INTO lv_pripar_key.

* Get select syntax
      mr_filter->get_select_syntax(
        IMPORTING
          ev_where_clause  = lv_where_clause
          ev_column_syntax = lv_column_syntax
          ev_dbtab_syntax  = lv_dbtab_syntax
             ).

* Update column syntax
* nothing to do here

* Update DB tables syntax
      CONCATENATE lv_dbtab_syntax ` inner join tbtcp as tbtcp on tbtco~jobname = tbtcp~jobname and tbtco~jobcount = tbtcp~jobcount`
             INTO lv_dbtab_syntax.

* Update Where clause
* Update report and variant
      ms_log_header-mode = mc_mode_change_variant.
      CONCATENATE lv_where_clause ` and `
                  `tbtcp~priparkey = '` lv_pripar_key `'` INTO lv_where_clause.

      DO mc_max_iterations TIMES.
* Do select
        SELECT (lv_column_syntax) FROM (lv_dbtab_syntax)
             INTO CORRESPONDING FIELDS OF TABLE lt_selected_jobs
              UP TO mv_portion ROWS
              WHERE (lv_where_clause).

        DELETE ADJACENT DUPLICATES FROM lt_selected_jobs COMPARING jobname jobcount.

        IF mv_test_mode = abap_true.
          SELECT COUNT(*) FROM (lv_dbtab_syntax)
                INTO lv_total_matches
                WHERE (lv_where_clause).

          me->log_test_run( lt_selected_jobs  ).
          ADD lv_total_matches TO ev_total_matches.
          EXIT.
        ENDIF.

* Do update
        me->update_printer(
            it_selected_jobs = lt_selected_jobs
            iv_no_dialog     = iv_no_dialog
            iv_old_key       = lv_pripar_key
            iv_new_printer   = iv_new_printer ).

        IF LINES( lt_selected_jobs ) < mv_portion.
          EXIT.
        ENDIF.
      ENDDO.

    ENDLOOP.


* Save header in trace
    me->log_save( ).

  ENDMETHOD.                    "change_printer


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method LCL_SBTI_MASS_JOB_CHANGE->CHANGE_REPORT_VARIANT
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_OLD_REPORT                  TYPE        BTCPROG
* | [--->] IV_OLD_VARIANT                 TYPE        BTCVARIANT(optional)
* | [--->] IV_NEW_REPORT                  TYPE        BTCPROG
* | [--->] IV_NEW_VARIANT                 TYPE        BTCVARIANT(optional)
* | [<-()] EV_TOTAL_MATCHES               TYPE        INT4
* | [!CX!] lcx_sbti_mass_job_change
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD change_report_variant.
    DATA: lv_where_clause TYPE string,
          lv_column_syntax TYPE string,
          lv_dbtab_syntax TYPE string,
          lt_selected_jobs TYPE btc_t_joblist.

* Check input parameters
    IF iv_old_report IS INITIAL.
* Report is initial
* Raise exception
      RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
        EXPORTING
          textid = lcx_sbti_mass_job_change=>cx_no_report.
    ENDIF.

* Check vreport/variant existence
    IF iv_new_variant IS INITIAL.
      DATA:
            lv_secu TYPE trdir-secu,
            lv_subc TYPE trdir-subc.

      SELECT SINGLE subc secu FROM trdir INTO (lv_subc, lv_secu)
        WHERE name = iv_new_report.
      IF sy-subrc NE 0.
* Report_does_not_exist
* Raise exception
        RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
          EXPORTING
            textid = lcx_sbti_mass_job_change=>cx_no_report
            report = iv_new_report.
      ENDIF.
      IF lv_subc NE '1'.
* Report_not_to_be_scheduled
        RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
          EXPORTING
            textid = lcx_sbti_mass_job_change=>cx_report_not_to_schedule
            report = iv_new_report.
      ENDIF.
    ELSE.
      DATA lv_rc TYPE sysubrc.
      CALL FUNCTION 'RS_VARIANT_EXISTS'
        EXPORTING
          report              = iv_new_report
          variant             = iv_new_variant
        IMPORTING
          r_c                 = lv_rc
        EXCEPTIONS
          not_authorized      = 1
          no_report           = 2
          report_not_existent = 3
          report_not_supplied = 4
          OTHERS              = 5.
      IF sy-subrc NE 0.
* Raise exception
        RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
          EXPORTING
            textid = lcx_sbti_mass_job_change=>cx_no_report
            report = iv_new_report.
      ENDIF.
      IF lv_rc NE 0.
* Raise exception
        RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
          EXPORTING
            textid  = lcx_sbti_mass_job_change=>cx_no_variant
            report  = iv_new_report
            variant = iv_new_variant.
      ENDIF.
    ENDIF.

* Log header
    GET TIME STAMP FIELD ms_log_header-timestamp.
    ms_log_header-user = sy-uname.
    ms_log_header-client = sy-mandt.
    ms_log_header-test_run = mv_test_mode.
    ms_log_header-filter = mr_filter->get_filter_text( ).
    CONCATENATE 'PROGNAME=' iv_old_report '; VARIANT=' iv_old_variant
     INTO ms_log_header-old_params.
    CONCATENATE 'PROGNAME=' iv_new_report '; VARIANT=' iv_new_variant
     INTO ms_log_header-new_params.


* Get select syntax
    mr_filter->get_select_syntax(
      IMPORTING
        ev_where_clause  = lv_where_clause
        ev_column_syntax = lv_column_syntax
        ev_dbtab_syntax  = lv_dbtab_syntax
           ).

* Update column syntax
* noching to do here

* Update DB tables syntax
    CONCATENATE lv_dbtab_syntax ` inner join tbtcp as tbtcp on tbtco~jobname = tbtcp~jobname and tbtco~jobcount = tbtcp~jobcount`
           INTO lv_dbtab_syntax.

* Update Where clause
    IF iv_old_variant = iv_new_variant.
* Update report
      ms_log_header-mode = mc_mode_change_report.
      CONCATENATE lv_where_clause ` and `
                  `tbtcp~progname = '` iv_old_report `'` INTO lv_where_clause.
    ELSE.
* Update report and variant
      ms_log_header-mode = mc_mode_change_variant.
      CONCATENATE lv_where_clause ` and `
                  `tbtcp~variant = '` iv_old_variant `' and `
                  `tbtcp~progname = '` iv_old_report `'` INTO lv_where_clause.
    ENDIF.

    DO mc_max_iterations TIMES.
* Do select
      SELECT (lv_column_syntax) FROM (lv_dbtab_syntax)
           INTO CORRESPONDING FIELDS OF TABLE lt_selected_jobs
            UP TO mv_portion ROWS
            WHERE (lv_where_clause).

      DELETE ADJACENT DUPLICATES FROM lt_selected_jobs COMPARING jobname jobcount.

      IF mv_test_mode = abap_true.
        SELECT COUNT(*) FROM (lv_dbtab_syntax)
              INTO ev_total_matches
              WHERE (lv_where_clause).

        me->log_test_run( lt_selected_jobs  ).

        EXIT.
      ENDIF.

* Do update
      me->update_report_variant(
          it_selected_jobs = lt_selected_jobs
          iv_old_report    = iv_old_report
          iv_old_variant   = iv_old_variant
          iv_new_report    = iv_new_report
          iv_new_variant   = iv_new_variant
             ).

      IF LINES( lt_selected_jobs ) < mv_portion.
        EXIT.
      ENDIF.
    ENDDO.

* Save header in trace
    me->log_save( ).

  ENDMETHOD.                    "change_report_variant


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method LCL_SBTI_MASS_JOB_CHANGE->CHANGE_SPOOL_RECIPIENT
* +-------------------------------------------------------------------------------------------------+
* | [!CX!] lcx_sbti_mass_job_change
* +--------------------------------------------------------------------------------------</SIGNATURE>
*  METHOD change_spool_recipient.
*  ENDMETHOD.                    "CHANGE_SPOOL_RECIPIENT


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method LCL_SBTI_MASS_JOB_CHANGE->CHANGE_USER
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_OLD_USER                    TYPE        UNAME
* | [--->] IV_CHANGE_STEP_USER            TYPE        BOOLEAN
* | [--->] IV_CHANGE_OWNER                TYPE        BOOLEAN
* | [--->] IV_NEW_USER                    TYPE        UNAME
* | [<-()] EV_TOTAL_MATCHES               TYPE        INT4
* | [!CX!] lcx_sbti_mass_job_change
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD change_user.
    DATA: lv_where_clause TYPE string,
          lv_column_syntax TYPE string,
          lv_dbtab_syntax TYPE string,
          lt_selected_jobs TYPE btc_t_joblist.
    DATA: ls_bapi_ret TYPE bapiret2.


* Check input parameters
    IF iv_old_user IS INITIAL.
* Raise exception
      RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
        EXPORTING
          textid = lcx_sbti_mass_job_change=>cx_no_step_user.
    ENDIF.

* Check new user
    CALL FUNCTION 'BAPI_USER_EXISTENCE_CHECK'
      EXPORTING
        username = iv_new_user
      IMPORTING
        return   = ls_bapi_ret.
    IF ls_bapi_ret-type NE 'S' AND  ls_bapi_ret-type NE 'I'.
* Raise exception
      RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
        EXPORTING
          textid = lcx_sbti_mass_job_change=>cx_step_user_not_found
          user   = iv_new_user.
    ENDIF.

* Log header
    GET TIME STAMP FIELD ms_log_header-timestamp.
    ms_log_header-user = sy-uname.
    ms_log_header-client = sy-mandt.
    ms_log_header-test_run = mv_test_mode.
    ms_log_header-filter = mr_filter->get_filter_text( ).
    CONCATENATE 'AUTHCKNAM=' iv_old_user
     INTO ms_log_header-old_params.
    CONCATENATE 'AUTHCKNAM=' iv_new_user
     INTO ms_log_header-new_params.

* Get select syntax
    mr_filter->get_select_syntax(
      IMPORTING
        ev_where_clause  = lv_where_clause
        ev_column_syntax = lv_column_syntax
        ev_dbtab_syntax  = lv_dbtab_syntax
           ).

* Update column syntax
* nothing to do here

* Update DB tables syntax
    CONCATENATE lv_dbtab_syntax ` inner join tbtcp as tbtcp on tbtco~jobname = tbtcp~jobname and tbtco~jobcount = tbtcp~jobcount`
           INTO lv_dbtab_syntax.

* Update Where clause
    ms_log_header-mode = mc_mode_change_user.
    IF iv_change_owner = abap_true AND iv_change_step_user = abap_true.
      CONCATENATE lv_where_clause ` and `
                  `( tbtcp~authcknam = '` iv_old_user `' or tbtco~sdluname = '` iv_old_user `' )` INTO lv_where_clause.
    ELSEIF iv_change_owner = abap_true.
      CONCATENATE lv_where_clause ` and `
                  `tbtco~sdluname = '` iv_old_user `'` INTO lv_where_clause.
    ELSEIF iv_change_step_user = abap_true.
      CONCATENATE lv_where_clause ` and `
                  `tbtcp~authcknam = '` iv_old_user `'` INTO lv_where_clause.
    ELSE.
* Nothing to do here
      EXIT.
    ENDIF.


    DO mc_max_iterations TIMES.
* Do select
      SELECT (lv_column_syntax) FROM (lv_dbtab_syntax)
           INTO CORRESPONDING FIELDS OF TABLE lt_selected_jobs
            UP TO mv_portion ROWS
            WHERE (lv_where_clause).

      DELETE ADJACENT DUPLICATES FROM lt_selected_jobs COMPARING jobname jobcount.

      IF mv_test_mode = abap_true.
        SELECT COUNT(*) FROM (lv_dbtab_syntax)
              INTO ev_total_matches
              WHERE (lv_where_clause).

        me->log_test_run( lt_selected_jobs  ).

        EXIT.
      ENDIF.

* Do update
      me->update_user(
          it_selected_jobs = lt_selected_jobs
          iv_old_user = iv_old_user
          iv_new_user = iv_new_user
          iv_change_step_user = iv_change_step_user
          iv_change_owner     = iv_change_owner
             ).
      IF LINES( lt_selected_jobs ) < mv_portion.
        EXIT.
      ENDIF.
    ENDDO.

* Save header in trace
    me->log_save( ).

  ENDMETHOD.                    "change_user


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method LCL_SBTI_MASS_JOB_CHANGE->CHECK_TARGET
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_MODE                        TYPE        I
* | [--->] IV_TARGET                      TYPE        CHAR20
* | [<-()] EV_TARGET                      TYPE        CHAR40
* | [!CX!] lcx_sbti_mass_job_change
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD check_target.

    IF iv_target IS INITIAL.
      EXIT.
    ENDIF.

    IF iv_mode = mc_trg_mode_group_group OR iv_mode = mc_trg_mode_host_group.
* Target is a server group
      ev_target = me->get_srvgrp_id( iv_target ).

    ELSEIF iv_mode = mc_trg_mode_group_host OR iv_mode = mc_trg_mode_host_host.
* Target is server
      DATA lt_list TYPE STANDARD TABLE OF msxxlist.
      CALL FUNCTION 'TH_SERVER_LIST'
*      EXPORTING
*        services       = 8
        TABLES
          list           = lt_list
        EXCEPTIONS
          no_server_list = 1
          OTHERS         = 2.
      IF sy-subrc <> 0.
* Exception
        RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
          EXPORTING
            textid = lcx_sbti_mass_job_change=>cx_th_server_list_na.
      ENDIF.
      READ TABLE lt_list WITH KEY name = iv_target TRANSPORTING NO FIELDS.
      IF sy-subrc NE 0.
* Exception
        RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
          EXPORTING
            textid = lcx_sbti_mass_job_change=>cx_invalid_host
            target = iv_target.
      ENDIF.
      ev_target = iv_target.
    ENDIF.

  ENDMETHOD.                    "check_target


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method LCL_SBTI_MASS_JOB_CHANGE->CONSTRUCTOR
* +-------------------------------------------------------------------------------------------------+
* | [--->] IR_FILTER                      TYPE REF TO LCL_SBTI_JOB_FILTER_DEFAULT
* | [--->] IV_PORTION                     TYPE        INT4 (default =1000)
* | [--->] IV_DO_COMMIT                   TYPE        BOOLEAN (default =ABAP_TRUE)
* | [--->] IV_TEST_MODE                   TYPE        BOOLEAN (default =ABAP_TRUE)
* | [!CX!] lcx_sbti_mass_job_change
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD constructor.
    mr_filter = ir_filter.
    mv_portion = iv_portion.
    mv_do_commit = iv_do_commit.
    mv_test_mode = iv_test_mode.

* Authority checks
    AUTHORITY-CHECK
      OBJECT 'S_BTCH_ADM'
          ID 'BTCADMIN' FIELD 'Y'.
    IF sy-subrc NE 0.
* Raise exception
      RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
        EXPORTING
          textid = lcx_sbti_mass_job_change=>cx_no_authority.
    ENDIF.


  ENDMETHOD.                    "constructor


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method LCL_SBTI_MASS_JOB_CHANGE->DEQUEUE
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_JOBNAME                     TYPE        BTCJOB
* | [--->] IV_JOBCOUNT                    TYPE        BTCJOBCNT
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD dequeue.
    DATA lv_rc TYPE i.
* Enqueue Job
    PERFORM deq_job IN PROGRAM saplbtch
              USING
                 iv_jobname
                 iv_jobcount
                 'N'
              CHANGING
                 lv_rc.
  ENDMETHOD.                    "dequeue


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method LCL_SBTI_MASS_JOB_CHANGE->ENQUEUE
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_JOBNAME                     TYPE        BTCJOB
* | [--->] IV_JOBCOUNT                    TYPE        BTCJOBCNT
* | [<-()] EV_RC                          TYPE        INT1
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD enqueue.

*    DATA lv_rc TYPE i.
** Enqueue Job
*    PERFORM enq_job IN PROGRAM saplbtch
*              USING
*                 iv_jobname
*                 iv_jobcount
**                 abap_true
*                 'N'
**              CHANGING
*                 lv_rc.

    CALL FUNCTION 'ENQUEUE_ESTBTCO'
      EXPORTING
         jobname        = iv_jobname
         jobcount       = iv_jobcount
         _scope         = '1'
      EXCEPTIONS
         foreign_lock   = 201
         system_failure = 203
         OTHERS         = 203.


    ev_rc = sy-subrc.

  ENDMETHOD.                    "enqueue


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method LCL_SBTI_MASS_JOB_CHANGE->GET_SRVGRP_ID
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_GRP                         TYPE        BPSRVGRP
* | [<-()] EV_ID                          TYPE        CSMSYSGUID
* | [!CX!] lcx_sbti_mass_job_change
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD get_srvgrp_id.
    IF iv_grp IS INITIAL.
* Empty group is correct
      EXIT.
    ENDIF.
    TRANSLATE iv_grp TO UPPER CASE.
    SELECT SINGLE guid FROM tsrvgrp INTO ev_id
       WHERE grpname = iv_grp.                              "#EC WARNOK
    IF sy-subrc NE 0.
* Exception
      RAISE EXCEPTION TYPE lcx_sbti_mass_job_change
        EXPORTING
          textid = lcx_sbti_mass_job_change=>cx_invalid_group
          target = iv_grp.

    ENDIF.
  ENDMETHOD.                    "get_srvgrp_id


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Public Method LCL_SBTI_MASS_JOB_CHANGE->LOG_GET
* +-------------------------------------------------------------------------------------------------+
* | [<---] ES_HEADER                      TYPE        LT_S_ERROR_HEADER
* | [<---] ET_RECORDS                     TYPE        LT_T_ERROR_LOG
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD log_get.
    es_header = ms_log_header.
    et_records = mt_log_records.
  ENDMETHOD.                    "LOG_GET


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method LCL_SBTI_MASS_JOB_CHANGE->LOG_JOB_CHANGE
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_JOBNAME                     TYPE        BTCJOB
* | [--->] IV_JOBCOUNT                    TYPE        BTCJOBCNT
* | [--->] IV_STATUS                      TYPE        C
* | [--->] IV_RC                          TYPE        INT1
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD log_job_change.
    DATA ls_log TYPE lt_s_error_log.
    IF mv_test_mode NE abap_true.
      IF iv_rc = 0.
        ADD 1 TO ms_log_header-changed_jobs.
      ELSE.
        ADD 1 TO ms_log_header-errors.
      ENDIF.
    ENDIF.

    ls_log-jobname = iv_jobname.
    ls_log-jobcount = iv_jobcount.
    ls_log-status = iv_status.
    ls_log-rc = iv_rc.
    APPEND ls_log TO mt_log_records.
  ENDMETHOD.                    "log_job_change


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method LCL_SBTI_MASS_JOB_CHANGE->LOG_SAVE
* +-------------------------------------------------------------------------------------------------+
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD log_save.
    DATA: lv_par_txt1(254) TYPE c,
          lv_par_txt2(254) TYPE c.
    lv_par_txt1 = ms_log_header-old_params.
    lv_par_txt2 = ms_log_header-new_params.
    CALL 'WriteTrace'
          ID 'CALL' FIELD 'CL_SBTI_MADSS_JOB_CHANGE'        "#EC NOTEXT
          ID 'PAR1' FIELD ms_log_header-user
          ID 'PAR2' FIELD ms_log_header-client
          ID 'PAR3' FIELD ms_log_header-mode
          ID 'PAR4' FIELD ms_log_header-test_run.

    CALL 'WriteTrace'
          ID 'CALL' FIELD 'CL_SBTI_MADSS_JOB_CHANGE'        "#EC NOTEXT
          ID 'PAR1' FIELD ms_log_header-changed_jobs
          ID 'PAR2' FIELD ms_log_header-errors
          ID 'PAR3' FIELD lv_par_txt1
          ID 'PAR4' FIELD lv_par_txt2.
  ENDMETHOD.                    "log_save


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method LCL_SBTI_MASS_JOB_CHANGE->LOG_TEST_RUN
* +-------------------------------------------------------------------------------------------------+
* | [--->] IT_SELECTED_JOBS               TYPE        BTC_T_JOBLIST
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD log_test_run.
    DATA: ls_job_key TYPE tbtck,
          lv_job_status TYPE c.

    LOOP AT it_selected_jobs INTO ls_job_key.
* Check job status
      SELECT SINGLE status FROM tbtco INTO lv_job_status
        WHERE jobname  = ls_job_key-jobname AND
              jobcount = ls_job_key-jobcount.
      IF sy-subrc NE 0.
        CONTINUE.
      ENDIF.
* Write error
      me->log_job_change(
          iv_jobname  = ls_job_key-jobname
          iv_jobcount = ls_job_key-jobcount
          iv_status   = lv_job_status
          iv_rc       = 0
             ).
    ENDLOOP.

  ENDMETHOD.                    "log_test_run


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method LCL_SBTI_MASS_JOB_CHANGE->UPDATE_EXEC_TARGET
* +-------------------------------------------------------------------------------------------------+
* | [--->] IT_SELECTED_JOBS               TYPE        BTC_T_JOBLIST
* | [--->] IV_NEW_TARGET                  TYPE        CHAR40
* | [--->] IV_MODE                        TYPE        I
* | [!CX!] lcx_sbti_mass_job_change
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD update_exec_target.
    DATA: ls_job_key TYPE tbtck,
          lv_rc TYPE i,
          lv_job_status TYPE c.

    LOOP AT it_selected_jobs INTO ls_job_key.
      lv_rc = me->enqueue(
          iv_jobname  = ls_job_key-jobname
          iv_jobcount = ls_job_key-jobcount
             ).
      IF lv_rc NE 0.
* Cannt enqueue
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = space
            iv_rc       = mc_rc_cant_enqueue
               ).
        CONTINUE.
      ENDIF.

* Check job status
      SELECT SINGLE status FROM tbtco INTO lv_job_status
        WHERE jobname  = ls_job_key-jobname AND
              jobcount = ls_job_key-jobcount.
      IF sy-subrc NE 0.
* Job does not exists anymore
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = lv_job_status
            iv_rc       = mc_rc_not_exists
               ).
* Dequeue
        me->dequeue(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
               ).
        CONTINUE.
      ENDIF.
      IF lv_job_status NE 'P' AND lv_job_status NE 'S' AND lv_job_status NE 'Z'. "d023157
* Job status is changed, cannot change job anymore
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = lv_job_status
            iv_rc       = mc_rc_incorrect_status
               ).
* Dequeue
        me->dequeue(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
               ).
        CONTINUE.
      ENDIF.

* Update target in tbtcs
      IF ( lv_job_status = 'S' or lv_job_status = 'Z' ). "d023157
        IF   iv_mode = mc_trg_mode_group_host  OR
             iv_mode = mc_trg_mode_host_host.
          UPDATE tbtcs SET
           execserver = iv_new_target
           WHERE jobname  = ls_job_key-jobname AND
                 jobcount = ls_job_key-jobcount.
        ELSE.
          UPDATE tbtcs SET
           tgtsrvgrp = iv_new_target
           WHERE jobname  = ls_job_key-jobname AND
                 jobcount = ls_job_key-jobcount.
        ENDIF.
        IF sy-subrc NE 0.                                   "#EC NEEDED
* Nothing to do; possible
        ENDIF.

      ENDIF.

* Update target and
* last change end release info in tbtco
      IF   iv_mode = mc_trg_mode_group_host  OR
           iv_mode = mc_trg_mode_host_host.
* update execserver
        IF lv_job_status = 'S'.
          UPDATE tbtco SET
          execserver = iv_new_target
          reldate = sy-datum
          reltime = sy-uzeit
          reluname = sy-uname
          lastchdate = sy-datum
          lastchtime = sy-uzeit
          lastchname = sy-uname
          WHERE jobname  = ls_job_key-jobname AND
                jobcount = ls_job_key-jobcount.
        ELSE.
          UPDATE tbtco SET
          execserver = iv_new_target
          lastchdate = sy-datum
          lastchtime = sy-uzeit
          lastchname = sy-uname
          WHERE jobname  = ls_job_key-jobname AND
                 jobcount = ls_job_key-jobcount.

        ENDIF.
      ELSE.
        IF lv_job_status = 'S'.
          UPDATE tbtco SET
          tgtsrvgrp = iv_new_target
          reldate = sy-datum
          reltime = sy-uzeit
          reluname = sy-uname
          lastchdate = sy-datum
          lastchtime = sy-uzeit
          lastchname = sy-uname
          WHERE jobname  = ls_job_key-jobname AND
                 jobcount = ls_job_key-jobcount.
        ELSE.
          UPDATE tbtco SET
          tgtsrvgrp = iv_new_target
          lastchdate = sy-datum
          lastchtime = sy-uzeit
          lastchname = sy-uname
          WHERE jobname  = ls_job_key-jobname AND
                 jobcount = ls_job_key-jobcount.

        ENDIF.
      ENDIF.
      IF sy-subrc NE 0.
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = lv_job_status
            iv_rc       = mc_rc_cannt_update_tbtco
               ).
* Dequeue
        me->dequeue(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
               ).
        CONTINUE.
      ENDIF.

* Successfull
      me->log_job_change(
          iv_jobname  = ls_job_key-jobname
          iv_jobcount = ls_job_key-jobcount
          iv_status   = lv_job_status
          iv_rc       = '0'
             ).
* DB Commit
      IF mv_do_commit = abap_true.
        CALL FUNCTION 'DB_COMMIT'.
      ENDIF.

* Dequeue
      me->dequeue(
          iv_jobname  = ls_job_key-jobname
          iv_jobcount = ls_job_key-jobcount
             ).
    ENDLOOP.

  ENDMETHOD.                    "update_exec_target


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method LCL_SBTI_MASS_JOB_CHANGE->UPDATE_PRINTER
* +-------------------------------------------------------------------------------------------------+
* | [--->] IT_SELECTED_JOBS               TYPE        BTC_T_JOBLIST
* | [--->] IV_OLD_KEY                     TYPE        SYPRKEY
* | [--->] IV_NEW_PRINTER                 TYPE        SYPDEST
* | [--->] IV_NO_DIALOG                   TYPE        BOOLEAN
* | [!CX!] lcx_sbti_mass_job_change
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD update_printer.
    DATA: ls_job_key TYPE tbtck,
          lv_rc TYPE i,
          lv_job_status TYPE c,
          lv_user TYPE uname,
          lv_progname TYPE btcprog,
          lv_stepcount TYPE btcstepcnt,
          lv_valid TYPE c,
          lv_continue TYPE boolean,
          lv_new_key LIKE iv_old_key,
          ls_pri_par TYPE pri_params,
          ls_pri_par_new TYPE pri_params.

    IF NOT it_selected_jobs IS INITIAL.
* Load old priparams
      CALL FUNCTION 'LOAD_PRINT_PARAMETERS'
        EXPORTING
          key            = iv_old_key
        IMPORTING
          out_parameters = ls_pri_par
          key_found      = lv_valid
        EXCEPTIONS
          error_occured  = 1
          OTHERS         = 2.
      IF sy-subrc <> 0 OR lv_valid NE 'X'.
        me->log_job_change(
            iv_jobname  = '*'
            iv_jobcount = '*'
            iv_status   = '*'
            iv_rc       = mc_rc_cannt_load_pripar
               ).
        EXIT.
      ENDIF.
* Update PDEST parameter
      CALL FUNCTION 'GET_PRINT_PARAMETERS'
        EXPORTING
          no_dialog              = iv_no_dialog
          destination            = iv_new_printer
          in_parameters          = ls_pri_par
        IMPORTING
          out_parameters         = ls_pri_par_new
          valid                  = lv_valid
        EXCEPTIONS
          archive_info_not_found = 1
          invalid_print_params   = 2
          invalid_archive_params = 3
          OTHERS                 = 4.
      IF sy-subrc <> 0 OR lv_valid NE 'X'.
        me->log_job_change(
            iv_jobname  = '*'
            iv_jobcount = '*'
            iv_status   = '*'
            iv_rc       = mc_rc_cannt_update_pripar
               ).
        EXIT.
      ENDIF.

      LOOP AT it_selected_jobs INTO ls_job_key.
        lv_rc = me->enqueue(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
               ).
        IF lv_rc NE 0.
* Cannt enqueue
          me->log_job_change(
              iv_jobname  = ls_job_key-jobname
              iv_jobcount = ls_job_key-jobcount
              iv_status   = space
              iv_rc       = mc_rc_cant_enqueue
                 ).
          CONTINUE.
        ENDIF.

* Check job status
        SELECT SINGLE status FROM tbtco INTO lv_job_status
          WHERE jobname  = ls_job_key-jobname AND
                jobcount = ls_job_key-jobcount.
        IF sy-subrc NE 0.
* Job does not exists anymore
          me->log_job_change(
              iv_jobname  = ls_job_key-jobname
              iv_jobcount = ls_job_key-jobcount
              iv_status   = lv_job_status
              iv_rc       = mc_rc_not_exists
                 ).
* Dequeue
          me->dequeue(
              iv_jobname  = ls_job_key-jobname
              iv_jobcount = ls_job_key-jobcount
                 ).
          CONTINUE.
        ENDIF.
        IF lv_job_status NE 'P' AND lv_job_status NE 'S' AND lv_job_status NE 'Z'. "d023157
* Job status is changed, cannot change job anymore
          me->log_job_change(
              iv_jobname  = ls_job_key-jobname
              iv_jobcount = ls_job_key-jobcount
              iv_status   = lv_job_status
              iv_rc       = mc_rc_incorrect_status
                 ).
* Dequeue
          me->dequeue(
              iv_jobname  = ls_job_key-jobname
              iv_jobcount = ls_job_key-jobcount
                 ).
          CONTINUE.
        ENDIF.

* Loop at steps
        SELECT stepcount progname authcknam FROM tbtcp INTO (lv_stepcount, lv_progname,lv_user)
                  WHERE jobname  = ls_job_key-jobname AND
                        jobcount = ls_job_key-jobcount AND
                        priparkey = iv_old_key.

* Store new print params
          CALL FUNCTION 'STORE_PRINT_PARAMETERS'
            EXPORTING
              in_parameters = ls_pri_par_new
              applikation   = 'B'
              user          = lv_user
              priprog       = lv_progname
            IMPORTING
              key           = lv_new_key
            EXCEPTIONS
              error_occured = 1
              OTHERS        = 2.
          IF sy-subrc <> 0.
            lv_continue = abap_true.
            me->log_job_change(
                iv_jobname  = ls_job_key-jobname
                iv_jobcount = ls_job_key-jobcount
                iv_status   = lv_job_status
                iv_rc       = mc_rc_cannt_save_pripar
                   ).
            EXIT.
          ENDIF.

* Update printer in tbtcp
          UPDATE tbtcp
           SET   priparkey = lv_new_key
           WHERE jobname  = ls_job_key-jobname  AND
                 jobcount = ls_job_key-jobcount AND
                 stepcount = lv_stepcount.
*               priparkey = iv_old_key.
          IF sy-subrc NE 0.
* Write error
            me->log_job_change(
                iv_jobname  = ls_job_key-jobname
                iv_jobcount = ls_job_key-jobcount
                iv_status   = lv_job_status
                iv_rc       = mc_rc_cannt_update_tbtcp
                   ).
            lv_continue = abap_true.
            EXIT.
          ENDIF.
        ENDSELECT.
        IF   lv_continue = abap_true.
          lv_continue = abap_false.
* Dequeue
          me->dequeue(
              iv_jobname  = ls_job_key-jobname
              iv_jobcount = ls_job_key-jobcount
                 ).
          CONTINUE.
        ENDIF.

* Update last change end relese info in tbtco
        IF lv_job_status = 'S'.
          UPDATE tbtco SET
          reldate = sy-datum
          reltime = sy-uzeit
          reluname = sy-uname
          lastchdate = sy-datum
          lastchtime = sy-uzeit
          lastchname = sy-uname
          WHERE jobname  = ls_job_key-jobname AND
                jobcount = ls_job_key-jobcount.
        ELSE.
          UPDATE tbtco SET
          lastchdate = sy-datum
          lastchtime = sy-uzeit
          lastchname = sy-uname
          WHERE jobname  = ls_job_key-jobname AND
                jobcount = ls_job_key-jobcount.
        ENDIF.
        IF sy-subrc NE 0.
          me->log_job_change(
              iv_jobname  = ls_job_key-jobname
              iv_jobcount = ls_job_key-jobcount
              iv_status   = lv_job_status
              iv_rc       = mc_rc_cannt_update_tbtco
                 ).
* Dequeue
          me->dequeue(
              iv_jobname  = ls_job_key-jobname
              iv_jobcount = ls_job_key-jobcount
                 ).
          CONTINUE.
        ENDIF.

* Successfull
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = lv_job_status
            iv_rc       = '0'
               ).
* DB Commit
        IF mv_do_commit = abap_true.
          CALL FUNCTION 'DB_COMMIT'.
        ENDIF.

* Dequeue
        me->dequeue(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
               ).
      ENDLOOP.

    ENDIF.
  ENDMETHOD.                    "update_printer


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method LCL_SBTI_MASS_JOB_CHANGE->UPDATE_REPORT_VARIANT
* +-------------------------------------------------------------------------------------------------+
* | [--->] IT_SELECTED_JOBS               TYPE        BTC_T_JOBLIST
* | [--->] IV_OLD_REPORT                  TYPE        BTCPROG
* | [--->] IV_OLD_VARIANT                 TYPE        BTCVARIANT(optional)
* | [--->] IV_NEW_REPORT                  TYPE        BTCPROG
* | [--->] IV_NEW_VARIANT                 TYPE        BTCVARIANT(optional)
* | [!CX!] lcx_sbti_mass_job_change
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD update_report_variant.
    DATA: ls_job_key TYPE tbtck,
          lv_rc TYPE i,
          lv_job_status TYPE c.

    LOOP AT it_selected_jobs INTO ls_job_key.
      lv_rc = me->enqueue(
          iv_jobname  = ls_job_key-jobname
          iv_jobcount = ls_job_key-jobcount
             ).
      IF lv_rc NE 0.
* Cannt enqueue
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = space
            iv_rc       = mc_rc_cant_enqueue
               ).
        CONTINUE.
      ENDIF.

* Check job status
      SELECT SINGLE status FROM tbtco INTO lv_job_status
        WHERE jobname  = ls_job_key-jobname AND
              jobcount = ls_job_key-jobcount.
      IF sy-subrc NE 0.
* Job does not exists anymore
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = lv_job_status
            iv_rc       = mc_rc_not_exists
               ).
* Dequeue
        me->dequeue(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
               ).
        CONTINUE.
      ENDIF.
      IF lv_job_status NE 'P' AND lv_job_status NE 'S' AND lv_job_status NE 'Z'. "d023157
* Job status is changed, cannot change job anymore
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = lv_job_status
            iv_rc       = mc_rc_incorrect_status
               ).
* Dequeue
        me->dequeue(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
               ).
        CONTINUE.
      ENDIF.

* Update progname and variant in tbtcp
      IF iv_old_variant = iv_new_variant.
        UPDATE tbtcp
         SET   progname = iv_new_report
         WHERE jobname  = ls_job_key-jobname  AND
               jobcount = ls_job_key-jobcount AND
               progname = iv_old_report.
      ELSE.
        UPDATE tbtcp
         SET   progname = iv_new_report
               variant  = iv_new_variant
         WHERE jobname  = ls_job_key-jobname  AND
               jobcount = ls_job_key-jobcount AND
               progname = iv_old_report       AND
               variant  = iv_old_variant.
      ENDIF.
      IF sy-subrc NE 0.
* Write error
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = lv_job_status
            iv_rc       = mc_rc_cannt_update_tbtcp
               ).
* Dequeue
        me->dequeue(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
               ).
        CONTINUE.
      ENDIF.

* Update last change end relese info in tbtco
      IF lv_job_status = 'S'.
        UPDATE tbtco SET
        reldate = sy-datum
        reltime = sy-uzeit
        reluname = sy-uname
        lastchdate = sy-datum
        lastchtime = sy-uzeit
        lastchname = sy-uname
        WHERE jobname  = ls_job_key-jobname AND
               jobcount = ls_job_key-jobcount.
      ELSE.
        UPDATE tbtco SET
        lastchdate = sy-datum
        lastchtime = sy-uzeit
        lastchname = sy-uname
        WHERE jobname  = ls_job_key-jobname AND
               jobcount = ls_job_key-jobcount.

      ENDIF.
      IF sy-subrc NE 0.
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = lv_job_status
            iv_rc       = mc_rc_cannt_update_tbtco
               ).
* Dequeue
        me->dequeue(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
               ).
        CONTINUE.
      ENDIF.

* Successfull
      me->log_job_change(
          iv_jobname  = ls_job_key-jobname
          iv_jobcount = ls_job_key-jobcount
          iv_status   = lv_job_status
          iv_rc       = '0'
             ).
* DB Commit
      IF mv_do_commit = abap_true.
        CALL FUNCTION 'DB_COMMIT'.
      ENDIF.

* Dequeue
      me->dequeue(
          iv_jobname  = ls_job_key-jobname
          iv_jobcount = ls_job_key-jobcount
             ).
    ENDLOOP.

  ENDMETHOD.                    "update_report_variant


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Private Method LCL_SBTI_MASS_JOB_CHANGE->UPDATE_USER
* +-------------------------------------------------------------------------------------------------+
* | [--->] IT_SELECTED_JOBS               TYPE        BTC_T_JOBLIST
* | [--->] IV_OLD_USER                    TYPE        UNAME
* | [--->] IV_NEW_USER                    TYPE        UNAME
* | [--->] IV_CHANGE_STEP_USER            TYPE        BOOLEAN
* | [--->] IV_CHANGE_OWNER                TYPE        BOOLEAN
* | [!CX!] lcx_sbti_mass_job_change
* +--------------------------------------------------------------------------------------</SIGNATURE>
  METHOD update_user.
    DATA: ls_job_key TYPE tbtck,
          lv_rc TYPE i,
          lv_job_status TYPE c,
          lv_owner TYPE uname.

    LOOP AT it_selected_jobs INTO ls_job_key.
      lv_rc = me->enqueue(
          iv_jobname  = ls_job_key-jobname
          iv_jobcount = ls_job_key-jobcount
             ).
      IF lv_rc NE 0.
* Cannt enqueue
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = space
            iv_rc       = mc_rc_cant_enqueue
               ).
        CONTINUE.
      ENDIF.

* Check job status
      SELECT SINGLE status sdluname FROM tbtco INTO (lv_job_status,lv_owner)
        WHERE jobname  = ls_job_key-jobname AND
              jobcount = ls_job_key-jobcount.
      IF sy-subrc NE 0.
* Job does not exists anymore
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = lv_job_status
            iv_rc       = mc_rc_not_exists
               ).
* Dequeue
        me->dequeue(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
               ).
        CONTINUE.
      ENDIF.
      IF lv_job_status NE 'P' AND lv_job_status NE 'S' AND lv_job_status NE 'Z'. "d023157
* Job status is changed, cannot change job anymore
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = lv_job_status
            iv_rc       = mc_rc_incorrect_status
               ).
* Dequeue
        me->dequeue(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
               ).
        CONTINUE.
      ENDIF.

      IF iv_change_step_user = abap_true.
* Update step user in tbtcp
        UPDATE tbtcp
         SET   authcknam = iv_new_user
         WHERE jobname  = ls_job_key-jobname  AND
               jobcount = ls_job_key-jobcount AND
               authcknam = iv_old_user.
        IF sy-subrc NE 0 AND iv_change_owner NE abap_true.
* Write error
          me->log_job_change(
              iv_jobname  = ls_job_key-jobname
              iv_jobcount = ls_job_key-jobcount
              iv_status   = lv_job_status
              iv_rc       = mc_rc_cannt_update_tbtcp
                 ).
* Dequeue
          me->dequeue(
              iv_jobname  = ls_job_key-jobname
              iv_jobcount = ls_job_key-jobcount
                 ).
          CONTINUE.
        ENDIF.
      ENDIF.

* Update last change end relese info in tbtco
      IF iv_change_owner = abap_true AND lv_owner = iv_old_user.
        IF lv_job_status = 'S'.
          UPDATE tbtco SET
          sdluname = iv_new_user
          reldate = sy-datum
          reltime = sy-uzeit
          reluname = sy-uname
          lastchdate = sy-datum
          lastchtime = sy-uzeit
          lastchname = sy-uname
          WHERE jobname  = ls_job_key-jobname AND
                jobcount = ls_job_key-jobcount.
        ELSE.
          UPDATE tbtco SET
          sdluname = iv_new_user
          lastchdate = sy-datum
          lastchtime = sy-uzeit
          lastchname = sy-uname
          WHERE jobname  = ls_job_key-jobname AND
                jobcount = ls_job_key-jobcount.
        ENDIF.
      ELSE.
        IF lv_job_status = 'S'.
          UPDATE tbtco SET
          reldate = sy-datum
          reltime = sy-uzeit
          reluname = sy-uname
          lastchdate = sy-datum
          lastchtime = sy-uzeit
          lastchname = sy-uname
          WHERE jobname  = ls_job_key-jobname AND
                jobcount = ls_job_key-jobcount.
        ELSE.
          UPDATE tbtco SET
          lastchdate = sy-datum
          lastchtime = sy-uzeit
          lastchname = sy-uname
          WHERE jobname  = ls_job_key-jobname AND
                jobcount = ls_job_key-jobcount.
        ENDIF.
      ENDIF.
      IF sy-subrc NE 0.
        me->log_job_change(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
            iv_status   = lv_job_status
            iv_rc       = mc_rc_cannt_update_tbtco
               ).
* Dequeue
        me->dequeue(
            iv_jobname  = ls_job_key-jobname
            iv_jobcount = ls_job_key-jobcount
               ).
        CONTINUE.
      ENDIF.

* Successfull
      me->log_job_change(
          iv_jobname  = ls_job_key-jobname
          iv_jobcount = ls_job_key-jobcount
          iv_status   = lv_job_status
          iv_rc       = '0'
             ).
* DB Commit
      IF mv_do_commit = abap_true.
        CALL FUNCTION 'DB_COMMIT'.
      ENDIF.

* Dequeue
      me->dequeue(
          iv_jobname  = ls_job_key-jobname
          iv_jobcount = ls_job_key-jobcount
             ).
    ENDLOOP.

  ENDMETHOD.                    "update_user
ENDCLASS.                    "LCL_SBTI_MASS_JOB_CHANGE IMPLEMENTATION

***INCLUDE****


DATA: lr_mass_change TYPE REF TO lcl_sbti_mass_job_change,
      lx_exception   TYPE REF TO cx_sbti_exception,
      lv_text TYPE string.

DATA: lv_owner TYPE uname,
      lv_jobname TYPE btcjob.

SELECTION-SCREEN BEGIN OF BLOCK bl0 WITH FRAME TITLE txtgrp1.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) tjobname FOR FIELD jobname.
SELECT-OPTIONS: jobname  FOR lv_jobname NO INTERVALS.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) towner FOR FIELD owner.
SELECT-OPTIONS: owner    FOR lv_owner NO INTERVALS.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(24) tdfrom FOR FIELD dfrom.
PARAMETERS: dfrom    TYPE datum.
PARAMETERS: dto      TYPE datum.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK bl0.

SELECTION-SCREEN BEGIN OF BLOCK bl7 WITH FRAME TITLE txtgrp6.
SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: m_rpvar   RADIOBUTTON GROUP grp1.
SELECTION-SCREEN COMMENT 5(21) tm_rpvar FOR FIELD m_rpvar.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF BLOCK bl1 WITH FRAME TITLE txtgrp2.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) told_rep FOR FIELD old_rep.
PARAMETERS: old_rep     TYPE btcprog
                        MATCHCODE OBJECT help_trdir.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) told_var FOR FIELD old_var.
PARAMETERS: old_var     TYPE btcvariant.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) tnew_rep FOR FIELD new_rep.
PARAMETERS: new_rep     TYPE btcprog
                        MATCHCODE OBJECT help_trdir.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) tnew_var FOR FIELD new_var.
PARAMETERS: new_var     TYPE btcvariant.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK bl1.

SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: m_clusr   RADIOBUTTON GROUP grp1.
SELECTION-SCREEN COMMENT 5(21) tm_clusr FOR FIELD m_clusr.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF BLOCK bl2 WITH FRAME TITLE txtgrp3.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) told_use FOR FIELD old_user.
PARAMETERS: old_user    TYPE uname
                        MATCHCODE OBJECT user_comp.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) tnew_use FOR FIELD new_user.
PARAMETERS: new_user    TYPE uname
                        MATCHCODE OBJECT user_comp.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) tch_owne FOR FIELD ch_owner.
PARAMETERS: ch_owner   TYPE c AS CHECKBOX DEFAULT abap_true.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) tch_step FOR FIELD ch_stepu.
PARAMETERS: ch_stepu   TYPE c AS CHECKBOX DEFAULT abap_true.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK bl2.

SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: m_target   RADIOBUTTON GROUP grp1.
SELECTION-SCREEN COMMENT 5(21) tm_targe FOR FIELD m_target.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF BLOCK bl4 WITH FRAME TITLE txtgrp4.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) told_trg FOR FIELD old_trg.
PARAMETERS: old_trg  TYPE btcsrvname.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) tnew_trg FOR FIELD new_trg.
PARAMETERS: new_trg  TYPE btcsrvname.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) ttm_hh FOR FIELD tm_hh.
PARAMETERS: tm_hh   RADIOBUTTON GROUP grp2.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) ttm_hg FOR FIELD tm_hg.
PARAMETERS: tm_hg   RADIOBUTTON GROUP grp2.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) ttm_gh FOR FIELD tm_gh.
PARAMETERS: tm_gh   RADIOBUTTON GROUP grp2.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) ttm_gg FOR FIELD tm_gg.
PARAMETERS: tm_gg   RADIOBUTTON GROUP grp2.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK bl4.
SELECTION-SCREEN END OF BLOCK bl7.
SELECTION-SCREEN BEGIN OF BLOCK bl9 WITH FRAME TITLE txtgrp5.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) tportion FOR FIELD portion.
PARAMETERS: portion    TYPE int4 DEFAULT 500.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) ttestmod FOR FIELD testmode.
PARAMETERS: testmode   TYPE c AS CHECKBOX DEFAULT abap_true.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(21) tcommit FOR FIELD commit.
PARAMETERS: commit     TYPE c AS CHECKBOX DEFAULT abap_true.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK bl9.


AT SELECTION-SCREEN OUTPUT.
  towner  = `Created by`.                                   "#EC NOTEXT
  tjobname  = `Jobname`.                                    "#EC NOTEXT
  tdfrom  = `Created between`.                              "#EC NOTEXT
  told_rep  = `Old program`.                                "#EC NOTEXT
  told_var  = `Old variant`.                                "#EC NOTEXT
  tnew_rep  = `New program`.                                "#EC NOTEXT
  tnew_var  = `New variant`.                                "#EC NOTEXT
  told_use  = `Old user`.                                   "#EC NOTEXT
  tnew_use  = `New user`.                                   "#EC NOTEXT
  tch_owne  = `Change owner`.                               "#EC NOTEXT
  tch_step  = `Change step user`.                           "#EC NOTEXT
  told_trg  = `Old target`.                                 "#EC NOTEXT
  tnew_trg  = `New target`.                                 "#EC NOTEXT
  ttm_hh  = `Server->Server`.                               "#EC NOTEXT
  ttm_hg  = `Server->Group`.                                "#EC NOTEXT
  ttm_gh  = `Group->Server`.                                "#EC NOTEXT
  ttm_gg  = `Group->Group`.                                 "#EC NOTEXT
  tportion  = `Portion`.                                    "#EC NOTEXT
  ttestmod  = `Test mode`.                                  "#EC NOTEXT
  tcommit  = `Commit`.                                      "#EC NOTEXT
  tm_rpvar = 'change program/variant'.                      "#EC NOTEXT
  tm_clusr = 'change user'.                                 "#EC NOTEXT
  tm_targe = 'change target'.                               "#EC NOTEXT
  txtgrp1 = 'Filter Parameters'.                            "#EC NOTEXT
  txtgrp2 = 'Program/Variant'.                              "#EC NOTEXT
  txtgrp3 = 'User'.                                         "#EC NOTEXT
  txtgrp4 = 'Target'.                                       "#EC NOTEXT
  txtgrp5 = 'Execution Options'.                            "#EC NOTEXT
  txtgrp6 = 'Execution Mode'.                               "#EC NOTEXT


START-OF-SELECTION.

  DATA: lt_log    TYPE lcl_sbti_mass_job_change=>lt_t_error_log,
        ls_header TYPE lcl_sbti_mass_job_change=>lt_s_error_header,
        lr_filter TYPE REF TO lcl_sbti_job_filter_default,
        lv_answer TYPE c,
        lv_from   TYPE timestamp,
        lv_to     TYPE timestamp,
        lv_total  TYPE int4.


  IF sy-batch NE 'X' AND testmode NE abap_true.
    CALL FUNCTION 'POPUP_TO_CONFIRM'
      EXPORTING
        titlebar       = 'Change confirmation'(010)
        text_question  = 'The operation is nonreversible. Are you sure? '(011)
      IMPORTING
        answer         = lv_answer
      EXCEPTIONS
        text_not_found = 1
        OTHERS         = 2.
    IF sy-subrc <> 0.
      EXIT.
    ENDIF.
    IF lv_answer = '2' OR lv_answer = 'A'.
      EXIT.
    ENDIF.
  ENDIF.

  TRY.

* Date, Time -> timestamp
      CONVERT DATE dfrom TIME '000000' INTO TIME STAMP lv_from TIME ZONE sy-zonlo.
      CONVERT DATE dto TIME '000000' INTO TIME STAMP lv_to TIME ZONE sy-zonlo.

* Create filter
      CREATE OBJECT lr_filter
        EXPORTING
          iv_from      = lv_from
          it_job_name  = jobname[]
          it_job_owner = owner[]
          iv_to        = lv_to.

* Create mass change
      CREATE OBJECT lr_mass_change
        EXPORTING
          ir_filter    = lr_filter
          iv_portion   = portion
          iv_do_commit = commit
          iv_test_mode = testmode.

      IF m_rpvar = abap_true.
* Change report and variant
        lv_total = lr_mass_change->change_report_variant(
            iv_old_report  = old_rep
            iv_old_variant = old_var
            iv_new_report  = new_rep
            iv_new_variant = new_var
               ).
      ELSEIF m_clusr = abap_true.
* Change step_user
        lv_total = lr_mass_change->change_user(
            iv_old_user = old_user
            iv_change_step_user = ch_stepu
            iv_change_owner     = ch_owner
            iv_new_user = new_user
               ).
      ELSEIF m_target = abap_true.
* Change target
        DATA lv_mode TYPE i.
        IF tm_gg = 'X'.
          lv_mode = lcl_sbti_mass_job_change=>mc_trg_mode_group_group.
        ELSEIF tm_gh = 'X'.
          lv_mode = lcl_sbti_mass_job_change=>mc_trg_mode_group_host.
        ELSEIF tm_hg = 'X'.
          lv_mode = lcl_sbti_mass_job_change=>mc_trg_mode_host_group.
        ELSEIF tm_hh = 'X'.
          lv_mode = lcl_sbti_mass_job_change=>mc_trg_mode_host_host  .
        ENDIF.
        lv_total = lr_mass_change->change_exec_target(
            iv_old_target    = old_trg
            iv_new_target    = new_trg
            iv_mode          = lv_mode
               ).
      ENDIF.
* Get execution log
      lr_mass_change->log_get(
        IMPORTING
          es_header  = ls_header
          et_records = lt_log
             ).

* Write log
      PERFORM write_log
               TABLES
                  lt_log
               USING
                  ls_header.

* Totals
      IF  testmode = abap_true AND lv_total > 0.
        ULINE.
        WRITE:/ 'Total matches:'(027), lv_total.         "#EC TEXT_DIFF
      ENDIF.
    CATCH cx_sbti_exception INTO lx_exception.
*Exceptions handling
      lx_exception->show_as_infomessage( ).
      lv_text = lx_exception->get_text( ).
      WRITE:/ 'Error text:'(028), lv_text.               "#EC TEXT_DIFF
  ENDTRY.


*&---------------------------------------------------------------------*
*&      Form  write_log
*&---------------------------------------------------------------------*
FORM write_log TABLES it_log TYPE lcl_sbti_mass_job_change=>lt_t_error_log
               USING  is_header TYPE lcl_sbti_mass_job_change=>lt_s_error_header.
  DATA: ls_log TYPE lcl_sbti_mass_job_change=>lt_s_error_log,
        lv_date TYPE datum,
        lv_time TYPE uzeit.

  SORT it_log BY rc status jobname jobcount DESCENDING.

  CONVERT TIME STAMP is_header-timestamp TIME ZONE sy-zonlo
    INTO DATE lv_date TIME lv_time.

  WRITE:/ 'Test run: '(012), 25 is_header-test_run AS CHECKBOX INPUT OFF. "#EC TEXT_DIFF
  WRITE:/ 'Date: '(013), 25 lv_date.                     "#EC TEXT_DIFF
  WRITE:/ 'Time: '(014), 25 lv_time.                     "#EC TEXT_DIFF
  WRITE:/ 'Client: '(015), 25 is_header-client.          "#EC TEXT_DIFF
  WRITE:/ 'User: '(016), 25 is_header-user.              "#EC TEXT_DIFF
  WRITE:/ 'Filter: '(017), 25 is_header-filter.          "#EC TEXT_DIFF
  WRITE:/ 'Old job parameters: '(018), 25 is_header-old_params. "#EC TEXT_DIFF
  WRITE:/ 'New job parameters: '(019), 25 is_header-new_params. "#EC TEXT_DIFF
  WRITE:/ 'Total changed: '(020), 25 is_header-changed_jobs  LEFT-JUSTIFIED. "#EC TEXT_DIFF
  WRITE:/ 'Total errors: '(021), 25 is_header-errors  LEFT-JUSTIFIED. "#EC TEXT_DIFF
  ULINE.
  IF LINES( it_log ) > 0.
    WRITE: 22 'Jobname', 12 'Jobcount', 5 'Status', 1 'RC' . "#EC NOTEXT
    ULINE.
    LOOP AT it_log INTO ls_log.
      WRITE:/ ls_log-jobname UNDER 'Jobname',               "#EC NOTEXT
              ls_log-jobcount UNDER 'Jobcount',             "#EC NOTEXT
              ls_log-status UNDER 'Status',                 "#EC NOTEXT
              ls_log-rc UNDER 'RC'.                         "#EC NOTEXT
    ENDLOOP.
  ELSE.
    WRITE:/ 'No jobs found'(026). "#EC TEXT_DIFF
  ENDIF.


ENDFORM.                    "write_log

*Selection text
*DFROM:        Date from
*DTO:        Date to
*JOBNAME:D       .
*NEW_REP:D       .
*NEW_TRG:D       .
*NEW_USER:D       .
*NEW_VAR:D       .
*OLD_REP:D       .
*OLD_TRG:D       .
*OLD_USER:D       .
*OLD_VAR:D       .
*OWNER:D       .
*PORTION:D       .
