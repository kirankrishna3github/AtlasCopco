*&---------------------------------------------------------------------*
*& Report  YSE_INV_RECON_REPORT
*&
*&---------------------------------------------------------------------*
*&
*& E-payable Invoice Reconciliation Report
*&
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 26.03.2009
*  Change Request Number : CR0077
*  Transport request Nr. : CD1K947253
*----------------------------------------------------------------------*
*                                                                      *
************************************************************************

*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |01.01.9999|          |CD1K9.....       |(Description)       *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_inv_recon_report.

TABLES: yse_inv_recon.

* ----- ALV GRID ---------------------------------------------------*
TYPE-POOLS: slis.

TYPES: BEGIN OF ty_recon.
        INCLUDE STRUCTURE yse_inv_recon.
TYPES:  cnt   TYPE i,
        selkz TYPE selkz_list,
       END   OF ty_recon.

* Global internal tables
DATA: gt_recon     TYPE STANDARD TABLE OF ty_recon WITH HEADER LINE.

DATA: BEGIN OF gt_cc  OCCURS 0,
        bukrs     TYPE bukrs,
      END OF gt_cc.

* Global variables
DATA: xv_variant         LIKE disvariant,
      xv_variant_flag    TYPE c,
      xv_sd_alv_variant  LIKE disvariant,
      xt_fcat            TYPE slis_t_fieldcat_alv,   " lvc_t_fcat,
      ls_fcat            LIKE LINE OF xt_fcat,
      xt_events          TYPE slis_t_event,
      ls_events          LIKE LINE OF xt_events,
      xt_alv_sort        TYPE slis_t_sortinfo_alv,
      xv_layout          TYPE slis_layout_alv,    "lvc_s_layo.
      xv_grid            TYPE lvc_s_glay,
      gt_seltab          TYPE STANDARD TABLE OF rsparams
                         WITH HEADER LINE.

DATA: gv_answer          TYPE c,
      gv_repid           LIKE sy-repid.

* Global constants
CONSTANTS: gc_charx     TYPE c              VALUE 'X'.

* Selection screen
SELECTION-SCREEN  BEGIN OF BLOCK sel WITH FRAME TITLE text-s01.
SELECT-OPTIONS: so_docnr  FOR yse_inv_recon-docnum,
                so_bukrs  FOR yse_inv_recon-bukrs  OBLIGATORY,
                so_zuonr  FOR yse_inv_recon-zuonr,
                so_blart  FOR yse_inv_recon-blart,
                so_belnr  FOR yse_inv_recon-belnr,
                so_xblnr  FOR yse_inv_recon-xblnr,
                so_bldat  FOR yse_inv_recon-bldat,
                so_crdat  FOR yse_inv_recon-credat,
                so_budat  FOR yse_inv_recon-budat.
SELECTION-SCREEN  END OF BLOCK sel.


*----------------------------------------------------------------------*
* Checks on the selection screen                                       *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

  PERFORM  check_authorization.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*

START-OF-SELECTION.

  PERFORM select_data.
  PERFORM alv_fcat.
  PERFORM alv_layout.
  PERFORM alv_event.
  PERFORM alv_display.


*&---------------------------------------------------------------------*
*&  Forms                                                              *
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  SELECT bukrs FROM t001
               INTO TABLE gt_cc
               WHERE bukrs IN so_bukrs.

  LOOP AT gt_cc.
    AUTHORITY-CHECK OBJECT 'F_SKA1_BUK'
                        ID 'BUKRS' FIELD gt_cc-bukrs
                        ID 'ACTVT' DUMMY.
    IF sy-subrc = 4.
*     No authorisation to display the data
      MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH gt_cc-bukrs.
    ELSEIF sy-subrc <> 0.
*     Error checking authorization.
      MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

  SELECT * FROM yse_inv_recon
           INTO TABLE gt_recon
           WHERE docnum IN so_docnr
             AND bukrs  IN so_bukrs
             AND zuonr  IN so_zuonr
             AND blart  IN so_blart
             AND belnr  IN so_belnr
             AND xblnr  IN so_xblnr
             AND bldat  IN so_bldat
             AND credat IN so_crdat
             AND budat  IN so_budat.

  SORT gt_recon.

  LOOP AT gt_recon.
    gt_recon-cnt = 1.
    MODIFY gt_recon.
  ENDLOOP.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  ALV_FCAT
*&---------------------------------------------------------------------*
*       ALV field catalog
*----------------------------------------------------------------------*
FORM alv_fcat .

  DATA:lv_repid LIKE sy-repid.

  CLEAR: xt_fcat.
  REFRESH: xt_fcat.

  lv_repid = sy-repid.

* Create Fieldcatalogue
  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
    EXPORTING
      i_program_name         = lv_repid
*      i_internal_tabname     = 'GT_RECON'
      i_structure_name       = 'YSE_INV_RECON'
      i_inclname             = lv_repid
    CHANGING
      ct_fieldcat            = xt_fcat[]
    EXCEPTIONS
      inconsistent_interface = 1
      program_error          = 2
      OTHERS                 = 3.

  ls_fcat-fieldname = 'CNT'.
  ls_fcat-col_pos   = 999.
*  ls_fcat-no_out    = gc_charx.
  ls_fcat-do_sum    = gc_charx.
  ls_fcat-emphasize = gc_charx.
  APPEND ls_fcat TO xt_fcat.

ENDFORM.                    " ALV_FCAT

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT
*&---------------------------------------------------------------------*
*       ALV layout
*----------------------------------------------------------------------*
FORM alv_layout .

  CLEAR xv_layout.
  xv_layout-zebra             = gc_charx.
  xv_layout-get_selinfos      = gc_charx.
  xv_layout-colwidth_optimize = gc_charx.
  xv_layout-detail_popup      = gc_charx.
  xv_layout-box_fieldname     = 'SELKZ'.
  xv_layout-no_keyfix         = gc_charx.
  xv_layout-info_fieldname    = 'ZZCOL'.
  xv_layout-totals_text       = 'Number of lines'.
*  xv_layout-smalltitle        = gc_charx.
*  xv_layout-grid_title        = 'title'.

* Define grid settings
  xv_grid-coll_end_l = gc_charx.

ENDFORM.                    " ALV_LAYOUT

*&---------------------------------------------------------------------*
*&      Form  ALV_EVENT
*&---------------------------------------------------------------------*
*       ALV events
*----------------------------------------------------------------------*
FORM alv_event .

  REFRESH xt_events.

  ls_events-name = 'USER_COMMAND'.
  ls_events-form = 'USER_COMMAND_RECON'.
  APPEND ls_events TO xt_events.

  ls_events-name = 'PF_STATUS_SET'.
  ls_events-form = 'PF_STATUS_SET_RECON'.
  APPEND ls_events TO xt_events.

ENDFORM.                    " ALV_EVENT

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       Display ALV
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_repid LIKE sy-repid.

*  xv_sd_alv_variant = xv_variant.
  lv_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = lv_repid
*      i_structure_name        = 'YSE_INV_RECON'
      i_grid_title            = 'E-payable Invoice Reconciliation Report'
      i_grid_settings         = xv_grid
      is_layout               = xv_layout
      it_fieldcat             = xt_fcat
      it_events               = xt_events
*      it_sort                 = xt_alv_sort
*      i_default               = 'X'
      i_save                  = 'A'
*      is_variant              = xv_sd_alv_variant
      i_screen_start_column   = 0
      i_screen_start_line     = 0
      i_screen_end_column     = 0
      i_screen_end_line       = 0
    TABLES
      t_outtab                = gt_recon
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.
  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  user_command_recon
*&---------------------------------------------------------------------*
*       Process user commands
*----------------------------------------------------------------------*
*      -->R_UCOMM     : user command
*      -->RS_SELFIELD : selected field
*----------------------------------------------------------------------*
FORM user_command_recon USING r_ucomm      LIKE sy-ucomm
                              rs_selfield  TYPE slis_selfield.

  CASE r_ucomm.

* delete entries in table
    WHEN 'DELR'.

      CALL FUNCTION 'POPUP_TO_CONFIRM'
        EXPORTING
          titlebar              = text-p01
          text_question         = text-p02
          icon_button_1         = 'ICON_OKAY'
          icon_button_2         = 'ICON_CANCEL'
          default_button        = '2'
          display_cancel_button = space
        IMPORTING
          answer                = gv_answer
        EXCEPTIONS
          text_not_found        = 1
          OTHERS                = 2.
      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      ENDIF.

      IF gv_answer = '1'.
        LOOP AT gt_recon WHERE selkz EQ gc_charx.
          DELETE FROM yse_inv_recon WHERE docnum = gt_recon-docnum.
          CLEAR gt_recon-selkz.
          MODIFY gt_recon TRANSPORTING selkz.
        ENDLOOP.
        COMMIT WORK.
      ENDIF.

      PERFORM restart.

  ENDCASE.

ENDFORM.                    "user_command_recon

*&---------------------------------------------------------------------*
*&      Form  pf_status_set_recon
*&---------------------------------------------------------------------*
*       Set PF-status
*----------------------------------------------------------------------*
*      -->RT_EXTAB : Exclude table
*----------------------------------------------------------------------*
FORM pf_status_set_recon  USING rt_extab TYPE slis_t_extab.

  SET PF-STATUS 'YSE_RECON'.

ENDFORM.                    "pf_status_set_recon

*&---------------------------------------------------------------------*
*&      Form  RESTART
*&---------------------------------------------------------------------*
*       Restart program
*----------------------------------------------------------------------*
FORM restart .

  gv_repid = sy-repid.

* Get report selection-screen selections:
  CALL FUNCTION 'RS_REFRESH_FROM_SELECTOPTIONS'
    EXPORTING
      curr_report     = gv_repid
*    IMPORTING
*      SP              =
    TABLES
      selection_table = gt_seltab
    EXCEPTIONS
      not_found       = 1
      no_report       = 2
      OTHERS          = 3.
  IF sy-subrc <> 0.
*   MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*           WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

* Restart
  SUBMIT (gv_repid) WITH SELECTION-TABLE gt_seltab.

ENDFORM.                    " RESTART

*Text symbol text£º
*P01:Delete E-payable Invoice Reconciliation
*P02:Delete data for E-payable Invoice Reconciliation Report ?

*S01:Selection
*Selection text£º
*SO_BELNR:D       .
*SO_BLART:D       .
*SO_BLDAT:D       .
*SO_BUDAT:D       .
*SO_BUKRS:D       .
*SO_CRDAT:D       .
*SO_DOCNR:D       .
*SO_XBLNR:D       .
*SO_ZUONR:D       .
