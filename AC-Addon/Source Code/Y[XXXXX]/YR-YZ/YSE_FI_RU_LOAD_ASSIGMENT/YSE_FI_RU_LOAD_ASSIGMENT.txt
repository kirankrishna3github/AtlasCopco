************************************************************************
* Program ID     : YSE_FI_RU_LOAD_ASSIGMENT                            *
* Program title  : Load assignments for FI documents                   *
* Author         : Jules Smets                                         *
* Date           : 12/07/2011                                          *
* Change request : CR1801                                              *
* Transport      : CD1K966470                                          *
************************************************************************
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.| DATE       | NAME              | CORRECT. NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | xx/xx/20xx | x                 | CD1K9xxxxx  | CRxxxx      *
************************************************************************

REPORT  yse_fi_ru_load_assigment.

TABLES : bkpf,
         bseg,
         vbrp,
         vbak.

TYPES: BEGIN OF ty_bkpf,
         bukrs   TYPE bukrs,
         belnr   TYPE belnr_d,
         gjahr   TYPE gjahr,
       END OF ty_bkpf.

TYPES: BEGIN OF ty_bseg,
         bukrs   TYPE bukrs,
         belnr   TYPE belnr_d,
         gjahr   TYPE gjahr,
         buzei   TYPE buzei,
         vbeln   TYPE vbeln_vf,
         zuonr   TYPE dzuonr,
      END OF ty_bseg.

TYPES: BEGIN OF ty_ref,
         vbeln   TYPE vbeln_vf,
         xblnr   TYPE xblnr,
      END OF ty_ref.

DATA: gt_bkpf   TYPE TABLE OF ty_bkpf.

DATA: gt_bseg   TYPE TABLE OF ty_bseg
                     WITH HEADER LINE.

DATA: gt_refi   TYPE TABLE OF ty_ref,
      gt_ref    TYPE HASHED TABLE OF ty_ref
                     WITH UNIQUE KEY vbeln
                     WITH HEADER LINE.

DATA: i_bdcdata      LIKE bdcdata     OCCURS 0
                          WITH HEADER LINE,
      struct_bdcdata TYPE bdcdata,
      gt_err         LIKE bdcmsgcoll  OCCURS 0
                          WITH HEADER LINE.

DATA: gv_zuonr       TYPE dzuonr,
      gv_idx(02)     TYPE n,
      gv_scr_fld(20) TYPE c,
      gv_mode        TYPE c       VALUE 'N',
      gv_first(1)    TYPE c,
      gv_msgtxt      TYPE natxt,
      gv_doc_upd     TYPE i.

CONSTANTS: gc_d(1)   TYPE c       VALUE 'D'.

* Selection
SELECTION-SCREEN: BEGIN OF BLOCK block1  WITH FRAME  TITLE text-s01.
PARAMETERS: p_blart  TYPE blart  OBLIGATORY  DEFAULT 'RV'
                                 MODIF ID nup,
            p_bukrs  TYPE bukrs  OBLIGATORY  DEFAULT 'MRUA'
                                 MODIF ID nup.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_belnr FOR bkpf-belnr,
                s_gjahr FOR bkpf-gjahr.
SELECTION-SCREEN: END OF BLOCK block1.


*----------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-group1 = 'NUP'.
      screen-input = 0.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.


*----------------------------------------------------------------------*
*       MAIN PROGRAM
*----------------------------------------------------------------------*
START-OF-SELECTION.

* Select data
  PERFORM select_data.

* Process data
  PERFORM process_data.


*----------------------------------------------------------------------*
END-OF-SELECTION.

  ULINE.
  WRITE: /01 gv_doc_upd, 'documents updated'(001).
  SKIP.
  WRITE: /01 'Job finished'(002).
  ULINE.


*----------------------------------------------------------------------*
*       FORMS
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Select documents to be processed
  SELECT bukrs belnr gjahr
         INTO TABLE gt_bkpf
         FROM bkpf
         WHERE bukrs =  p_bukrs
           AND belnr IN s_belnr
           AND gjahr IN s_gjahr
           AND blart =  p_blart.

* Get corresponding item data
  SELECT bukrs belnr gjahr buzei vbeln zuonr
         INTO TABLE gt_bseg
         FROM bseg
         FOR ALL ENTRIES IN gt_bkpf
         WHERE bukrs = gt_bkpf-bukrs
           AND belnr = gt_bkpf-belnr
           AND gjahr = gt_bkpf-gjahr
           AND koart = gc_d.            "customer line

  FREE gt_bkpf.

* Get corresponding references (from Sales Order)
  SELECT b~vbeln s~xblnr
         INTO TABLE gt_refi
         FROM vbrp AS b
         INNER JOIN vbak AS s
                    ON b~aubel = s~vbeln
         FOR ALL ENTRIES IN gt_bseg
         WHERE b~vbeln = gt_bseg-vbeln.

  SORT gt_refi BY vbeln.
  DELETE ADJACENT DUPLICATES FROM gt_refi
         COMPARING vbeln.
  gt_ref[] = gt_refi[].

  FREE gt_refi.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  PROCESS_DATA
*&---------------------------------------------------------------------*
*       Process data
*----------------------------------------------------------------------*
FORM process_data .

  LOOP AT gt_bseg.

*   Get reference (for assignment)
    CLEAR gv_zuonr.
    READ TABLE gt_ref
         WITH TABLE KEY vbeln = gt_bseg-vbeln.
*   Reference found
    IF sy-subrc = 0.
*     Reference filled
      IF NOT gt_ref-xblnr IS INITIAL.
*       New assignment ('Q' + reference)
        CONCATENATE 'Q' gt_ref-xblnr INTO gv_zuonr.
      ENDIF.
    ENDIF.

*   New assignment found
    IF NOT gv_zuonr IS INITIAL.
*     Assignment changed
      IF gv_zuonr NE gt_bseg-zuonr.
*       Call transaction FB02
        PERFORM call_fb02.
      ENDIF.
    ELSE.
      WRITE: /01 'No assignment found for document :'(e01),
                 gt_bseg-bukrs,
                 gt_bseg-belnr,
                 gt_bseg-gjahr.
      SKIP.
    ENDIF.

  ENDLOOP.

ENDFORM.                    " PROCESS_DATA

*&---------------------------------------------------------------------*
*&      Form  CALL_FB02
*&---------------------------------------------------------------------*
*       Call transaction FB02
*----------------------------------------------------------------------*
FORM call_fb02 .

  REFRESH: i_bdcdata,
           gt_err.

* Select document
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    'SAPMF05L'  '0100'  'X'  ''   ''
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'RF05L-BELNR'  gt_bseg-belnr
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'RF05L-BUKRS'  gt_bseg-bukrs
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'RF05L-GJAHR'  gt_bseg-gjahr
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_OKCODE'  '/00'
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    'SAPMF05L'  '0700'  'X'  ''   ''
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

* Select item (customer line)
  CLEAR gv_scr_fld.
  gv_idx = gt_bseg-buzei.
  CONCATENATE 'RF05L-ANZDT(' gv_idx ')' INTO gv_scr_fld.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_CURSOR' gv_scr_fld
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_OKCODE'  '=PK'
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

* Fill assignment & Save
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    'SAPMF05L'  '0301'  'X'  ''   ''
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BSEG-ZUONR' gv_zuonr
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_OKCODE'  '=AE'
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

* Update document
  CALL TRANSACTION 'FB02' USING i_bdcdata
                          UPDATE 'S'
                          MODE gv_mode
                          MESSAGES INTO gt_err.

* Display errors
  PERFORM display_errors.

ENDFORM.                    " CALL_FB02

*&---------------------------------------------------------------------*
*&      Form  DISPLAY_ERRORS
*&---------------------------------------------------------------------*
*       Display errors
*       (If any, else count updates)
*----------------------------------------------------------------------*
FORM display_errors .

  CLEAR gv_first.

  LOOP AT gt_err WHERE msgtyp = 'A'
                    OR msgtyp = 'E'.

*   Display key
    IF gv_first IS INITIAL.
      WRITE: /01 'Transaction FB02 failed for document :'(e02),
                 gt_bseg-bukrs,
                 gt_bseg-belnr,
                 gt_bseg-gjahr.
      gv_first = 'X'.
    ENDIF.

*   Build & Display error
    MESSAGE ID gt_err-msgid  TYPE gt_err-msgtyp  NUMBER gt_err-msgnr
            WITH gt_err-msgv1 gt_err-msgv2 gt_err-msgv3 gt_err-msgv4
            INTO gv_msgtxt.
    WRITE: /05 gv_msgtxt.

  ENDLOOP.

  IF sy-subrc = 0.
    SKIP.
  ELSE.
*   No error, Count updates
    gv_doc_upd = gv_doc_upd + 1.
  ENDIF.

ENDFORM.                    " DISPLAY_ERRORS

*Text symbol text£º
*001:documents updated
*002:Job finished
*E01:No assignment found for document :
*E02:Transaction FB02 failed for document :

*S01:Selection
*Selection text£º
*P_BLART:D       .
*P_BUKRS:D       .
*S_BELNR:D       .
*S_GJAHR:D       .
