*&---------------------------------------------------------------------*
*& Report  YSE_SALESORDER_EXTRACTION
*&
*&---------------------------------------------------------------------*
*& Author: Luc Mertens
*& Date  : 22.09.2015
*& Devt  : CR5374
*&---------------------------------------------------------------------*
REPORT  yse_SALESORDER_extraction.

************************************************************************
*                   T A B L E S                                        *
************************************************************************
TABLES: vbak, vbap, vbkd, vbpa, adrc, adrct, vbep, vbuk, vbfa.

************************************************************************
*                  I N T E R N A L   T A B L E S                       *
************************************************************************
DATA: gt_yse_em_plant    TYPE TABLE OF yse_em_plant,
      gw_em_pl           LIKE LINE OF gt_yse_em_plant,
      gt_yse_atp_itemcat TYPE TABLE OF yse_atp_itemcat,
      gw_atp             LIKE LINE OF gt_yse_atp_itemcat.


DATA: BEGIN OF gt_sales OCCURS 0,
          hord       TYPE vbak-vbeln,
          lline      TYPE vbap-posnr,
          hcurr      TYPE vbak-waerk,
          hrout      TYPE vbap-route,
          hcpo       TYPE vbkd-bstkd,
          hprf       TYPE vbap-prctr,
          hterm      TYPE vbkd-zterm,
          ldesc      TYPE vbap-arktx,
          lnet       TYPE vbap-netpr,
          lprod(18)  TYPE c,
          hxnodc     TYPE vbak-kvgr4,
          lqord      TYPE vbap-kwmeng,
          hxwebn     TYPE vbkd-ihrez,
          hsrce      TYPE vbak-vkgrp,
          hsrce2     TYPE vbak-vkbur,
          lum        TYPE vbap-vrkme,
          hxortc     TYPE vbak-vsbed,
          lwhs       TYPE vbap-werks,
          lloc       TYPE vbap-lgort,
          seed_spart TYPE vbak-spart,
          seed_itemcat TYPE vbap-pstyv,
          seed_auart TYPE vbak-auart,
          seed_bstkd_e TYPE vbkd-bstkd_e,
          knumv      TYPE vbak-knumv,
          vkorg      TYPE vbak-vkorg,
      END OF gt_sales.

DATA: BEGIN OF gt_sales_h OCCURS 0,
          hord       TYPE vbak-vbeln,
          lline      TYPE vbap-posnr,
          hcurr      TYPE vbak-waerk,
          hrout      TYPE vbap-route,
          hcpo       TYPE vbkd-bstkd,
          hprf       TYPE vbap-prctr,
          hterm      TYPE vbkd-zterm,
          ldesc      TYPE vbap-arktx,
          lnet       TYPE vbap-netpr,
          lprod(18)  TYPE c,
          hxnodc     TYPE vbak-kvgr4,
          lqord      TYPE vbap-kwmeng,
          hxwebn     TYPE vbkd-ihrez,
          hsrce      TYPE vbak-vkgrp,
          hsrce2     TYPE vbak-vkbur,
          lum        TYPE vbap-vrkme,
          hxortc     TYPE vbak-vsbed,
          lwhs       TYPE vbap-werks,
          lloc       TYPE vbap-lgort,
          seed_spart TYPE vbak-spart,
          seed_itemcat TYPE vbap-pstyv,
          seed_auart TYPE vbak-auart,
          seed_bstkd_e TYPE vbkd-bstkd_e,
*
          seed_scenario(4) TYPE c,
          famcd(3)   TYPE c,
          hxrelf(1)  TYPE c,
          hstat(1)   TYPE c,
          x_fixprice TYPE konv-kbetr,
          hxfxpf,
          lqall      TYPE mska-kalab,
          had3       TYPE adrc-city1,
          hcust      TYPE vbpa-kunnr,
          hpost      TYPE adrc-post_code1,
          lsal       TYPE vbpa-pernr,
          hsal       TYPE vbpa-pernr,
          hsal2      TYPE vbpa-pernr,
          hship      TYPE vbpa-kunnr,
          hcoun      TYPE adrc-country,
          lqshp      TYPE vbfa-rfmng,
          lrdte      TYPE vbep-edatu,
          had1       TYPE adrc-street,
          had2       TYPE adrc-str_suppl1,
          hname      TYPE adrc-name1,
          lxs1ap     TYPE adrct-remark,
          lxs2ap     TYPE adrct-remark,
          lxs3ap     TYPE adrct-remark,
       END OF gt_sales_h.

TYPES: BEGIN OF ty_sales_pa,
         vbeln  TYPE  vbpa-vbeln,
         posnr  TYPE  vbpa-posnr,
         parvw  TYPE  vbpa-parvw,
         kunnr  TYPE  vbpa-kunnr,
         pernr  TYPE  vbpa-pernr,
         adrnr  TYPE  vbpa-adrnr,
       END OF ty_sales_pa,

       BEGIN OF ty_addr,
         addrnumber  TYPE adrc-addrnumber,
         name1       TYPE adrc-name1,
         post_code1  TYPE adrc-post_code1,
         street      TYPE adrc-street,
         str_suppl1  TYPE adrc-str_suppl1,
         city1       TYPE adrc-city1,
         country     TYPE adrc-country,
       END OF ty_addr,

       BEGIN OF ty_adrct,
         addrnumber  TYPE adrct-addrnumber,
         remark      TYPE adrct-remark,
       END OF ty_adrct,

       BEGIN OF ty_vbfa,
         vbelv       TYPE vbfa-vbelv,
         posnv       TYPE vbfa-posnv,
         vbeln       TYPE vbfa-vbeln,
         posnn       TYPE vbfa-posnn,
         rfmng       TYPE vbfa-rfmng,
       END OF ty_vbfa,

       BEGIN OF ty_vbep,
         vbeln       TYPE vbep-vbeln,
         posnr       TYPE vbep-posnr,
         etenr       TYPE vbep-etenr,
         edatu       TYPE vbep-edatu,
       END OF ty_vbep,

       BEGIN OF ty_mska,
         matnr       TYPE mska-matnr,
         werks       TYPE mska-werks,
         lgort       TYPE mska-lgort,
         vbeln       TYPE mska-vbeln,
         posnr       TYPE mska-posnr,
         kalab       TYPE mska-kalab,
       END OF ty_mska,

       BEGIN OF ty_sales_n,
          vbeln	    TYPE vbeln,
          tdid      TYPE tdid,
          snseq(4)  TYPE n,
          snprt(4)  TYPE c,
          snpic(4)  TYPE c,
          sninv(4)  TYPE c,
          tdobject  TYPE tdobject,
          tdline    TYPE tdline,
          fam_code(3) TYPE c,
       END OF ty_sales_n,

       BEGIN OF ty_stxh,
          tdobject    TYPE tdobject,
          tdname      TYPE tdobname,
          tdid        TYPE tdid,
          tdspras     TYPE spras,
       END OF ty_stxh.

DATA:  BEGIN OF gt_sales_n1_out OCCURS 0,
          hord      TYPE vbeln,
          tab01(1)  TYPE c,
          snseq(5)  TYPE c,
          tab02(1)  TYPE c,
          sndesc    TYPE tdline,
          tab03(1)  TYPE c,
          snprt(5)  TYPE c,
          tab04(1)  TYPE c,
          snpic(5)  TYPE c,
          tab05(1)  TYPE c,
          sninv(5)  TYPE c,
          tab06(1)  TYPE c,
          snhealin(10) TYPE c,
          tab07(1)  TYPE c,
          fam_code(03) TYPE c,
       END OF gt_sales_n1_out.

DATA: gt_sales2   like TABLE OF gt_sales,
      gw_sales2   LIKE LINE OF gt_sales,
      gt_sales_pa TYPE STANDARD TABLE OF ty_sales_pa,
      gw_sales_pa LIKE LINE OF gt_sales_pa,
      gt_addr     TYPE STANDARD TABLE OF ty_addr,
      gw_addr     LIKE LINE OF gt_addr,
      gt_adrct    TYPE STANDARD TABLE OF ty_adrct,
      gw_adrct    LIKE LINE OF gt_adrct,
      gt_vbfa     TYPE STANDARD TABLE OF ty_vbfa,
      gw_vbfa     LIKE LINE OF gt_vbfa,
      gt_vbep     TYPE STANDARD TABLE OF ty_vbep,
      gw_vbep     LIKE LINE OF gt_vbep,
      gt_mska     TYPE STANDARD TABLE OF ty_mska,
      gw_mska     LIKE LINE OF gt_mska,
      gw_sales    LIKE LINE OF gt_sales,
      gw_sales_h  LIKE LINE OF gt_sales_h,
      gw_sales_n1_out LIKE LINE OF gt_sales_n1_out,
      gt_sales_n1 TYPE STANDARD TABLE OF ty_sales_n,
      gw_sales_n1 LIKE LINE OF gt_sales_n1.

DATA: BEGIN OF gt_sales_h_out OCCURS 0,
          hord       TYPE vbak-vbeln,
          tab01(1)   TYPE c,
          lline      TYPE vbap-posnr,
          tab02(1)   TYPE c,
          hcurr(5)   TYPE c,
          tab03(1)   TYPE c,
          hrout      TYPE vbap-route,
          tab04(1)   TYPE c,
          hcpo       TYPE vbkd-bstkd,
          tab05(1)   TYPE c,
          hprf       TYPE vbap-prctr,
          tab06(1)   TYPE c,
          hterm(5)   TYPE c,
          tab07(1)   TYPE c,
          ldesc      TYPE vbap-arktx,
          tab08(1)   TYPE c,
          lnet(11)   TYPE c,
          tab09(1)   TYPE c,
          lprod(18)  TYPE c,
          tab10(1)   TYPE c,
          hxnodc(6)  TYPE c,
          tab11(1)   TYPE c,
          lqord(15)  TYPE c,
          tab12(1)   TYPE c,
          hxwebn     TYPE vbkd-ihrez,
          tab13(1)   TYPE c,
          hsrce(8)   TYPE c,
          tab14(1)   TYPE c,
          lum(3)     TYPE c,
          tab15(1)   TYPE c,
          hxortc(6)  TYPE c,
          tab16(1)   TYPE c,
          lwhs       TYPE vbap-werks,
          tab17(1)   TYPE c,
          lloc       TYPE vbap-lgort,
          tab18(1)   TYPE c,
          seed_spart(10)   TYPE c,
          tab19(1)   TYPE c,
          seed_itemcat(12) TYPE c,
          tab20(1)   TYPE c,
          seed_auart(10)   TYPE c,
          tab21(1)   TYPE c,
*
          seed_scenario(13) TYPE c,
          tab22(1)   TYPE c,
          famcd(8)   TYPE c,
          tab23(1)   TYPE c,
          hxrelf(6)  TYPE c,
          tab24(1)   TYPE c,
          hstat(5)   TYPE c,
          tab25(1)   TYPE c,
          x_fixprice(11) TYPE c,
          tab26(1)   TYPE c,
          hxfxpf(6)  TYPE c,
          tab27(1)   TYPE c,
          lqall(15)  TYPE c,
          tab28(1)   TYPE c,
          had3       TYPE adrc-city1,
          tab29(1)   TYPE c,
          hcust      TYPE vbpa-kunnr,
          tab30(1)   TYPE c,
          hpost      TYPE adrc-post_code1,
          tab31(1)   TYPE c,
          lsal       TYPE vbpa-pernr,
          tab32(1)   TYPE c,
          hsal       TYPE vbpa-pernr,
          tab33(1)   TYPE c,
          hsal2      TYPE vbpa-pernr,
          tab34(1)   TYPE c,
          hship      TYPE vbpa-kunnr,
          tab35(1)   TYPE c,
          hcoun(05)  TYPE c,
          tab36(1)   TYPE c,
          lqshp(15)  TYPE c,
          tab37(1)   TYPE c,
          lrdte(10)  TYPE c,
          tab38(1)   TYPE c,
          had1       TYPE adrc-street,
          tab39(1)   TYPE c,
          had2       TYPE adrc-str_suppl1,
          tab40(1)   TYPE c,
          hname      TYPE adrc-name1,
          tab41(1)   TYPE c,
          lxs1ap(6)  TYPE c,
          tab42(1)   TYPE c,
          lxs2ap(6)  TYPE c,
          tab43(1)   TYPE c,
          lxs3ap(6)  TYPE c,
          tab44(1)   TYPE c,
          seed_bstkd_e TYPE vbkd-bstkd_e,
       END OF gt_sales_h_out.

DATA: BEGIN OF gs_head,
          hord       TYPE vbak-vbeln VALUE 'hord',
          tab01(1)    TYPE c   VALUE '|',
          lline(6)   TYPE c VALUE 'lline',
          tab02(1)    TYPE c   VALUE '|',
          hcurr(5)   TYPE c VALUE 'hcurr',
          tab03(1)    TYPE c   VALUE '|',
          hrout      TYPE vbap-route VALUE 'hrout',
          tab04(1)    TYPE c   VALUE '|',
          hcpo       TYPE vbkd-bstkd VALUE 'hcpo',
          tab05(1)    TYPE c   VALUE '|',
          hprf       TYPE vbap-prctr VALUE 'hprf',
          tab06(1)    TYPE c   VALUE '|',
          hterm(5)   TYPE c VALUE 'hterm',
          tab07(1)    TYPE c   VALUE '|',
          ldesc      TYPE vbap-arktx VALUE 'ldesc',
          tab08(1)    TYPE c   VALUE '|',
          lnet(11)   TYPE c VALUE 'lnet',
          tab09(1)    TYPE c   VALUE '|',
          lprod      TYPE vbap-matnr VALUE 'lprod',
          tab10(1)    TYPE c   VALUE '|',
          hxnodc(6)  TYPE c VALUE 'hxnodc',
          tab11(1)    TYPE c   VALUE '|',
          lqord(15)  TYPE c VALUE 'lqord',
          tab12(1)    TYPE c   VALUE '|',
          hxwebn     TYPE vbkd-ihrez VALUE 'hxwebn',
          tab13(1)    TYPE c   VALUE '|',
          hsrce(8)   TYPE c VALUE 'hsrce',
          tab14(1)    TYPE c   VALUE '|',
          lum(3)     TYPE c VALUE 'lum',
          tab15(1)    TYPE c   VALUE '|',
          hxortc(6)  TYPE c VALUE 'hxortc',
          tab16(1)    TYPE c   VALUE '|',
          lwhs       TYPE vbap-werks VALUE 'lwhs',
          tab17(1)    TYPE c   VALUE '|',
          lloc       TYPE vbap-lgort VALUE 'lloc',
          tab18(1)    TYPE c   VALUE '|',
          seed_spart(10)   TYPE c VALUE 'seed_spart',
          tab19(1)    TYPE c   VALUE '|',
          seed_itemcat(12) TYPE c VALUE 'seed_itemcat',
          tab20(1)    TYPE c   VALUE '|',
          seed_auart(10)   TYPE c VALUE 'seed_auart',
          tab21(1)    TYPE c   VALUE '|',
*
          seed_scenario(13) TYPE c VALUE 'seed_scenario',
          tab22(1)    TYPE c   VALUE '|',
          famcd(8)   TYPE c VALUE 'fam_code',
          tab23(1)    TYPE c   VALUE '|',
          hxrelf(6)  TYPE c VALUE 'hxrelf',
          tab24(1)    TYPE c   VALUE '|',
          hstat(5)   TYPE c VALUE 'hstat',
          tab25(1)    TYPE c   VALUE '|',
          x_fixprice(11) TYPE c VALUE 'x_fixprice',
          tab26(1)    TYPE c   VALUE '|',
          hxfxpf(6)  TYPE c VALUE 'hxfxpf',
          tab27(1)    TYPE c   VALUE '|',
          lqall(15)  TYPE c VALUE 'lqall',
          tab28(1)    TYPE c   VALUE '|',
          had3       TYPE adrc-city1 VALUE 'had3',
          tab29(1)    TYPE c   VALUE '|',
          hcust      TYPE vbpa-kunnr VALUE 'hcust',
          tab30(1)    TYPE c   VALUE '|',
          hpost      TYPE adrc-post_code1 VALUE 'hpost',
          tab31(1)    TYPE c   VALUE '|',
          lsal(8)    TYPE c VALUE 'lsal',
          tab32(1)    TYPE c   VALUE '|',
          hsal(8)    TYPE c VALUE 'hsal',
          tab33(1)    TYPE c   VALUE '|',
          hsal2(8)   TYPE c VALUE 'hsal2',
          tab34(1)    TYPE c   VALUE '|',
          hship      TYPE vbpa-kunnr VALUE 'hship',
          tab35(1)    TYPE c   VALUE '|',
          hcoun(5)   TYPE c VALUE 'hcoun',
          tab36(1)    TYPE c   VALUE '|',
          lqshp(15)  TYPE c VALUE 'lqshp',
          tab37(1)    TYPE c   VALUE '|',
          lrdte(10)  TYPE c VALUE 'lrdte',
          tab38(1)    TYPE c   VALUE '|',
          had1       TYPE adrc-street VALUE 'had1',
          tab39(1)    TYPE c   VALUE '|',
          had2       TYPE adrc-str_suppl1 VALUE 'had2',
          tab40(1)    TYPE c   VALUE '|',
          hname      TYPE adrc-name1 VALUE 'hname',
          tab41(1)    TYPE c   VALUE '|',
          lxs1ap(6)  TYPE c VALUE 'lxs1ap',
          tab42(1)    TYPE c   VALUE '|',
          lxs2ap(6)  TYPE c VALUE 'lxs2ap',
          tab43(1)    TYPE c   VALUE '|',
          lxs3ap(6)  TYPE c VALUE 'lxs3ap',
          tab44(1)    TYPE c   VALUE '|',
          seed_bstkd_e TYPE vbkd-bstkd_e VALUE 'seed_bstkd_e',
       END OF gs_head.

DATA: gt_stxh  TYPE SORTED TABLE OF ty_stxh
                    WITH NON-UNIQUE KEY tdname
                    WITH HEADER LINE.

DATA: BEGIN OF gs_head_text,
          hord       TYPE vbak-vbeln VALUE 'hord',
          tab01(1)   TYPE c   VALUE '|',
          snseq(5)   TYPE c VALUE 'snseq',
          tab02(1)   TYPE c   VALUE '|',
          sndesc(132) TYPE c VALUE 'sndesc',
          tab03(1)   TYPE c   VALUE '|',
          snprt(5)   TYPE c VALUE 'snprt',
          tab04(1)   TYPE c   VALUE '|',
          snpic(5)   TYPE c VALUE 'snpic',
          tab05(1)   TYPE c   VALUE '|',
          sninv(5)   TYPE c VALUE 'sninv',
          tab06(1)   TYPE c   VALUE '|',
          snhealin(10) TYPE c VALUE 'snhealin',
          tab07(1)   TYPE c   VALUE '|',
          fam_code(08) TYPE c VALUE 'fam_code',
       END OF gs_head_text.

DATA: BEGIN OF i_lines OCCURS 0.
        INCLUDE STRUCTURE tline.
DATA: END OF i_lines.

************************************************************************
*                   C O N S T A N T S                                  *
************************************************************************
CONSTANTS : c_filen(8)    TYPE c          VALUE 'SALESORD',
            c_filet(8)    TYPE c          VALUE 'SALESTXT',
            c_underscore  TYPE c          VALUE '_',
            c_000000      TYPE vbpa-posnr VALUE '000000',
            c_ag(2)       TYPE c          VALUE 'AG',
            c_ve(2)       TYPE c          VALUE 'VE',
            c_zx(2)       TYPE c          VALUE 'ZX',
            c_zy(2)       TYPE c          VALUE 'ZY',
            c_we(2)       TYPE c          VALUE 'WE'.

************************************************************************
*                   V A R I A B L E S                                  *
************************************************************************
DATA: g_ofile         LIKE /sapdmc/lsoinp-filename,
      g_ofile2        LIKE /sapdmc/lsoinp-filename,
      g_directory(25) TYPE c VALUE '/var/load/xxx/UK/convert/'.

DATA: p_logsys        LIKE tbdlst-logsys,
      g_ersda(12)     type c.                      " YYYYMMDDHHMM.

DATA: g_sh_adrnr      TYPE adrc-addrnumber,
      g_pe_adrnr      TYPE adrc-addrnumber,
      g_zx_adrnr      TYPE adrc-addrnumber,
      g_zy_adrnr      TYPE adrc-addrnumber,
      g_hstat(1)      TYPE c,
      g_name(70)      TYPE c,
      g_bismt         TYPE bismt,
      g_vkorg         TYPE vbak-vkorg,
      g_zm00          TYPE konv-kbetr.


************************************************************************
*                   R A N G E S                                        *
************************************************************************
RANGES : r_tdname FOR stxh-tdname,
         r_tdid   FOR ttxid-tdid.

************************************************************************
*       S E L E C T - O P T I O N S / P A R A M E T E R S              *
************************************************************************
SELECTION-SCREEN: BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.
SELECT-OPTIONS: s_vkorg  FOR  vbak-vkorg OBLIGATORY.
SELECT-OPTIONS: s_vbeln  FOR  vbak-vbeln.
SELECT-OPTIONS: s_posnr  FOR  vbap-posnr.
SELECT-OPTIONS: s_auart  FOR  vbak-auart.
SELECTION-SCREEN: END OF BLOCK b1.

************************************************************************
INITIALIZATION.

  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = p_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.

  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

  PERFORM select_data.

  IF NOT gt_sales[] IS INITIAL.
    PERFORM data_manipulation.

*   write outputfile to application server
    CONCATENATE sy-datum sy-uzeit(4) INTO g_ersda.
    CONCATENATE c_filen g_ersda
              INTO g_ofile SEPARATED BY c_underscore.
    REPLACE 'xxx' IN g_directory WITH p_logsys(3).
    CONCATENATE g_directory g_ofile INTO g_ofile.

    PERFORM write_outputfile.

*   write textfile to application server
    CONCATENATE sy-datum sy-uzeit(4) INTO g_ersda.
    CONCATENATE c_filet g_ersda
              INTO g_ofile2 SEPARATED BY c_underscore.
    REPLACE 'xxx' IN g_directory WITH p_logsys(3).
    CONCATENATE g_directory g_ofile2 INTO g_ofile2.

    PERFORM write_textfile.
  ENDIF.

************************************************************************
*       E N D - O F - S E L E C T I O N    E V E N T                   *
************************************************************************
END-OF-SELECTION .

  WRITE: / 'Job finished'(i02).


*--- S U B R O U T I N E S --------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
FORM select_data.

* ATP Item Category Configuration
  SELECT * INTO TABLE gt_YSE_ATP_ITEMCAT FROM YSE_ATP_ITEMCAT
    ORDER BY pstyv_atp.

* EM: Plant parameters
  SELECT * INTO TABLE gt_YSE_EM_PLANT FROM YSE_EM_PLANT
    ORDER BY werks.

* Select Sales Order Lines (open)
  CLEAR gt_sales.
  REFRESH gt_sales.

  SELECT a~vbeln b~posnr a~waerk b~route c~bstkd
         b~prctr c~zterm b~arktx b~netpr b~matnr
         a~kvgr4 b~kwmeng c~ihrez a~vkgrp a~vkbur b~vrkme a~vsbed
         b~werks b~lgort a~spart b~pstyv a~auart c~bstkd_e a~knumv a~vkorg
    INTO TABLE gt_sales
    FROM vbak AS a
    INNER JOIN vbap AS b
     ON a~vbeln = b~vbeln
    INNER JOIN vbkd AS c
     ON a~vbeln = c~vbeln
    WHERE a~vbeln IN s_vbeln AND
          a~vkorg IN s_vkorg AND
          a~auart IN s_auart AND
          b~posnr IN s_posnr AND
          b~abgru EQ '  '    AND
          c~posnr EQ c_000000.

* Process Error - No data found
  IF sy-subrc NE 0.
    WRITE: text-e01.
    EXIT.
  ENDIF.

ENDFORM.                    "SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  DATA_MANIPULATION
*&---------------------------------------------------------------------*
FORM data_manipulation.

* Fetch Header level Partner details
  gt_sales2[] = gt_sales[].
  DELETE ADJACENT DUPLICATES FROM gt_sales2 COMPARING hord.

  SELECT vbeln posnr parvw kunnr pernr adrnr
    FROM vbpa INTO TABLE gt_sales_pa
    FOR ALL ENTRIES IN gt_sales2
    WHERE vbeln = gt_sales2-hord AND
          posnr = c_000000 AND
          parvw in ('AG', 'WE', 'VE', 'ZX', 'ZY').

  sort gt_sales_pa by vbeln posnr parvw.

* Fetch Address Data
  SELECT addrnumber name1 post_code1 street
         str_suppl1 str_suppl2 country
    FROM adrc INTO TABLE gt_addr
    FOR ALL ENTRIES IN gt_sales_pa
    WHERE addrnumber EQ gt_sales_pa-adrnr.

  sort gt_addr by addrnumber.

* Fetch Address Notes
  SELECT addrnumber remark
    FROM adrct INTO TABLE gt_adrct
    FOR ALL ENTRIES IN gt_addr
    WHERE addrnumber EQ gt_addr-addrnumber.

  sort gt_adrct by addrnumber.

* Fetch Referenced Quantity
  SELECT vbelv posnv vbeln posnn rfmng
    FROM vbfa INTO TABLE gt_vbfa
    FOR ALL ENTRIES IN gt_sales
    WHERE vbelv   = gt_sales-hord
      AND posnv   = gt_sales-lline
      AND vbtyp_n = 'R'
      AND bwart  IN ('261', '601', '   ').

  SORT gt_vbfa by vbelv posnv.

* Fetch Schedule line date
  SELECT vbeln posnr etenr edatu
    FROM vbep INTO TABLE gt_vbep
    FOR ALL ENTRIES IN gt_sales
    WHERE vbeln = gt_sales-hord
      AND posnr = gt_sales-lline
      AND etenr = '0001'.          "first schedule line

  SORT gt_vbep by vbeln posnr.

* Sales Order stock --> allocated qty
  SELECT matnr werks lgort vbeln posnr kalab
    FROM mska INTO TABLE gt_mska
    FOR ALL ENTRIES IN gt_sales
    WHERE matnr = gt_sales-lprod
      AND werks = gt_sales-lwhs
*      AND lgort = gt_sales-lloc
      AND vbeln = gt_sales-hord
      AND posnr = gt_sales-lline.

  SORT gt_mska by vbeln posnr matnr werks lgort.

*
  LOOP AT gt_sales INTO gw_sales.
    CLEAR gw_sales_h.
    MOVE-CORRESPONDING gw_sales to gw_sales_h.

*.. Read partner details
    CLEAR gw_sales_pa.
    READ TABLE gt_sales_pa INTO gw_sales_pa WITH KEY
         vbeln = gw_sales-hord
         parvw = c_ag
         BINARY SEARCH.

    IF sy-subrc = 0.
      gw_sales_h-hcust = gw_sales_pa-kunnr.
    ENDIF.

    CLEAR: gw_sales_pa, g_pe_adrnr.
    READ TABLE gt_sales_pa INTO gw_sales_pa WITH KEY
         vbeln = gw_sales-hord
         parvw = c_ve
         BINARY SEARCH.

    IF sy-subrc = 0.
      gw_sales_h-hsal = gw_sales_pa-pernr.
      g_pe_adrnr = gw_sales_pa-adrnr.
    ENDIF.

    CLEAR: gw_sales_pa, g_zx_adrnr.
    READ TABLE gt_sales_pa INTO gw_sales_pa WITH KEY
         vbeln = gw_sales-hord
         parvw = c_zx
         BINARY SEARCH.

    IF sy-subrc = 0.
      gw_sales_h-hsal2 = gw_sales_pa-pernr.
      g_zx_adrnr = gw_sales_pa-adrnr.
    ENDIF.

    CLEAR: gw_sales_pa, g_zy_adrnr.
    READ TABLE gt_sales_pa INTO gw_sales_pa WITH KEY
         vbeln = gw_sales-hord
         parvw = c_zy
         BINARY SEARCH.

    IF sy-subrc = 0.
      gw_sales_h-lsal = gw_sales_pa-pernr.
      g_zy_adrnr = gw_sales_pa-adrnr.
    ENDIF.

    CLEAR: gw_sales_pa, g_sh_adrnr.
    READ TABLE gt_sales_pa INTO gw_sales_pa WITH KEY
         vbeln = gw_sales-hord
         parvw = c_we
         BINARY SEARCH.

    IF sy-subrc = 0.
      gw_sales_h-hship = gw_sales_pa-kunnr.
      g_sh_adrnr = gw_sales_pa-adrnr.
    ENDIF.

*.. Read address data
    CLEAR gw_addr.
    READ TABLE gt_addr INTO gw_addr WITH KEY
         addrnumber = g_sh_adrnr
      BINARY SEARCH.

    IF sy-subrc = 0.
      gw_sales_h-hname = gw_addr-name1.
      gw_sales_h-had1  = gw_addr-street.
      gw_sales_h-hpost = gw_addr-post_code1.
      gw_sales_h-had2  = gw_addr-str_suppl1.
      gw_sales_h-had3  = gw_addr-city1.
      gw_sales_h-hcoun = gw_addr-country.
    ENDIF.

*.. Read address remark for sales employees 1-3
    CLEAR gw_adrct.
    READ TABLE gt_adrct INTO gw_adrct WITH KEY
         addrnumber = g_pe_adrnr
      BINARY SEARCH.

    IF sy-subrc = 0.
      gw_sales_h-lxs1ap = gw_adrct-remark.
    ENDIF.

    CLEAR gw_adrct.
    READ TABLE gt_adrct INTO gw_adrct WITH KEY
         addrnumber = g_zx_adrnr
      BINARY SEARCH.

    IF sy-subrc = 0.
      gw_sales_h-lxs2ap = gw_adrct-remark.
    ENDIF.

    CLEAR gw_adrct.
    READ TABLE gt_adrct INTO gw_adrct WITH KEY
         addrnumber = g_zy_adrnr
      BINARY SEARCH.

    IF sy-subrc = 0.
      gw_sales_h-lxs3ap = gw_adrct-remark.
    ENDIF.

*.. Read Reference quantity
    CLEAR gw_vbfa.
    READ TABLE gt_vbfa INTO gw_vbfa WITH KEY
         vbelv = gw_sales-hord
         posnv = gw_sales-lline
      BINARY SEARCH.

    if sy-subrc = 0.
      CLEAR gw_vbfa.
      LOOP AT gt_vbfa INTO gw_vbfa FROM sy-tabix.
        if gw_vbfa-vbelv <> gw_sales-hord or
           gw_vbfa-posnv <> gw_sales-lline.
          EXIT.
        endif.
        gw_sales_h-lqshp = gw_sales_h-lqshp + gw_vbfa-rfmng.
      ENDLOOP.
    ENDIF.

*.. Read first schedule line date
    CLEAR gw_vbep.
    READ TABLE gt_vbep INTO gw_vbep WITH KEY
         vbeln = gw_sales-hord
         posnr = gw_sales-lline
      BINARY SEARCH.

    IF sy-subrc = 0.
      gw_sales_h-lrdte = gw_vbep-edatu.
    ENDIF.

*.. Get Overall credit status
    CLEAR g_hstat.
    SELECT SINGLE cmgst INTO g_hstat
      FROM vbuk WHERE vbeln = gw_sales-hord.

    IF sy-subrc = 0 AND g_hstat = 'B'.
      gw_sales_h-hstat = 'H'.
    ENDIF.

*.. Read allocated qty
    CLEAR gw_mska.
    READ TABLE gt_mska INTO gw_mska WITH KEY
         vbeln = gw_sales-hord
         posnr = gw_sales-lline
         matnr = gw_sales-lprod
         werks = gw_sales-lwhs
*         lgort = gw_sales-lloc
      BINARY SEARCH.

    IF sy-subrc = 0.
      gw_sales_h-lqall = gw_mska-kalab.
    ENDIF.

*.. Build scenario
    CLEAR gw_atp.
    READ TABLE gt_yse_atp_itemcat INTO gw_atp WITH KEY
          pstyv_atp = gw_sales-seed_itemcat
        BINARY SEARCH.

    CLEAR gw_em_pl.
    READ TABLE gt_yse_em_plant INTO gw_em_pl WITH KEY
          werks = gw_sales-lwhs
        BINARY SEARCH.

    If gw_atp-dtc IS initial.
      gw_sales_h-seed_scenario = 'SFS'.
    ELSEIF gw_em_pl-whtype = 'D'.
      gw_sales_h-seed_scenario = 'DTC'.
    ELSE.
      gw_sales_h-seed_scenario = 'SDTC'.
    ENDIF.

*.. Individual fields
    gw_sales_h-hxrelf = 'N'.

    CLEAR: g_zm00.
    SELECT SINGLE kbetr INTO g_zm00
      FROM konv
      WHERE knumv = gw_sales-knumv
        AND kposn = c_000000
        AND kschl = 'ZM00'.

    IF sy-subrc = 0.
      gw_sales_h-x_fixprice = g_zm00.
      gw_sales_h-hxfxpf = 'Y'.
    ELSE.
      gw_sales_h-hxfxpf = 'N'.
    ENDIF.

*   FAMCODE
    CASE gw_sales-vkorg.
      WHEN 'PL01'.
        gw_sales_h-famcd = 'PL1'.
      WHEN 'PL02'.
        gw_sales_h-famcd = 'PL2'.
      WHEN 'PL03'.
        gw_sales_h-famcd = 'PLA'.
      WHEN 'PL04'.
        gw_sales_h-famcd = 'PL4'.
    ENDCASE.

*
    APPEND gw_sales_h TO gt_sales_h.
    CLEAR: gw_sales.
  ENDLOOP.


*----------------------------------------------------------------------*
* Sales Order Texts
* Text-id selection
  r_tdid-low    = 'SL01'.
  r_tdid-high   = 'SL04'.
  r_tdid-sign   = 'I'.
  r_tdid-option = 'BT'.
  APPEND r_tdid.

  r_tdid-low    = 'SL06'.
  r_tdid-high   = 'SL09'.
  r_tdid-sign   = 'I'.
  r_tdid-option = 'BT'.
  APPEND r_tdid.

  r_tdid-low    = '0001'.
  r_tdid-high   = '0003'.
  r_tdid-sign   = 'I'.
  r_tdid-option = 'BT'.
  APPEND r_tdid.

  REFRESH gt_stxh.
  LOOP at gt_sales2 INTO gw_sales2.
    g_name = gw_sales2-hord.
    SELECT tdobject tdname tdid tdspras FROM stxh APPENDING TABLE gt_stxh
      WHERE tdobject EQ 'VBBK' AND
            tdname   EQ g_name AND
            tdid     IN r_tdid AND
            tdspras  EQ sy-langu.

    CONCATENATE gw_sales2-hord gw_sales2-lline INTO g_name.
    SELECT tdobject tdname tdid tdspras FROM stxh APPENDING TABLE gt_stxh
        WHERE tdobject EQ 'VBBP' AND
              tdname   EQ g_name AND
              tdid     EQ '0001' AND
              tdspras  EQ sy-langu.
  ENDLOOP.

  LOOP AT gt_stxh.
    REFRESH i_lines.

    gw_sales_n1-vbeln    = gt_stxh-tdname.
    gw_sales_n1-tdid     = gt_stxh-tdid.
    gw_sales_n1-tdobject = gt_stxh-tdobject.

    CALL FUNCTION 'READ_TEXT'
      EXPORTING
        id                      = gt_stxh-tdid
        language                = gt_stxh-tdspras
        name                    = gt_stxh-tdname
        object                  = gt_stxh-tdobject
      TABLES
        lines                   = i_lines
      EXCEPTIONS
        id                      = 1
        language                = 2
        name                    = 3
        not_found               = 4
        object                  = 5
        reference_check         = 6
        wrong_access_to_archive = 7
        OTHERS                  = 8.

    IF sy-subrc = 0.
*     GET fam_code
      CLEAR g_vkorg.
      SELECT SINGLE vkorg INTO g_vkorg
        FROM vbak WHERE vbeln = gw_sales_n1-vbeln.

      IF sy-subrc = 0.
        CASE g_vkorg.
          WHEN 'PL01'.
            gw_sales_n1-fam_code = 'PL1'.
          WHEN 'PL02'.
            gw_sales_n1-fam_code = 'PL2'.
          WHEN 'PL03'.
            gw_sales_n1-fam_code = 'PLA'.
          WHEN 'PL04'.
            gw_sales_n1-fam_code = 'PL4'.
        ENDCASE.
      ENDIF.

      CLEAR gw_sales_n1-snseq.
      LOOP AT i_lines.
        gw_sales_n1-tdline = i_lines-tdline.
        gw_sales_n1-snseq = gw_sales_n1-snseq + 1.

        CLEAR: gw_sales_n1-snprt, gw_sales_n1-snpic , gw_sales_n1-sninv.
        CASE gt_stxh-tdobject.
          WHEN 'VBBK'.
            IF gt_stxh-tdid = 'SL01' or gt_stxh-tdid = 'SL03'.
              gw_sales_n1-snprt = 'X'.
              gw_sales_n1-sninv = 'X'.
            ELSEIF gt_stxh-tdid = 'SL02' or gt_stxh-tdid = 'SL04'.
              gw_sales_n1-snpic = 'X'.
            ELSEIF gt_stxh-tdid = 'SL06'.
              gw_sales_n1-sninv = 'X'.
            ELSEIF gt_stxh-tdid = 'SL07' or gt_stxh-tdid = 'SL08' or
                   gt_stxh-tdid = 'SL09'.
              gw_sales_n1-snprt = 'X'.
              gw_sales_n1-snpic = 'X'.
              gw_sales_n1-sninv = 'X'.
            ELSEIF gt_stxh-tdid = '0001' or gt_stxh-tdid = '0002' or
                   gt_stxh-tdid = '0003'.
              gw_sales_n1-snprt = 'X'.
            ENDIF.
          WHEN 'VBBP'.
            IF gt_stxh-tdid = '0001'.
              gw_sales_n1-snprt = 'X'.
              gw_sales_n1-snpic = 'X'.
              gw_sales_n1-sninv = 'X'.
            ENDIF.
          ENDCASE.

        APPEND gw_sales_n1 TO gt_sales_n1.
      ENDLOOP.
    ENDIF.

    CLEAR: gw_sales_n1, i_lines.
  ENDLOOP.

ENDFORM.                    "DATA_MANIPULATION

*&---------------------------------------------------------------------*
*&      Form  Write_outputfile
*&---------------------------------------------------------------------*
*       Write outputfile
*----------------------------------------------------------------------*
FORM write_outputfile.

  OPEN DATASET g_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, g_ofile.                "Open dataset failed
    EXIT.
  ENDIF.

  SORT gt_sales_h BY hord lline.

  loop at gt_sales_h.
    IF gt_sales_h-lqord <= gt_sales_h-lqshp.
      CONTINUE.
    ENDIF.

    move-corresponding gt_sales_h to gt_sales_h_out.
    CONCATENATE gt_sales_h-hsrce '_' gt_sales_h-hsrce2 INTO gt_sales_h_out-hsrce.
    MOVE '|' TO: gt_sales_h_out-tab01, gt_sales_h_out-tab02, gt_sales_h_out-tab03,
                 gt_sales_h_out-tab04, gt_sales_h_out-tab05, gt_sales_h_out-tab06,
                 gt_sales_h_out-tab07, gt_sales_h_out-tab08, gt_sales_h_out-tab09,
                 gt_sales_h_out-tab10, gt_sales_h_out-tab11, gt_sales_h_out-tab12,
                 gt_sales_h_out-tab13, gt_sales_h_out-tab14, gt_sales_h_out-tab15,
                 gt_sales_h_out-tab16, gt_sales_h_out-tab17, gt_sales_h_out-tab18,
                 gt_sales_h_out-tab19, gt_sales_h_out-tab20, gt_sales_h_out-tab21,
                 gt_sales_h_out-tab22, gt_sales_h_out-tab23, gt_sales_h_out-tab24,
                 gt_sales_h_out-tab25, gt_sales_h_out-tab26, gt_sales_h_out-tab27,
                 gt_sales_h_out-tab28, gt_sales_h_out-tab29, gt_sales_h_out-tab30,
                 gt_sales_h_out-tab31, gt_sales_h_out-tab32, gt_sales_h_out-tab33,
                 gt_sales_h_out-tab34, gt_sales_h_out-tab35, gt_sales_h_out-tab36,
                 gt_sales_h_out-tab37, gt_sales_h_out-tab38, gt_sales_h_out-tab39,
                 gt_sales_h_out-tab40, gt_sales_h_out-tab41, gt_sales_h_out-tab42,
                 gt_sales_h_out-tab43, gt_sales_h_out-tab44.

    CLEAR g_bismt.
    SELECT SINGLE bismt INTO g_bismt
      FROM mara WHERE matnr = gt_sales_h-lprod.

    IF NOT g_bismt IS INITIAL.
      gt_sales_h_out-lprod = g_bismt.
    ENDIF.
    append gt_sales_h_out.
    clear gt_sales_h_out.
  endloop.

* Write heading
  TRANSFER gs_head to g_ofile.

  LOOP AT gt_sales_h_out.
    TRANSFER gt_sales_h_out TO g_ofile.
  ENDLOOP.

  CLOSE DATASET g_ofile.

ENDFORM.                    " Write_outputfile

*&---------------------------------------------------------------------*
*&      Form  Write_textfile
*&---------------------------------------------------------------------*
*       Write textfile
*----------------------------------------------------------------------*
FORM write_textfile.

  OPEN DATASET g_ofile2 FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, g_ofile2.                "Open dataset failed
    EXIT.
  ENDIF.

  SORT gt_sales_n1 BY vbeln tdid snseq.

  LOOP AT gt_sales_n1 INTO gw_sales_n1.
    MOVE-CORRESPONDING gw_sales_n1 to gt_sales_n1_out.
    gt_sales_n1_out-hord = gw_sales_n1-vbeln.
    gt_sales_n1_out-sndesc = gw_sales_n1-tdline.
    CONCATENATE gw_sales_n1-tdobject gw_sales_n1-tdid
          INTO gt_sales_n1_out-snhealin SEPARATED BY '-'.
    MOVE '|' TO: gt_sales_n1_out-tab01, gt_sales_n1_out-tab02, gt_sales_n1_out-tab03,
                 gt_sales_n1_out-tab04, gt_sales_n1_out-tab05, gt_sales_n1_out-tab06,
                 gt_sales_n1_out-tab07.
    append gt_sales_n1_out.
    clear gt_sales_n1_out.
  ENDLOOP.

* Write heading
  TRANSFER gs_head_text to g_ofile2.

  LOOP AT gt_sales_n1_out.
    TRANSFER gt_sales_n1_out TO g_ofile2.
  ENDLOOP.

  CLOSE DATASET g_ofile2.

ENDFORM.                    " Write_textfile

*Text symbol text£º
*E01:No data found !
*E03:Open dataset failed:

*I02:Job finished
*Selection text£º
*S_AUART:D       .
*S_POSNR:D       .
*S_VBELN:D       .
*S_VKORG:D       .
