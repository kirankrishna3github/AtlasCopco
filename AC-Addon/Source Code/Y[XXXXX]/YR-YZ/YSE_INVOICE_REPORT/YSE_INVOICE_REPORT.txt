*&--------------------------------------------------------------------------*
*& Report  YSE_INVOICE_REPORT                                               *
*&                                                                          *
*&--------------------------------------------------------------------------*
*& Created by Peter Dudas                                                   *
*& Spec: Dxx-SDMM-Rep-GTS Invoice report.doc                                *
*          by Sarah
*---------------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                        *
*---------------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME        |CORRECTION NUMBER  | CHANGE REFERENCE # *
*---------------------------------------------------------------------------*
* MOD-001 |22/09/2008| Pavan Kaipa | CD1K943484        | CR-0381            *
* Description: Adding two fields 'Invoice Date' and 'RV Documaent No.' in   *
*              Selection screen.                                            *
*****************************************************************************
REPORT yse_invoice_report  NO STANDARD PAGE HEADING
                        LINE-COUNT 60 LINE-SIZE 200.
*                        message-id 00.

TABLES: vbrk, vbak, bseg, vbpa, adrc, knvk.

*--- Type pools for application server transfer
TYPE-POOLS truxs.

TYPES:

  BEGIN OF xtyp_s_bseg,
       bukrs LIKE bseg-bukrs,
       belnr LIKE bseg-belnr,
       gjahr LIKE bseg-gjahr,
       buzei LIKE bseg-buzei,
       kunnr LIKE bseg-kunnr,
       mwskz LIKE bseg-mwskz,
       zuonr LIKE bseg-zuonr,
       vbeln LIKE bseg-vbeln,
  END OF xtyp_s_bseg,

  BEGIN OF xtyp_s_bkpf,
       bukrs LIKE bkpf-bukrs,
       belnr LIKE bkpf-belnr,
       gjahr LIKE bkpf-gjahr,
       tcode LIKE bkpf-tcode,
       stblg LIKE bkpf-stblg,
  END OF xtyp_s_bkpf,

  BEGIN OF xtyp_s_struc,
       name(30)  TYPE c,
       idx TYPE i,
  END OF xtyp_s_struc,

  BEGIN OF xtyp_stxh,
      tdobject LIKE stxh-tdobject,
      tdname LIKE stxh-tdname,
      tdid LIKE stxh-tdid,
      tdspras LIKE stxh-tdspras,
  END OF xtyp_stxh,

  BEGIN OF xtyp_konv,
        knumv LIKE konv-knumv,
        kposn LIKE konv-kposn,
        kawrt LIKE konv-kawrt,
        kbetr LIKE konv-kbetr,
        waers LIKE konv-waers,
  END OF xtyp_konv,

  BEGIN OF xtyp_cpers,
        vbeln     LIKE vbpa-vbeln,
        namev     LIKE knvk-namev,
        name1     LIKE knvk-name1,
        name3     LIKE adrc-name3,
        date_from LIKE adrc-date_from,
  END OF xtyp_cpers,

  BEGIN OF xtyp_s_kna1,
    kunnr LIKE kna1-kunnr,
    stcd1 LIKE kna1-stcd1,
    adrnr LIKE kna1-adrnr,
  END OF xtyp_s_kna1,

  BEGIN OF xtyp_s_vbeln,
       vbeln LIKE bseg-vbeln,
  END OF xtyp_s_vbeln,

  BEGIN OF xtyp_s_knbk,
      kunnr LIKE knbk-kunnr,
      koinh LIKE knbk-koinh,
      bankn LIKE knbk-bankn,
  END OF xtyp_s_knbk,

  BEGIN OF xtyp_s_knvp,
      kunnr LIKE knvp-kunnr,
      vkorg LIKE knvp-vkorg,
      vtweg LIKE knvp-vtweg,
      spart LIKE knvp-spart,
      pernr LIKE knvp-pernr,
  END OF xtyp_s_knvp,


* When changed please change header information in download form
  BEGIN OF xtyp_s_out,
       belnr      LIKE bseg-belnr,    "= bseg-belnr
       bukrs      LIKE bseg-bukrs,
       vkorg      LIKE vbrk-vkorg,
       fkdat      LIKE vbrk-fkdat,
       vbeln      LIKE bseg-vbeln,    " = vbrp-vbeln
       posnr      LIKE vbrp-posnr,
       kunnr      LIKE bseg-kunnr,
       name1      LIKE adrc-name1,
       tel_number LIKE adrc-tel_number,
       fax_number LIKE adrc-fax_number,
       cpers_name(30) TYPE c,
       post_code1 LIKE adrc-post_code1,
       street     LIKE adrc-street,
       str_suppl2 LIKE adrc-str_suppl2,                     " street 3
       str_suppl3 LIKE adrc-str_suppl3,                     " street 4
       region     LIKE adrc-region,
       stcd1      LIKE kna1-stcd1,
       koinh      LIKE knbk-koinh,
       bankn      LIKE knbk-bankn,
       waerk      LIKE vbrk-waerk,
       matnr      LIKE vbrp-matnr,
       arktx      LIKE vbrp-arktx,
       vrkme      LIKE vbrp-vrkme,
* MODEL, EQUIPMENT?      LIKE  -,
       uprice     LIKE vbrp-kzwi3,  " vbrp-netwr/fkimg
       fkimg      LIKE vbrp-fkimg,
       stcd1_2    LIKE kna1-stcd1,
       kbetr(7)   TYPE c,              "  LIKE komv-kbetr,
       mwsbp      LIKE vbrp-mwsbp,
       mwskz      LIKE bseg-mwskz,
       text(120)  TYPE c,
       grossval   LIKE vbrp-netwr,
       bstkd      LIKE vbkd-bstkd,
       aubel      LIKE vbrp-aubel, " billtyp ZF3->Contract else Sales Order nr.
       fkart      LIKE vbrk-fkart,
       pernr      LIKE knvp-pernr,
       vkgrp      LIKE vbrp-vkgrp,
       stblg      LIKE bkpf-stblg,
* Optionals
  END OF xtyp_s_out.


TYPES: BEGIN OF xtyp_s_data,
*       vkorg LIKE vbrk-vkorg, " for technical reasons only.
       vtweg LIKE vbrk-vtweg, " for technical reasons only.
       spart LIKE vbrk-spart, " for technical reasons only.
       knumv LIKE vbrk-knumv, " to get link to komv-kbetr
       netwr LIKE vbrp-netwr.
INCLUDE TYPE xtyp_s_out.
TYPES:  END OF xtyp_s_data.

DATA: x_repid   LIKE sy-repid,
      xt_data TYPE TABLE OF xtyp_s_data,
      xt_out TYPE TABLE OF xtyp_s_out,
      xv_data TYPE xtyp_s_data,
      xv_lines TYPE i,
      xv_char4(4) TYPE c,
      xp_csv(1) TYPE c VALUE 'X',
      variant           LIKE disvariant.

RANGES:  xr_bukrs FOR bseg-bukrs,
         xr_belnr FOR bseg-belnr.

FIELD-SYMBOLS: <x_data>  TYPE xtyp_s_data.


*----------------------------------------------------------------------*
* Selection screen                                                     *
*----------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK b01 WITH FRAME TITLE text-t01.
PARAMETERS:
            xp_gjahr LIKE bseg-gjahr,
*Begin of change MOD-001.
            p_fkdat  TYPE fkdat,
            p_belnr  TYPE belnr_d,
*End of change MOD-001.
            xp_inv RADIOBUTTON GROUP rad DEFAULT 'X',
            xp_canc RADIOBUTTON GROUP rad.
PARAMETERS: xp_datei LIKE rlgrap-filename.
*  PARAMETERS:
*              xp_xls RADIOBUTTON GROUP fil DEFAULT 'X',
*              xp_xml RADIOBUTTON GROUP fil,
*              xp_csv RADIOBUTTON GROUP fil DEFAULT 'X'.


SELECTION-SCREEN END OF BLOCK b01.
*----------------------------------------------------------------------*
* Initialization of the selection screen                               *
*----------------------------------------------------------------------*
INITIALIZATION.
  xp_datei = 'c:\temp\gts_report.txt'.
  xp_gjahr = sy-datum+0(4).



*&---------------------------------------------------------------------*
*& SELECTION-SCREEN
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR xp_datei.
  PERFORM get_save_name USING ''
                        CHANGING xp_datei.


*&---------------------------------------------------------------------*
*&   Event top-of-page
*&---------------------------------------------------------------------*
TOP-OF-PAGE.
  FORMAT COLOR COL_HEADING.
  ULINE.
  WRITE:/       text-to1.
*          AT 35 text-to2,
  ULINE.
  FORMAT COLOR COL_NORMAL.


* AT SELECTION-SCREEN.
*  PERFORM check_authorization.

************************************************************************
* START MAIN PROGRAM                                                   *
************************************************************************
INITIALIZATION.
  PERFORM initialize_data.

START-OF-SELECTION.
  PERFORM select_data.

END-OF-SELECTION.
  PERFORM download_data.
  MOVE xv_lines TO xv_char4.
  WRITE: xv_char4 , text-r01 , xp_datei , text-r02 .


*&---------------------------------------------------------------------*
*&      Form  initialize_data
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM initialize_data .

  REFRESH xt_data.


* predefined selection criterias:
  REFRESH xr_bukrs.
  xr_bukrs-sign = 'I'.
  xr_bukrs-option = 'EQ'.
  xr_bukrs-low = 'CODA'.
  APPEND xr_bukrs.
  xr_bukrs-low = 'SHTA'.
  APPEND xr_bukrs.

* Authority check:
  LOOP AT xr_bukrs.
    AUTHORITY-CHECK OBJECT 'F_SKA1_BUK'
                        ID 'BUKRS' FIELD xr_bukrs-low
                        ID 'ACTVT' DUMMY.
    IF sy-subrc = 4.
*   No authorisation to display the data
      MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH xr_bukrs-low.
    ELSEIF sy-subrc <> 0.
*   Error checking authorization.
      MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
    ENDIF.
  ENDLOOP.


*Begin of change MOD-001.
*  REFRESH xr_belnr.
*  xr_belnr-sign = 'I'.
*  xr_belnr-option = 'BT'.
*  xr_belnr-low = '1100000000'.
*  xr_belnr-high = '1199999999'.
** Just for testing a different number range
*  IF sy-mandt = '160'.
*    xr_belnr-high = '9999999999'.
*  ENDIF.
*  APPEND xr_belnr.
*End of change MOD-001.


ENDFORM.                    " initialize_data
*&---------------------------------------------------------------------*
*&      Form  select_data
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM select_data .


  DATA: lt_bseg TYPE TABLE OF xtyp_s_bseg,
        lv_bseg LIKE LINE OF lt_bseg,
        lt_bkpf TYPE TABLE OF xtyp_s_bkpf,
        lv_bkpf LIKE LINE OF lt_bkpf,
        lt_adrc TYPE TABLE OF adrc,
        lv_adrc LIKE LINE OF lt_adrc,
        lt_knvp TYPE TABLE OF xtyp_s_knvp,
        lv_knvp LIKE LINE OF lt_knvp,
        lt_kna1 TYPE TABLE OF xtyp_s_kna1,
        lv_kna1 LIKE LINE OF lt_kna1,
        lt_knbk TYPE TABLE OF xtyp_s_knbk,
        lv_knbk LIKE LINE OF lt_knbk,
*        lt_vbeln TYPE TABLE OF xtyp_s_vbeln,
*        lv_vbeln LIKE LINE OF lt_vbeln,
        lv_vbeln LIKE bseg-vbeln,
        lt_stxh TYPE TABLE OF xtyp_stxh,
        lt_stxh_tmp TYPE TABLE OF xtyp_stxh,
        lv_stxh TYPE xtyp_stxh,
        lt_lines TYPE TABLE OF tline,
        lt_konv TYPE TABLE OF xtyp_konv,
        lv_konv TYPE xtyp_konv,
        lt_cpers TYPE TABLE OF xtyp_cpers,
        lt_cpers_2 TYPE TABLE OF xtyp_cpers,
        lv_cpers TYPE xtyp_cpers,
        lv_vrkme LIKE vbap-vrkme,
        lv_tabix LIKE sy-tabix,
        lv_out TYPE xtyp_s_out.

  FIELD-SYMBOLS: <l_lines> TYPE tline.

* FKDAT is indexed in VKDFS

  SELECT bukrs belnr gjahr buzei kunnr mwskz zuonr vbeln
    FROM bseg
    INTO TABLE lt_bseg
      WHERE bukrs IN xr_bukrs
*Begin of change MOD-001.
*        AND belnr IN xr_belnr
        AND belnr = p_belnr
*End of change MOD-001.
        AND gjahr = xp_gjahr
        AND koart = 'D'
        AND zuonr NOT LIKE 'G%'
        AND zuonr NOT LIKE 'N%'
        AND vbeln <> ''.

  CHECK sy-subrc = 0.

* BKPF data
  IF xp_inv = 'X'.   " INVOICES

    SELECT bukrs belnr gjahr tcode stblg
        FROM bkpf
        INTO TABLE lt_bkpf
        FOR ALL ENTRIES IN lt_bseg
        WHERE bukrs = lt_bseg-bukrs
            AND belnr = lt_bseg-belnr
            AND gjahr = lt_bseg-gjahr
            AND stblg = ''.
*            AND xblnr <> ''             " just an extra option
  ELSE.              " CANCELATIONS
    SELECT bukrs belnr gjahr tcode stblg
        FROM bkpf
        INTO TABLE lt_bkpf
        FOR ALL ENTRIES IN lt_bseg
        WHERE bukrs = lt_bseg-bukrs
            AND belnr = lt_bseg-belnr
            AND gjahr = lt_bseg-gjahr
            AND tcode = 'VF11'
            AND stblg <> ''.
  ENDIF.
* Delete unnecessary line from lt_bseg.
  SORT lt_bkpf BY bukrs belnr gjahr.
  LOOP AT lt_bseg INTO lv_bseg.
    lv_tabix = sy-tabix.
    READ TABLE lt_bkpf TRANSPORTING NO FIELDS
          WITH KEY bukrs = lv_bseg-bukrs
                          belnr = lv_bseg-belnr
                            gjahr = lv_bseg-gjahr.
    IF sy-subrc <> 0.
      DELETE lt_bseg INDEX lv_tabix.
    ENDIF.
  ENDLOOP.



  IF lt_bseg IS INITIAL.
    EXIT.
  ENDIF.

* MAIN SELECT GET billing document data
  SELECT vbrp~vbeln vbrp~posnr
         vbrk~fkdat vbrk~waerk vbrk~vkorg vbrk~vtweg
         vbrp~matnr vbrp~vrkme vbrp~fkimg vbrp~arktx
         vbrp~netwr vbrp~aubel vbrk~fkart vbrp~vkgrp vbrp~mwsbp
         vbkd~bstkd
         vbrk~knumv                      " to get tax rate
      FROM vbrp
        JOIN vbrk ON vbrk~vbeln = vbrp~vbeln
        LEFT JOIN vbkd ON vbkd~vbeln = vbrp~aubel
                    AND vbkd~posnr = vbrp~aupos
      INTO CORRESPONDING FIELDS OF TABLE xt_data
      FOR ALL ENTRIES IN lt_bseg
        WHERE vbrp~vbeln = lt_bseg-vbeln
*Begin of change MOD-001.
         AND  vbrk~fkdat = p_fkdat.
*End of change MOD-001.

* GET kondition data

  SELECT knumv kposn kawrt kbetr waers
        FROM konv
      INTO TABLE lt_konv
      FOR ALL ENTRIES IN xt_data
      WHERE  konv~knumv = xt_data-knumv
         AND konv~kposn = xt_data-posnr
         AND konv~kschl = 'MWST'.


* GET customer data KNA1
* Optimalization hint: first delete from lt_bseg the unnecessary records
  SELECT DISTINCT kna1~kunnr kna1~stcd1 kna1~adrnr
    FROM kna1
    INTO TABLE lt_kna1
    FOR ALL ENTRIES IN lt_bseg
    WHERE kna1~kunnr = lt_bseg-kunnr
   .

* then GET customer data ADDR valid lines
  SELECT addrnumber  name1 tel_number fax_number post_code1 region
         street str_suppl2 str_suppl3
    FROM adrc
    INTO CORRESPONDING FIELDS OF TABLE lt_adrc
    FOR ALL ENTRIES IN lt_kna1
    WHERE      addrnumber = lt_kna1-adrnr
        .

* GET BANK DETAILS (select any - validity dates are empty)
  SELECT DISTINCT kunnr koinh bankn
      FROM knbk
      INTO TABLE lt_knbk
      FOR ALL ENTRIES IN lt_kna1
          WHERE kunnr = lt_kna1-kunnr.
* sorting for binary search
  SORT lt_adrc BY addrnumber.
  SORT lt_kna1 BY kunnr.
  SORT lt_knbk BY kunnr.
  SORT lt_bseg BY vbeln.
  SORT lt_bkpf BY belnr.
  SORT lt_konv BY knumv kposn.
* compile main data
  LOOP AT xt_data ASSIGNING <x_data>.


* Convert VRKME:
    IF NOT <x_data>-vrkme IS INITIAL.
      CALL FUNCTION 'CONVERSION_EXIT_CUNIT_OUTPUT'
        EXPORTING
          input                = <x_data>-vrkme
          language             = 'E'
       IMPORTING
*         LONG_TEXT            =
          output               = lv_vrkme
*         SHORT_TEXT           =
       EXCEPTIONS
         unit_not_found       = 1
         OTHERS               = 2
                .
      IF sy-subrc = 0.
        MOVE lv_vrkme TO <x_data>-vrkme.
      ENDIF.
    ENDIF.



*  ACCOUNTING (1. bseg than bkpf)
    READ TABLE lt_bseg INTO lv_bseg
        WITH KEY vbeln = <x_data>-vbeln
        BINARY SEARCH.
    IF sy-subrc = 0.
      MOVE lv_bseg-bukrs TO <x_data>-bukrs.
      MOVE lv_bseg-kunnr TO <x_data>-kunnr.
      MOVE lv_bseg-mwskz TO <x_data>-mwskz.
      MOVE lv_bseg-belnr TO <x_data>-belnr.
    ENDIF.

* get stblg only for cancellations
    IF xp_inv <> 'X'.      " cancellations
      READ TABLE lt_bkpf INTO lv_bkpf
          WITH KEY belnr = lv_bseg-belnr
          BINARY SEARCH.
      IF sy-subrc = 0.
        MOVE lv_bkpf-stblg TO <x_data>-stblg.
      ELSE.
        CLEAR lv_bkpf.
      ENDIF.
    ENDIF.
* UPDATE CUSTOMER DATA
    READ TABLE lt_kna1 INTO lv_kna1
        WITH KEY kunnr = <x_data>-kunnr
        BINARY SEARCH.
    IF sy-subrc = 0.
      MOVE lv_kna1-stcd1 TO <x_data>-stcd1.
      MOVE lv_kna1-stcd1 TO <x_data>-stcd1_2.
    ENDIF.

    READ TABLE lt_adrc INTO lv_adrc
        WITH KEY addrnumber = lv_kna1-adrnr
        BINARY SEARCH.
    IF sy-subrc = 0.
      MOVE-CORRESPONDING lv_adrc TO <x_data>.
    ENDIF.

* BANK DATA
    READ TABLE lt_knbk INTO lv_knbk
        WITH KEY kunnr = <x_data>-kunnr
        BINARY SEARCH.
    IF sy-subrc = 0.
      MOVE lv_knbk-bankn TO <x_data>-bankn.
      MOVE lv_knbk-koinh TO <x_data>-koinh.
    ENDIF.

* collect data for stxh
    MOVE <x_data>-vbeln TO lv_stxh-tdname.
    APPEND lv_stxh TO lt_stxh_tmp.

  ENDLOOP.

* KNVP can be asked when all compiled
  SELECT kunnr vkorg vtweg spart pernr
    FROM knvp
    INTO TABLE lt_knvp
    FOR ALL ENTRIES IN xt_data
       WHERE knvp~kunnr = xt_data-kunnr
         AND knvp~vkorg = xt_data-vkorg
         AND knvp~vtweg = xt_data-vtweg
         AND knvp~spart = xt_data-spart
         AND knvp~parvw = 'PE'.
  SORT lt_knvp BY kunnr vkorg vtweg spart.

* Buffer Contact Persons data
*case 2
  IF NOT xt_data IS INITIAL.
    SELECT vbpa~vbeln adrc~name3 adrc~date_from
        FROM vbpa
        JOIN adrc ON adrc~addrnumber = vbpa~parnr
                    AND adrc~date_from < sy-datum
        INTO CORRESPONDING FIELDS OF TABLE lt_cpers
        FOR ALL ENTRIES IN xt_data
          WHERE vbpa~vbeln = xt_data-vbeln
            AND vbpa~parvw = 'AP'    " Exit conversion to 'CP' !!!
            AND vbpa~adrda <> 'D'
            .
*   remove non valid lines
    SORT lt_cpers BY vbeln date_from DESCENDING.
    CLEAR lv_vbeln.
    LOOP AT lt_cpers INTO lv_cpers.
      IF lv_cpers-vbeln = lv_vbeln.
        DELETE lt_cpers INDEX sy-tabix.
        CONTINUE.
      ENDIF.
      lv_vbeln = lv_cpers-vbeln.
    ENDLOOP.
*case 1
    SELECT vbpa~vbeln knvk~namev knvk~name1
        FROM vbpa
        JOIN knvk ON knvk~parnr = vbpa~parnr
        INTO CORRESPONDING FIELDS OF TABLE lt_cpers_2
        FOR ALL ENTRIES IN xt_data
          WHERE vbpa~vbeln = xt_data-vbeln
            AND vbpa~parvw = 'AP'  " Exit conversion to 'CP' !!!
            AND vbpa~adrda = 'D'.

    APPEND LINES OF lt_cpers_2 TO lt_cpers.
    REFRESH lt_cpers_2.
    SORT lt_cpers BY vbeln.
  ENDIF.


* find what text we have:
  IF NOT lt_stxh_tmp IS INITIAL.
    SELECT tdobject tdname tdid tdspras
        FROM stxh
        INTO TABLE lt_stxh
        FOR ALL ENTRIES IN lt_stxh_tmp
      WHERE tdobject = 'VBBK'
        AND tdname = lt_stxh_tmp-tdname
        AND ( tdid = 'SL03' OR    tdid = '001').
    REFRESH lt_stxh_tmp.
    SORT lt_stxh BY tdname.
  ENDIF.


* GET MISSING DATA AND MOVE EVERTHING TO XT_OUT.
  REFRESH xt_out.
  LOOP AT xt_data ASSIGNING <x_data>.
    CLEAR lv_out.
    MOVE-CORRESPONDING <x_data> TO lv_out.

* Load condition:
    READ TABLE lt_konv INTO lv_konv WITH KEY knumv = <x_data>-knumv kposn = <x_data>-posnr
                                            BINARY SEARCH.
    IF sy-subrc = 0.
      lv_konv-kbetr = lv_konv-kbetr / 10.
      WRITE lv_konv-kbetr TO lv_out-kbetr CURRENCY lv_konv-waers.
      CONCATENATE lv_out-kbetr '%' INTO lv_out-kbetr.
*      lv_out-kbetr = lv_konv-kbetr.

    ENDIF.

    lv_out-grossval = <x_data>-netwr + <x_data>-mwsbp.
    IF <x_data>-fkimg > 0.
      lv_out-uprice = <x_data>-netwr / <x_data>-fkimg.
    ELSE.
      lv_out-uprice = 0.
    ENDIF.

* get Salesman from buffer
    READ TABLE lt_knvp INTO lv_knvp
        WITH KEY kunnr = <x_data>-kunnr
                 vkorg = <x_data>-vkorg
                 vtweg = <x_data>-vtweg
                 spart = <x_data>-spart
        BINARY SEARCH.
    IF sy-subrc = 0.
      MOVE lv_knvp-pernr TO lv_out-pernr.
    ENDIF.
* get contact person name from buffer
    READ TABLE lt_cpers INTO lv_cpers WITH KEY vbeln = <x_data>-vbeln.
    IF sy-subrc = 0.
      IF NOT lv_cpers-name3 IS INITIAL.
        lv_out-cpers_name = lv_cpers-name3.
      ELSE.
        CONCATENATE lv_cpers-namev ' ' lv_cpers-name1 INTO lv_out-cpers_name IN CHARACTER MODE.
      ENDIF.
    ENDIF.

* get remark  from buffer
    READ TABLE lt_stxh INTO lv_stxh
        WITH KEY tdname = <x_data>-vbeln
                  BINARY SEARCH.
    IF sy-subrc = 0.
      CALL FUNCTION 'READ_TEXT'
        EXPORTING
          id       = lv_stxh-tdid
          language = lv_stxh-tdspras
          name     = lv_stxh-tdname
          object   = lv_stxh-tdobject
        TABLES
          lines    = lt_lines
        EXCEPTIONS
          OTHERS   = 8.
      IF sy-subrc EQ 0.
        LOOP AT lt_lines ASSIGNING <l_lines>.
          CONCATENATE lv_out-text <l_lines>-tdline ' ' INTO lv_out-text.
        ENDLOOP.
      ENDIF.
    ENDIF.

* store all in out data

    APPEND lv_out TO xt_out.
  ENDLOOP.
  DESCRIBE TABLE xt_data LINES xv_lines.
  REFRESH xt_data.

* for case 2 bkpf-STBLG must be queried



ENDFORM.                    " select_data


*&---------------------------------------------------------------------*
*&      Form  download_data
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM download_data .
  DATA: lt_converted TYPE truxs_t_text_data,
        lv_converted LIKE LINE OF lt_converted,
        BEGIN OF lt_header OCCURS 30,
          txt TYPE char20,
        END OF lt_header,
        lv_header LIKE LINE OF lt_header,
        lv_headerstr TYPE string,
        lv_field_str TYPE string,
        lv_doquote(1) TYPE c,
        lv_xstr TYPE xstring,
        lv_fs(1) TYPE c,
        lref_conv    TYPE REF TO cl_abap_conv_out_ce,
        lv_struc_idx LIKE sy-index,
        lv_str  TYPE string,

        lt_struc TYPE TABLE OF xtyp_s_struc,
        lv_struc LIKE LINE OF lt_struc,
        descr_ref TYPE REF TO cl_abap_structdescr.

  FIELD-SYMBOLS:  <l_out> TYPE xtyp_s_out,
                  <l_comp> TYPE abap_compdescr,
                  <l_field>.

  lv_fs = ';'.                                              "#EC NOTEXT

  APPEND:
       'RV document No.'(038) TO lt_header,             " bseg-belnr
       'Company'(001)        TO lt_header,              "  bseg-bukrs,
       'Sales Organization'(037) TO lt_header,          "  vbrk-vkorg
       'Inv. date'(002)      TO lt_header,               "  vbrk-fkdat,
       'Inv nr'(003)         TO lt_header,               "  bseg-vbeln, vbrp-vbeln
       'Inv. reversed'(036)  TO lt_header,               "  vbrp-posnr,
       'Customer nr.'(004)   TO lt_header,               "  bseg-kunnr,
       'Cust name Chinese'(005) TO lt_header,               "  adrc-name1,
       'Cust. tel'(006)      TO lt_header,         "  adrc-tel_number,
       'Cust fax'(007)       TO lt_header,         "  adrc-fax_number,
       'Contact Pers'(033)   TO lt_header,         "  cpers_name
       'Cust ZIP'(008)       TO lt_header,         "  adrc-POST_CODE1,
       'Cust. Inv. addr.'(009) TO lt_header,        "  adrc-street,
       'Cust Addr. 3'(010)   TO lt_header,         "  adrc-STR_SUPPL2,    " street 3
       'Cust Addr. 4'(011)   TO lt_header,         "  adrc-STR_SUPPL3,    " street 4
       'Cust. Area'(012)     TO lt_header,         "  adrc-region,
       'Cust Tax No.'(013)   TO lt_header,         "  kna1-stcd1,
       'Cust Bank Name'(014) TO lt_header,         "  knbk-koinh,
       'Cust Bank Account'(015) TO lt_header,       "  knbk-bankn,
       'Currency'(016)       TO lt_header,         "  vbrk-waerk,
       'Prod Nr.'(017)       TO lt_header,         "  vbrp-matnr,
       'Prod Name'(018)      TO lt_header,         "  vbrp-arktx,
       'Unit of measure'(019) TO lt_header,         "  vbrp-vrkme,
* MODEL, EQUIPMENT?      LIKE  -,
       'Unit price'(020)     TO lt_header,         "  komv-uprice, " ex kzwi3
       'Qty'(021)            TO lt_header,         "  vbrp-fkimg,
       'Tax classification'(034)   TO lt_header,            " stcd1_2
       'Tax Rate'(022)       TO lt_header,         " komv-kbetr,
       'Tax ammount'(023)    TO lt_header,         " vbrp-mwsbp,
       'Tax code'(024)       TO lt_header,         " bseg-mwskz,
       'Remark'              TO lt_header,         " text
       'Line amount'(025)    TO lt_header,         " vbrp-grossval,  " netwr
       'Cust PO nr.'(026)    TO lt_header,         " vbkd-bstkd,
       'Contract/SO No.'(027) TO lt_header,         " vbrp-aubel, " billtyp ZF3->Contract else Sales Order nr.
       'Invoice type'(028)   TO lt_header,         " vbrk-fkart,
       'Salesman'(030)       TO lt_header,         " knvp-pernr,
       'Salesgrp'(029)       TO lt_header.         " vbrp-vkgrp.


  IF xp_inv <> 'X'.         " cancellations
    APPEND:     'Inv. reversed'(031)   TO lt_header.         " bkpf-stblg,
  ENDIF.

* ';' is burnt into FUNCTION 'SAP_CONVERT_TO_CSV_FORMAT'

  IF xp_csv = 'X'.    " DO CSV

    CLEAR lv_headerstr.
    LOOP AT lt_header INTO lv_header.
      CONCATENATE lv_headerstr lv_header-txt lv_fs INTO lv_headerstr .
    ENDLOOP.


* create an index for table fields
    lv_struc_idx = 0.
    descr_ref ?= cl_abap_typedescr=>describe_by_name( 'XTYP_S_OUT' ).
*  LOOP AT descr_ref->components ASSIGNING <l_comp>.
*      lv_struc_idx = lv_struc_idx + 1.
*      lv_struc-idx = lv_struc_idx.
*      lv_struc-name = <l_comp>-name.
*      APPEND lv_struc TO lt_struc.
*  ENDLOOP.

* make a safe csv content
    LOOP AT xt_out ASSIGNING <l_out>.
      lv_struc_idx = 0.
      CLEAR lv_converted.
      DO.
        lv_struc_idx = lv_struc_idx + 1.
        ASSIGN COMPONENT lv_struc_idx OF
                 STRUCTURE <l_out> TO <l_field>.
        IF sy-subrc <> 0.
          EXIT.
        ELSE.

* Invoices reversed - conditional show.
          IF xp_inv = 'X'.
* get the name of the actual field
            READ TABLE descr_ref->components  ASSIGNING <l_comp> INDEX lv_struc_idx.
            IF <l_comp>-name = 'STBLG'.
              CONTINUE.  " just skip this field
            ENDIF.
          ENDIF.


          MOVE <l_field> TO lv_field_str.
* find field separators
          SEARCH lv_field_str FOR lv_fs.
          IF sy-subrc = 0.
            lv_doquote = 'X'.
          ENDIF.
* find quotes
          SEARCH lv_field_str FOR '"'.
          IF sy-subrc = 0.
            lv_doquote = 'X'.
            REPLACE ALL OCCURRENCES OF '"' IN lv_field_str WITH '""'.
          ENDIF.

* let's quote everything to Excel won't try to be smart
          lv_doquote = 'X'.

          IF lv_doquote = 'X'.
            CONCATENATE lv_converted '"' lv_field_str '"' lv_fs INTO lv_converted.
          ELSE.
            CONCATENATE lv_converted lv_field_str lv_fs INTO lv_converted.
          ENDIF.
        ENDIF.
      ENDDO.
      APPEND lv_converted TO lt_converted.
    ENDLOOP.

    lv_converted = lv_headerstr.
    INSERT lv_converted INTO lt_converted INDEX 1.

    MOVE xp_datei TO lv_str.
    CALL FUNCTION 'GUI_DOWNLOAD'
        EXPORTING
          filename = lv_str
          filetype = 'DAT'
          write_field_separator = 'X'
        TABLES
*        data_tab = xt_out
          data_tab = lt_converted
*        FIELDNAMES = lt_header
        EXCEPTIONS
         file_write_error                = 1
         no_batch                        = 2
         gui_refuse_filetransfer         = 3
         invalid_type                    = 4
         no_authority                    = 5
         unknown_error                   = 6
         header_not_allowed              = 7
         separator_not_allowed           = 8
         filesize_not_allowed            = 9
         header_too_long                 = 10
         dp_error_create                 = 11
         dp_error_send                   = 12
         dp_error_write                  = 13
         unknown_dp_error                = 14
         access_denied                   = 15
         dp_out_of_memory                = 16
         disk_full                       = 17
         dp_timeout                      = 18
         file_not_found                  = 19
         dataprovider_exception          = 20
         control_flush_error             = 21
         OTHERS                          = 22.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
      WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.
  ENDIF.
ENDFORM.                    " download_data



*&---------------------------------------------------------------------*
*&      Form  get_file_name
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      <--P_XP_DATEI  text
*----------------------------------------------------------------------*
FORM get_save_name USING  ph_filter TYPE string
                   CHANGING ph_datei.
  DATA:    lh_title    TYPE string,
           lh_filter   TYPE string,
           lh_initdir  TYPE string,
           lh_filename TYPE string,
           lh_path     TYPE string,
           lh_fullpath TYPE string.

  IF ph_filter IS INITIAL.
    lh_filter =
  'XLS (TAB getrennt)(*.XLS)|*.xls|Alle-Dateien (*.*)|*.*'.
  ELSE.
    lh_filter = ph_filter.
  ENDIF.

  lh_title = text-t04.
  MOVE ph_datei TO lh_initdir.

  CALL METHOD cl_gui_frontend_services=>file_save_dialog
     EXPORTING
       window_title      = lh_title
*      DEFAULT_EXTENSION =
*      DEFAULT_FILE_NAME =
       file_filter       = lh_filter
       initial_directory = lh_initdir
    CHANGING
      filename          = lh_filename
      path              = lh_path
      fullpath          = lh_fullpath
*    EXCEPTIONS
*      CNTL_ERROR        = 1
*      ERROR_NO_GUI      = 2
*      others            = 3
          .
  IF sy-subrc = 0.
    CALL METHOD cl_gui_cfw=>flush
      EXCEPTIONS
        cntl_system_error = 1
        cntl_error        = 2
        OTHERS            = 3.
  ENDIF.
  MOVE lh_fullpath TO ph_datei.

ENDFORM.                    " get_file_name

*Text symbol text£º
*001:Company
*002:Inv. date
*003:Inv nr
*004:Customer nr.
*005:Cust name Chinese
*006:Cust. tel
*007:Cust fax
*008:Cust ZIP
*009:Cust. Inv. addr.
*010:Cust Addr. 3
*011:Cust Addr. 4
*012:Cust. Area
*013:Cust Tax No.
*014:Cust Bank Name
*015:Cust Bank Account
*016:Currency
*017:Prod Nr.
*018:Prod Name
*019:Unit of measure
*020:Unit price
*021:Qty
*022:Tax Rate
*023:Tax ammount
*024:Tax code
*025:Line amount
*026:Cust PO nr.
*027:Contract/SO No.
*028:Invoice type
*029:Salesgrp
*030:Salesman
*031:Inv. Item No.
*032:Payee
*033:Contact Pers
*034:Tax classification
*035:Inv. reversed
*036:Inv item no.
*037:Sales Organization
*038:RV document No.
*R01:lines written to
*R02:.
*SS1:Export filename
*T01:GTS Invoice report
*T04:Save file to

*TO1:Result:
*Selection text£º
*P_BELNR:        RV Document No
*P_FKDAT:        Invoice Date
*XP_CANC:        Cancellations
*XP_DATEI:        Export filename
*XP_GJAHR:D       .
*XP_INV:        Invoices
