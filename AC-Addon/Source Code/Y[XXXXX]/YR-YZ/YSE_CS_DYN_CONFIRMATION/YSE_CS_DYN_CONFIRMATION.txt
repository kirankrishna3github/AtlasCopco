*----------------------------------------------------------------------*
* PROGRAM ID    : YSE_CS_DYN_CONFIRMATION                              *
* PROGRAM TITLE : Dynapac CS Order Mass confirmation per plan          *
* AUTHOR        : Anda Wu                                              *
* DATE          : 27/06/2017                                           *
* DEVELOPMENT ID:                                                      *
*                                                                      *
* CHANGE REQUEST NUMBER:                                               *
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
*  DATE    | NAME     |TRANSPORT  | CR# and short Description          *
*----------------------------------------------------------------------*
*27/06/2017|Anda Wu   |CD1K992489 |CR3297: Initial version             *
*----------------------------------------------------------------------*
REPORT  yse_cs_dyn_confirmation.
************************************************************************
* TABLES                                                               *
************************************************************************
TABLES: aufk.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:
  BEGIN OF ty_aufk,
    aufnr	TYPE aufk-aufnr,              "Order Number
    auart TYPE aufk-auart,              "Order Type
    autyp TYPE aufk-autyp,              "Order category
    ernam	TYPE aufk-ernam,              "Entered by
    erdat TYPE aufk-erdat,              "Created on
    aenam TYPE aufk-aenam,              "Last changed by
    aedat	TYPE aufk-aedat,              "Change date for Order Master
    ktext TYPE aufk-ktext,              "Description
    bukrs	TYPE aufk-bukrs,              "Company Code
    werks TYPE aufk-werks,              "Plant
    objnr TYPE aufk-objnr,              "Object number
    bemot TYPE aufk-bemot,              "Accounting Indicator
  END OF ty_aufk,

  BEGIN OF ty_log,
    aufnr	TYPE aufk-aufnr,              "Order Number
    ertyp TYPE sy-msgty,                "Message type
    mess  TYPE string,                  "Error messge
  END OF ty_log.


************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_aufk       TYPE ty_aufk.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
*  gt_lfile        TYPE yse_t_lfile,                 "Local file name
  gt_aufk         TYPE STANDARD TABLE OF ty_aufk,
*  gt_vbak         TYPE STANDARD TABLE OF vbak,
*  gt_list         TYPE STANDARD TABLE OF bapi_conf_key,
  gt_log          TYPE STANDARD TABLE OF ty_log.

************************************************************************
* VARIABLES                                                            *
************************************************************************
*DATA:
*  gv_tot_lines    TYPE i,                           "Process lines
*  gv_suc_lines    TYPE i,                           "Success Lines
*  gv_fai_lines    TYPE i.                           "Failed lines

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
*  gc_tab          TYPE c VALUE cl_abap_char_utilities=>horizontal_tab,
  gc_type_e       TYPE c VALUE 'E'.
*  gc_hifi         TYPE c VALUE '-',
*  gc_colon        TYPE c VALUE ':'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.
PARAMETERS: p_werks TYPE aufk-werks DEFAULT 'CD01'.
SELECT-OPTIONS: s_aufnr FOR aufk-aufnr,
                s_ernam FOR aufk-ernam,
                s_erdat FOR aufk-erdat.
SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: cb_hflg    AS CHECKBOX                    "Header text flag
              DEFAULT 'X' MODIF ID m1.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK b1.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initialization
  PERFORM frm_do_init.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN OUTPUT                                          *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.
* Set selection screen
  PERFORM set_screen.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
  DATA: lt_status TYPE STANDARD TABLE OF jstat,
        ls_jstat  TYPE jstat.
* Get the SEOs to be processed
  PERFORM get_data.
  LOOP AT gt_aufk INTO gs_aufk.
    REFRESH: lt_status.
    CALL FUNCTION 'STATUS_READ'
      EXPORTING
        objnr            = gs_aufk-objnr
        only_active      = 'X'
      TABLES
        status           = lt_status
      EXCEPTIONS
        object_not_found = 1
        OTHERS           = 2.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      CONTINUE.
    ELSE.
      READ TABLE lt_status INTO ls_jstat
        WITH KEY stat = 'I0009'. "Already confirmed
      IF sy-subrc = 0.
        CONTINUE.
      ENDIF.
    ENDIF.
*   Do confirmation based on SEOs above
    PERFORM do_confirmation.
  ENDLOOP.

END-OF-SELECTION.
  PERFORM show_log.

*&---------------------------------------------------------------------*
*&      Form  FRM_DO_INIT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM frm_do_init .

ENDFORM.                    " FRM_DO_INIT
*&---------------------------------------------------------------------*
*&      Form  SET_SCREEN
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM set_screen .

* Set editable for parameter [S_WERKS]
  LOOP AT SCREEN.
    IF screen-group1 = 'M1'.
      screen-input = '0'.
      screen-invisible = '1'.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.
  LOOP AT SCREEN.
    IF screen-name = 'P_WERKS'.
      screen-input = '0'.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " SET_SCREEN
*&---------------------------------------------------------------------*
*&      Form  GET_DATA
*&---------------------------------------------------------------------*
*       Get the SEOs to be processed
*----------------------------------------------------------------------*
FORM get_data .

  SELECT  aufnr	              "Order Number
          auart               "Order Type
          autyp               "Order category
          ernam	              "Entered by
          erdat               "Created on
          aenam               "Last changed by
          aedat	              "Change date for Order Master
          ktext               "Description
          bukrs	              "Company Code
          werks               "Plant
          objnr               "Object number
          bemot               "Accounting Indicator
    FROM aufk
    INTO TABLE gt_aufk
    WHERE aufnr IN s_aufnr
      AND ernam IN s_ernam
      AND erdat IN s_erdat
      AND werks = p_werks.

ENDFORM.                    " GET_DATA
*&---------------------------------------------------------------------*
*&      Form  DO_CONFIRMATION
*&---------------------------------------------------------------------*
*       Do confirmation based on SEOs above
*----------------------------------------------------------------------*
FORM do_confirmation.
  DATA: ls_confirmation TYPE bapi_alm_timeconfirmation,
        ls_log          TYPE ty_log,
        lt_confirmation TYPE STANDARD TABLE OF bapi_alm_timeconfirmation,
        lt_detail_return  TYPE STANDARD TABLE OF bapi_alm_return,
        ls_detail_return  TYPE bapi_alm_return,
        ls_header	  TYPE bapi_alm_order_header_e,
        lt_operation TYPE STANDARD TABLE OF bapi_alm_order_operation_e,
        ls_operation TYPE bapi_alm_order_operation_e,
        ls_return  TYPE bapiret2,
        lt_return  TYPE STANDARD TABLE OF bapiret2.
  DATA: lv_cfm_txt TYPE char40.

  CLEAR: ls_log.

  CALL FUNCTION 'BAPI_ALM_ORDER_GET_DETAIL'
    EXPORTING
      number        = gs_aufk-aufnr
    IMPORTING
      es_header     = ls_header
    TABLES
      et_operations = lt_operation
      return        = lt_return.
  LOOP AT lt_operation INTO ls_operation.
    CLEAR: ls_confirmation,lv_cfm_txt.
    CONCATENATE 'Auto-Confirmed on'(t25)
                sy-datum
                'by'(t26)
                sy-uname
        INTO lv_cfm_txt
        SEPARATED BY space.
    ls_confirmation-orderid   = gs_aufk-aufnr.
    ls_confirmation-operation = ls_operation-activity.
    ls_confirmation-work_cntr = ls_operation-work_cntr.
    ls_confirmation-act_work  = ls_operation-work_activity.
    ls_confirmation-fin_conf  = 'X'.
    ls_confirmation-complete  = 'X'.
    ls_confirmation-clear_res = 'X'.
    ls_confirmation-un_work   = ls_operation-un_work.
    ls_confirmation-act_type  = ls_operation-acttype.
    ls_confirmation-calc_motive = gs_aufk-bemot.
    ls_confirmation-conf_text = lv_cfm_txt.
    APPEND ls_confirmation TO lt_confirmation.
  ENDLOOP.

  CALL FUNCTION 'BAPI_ALM_CONF_CREATE'
    IMPORTING
      return        = ls_return
    TABLES
      timetickets   = lt_confirmation
      detail_return = lt_detail_return.

  ls_log-aufnr = gs_aufk-aufnr.
  READ TABLE lt_detail_return INTO ls_detail_return
    WITH KEY type = gc_type_e.
  IF sy-subrc <> 0.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.
    ls_log-ertyp = 'S'.
  ELSE.
    ls_log-ertyp = 'E'.
*    MESSAGE ID ls_detail_return-message_id
*            TYPE ls_detail_return-type
*            NUMBER ls_detail_return-message_number
*            WITH ls_detail_return-message_v1
*            ls_detail_return-message_v2
*            ls_detail_return-message_v3
*            ls_detail_return-message_v4
*       INTO ls_log-mess.
    ls_log-mess = ls_detail_return-message.
  ENDIF.
  APPEND ls_log TO gt_log.
ENDFORM.                    " DO_CONFIRMATION
*&---------------------------------------------------------------------*
*&      Form  SHOW_LOG
*&---------------------------------------------------------------------*
*       Show process log
*----------------------------------------------------------------------*
FORM show_log .
  DATA: ls_log TYPE ty_log.
  READ TABLE gt_log WITH KEY ertyp = gc_type_e TRANSPORTING NO FIELDS .
  IF sy-subrc <> 0.
    MESSAGE s001(00) WITH 'All confirmed successfully'(s88).
  ELSE.
    WRITE: 1(15) 'SEO', sy-vline, 18(80) 'Error Log'(t30).
    ULINE.
    LOOP AT gt_log INTO ls_log WHERE ertyp = gc_type_e.
      WRITE:/1(15) ls_log-aufnr,sy-vline, 18(80) ls_log-mess.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " SHOW_LOG

*Text symbol text£º
*S88:All confirmed successfully
*T01:Selection Input
*T25:Auto-Confirmed on
*T26:by

*T30:Error Log
*Selection text£º
*P_WERKS:D       .
*S_AUFNR:D       .
*S_ERDAT:D       .
*S_ERNAM:D       .
