*&---------------------------------------------------------------------*
*& Report  YSE_CS_NOTI_OSNO
*&
*&---------------------------------------------------------------------*
*----------------------------------------------------------------------*
* Author  : Anda Wu                                                    *
* Created : 03.08.2016                                                 *
* Dev.nbr: CR4011                                                      *
*----------------------------------------------------------------------*
* This report  is  new report for returns  of Russia                   *
************************************************************************
* Mod-xxx | xxxx.xx.xx | xxxx xxxx     | CD1Kxxxxxxxxxx  |   CRXXXX    *
************************************************************************
************************************************************************

*Update the cancel order to CRM WEB
REPORT  yse_cs_noti_osno.

************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES:
  qmel.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:

  BEGIN OF ty_alv_output,
    qmnum    TYPE qmnum,        "Notification No
    aenam    TYPE qmel-aenam,   "Last changed by
    ernam    TYPE qmel-ernam,   "Entered by
    erdat    TYPE qmel-erdat,   "Created on
    aedat    TYPE qmel-aedat,   "Change date for Order Master
    aufnr    TYPE char12,       "SEO Order Number
    msgty    TYPE bdc_mart  ,   "Message Type
    msgtx    TYPE char120,      "Message text
  END OF ty_alv_output.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_qmel     TYPE STANDARD TABLE OF qmel,
  gt_bdcdata  TYPE STANDARD TABLE OF bdcdata,
  gt_alvdata  TYPE STANDARD TABLE OF ty_alv_output,
  gt_fieldcat TYPE slis_t_fieldcat_alv.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_qmel     TYPE qmel,
  gs_alvdata  TYPE ty_alv_output,
  gs_disvar   TYPE disvariant,
  gs_layout   TYPE slis_layout_alv.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_col(10)  TYPE n,
  gv_text     TYPE string,
  gv_save     TYPE c,
  gv_callback TYPE slis_formname.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x                TYPE c VALUE 'X',
  gc_callback_routine TYPE slis_formname VALUE 'USER_COMMAND'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-t02 .
SELECT-OPTIONS:
  s_aedat      FOR qmel-aedat OBLIGATORY, "Change date for Order Master
  s_aenam      FOR qmel-aenam,            "Material ID
  s_qmnum      FOR qmel-qmnum,            "Service order number
  s_ernam      FOR qmel-ernam,
  s_erdat      FOR qmel-erdat.
SELECTION-SCREEN END OF BLOCK b2.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initilization
  PERFORM frm_init.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* get data
  PERFORM get_alv_data.
* alv property set
  PERFORM alv_prop_set.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
* ALV display
  PERFORM alv_display.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do initilization
*----------------------------------------------------------------------*
FORM frm_init .

* initialize the internal table,workareas and variables
  REFRESH:
    gt_qmel,
    gt_alvdata,
    gt_fieldcat.

  CLEAR:
    gs_alvdata,
    gs_disvar,
    gs_layout,
    gv_col,
    gv_text,
    gv_save,
    gv_callback.

ENDFORM.                    " FRM_INIT

*&---------------------------------------------------------------------*
*&      Form  GET_ALV_DATA
*&---------------------------------------------------------------------*
*       get data
*---------------------------------------------------------------------c-*
FORM get_alv_data .
** Gain data from table qmel - Order master data
  PERFORM get_data_qmel.
** Generate ALV data for output
*  PERFORM generate_alv_data.
* Edit all available
*  PERFORM edit_alv_data.
ENDFORM.                    " GET_ALV_DATA

*&---------------------------------------------------------------------*
*&      Form  ALV_PROP_SET
*&---------------------------------------------------------------------*
*       alv property set
*----------------------------------------------------------------------*
FORM alv_prop_set .

* FIELDCAT set
  PERFORM alv_fieldcat_set.

* LAYOUT set
  PERFORM alv_layout_set.

* Set Others
  PERFORM alv_others_set.

ENDFORM.                    " ALV_PROP_SET


*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       Set Others
*----------------------------------------------------------------------*
FORM alv_others_set .

  gv_callback = gc_callback_routine.

ENDFORM.                    " ALV_OTHERS_SET

*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM alv_fieldcat_set .

** Create Fieldcatalogue from internal table

  CLEAR gv_col.

* Field definitions
  PERFORM add_field USING:
     'QMNUM'    12  'Notification'(s01)     'Notification'(l01) 'X',
     'ERNAM'    12  'Entered by'(s07) 'Entered by'(s07) ' ',
     'ERDAT'    8   'Created on'(s08) 'Created on'(s08) ' ',
     'AENAM'    12  'Changed by'(s02) 'Last changed by'(l02) ' ',
     'AEDAT'    8   'Changed on'(s03) 'Change date for order master'(l03) ' ',
     'MSGTY'    1 'Msg.Typ'(s04)   'Message Type'(l04) ' ',
     'MSGTX'    120 'Msg.Txt'(s05)    'Process Message'(l05)' '.
ENDFORM.                    " ALV_FIELDCAT_SET

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr_s
                         p_descr_l
                         p_hotsp.
  DATA:
     ls_fieldcat TYPE slis_fieldcat_alv.

  gv_col = gv_col + 1.

  CLEAR ls_fieldcat.
  ls_fieldcat-col_pos    = gv_col.
  ls_fieldcat-fieldname  = p_field.
  ls_fieldcat-outputlen  = p_len.
  ls_fieldcat-seltext_l  = p_descr_l.
  ls_fieldcat-seltext_m  = p_descr_l.
  ls_fieldcat-seltext_s  = p_descr_s.
  ls_fieldcat-fix_column = 'X'.
  ls_fieldcat-emphasize  = 'X'.
  ls_fieldcat-hotspot    = p_hotsp.

* Suppress leading zeros for the material number
  IF p_field = 'MATNR'.
    ls_fieldcat-edit_mask = '==MATN1'.
  ENDIF.

  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ADD_FIELD
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_lines(10)  TYPE c.

  CLEAR: gv_text.
  DESCRIBE TABLE gt_alvdata LINES lv_lines.
  CONCATENATE 'Totally'(t62) lv_lines 'entries are process'(t63)
                INTO gv_text SEPARATED BY space.
  MESSAGE i000(yse_sales_log) WITH gv_text.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'FRM_MY_STATUS'
      i_callback_user_command  = gv_callback
      is_layout                = gs_layout
      it_fieldcat              = gt_fieldcat
      i_save                   = gv_save
      is_variant               = gs_disvar
    TABLES
      t_outtab                 = gt_alvdata
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   "Auto optimize column width
  gs_layout-detail_popup = gc_x.        "Show detail screen

ENDFORM.                    " ALV_LAYOUT_SET

*&---------------------------------------------------------------------*
*&      Form  FRM_MY_STATUS
*&---------------------------------------------------------------------*
*       Status Set
*----------------------------------------------------------------------*
*  --> I_EXTAB    STATUS
*----------------------------------------------------------------------*
FORM frm_my_status
  USING i_extab TYPE slis_t_extab.              "#EC CALLED "#EC NEEDED

  SET PF-STATUS '9100'.

ENDFORM.        "FRM_MY_STATUS

*&---------------------------------------------------------------------*
*&      Form  FRM_SEND_STATUS
*&---------------------------------------------------------------------*
*       Write entry to datebase table
*----------------------------------------------------------------------*
FORM frm_send_status .

  DATA: i_edidc_control_comm LIKE edidc OCCURS 1 WITH HEADER LINE ,
        i_edidd_data TYPE edidd OCCURS 0 WITH HEADER LINE         ,
        wa_edidc     TYPE edidc,
        lv_mess_swt  TYPE string,
        ls_jcds      TYPE jcds,
        wa_srm_segm1          TYPE yse_srm_call.

  CONSTANTS: c_mestyp           LIKE edidc-mestyp  VALUE 'YSE_SRM_CALL',
             c_idoc_type        LIKE edidc-idoctp  VALUE 'YSE_SRM_CALL',
             c_segm1(20)        TYPE c             VALUE 'YSE_SRM_CALL',
             c_segm2(20)        TYPE c             VALUE 'YSE_SRM_CALL_1',
             c_segm3(20)        TYPE c             VALUE 'YSE_SRM_CALL_2',
             c_ls(2)            TYPE c             VALUE 'LS'.

  CLEAR : i_edidc_control_comm,
          wa_edidc,
          i_edidd_data.

* Find receiving partner
  SELECT SINGLE rcvprn INTO wa_edidc-rcvprn
         FROM edp13
         WHERE mestyp = c_mestyp.

* Polulate Control Record
  wa_edidc-mestyp =  c_mestyp.
  wa_edidc-idoctp =  c_idoc_type.
  wa_edidc-rcvprt =  c_ls.

  wa_srm_segm1-indicator = 'CAN'.
  wa_srm_segm1-qmnum = gs_qmel-qmnum.

  SELECT SINGLE * FROM jcds
    INTO ls_jcds
    WHERE objnr = gs_qmel-objnr
      AND stat  ='I0076'  "DLFL; I0068-OSNO
      AND inact = ' '.

  wa_srm_segm1-qmtxt  = ls_jcds-usnam.
  wa_srm_segm1-dateto = ls_jcds-udate.


* Create Idoc
  CLEAR i_edidd_data .
  i_edidd_data-segnam  = c_segm1.
  i_edidd_data-sdata   = wa_srm_segm1.
  APPEND i_edidd_data.
  CLEAR i_edidd_data.
  i_edidd_data-segnam  = c_segm2.
  APPEND i_edidd_data.
  CLEAR i_edidd_data.
  i_edidd_data-segnam  = c_segm3.
  APPEND i_edidd_data.

* Generate Idoc
  CALL FUNCTION 'MASTER_IDOC_DISTRIBUTE'
    EXPORTING
      master_idoc_control            = wa_edidc
    TABLES
      communication_idoc_control     = i_edidc_control_comm
      master_idoc_data               = i_edidd_data
    EXCEPTIONS
      error_in_idoc_control          = 1
      error_writing_idoc_status      = 2
      error_in_idoc_data             = 3
      sending_logical_system_unknown = 4
      OTHERS                         = 5.

  IF sy-subrc <> 0.
    gs_alvdata-msgty = 'E'.
    gs_alvdata-msgtx = 'Failed to send the data out'(t88).
    EXIT.
  ENDIF.

  CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
    EXPORTING
      wait = 'X'.
*
  CALL FUNCTION 'EDI_DOCUMENT_DEQUEUE_LATER'
    EXPORTING
      docnum                 = i_edidc_control_comm-docnum
    EXCEPTIONS
      idoc_is_not_to_dequeue = 1
      OTHERS                 = 2.

  IF sy-subrc = 0.
    gs_alvdata-msgty = 'S'.
    gs_alvdata-msgtx = i_edidc_control_comm-docnum.
  ENDIF.

*    gs_alvdata-msgtx = 'No error'(088).

ENDFORM.                    " FRM_SEND_STATUS

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       USER_COMMAND
*----------------------------------------------------------------------*
FORM user_command USING u_ucomm     LIKE sy-ucomm           "#EC CALLED
                        p_selfield  TYPE slis_selfield.

  CASE u_ucomm.
    WHEN '&IC1'.        " SAP standard code for double-clicking
      IF p_selfield-fieldname = 'QMNUM'.
        IF p_selfield-value IS NOT INITIAL.
          SET PARAMETER ID 'IQM' FIELD p_selfield-value.
          CALL TRANSACTION 'IW53'.
        ENDIF.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.

ENDFORM.                    " USER_COMMAND
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_qmel
*&---------------------------------------------------------------------*
*       Gain data from table qmel - Order master data
*----------------------------------------------------------------------*
FORM get_data_qmel .

  DATA: lt_qmel TYPE STANDARD TABLE OF qmel,
        lt_qmih TYPE STANDARD TABLE OF qmih,
        ls_qmih TYPE qmih,
        ls_srm_sel  TYPE yse_srm_sel,
        lt_status     TYPE STANDARD TABLE OF jstat.

  SELECT *
    FROM qmel
    INTO TABLE gt_qmel
    WHERE qmnum IN s_qmnum
      AND ernam IN s_ernam
      AND erdat IN s_erdat
      AND aenam IN s_aenam
      AND aedat IN s_aedat.
  IF sy-subrc <> 0.
    MESSAGE s001(00) DISPLAY LIKE 'E' WITH text-t01.
    LEAVE LIST-PROCESSING.
  ENDIF.
  IF gt_qmel IS NOT INITIAL.
    SELECT *
      FROM qmih
      INTO TABLE lt_qmih
      FOR ALL ENTRIES IN gt_qmel
      WHERE qmnum = gt_qmel-qmnum.
  ENDIF.

  LOOP AT gt_qmel INTO gs_qmel.
    IF  gs_qmel-vkorg <> 'CN01'.
      CONTINUE.
    ENDIF.
    READ TABLE lt_qmih INTO ls_qmih
      WITH KEY qmnum = gs_qmel-qmnum.
    IF sy-subrc = 0.
*      CLEAR: ls_srm_sel.
*      SELECT SINGLE *
*        FROM yse_srm_sel
*        INTO ls_srm_sel
*        WHERE iwerk = ls_qmih-iwerk
*           AND  ingpr = ls_qmih-ingrp.
*      IF sy-subrc <> 0 or ls_srm_sel IS INITIAL.
*        CONTINUE.
*      ENDIF.
*      SELECT COUNT(*)
*        FROM   yse_srm_excl
*         where iwerk = ls_qmih-iwerk
*          and  arbpl = gs_qmel-vaplz .
*      IF sy-subrc = 0.
*        CONTINUE.
*      ENDIF.
    ENDIF.

    CLEAR: gs_alvdata.

    REFRESH: lt_status.
*   Notification status
    CALL FUNCTION 'STATUS_READ'
      EXPORTING

        objnr                  = gs_qmel-objnr
        only_active            = 'X'
*     IMPORTING
*       OBTYP                  =
*       STSMA                  =
*       STONR                  =
      TABLES
        status                 =  lt_status
      EXCEPTIONS
        object_not_found       = 1
        OTHERS                 = 2
              .
    IF sy-subrc <> 0.
      CONTINUE.
    ENDIF.
    READ TABLE lt_status TRANSPORTING NO FIELDS WITH KEY
         stat = 'I0076'. "DLFL " "I0068-OSNO
    IF sy-subrc <> 0.
      CONTINUE.
    ELSE.
      READ TABLE lt_status TRANSPORTING NO FIELDS WITH KEY
         stat = 'I0072'. "NOCO
    ENDIF.
    PERFORM frm_send_status.
    MOVE-CORRESPONDING gs_qmel TO gs_alvdata.
    APPEND gs_alvdata TO gt_alvdata.
  ENDLOOP.

ENDFORM.                    " GET_DATA_qmel

*Text symbol text£º
*L01:Notification
*L02:Last changed by
*L03:Change date for order master
*L04:Message Type
*L05:Process Message
*S01:Notification
*S02:Changed by
*S03:Changed on
*S04:Msg.Typ
*S05:Msg.Txt
*S07:Entered by
*S08:Created on
*T02:Selection screen

*T88:Failed to send the data out
*Selection text£º
*S_AEDAT:D       .
*S_AENAM:D       .
*S_ERDAT:D       .
*S_ERNAM:D       .
*S_QMNUM:D       .
