************************************************************************
* Program ID           : YSE_UPDATE_CALL_HORIZON                       *
* Program Title        : Update Call Horizon in Contracts              *
* Author               : Lakshmi Reddy                                 *
* Date                 : 15/02/2011                                    *
* Change Request Number: CD1K963002                                    *
* Description          : The program reads an input file which contains*
*                        maintenance plan and new call horizon details *
*                        and update the maintenance plans with the new *
*                        Call horizon.                                 *
*======================================================================*
* Copied From          : N/A                                           *
* Title                : N/A                                           *
* Other Related obj    : N/A                                           *
*======================================================================*
* Change History Log                                                   *
*----------------------------------------------------------------------*
* Mod. no.|Date      |Name        |Correction Number|Change Reference  *
*----------------------------------------------------------------------*
* MOD-001 |15/02/2011|LakshmiReddy|CD1K963002       |CR1925            *
* Description: Initial Development                                     *
*----------------------------------------------------------------------*

REPORT  yse_update_call_horizon MESSAGE-ID yam_re.

************************************************************************
*                  T Y P E S  S T R U C T U R E S
************************************************************************
TYPE-POOLS: slis.

TYPES: BEGIN OF ty_inputdata,
        warpl   TYPE warpl,
        horiz   TYPE horizont,
       END OF ty_inputdata.

TYPES: BEGIN OF ty_report,
        warpl   TYPE warpl,
        horiz   TYPE char3,
        errtyp  TYPE char1,
        errdes  TYPE string,
       END OF ty_report.

************************************************************************
*                 I N T E R N A L  T A B L E S
************************************************************************
DATA:i_files     TYPE filetable,
     i_inputdata TYPE STANDARD TABLE OF ty_inputdata,
     i_report    TYPE STANDARD TABLE OF ty_report,
     i_fieldcatalog TYPE slis_t_fieldcat_alv,
     i_events       TYPE slis_t_event,
     i_header       TYPE slis_t_listheader.

*----------------------------------------------------------------------*
*                 V A R I A B L E S                                    *
*----------------------------------------------------------------------*
DATA:wa_file    TYPE file_table,
     v_title    TYPE string,
     v_subrc    TYPE i,
     v_usract   TYPE i,
     v_deffile  TYPE string,
     wa_inputdata TYPE ty_inputdata,
     wa_report    TYPE ty_report.

*----------------------------------------------------------------------*
*                 S E L E C T I O N  S C R E E N                       *
*----------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK s1 WITH FRAME TITLE text-001.
PARAMETERS: p_file TYPE rlgrap-filename OBLIGATORY.
SELECTION-SCREEN END OF BLOCK s1.

*----------------------------------------------------------------------*
*                 A T  S E L E C T I O N  S C R E E N                  *
*----------------------------------------------------------------------*

*F4-Help for filename
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_file.
  PERFORM f100_f4_help_for_input_file CHANGING p_file.

************************************************************************
*                 S T A R T - O F - S E L E C T I O N
************************************************************************
START-OF-SELECTION.

  PERFORM f200_prepare_input_data.

  IF NOT i_inputdata IS INITIAL.

    PERFORM f300_update_call_horizon.

    PERFORM f400_display_report.

  ELSE.

    MESSAGE s320 WITH
           'The file does not contain any records'(002).
  ENDIF.

*----------------------------------------------------------------------*
*                            F O R M S                                 *
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  F100_F4_HELP_FOR_INPUT_FILE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM f100_f4_help_for_input_file CHANGING f_c_file.

  v_title = 'Select Input File'(011).
  MOVE f_c_file TO v_deffile.
  CALL METHOD cl_gui_frontend_services=>file_open_dialog
    EXPORTING
      window_title            = v_title
      default_filename        = v_deffile
      file_filter             = '*.*|*.*'
    CHANGING
      file_table              = i_files
      rc                      = v_subrc
      user_action             = v_usract
    EXCEPTIONS
      file_open_dialog_failed = 1
      cntl_error              = 2
      error_no_gui            = 3
      not_supported_by_gui    = 4
      OTHERS                  = 5.
  IF sy-subrc = 0 AND v_usract <> cl_gui_frontend_services=>action_cancel.
    LOOP AT  i_files INTO wa_file.
      MOVE wa_file-filename TO f_c_file.
      EXIT.
    ENDLOOP.
  ELSEIF v_usract EQ cl_gui_frontend_services=>action_cancel.
    MOVE v_deffile TO f_c_file.
  ENDIF.

ENDFORM.                    " F100_F4_HELP_FOR_INPUT_FILE

*&---------------------------------------------------------------------*
*&      Form  F200_PREPARE_INPUT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM f200_prepare_input_data .

  DATA: lv_file TYPE string,
        v_index TYPE sy-tabix.
  REFRESH: i_inputdata.
  MOVE p_file TO lv_file.

  CALL FUNCTION 'GUI_UPLOAD'
    EXPORTING
      filename                = lv_file
      filetype                = 'DAT'
    TABLES
      data_tab                = i_inputdata
    EXCEPTIONS
      file_open_error         = 1
      file_read_error         = 2
      no_batch                = 3
      gui_refuse_filetransfer = 4
      invalid_type            = 5
      no_authority            = 6
      unknown_error           = 7
      bad_data_format         = 8
      header_not_allowed      = 9
      separator_not_allowed   = 10
      header_too_long         = 11
      unknown_dp_error        = 12
      access_denied           = 13
      dp_out_of_memory        = 14
      disk_full               = 15
      dp_timeout              = 16
      OTHERS                  = 17.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  CLEAR: wa_inputdata,
         v_index.
  LOOP AT i_inputdata INTO wa_inputdata.
    v_index = sy-tabix.
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        input  = wa_inputdata-warpl
      IMPORTING
        output = wa_inputdata-warpl.
    MODIFY i_inputdata
    INDEX  v_index
    FROM   wa_inputdata.
    CLEAR: wa_inputdata,
           v_index.
  ENDLOOP.

ENDFORM.                    " F200_PREPARE_INPUT_DATA
*&---------------------------------------------------------------------*
*&      Form  F300_UPDATE_CALL_HORIZON
*&---------------------------------------------------------------------*
*       Update MPLA table with new call Horizon
*----------------------------------------------------------------------*
FORM f300_update_call_horizon .

  DATA: i_mpla  TYPE STANDARD TABLE OF mpla,
        wa_mpla TYPE mpla,
        v_index TYPE sy-tabix.

  REFRESH: i_mpla.
  IF NOT i_inputdata IS INITIAL.
    SELECT *
    FROM   mpla
    INTO   TABLE i_mpla
    FOR    ALL ENTRIES IN i_inputdata
    WHERE  warpl EQ i_inputdata-warpl.
    IF sy-subrc EQ 0.
      CLEAR: wa_mpla.
      LOOP AT i_mpla INTO wa_mpla.
        v_index = sy-tabix.
        READ TABLE i_inputdata INTO wa_inputdata WITH KEY
                                                 warpl = wa_mpla-warpl.
        IF sy-subrc EQ 0.
          wa_mpla-horiz = wa_inputdata-horiz.
          MODIFY i_mpla
          INDEX  v_index
          FROM   wa_mpla.
        ENDIF.
        CLEAR: wa_mpla,
               v_index.
      ENDLOOP.

      IF NOT i_mpla IS INITIAL.
        MODIFY mpla FROM TABLE i_mpla.
        IF sy-subrc EQ 0.
          COMMIT WORK.
          CLEAR wa_mpla.
          LOOP AT i_mpla INTO wa_mpla.
            CLEAR: wa_report.
            wa_report-warpl = wa_mpla-warpl.
            wa_report-horiz  = wa_mpla-horiz.
            wa_report-errtyp = 'S'.
            wa_report-errdes = 'Call Horizon has been updated successfully'(008).
            APPEND wa_report TO i_report.
            CLEAR: wa_mpla,
                   wa_report.
          ENDLOOP.
          CLEAR: wa_inputdata,
                 wa_mpla,
                 wa_report.
          LOOP AT i_inputdata INTO wa_inputdata.
            READ TABLE i_mpla INTO wa_mpla WITH KEY
                                                warpl = wa_inputdata-warpl.
            IF sy-subrc NE 0.
              wa_report-warpl = wa_inputdata-warpl.
              wa_report-horiz = wa_inputdata-horiz.
              wa_report-errtyp = 'E'.
              wa_report-errdes = 'The Maintenance plan does not exist in MPLA table'(009).
              APPEND wa_report TO i_report.
              CLEAR: wa_mpla,
                     wa_report,
                     wa_inputdata.
            ENDIF.
          ENDLOOP.
        ELSE.
          ROLLBACK WORK.
          CLEAR wa_mpla.
          LOOP AT i_mpla INTO wa_mpla.
            CLEAR: wa_report.
            wa_report-warpl = wa_mpla-warpl.
            wa_report-horiz = wa_mpla-horiz.
            wa_report-errtyp = 'E'.
            wa_report-errdes = 'Call Horizon not updated due to an error while updating database table'(010).
            APPEND wa_report TO i_report.
            CLEAR: wa_mpla.
          ENDLOOP.
          CLEAR: wa_inputdata,
                 wa_mpla,
                 wa_report.
          LOOP AT i_inputdata INTO wa_inputdata.
            READ TABLE i_mpla INTO wa_mpla WITH KEY warpl = wa_inputdata-warpl.
            IF sy-subrc NE 0.
              wa_report-warpl = wa_inputdata-warpl.
              wa_report-horiz = wa_inputdata-horiz.
              wa_report-errtyp = 'E'.
              wa_report-errdes = 'The Maintenance plan does not exist in MPLA table'(009).
              APPEND wa_report TO i_report.
              CLEAR: wa_mpla,
                     wa_report.
            ENDIF.
          ENDLOOP.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDIF.

ENDFORM.                    " F300_UPDATE_CALL_HORIZON

*&---------------------------------------------------------------------*
*&      Form  F400_DISPLAY_REPORT
*&---------------------------------------------------------------------*
*       Display ALV report
*----------------------------------------------------------------------*
FORM f400_display_report .

  PERFORM f410_build_field_catalog.

  PERFORM f420_build_events.

  PERFORM f430_display_alv_report.

ENDFORM.                    " F400_DISPLAY_REPORT
*&---------------------------------------------------------------------*
*&      Form  F410_BUILD_FIELD_CATALOG
*&---------------------------------------------------------------------*
*       Build field catalog
*----------------------------------------------------------------------*
FORM f410_build_field_catalog .

  PERFORM f411_field_catalog USING 'WARPL'
                                   'Maintenance Plan'(003).

  PERFORM f411_field_catalog USING 'HORIZ'
                                   'Call Horizon'(004).

  PERFORM f411_field_catalog USING 'ERRTYP'
                                   'Message Type'(005).

  PERFORM f411_field_catalog USING 'ERRDES'
                                   'Message Description'(006).

ENDFORM.                    " F410_BUILD_FIELD_CATALOG

*&---------------------------------------------------------------------*
*&      Form  F411_FIELD_CATALOG
*&---------------------------------------------------------------------*
*       Prepare field catalog with output fields
*----------------------------------------------------------------------*
FORM f411_field_catalog USING p_fname   TYPE any
                              p_seltext TYPE any.

  DATA: wa_fieldcatalog TYPE slis_fieldcat_alv.

*appending parameters of a particular field of a fieldcatalog
  wa_fieldcatalog-tabname   = 'I_REPORT'.
  wa_fieldcatalog-fieldname = p_fname.
  wa_fieldcatalog-seltext_m = p_seltext.

*appending parameters of a particular field to fieldcatalog
  APPEND wa_fieldcatalog TO i_fieldcatalog.

ENDFORM.                    " f411_field_catalog

*---------------------------------------------------------------------*
*       FORM top_of_page                                              *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
FORM top_of_page.

  DATA : wa_header TYPE slis_listheader.

  CLEAR wa_header.
  REFRESH i_header.

  wa_header-typ = 'H'.
  wa_header-info = 'List of Success and Error Records'(007).
  APPEND wa_header TO i_header.

  CALL FUNCTION 'REUSE_ALV_COMMENTARY_WRITE'
    EXPORTING
      it_list_commentary = i_header.

ENDFORM.                    "top_of_page

*&---------------------------------------------------------------------*
*&      Form  f420_build_events
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM f420_build_events.

  DATA : wa_events TYPE slis_alv_event.

  CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
    EXPORTING
      i_list_type = 0
    IMPORTING
      et_events   = i_events.

  READ TABLE i_events WITH KEY name = 'TOP_OF_PAGE'
                                      INTO wa_events.

  wa_events-form = 'TOP_OF_PAGE'.

  MODIFY i_events
  FROM wa_events
  TRANSPORTING form
  WHERE name EQ wa_events-name.

ENDFORM.                    " f420_build_events

*&---------------------------------------------------------------------*
*&      Form  f430_display_alv_report
*&---------------------------------------------------------------------*
*       Display ALV report
*----------------------------------------------------------------------*
FORM f430_display_alv_report .

  DATA: l_repid TYPE sy-repid.

  l_repid = sy-repid.
  SORT i_report BY warpl.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program = l_repid
      it_fieldcat        = i_fieldcatalog
      it_events          = i_events
    TABLES
      t_outtab           = i_report
    EXCEPTIONS
      program_error      = 1
      OTHERS             = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " f430_display_alv_report

*Text symbol text£º
*001:Input File Selection
*002:The file does not contain any records
*003:Maintenance Plan
*004:Call Horizon
*005:Message Type
*006:Message Description
*007:List of Success and Error Records
*008:Call Horizon has been updated successfully
*009:The Maintenance plan does not exist in MPLA table
*010:Call Horizon not updated due to an error while updating database table

*011:Select Input File
*Selection text£º
*P_FILE:        File Path and Name
