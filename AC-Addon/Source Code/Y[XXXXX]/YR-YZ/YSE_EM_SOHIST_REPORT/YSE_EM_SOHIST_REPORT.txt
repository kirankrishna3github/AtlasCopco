************************************************************************
* Program ID            : YSE_ME_SOHIST_REPORT                         *
* Program Title         : EM: Sales History Report                     *
* Author                : Inge Bruyndonckx                             *
* Date                  : 14.11.2006                                   *
* Change Request Number : 141106-SE-OTH-D168-Exception Management      *
* Description           :                                              *
*----------------------------------------------------------------------*
* With the options 'Item/Plant' and 'Item/Responsible plant', columns  *
* 'corrected quantity' and 'corrected # lines' should not be displayed *
* anymore ...                                                          *
************************************************************************
report yse_em_sohist_report no standard page heading
*                           line-size 132.
                            line-size 80.

*----------------------------------------------------------------------*
* Data declarations                                                    *
*----------------------------------------------------------------------*

*--- Dictionary tables
tables: mvke.
TABLES: MARC.
TABLES: MAKT.
tables: sscrfields.
tables: t001w.
tables: tvkwz.
TABLES: TVKOT.
tables: yse_em_matso.
tables: yse_em_plant.
tables: yse_em_pl_distr.
tables: yse_em_sohist.
tables: yse_em_sohistmig.
tables: yse_em_whstypar.
TABLES: YSE_PO_SORG_PORG.
TABLES: YSE_EM_FCHDR.
TABLES: YSE_EM_MATPLANT.

*--- Internal tables
data: begin of i_sohist occurs 0.
        include structure yse_em_sohist.
data: end of i_sohist.

data: begin of i_sohist1 occurs 0,
        period       like yse_em_sohist-period,
        zmeng        like yse_em_sohist-zmeng,
        zline        like yse_em_sohist-zline.
data: end of i_sohist1.

data: begin of i_sohist2 occurs 0,
        period       like yse_em_sohist-period,
        zmeng        like yse_em_sohist-zmeng,
        zline        like yse_em_sohist-zline,
        zmeng_corr   like yse_em_sohist-zmeng,
        zline_corr   like yse_em_sohist-zline.
data: end of i_sohist2.

data: begin of i_rsparams occurs 0.
        include structure rsparams.
data: end of i_rsparams.

data: begin of i_plants occurs 0.
        include structure yse_em_plant.
data: end of i_plants.

data: begin of i_plants_f4 occurs 0,
        mandt        like yse_em_plant-mandt,
        werks        like yse_em_plant-werks,
        name1        like t001w-name1,
        whtype       like yse_em_plant-whtype,
        whstdes      like yse_em_plant-whstdes.
data: end of i_plants_f4.

data: begin of i_branches occurs 0.
        include structure yse_em_pl_centr.
data: end of i_branches.

data: begin of i_marc occurs 0.
        include structure marc.
data: end of i_marc.

data: begin of i_marc_tmp occurs 0.
        include structure marc.
data: end of i_marc_tmp.

data: begin of i_tvkwz occurs 0,
        vkorg      like tvkwz-vkorg,
        vtweg      like tvkwz-vtweg,
        werks      like tvkwz-werks.
data: end of i_tvkwz.

data: begin of i_ddshretval occurs 0.
        include structure ddshretval.
data: end of i_ddshretval.
DATA:
  IT_FCVAL             TYPE YSE_EM_FCVAL     OCCURS 0 WITH HEADER LINE,
  IT_FCMSG             TYPE YSE_EM_FCMSG     OCCURS 0 WITH HEADER LINE.

*--- Structures
data: begin of str_totals,
        zmeng        like yse_em_sohist-zmeng,
        zline        like yse_em_sohist-zline,
        zmeng_corr   like yse_em_sohist-zmeng,
        zline_corr   like yse_em_sohist-zline.
data: end of str_totals.

data: begin of str_whspar.
        include structure yse_em_whstypar.
data: end of str_whspar.

data: begin of str_matsorg.
        include structure yse_em_matso.
data: end of str_matsorg.

*--- Variables
data: v_nfmat        like marc-nfmat.
data: v_matnr        like marc-matnr.
data: v_period       like yse_em_sohist-period.
data: v_period_begin like yse_em_sohist-period.
data: v_period_end   like yse_em_sohist-period.
data: v_run          type c.
data: v_stext(65)    type c.
data: v_text(80)     type c.
data: v_valid        type c.
data: v_wrper(7)     type c.

*--- Ranges
ranges: r_bzirk      for yse_em_pl_distr-bzirk.
ranges: r_werks      for yse_em_pl_distr-werks.


* Screen variables
* ****************
DATA:
  OK_CODE              LIKE SY-UCOMM,
  SV_VKORG             TYPE VKORG,
  SV_VKORG_VTEXT       TYPE VTXTK,
  SV_WERKS             TYPE WERKS_D,
  SV_WERKS_NAME1       TYPE NAME1,
  SV_MATNR             TYPE MATNR,
  SV_MAKTX             TYPE MAKTX,
  SV_EXTYPDESC         TYPE ZEXTYPDESC,
  SV_HIST              TYPE CHAR20,
  SV_CUDIMO            TYPE CHAR20,      "Distribution mode
  SV_ADDIMO            TYPE CHAR20,      "Distribution mode
  SV_CUSTOPO           TYPE CHAR20,      "Stock Policy
  SV_ADSTOPO           TYPE CHAR20,      "Stock Policy
  SV_CULSFL            TYPE TEXT40,      "Lot size flag
  SV_ADLSFL            TYPE TEXT40,      "Lot size flag
  SV_CULOSI            TYPE CHAR15,      "Lot size
  SV_ADLOSI            TYPE CHAR15,      "Lot size
  SV_CUEISBE           TYPE CHAR15,      "Safety Stock
  SV_ADEISBE           TYPE CHAR15,      "Safety Stock
  SV_CUREPO            TYPE CHAR15,      "Reorder point
  SV_ADREPO            TYPE CHAR15,      "Reorder point
  SV_DMAXSVAL          TYPE CHAR20,      "Delta max stock value
  SV_FCDEM             TYPE CHAR20,      "12 month forecast
  SV_LIFNR             LIKE EINA-LIFNR,  "Vendor
  SV_LIFNR_NAME1       LIKE LFA1-NAME1,  "Vendor name
  SV_NRLINES           TYPE ZNRLINES,    "# Lines
  SV_PACK_QTY          TYPE CHAR15,      "Pack qty
  SV_FCV_SUM           TYPE CHAR15,
  SV_FCV01             TYPE CHAR15,
  SV_FCV02             TYPE CHAR15,
  SV_FCV03             TYPE CHAR15,
  SV_FCV04             TYPE CHAR15,
  SV_FCV05             TYPE CHAR15,
  SV_FCV06             TYPE CHAR15,
  SV_FCV07             TYPE CHAR15,
  SV_FCV08             TYPE CHAR15,
  SV_FCV09             TYPE CHAR15,
  SV_FCV10             TYPE CHAR15,
  SV_FCV11             TYPE CHAR15,
  SV_FCV12             TYPE CHAR15,
  SV_FSUMM             TYPE CHAR15,
  SV_GWERT             TYPE CHAR15,
  SV_PER01             TYPE CHAR15,
  SV_PER02             TYPE CHAR15,
  SV_PER03             TYPE CHAR15,
  SV_PER04             TYPE CHAR15,
  SV_PER05             TYPE CHAR15,
  SV_PER06             TYPE CHAR15,
  SV_PER07             TYPE CHAR15,
  SV_PER08             TYPE CHAR15,
  SV_PER09             TYPE CHAR15,
  SV_PER10             TYPE CHAR15,
  SV_PER11             TYPE CHAR15,
  SV_PER12             TYPE CHAR15,
  SV_PRMAD             TYPE CHAR15,
  SV_TWERT             TYPE CHAR15.

* Variables for ALV
* *****************
DATA:
  WA_LAYOUT            TYPE LVC_S_LAYO,
  WA_STABLE            TYPE LVC_S_STBL,
  IT_FIELDCAT          TYPE LVC_T_FCAT,
  OBJ_CUST_CONT_600    TYPE REF TO CL_GUI_CUSTOM_CONTAINER,
  OBJ_ALV_GRID_600     TYPE REF TO CL_GUI_ALV_GRID.


*----------------------------------------------------------------------*
* Selection screen                                                     *
*----------------------------------------------------------------------*
*--- Layout selection screen
selection-screen begin of block b1 with frame title text-s01.
parameters:p_vkorg like yse_em_sohist-vkorg MEMORY ID VKO obligatory.
select-options:s_vtweg for yse_em_sohist-vtweg MEMORY ID VTW.
selection-screen skip.
parameters: p_matnr like yse_em_sohist-matnr obligatory.
parameters: p_bzirk like yse_em_sohist-bzirk.
parameters: p_werks like yse_em_sohist-werks MEMORY ID WRK.
selection-screen end of block b1.

selection-screen begin of block b2 with frame title text-s02.
selection-screen begin of line.
parameters: p_rb1 radiobutton group r1 user-command u1.
selection-screen comment (30) text-p01 for field p_rb1.
selection-screen end of line.
selection-screen begin of line.
parameters: p_rb2 radiobutton group r1.
selection-screen comment (30) text-p02 for field p_rb2.
selection-screen end of line.
selection-screen begin of line.
parameters: p_rb3 radiobutton group r1.
selection-screen comment (30) text-p03 for field p_rb3.
selection-screen end of line.
selection-screen begin of line.
parameters: p_rb4 radiobutton group r1.
selection-screen comment (30) text-p04 for field p_rb4.
selection-screen end of line.
selection-screen end of block b2.

*--- Initialize selection screen
initialization.
  p_rb1 = 'X'.
* loop at screen.
*   if screen-name cs 'P_BZIRK' or screen-name cs 'P_WERKS'.
*     screen-input = '0'.
*     modify screen.
*   endif.
* endloop.

AT USER-COMMAND.
  CASE SY-UCOMM.
    WHEN 'FORECAST'.
      IF P_WERKS IS INITIAL.
        message e107(yse_sales_log).
      ELSE.
        CALL SCREEN 500.
      ENDIF.
  ENDCASE.

*--- Checks on the selection screen
at selection-screen.

  PERFORM check_authorization.

  if sy-ucomm = 'ONLI'.
    if p_rb3 eq 'X' or p_rb4 eq 'X'.
      perform select_plants_to_display.
      if p_werks is initial.
        message e000(yse_sales_log) with text-e01.
      else.
        read table i_plants with key werks = p_werks.
        if sy-subrc ne 0.
          message e000(yse_sales_log) with text-e02.
        endif.
      endif.
    elseif p_rb2 eq 'X'.
      if p_bzirk is initial.
        message e000(yse_sales_log) with text-e03.
      endif.
    endif.
  endif.

at selection-screen output.
  if p_rb1 eq 'X'.
    clear p_bzirk.
    clear p_werks.
    loop at screen.
      if screen-name cs 'P_BZIRK' or screen-name cs 'P_WERKS'.
        screen-input    = '0'.
        modify screen.
      endif.
    endloop.
  elseif p_rb2 eq 'X'.
    clear p_werks.
    loop at screen.
      if screen-name cs 'P_WERKS'.
        screen-input    = '0'.
        modify screen.
      endif.
    endloop.
  elseif p_rb3 eq 'X' or p_rb4 eq 'X'.
    clear p_bzirk.
    loop at screen.
      if screen-name cs 'P_BZIRK'.
        screen-input = '0'.
        modify screen.
      endif.
    endloop.
  endif.

* Fill plants table for limited selection
at selection-screen on value-request for p_werks.
  perform select_plants_to_display.

* Call list with selected plants
  call function 'F4IF_INT_TABLE_VALUE_REQUEST'
    exporting
      value_org       = 'S'
      retfield        = 'WERKS'
      window_title    = text-t07
    tables
      value_tab       = i_plants_f4
      return_tab      = i_ddshretval
    exceptions
      parameter_error = 1
      no_values_found = 2
      others          = 3.
  if sy-subrc eq 0.
    read table i_ddshretval index 1.
    p_werks = i_ddshretval-fieldval.
  endif.



************************************************************************
* START MAIN PROGRAM                                                   *
************************************************************************
start-of-selection.

  perform initialize_data.

  perform select_data.

  perform check_data.

  perform output_data.



************************************************************************
* SUBROUTINES LEVEL 1                                                  *
************************************************************************

*----------------------------------------------------------------------*
*   Form  SELECT_PLANTS_TO_DISPLAY                                     *
*----------------------------------------------------------------------*
*   text                                                               *
*----------------------------------------------------------------------*
form select_plants_to_display.

  clear: i_plants, i_plants_f4.
  refresh: i_plants, i_plants_f4.
  if p_rb4 eq 'X'.
    select * from yse_em_plant into table i_plants
                               where whtype eq 'B'
                                  or whtype eq 'C'.
  else.
    select * from yse_em_plant into table i_plants.
  endif.
  loop at i_plants.
    clear t001w.
    select single * from t001w where werks eq i_plants-werks.
    if sy-subrc eq 0.
      move-corresponding i_plants to i_plants_f4.
      move t001w-name1            to i_plants_f4-name1.
      append i_plants_f4.
    endif.
  endloop.

endform.                    " SELECT_PLANTS_TO_DISPLAY


*----------------------------------------------------------------------*
*   Form  INITIALIZE_DATA                                              *
*----------------------------------------------------------------------*
*   text                                                               *
*----------------------------------------------------------------------*
form initialize_data.

*-- selection of relevant period
  v_period_begin = sy-datum.
  v_period_begin(4) = v_period_begin(4) - 1.

  v_period_end = sy-datum.
  if v_period_end+4(2) eq '01'.
    v_period_end(4)   = v_period_end(4) - 1.
    v_period_end+4(2) = '12'.
  else.
    v_period_end+4(2) = v_period_end+4(2) - 1.
  endif.

*-- selection of sales organisation/plant combinations
  clear: i_tvkwz.
  refresh: i_tvkwz.
  select vkorg vtweg werks from tvkwz into table i_tvkwz
                           where vkorg eq p_vkorg
                             and vtweg in s_vtweg.

*-- selection of relevant plants and branches
  clear: r_bzirk.
  refresh: r_bzirk.
  if p_rb4 eq 'X'.
    clear: yse_em_plant.
    select single * from yse_em_plant where werks eq p_werks.
    if yse_em_plant-whtype eq 'B'.
      perform check_link_plant_salesorg using p_vkorg p_werks.
      if v_valid eq 'X'.
        perform select_districts using p_werks.
      endif.
      perform check_districts.
    else.
      perform check_link_plant_salesorg using p_vkorg p_werks.
      if v_valid eq 'X'.
        perform select_districts using p_werks.
        select * from yse_em_pl_centr into table i_branches
                                      where cwerks eq p_werks.
        loop at i_branches.
          perform check_link_plant_salesorg
                  using p_vkorg i_branches-bwerks.
          if v_valid eq 'X'.
            perform select_districts using i_branches-bwerks.
          endif.
        endloop.
      endif.
      perform check_districts.
    endif.
    sort r_bzirk.
    delete adjacent duplicates from r_bzirk.
  endif.

*-- selection of plant description
  select single * from t001w where werks eq p_werks.

*-- selection of material/plant combinations
  clear: r_werks.
  refresh: r_werks.
  if p_rb3 eq 'X' OR P_RB4 EQ 'X'.
    r_werks-sign   = 'I'.
    r_werks-option = 'EQ'.
    r_werks-low    = p_werks.
    append r_werks.
  endif.
* Select plant materials with status "A1" - "Supercession"
  select * from marc into table i_marc where matnr eq p_matnr
                                         and werks in r_werks
                                         and mmsta eq 'A1'.

*-- find the % to use
  select single * from yse_em_whstypar into str_whspar
                                       where vkorg  eq p_vkorg
                                         and whtype eq 'B'.

*-- find the special field
  select single * from yse_em_matso into str_matsorg
                                    where matnr eq p_matnr
                                      and vkorg eq p_vkorg.

endform.                    " INITIALIZE_DATA


*----------------------------------------------------------------------*
*   Form  SELECT_DATA                                                  *
*----------------------------------------------------------------------*
*   text                                                               *
*----------------------------------------------------------------------*
form select_data.

  clear: str_totals.
  if p_rb1 eq 'X'.
    clear: i_sohist.
    refresh: i_sohist.
    select * from yse_em_sohist into table i_sohist
                                where period ge v_period_begin
                                  and period le v_period_end
                                  and matnr  eq p_matnr
                                  and vkorg  eq p_vkorg
                                  and vtweg  in s_vtweg.
    select * from yse_em_sohistmig where period ge v_period_begin
                                     and period le v_period_end
                                     and matnr  eq p_matnr
                                     and vkorg  eq p_vkorg
                                     and vtweg  in s_vtweg.
      i_sohist = yse_em_sohistmig.
      collect i_sohist.
    endselect.
    describe table i_sohist lines sy-tfill.
    if sy-tfill ne 0.
      sort i_sohist.

      clear: i_sohist1.
      refresh: i_sohist1.
      loop at i_sohist.
        i_sohist1-zmeng = i_sohist1-zmeng + i_sohist-zmeng.
        perform round_value changing i_sohist1-zmeng.
        i_sohist1-zline = i_sohist1-zline + i_sohist-zline.

        at end of period.
          i_sohist1-period = i_sohist-period.
          append i_sohist1.
          str_totals-zmeng = str_totals-zmeng + i_sohist1-zmeng.
          str_totals-zline = str_totals-zline + i_sohist1-zline.
          clear i_sohist1.
        endat.
      endloop.
    endif.
  elseif p_rb2 eq 'X'.
    clear: i_sohist.
    refresh: i_sohist.
    select * from yse_em_sohist into table i_sohist
                                where period ge v_period_begin
                                  and period le v_period_end
                                  and matnr  eq p_matnr
                                  and vkorg  eq p_vkorg
                                  and vtweg  in s_vtweg
                                  and bzirk  eq p_bzirk.
    select * from yse_em_sohistmig where period ge v_period_begin
                                     and period le v_period_end
                                     and matnr  eq p_matnr
                                     and vkorg  eq p_vkorg
                                     and vtweg  in s_vtweg
                                     and bzirk  eq p_bzirk.
      i_sohist = yse_em_sohistmig.
      collect i_sohist.
    endselect.
    describe table i_sohist lines sy-tfill.
    if sy-tfill ne 0.
      sort i_sohist.

      clear: i_sohist1.
      refresh: i_sohist1.
      loop at i_sohist.
        i_sohist1-zmeng = i_sohist1-zmeng + i_sohist-zmeng.
        perform round_value changing i_sohist1-zmeng.
        i_sohist1-zline = i_sohist1-zline + i_sohist-zline.

        at end of period.
          i_sohist1-period = i_sohist-period.
          append i_sohist1.
          str_totals-zmeng = str_totals-zmeng + i_sohist1-zmeng.
          str_totals-zline = str_totals-zline + i_sohist1-zline.
          clear i_sohist1.
        endat.
      endloop.
    endif.
  elseif p_rb3 eq 'X'.
**-- first check if the combination sales organisation - plant is valid

*-- select warehouse type for the selected plant
    select single * from yse_em_plant where werks eq p_werks.
    if sy-subrc ne 0.
*     error-msg => plant not in maintenance table
    endif.

*-- select all lines from yse_em_sohist for selected plant
    clear: i_sohist.
    refresh: i_sohist.
    select * from yse_em_sohist into table i_sohist
                                where period ge v_period_begin
                                  and period le v_period_end
                                  and matnr  eq p_matnr
                                  and vkorg  eq p_vkorg
                                  and vtweg  in s_vtweg
                                  and werks  eq p_werks.
*-- select also all lines from yse_em_sohistmig (migration values)
    select * from yse_em_sohistmig where period ge v_period_begin
                                     and period le v_period_end
                                     and matnr  eq p_matnr
                                     and vkorg  eq p_vkorg
                                     and vtweg  in s_vtweg
                                     and werks  eq p_werks.
      i_sohist = yse_em_sohistmig.
      collect i_sohist.
    endselect.
    describe table i_sohist lines sy-tfill.
    if sy-tfill ne 0.
      sort i_sohist.

      clear: i_sohist2.
      refresh: i_sohist2.
      loop at i_sohist.
        i_sohist2-zmeng = i_sohist2-zmeng + i_sohist-zmeng.
        perform round_value changing i_sohist2-zmeng.
        i_sohist2-zline = i_sohist2-zline + i_sohist-zline.

        at end of period.
          i_sohist2-period = i_sohist-period.
          if yse_em_plant-whtype eq 'B'.
*-- calculate corrected sales quantity
*            IF str_matsorg-spec IS INITIAL.
*              i_sohist2-zmeng = i_sohist2-zmeng
*                                     * str_whspar-brkperc.
*              i_sohist2-zline = i_sohist2-zline
*                                     * str_whspar-brkperc.
*            ELSE.
*
*              i_sohist2-zmeng      = i_sohist2-zmeng
*                                     * str_whspar-altperc.
*              i_sohist2-zline = i_sohist2-zline
*                                     * str_whspar-altperc.
*            ENDIF.
          endif.
          append i_sohist2.
          str_totals-zmeng      = str_totals-zmeng
                                  + i_sohist2-zmeng.
          str_totals-zline      = str_totals-zline
                                  + i_sohist2-zline.
          str_totals-zmeng_corr = str_totals-zmeng_corr
                                  + i_sohist2-zmeng_corr.
          str_totals-zline_corr = str_totals-zline_corr
                                  + i_sohist2-zline_corr.
          clear i_sohist2.
        endat.
      endloop.
    endif.
  elseif p_rb4 eq 'X'.
*-- select all lines from yse_em_sohist for selected plant
    clear: i_sohist.
    refresh: i_sohist.
    select * from yse_em_sohist into table i_sohist
                                where period ge v_period_begin
                                  and period le v_period_end
                                  and matnr  eq p_matnr
                                  and vkorg  eq p_vkorg
                                  and vtweg  in s_vtweg
                                  and bzirk  in r_bzirk.
    select * from yse_em_sohistmig where period ge v_period_begin
                                     and period le v_period_end
                                     and matnr  eq p_matnr
                                     and vkorg  eq p_vkorg
                                     and vtweg  in s_vtweg
                                     and bzirk  in r_bzirk.
      i_sohist = yse_em_sohistmig.
      collect i_sohist.
    endselect.
    describe table i_sohist lines sy-tfill.
    if sy-tfill ne 0.
      sort i_sohist.

      clear: i_sohist2.
      refresh: i_sohist2.
      loop at i_sohist.
        i_sohist2-zmeng = i_sohist2-zmeng + i_sohist-zmeng.
        perform round_value changing i_sohist2-zmeng.
        i_sohist2-zline = i_sohist2-zline + i_sohist-zline.

        at end of period.
          i_sohist2-period = i_sohist-period.
          if yse_em_plant-whtype eq 'B'.
*-- calculate corrected sales quantity
*            IF str_matsorg-spec IS INITIAL.
*              i_sohist2-zmeng = i_sohist2-zmeng
*                                     * str_whspar-brkperc.
*              i_sohist2-zline = i_sohist2-zline
*                                     * str_whspar-brkperc.
*            ELSE.
*              i_sohist2-zmeng      = i_sohist2-zmeng
*                                     * str_whspar-altperc.
*              i_sohist2-zline = i_sohist2-zline
*                                     * str_whspar-altperc.
*            ENDIF.
          endif.
          append i_sohist2.
          str_totals-zmeng      = str_totals-zmeng
                                  + i_sohist2-zmeng.
          str_totals-zline      = str_totals-zline
                                  + i_sohist2-zline.
          str_totals-zmeng_corr = str_totals-zmeng_corr
                                  + i_sohist2-zmeng_corr.
          str_totals-zline_corr = str_totals-zline_corr
                                  + i_sohist2-zline_corr.
          clear i_sohist2.
        endat.
      endloop.
    endif.
  endif.

endform.                    " SELECT_DATA


*----------------------------------------------------------------------*
*   Form  CHECK_DATA                                                   *
*----------------------------------------------------------------------*
*   text                                                               *
*----------------------------------------------------------------------*
form check_data.

  v_period = v_period_begin.
  do 12 times.
    if p_rb1 eq 'X' or p_rb2 eq 'X'.
      read table i_sohist1 with key period = v_period.
      if sy-subrc ne 0.
        clear i_sohist1.
        i_sohist1-period = v_period.
        append i_sohist1.
      endif.
    else.
      read table i_sohist2 with key period = v_period.
      if sy-subrc ne 0.
        clear i_sohist2.
        i_sohist2-period = v_period.
        append i_sohist2.
      endif.
    endif.
    if v_period+4(2) ne '12'.
      v_period+4(2) = v_period+4(2) + 1.
    else.
      v_period(4)   = v_period(4) + 1.
      v_period+4(2) = '01'.
    endif.
  enddo.

endform.                    " CHECK_DATA


*----------------------------------------------------------------------*
*   Form  OUTPUT_DATA                                                  *
*----------------------------------------------------------------------*
*   text                                                               *
*----------------------------------------------------------------------*
form output_data.

  SET PF-STATUS 'SOHIST'.

  perform write_header.
  if p_rb1 eq 'X' or p_rb2 eq 'X'.
    sort i_sohist1.
    loop at i_sohist1.
      clear v_wrper.
      concatenate i_sohist1(4) '/' i_sohist1+4(2) into v_wrper.
      write: /   sy-vline,
              02 v_wrper,
              20 i_sohist1-zmeng decimals 0,
              50 i_sohist1-zline,
*            132 sy-vline.
              80 sy-vline.
    endloop.
  elseif p_rb3 eq 'X' or p_rb4 eq 'X'.
    sort i_sohist2.
    loop at i_sohist2.
      clear v_wrper.
      concatenate i_sohist2(4) '/' i_sohist2+4(2) into v_wrper.
      write: /   sy-vline,
              02 v_wrper,
              20 i_sohist2-zmeng decimals 0,
              50 i_sohist2-zline,
*             80 i_sohist2-zmeng_corr,
*            116 i_sohist2-zline_corr,
*            132 sy-vline.
              80 sy-vline.
    endloop.
  endif.
  perform write_totals.
  uline.

endform.                    " OUTPUT_DATA



************************************************************************
* SUBROUTINES LEVEL 2                                                  *
************************************************************************

*----------------------------------------------------------------------*
*   Form  CHECK_LINK_PLANT_SALESORG                                    *
*----------------------------------------------------------------------*
*   text                                                               *
*----------------------------------------------------------------------*
form check_link_plant_salesorg using salesorg plant.

  if plant cp 'Z*'.
    v_valid = 'X'.
  else.
    read table i_tvkwz with key vkorg = salesorg
                                werks = plant.
    if sy-subrc eq 0.
      v_valid = 'X'.
    else.
      clear v_valid.
    endif.
  endif.
endform.                    " CHECK_LINK_PLANT_SALESORG


*----------------------------------------------------------------------*
*   Form  SELECT_DISTRICTS                                             *
*----------------------------------------------------------------------*
*   text                                                               *
*----------------------------------------------------------------------*
form select_districts using plant.

  select * from yse_em_pl_distr where werks eq plant.
    clear: r_bzirk.
    r_bzirk-sign   = 'I'.
    r_bzirk-option = 'EQ'.
    r_bzirk-low    = yse_em_pl_distr-bzirk.
    append r_bzirk.
  endselect.

endform.                    " SELECT_DISTRICTS


*----------------------------------------------------------------------*
*   Form  CHECK_DISTRICTS                                              *
*----------------------------------------------------------------------*
*   text                                                               *
*----------------------------------------------------------------------*
form check_districts.

  if r_bzirk[] is initial.
    clear: r_bzirk.
    r_bzirk-sign   = 'I'.
    r_bzirk-option = 'EQ'.
    r_bzirk-low    = 'ZZZZZZ'.
    append r_bzirk.
  endif.

endform.                    " SELECT_DISTRICTS


*----------------------------------------------------------------------*
*   Form  ROUND_VALUE                                                  *
*----------------------------------------------------------------------*
*   text                                                               *
*----------------------------------------------------------------------*
form round_value changing number.

  call function 'ROUND'
    exporting
      decimals      = 0
      input         = number
*     sign          = ' '
    importing
      output        = number
    exceptions
      input_invalid = 1
      overflow      = 2
      type_invalid  = 3
      others        = 4.
  if sy-subrc <> 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  endif.

endform.                    " ROUND_VALUE


*----------------------------------------------------------------------*
*   Form  WRITE_HEADER                                                 *
*----------------------------------------------------------------------*
*   text                                                               *
*----------------------------------------------------------------------*
form write_header.

  DATA:
    LV_MARC,
    LV_DBCNT TYPE I,
    LV_NAME1 LIKE LFA1-NAME1,
    LA_EORD  TYPE EORD,
    LA_EINA  TYPE EINA,
    LA_EINE  TYPE EINE.

  uline.
  format color 1.
  write: /   sy-vline,
          02 text-h01,
*        132 sy-vline.
          80 sy-vline.
  format color off.
  uline.
*-- selection parameters
*  call function 'RS_REFRESH_FROM_SELECTOPTIONS'
*    exporting
*      curr_report           = sy-cprog
**   importing
**     sp                    =
*    tables
*      selection_table       = i_rsparams.
**   exceptions
**     not_found             = 1
**     no_report             = 2
**     others                = 3
*  if sy-subrc <> 0.
**   message id sy-msgid type sy-msgty number sy-msgno
**           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
*  endif.
*
*-- sales organisation
*  write: /   sy-vline,
*          02 'SELECTION',
*         132 sy-vline.
*  write: /   sy-vline,
*          02 'Sales Organisation', p_vkorg,
*         132 sy-vline.
*--
  if p_rb1 eq 'X'.
    concatenate text-t01 text-p01 into v_text.
  elseif p_rb2 eq 'X'.
    concatenate text-t01 text-p02 into v_text.
  elseif p_rb3 eq 'X'.
    concatenate text-t01 text-p03 into v_text.
  elseif p_rb4 eq 'X'.
    concatenate text-t01 text-p04 into v_text.
  endif.
  condense v_text.
  write: /   sy-vline,
*         03 v_text,
          03 v_text(75),
*        132 sy-vline.
          80 sy-vline.
  write: /   sy-vline,
*        132 sy-vline.
          80 sy-vline.
*-- all selections
  write: /   sy-vline,
          03 text-t02,
          25 p_vkorg,
*        132 sy-vline.
          80 sy-vline.

  loop at s_vtweg.
    write: /   sy-vline.
    if sy-tabix eq 1.
      write: 03 text-t03,
             25 s_vtweg-option, s_vtweg-low.
    else.
      write: 25 s_vtweg-option, s_vtweg-low.
    endif.
    if not s_vtweg-high is initial.
      write: '-', s_vtweg-high.
    endif.
    if s_vtweg-sign eq 'I'.
      write: ' (incl.)'.
    else.
      write: ' (excl.)'.
    endif.
*   write: 132 sy-vline.
    write:  80 sy-vline.
  endloop.
  if s_vtweg[] is initial.
    write: /   sy-vline,
            03 text-t03,
*          132 sy-vline.
            80 sy-vline.
  endif.

  write: /   sy-vline,
          03 text-t04,
          25 p_matnr,
*        132 sy-vline.
          80 sy-vline.

  if p_rb2 eq 'X' or p_rb4 eq 'X'.
    write: /   sy-vline,
            03 text-t05,
            25 p_bzirk,
*          132 sy-vline.
            80 sy-vline.
  endif.

  if p_rb3 eq 'X'.
    write: /   sy-vline,
            03 text-t06,
            25 p_werks,
            30 '-', t001w-name1,
*          132 sy-vline.
            80 sy-vline.
  endif.

  if p_rb4 eq 'X'.
    write: /   sy-vline,
            03 text-t07,
            25 p_werks,
            30 '-', t001w-name1,
*          132 sy-vline.
            80 sy-vline.
  endif.
  uline.

*-- superseded materials
  clear: v_run.
  i_marc_tmp[] = i_marc[].
  loop at i_marc where matnr eq p_matnr
                   and werks in r_werks.
    loop at i_tvkwz where vkorg eq p_vkorg
                      and vtweg in s_vtweg
                      and werks eq i_marc-werks.
      v_nfmat = i_marc-matnr.
      do.
        if not v_nfmat is initial.
          v_matnr = v_nfmat.
          read table i_marc_tmp with key matnr = v_nfmat
                                         werks = i_marc-werks.
          if sy-subrc eq 0.
            v_nfmat = i_marc_tmp-nfmat.
          else.
            clear v_nfmat.
          endif.
        else.
          exit.
        endif.
      enddo.
      v_run = 'X'.
      exit.
    endloop.
    if v_run eq 'X'.
      exit.
    endif.
  endloop.
  if p_matnr ne v_matnr and
     not v_matnr is initial.
    concatenate text-t09 v_matnr
                text-t10 p_vkorg
                into v_stext separated by space.
    condense v_stext.
    write: /   sy-vline,
            03 text-t08,
            12 v_stext,
            80 sy-vline.
    uline.
  endif.

* Header detail
  format color 2 intensified off.
* Map VKORG (SOrg) to EKORG (POrg)
  CLEAR YSE_PO_SORG_PORG.
  SELECT SINGLE * FROM YSE_PO_SORG_PORG
                 WHERE VKORG EQ P_VKORG.

* Only if plant / responsible plant is chosen
  IF P_RB3 NE SPACE OR P_RB4 NE SPACE.
*   Select plant view and print header detail --> only if plant / responsible plant is chosen
    SELECT SINGLE * FROM MARC
                   WHERE MATNR EQ P_MATNR
                     AND WERKS IN R_WERKS.
    IF SY-SUBRC EQ 0.
      LV_MARC = 'X'.
*     If more then 1 plant view found, inform user from which
*     plant following detail is sourced
      SELECT COUNT(*) FROM MARC
                      INTO LV_DBCNT
               WHERE MATNR EQ P_MATNR
                 AND WERKS IN R_WERKS.
      IF LV_DBCNT GT 1.
        WRITE:
          /    SY-VLINE,
            02 TEXT-H08,
            30 MARC-WERKS,
            80 SY-VLINE.
      ENDIF.
    ELSE.
      LV_MARC = SPACE.
    ENDIF.

*  -- Stock policy --> only if plant / responsible plant is chosen
    WRITE: /   SY-VLINE,
            02 TEXT-H07.
    IF NOT LV_MARC IS INITIAL.
      CASE MARC-DISMM.
        WHEN 'Z5'.
          WRITE: 30 TEXT-D04.
        WHEN 'Z6' OR 'Z7'.
          WRITE: 30 TEXT-D05.
        WHEN 'ND'.
          WRITE: 30 TEXT-D06.
        WHEN OTHERS.
          WRITE: 30 SPACE.
      ENDCASE.
    ENDIF.
    WRITE:  80 SY-VLINE.
  ENDIF.

*-- distribution mode
  select single * from mvke
                  where matnr eq p_matnr
                    and vkorg eq p_vkorg
                    and vtweg eq '01'.
  write: /   sy-vline,
          02 text-h02.
* set distribution mode description
  if mvke-mtpos eq 'NORM' and mvke-mvgr4 is initial.
    write:  30 text-d01.
  elseif mvke-mtpos eq 'NORM' and mvke-mvgr4 eq 'LCL'.
    write:  30 text-d02.
  elseif mvke-mtpos eq 'ZDTC'.
    write:  30 text-d03.
  else.
    write:  30 mvke-mtpos.
  endif.
*        132 sy-vline.
  write:  80 sy-vline.

* only if plant / responsible plant is chosen
  IF P_RB3 NE SPACE OR P_RB4 NE SPACE.
*  -- Stock on request
    WRITE: /   SY-VLINE,
            02 TEXT-H09.
    IF NOT LV_MARC IS INITIAL.
      IF MARC-MAXLZ EQ 1.
        WRITE  30 TEXT-D07.
      ELSE.
        WRITE  30 TEXT-D08.
      ENDIF.
    ENDIF.
    WRITE: 80 SY-VLINE.

*  -- Safety stock
    WRITE: /   SY-VLINE,
            02 TEXT-H10.
    IF NOT LV_MARC IS INITIAL.
      WRITE  30 MARC-EISBE.
    ENDIF.
    WRITE: 80 SY-VLINE.

*  -- ROP
    WRITE: /   SY-VLINE,
            02 TEXT-H11.
    IF NOT LV_MARC IS INITIAL.
      WRITE  30 MARC-MINBE.
    ENDIF.
    WRITE: 80 SY-VLINE.

*  -- Lot size flag
    CLEAR YSE_EM_MATPLANT.
    SELECT SINGLE * FROM YSE_EM_MATPLANT
          WHERE MATNR EQ P_MATNR
            AND WERKS EQ P_WERKS.
    WRITE: /   SY-VLINE,
            02 TEXT-H15.
    CASE YSE_EM_MATPLANT-LOTSIZE_IND.
      WHEN SPACE.
        WRITE:
            30 'EOQ',
            80 SY-VLINE.
      WHEN 'S'.
        WRITE:
            30 'Time based',
            80 SY-VLINE.
      WHEN 'M'.
        WRITE:
            30 'Manual override',
            80 SY-VLINE.
    ENDCASE.

*  -- Lot size
    WRITE: /   SY-VLINE,
            02 TEXT-H12.
    IF NOT LV_MARC IS INITIAL.
      WRITE  30 MARC-BSTFE.
    ENDIF.
    WRITE: 80 SY-VLINE.

*  -- Get default vendor
    SELECT SINGLE * FROM EORD INTO LA_EORD
           WHERE MATNR EQ P_MATNR
             AND WERKS EQ P_WERKS
             AND FLIFN NE SPACE
             AND EKORG EQ YSE_PO_SORG_PORG-EKORG.

*   If vendor found
    IF SY-SUBRC EQ 0 AND NOT LA_EORD-LIFNR IS INITIAL.
*     Get Purch Inforecord for material/vendor
      SELECT SINGLE * FROM EINA INTO LA_EINA
             WHERE MATNR EQ P_MATNR
               AND LIFNR EQ LA_EORD-LIFNR
               AND LOEKZ EQ SPACE.
      IF SY-SUBRC EQ 0.
*       Get lead time
        SELECT SINGLE * FROM EINE INTO LA_EINE
               WHERE INFNR EQ LA_EINA-INFNR
                 AND EKORG EQ LA_EORD-EKORG
                 AND WERKS EQ P_WERKS
                 AND LOEKZ EQ SPACE.

        WRITE: /   SY-VLINE,
                02 TEXT-H13,
                30 LA_EINE-APLFZ,
                80 SY-VLINE.

      ENDIF.

*     Get vendor name
      SELECT SINGLE NAME1 FROM LFA1 INTO LV_NAME1
             WHERE LIFNR EQ LA_EORD-LIFNR.
      WRITE: /   SY-VLINE,
              02 TEXT-H14,
              30 LA_EORD-LIFNR, LV_NAME1,
              80 SY-VLINE.

    ENDIF.


  ENDIF.

  format color off.
  uline.
  if p_rb1 eq 'X' or p_rb2 eq 'X'.
    write: /   sy-vline,
            20 text-h03 right-justified,
            50 text-h04,
*          132 sy-vline.
            80 sy-vline.
  else.
    write: /   sy-vline,
            20 text-h03 right-justified,
            50 text-h04,
*           80 text-h05 right-justified,
*          110 text-h06,
*          132 sy-vline.
            80 sy-vline.
  endif.
  uline.

endform.                    " WRITE_HEADER


*----------------------------------------------------------------------*
*   Form  WRITE_TOTALS                                                 *
*----------------------------------------------------------------------*
*   text                                                               *
*----------------------------------------------------------------------*
form write_totals.

  if p_rb1 eq 'X' or p_rb2 eq 'X'.
    write: / sy-vline.
    uline at 20(18).
    uline at 50(08).
*   write: 132 sy-vline.
    write:  80 sy-vline.
    write: /   sy-vline,
            20 str_totals-zmeng decimals 0,
            50 str_totals-zline,
*          132 sy-vline.
            80 sy-vline.
  else.
    write: / sy-vline.
    uline at  20(18).
    uline at  50(08).
*   uline at  80(18).
*   uline at 110(14).
*   write: 132 sy-vline.
    write: 80 sy-vline.
    write: /   sy-vline,
            20 str_totals-zmeng decimals 0,
            50 str_totals-zline,
*           80 str_totals-zmeng_corr,
*          116 str_totals-zline_corr,
*          132 sy-vline.
            80 sy-vline.
  endif.

endform.                    " WRITE_TOTALS
*&---------------------------------------------------------------------*
*&      Form  check_authorization
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form check_authorization .

  AUTHORITY-CHECK OBJECT 'V_VBAK_VKO'
                      ID 'VKORG' FIELD p_vkorg
                      ID 'VTWEG' DUMMY
                      ID 'SPART' DUMMY
                      ID 'ACTVT' DUMMY.

  IF sy-subrc = 4.
*   No authorisation to display data from Sales Organisation p_vkorg
    MESSAGE ID 'YSE_GENERAL' type 'E' number '006' WITH p_vkorg .
  ELSEIF sy-subrc <> 0.
*   Error checking authorization.
    MESSAGE ID 'YSE_RENTAL' TYPE 'E' NUMBER '046'.
  ENDIF.

endform.                    " check_authorization


*&---------------------------------------------------------------------*
*&      MODULE  STATUS_0500  OUTPUT
*&---------------------------------------------------------------------*
MODULE STATUS_0500 OUTPUT.

  SET PF-STATUS '500'.
  SET TITLEBAR  '500'.

ENDMODULE.                 " STATUS_0500  OUTPUT

*&---------------------------------------------------------------------*
*&      MODULE  PREPARE_FLD_500  OUTPUT
*&---------------------------------------------------------------------*
MODULE PREPARE_FLD_500 OUTPUT.

  FIELD-SYMBOLS:
    <FIELD>.

  DATA:
    LV_FIELDNAME TYPE STRING,
    LV_ROWNR     TYPE NUMC2,
    LV_TOTAL     TYPE I,
    LV_VALUE     TYPE I.

* Get VKORG etc...
  SELECT SINGLE *
           FROM TVKOT
          WHERE VKORG EQ P_VKORG
            AND SPRAS EQ 'EN'.
  IF SY-SUBRC EQ 0.
    SV_VKORG_VTEXT = TVKOT-VTEXT.
    SV_VKORG       = TVKOT-VKORG.
  ENDIF.
  SELECT SINGLE *
           FROM T001W
          WHERE WERKS EQ P_WERKS.
  IF SY-SUBRC EQ 0.
    SV_WERKS_NAME1 = T001W-NAME1.
    SV_WERKS       = T001W-WERKS.
  ENDIF.
  SELECT SINGLE *
           FROM MAKT
          WHERE MATNR EQ P_MATNR
            AND SPRAS EQ 'EN'.
  IF SY-SUBRC EQ 0.
    SV_MAKTX = MAKT-MAKTX.
    SV_MATNR = MAKT-MATNR.
  ENDIF.

* Clear target structures
  CLEAR:
    YSE_EM_FCHDR,
    IT_FCVAL,
    IT_FCVAL[].

* Get forecast header values
  SELECT SINGLE *
           FROM YSE_EM_FCHDR
          WHERE MATNR EQ P_MATNR
            AND WERKS EQ P_WERKS.

* Get forecast detail values
  SELECT      *
         FROM YSE_EM_FCVAL
         INTO TABLE IT_FCVAL
        WHERE MATNR EQ P_MATNR
          AND WERKS EQ P_WERKS.

* Move forecast header values to screen variables
  SV_GWERT = YSE_EM_FCHDR-GWERT.
  SV_TWERT = YSE_EM_FCHDR-TWERT.
  SV_PRMAD = YSE_EM_FCHDR-PRMAD.
  SV_FSUMM = YSE_EM_FCHDR-FSUMM.

  SHIFT SV_GWERT LEFT DELETING LEADING SPACE.
  SHIFT SV_TWERT LEFT DELETING LEADING SPACE.
  SHIFT SV_PRMAD LEFT DELETING LEADING SPACE.
  SHIFT SV_FSUMM LEFT DELETING LEADING SPACE.
  REPLACE '.000' WITH SPACE INTO SV_GWERT.
  REPLACE '.000' WITH SPACE INTO SV_TWERT.
  REPLACE '.000' WITH SPACE INTO SV_PRMAD.
  REPLACE '.000' WITH SPACE INTO SV_FSUMM.
  TRANSLATE SV_GWERT USING '.,'.
  TRANSLATE SV_TWERT USING '.,'.
  TRANSLATE SV_PRMAD USING '.,'.
  TRANSLATE SV_FSUMM USING '.,'.

* Move forecast detail values to screen variables
  CLEAR LV_TOTAL.
  LOOP AT IT_FCVAL.

*   There are only 12 output lines, so exit when exceeding that number of lines
    IF SY-TABIX GT 12.
      EXIT.
    ENDIF.

*   Build fieldname of period field to be filled
    LV_ROWNR = SY-TABIX.
    CONCATENATE 'SV_PER' LV_ROWNR INTO LV_FIELDNAME.
    ASSIGN (LV_FIELDNAME) TO <FIELD>.
    IF SY-SUBRC NE 0. EXIT. ENDIF.
    <FIELD> = IT_FCVAL-PERNA.

*   Build fieldname of period field to be filled
    CONCATENATE 'SV_FCV' LV_ROWNR INTO LV_FIELDNAME.
    ASSIGN (LV_FIELDNAME) TO <FIELD>.
    IF SY-SUBRC NE 0. EXIT. ENDIF.
    LV_VALUE = IT_FCVAL-OWERT.

    <FIELD> = LV_VALUE.

*   Update total
    LV_TOTAL = LV_TOTAL + IT_FCVAL-OWERT.

  ENDLOOP.

  SV_FCV_SUM = LV_TOTAL.

ENDMODULE.                 " PREPARE_FLD_500  OUTPUT

*&---------------------------------------------------------------------*
*&      MODULE  EXIT_500  INPUT
*&---------------------------------------------------------------------*
MODULE EXIT_500 INPUT.

  CASE OK_CODE.
    WHEN 'BACK' OR 'CLOSE'.
      LEAVE TO SCREEN 0.
    WHEN 'EXIT'.
*     Go back to first selection screen
      SUBMIT YSE_EM_EVALUATION_EXCEPTIONS VIA SELECTION-SCREEN  .
    WHEN 'CANCEL'.
      LEAVE PROGRAM.
  ENDCASE.
ENDMODULE.                 " EXIT_500  INPUT

*&---------------------------------------------------------------------*
*&      MODULE  USER_COMMAND_0500  INPUT
*&---------------------------------------------------------------------*
MODULE USER_COMMAND_0500 INPUT.

  CASE OK_CODE.

    WHEN 'MSGS'.
      CALL SCREEN 600 STARTING AT 1 1 ENDING AT 70 12.
    WHEN OTHERS.
      LEAVE TO SCREEN 0.

  ENDCASE.

ENDMODULE.                 " USER_COMMAND_0500  INPUT

*&---------------------------------------------------------------------*
*&      MODULE  STATUS_0600  OUTPUT
*&---------------------------------------------------------------------*
MODULE STATUS_0600 OUTPUT.

  SET PF-STATUS '600'.
  SET TITLEBAR  '600'.

ENDMODULE.                 " STATUS_0600  OUTPUT

*&---------------------------------------------------------------------*
*&      MODULE  PREPARE_ALV_600  OUTPUT
*&---------------------------------------------------------------------*
MODULE PREPARE_ALV_600 OUTPUT.

  PERFORM PREPARE_ALV_600.

ENDMODULE.                 " PREPARE_ALV_600  OUTPUT

*&---------------------------------------------------------------------*
*&      FORM  PREPARE_ALV_600
*&---------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
FORM PREPARE_ALV_600 .

  PERFORM GET_FCMSG.

  IF OBJ_CUST_CONT_600 IS INITIAL.
    IF CL_GUI_ALV_GRID=>OFFLINE( ) IS INITIAL.
      CREATE OBJECT OBJ_CUST_CONT_600
        EXPORTING
           CONTAINER_NAME              = 'CONTAINER_600'.
    ENDIF.

    CREATE OBJECT OBJ_ALV_GRID_600
      EXPORTING
        I_PARENT          = OBJ_CUST_CONT_600.

    PERFORM BUILD_FIELDCATALOG_600.

*   No toolbar for ALV grid
    WA_LAYOUT-NO_TOOLBAR = 'X'.
    WA_LAYOUT-CWIDTH_OPT = 'X'.

    CALL METHOD OBJ_ALV_GRID_600->SET_TABLE_FOR_FIRST_DISPLAY
      EXPORTING
        IS_LAYOUT                     = WA_LAYOUT
      CHANGING
        IT_OUTTAB                     = IT_FCMSG[]
        IT_FIELDCATALOG               = IT_FIELDCAT
      EXCEPTIONS
        INVALID_PARAMETER_COMBINATION = 1
        PROGRAM_ERROR                 = 2
        TOO_MANY_LINES                = 3
        OTHERS                        = 4.
  ELSE.

    WA_STABLE-ROW = 'X'.
    WA_STABLE-COL = 'X'.

    CALL METHOD OBJ_ALV_GRID_600->REFRESH_TABLE_DISPLAY
      EXPORTING
        IS_STABLE      = WA_STABLE
        I_SOFT_REFRESH = 'X'.
  ENDIF.

ENDFORM.                    " PREPARE_ALV_600

*&---------------------------------------------------------------------*
*&      FORM  BUILD_FIELDCATALOG_600
*&---------------------------------------------------------------------*
FORM BUILD_FIELDCATALOG_600 .

  FIELD-SYMBOLS:
    <FIELDCAT> TYPE LVC_S_FCAT.

  REFRESH IT_FIELDCAT.

  CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
   EXPORTING
*   I_BUFFER_ACTIVE              = 'X'
    I_STRUCTURE_NAME             = 'YSE_EM_FCMSG'
*   I_CLIENT_NEVER_DISPLAY       = 'X'
*   I_BYPASSING_BUFFER           =
    CHANGING
      CT_FIELDCAT                  = IT_FIELDCAT.

  LOOP AT IT_FIELDCAT ASSIGNING <FIELDCAT>.

    CASE <FIELDCAT>-FIELDNAME.
      WHEN  'MANDT' OR 'MATNR' OR 'WERKS' OR 'POSN2'.
        <FIELDCAT>-NO_OUT = 'X'.

    ENDCASE.

  ENDLOOP.

ENDFORM.                    " BUILD_FIELDCATALOG_600


*&---------------------------------------------------------------------*
*&      Form  GET_FCMSG
*&---------------------------------------------------------------------*
FORM GET_FCMSG .

  CLEAR:
    IT_FCMSG,
    IT_FCMSG[].

  SELECT      *
         FROM YSE_EM_FCMSG
         INTO TABLE IT_FCMSG
        WHERE MATNR = P_MATNR
          AND WERKS = P_WERKS.

ENDFORM.                    " GET_FCMSG

*&---------------------------------------------------------------------*
*&      MODULE  EXIT_600  INPUT
*&---------------------------------------------------------------------*
MODULE EXIT_600 INPUT.

  CASE SY-UCOMM.
    WHEN 'CANCEL'.
      LEAVE TO SCREEN 0.
  ENDCASE.
ENDMODULE.                 " EXIT_600  INPUT

*Text symbol text£º
*D01:NDTC
*D02:LCL
*D03:DTC
*D04:Stocked
*D05:Not stocked
*D06:Not planned
*D07:Yes
*D08:No
*E01:Make an entry in all required fields ( plant )
*E02:Entered plant is not allowed or does not exist
*E03:Sales district must have a value !
*H01:HISTORICAL DATA
*H02:Distribution mode :
*H03:Quantity
*H04:# lines
*H05:Corr. quantity
*H06:Corr. # lines
*H07:Stock policy :
*H08:Plant data for :
*H09:Stock on request :
*H10:Safety stock :
*H11:ROP :
*H12:Lot size :
*H13:Lead time :
*H14:Vendor :
*H15:Lot size flag :
*L01:
*P01:     Item
*P02:     Item / Sales district
*P03:     Item / Plant
*P04:     Item / Responsible plant
*S01:Primary selections
*S02:Secondary selections
*T01:Selections by
*T02:Sales organisation
*T03:Distribution channel
*T04:Material
*T05:Sales district
*T06:Plant
*T07:Responsible plant
*T08:Remark :
*T09:Material is superseded by
*T10:in sales org.

*T11:Plants
*Selection text£º
*P_BZIRK:D       Sales district
*P_MATNR:D       Material
*P_RB1:        Item
*P_RB2:        Item / Sales district
*P_RB3:        Item / Plant
*P_RB4:        Item / Responsible plant
*P_VKORG:D       Sales Organization
*P_WERKS:D       Plant
*S_VTWEG:D       Distribution Channel
