*&---------------------------------------------------------------------*
*& Report  YSE_FICO_RRB_REPOSTI_LOAD
*&
*&---------------------------------------------------------------------*
*&
*& Load repostings to new version table (from Excel file)
*&
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 16.08.2012
*  Change Request Number : CR2568
*  Transport request Nr. : CD1K972931
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.| DATE       | NAME          | CORRECTION NR.| CHANGE REF. # *
*----------------------------------------------------------------------*
* MOD-001 | xx/xx/xxxx | X             | CD1K9xxxxx    | CRxxxx        *
*         | Text                                                       *
*----------------------------------------------------------------------*

REPORT  yse_fico_rrb_reposti_load.

TABLES: yse_rrb_reposti.

* Internal tables
DATA: BEGIN OF gt_repin  OCCURS 0,
        line(250)   TYPE c,
      END OF gt_repin.

DATA: BEGIN OF gt_rep  OCCURS 0,
        bukrs             TYPE bukrs,
        invoice           TYPE vbeln_vf,
        item              TYPE posnr_vf,
        erdatc(10)        TYPE c,
        erzetc(8)         TYPE c,
        ernam             TYPE ernam,
        dmr               TYPE vbeln_va,
        dmr_item          TYPE posnr_va,
        sales_order       TYPE vbeln_va,
        sales_item        TYPE posnr_va,
        reposted          TYPE y_reposted,
        obj_key           TYPE awkey,
        obj_typ           TYPE awtyp,
        obj_sys           TYPE awsys,
        gjahr             TYPE gjahr,
        bus_act           TYPE glvor,
        redatc(10)        TYPE c,
        rezetc(8)         TYPE c,
        cancel_doc        TYPE z_canc_vbeln_vf,
        cancel_item       TYPE z_canc_posnr_vf,
        cancel_datec(10)  TYPE c,
        cancel_timec(8)   TYPE c,
        cancel_name       TYPE z_canc_name,
        repost_canceled   TYPE y_rep_cancel,
        obj_key_rev       TYPE zbelnr_d_rev,
        no_repost_req     TYPE y_no_repost,
      END OF gt_rep.

DATA: gt_rrb_reposti  TYPE TABLE OF yse_rrb_reposti
                           WITH HEADER LINE.

DATA: gv_invoice   TYPE vbeln_vf,
      gv_count(4)  TYPE p  DECIMALS 0.


*&---------------------------------------------------------------------*
*&  Selection screen                                                   *
*&---------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK b1  WITH FRAME  TITLE text-b01.
* Input file
PARAMETERS: p_infile TYPE  rlgrap-filename LOWER CASE.
*                 DEFAULT 'C:\REPOSTINGS.xls'.
SELECTION-SCREEN SKIP.
PARAMETERS: p_exist AS CHECKBOX  DEFAULT 'X'.
SELECTION-SCREEN END OF BLOCK b1.


AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_infile.

* Get the filename
  PERFORM get_filename USING p_infile.



*-------------------------------------------------------------------------------*
*  START-OF-SELECTION                                                           *
*-------------------------------------------------------------------------------*
START-OF-SELECTION.

* Read input file
  PERFORM read_file.

* Process data
  PERFORM process_data.

* Totals
  WRITE: / 'Entries loaded :'(i01), gv_count.
  ULINE.

*&---------------------------------------------------------------------*
*&  Forms                                                              *
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  GET_FILENAME
*&---------------------------------------------------------------------*
*       Get the filename
*----------------------------------------------------------------------*
*      -->P_INFILE : Input file
*----------------------------------------------------------------------*
FORM get_filename  USING    p_infile.

  CALL FUNCTION 'WS_FILENAME_GET'
    EXPORTING
      def_path         = 'C:\'
      mask             = ',*.XLS.'
    IMPORTING
      filename         = p_infile
    EXCEPTIONS
      inv_winsys       = 1
      no_batch         = 2
      selection_cancel = 3
      selection_error  = 4
      OTHERS           = 5.

  IF sy-subrc <> 0.
  ENDIF.

ENDFORM.                    " GET_FILENAME

*&---------------------------------------------------------------------*
*&      Form  READ_FILE
*&---------------------------------------------------------------------*
*       Read input file
*----------------------------------------------------------------------*
FORM read_file .

* Read EXCEL file
  CALL FUNCTION 'FAA_FILE_UPLOAD_EXCEL'
    EXPORTING
      i_filename                 = p_infile
*      I_TEST                     =
*      I_UCOMM                    =
*      i_maxcols                  =
      i_delimiter                = '|'
    TABLES
      et_filecontent             = gt_repin
    EXCEPTIONS
      error_accessing_file       = 1
      OTHERS                     = 2
            .
  IF sy-subrc <> 0.
*   MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*           WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

* Store in an internal table
  LOOP AT gt_repin.
    CLEAR gt_rep.
    SPLIT gt_repin AT '|'
                   INTO gt_rep-bukrs
                        gt_rep-invoice
                        gt_rep-item
                        gt_rep-erdatc
                        gt_rep-erzetc
                        gt_rep-ernam
                        gt_rep-dmr
                        gt_rep-dmr_item
                        gt_rep-sales_order
                        gt_rep-sales_item
                        gt_rep-reposted
                        gt_rep-obj_key
                        gt_rep-obj_typ
                        gt_rep-obj_sys
                        gt_rep-gjahr
                        gt_rep-bus_act
                        gt_rep-redatc
                        gt_rep-rezetc
                        gt_rep-cancel_doc
                        gt_rep-cancel_item
                        gt_rep-cancel_datec
                        gt_rep-cancel_timec
                        gt_rep-cancel_name
                        gt_rep-repost_canceled
                        gt_rep-obj_key_rev
                        gt_rep-no_repost_req.

    CHECK gt_rep-invoice CO ' 0123456789'.

    PERFORM add_zeros USING gt_rep-invoice.
    PERFORM add_zeros USING gt_rep-item.
    PERFORM add_zeros USING gt_rep-dmr.
    PERFORM add_zeros USING gt_rep-dmr_item.
    PERFORM add_zeros USING gt_rep-sales_order.
    PERFORM add_zeros USING gt_rep-sales_item.
    PERFORM add_zeros USING gt_rep-cancel_doc.
    PERFORM add_zeros USING gt_rep-cancel_item.

    APPEND gt_rep.

  ENDLOOP.

  SORT gt_rep BY bukrs invoice item.
  DELETE ADJACENT DUPLICATES FROM gt_rep COMPARING bukrs invoice item.

ENDFORM.                    " READ_FILE

*&---------------------------------------------------------------------*
*&      Form  PROCESS_DATA
*&---------------------------------------------------------------------*
*       Process data
*----------------------------------------------------------------------*
FORM process_data .
  LOOP AT gt_rep.

*   Check if entry already exists in new version table
    IF NOT p_exist  IS INITIAL.
      SELECT SINGLE invoice INTO gv_invoice
             FROM yse_rrb_reposti
             WHERE bukrs   = gt_rep-bukrs
               AND invoice = gt_rep-invoice
               AND item    = gt_rep-item.

      CHECK sy-subrc NE 0.
    ENDIF.

*   Add to new version
    CLEAR gt_rrb_reposti.
    MOVE-CORRESPONDING gt_rep TO gt_rrb_reposti.
    PERFORM convert_date USING gt_rep-erdatc gt_rrb_reposti-erdat.
    PERFORM convert_date USING gt_rep-redatc gt_rrb_reposti-redat.
    PERFORM convert_date USING gt_rep-cancel_datec gt_rrb_reposti-cancel_date.
    PERFORM convert_time USING gt_rep-erzetc gt_rrb_reposti-erzet.
    PERFORM convert_time USING gt_rep-rezetc gt_rrb_reposti-rezet.
    PERFORM convert_time USING gt_rep-cancel_timec gt_rrb_reposti-cancel_time.
    APPEND gt_rrb_reposti.
    gv_count = gv_count + 1.

  ENDLOOP.

* Load data to NEW version table
  IF NOT gt_rrb_reposti[] IS INITIAL.
    MODIFY yse_rrb_reposti FROM TABLE gt_rrb_reposti.
    COMMIT WORK.
  ENDIF.

ENDFORM.                    " PROCESS_DATA

*&---------------------------------------------------------------------*
*&      Form  ADD_ZEROS
*&---------------------------------------------------------------------*
*       Add leading zeros
*----------------------------------------------------------------------*
*      -->P_FIELD : field
*----------------------------------------------------------------------*
FORM add_zeros  USING    p_field.

  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
    EXPORTING
      input  = p_field
    IMPORTING
      output = p_field.

ENDFORM.                    " ADD_ZEROS

*&---------------------------------------------------------------------*
*&      Form  CONVERT_DATE
*&---------------------------------------------------------------------*
*       Convert date from edited field to internal format
*----------------------------------------------------------------------*
*      -->P_DATEC : Edited date (character format)
*      -->P_DATEI : Date (in internal format)
*----------------------------------------------------------------------*
FORM convert_date  USING    p_datec
                            p_datei.

  CALL FUNCTION 'CONVERT_DATE_TO_INTERN_FORMAT'
    EXPORTING
      datum         = p_datec
      dtype         = 'DATS'
    IMPORTING
*      ERROR         =
      idate         = p_datei
*      MESSG         =
*      MSGLN         =
            .

ENDFORM.                    " CONVERT_DATE

*&---------------------------------------------------------------------*
*&      Form  CONVERT_TIME
*&---------------------------------------------------------------------*
*       Convert time from edited field to internal format
*----------------------------------------------------------------------*
*      -->P_TIMEC : Edited time (character format)
*      -->P_TIMEI : Time (in internal format)
*----------------------------------------------------------------------*
FORM convert_time  USING    p_timec
                            p_timei.

  CALL FUNCTION 'CONVERT_TIME_INPUT'
    EXPORTING
      input                           = p_timec
*      PLAUSIBILITY_CHECK              = 'X'
    IMPORTING
      output                          = p_timei
    EXCEPTIONS
      plausibility_check_failed       = 1
      wrong_format_in_input           = 2
      OTHERS                          = 3
            .
  IF sy-subrc <> 0.
*    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

ENDFORM.                    " CONVERT_TIME

*Text symbol text£º
*B01:Input File

*I01:Entries loaded :
*Selection text£º
*P_EXIST:        Check if entry already exists
*P_INFILE:        Input File
