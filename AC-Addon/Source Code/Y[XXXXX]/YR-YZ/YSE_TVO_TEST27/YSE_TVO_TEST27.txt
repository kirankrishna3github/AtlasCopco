*&---------------------------------------------------------------------*
*& Report  YSE_TVO_TEST27                                              *
*&                                                                     *
*&---------------------------------------------------------------------*
*&                                                                     *
*&                                                                     *
*&---------------------------------------------------------------------*

REPORT  YSE_TVO_TEST27                                              .

*&---------------------------------------------------------------------*
*& CLASS DEFINITION (ALV GRID) ----------------------------------------*
*&---------------------------------------------------------------------*
CLASS LCL_EVENT_HANDLER DEFINITION.
  PUBLIC SECTION.
    METHODS:
      HANDLE_DOUBLE_CLICK
          FOR EVENT DOUBLE_CLICK
                 OF CL_GUI_ALV_GRID
          IMPORTING E_ROW
                    E_COLUMN
                    ES_ROW_NO.

ENDCLASS.                    "LCL_EVENT_HANDLER DEFINITION

CLASS LCL_EVENT_HANDLER IMPLEMENTATION.

  METHOD HANDLE_DOUBLE_CLICK.
    PERFORM 0100_DBLCLICK USING E_ROW E_COLUMN ES_ROW_NO.
  ENDMETHOD.

ENDCLASS.                    "LCL_EVENT_HANDLER IMPLEMENTATION

*&---------------------------------------------------------------------*
*& DATA DECLARATION ---------------------------------------------------*
*&---------------------------------------------------------------------*
TABLES:
  E070,
  E07T,
  E071.

TYPE-POOLS:
  TRWBO.

DATA:
  IT_E070  TYPE E070     OCCURS 0 WITH HEADER LINE,
  IT_E07T  TYPE E07T     OCCURS 0 WITH HEADER LINE,
  IT_E071  TYPE E071     OCCURS 0 WITH HEADER LINE,
  IT_OUT   TYPE YSE_E070 OCCURS 0 WITH HEADER LINE,
  IT_FLDCT TYPE LVC_T_FCAT,
  BEGIN OF IT_OUT_RESTR  OCCURS 0,
    TRKORR TYPE TRKORR,
    TRFUNCTION TYPE TRFUNCTION,
    TRSTATUS TYPE TRSTATUS,
    TARSYSTEM TYPE TR_TARGET,
    KORRDEV TYPE TRCATEG,
    AS4USER TYPE TR_AS4USER,
    AS4DATE TYPE AS4DATE,
    AS4TIME TYPE AS4TIME,
    STRKORR TYPE STRKORR,
    LANGU TYPE DDLANGUAGE,
    AS4TEXT TYPE AS4TEXT,
  END   OF IT_OUT_RESTR,
  LV_OKCODE         LIKE SY-UCOMM,
  OBJ_ALV_OV        TYPE REF TO CL_GUI_ALV_GRID,
  OBJ_CONT_OV       TYPE REF TO CL_GUI_CUSTOM_CONTAINER,
  OBJ_EVENT_HNDL_OV TYPE REF TO LCL_EVENT_HANDLER,
  GS_LAYOUT         TYPE LVC_S_LAYO,
  GS_ROWIDS         TYPE LVC_S_ROID,
  GS_KORR           TYPE TRWBO_SELECTION,
  IT_FIELDCAT_OV    TYPE LVC_T_FCAT,
  IT_ROWIDS         TYPE LVC_T_ROID,
  IT_KORR           TYPE TRWBO_SELECTIONS,
  LV_CONT_NAME      TYPE SCRFNAME VALUE 'BCALV_GRID_02_100',
  LV_ROWCNT         TYPE I,
  LV_ROWCNT0        TYPE I,
  LV_ROWCNTT        TYPE I,
  LV_ROWCNT1        TYPE I.

FIELD-SYMBOLS:
  <FS_TABLE> TYPE TABLE,
  <FIELDCAT> TYPE LVC_S_FCAT.

*&---------------------------------------------------------------------*
*& SELECTION SCREEN ---------------------------------------------------*
*&---------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK A02 WITH FRAME TITLE TEXT-002.
  SELECT-OPTIONS:                "
    SO_KORR  FOR E070-TRKORR,    "
    SO_FUNC  FOR E070-TRFUNCTION,"
    SO_STAT  FOR E070-TRSTATUS,  "
    SO_TARG  FOR E070-TARSYSTEM, "
    SO_CATG  FOR E070-KORRDEV,   "
    SO_USER  FOR E070-AS4USER DEFAULT SY-UNAME,   "
    SO_DATE  FOR E070-AS4DATE,   "
    SO_TIME  FOR E070-AS4TIME,   "
    SO_SKOR  FOR E070-STRKORR.   "
SELECTION-SCREEN END OF BLOCK A02.

SELECTION-SCREEN BEGIN OF BLOCK A03 WITH FRAME TITLE TEXT-003.
  SELECT-OPTIONS:                "
    SO_LANG  FOR E07T-LANGU DEFAULT 'EN'.     "
  PARAMETERS:                    "
    P_TEXT   LIKE E07T-AS4TEXT.  "
SELECTION-SCREEN END OF BLOCK A03.

SELECTION-SCREEN BEGIN OF BLOCK A04 WITH FRAME TITLE TEXT-004.
  SELECT-OPTIONS:                "
    SO_PGID  FOR E071-PGMID,     "
    SO_OBJT  FOR E071-OBJECT,    "
    SO_OBJN  FOR E071-OBJ_NAME,  "
    SO_OBJF  FOR E071-OBJFUNC.   "
SELECTION-SCREEN END OF BLOCK A04.

SELECTION-SCREEN BEGIN OF BLOCK A05 WITH FRAME TITLE TEXT-005.
  PARAMETERS:
    RB_HIER TYPE C RADIOBUTTON GROUP 001 USER-COMMAND OUT,
    RB_LIST TYPE C RADIOBUTTON GROUP 001,
    P_RESTR TYPE C AS CHECKBOX.
SELECTION-SCREEN END OF BLOCK A05.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN OUTPUT -----------------------------------------*
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.

    IF SCREEN-NAME EQ 'P_RESTR'.
      IF RB_LIST NE SPACE.
        SCREEN-INPUT = 1.
      ELSE.
        SCREEN-INPUT = 0.
      ENDIF.
      MODIFY SCREEN.
    ENDIF.

  ENDLOOP.

*&---------------------------------------------------------------------*
*& START OF SELECTION -------------------------------------------------*
*&---------------------------------------------------------------------*
START-OF-SELECTION.

  DATA:
    LV_RCT TYPE I.

* Select data
* **********************************************************************
  SELECT      *
         FROM E070
         INTO TABLE IT_E070
        WHERE TRKORR     IN SO_KORR
          AND TRFUNCTION IN SO_FUNC
          AND TRSTATUS   IN SO_STAT
          AND TARSYSTEM  IN SO_TARG
          AND KORRDEV    IN SO_CATG
          AND AS4USER    IN SO_USER
          AND AS4DATE    IN SO_DATE
          AND AS4TIME    IN SO_TIME
          AND STRKORR    IN SO_SKOR.
  LV_ROWCNT0 = SY-DBCNT.
  REPLACE ALL OCCURRENCES OF '*' IN P_TEXT WITH '%'.
  IF NOT P_TEXT IS INITIAL.
    SELECT      *
           FROM E07T
           INTO TABLE IT_E07T
            FOR ALL ENTRIES IN IT_E070
          WHERE TRKORR     EQ IT_E070-TRKORR
            AND LANGU      IN   SO_LANG
            AND AS4TEXT    LIKE P_TEXT.
  ELSE.
    SELECT      *
           FROM E07T
           INTO TABLE IT_E07T
            FOR ALL ENTRIES IN IT_E070
          WHERE TRKORR     EQ IT_E070-TRKORR
            AND LANGU      IN   SO_LANG.
  ENDIF.
  LV_ROWCNTT = SY-DBCNT.
  SELECT      *
         FROM E071
         INTO TABLE IT_E071
          FOR ALL ENTRIES IN IT_E070
        WHERE TRKORR     EQ IT_E070-TRKORR
          AND PGMID      IN SO_PGID
          AND OBJECT     IN SO_OBJT
          AND OBJ_NAME   IN SO_OBJN
          AND OBJFUNC    IN SO_OBJF.
  LV_ROWCNT1 = SY-DBCNT.

* Further processing depends on output options
  IF RB_LIST NE SPACE.

*   Process data
*   ********************************************************************
    LOOP AT IT_E070.

      MOVE-CORRESPONDING IT_E070 TO IT_OUT.
      LOOP AT IT_E07T WHERE TRKORR EQ IT_E070-TRKORR.

        MOVE-CORRESPONDING IT_E07T TO IT_OUT.

        LOOP AT IT_E071 WHERE TRKORR EQ IT_E070-TRKORR.

          MOVE-CORRESPONDING IT_E071 TO IT_OUT.
          APPEND IT_OUT.

        ENDLOOP.

      ENDLOOP.

    ENDLOOP.

    IF NOT P_RESTR IS INITIAL.
      IT_OUT_RESTR[] = IT_OUT[].
      SORT IT_OUT.
      DELETE ADJACENT DUPLICATES FROM IT_OUT_RESTR.
      ASSIGN IT_OUT_RESTR[] TO <FS_TABLE>.
    ELSE.
      ASSIGN IT_OUT[] TO <FS_TABLE>.
    ENDIF.

*   Display data
*   ********************************************************************
    DESCRIBE TABLE <FS_TABLE> LINES LV_ROWCNT.
    CALL SCREEN 0100.

  ELSE.

*   Process data
*   ********************************************************************
    LOOP AT IT_E070.

      LOOP AT IT_E07T WHERE TRKORR EQ IT_E070-TRKORR.

        LOOP AT IT_E071 WHERE TRKORR EQ IT_E070-TRKORR.

          IF IT_E070-STRKORR NE SPACE.
            GS_KORR-TRKORRPATTERN = IT_E070-STRKORR.
          ELSE.
            GS_KORR-TRKORRPATTERN = IT_E070-TRKORR.
          ENDIF.
          APPEND GS_KORR TO IT_KORR.
        ENDLOOP.

      ENDLOOP.

    ENDLOOP.

    DESCRIBE TABLE IT_KORR LINES LV_RCT.

*   Display data
*   ********************************************************************
    IF NOT LV_RCT EQ 0.

*     Remove duplicates
      SORT IT_KORR BY TRKORRPATTERN.
      DELETE ADJACENT DUPLICATES FROM IT_KORR.

      CALL FUNCTION 'TR_PRESENT_REQUESTS_ORGANIZER'
        EXPORTING
          IV_ORGANIZER_TYPE       = 'A'
          IT_SELECTIONS           = IT_KORR.
    ENDIF.

  ENDIF.

*&---------------------------------------------------------------------*
*& PBO MODULES --------------------------------------------------------*
*&---------------------------------------------------------------------*
MODULE 0100_PBO OUTPUT.

  SET PF-STATUS 'MAIN100'.
  SET TITLEBAR  'MAIN100'.

  IF OBJ_CONT_OV IS INITIAL .
    CREATE OBJECT OBJ_CONT_OV
      EXPORTING
        CONTAINER_NAME = LV_CONT_NAME
      EXCEPTIONS
        CNTL_ERROR = 1
        CNTL_SYSTEM_ERROR = 2
        CREATE_ERROR = 3
        LIFETIME_ERROR = 4
        LIFETIME_DYNPRO_DYNPRO_LINK = 5.
    IF SY-SUBRC NE 0.
      CALL FUNCTION 'POPUP_TO_INFORM'
           EXPORTING
                TITEL = SY-REPID
                TXT2  = SY-SUBRC
                TXT1  = 'The control could not be created'(001).
    ENDIF.

    CREATE OBJECT OBJ_ALV_OV
      EXPORTING
        I_PARENT = OBJ_CONT_OV.

    CREATE OBJECT OBJ_EVENT_HNDL_OV.

*   Enable Line Selection And Double-Clicking
    SET HANDLER OBJ_EVENT_HNDL_OV->HANDLE_DOUBLE_CLICK FOR OBJ_ALV_OV.

*   Clear the field catalog table
    REFRESH: IT_FIELDCAT_OV.

*   Fetch the field catalog for structure YSE_RENT_EHRSP_OUTPUT
    CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
      EXPORTING
        I_BUFFER_ACTIVE              = ' '
        I_STRUCTURE_NAME             = 'YSE_E070'
      CHANGING
        CT_FIELDCAT                  = IT_FLDCT.

*   Hide object details
    LOOP AT IT_FLDCT ASSIGNING <FIELDCAT>.
      CASE <FIELDCAT>-FIELDNAME.
        WHEN 'AS4POS' OR 'PGMID' OR 'OBJECT' OR 'OBJFUNC' OR 'LOCKFLAG'
                      OR 'GENNUM' OR 'LANG' OR 'ACTIVITY'.
          IF P_RESTR NE SPACE.
            <FIELDCAT>-NO_OUT = 'X'.
          ENDIF.
        WHEN 'OBJ_NAME'.
         IF P_RESTR NE SPACE.
           <FIELDCAT>-NO_OUT = 'X'.
         ELSE.
           <FIELDCAT>-OUTPUTLEN = 20.
         ENDIF.
      ENDCASE.
    ENDLOOP.

*   Set layout variables
    GS_LAYOUT-SEL_MODE   = 'D'.

*   Display the grid
    CALL METHOD OBJ_ALV_OV->SET_TABLE_FOR_FIRST_DISPLAY
      EXPORTING
        I_STRUCTURE_NAME              = 'YSE_E070'
        IS_LAYOUT                     = GS_LAYOUT
      CHANGING
        IT_OUTTAB                     = <FS_TABLE>
        IT_FIELDCATALOG               = IT_FLDCT
      EXCEPTIONS
        INVALID_PARAMETER_COMBINATION = 1
        PROGRAM_ERROR                 = 2
        TOO_MANY_LINES                = 3
        OTHERS                        = 4.
  ENDIF.

* CALL METHOD CL_GUI_CONTROL=>SET_FOCUS EXPORTING CONTROL = OBJ_ALV_OV.

ENDMODULE.                 " 0100_PBO  OUTPUT

*&---------------------------------------------------------------------*
*& PAI MODULES --------------------------------------------------------*
*&---------------------------------------------------------------------*
MODULE 0100_PAI INPUT.

  CASE LV_OKCODE.
    WHEN 'BACK'.
      PERFORM EXIT_PROGRAM.
    WHEN 'EXIT'.
      PERFORM EXIT_PROGRAM.
    WHEN 'DIS_ALL'.
      PERFORM DISPLAY_ALL.
  ENDCASE.

ENDMODULE.                 " 0100_PAI  INPUT


*&---------------------------------------------------------------------*
*& SUPPORTING FORMS ---------------------------------------------------*
*&---------------------------------------------------------------------*

*&      Form  EXIT_PROGRAM
*&---------------------------------------------------------------------*
FORM EXIT_PROGRAM .

  CALL METHOD OBJ_ALV_OV->FREE.
  FREE OBJ_ALV_OV.
  CALL METHOD OBJ_CONT_OV->FREE.
  FREE OBJ_CONT_OV.

  CALL METHOD CL_GUI_CFW=>FLUSH.

  LEAVE TO SCREEN 0.

ENDFORM.                    " EXIT_PROGRAM

*&      Form  0100_DBLCLICK
*&---------------------------------------------------------------------*
FORM 0100_DBLCLICK  USING    P_E_ROW
                             P_E_COLUMN
                             P_ES_ROW_NO STRUCTURE LVC_S_ROID.

  CHECK P_ES_ROW_NO-ROW_ID GT 0.

  READ TABLE <FS_TABLE> INDEX P_ES_ROW_NO-ROW_ID INTO IT_OUT.
  CHECK SY-SUBRC EQ 0.

  CALL FUNCTION 'TR_PRESENT_REQUEST'
   EXPORTING
     IV_TRKORR          = IT_OUT-TRKORR
     IV_HIGHLIGHT       = 'X'.

ENDFORM.                    " 0100_DBLCLICK

*&      Form  DISPLAY_ALL
*&---------------------------------------------------------------------*
FORM DISPLAY_ALL .

  DATA:
    LV_RCT TYPE I.

  CALL METHOD OBJ_ALV_OV->GET_SELECTED_ROWS
    IMPORTING
      ET_ROW_NO     = IT_ROWIDS.

  DESCRIBE TABLE IT_ROWIDS LINES LV_RCT.

  IF NOT LV_RCT EQ 0.

    CLEAR IT_KORR[].

*   Loop over the row ids
    LOOP AT IT_ROWIDS INTO GS_ROWIDS.

*     Read the corresponding line in the out table
      READ TABLE <FS_TABLE> INDEX GS_ROWIDS-ROW_ID INTO IT_OUT.

*     If a higher level is specified, use that one
      IF IT_OUT-STRKORR NE SPACE.
        GS_KORR-TRKORRPATTERN = IT_OUT-STRKORR.
      ELSE.
        GS_KORR-TRKORRPATTERN = IT_OUT-TRKORR.
      ENDIF.
      APPEND GS_KORR TO IT_KORR.

    ENDLOOP.

*   Remove duplicates
    SORT IT_KORR BY TRKORRPATTERN.
    DELETE ADJACENT DUPLICATES FROM IT_KORR.

    CALL FUNCTION 'TR_PRESENT_REQUESTS_ORGANIZER'
      EXPORTING
        IV_ORGANIZER_TYPE       = 'A'
        IT_SELECTIONS           = IT_KORR.
  ENDIF.

ENDFORM.                    " DISPLAY_ALL

*Text symbol text£º
*001:The control could not be created
*002:Request/Task details
*003:Request/Task description
*004:Objects in Tasks/Requests

*005:Output
*Selection text£º
*P_RESTR:        Don't list objects
*P_TEXT:D       Short Description
*RB_HIER:        Hierarchy
*RB_LIST:        List
*SO_CATG:D       Category
*SO_DATE:D       Date
*SO_FUNC:D       Type of request/task
*SO_KORR:D       Request/Task
*SO_LANG:D       Lang.
*SO_OBJF:D       Function
*SO_OBJN:D       Obj. name
*SO_OBJT:D       Object Type
*SO_PGID:D       Program ID
*SO_SKOR:D       Higher-Level Request
*SO_STAT:D       Status
*SO_TARG:D       Transport targ.
*SO_TIME:D       Time
*SO_USER:D       Owner
