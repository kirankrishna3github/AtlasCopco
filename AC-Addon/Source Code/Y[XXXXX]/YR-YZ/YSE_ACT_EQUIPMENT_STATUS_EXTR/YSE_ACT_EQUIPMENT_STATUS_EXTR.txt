*&---------------------------------------------------------------------*
*& Report  YSE_ACT_EQUIPMENT_STATUS_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Equipment Status extraction                                  *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 08.11.2012
*  Change Request Number :
*  Transport request Nr. : CD1K973815
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 19.03.2014 | Jules Smets     | CD1K980489    | CR1794 (Act)*
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_act_equipment_status_extr.

TABLES: equi,
        equz,
        iloa,
        jest,
        tj02t,
        tj30t.

* Types

TYPES: BEGIN OF ty_equi,
         equnr      TYPE equnr,
         objnr      TYPE j_objnr,
      END OF ty_equi.

TYPES: BEGIN OF ty_dlfl,
         objnr      TYPE j_objnr,
      END OF ty_dlfl.

TYPES: BEGIN OF ty_jest,
         objnr      TYPE j_objnr,
         stat       TYPE j_status,
      END OF ty_jest.

TYPES: BEGIN OF ty_tj02t,
         istat      TYPE j_istat,
         txt04      TYPE j_txt04,
      END OF ty_tj02t.

TYPES: BEGIN OF ty_tj30t,
         estat      TYPE j_estat,
         txt04      TYPE j_txt04,
      END OF ty_tj30t.

* Internal tables
DATA: gt_equi  TYPE TABLE OF ty_equi
                    WITH HEADER LINE.

DATA: gt_dlfl  TYPE HASHED TABLE OF ty_dlfl
                    WITH UNIQUE KEY objnr
                    WITH HEADER LINE,
      gt_dlfli TYPE TABLE OF ty_dlfl
                    WITH HEADER LINE.

DATA: gt_jest  TYPE SORTED TABLE OF ty_jest
                    WITH NON-UNIQUE KEY objnr
                    WITH HEADER LINE.

DATA: gt_tj02t TYPE HASHED TABLE OF ty_tj02t
                    WITH UNIQUE KEY istat
                    WITH HEADER LINE.

DATA: gt_tj30t TYPE HASHED TABLE OF ty_tj30t
                    WITH UNIQUE KEY estat
                    WITH HEADER LINE.

DATA: BEGIN OF gt_out  OCCURS 0,
        equnr       TYPE equnr,
        tab01(1)    TYPE c,
        sttxu(50)   TYPE c,
        tab02(1)    TYPE c,
        sttxs(50)   TYPE c,
        tab03(1)    TYPE c,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        equnr       TYPE fieldname,
        tab01(1)    TYPE c,
        sttxu       TYPE fieldname,
        tab02(1)    TYPE c,
        sttxs       TYPE fieldname,
        tab03(1)    TYPE c,
      END OF gt_outh.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_ersda(14)     TYPE c,                      "YYYYMMDDHHMMSS
      gv_sttxu(50)     TYPE c,
      gv_sttxs(50)     TYPE c,
      gv_error         TYPE xfeld.


CONSTANTS: gc_stat_dl  TYPE j_status  VALUE 'I0076',
           gc_stat_in  TYPE j_status  VALUE 'I0320',
           gc_stat_scr TYPE j_status  VALUE 'E0005'.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
PARAMETERS: p_bukrs  LIKE iloa-bukrs  OBLIGATORY  MEMORY ID buk.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_equnr  FOR equi-equnr,
                s_istat  FOR tj02t-istat,
                s_estat  FOR tj30t-estat.
SELECTION-SCREEN  END OF BLOCK input.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Timestamp
  CONCATENATE sy-datum sy-uzeit INTO gv_ersda.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Status selection
  PERFORM status_selection.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_equi[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No equipments selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  STATUS_SELECTION
*&---------------------------------------------------------------------*
*       Status selection
*----------------------------------------------------------------------*
FORM status_selection .

* System status
  SELECT istat txt04
         INTO TABLE gt_tj02t
         FROM tj02t
         WHERE spras = 'E'.
* Selection
  IF s_istat[] IS INITIAL.
    LOOP AT gt_tj02t.
      s_istat-low    = gt_tj02t-istat.
      s_istat-sign   = 'I'.
      s_istat-option = 'EQ'.
      APPEND s_istat.
    ENDLOOP.
  ENDIF.

* User status
  SELECT estat txt04
         INTO TABLE gt_tj30t
         FROM tj30t
         WHERE stsma = 'ZAM00004'
           AND spras = 'E'.
* Selection
  IF s_estat[] IS INITIAL.
    LOOP AT gt_tj30t.
      s_estat-low    = gt_tj30t-estat.
      s_estat-sign   = 'I'.
      s_estat-option = 'EQ'.
      APPEND s_estat.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " STATUS_SELECTION

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Equipment time segment
  SELECT e~equnr e~objnr
         INTO TABLE gt_equi
         FROM equi AS e
         INNER JOIN equz AS z
                    ON z~equnr = e~equnr
         INNER JOIN iloa AS l
                    ON l~iloan = z~iloan
         WHERE e~equnr IN s_equnr
           AND z~datbi =  '99991231'
           AND l~bukrs =  p_bukrs.
  CHECK NOT gt_equi[] IS INITIAL.

*** MOD-001 * begin ***
** Check for deleted/scrapped equipments
*  SELECT objnr
*         INTO TABLE gt_dlfli
*         FROM jest
*         FOR ALL ENTRIES IN gt_equi
*         WHERE objnr = gt_equi-objnr
*           AND ( stat = gc_stat_dl   OR
*                 stat = gc_stat_in   OR
*                 stat = gc_stat_scr )
*           AND inact = ' '.
*  SORT gt_dlfli BY objnr.
*  DELETE ADJACENT DUPLICATES FROM gt_dlfli
*                                  COMPARING objnr.
*  gt_dlfl[] = gt_dlfli[].
*  FREE gt_dlfli.
*
*  LOOP AT gt_equi.
*    READ TABLE gt_dlfl WITH TABLE KEY objnr = gt_equi-objnr.
*    IF sy-subrc = 0.
*      DELETE gt_equi.
*    ENDIF.
*  ENDLOOP.
*  FREE gt_dlfl.
*  CHECK NOT gt_equi[] IS INITIAL.
*** MOD-001 * end ***

* Equipment status data
  SELECT objnr stat
         INTO TABLE gt_jest
         FROM jest
         FOR ALL ENTRIES IN gt_equi
         WHERE objnr = gt_equi-objnr
           AND ( stat IN s_istat  OR
                 stat IN s_estat )
           AND inact = ' '.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_equi.

    CLEAR: gv_sttxu, gv_sttxs.
    CLEAR: gt_out.
    MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03.

    gt_out-equnr = gt_equi-equnr.

*   Status
    LOOP AT gt_jest WHERE objnr = gt_equi-objnr.

      CASE gt_jest-stat(1).
*       User status
        WHEN 'E'.
          READ TABLE gt_tj30t WITH TABLE KEY estat = gt_jest-stat.
          IF sy-subrc = 0.
            IF gv_sttxu IS INITIAL.
              gv_sttxu = gt_tj30t-txt04.
            ELSE.
              CONCATENATE gv_sttxu gt_tj30t-txt04 INTO gv_sttxu
                                                  SEPARATED BY '/'.
            ENDIF.
          ENDIF.
*       System status
        WHEN 'I'.
          READ TABLE gt_tj02t WITH TABLE KEY istat = gt_jest-stat.
          IF sy-subrc = 0.
            IF gv_sttxs IS INITIAL.
              gv_sttxs = gt_tj02t-txt04.
            ELSE.
              CONCATENATE gv_sttxs gt_tj02t-txt04 INTO gv_sttxs
                                                  SEPARATED BY '/'.
            ENDIF.
          ENDIF.
        WHEN OTHERS.
      ENDCASE.

    ENDLOOP.

    gt_out-sttxu = gv_sttxu.
    gt_out-sttxs = gv_sttxs.
    APPEND gt_out.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

* Open file
  CONCATENATE 'EQUIPMENT_STATUS' p_bukrs
             INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM fill_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

* Fill output
  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  FILL_HEADER
*&---------------------------------------------------------------------*
*       Fill header
*----------------------------------------------------------------------*
FORM fill_header .

  CLEAR: gt_outh.
  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03.

  gt_outh-equnr  = 'EQUNR'.
  gt_outh-sttxu  = 'ILOM_STTXU'.
  gt_outh-sttxs  = 'ILOM_STTXS'.
  APPEND gt_outh.

  gt_outh-equnr  = 'Equipment'.
  gt_outh-sttxu  = 'User Status'.
  gt_outh-sttxs  = 'System Status'.
  APPEND gt_outh.

ENDFORM.                    " FILL_HEADER

*Text symbol text£º
*E01:No equipments selected
*E02:Could not open output file
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_BUKRS:D       .
*S_EQUNR:D       .
*S_ESTAT:D       .
*S_ISTAT:D       .
