*&---------------------------------------------------------------------*
*& Report  YSE_FI_RR_LT_ST_3111
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& Split Deferred Revenue (LT/ST) / Reclass GPS code 3111 (SEED)       *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 22.08.2011
*  Change Request Number : CR2012
*  Transport request Nr. : CD1K967345
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME           | CORRECTION NR. | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 10.10.2014 |DASHMANTHA      | CD1K983310     | CR3390      *
*         | CR3390: improve performance of ST/LT 3111 reclass program  *
*----------------------------------------------------------------------*
* MOD-002 | 15.09.2016 |Vishnupriya N   | CD1K989749    | CR4042       *
*         | CR4042: Issue for LT/ST for billing Plan Z7                *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_fi_rr_lt_st_3111 NO STANDARD PAGE HEADING
                            LINE-SIZE 170.

TABLES: vbak,
        vbap,
        veda,
        vbfa,
        vbrp,
        bkpf,
        vbkd,
        fplt,
        fpla,
        bsis,
        bsas,
        bseg,
        konv,
        coep,
        cobk,
        tcurr,
        t001,
        t001b.

TYPES: BEGIN OF ty_vbap,
         vbeln     TYPE vbeln,
         posnr     TYPE posnr,
         waerk     TYPE waers,
         netwr     TYPE netwr,
         objnr     TYPE j_objnr,
       END OF ty_vbap.

TYPES: BEGIN OF ty_veda,
         vbeln     TYPE vbeln,
         vposn     TYPE posnr,
         vbegdat   TYPE datum,
         venddat   TYPE datum,
         vkuegru   TYPE kuegru,
       END OF ty_veda.
*Begin of insertion by MOD-002
TYPES: BEGIN OF ty_veda1,
         vbeln     TYPE vbeln,
         venddat   TYPE datum,
       END OF ty_veda1.
*End of insertion by MOD-002


TYPES: BEGIN OF ty_bsis,
         vbeln     TYPE vbeln,
         posnr     TYPE posnr,
         vbeln_r   TYPE vbeln,
         posnr_r   TYPE posnr,
         belnr     TYPE belnr_d,
         gjahr     TYPE gjahr,
         buzei     TYPE buzei,
         budat     TYPE budat,
         dmbtr     TYPE dmbtr,
         wrbtr     TYPE wrbtr,
         waers     TYPE waers,
         shkzg     TYPE shkzg,
         segment   TYPE fb_segment,
       END OF ty_bsis.

TYPES: BEGIN OF ty_bseg,
         belnr     TYPE belnr_d,
         gjahr     TYPE gjahr,
         buzei     TYPE buzei,
         vbel2     TYPE vbeln,
         posn2     TYPE posnr,
       END OF ty_bseg.

TYPES: BEGIN OF ty_fpla,
         vbeln     TYPE vbeln_va,
         posnr     TYPE posnr_va,
         fplnr     TYPE fplnr,
         fpart     TYPE fpart,"+MOD-002
         bedat     TYPE bedat_fp,
         endat     TYPE endat_fp,
       END OF ty_fpla.

TYPES: BEGIN OF ty_curr,
         kurst     TYPE kurst,
         fcurr     TYPE fcurr,
         tcurr     TYPE tcurr_curr,
         gdatu     TYPE gdatu_inv,
         ukurs     TYPE ukurs_curr,
       END OF ty_curr.

TYPES: BEGIN OF ty_coep,
         objnr     TYPE j_objnr,
         budat     TYPE budat,
         twaer     TYPE twaer,
         wtgbtr    TYPE wtgxxx,
         wogbtr    TYPE wogxxx,
         belnr     TYPE co_belnr,
         buzei     TYPE co_buzei,
       END OF ty_coep.

TYPES: BEGIN OF ty_segm,
         zuonr     TYPE dzuonr,
         blart     TYPE blart,
         hkont     TYPE hkont,
         budat     TYPE budat,
         belnr     TYPE belnr_d,
         gjahr     TYPE gjahr,
         segment   TYPE fb_segment,
       END OF ty_segm.

DATA: gt_vbap  TYPE HASHED TABLE OF ty_vbap
                    WITH UNIQUE KEY vbeln posnr
                    WITH HEADER LINE.

DATA: gt_veda  TYPE TABLE OF ty_veda
                    WITH HEADER LINE.
*Begin Of Insertion by MOD-002
DATA: gt_veda1 TYPE TABLE OF ty_veda1
                    WITH HEADER LINE.
*End Of Insertion by MOD-002
DATA: gt_bsis  TYPE SORTED TABLE OF ty_bsis
                    WITH NON-UNIQUE KEY vbeln posnr
                    WITH HEADER LINE,
      gt_bsisi TYPE TABLE OF ty_bsis
                    WITH HEADER LINE.

DATA: gt_bseg  TYPE HASHED TABLE OF ty_bseg
                    WITH UNIQUE KEY belnr gjahr buzei
                    WITH HEADER LINE.

DATA: gt_fpla  TYPE HASHED TABLE OF ty_fpla
                    WITH UNIQUE KEY vbeln posnr
                    WITH HEADER LINE.

DATA: gt_curr  TYPE HASHED TABLE OF ty_curr
                    WITH UNIQUE KEY kurst fcurr tcurr
                    WITH HEADER LINE,
      gt_curri TYPE TABLE OF ty_curr
                    WITH HEADER LINE.

DATA: gt_coep  TYPE SORTED TABLE OF ty_coep
                    WITH NON-UNIQUE KEY objnr budat
                    WITH HEADER LINE.

DATA: gt_segm  TYPE HASHED TABLE OF ty_segm
                    WITH UNIQUE KEY zuonr blart hkont
                    WITH HEADER LINE,
      gt_segmi TYPE TABLE OF ty_segm
                    WITH HEADER LINE.

* Selection table
DATA: BEGIN OF gt_sel  OCCURS 0,
         vbeln       TYPE vbeln_va,
         posnr       TYPE posnr_va,
         waers       TYPE waers,            "Local currency
         bil_val_lc  TYPE wogxxx,
         rev_rec_lc  TYPE wogxxx,
         def_rr_lc   TYPE wogxxx,
         lt_defr_lc  TYPE wogxxx,
         segment     TYPE fb_segment,
         bil_cy_lc   TYPE wogxxx,
         segm_bcy    TYPE fb_segment,
         revr_cy_lc  TYPE wogxxx,
         segm_rcy    TYPE fb_segment,
      END OF gt_sel.

* Table for Split
DATA: BEGIN OF gt_split  OCCURS 0,
         waers       TYPE waers,            "Local currency
         segment     TYPE fb_segment,
         lt_defr_lc  TYPE wogxxx,
      END OF gt_split,
      wa_split       LIKE gt_split.

* Table for Reclass 3115
DATA: BEGIN OF gt_3115  OCCURS 0,
         waers       TYPE waers,            "Local currency
         segm_bcy    TYPE fb_segment,
         bil_cy_lc   TYPE wogxxx,
      END OF gt_3115,
      wa_3115       LIKE gt_3115.

* Table for Reclass 3116
DATA: BEGIN OF gt_3116  OCCURS 0,
         waers       TYPE waers,            "Local currency
         segm_rcy    TYPE fb_segment,
         revr_cy_lc  TYPE wogxxx,
      END OF gt_3116,
      wa_3116       LIKE gt_3116.

* Document data
DATA: BEGIN OF gt_doc  OCCURS 0,
         belnr       TYPE belnr_d,
         waers       TYPE waers,            "Local currency
         segment     TYPE fb_segment,
         zuonr       TYPE dzuonr,
         val_lc      TYPE wogxxx,
      END OF gt_doc.

DATA: i_bdcdata LIKE bdcdata  OCCURS 0  WITH HEADER LINE,
      struct_bdcdata TYPE bdcdata,
      gt_err    LIKE bdcmsgcoll  OCCURS 0  WITH HEADER LINE.

DATA: gv_waers_cc  TYPE waers,
      gv_waers_int TYPE waers,
      gv_gdatu     TYPE gdatu_inv,
      gv_kurst     TYPE kurst,
      gv_slt_bp    TYPE datum,
      gv_slt_ep    TYPE datum,
      gv_slt_by    TYPE datum,
      gv_slt_bnp   TYPE datum,
      gv_bil_lc    TYPE wogxxx,
      gv_revr_lc   TYPE wogxxx,
      gv_revr_cc   TYPE wogxxx,
      gv_defr_so   TYPE wtgxxx,
      gv_defr_lc   TYPE wogxxx,
      gv_ltdefr_lc TYPE wogxxx,
      gv_diff_lc   TYPE wogxxx,
      gv_bilcy_lc  TYPE wogxxx,
      gv_revrcy_lc TYPE wogxxx,
      gv_days_settl(4)   TYPE p,
      gv_yp_open(6)      TYPE n,
      gv_yp_post(6)      TYPE n,
      gv_error     TYPE xfeld,
      gv_zuonr     TYPE dzuonr,
      gv_xintb     TYPE xfeld,
      gv_hkont     TYPE hkont,
      gv_xblnr     TYPE xblnr,
      gv_msgtxt    LIKE t100-text,
      gv_errlst    TYPE xfeld     VALUE 'X',
      gv_detlst    TYPE xfeld     VALUE ' ',
      gv_mode(1)   TYPE c         VALUE 'N',
      gv_fpart(2)  TYPE c         VALUE 'Z7'.

CONSTANTS: gc_a(1)    TYPE c      VALUE 'A',
           gc_g(1)    TYPE c      VALUE 'G',
           gc_m(1)    TYPE c      VALUE 'M',
           gc_n(1)    TYPE c      VALUE 'N',
           gc_o(1)    TYPE c      VALUE 'O',
           gc_p(1)    TYPE c      VALUE 'P',
           gc_vbrk    TYPE awtyp  VALUE 'VBRK',
           gc_sa      TYPE blart  VALUE 'SA',
           gc_zz      TYPE blart  VALUE 'ZZ',
           gc_zs      TYPE mwskz  VALUE 'ZS'.

RANGES: r_zuonr  FOR  bsis-zuonr.

* Selections
SELECTION-SCREEN  BEGIN OF BLOCK sel  WITH FRAME  TITLE text-s01.
PARAMETERS: p_bukrs   TYPE bukrs  OBLIGATORY
                                  MEMORY ID buk.
SELECT-OPTIONS: so_vbeln  FOR vbap-vbeln.
SELECTION-SCREEN SKIP.
PARAMETERS: p_detail AS CHECKBOX.
SELECTION-SCREEN  END OF BLOCK sel.
* Def Rev Short-Long Term
SELECTION-SCREEN  BEGIN OF BLOCK slt  WITH FRAME  TITLE text-s02.
PARAMETERS:     p_slt_p TYPE monat  OBLIGATORY,
                p_slt_y TYPE gjahr  OBLIGATORY.
SELECTION-SCREEN SKIP.
PARAMETERS:     p_slt_cp AS CHECKBOX.
SELECTION-SCREEN  END OF BLOCK slt.
* Reclass 3111/3115/3116
SELECTION-SCREEN  BEGIN OF BLOCK recl  WITH FRAME  TITLE text-s03.
PARAMETERS:     p_rec_pf TYPE monat  OBLIGATORY,
                p_rec_pt TYPE monat  MODIF ID nup,
                p_rec_y  TYPE gjahr  MODIF ID nup.
SELECTION-SCREEN SKIP.
PARAMETERS:     p_rec_cp AS CHECKBOX  MODIF ID nup.
SELECTION-SCREEN  END OF BLOCK recl.
* G/L Accounts
SELECTION-SCREEN  BEGIN OF BLOCK acc  WITH FRAME  TITLE text-s04.
PARAMETERS:     p_acc1     TYPE hkont  DEFAULT '0002997901'  OBLIGATORY
                                       MODIF ID nup,
                p_acc2     TYPE hkont  DEFAULT '0001780002'  OBLIGATORY
                                       MODIF ID nup,
                p_acc3     TYPE hkont  DEFAULT '0002398001'  OBLIGATORY
                                       MODIF ID nup,
                p_acc4     TYPE hkont  DEFAULT '0003210911'  OBLIGATORY
                                       MODIF ID nup,
                p_acc5     TYPE hkont  DEFAULT '0003290901'  OBLIGATORY
                                       MODIF ID nup,
                p_acc6     TYPE hkont  DEFAULT '0003292901'  OBLIGATORY
                                       MODIF ID nup.
SELECTION-SCREEN  END OF BLOCK acc.


*----------------------------------------------------------------------*
* On the selection screen                                              *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-group1 = 'NUP'.
      screen-input = 0.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON p_bukrs.

  PERFORM  check_authorization.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

  p_rec_pt = p_slt_p.
  p_rec_y  = p_slt_y.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*

INITIALIZATION.

  CLEAR p_rec_cp.

  p_rec_pf = 1.
  p_slt_p = p_rec_pt = sy-datum+4(2).
  p_slt_y = p_rec_y  = sy-datum(4).


*----------------------------------------------------------------------*
START-OF-SELECTION.

  CLEAR p_rec_cp.

  PERFORM initial.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_vbap[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No contracts selected'(e01).
    EXIT.
  ENDIF.

* Process data
  PERFORM process_data.

** Postings : Reclass GPS code 3111
*  IF NOT p_rec_cp IS INITIAL.
*    PERFORM postings_reclass.
*  ENDIF.

* Postings : Split Deferred Revenue (LT/ST)
  IF NOT p_slt_cp IS INITIAL.
    PERFORM postings_split.
  ENDIF.

* List data
  PERFORM list_data.


*----------------------------------------------------------------------*
TOP-OF-PAGE.

  WRITE: /01 'Date:', sy-datum,
          40 'Split Deferred Revenue (LT/ST) / Reclass GPS code 3111 (SEED)',
         120 'Company:', p_bukrs,
         167(4) sy-pagno.
  ULINE.

  IF NOT gv_errlst IS INITIAL.
    WRITE: /01 'Errors'.
  ELSE.
    IF gv_detlst IS INITIAL.
      WRITE: /01 'Document',
              13 'Curr.',
              20 'Segment',
              32 'Assignment',
              52 '        Posted Value '.
    ELSE.
      WRITE: /01 'Contract',
              12 'Item',
              20 'Curr.',
              26 '     Billing Val.',
              44 '     Revenue Rec.',
              62 '     Def. RR 12m.',
              80 '     LT Def. Rev.',
              99 'Segment',
             111 '    CY Bill. Val.',
             130 'Segment',
             142 '     CY Rev. Rec.',
             161 'Segment'.
    ENDIF.
  ENDIF.

  ULINE.


*----------------------------------------------------------------------*
*       FORMS
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  AUTHORITY-CHECK OBJECT 'F_BKPF_BUK'
           ID 'ACTVT' DUMMY
           ID 'BUKRS' FIELD p_bukrs.

  IF sy-subrc = 4.
*     No authorisation to display data
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH p_bukrs.
    EXIT.
  ELSEIF sy-subrc <> 0.
*     Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
    EXIT.
  ENDIF.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  INITIAL
*&---------------------------------------------------------------------*
*       Initialization
*----------------------------------------------------------------------*
FORM initial .

* Company currency
  SELECT SINGLE waers INTO gv_waers_cc
         FROM t001
         WHERE bukrs = p_bukrs.
  IF sy-subrc NE 0.
  ENDIF.

* Exchange rate type
  CASE p_bukrs.
    WHEN 'MRUA'.
      gv_kurst = 'DRU'.
    WHEN 'POLA'.
      gv_kurst = 'D'.
    WHEN OTHERS.
      gv_kurst = 'EURX'.
  ENDCASE.

* Intermediate currency
  CASE p_bukrs.
    WHEN 'MRUA'.
      gv_waers_int = gv_waers_cc.
    WHEN 'POLA'.
      gv_waers_int = gv_waers_cc.
    WHEN OTHERS.
      gv_waers_int = 'EUR'.
  ENDCASE.

* First day of year
  gv_slt_by(4) = p_slt_y.
  gv_slt_by+4(4) = '0101'.

* First day of period
  gv_slt_bp(4) = p_rec_y.
  gv_slt_bp+4(2) = p_rec_pf.
  gv_slt_bp+6(2) = '01'.

* Get last day of the period
  gv_slt_ep(4) = p_rec_y.
  gv_slt_ep+4(2) = p_rec_pt.
  gv_slt_ep+6(2) = '01'.
  CALL FUNCTION 'DATE_GET_MONTH_LASTDAY'
    EXPORTING
      i_date = gv_slt_ep
    IMPORTING
      e_date = gv_slt_ep.

* First day of next period
  gv_slt_bnp = gv_slt_ep + 1.

* Invert current date
  CONVERT DATE gv_slt_ep INTO INVERTED-DATE gv_gdatu.

ENDFORM.                    " INITIAL

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Contracts
  SELECT h~vbeln i~posnr h~waerk i~netwr i~objnr
         INTO TABLE gt_vbap
         FROM vbak AS h
         INNER JOIN vbap AS i
                    ON h~vbeln = i~vbeln
         INNER JOIN veda AS c
                    ON h~vbeln = c~vbeln
         WHERE h~vbeln    IN so_vbeln
           AND h~bukrs_vf =  p_bukrs
           AND h~vbtyp    =  gc_g
           AND c~vposn    = 0
           AND c~vbegdat  LE gv_slt_ep
           AND c~venddat  GE gv_slt_by
           AND c~vkuegru  =  '  '.

  CHECK NOT gt_vbap[] IS INITIAL.

  SORT gt_vbap.

* Check contract data
  SELECT vbeln vposn vbegdat venddat vkuegru
         INTO TABLE gt_veda
         FROM veda
         FOR ALL ENTRIES IN gt_vbap
         WHERE vbeln = gt_vbap-vbeln
           AND vposn = gt_vbap-posnr.
* Canceled
  LOOP AT gt_veda WHERE vkuegru NE space.
    DELETE gt_vbap WHERE vbeln = gt_veda-vbeln
                     AND posnr = gt_veda-vposn.
  ENDLOOP.
* Not yet started
  LOOP AT gt_veda WHERE vbegdat > gv_slt_ep.
    DELETE gt_vbap WHERE vbeln = gt_veda-vbeln
                     AND posnr = gt_veda-vposn.
  ENDLOOP.
* Finished
  LOOP AT gt_veda WHERE venddat < gv_slt_by.
    DELETE gt_vbap WHERE vbeln = gt_veda-vbeln
                     AND posnr = gt_veda-vposn.
  ENDLOOP.
  FREE gt_veda.
*Begin of Insertion by MOD-002
 SELECT vbeln venddat INTO TABLE gt_veda1
         FROM veda
         FOR ALL ENTRIES IN gt_vbap
         WHERE vbeln = gt_vbap-vbeln.
*End of Insertion by MOD-002
* Billing values
  SELECT f~vbelv f~posnv f~vbeln f~posnn d~belnr d~gjahr d~buzei
          d~budat d~dmbtr d~wrbtr d~waers d~shkzg d~segment
         INTO TABLE gt_bsis
         FROM vbfa AS f
         INNER JOIN bkpf AS h
                    ON h~awkey = f~vbeln
         INNER JOIN bsis AS d
                    ON d~bukrs = h~bukrs AND
                       d~gjahr = h~gjahr AND
                       d~belnr = h~belnr
         FOR ALL ENTRIES IN gt_vbap
         WHERE f~vbelv   =  gt_vbap-vbeln
           AND f~posnv   =  gt_vbap-posnr
           AND f~vbtyp_n IN (gc_m, gc_n, gc_o, gc_p)
           AND h~bukrs   =  p_bukrs
           AND h~awtyp   =  gc_vbrk
           AND h~budat   LE gv_slt_ep.
  SELECT f~vbelv f~posnv f~vbeln f~posnn d~belnr d~gjahr d~buzei
          d~budat d~dmbtr d~wrbtr d~waers d~shkzg d~segment
         APPENDING TABLE gt_bsis
         FROM vbfa AS f
         INNER JOIN bkpf AS h
                    ON h~awkey = f~vbeln
         INNER JOIN bsas AS d
                    ON d~bukrs = h~bukrs AND
                       d~gjahr = h~gjahr AND
                       d~belnr = h~belnr
         FOR ALL ENTRIES IN gt_vbap
         WHERE f~vbelv   =  gt_vbap-vbeln
           AND f~posnv   =  gt_vbap-posnr
           AND f~vbtyp_n IN (gc_m, gc_n, gc_o, gc_p)
           AND d~bukrs   =  p_bukrs
           AND h~awtyp   =  gc_vbrk
           AND h~budat   LE gv_slt_ep.
* Remove duplicates
  gt_bsisi[] = gt_bsis[].
  SORT gt_bsisi BY vbeln posnr belnr gjahr buzei.
  DELETE ADJACENT DUPLICATES FROM gt_bsisi
         COMPARING vbeln posnr belnr gjahr buzei.
  SORT gt_bsisi.
  gt_bsis[] = gt_bsisi[].
  FREE gt_bsisi.
* Check contract item (via BSEG)
  SELECT belnr gjahr buzei vbel2 posn2 INTO TABLE gt_bseg
         FROM bseg
         FOR ALL ENTRIES IN gt_bsis
         WHERE bukrs = p_bukrs
           AND belnr = gt_bsis-belnr
           AND gjahr = gt_bsis-gjahr
           AND buzei = gt_bsis-buzei.
  LOOP AT gt_bsis.
    CLEAR gt_bseg.
    READ TABLE gt_bseg WITH TABLE KEY belnr = gt_bsis-belnr
                                      gjahr = gt_bsis-gjahr
                                      buzei = gt_bsis-buzei.
    IF gt_bseg-vbel2 NE gt_bsis-vbeln  OR
       gt_bseg-posn2 NE gt_bsis-posnr.
      DELETE gt_bsis.
    ENDIF.
  ENDLOOP.
  FREE gt_bseg.

* Revenue
  SELECT ci~objnr ch~budat ci~twaer ci~wtgbtr ci~wogbtr
         ci~belnr ci~buzei
         INTO TABLE gt_coep
         FROM coep AS ci
         INNER JOIN cobk AS ch
                    ON ci~kokrs = ch~kokrs  AND
                       ci~belnr = ch~belnr
         FOR ALL ENTRIES IN gt_vbap
         WHERE ci~objnr =  gt_vbap-objnr
           AND ci~wrttp =  '04'             "Actual
           AND ci~lednr =  '00'
           AND ci~versn =  '000'
           AND ci~beknz IN ('H', 'S')
           AND ci~beltp = '2'
           AND ch~budat LE gv_slt_ep.

* Billing plan
  SELECT so~vbeln so~posnr so~fplnr
         bp~fpart bp~bedat bp~endat
         INTO TABLE gt_fpla
         FROM vbkd AS so
         INNER JOIN fpla AS bp
                    ON so~fplnr = bp~fplnr
         FOR ALL ENTRIES IN gt_vbap
         WHERE so~vbeln =  gt_vbap-vbeln
           AND so~posnr =  gt_vbap-posnr.

* Build range for assignments (contract nr + item)
  r_zuonr-sign = 'I'.
  r_zuonr-option = 'EQ'.
  LOOP AT gt_vbap.
    CONCATENATE gt_vbap-vbeln gt_vbap-posnr INTO r_zuonr-low.
    APPEND r_zuonr.
  ENDLOOP.

* Segment
* Begin of deletion by MOD-001
*  SELECT zuonr blart hkont budat belnr gjahr segment
*         INTO TABLE gt_segmi
*         FROM bsis
*         WHERE bukrs =  p_bukrs
*           AND hkont IN (p_acc1, p_acc2)
*           AND zuonr IN r_zuonr
*           AND blart IN (gc_sa, gc_zz)
*           AND budat BETWEEN gv_slt_by AND gv_slt_ep.
*  SELECT zuonr blart hkont budat belnr gjahr segment
*         APPENDING TABLE gt_segmi
*         FROM bsas
*         WHERE bukrs =  p_bukrs
*           AND hkont IN (p_acc1, p_acc2)
*           AND zuonr IN r_zuonr
*           AND blart IN (gc_sa, gc_zz)
*           AND budat BETWEEN gv_slt_by AND gv_slt_ep.
*  DELETE gt_segmi WHERE segment IS INITIAL.
* End of deletion by MOD-001

* Begin of insertion by MOD-001
  IF NOT r_zuonr[] IS INITIAL.
    SELECT zuonr     " Assignment Number
           blart     " Document Type
           hkont     " General Ledger Account
           budat     " Posting Date in the Document
           belnr     " Accounting Document Number
           gjahr     " Fiscal Year
           segment   " Segment for Segmental Reporting
           INTO TABLE gt_segmi
           FROM bsis " Accounting: Secondary Index for G/L Accounts
           FOR ALL ENTRIES IN r_zuonr
           WHERE bukrs =  p_bukrs
             AND hkont IN (p_acc1, p_acc2)
             AND zuonr = r_zuonr-low
             AND blart IN (gc_sa, gc_zz)
             AND budat BETWEEN gv_slt_by AND gv_slt_ep.

    SELECT zuonr     " Assignment Number
           blart     " Document Type
           hkont     " General Ledger Account
           budat     " Posting Date in the Document
           belnr     " Accounting Document Number
           gjahr     " Fiscal Year
           segment   " Segment for Segmental Reporting
           APPENDING TABLE gt_segmi
           FROM bsas " Accounting: Secondary Index for G/L Accounts (Cleared Items)
           FOR ALL ENTRIES IN r_zuonr
           WHERE bukrs =  p_bukrs
             AND hkont IN (p_acc1, p_acc2)
             AND zuonr = r_zuonr-low
             AND blart IN (gc_sa, gc_zz)
             AND budat BETWEEN gv_slt_by AND gv_slt_ep.
  ELSE. " ELSE -> IF NOT r_zuonr[] IS INITIAL
    SELECT zuonr     " Assignment Number
           blart     " Document Type
           hkont     " General Ledger Account
           budat     " Posting Date in the Document
           belnr     " Accounting Document Number
           gjahr     " Fiscal Year
           segment   " Segment for Segmental Reporting
           INTO TABLE gt_segmi
           FROM bsis " Accounting: Secondary Index for G/L Accounts
           WHERE bukrs =  p_bukrs
             AND hkont IN (p_acc1, p_acc2)
             AND zuonr IN r_zuonr
             AND blart IN (gc_sa, gc_zz)
             AND budat BETWEEN gv_slt_by AND gv_slt_ep.
    SELECT zuonr     " Assignment Number
           blart     " Document Type
           hkont     " General Ledger Account
           budat     " Posting Date in the Document
           belnr     " Accounting Document Number
           gjahr     " Fiscal Year
           segment   " Segment for Segmental Reporting
           APPENDING TABLE gt_segmi
           FROM bsas " Accounting: Secondary Index for G/L Accounts (Cleared Items)
           WHERE bukrs =  p_bukrs
             AND hkont IN (p_acc1, p_acc2)
             AND zuonr IN r_zuonr
             AND blart IN (gc_sa, gc_zz)
             AND budat BETWEEN gv_slt_by AND gv_slt_ep.
  ENDIF. " IF NOT r_zuonr[] IS INITIAL
* End of insertion by MOD-001
  DELETE gt_segmi WHERE segment IS INITIAL.

* Get the most recent documents per contract item
  SORT gt_segmi BY zuonr blart DESCENDING hkont DESCENDING budat DESCENDING.
  DELETE ADJACENT DUPLICATES FROM gt_segmi
         COMPARING zuonr blart hkont.
  gt_segm[] = gt_segmi[].
  FREE gt_segmi.

* Exchange rates
  SELECT kurst fcurr tcurr gdatu ukurs INTO TABLE gt_curri
         FROM tcurr
         WHERE kurst =  gv_kurst
           AND tcurr =  gv_waers_int
           AND gdatu GE gv_gdatu.
  SORT gt_curri BY kurst fcurr tcurr gdatu.
  DELETE ADJACENT DUPLICATES FROM gt_curri COMPARING kurst fcurr tcurr.
  gt_curr[] = gt_curri[].
  FREE gt_curri.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  PROCESS_DATA
*&---------------------------------------------------------------------*
*       Process data
*----------------------------------------------------------------------*
FORM process_data .

  LOOP AT gt_vbap.

    CLEAR gt_sel.
    MOVE-CORRESPONDING gt_vbap TO gt_sel.
    gt_sel-waers      = gv_waers_cc.

    gv_zuonr(10)   = gt_vbap-vbeln.
    gv_zuonr+10(6) = gt_vbap-posnr.

*   Billing Value
    CLEAR: gv_bil_lc,
           gv_bilcy_lc.
    LOOP AT gt_bsis WHERE vbeln = gt_vbap-vbeln
                      AND posnr = gt_vbap-posnr.
      IF gt_bsis-shkzg = 'S'.
        gv_bil_lc = gv_bil_lc - gt_bsis-dmbtr.
      ELSE.
        gv_bil_lc = gv_bil_lc + gt_bsis-dmbtr.
      ENDIF.
      IF gt_bsis-budat BETWEEN gv_slt_bp AND gv_slt_ep.
        IF gt_bsis-shkzg = 'S'.
          gv_bilcy_lc = gv_bilcy_lc - gt_bsis-dmbtr.
        ELSE.
          gv_bilcy_lc = gv_bilcy_lc + gt_bsis-dmbtr.
        ENDIF.
      ENDIF.
    ENDLOOP.
    gt_sel-bil_val_lc = gv_bil_lc.

*   Total Revenue Recognized
    CLEAR: gv_revr_lc,
           gv_revrcy_lc.
    LOOP AT gt_coep WHERE objnr = gt_vbap-objnr.
      gv_revr_lc = gv_revr_lc - gt_coep-wogbtr.
      IF gt_coep-budat BETWEEN gv_slt_bp AND gv_slt_ep.
        gv_revrcy_lc = gv_revrcy_lc - gt_coep-wogbtr.
      ENDIF.
    ENDLOOP.
    gt_sel-rev_rec_lc = gv_revr_lc.

*   Total days
    CLEAR gv_days_settl.
    READ TABLE gt_fpla WITH TABLE KEY vbeln = gt_vbap-vbeln
                                      posnr = gt_vbap-posnr.
    IF sy-subrc = 0
       AND gt_fpla-fpart NE gv_fpart. "+MOD-002
      gv_days_settl = gt_fpla-endat - gt_fpla-bedat.
    ENDIF.
*    Begin of Insertion by MOD-002
    IF gt_fpla-fpart EQ gv_fpart.
      READ TABLE gt_veda1 WITH KEY vbeln = gt_vbap-vbeln.
      IF sy-subrc = 0.
        gv_days_settl = gt_veda1-venddat - gt_fpla-bedat.
      ENDIF.
    ENDIF.
*  End of Insertion by MOD-002
    IF gv_days_settl = 0.
      gv_days_settl = 1.
    ENDIF.

*   Def RR 12 months
    CLEAR: gv_defr_so, gv_defr_lc, gv_diff_lc,
           gv_ltdefr_lc.
    gv_defr_so = ( gt_vbap-netwr * 365 ) / gv_days_settl.
    PERFORM convert_amount_int USING gv_defr_so
                                     gt_vbap-waerk
                                     gv_defr_lc
                                     gv_waers_cc.
    gt_sel-def_rr_lc  = gv_defr_lc.

    gv_diff_lc = gv_bil_lc - gv_revr_lc.

    IF gv_bil_lc > gv_revr_lc  AND
       gv_diff_lc > gv_defr_lc.
      gv_ltdefr_lc = gv_diff_lc - gv_defr_lc.
    ENDIF.

    gt_sel-lt_defr_lc = gv_ltdefr_lc.
    READ TABLE gt_segm WITH TABLE KEY zuonr = gv_zuonr
                                      blart = gc_zz
                                      hkont = p_acc1.
    IF sy-subrc = 0.
      gt_sel-segment = gt_segm-segment.
    ENDIF.

    gt_sel-bil_cy_lc  = gv_bilcy_lc.

    gt_sel-revr_cy_lc = gv_revrcy_lc.
    READ TABLE gt_segm WITH TABLE KEY zuonr = gv_zuonr
                                       blart = gc_zz
                                       hkont = p_acc1.
    IF sy-subrc = 0.
      gt_sel-segm_rcy = gt_segm-segment.
    ELSE.
      READ TABLE gt_segm WITH TABLE KEY zuonr = gv_zuonr
                                         blart = gc_zz
                                         hkont = p_acc2.
      IF sy-subrc = 0.
        gt_sel-segm_rcy = gt_segm-segment.
      ELSE.
        READ TABLE gt_segm WITH TABLE KEY zuonr = gv_zuonr
                                           blart = gc_sa
                                           hkont = p_acc1.
        IF sy-subrc = 0.
          gt_sel-segm_rcy = gt_segm-segment.
        ELSE.
          READ TABLE gt_segm WITH TABLE KEY zuonr = gv_zuonr
                                             blart = gc_sa
                                             hkont = p_acc2.
          IF sy-subrc = 0.
            gt_sel-segm_rcy = gt_segm-segment.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.
    gt_sel-segm_bcy = gt_sel-segm_rcy.

    APPEND gt_sel.

  ENDLOOP.

ENDFORM.                    " PROCESS_DATA

*&---------------------------------------------------------------------*
*&      Form  CONVERT_AMOUNT_INT
*&---------------------------------------------------------------------*
*       Convert amount via intermediate currency
*----------------------------------------------------------------------*
*      -->P_KWERT_IN  : Given Value
*      -->P_WAERS_IN  : Given Currency
*      -->P_KWERT_OUT : Converted Value
*      -->P_WAERS_RES : Result Currency
*----------------------------------------------------------------------*
FORM convert_amount_int  USING    p_kwert_in
                                  p_waers_in
                                  p_kwert_out
                                  p_waers_res.

  DATA: lv_value_int   TYPE wtgxxx.

* Input Currency = Result currency -> NO conversion
  IF p_waers_in = p_waers_res.
    p_kwert_out = p_kwert_in.
    RETURN.
  ENDIF.

* Intermediate exchange rate
  IF p_waers_in = gv_waers_int.
    lv_value_int = p_kwert_in.
  ELSE.
    CLEAR gt_curr.
    READ TABLE gt_curr WITH TABLE KEY kurst = gv_kurst
                                      fcurr = p_waers_in
                                      tcurr = gv_waers_int.
    CHECK sy-subrc = 0.
*   Calculate ammount in intermediate currency
    IF gt_curr-ukurs = 0.
      gt_curr-ukurs = 1.
    ENDIF.
    lv_value_int = p_kwert_in * gt_curr-ukurs.
  ENDIF.

* Result exchange rate
  IF p_waers_res = gv_waers_int.
    p_kwert_out = lv_value_int.
  ELSE.
    CLEAR gt_curr.
    READ TABLE gt_curr WITH TABLE KEY kurst = gv_kurst
                                      fcurr = p_waers_res
                                      tcurr = gv_waers_int.
    CHECK sy-subrc = 0.
*   Calculate ammount in result currency
    IF gt_curr-ukurs = 0.
      gt_curr-ukurs = 1.
    ENDIF.
    p_kwert_out = lv_value_int / gt_curr-ukurs.
  ENDIF.

ENDFORM.                    " CONVERT_AMOUNT_INT

*&---------------------------------------------------------------------*
*&      Form  POSTINGS_SPLIT
*&---------------------------------------------------------------------*
*       Postings : Split Deferred Revenue (LT/ST)
*----------------------------------------------------------------------*
FORM postings_split .

  CHECK NOT gt_sel[] IS INITIAL.

* Check if next period is open
  gv_yp_post = gv_slt_bnp(6).
  CLEAR gv_error.
* All account types
  PERFORM check_open_period USING '+'
                                  '          '.
  CHECK gv_error IS INITIAL.
* G/L accounts
  PERFORM check_open_period USING 'S'
                                  'ZZZZZZZZZZ'.
  CHECK gv_error IS INITIAL.

* Open accounts
  gv_xintb = ' '.
  gv_hkont = p_acc1.
  PERFORM call_fss0.

  gv_xblnr = 'LT/ST Split'.
  gv_zuonr = 'LT/ST Split'.

  SORT gt_sel BY segment.

  LOOP AT gt_sel WHERE NOT segment IS INITIAL
                   AND lt_defr_lc NE 0.
    MOVE-CORRESPONDING gt_sel TO gt_split.
    APPEND gt_split.
  ENDLOOP.

  SORT gt_split.

  LOOP AT gt_split INTO wa_split.
    AT END OF segment.
      SUM.
      gv_ltdefr_lc = wa_split-lt_defr_lc.
      IF gv_ltdefr_lc NE 0.
*       Call transaction FB01 : Split
        PERFORM call_fb01 USING wa_split-waers
                                gv_ltdefr_lc
                                p_acc1
                                p_acc3
                                wa_split-segment
                                gv_slt_ep.
        IF gv_error IS INITIAL.
*         Call transaction FB01 : Reverse
          PERFORM call_fb01 USING wa_split-waers
                                  gv_ltdefr_lc
                                  p_acc3
                                  p_acc1
                                  wa_split-segment
                                  gv_slt_bnp.
        ENDIF.
      ENDIF.
    ENDAT.

  ENDLOOP.

* close accounts
  gv_xintb = 'X'.
  gv_hkont = p_acc1.
  PERFORM call_fss0.

  FREE gt_split.

ENDFORM.                    " POSTINGS_SPLIT

*&---------------------------------------------------------------------*
*&      Form  CHECK_OPEN_PERIOD
*&---------------------------------------------------------------------*
*       Check if period is open for posting
*----------------------------------------------------------------------*
*      -->P_MKOAR : Type of account
*      -->P_BKONT : Account (TO)
*----------------------------------------------------------------------*
FORM check_open_period  USING    p_mkoar
                                 p_bkont.

  SELECT SINGLE * FROM t001b
         WHERE rrcty = '0'
           AND bukrs = p_bukrs
           AND mkoar = p_mkoar
           AND bkont = p_bkont.

  IF sy-subrc NE 0.
    gv_error = 'X'.
  ELSE.
    gv_yp_open(4)   = t001b-toye1.
    gv_yp_open+4(2) = t001b-tope1+1(2).
    IF gv_yp_post > gv_yp_open.
      gv_error = 'X'.
    ENDIF.
  ENDIF.

  IF gv_error = 'X'.
    WRITE: / 'Period', gv_yp_post(4), gv_yp_post+4(2), 'not open for',
             p_bukrs, p_mkoar.
    ULINE.
  ENDIF.

ENDFORM.                    " CHECK_OPEN_PERIOD

*&---------------------------------------------------------------------*
*&      Form  POSTINGS_RECLASS
*&---------------------------------------------------------------------*
*       Postings : Reclass GPS code 3111
*----------------------------------------------------------------------*
FORM postings_reclass .

  CHECK NOT gt_sel[] IS INITIAL.

* Open accounts
  gv_xintb = ' '.
  gv_hkont = p_acc4.
  PERFORM call_fss0.
  gv_hkont = p_acc5.
  PERFORM call_fss0.
  gv_hkont = p_acc6.
  PERFORM call_fss0.

* Reclass Billing Value 3111_3115
  gv_xblnr = 'Recl.3115'.
  gv_zuonr = 'Recl.Bill.Val.3115'.

  SORT gt_sel BY segm_bcy.

  LOOP AT gt_sel WHERE NOT segm_bcy IS INITIAL
                   AND bil_cy_lc  NE 0.
    MOVE-CORRESPONDING gt_sel TO gt_3115.
    APPEND gt_3115.
  ENDLOOP.

  SORT gt_3115.

  LOOP AT gt_3115 INTO wa_3115.

    AT END OF segm_bcy.
      SUM.
      gv_bilcy_lc = wa_3115-bil_cy_lc.
      IF gv_bilcy_lc NE 0.
*       Call transaction FB01 : Reclass 3115
        PERFORM call_fb01 USING wa_3115-waers
                                gv_bilcy_lc
                                p_acc5
                                p_acc4
                                wa_3115-segm_bcy
                                gv_slt_ep.
      ENDIF.
    ENDAT.

  ENDLOOP.

  FREE gt_3115.

* Reclass RR Value 3111_3116
  gv_xblnr = 'Recl.3116'.
  gv_zuonr = 'Recl.RR.Val.3116'.

  SORT gt_sel BY segm_rcy.

  LOOP AT gt_sel WHERE NOT segm_rcy IS INITIAL
                   AND revr_cy_lc NE 0.
    MOVE-CORRESPONDING gt_sel TO gt_3116.
    APPEND gt_3116.
  ENDLOOP.

  SORT gt_3116.

  LOOP AT gt_3116 INTO wa_3116.

    AT END OF segm_rcy.
      SUM.
      gv_revrcy_lc = wa_3116-revr_cy_lc.
      IF gv_revrcy_lc NE 0.
*       Call transaction FB01 : Reclass 3116
        PERFORM call_fb01 USING wa_3116-waers
                                gv_revrcy_lc
                                p_acc4
                                p_acc6
                                wa_3116-segm_rcy
                                gv_slt_ep.
      ENDIF.
    ENDAT.

  ENDLOOP.

* Close accounts
  gv_xintb = 'X'.
  gv_hkont = p_acc4.
  PERFORM call_fss0.
  gv_hkont = p_acc5.
  PERFORM call_fss0.
  gv_hkont = p_acc6.
  PERFORM call_fss0.

  FREE gt_3116.

ENDFORM.                    " POSTINGS_RECLASS

*&---------------------------------------------------------------------*
*&      Form  CALL_FB01
*&---------------------------------------------------------------------*
*       Prepare & Call transaction FB01
*----------------------------------------------------------------------*
*      -->P_WAERS    : Currency (Local)
*      -->P_VALUE_LC : Value in Local Currency
*      -->P_ACCF     : Account From (40)
*      -->P_ACCT     : Account to (50)
*      -->P_SEGMENT  : Segment
*----------------------------------------------------------------------*
FORM call_fb01  USING    p_waers
                         p_value_lc
                         p_accf
                         p_acct
                         p_segment
                         p_date.

  DATA: lv_date(10)     TYPE c,
        lv_wrbtr_lc(16) TYPE c.


  REFRESH: i_bdcdata, gt_err.
  CLEAR: gv_error.

  WRITE p_date TO lv_date.
  WRITE p_value_lc TO lv_wrbtr_lc
                      CURRENCY p_waers NO-SIGN.

* Header
  PERFORM bdc_dynpro      USING 'SAPMF05A' '0100'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '/00'.
  PERFORM bdc_field       USING 'BKPF-BLDAT' lv_date.
  PERFORM bdc_field       USING 'BKPF-BLART' gc_sa.
  PERFORM bdc_field       USING 'BKPF-BUKRS' p_bukrs.
  PERFORM bdc_field       USING 'BKPF-BUDAT' lv_date.
  PERFORM bdc_field       USING 'BKPF-WAERS' p_waers.
  PERFORM bdc_field       USING 'BKPF-XBLNR' gv_xblnr.
  PERFORM bdc_field       USING 'RF05A-NEWBS' '40'.
  PERFORM bdc_field       USING 'RF05A-NEWKO' p_accf.

* Item 1
  PERFORM bdc_dynpro      USING 'SAPMF05A' '0300'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=ZK'.
  PERFORM bdc_field       USING 'BSEG-WRBTR' lv_wrbtr_lc.
  IF p_accf = p_acc1.
    PERFORM bdc_field       USING 'BSEG-MWSKZ' gc_zs.
  ENDIF.
  PERFORM bdc_field       USING 'BSEG-ZUONR' gv_zuonr.
  PERFORM bdc_field       USING 'DKACB-FMORE' 'X'.

* Item 1: more date
  PERFORM bdc_dynpro      USING 'SAPLKACB' '0002'.
  PERFORM bdc_field       USING 'BDC_OKCODE' 'ENTE'.
  PERFORM bdc_field       USING 'COBL-SEGMENT' p_segment.

* Item 2
  PERFORM bdc_dynpro      USING 'SAPMF05A' '0330'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '/00'.

  PERFORM bdc_field       USING 'RF05A-NEWBS' '50'.
  PERFORM bdc_field       USING 'RF05A-NEWKO' p_acct.

  PERFORM bdc_dynpro      USING 'SAPMF05A' '0300'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=ZK'.
  PERFORM bdc_field       USING 'BSEG-WRBTR' lv_wrbtr_lc.
  IF p_acct = p_acc1.
    PERFORM bdc_field       USING 'BSEG-MWSKZ' gc_zs.
  ENDIF.
  PERFORM bdc_field       USING 'BSEG-ZUONR' gv_zuonr.
  PERFORM bdc_field       USING 'DKACB-FMORE' 'X'.

* Item 2: more date
  PERFORM bdc_dynpro      USING 'SAPLKACB' '0002'.
  PERFORM bdc_field       USING 'BDC_OKCODE' 'ENTE'.
  PERFORM bdc_field       USING 'COBL-SEGMENT' p_segment.

* Save
  PERFORM bdc_dynpro      USING 'SAPMF05A' '0330'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=BU'.

  CALL TRANSACTION 'FB01' USING i_bdcdata
                          MODE gv_mode
                          UPDATE 'S'
                          MESSAGES INTO gt_err.

* Check for errors
  LOOP AT gt_err WHERE msgtyp = 'E'
                    OR msgtyp = 'A'.
    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*        SPRSL                   = SY-LANGU
        arbgb                   = gt_err-msgid
        msgnr                   = gt_err-msgnr
        msgv1                   = gt_err-msgv1
        msgv2                   = gt_err-msgv2
        msgv3                   = gt_err-msgv3
        msgv4                   = gt_err-msgv4
      IMPORTING
        msgtext                 = gv_msgtxt
      EXCEPTIONS
        message_not_found       = 1
        OTHERS                  = 2.

    WRITE: / gv_msgtxt.
  ENDLOOP.

* No errors
  IF sy-subrc NE 0.
*   Get document number
*    GET PARAMETER ID 'BLN' FIELD gt_doc-belnr.
    READ TABLE gt_err WITH KEY msgid = 'F5'
                               msgnr = '312'.
    IF sy-subrc NE 0.
      WRITE: / 'No posting made for', p_waers, p_segment,
                                      gv_zuonr, p_value_lc.
      gv_error = 'X'.
    ELSE.
      CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
        EXPORTING
          input  = gt_err-msgv1
        IMPORTING
          output = gt_doc-belnr.
      gt_doc-waers   = p_waers.
      gt_doc-segment = p_segment.
      gt_doc-zuonr   = gv_zuonr.
      gt_doc-val_lc  = p_value_lc.
      APPEND gt_doc.
    ENDIF.
  ENDIF.

ENDFORM.                    " CALL_FB01

*&---------------------------------------------------------------------*
*&      Form  CALL_FSS0
*&---------------------------------------------------------------------*
*       Prepare & Call transaction FSS0
*----------------------------------------------------------------------*
FORM call_fss0 .

  REFRESH: i_bdcdata, gt_err.

  PERFORM bdc_dynpro      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=ACC_MOD'.
  PERFORM bdc_field       USING 'GLACCOUNT_SCREEN_KEY-SAKNR' gv_hkont.
  PERFORM bdc_field       USING 'GLACCOUNT_SCREEN_KEY-BUKRS' p_bukrs.

  PERFORM bdc_dynpro      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=TAB02'.

  PERFORM bdc_dynpro      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=SAVE'.
  PERFORM bdc_field       USING 'GLACCOUNT_SCREEN_CCODE-XINTB' gv_xintb.

  CALL TRANSACTION 'FSS0' USING i_bdcdata
                          MODE gv_mode
                          UPDATE 'S'
                          MESSAGES INTO gt_err.

* Check for errors
  LOOP AT gt_err WHERE msgtyp = 'E'
                    OR msgtyp = 'A'.
    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*        SPRSL                   = SY-LANGU
        arbgb                   = gt_err-msgid
        msgnr                   = gt_err-msgnr
        msgv1                   = gt_err-msgv1
        msgv2                   = gt_err-msgv2
        msgv3                   = gt_err-msgv3
        msgv4                   = gt_err-msgv4
      IMPORTING
        msgtext                 = gv_msgtxt
      EXCEPTIONS
        message_not_found       = 1
        OTHERS                  = 2.

    WRITE: / gv_msgtxt.
  ENDLOOP.

ENDFORM.                    " CALL_FSS0

*----------------------------------------------------------------------*
*        Start new screen                                              *
*----------------------------------------------------------------------*
FORM bdc_dynpro USING program dynpro.

  CLEAR i_bdcdata.
  i_bdcdata-program  = program.
  i_bdcdata-dynpro   = dynpro.
  i_bdcdata-dynbegin = 'X'.
  APPEND i_bdcdata.

ENDFORM.                    "BDC_DYNPRO

*----------------------------------------------------------------------*
*        Insert field                                                  *
*----------------------------------------------------------------------*
FORM bdc_field USING fnam fval.

  CLEAR i_bdcdata.
  i_bdcdata-fnam = fnam.
  i_bdcdata-fval = fval.
  APPEND i_bdcdata.

ENDFORM.                    "bdc_field

*&---------------------------------------------------------------------*
*&      Form  LIST_DATA
*&---------------------------------------------------------------------*
*       List data
*----------------------------------------------------------------------*
FORM list_data .

  CLEAR gv_errlst.
  NEW-PAGE.

  IF gt_doc[] IS INITIAL.
    WRITE: /01 'NO documents posted'.
  ELSE.
*   Posted documents
    LOOP AT gt_doc.
      WRITE: /01 gt_doc-belnr,
              13 gt_doc-waers,
              20 gt_doc-segment,
              32 gt_doc-zuonr,
              52(20) gt_doc-val_lc CURRENCY gv_waers_cc.
    ENDLOOP.
  ENDIF.

  ULINE.

* Detail list
  IF NOT p_detail IS INITIAL.
    gv_detlst = 'X'.
    NEW-PAGE.

    IF gt_sel[] IS INITIAL.
      WRITE: /01 'No amounts calculated (to be posted)'(i01).
      ULINE.
      RETURN.
    ENDIF.

    SORT gt_sel.

    LOOP AT gt_sel.
      WRITE: /01     gt_sel-vbeln,
              12     gt_sel-posnr,
              20     gt_sel-waers,            "Local curr.
              26(17) gt_sel-bil_val_lc  CURRENCY gt_sel-waers,
              44(17) gt_sel-rev_rec_lc  CURRENCY gt_sel-waers,
              62(17) gt_sel-def_rr_lc   CURRENCY gt_sel-waers,
              80(17) gt_sel-lt_defr_lc  CURRENCY gt_sel-waers,
              99     gt_sel-segment,
             111(17) gt_sel-bil_cy_lc   CURRENCY gt_sel-waers,
             130     gt_sel-segm_bcy,
             142(17) gt_sel-revr_cy_lc  CURRENCY gt_sel-waers,
             161    gt_sel-segm_rcy.
    ENDLOOP.

    ULINE.
  ENDIF.

ENDFORM.                    " LIST_DATA

*Text symbol text£º
*I01:No amounts calculated (to be posted)
*S01:Selection
*S02:Def. Rev. Short-Long Term
*S03:Reclass 3111/3115/3116

*S04:G/L Accounts
*Selection text£º
*P_ACC1:        G/L Account 1
*P_ACC2:        G/L Account 2
*P_ACC3:        G/L Account 3
*P_ACC4:        G/L Account 4
*P_ACC5:        G/L Account 5
*P_ACC6:        G/L Account 6
*P_BUKRS:D       .
*P_DETAIL:        List Detail
*P_REC_CP:        Create postings
*P_REC_PF:        Posting period From
*P_REC_PT:        To
*P_REC_Y:        Year
*P_SLT_CP:        Create postings
*P_SLT_P:        Posting period
*P_SLT_Y:        Year
*SO_VBELN:        Contract
