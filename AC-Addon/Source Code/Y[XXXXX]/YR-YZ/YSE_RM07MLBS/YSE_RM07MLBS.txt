************************************************************************
*     REPORT RM07MLBS   (Transaction MB52)
*
*     History:
* Aug 2013 EH                                               "n1895376
* - corrected display val. blocked stock for E/Q non-batch  "n1895376
* - no value for V,W,O in special circumstances             "n1895376
* - some perfomance optimization by replacing reads         "n1895376
*
* Jan 2013 EH                                               "n1795093
* - optimzed DB access/ new select statement for stocks     "n1795093
*
* new function Nov 2012 MR                                  "n1710852
* - added secondary database connection                     "n1710852
*
* February 2009 RR                                          "1311282
* added field LGOBE into output list with parameter         "1311282
* "c-no-out" in form F0300_FIELDCAT_FLAT.                   "1311282
*
* July 2006 MS                                              "n960980
* changed output of first line in print modus. If you enter "n960980
* user commands after function print, you get the list in   "n960980
* print modus view.                                         "n960980
*
* Jan. 2006 MS                                              "n912093
* added fields GLGMG,WGLGM,UMLMC,WUMLC,TRAME,WTRAM          "n912093
* into output list with parameter "c-no-out"                "n912093
* corrected documentation for field "Stock in transfer"     "n912093
* down with release 6.0                                     "n912093
* added dynamic break-points from cp-group MMIM_REP_MB52    "n912093

* M#rz 2005 TW                                              "n829722
* authorization check should be always processed            "n829722
*
* Nov. 2004 MM :                                         "AC0K020254
* origin acceptance : process valuated block GR          "AC0K020254
* stock MARC-BWESB as hidden field                       "AC0K020254

* July 2004 MM                                              "n759412
*  Avoid shortdump DBIF_RSQL_INVALID_RSQL
*
* Improvements :                       Nov 2003 TW          "n667256
* - enable Webreporting                                     "n667256
* - print the page numbers                                  "n667256
* - use function module for database commit for the update  "n667256
*   of the parameters in table ESDUS. This allows to record "n667256
*   this transaction for a batch input session using        "n667256
*   transaction SHDB                                        "n667256

* Feb. 2003 MM                                              "n577268
* ignore stocks or special stocks from plant level when     "n577268
* user selects via storage location                         "n577268

* Jan. 2003 MM                                              "n591618
* report displayed stocks for plant although the user has   "n591618
* not the required authorization for that plant             "n591618

* Dec. 2002 MM                                              "n579976
* M7 393 when user deletes the initial display variant      "n579976

*-----------------------------------------------------------"n546707
* note 546707                         August 19th, 2002 MM  "n546707
* wrong parameters for AUTHORITY-CHECK 'F_BKPF_BUK' fixed   "n546707
*-----------------------------------------------------------"n531604
*     performance improved  4.6 and higher   July 2002 MM   "n531604
*     - consider settings from view V_MMIM_REP_PRINT        "n531604
*     - choose flat or hierarchic list                      "n531604
*     - show the individual line of special stocks E and Q  "n531604
*     - authority-check for values per company code         "n531604
*     - work with a lean table ORGAN                        "n531604
*     - new function processing special stocks              "n531604
*     - new function of parameter XLVORM                    "n531604
*     - evaluate and show the flag for deletion             "n531604
*     - contents of parameters :                            "n531604
*       - in dialog mode : preset parameters from last run  "n531604
*       - save the parameters during each run               "n531604
*-----------------------------------------------------------"n531604
*
*     note 494306  FI help dokumentation for parameters improved
*                  Feb. 11th 2002 XJD
*
*     Note 155853: First version
*     Note 161442: Display variante possible again
*     Note 177898: MSKA/MSPR with duplicate entires, SELECT error
*     Note 182322: Error during authorization check for plant
*     Note 307852: Jump back to initial screen for empty selection
*     Note 311770: Batch selection
*     Note 353428: Value even for valuation area with 0 stock
*     Note 304353: New selection fields into the standard.
*     Note 388735: Abort COLLECT_OVERFLOW_TYPE_P due to note 353428
*     Note 407810: Typo-error causes unprecise values (follow up
*                  of note 388735).
*
************************************************************************
*     List of stock quantities and values
************************************************************************
REPORT yse_rm07mlbs MESSAGE-ID m7 NO STANDARD PAGE HEADING LINE-SIZE 170.
*ENHANCEMENT-POINT RM07MLBS_G4 SPOTS ES_RM07MLBS STATIC.
*ENHANCEMENT-POINT RM07MLBS_G5 SPOTS ES_RM07MLBS.
*ENHANCEMENT-POINT RM07MLBS_G6 SPOTS ES_RM07MLBS STATIC.
*ENHANCEMENT-POINT RM07MLBS_G7 SPOTS ES_RM07MLBS.

************************************************************************
* Data declarations
************************************************************************

* Type pools
TYPE-POOLS: slis, imrep.

* Database tables
TABLES: mara, makt, mard, mchb, mkol, mslb, mska, msku, mssa, mspr,
        mssq, mbew, ebew, qbew, t134m, t001w, t001l, marc, t001, t001k,
        t023, t024.

Tables : SSCRFIELDS.         "for the user-commands

* working table for the entries of all stock tables
  DATA: BEGIN OF collector OCCURS 0,
         matnr LIKE mara-matnr,
         werks LIKE t001w-werks,
         lgort LIKE mard-lgort,
         sobkz LIKE mkol-sobkz,
       bwtar LIKE mcha-bwtar,                               "1795093
         pspnr               like  mspr-pspnr,
         vbeln               like  mska-vbeln,
         posnr               like  mska-posnr,
         lifnr LIKE mslb-lifnr,
         kunnr LIKE msku-kunnr,
         lvorm               like  mard-lvorm,

         kzbws LIKE mssa-kzbws,
         charg LIKE mchb-charg,
         labst LIKE mard-labst,
         insme LIKE mard-insme,
         speme LIKE mard-speme,
         einme LIKE mard-einme,
         retme LIKE mard-retme,
         umlme LIKE mard-umlme,
         bwesb               like  marc-bwesb,           "AC0K020254
         glgmg LIKE marc-glgmg,                           "n912093
         trame LIKE marc-trame,                           "n912093
       umlmc LIKE marc-umlmc,                               "n912093
       maktx LIKE makt-maktx,                               "1795093
       xchar LIKE marc-xchar.                               "1795093
*ENHANCEMENT-POINT EHP604_RM07MLBS_01 SPOTS ES_RM07MLBS STATIC .
  DATA: END OF collector.

* Internal tables
DATA: BEGIN OF header OCCURS 0,
        matnr LIKE mara-matnr,
        maktx LIKE makt-maktx,
        werks LIKE t001w-werks,
        name1 LIKE t001w-name1,
        mtart LIKE mara-mtart,
        matkl LIKE mara-matkl.
*ENHANCEMENT-POINT EHP604_RM07MLBS_02 SPOTS ES_RM07MLBS STATIC .
DATA: END OF header.

DATA: BEGIN OF bestand OCCURS 0,
*        Key fields
         matnr LIKE mara-matnr,
         werks LIKE t001w-werks,
         lgort LIKE mard-lgort,
         sobkz LIKE mkol-sobkz,
         ssnum               like  bickey-ssnum,            "n531604
         pspnr               like  mspr-pspnr,              "n531604
         vbeln               like  mska-vbeln,              "n531604
         posnr               like  mska-posnr,              "n531604
         lifnr LIKE mkol-lifnr,
         kunnr LIKE msku-kunnr,
         kzbws LIKE mssa-kzbws,
         charg LIKE mchb-charg,
*        Additional data (texts, unit, ...)
         maktx LIKE marav-maktx,
         bwkey LIKE mbew-bwkey,
         mtart LIKE marav-mtart,
         matkl LIKE marav-matkl,
         meins LIKE marav-meins,
         bwtty LIKE marc-bwtty,
         xchar LIKE marc-xchar,
         lgobe LIKE t001l-lgobe,
         bwtar LIKE mcha-bwtar,
         waers LIKE t001-waers,
         name1 LIKE t001w-name1,
*        Quantities and currencies
         labst LIKE mard-labst,
         wlabs LIKE mbew-salk3,
         insme LIKE mard-insme,
         winsm LIKE mbew-salk3,
         speme LIKE mard-speme,
         wspem LIKE mbew-salk3,
         einme LIKE mard-einme,
         weinm LIKE mbew-salk3,
         retme LIKE mard-retme,
         wretm LIKE mbew-salk3,
         umlme LIKE mard-umlme,
         wumlm LIKE mbew-salk3,
         glgmg LIKE marc-glgmg,                             "n912093
         wglgm LIKE mbew-salk3,                             "n912093
         trame LIKE marc-trame,                             "n912093
         wtram LIKE mbew-salk3,                             "n912093
         umlmc LIKE marc-umlmc,                             "n912093
         wumlc LIKE mbew-salk3,                             "n912093

*        Dummy field
         dummy               TYPE  ALV_DUMMY,
*        Colour
         farbe TYPE slis_t_specialcol_alv,
         lvorm               like  mard-lvorm,

*        valuated blocked GR stock                       "AC0K020254
         bwesb               like  marc-bwesb,           "AC0K020254
         wbwesb              like  mbew-salk3.           "AC0K020254
*ENHANCEMENT-POINT EHP604_RM07MLBS_03 SPOTS ES_RM07MLBS STATIC .
DATA:  END OF bestand.

* define a lean table organ
types : begin of stype_organ,
          werks              like  t001w-werks,
          bwkey              like  t001w-bwkey,
          name1              like  t001w-name1,
          bukrs              like  t001-bukrs,
          waers              like  t001-waers,
        end of stype_organ,

        stab_organ           type standard table of
                             stype_organ
                             with default key.

DATA: g_t_organ              TYPE  stab_organ,
      g_s_organ              type  stype_organ.

* define a buffer table for the MARD entries with flag
* for deletion
types : begin of stype_mard_lv,
          matnr              like  mard-matnr,
          werks              like  mard-werks,
          lgort              like  mard-lgort,
          lvorm              like  mard-lvorm,
        end of stype_mard_lv,

        htab_mard_lv         type hashed table of
                             stype_mard_lv
                   with unique key matnr werks lgort.

data : g_s_mard_lv           type  stype_mard_lv,
       g_t_mard_lv           type  htab_mard_lv.

* define a buffer table for the storage bins
types : begin of stype_t001l,
          werks              like  t001l-werks,
          lgort              like  t001l-lgort,
          lgobe              like  t001l-lgobe,
        end of stype_t001l,

        htab_t001l           type hashed table of
                             stype_t001l
                             with unique key werks lgort.

data : g_s_t001l             type  stype_t001l,
       g_t_t001l             type  htab_t001l.

* define working areas for access table organ               "n531604
types : begin of stype_buffer,                              "n531604
         werks               like  t001w-werks,
         bukrs               like  t001-bukrs,
         subrc               like  syst-subrc,
       end of stype_buffer,

       stab_buffer           type standard table of
                             stype_buffer
                             with default key.

data : g_s_buffer          type  stype_buffer,
       g_t_buffer          type  stab_buffer.

* Data for listviewer
DATA: repid     LIKE sy-repid.
DATA: fieldcat  TYPE slis_t_fieldcat_alv WITH HEADER LINE.
DATA: keyinfo   TYPE slis_keyinfo_alv.
DATA: color     TYPE slis_t_specialcol_alv WITH HEADER LINE.
DATA: layout    TYPE slis_layout_alv.

DATA: sort      TYPE slis_t_sortinfo_alv WITH HEADER LINE.
DATA: excluding TYPE slis_t_extab WITH HEADER LINE.

* internal working table for events / for the headlines     "n667256
DATA: GS_EVENTS     TYPE slis_alv_event.                    "n667256
DATA: GT_EVENTS     TYPE SLIS_T_EVENT.                      "n667256

                                                            "n667256
* for the header of the list, when alv grid is in use       "n667256
DATA : gt_UEB                TYPE  slis_t_listheader,       "n667256
       gs_ueb                type  SLIS_LISTHEADER.         "n667256

* Variants
DATA: variante        LIKE disvariant,
      variante_flat   like disvariant,
      def_variante    LIKE disvariant,
      def_variante_f4 like disvariant,
      variant_exit(1) TYPE c.

*ENHANCEMENT-POINT RM07MLBS_01 SPOTS ES_RM07MLBS STATIC.
*ENHANCEMENT-POINT RM07MLBS_13 SPOTS ES_RM07MLBS STATIC .
data : g_f_vari_hsq          like  disvariant-variant,
       g_f_vari_flt          LIKE  disvariant-variant.

* working fields to save the initial display variants       "n579976
data : g_f_vari_hsq_initial  like  disvariant-variant,      "n579976
       g_f_vari_flt_initial  LIKE  disvariant-variant.      "n579976

* Global variables for handling ALV functionality
tables: mmim_rep_print.

DATA: alv_keyinfo      TYPE slis_keyinfo_alv.
DATA: alv_variant      LIKE disvariant.
DATA: alv_layout       TYPE slis_layout_alv.
data: alv_repid        like sy-repid.
data: alv_print        type slis_print_alv.
data: alv_detail_func(30)    type  c,
      alv_color              like      mmim_rep_print-color.

* User settings for the checkboxes
  DATA: oref_settings TYPE REF TO cl_mmim_userdefaults.

* define working fields
data : g_cnt_col_pos         type i,
       g_cnt_spos            type i.

data : g_flag_ok(01)         type c,
       g_flag_mess_333(01)   type c,
       g_flag_t001l(01)      type c.

data : g_cnt_variant_error   type i.                        "n667256

* does the user want to suppress objects from plant level ? "n577268
data : g_flag_suppress_init_lgort(01)  type c.              "n577268

data : begin of g_flag_sobkz,
         vbeln(01)           type c,
         pspnr(01)           type c,
         lifnr(01)           type c,
         kunnr(01)           type c,
       end of g_flag_sobkz.

constants : c_no_out(01)     type c    value 'X',
            c_out(01)        type c    value space.

* flag to be set when INITIALIZATION was processed          "n667256
data g_flag_initialization(01) type c.                      "n667256

* authorization check should be always processed            "n829722
data t_flag_launched(01) type c.                            "n829722

DATA:
  dbcon           TYPE dbcon_name,                          "n1710852
  newsel          TYPE xfeld,                               "n1795093
  lv_dontpanic    TYPE symsgv.                              "n1795093

CONSTANTS:
  c_hdb_dbcon_get TYPE funcname VALUE 'MM_HDB_DBCON_GET',   "n1710852
  c_hdb_subappl   TYPE program  VALUE 'MB52'.               "n1710852

DATA:                                                       "n1795093
  collector_mard LIKE STANDARD TABLE OF collector,          "n1795093
  collector_mchb LIKE STANDARD TABLE OF collector,          "n1795093
  collector_mkol LIKE STANDARD TABLE OF collector,          "n1795093
  collector_mska LIKE STANDARD TABLE OF collector,          "n1795093
  collector_mssa LIKE STANDARD TABLE OF collector,          "n1795093
  collector_mspr LIKE STANDARD TABLE OF collector,          "n1795093
  collector_mssq LIKE STANDARD TABLE OF collector,          "n1795093
  collector_mbew LIKE STANDARD TABLE OF collector,          "n1795093
  collector_ebew LIKE STANDARD TABLE OF collector,          "n1795093
  collector_msku LIKE STANDARD TABLE OF collector,          "n1795093
  collector_mslb LIKE STANDARD TABLE OF collector,          "n1795093
  collector_uml  LIKE STANDARD TABLE OF collector.          "n1795093


************************************************************************
* Selection screen
************************************************************************

SELECTION-SCREEN BEGIN OF BLOCK abgrenzung WITH FRAME TITLE text-001.

SELECT-OPTIONS:
  matnr  FOR mara-matnr MEMORY ID mat MATCHCODE OBJECT mat1,
  werks  FOR t001l-werks  MEMORY ID wrk,                        "718285
  lgort  FOR t001l-lgort  MEMORY ID lag,
  charg  FOR mchb-charg  MEMORY ID cha MATCHCODE OBJECT mch1.

SELECTION-SCREEN END OF BLOCK abgrenzung.

*----------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF BLOCK lbs WITH FRAME TITLE text-070.

SELECT-OPTIONS:
  matart FOR mara-mtart,
  matkla FOR mara-matkl,
  ekgrup FOR marc-ekgrp.

*ENHANCEMENT-POINT RM07MLBS_3 SPOTS ES_RM07MLBS STATIC .

SELECTION-SCREEN END OF BLOCK lbs.

*----------------------------------------------------------------------*

* for the selection os special stocks
SELECTION-SCREEN BEGIN OF BLOCK lb2 WITH FRAME TITLE text-071.

* for the selection os special stocks
  parameters : pa_sond       like      rmmmb-kzlso
                             default   'X'.

  SELECT-OPTIONS:
    so_sobkz                 for  mkol-sobkz.

SELECTION-SCREEN END OF BLOCK lb2.

*----------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF BLOCK lb1 WITH FRAME TITLE text-080.

* select only lines who contain at least one negative stock
PARAMETERS: negativ LIKE am07m-seneg.

* Documentation for parameter XMCHB improved                "n494306
PARAMETERS: xmchb            like      am07m-mb52_xmchb     "n494306
                             default   'X'.

* Checkbox to eliminate lines with zero stocks
PARAMETERS: nozero   LIKE rmmmb-kznul.

* Checkbox to disable value processing.
* Documentation for parameter NOVALUES improved             "n494306
PARAMETERS: novalues         like      am07m-mb52_noval.    "n494306


SELECTION-SCREEN end OF BLOCK lb1.

*----------------------------------------------------------------------*

SELECTION-SCREEN BEGIN OF BLOCK liste WITH FRAME TITLE text-005.

* choose flat or hierarchic list                            "n531604
  selection-screen begin of line.                           "n531604
    selection-screen         position 1.                    "n531604
    parameters : pa_hsq      like  am07m-mb52_alv_hsq       "n531604
                             default  'X'                   "n531604
                             radiobutton group alvv         "n531604
                             user-command alvv.             "n531604
    selection-screen         comment 3(40)  text-006        "n531604
                             for field pa_hsq.              "n531604
  selection-screen end of line.                             "n531604
                                                            "n531604
  selection-screen begin of line.                           "n531604
    selection-screen         position 1.                    "n531604
    parameters : pa_flt      like  am07m-mb52_alv_flt       "n531604
                             radiobutton group alvv.        "n531604
    selection-screen         comment 3(40)  text-007        "n531604
                             for field pa_flt.              "n531604
  selection-screen end of line.                             "n531604

*  parameters :
*    pa_grid                  type  MB_XFELD
*                             default 'X'
*                             radiobutton group alv1,
*    pa_class                 type  MB_XFELD
*                             radiobutton group alv1.

  PARAMETERS: p_vari LIKE disvariant-variant.

SELECTION-SCREEN END OF BLOCK liste.
*ENHANCEMENT-POINT EHP604_RM07MLBS_04 SPOTS ES_RM07MLBS STATIC .

*----------------------------------------------------------------------*

* F4-Help for variant
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
  PERFORM f4_for_variant.

************************************************************************
* Main program
************************************************************************

AT SELECTION-SCREEN.

* check radiobuttons                                          "n667256
  if  pa_hsq is initial.                                      "n667256
    if  pa_flt is initial.                                    "n667256
*     not allowed                                             "n667256
      move  'X'              to  pa_hsq.                      "n667256
      add  1                 to  g_cnt_variant_error.         "n667256
    endif.                                                    "n667256
  else.                                                       "n667256
    if  pa_flt is initial.                                    "n667256
    else.                                                     "n667256
*     not allowed                                             "n667256
      clear                  pa_flt.                          "n667256
      add  1                 to  g_cnt_variant_error.         "n667256
    endif.                                                    "n667256
  endif.                                                      "n667256
                                                              "n667256
* the user will get the info about the old variant only once  "n667256
  if  g_cnt_variant_error = 1.                                "n667256
    if  not sy-slset is initial.                              "n667256
      message i634(db)       with  sy-slset sy-repid.         "n667256
    endif.                                                    "n667256
  endif.                                                      "n667256

* has the user changed the radiobuttons for the mode of the "n531604
* SAP-LIST-VIEWER ?                                         "n531604
  if  SSCRFIELDS-UCOMM  =  'ALVV'.
*   yes, restore the old entry if extists                   "n531604
    if      not pa_hsq is initial.
      move  g_f_vari_hsq     to  p_vari.
    elseif  not  pa_flt is initial.
*     for flat ( simple ) list
      move  g_f_vari_flt     to  p_vari.
    endif.

  else.
*   save the display variant depending on the selected mode "n531604
*   of the SAP-LIST-VIEWER
    if      not pa_hsq is initial.
*     for hierarchic seq. list
      MOVE p_vari            TO  g_f_vari_hsq.

    elseif  not  pa_flt is initial.
*     for flat ( simple ) list
      MOVE p_vari            TO  g_f_vari_flt.

    endif.
  endif.

* it is necessary to set flag xmchb if batch has been entered because
* otherwise MCHB will not be read and non suitable items can't be
* be removed later on in form data_selection
  IF NOT charg[] IS INITIAL. xmchb = 'X'. ENDIF.    "note 311770

* send a warning if the user starts this report without any "n531604
* restrictions for the database selection                   "n531604
* only when this report is started                          "n531604
  IF MATNR IS INITIAL AND                                   "n531604
     WERKS IS INITIAL AND                                   "n531604
     LGORT IS INITIAL AND                                   "n531604
     CHARG IS INITIAL.                                      "n531604
    IF  SY-UCOMM  =  'ONLI'  OR                             "n531604
        SY-UCOMM  =  'PRIN'.                                "n531604
*ENHANCEMENT-SECTION EHP604_RM07MLBS_43 SPOTS ES_RM07MLBS .
      MESSAGE  W689.    "The selection was not restricted   "n531604
*END-ENHANCEMENT-SECTION.
    ENDIF.                                                  "n531604
  ENDIF.                                                    "n531604
* go on only if the user wants to launch this report        "n667256
* the authorization check should be always processed        "n829722
If sy-ucomm = 'ONLI' or                                     "n829722
   sy-ucomm = 'PRIN' or                                     "n829722
   sy-ucomm = 'SJOB' or                                     "n829722
   sy-ucomm = SPACE.                                        "n829722
 move 'X' to t_flag_launched.                               "n829722
else.                                                       "n829722
 if sy-ucomm <> SPACE.                                      "n829722
  clear t_flag_launched.                                    "n829722
 endif.                                                     "n829722
endif.                                                      "n829722
CHECK t_flag_launched = 'X'.                                "n829722
  PERFORM check_entry.
  PERFORM organisation.
  PERFORM check_authorization.

* save the parameters of this run in database table ESDUS   "n531604
  PERFORM                    f0200_settings_save.           "n531604

*------------------------ Initialisierung -----------------------------*
INITIALIZATION.
*ENHANCEMENT-POINT RM07MLBS_05 SPOTS ES_RM07MLBS.
  PERFORM                    f0000_get_print_settings.      "n531604

* look for the setting of the parameters from the last run  "n531604
  PERFORM                    f0100_settings_init.           "n531604

  PERFORM initialisierung.

* set flag when INITILIZATION is processed                  "n667256
  move  'X'        to  g_flag_initialization.               "n667256
                                                            "n667256
*-----------------------------------------------------------"n667256
                                                            "n667256
*ENHANCEMENT-POINT RM07MLBS_07 SPOTS ES_RM07MLBS.
START-OF-SELECTION.

* it makes no sence to carry out this report                "n667256
  if  g_cnt_variant_error > 0.                              "n667256
    if  not sy-slset is initial.                            "n667256
      message e634(db)       with  sy-slset sy-repid.       "n667256
    endif.                                                  "n667256
  endif.                                                    "n667256

* does the user restrict the storage locations and want to  "n577268
* suppress stock objects from plant level ?                 "n577268
  clear                      collector-lgort.               "n577268
                                                            "n577268
  if  collector-lgort in lgort.                             "n577268
    clear                    g_flag_suppress_init_lgort.    "n577268
  else.                                                     "n577268
    move  'X'                to  g_flag_suppress_init_lgort."n577268
  endif.                                                    "n577268

  BREAK-POINT ID mmim_rep_mb52.                             "n1795093

  IF newsel IS INITIAL.                                     "n1795093
    PERFORM data_selection.
  ELSE.                                                     "n1795093
    PERFORM data_selection_new.                             "n1795093
  ENDIF.                                                    "n1795093

*-------------------------- Datenausgabe-------------------------------*
END-OF-SELECTION.
  READ TABLE bestand INDEX 1 TRANSPORTING NO FIELDS.

  IF sy-subrc = 0.
*   the fieldcatalog depends on the type of list            "n531604
    if      not pa_hsq is initial.                          "n531604
*     create hierarchic list                                "n531604
      PERFORM                fieldcatalog.                  "n531604
    elseif  not  pa_flt is initial.                         "n531604
      perform                f0300_fieldcat_flat.           "n531604
    endif.                                                  "n531604

    if  g_flag_mess_333 = 'X'.
*     "The list is incomplete due to lacking authorization
      message                s333.
    endif.

    PERFORM list_output.
  ELSE.
    MESSAGE s843.
*   Zu den vorgegebenen Daten ist kein Bestand vorhanden
    IF NOT sy-calld IS INITIAL.                                  "307852
      LEAVE.                                                     "307852
    ELSE.                                                        "307852
*      LEAVE TO TRANSACTION sy-tcode.                            "n1590783
*     SELECT-OPTIONS will be lost with leave to transaction..    "n1590783
*     Change logic to submit and provide select-options          "n1590783
      SUBMIT RM07MLBS                                            "n1590783
       with matnr    in matnr                                    "n1590783
       with werks    in werks                                    "n1590783
       with lgort    in lgort                                    "n1590783
       with charg    in charg                                    "n1590783
       with matart   in matart                                   "n1590783
       with matkla   in matkla                                   "n1590783
       with ekgrup   in ekgrup                                   "n1590783
       with pa_sond  =  pa_sond                                  "n1590783
       with so_sobkz in so_sobkz                                 "n1590783
       with negativ  =  negativ                                  "n1590783
       with xmchb    = xmchb                                     "n1590783
       with nozero   = nozero                                    "n1590783
       with novalues = novalues                                  "n1590783
       with pa_hsq   = pa_hsq                                    "n1590783
       with pa_flt   = pa_flt                                    "n1590783
       with p_vari   = p_vari                                    "n1590783
       VIA SELECTION-SCREEN.                                     "n1590783
    ENDIF.                                                       "307852
  ENDIF.

************************************************************************
* Read organisation
************************************************************************
FORM organisation.

* get all existing storage bins of the required plants
  refresh : g_t_t001l, g_t_organ.
  clear   : g_s_t001l, g_s_organ, g_flag_t001l.

  select  werks lgort lgobe  from t001l
                   into corresponding fields of table g_t_t001l
                   where  werks in werks
                     and  lgort in lgort
                     and  lgobe ne space.

  if  sy-subrc is initial.
    move  'X'                to  g_flag_t001l.
  endif.
  IF newsel IS INITIAL.                                     "n1795093
    SELECT DISTINCT werks name1 bwkey
                    INTO CORRESPONDING FIELDS OF TABLE g_t_organ
                    FROM t001w
                    WHERE werks IN werks.

    SORT g_t_organ             BY bwkey.

    LOOP AT g_t_organ          into  g_s_organ.
      ON CHANGE OF g_s_organ-bwkey.
        clear                  G_flag_ok.

        SELECT SINGLE * FROM t001k
                     WHERE bwkey EQ g_s_organ-bwkey.

        if sy-subrc is initial.
          SELECT SINGLE * FROM t001
                     WHERE bukrs EQ t001k-bukrs.

          if sy-subrc is initial.
            move  'X'          to  G_flag_ok.
          endif.
        endif.
      ENDON.

      if  g_flag_ok = 'X'.
        MOVE-CORRESPONDING t001  TO  g_s_organ.
        MODIFY  g_t_organ        from  g_s_organ.
      endif.
    ENDLOOP.

  ELSE.                                                     "n1795093
    SELECT t001w~werks t001w~name1 t001w~bwkey t001k~bukrs t001~waers
      INTO CORRESPONDING FIELDS OF TABLE g_t_organ
      FROM t001w
      INNER JOIN t001k
      ON t001k~bwkey = t001w~bwkey
      LEFT JOIN t001
      ON t001~bukrs = t001k~bukrs
      WHERE t001w~werks IN werks.
  ENDIF.                                                    "n1795093

ENDFORM.                               " ORGANISATION

************************************************************************
* Check authorization on plant level for all selected plants
************************************************************************
FORM check_authorization.

* define local working areas
  data : l_s_bukrs           type  stype_buffer,
         l_t_bukrs           type  stab_buffer.

  SORT   g_t_organ           BY werks.

* report displayed stocks for plant although the user has   "n591618
* not the required authorization for that plant             "n591618
  clear                      g_s_buffer.                    "n591618

  LOOP AT g_t_organ          into  g_s_organ.
*   check the authority only after the plant has changed    "n531604
    if  g_s_organ-werks ne g_s_buffer-werks.
      move  g_s_organ-werks  to  g_s_buffer-werks.

      AUTHORITY-CHECK OBJECT 'M_MATE_WRK'
                     ID 'ACTVT' FIELD '03'
                     ID 'WERKS' FIELD g_s_organ-werks.

      IF NOT sy-subrc IS INITIAL.
        set  cursor          field  'WERKS-LOW'.
        MESSAGE e120         WITH  g_s_organ-werks.
      ENDIF.
    endif.

    if  novalues is initial.                                "n531604
*     the user wants to see the values
      if  g_s_organ-bukrs ne g_s_buffer-bukrs.
*       check the authority after the company code changed

        AUTHORITY-CHECK OBJECT 'F_BKPF_BUK'
*           parameters for AUTHORITY-CHECK command fixed    "n546707
            ID 'BUKRS' FIELD g_s_organ-bukrs                "n546707
            ID 'ACTVT' FIELD '03'.                          "n546707

        move : g_s_organ-bukrs    to  g_s_buffer-bukrs,     "n667256
               sy-subrc           to  g_s_buffer-subrc.     "n667256

        if sy-subrc <> 0.
*         no authorization -> save the company code
          move : 'X'              to  g_flag_mess_333,
                 g_s_organ-bukrs  to  l_s_bukrs-bukrs.
          collect l_s_bukrs       into  l_t_bukrs.
        endif.
      endif.

*     use the result from the buffer
      if  g_s_buffer-subrc <> 0.
        clear                g_s_organ-waers.
        modify  g_t_organ    from  g_s_organ
                             transporting  waers.
      endif.
    endif.
  ENDLOOP.

* send the info for each missing autority
  if  g_flag_mess_333  =  'X'.
    sort                     l_t_bukrs.
    set cursor               field  'WERKS-LOW'.

    loop at l_t_bukrs        into  l_s_bukrs.
*     No authorization to display data for company code &
      message  i862(m3)      with  l_s_bukrs-bukrs.
    endloop.
  endif.

ENDFORM.

************************************************************************
* Check data on selection screen
************************************************************************
FORM check_entry.

* Check some entered data for consistency
  CALL FUNCTION 'MMIM_ENTRYCHECK_MAIN'
     TABLES
       it_matnr = matnr
       it_werks = werks
       it_lgort = lgort
       it_ekgrp = ekgrup
       it_sobkz = so_sobkz.

* Material type
  IF NOT matart-low IS INITIAL OR NOT matart-high IS INITIAL.
    SELECT SINGLE * FROM t134m WHERE mtart IN matart.
    IF NOT sy-subrc IS INITIAL.
      MESSAGE e104(m3) WITH matart-low.
    ENDIF.
  ENDIF.

* Material class
  IF NOT matkla-low IS INITIAL OR NOT matart-high IS INITIAL.
    SELECT SINGLE * FROM t023 WHERE matkl IN matkla.
    IF NOT sy-subrc IS INITIAL.
      MESSAGE e883 WITH matkla-low.
    ENDIF.
  ENDIF.

* Display variant
  IF NOT p_vari IS INITIAL.
    set cursor               field  'P_VARI'.

    if      not pa_hsq is initial.
*     for hierarchic seq. list
      MOVE : p_vari          TO  variante-variant,
             variante        TO  def_variante.

    elseif  not  pa_flt is initial.
*     for flat ( simple ) list
      MOVE : p_vari          TO  variante_flat-variant,
             variante_flat   TO  def_variante.
    endif.

    CALL FUNCTION 'REUSE_ALV_VARIANT_EXISTENCE'
         EXPORTING
              i_save     = 'A'
         CHANGING
              cs_variant = def_variante.
  else.                                                     "n579976
*   the user wants no initial display variant               "n579976
                                                            "n579976
    if      not pa_hsq is initial.                          "n579976
*     for hierarchic seq. list                              "n579976
      if  not g_f_vari_hsq_initial is initial.              "n579976
*       but the SAP-LIST-VIEWER will apply the existing     "n579976
*       initial display variant for hierarchic lists        "n579976
        perform  F3000_SEND_WARNING_M7_393                  "n579976
                             using  g_f_vari_hsq_initial.   "n579976
      endif.                                                "n579976
                                                            "n579976
    elseif  not  pa_flt is initial.                         "n579976
*     for flat ( simple ) list                              "n579976
      if  not g_f_vari_flt_initial is initial.              "n579976
*       but the SAP-LIST-VIEWER will apply the existing     "n579976
*       initial display variant for flat lists              "n579976
        perform  F3000_SEND_WARNING_M7_393                  "n579976
                             using  g_f_vari_flt_initial.   "n579976
      endif.                                                "n579976
    endif.                                                  "n579976
  ENDIF.

ENDFORM.                               "check_entry

************************************************************************
* Initialization: Read default variant
************************************************************************
FORM initialisierung.

* prepare the areas for the different display variants
  repid = sy-repid.
  CLEAR : variante,          variante_flat.
  MOVE  : repid              to  variante-report,
          repid              to  variante_flat-report,
          'FLAT'             to  variante_flat-handle.

* the display variant is depending on the seleted mode of   "n531604
* the SAP-LIST-VIEWER : look for both variants              "n531604

* a) Get default variant for the hierarchic list
  def_variante = variante.

  CALL FUNCTION 'REUSE_ALV_VARIANT_DEFAULT_GET'
       EXPORTING
            i_save     = 'A'
       CHANGING
            cs_variant = def_variante
       EXCEPTIONS
            not_found  = 2.

  IF sy-subrc = 0.
    move  def_variante-variant    TO  g_f_vari_hsq.
*   save the initial display variant for the hierseq. list  "n579976
    move  def_variante-variant    TO  g_f_vari_hsq_initial. "n579976
  endif.

* b) Get default variant for the non-hierarchic list
  def_variante = variante_flat.

  CALL FUNCTION 'REUSE_ALV_VARIANT_DEFAULT_GET'
       EXPORTING
            i_save     = 'A'
       CHANGING
            cs_variant = def_variante
       EXCEPTIONS
            not_found  = 2.

  IF sy-subrc = 0.
    move  def_variante-variant    TO  g_f_vari_flt.
*   save the initial display variant for the flat list      "n579976
    move  def_variante-variant    TO  g_f_vari_flt_initial. "n579976
  endif.

* take the required variant
  if      not pa_hsq is initial.
*   for hierarchic seq. list
    p_vari =  g_f_vari_hsq.

  elseif  not  pa_flt is initial.
*   for flat ( simple ) list
    p_vari = g_f_vari_flt.
  endif.

* begin of secondary database settings                    "n1710852
  CALL FUNCTION 'FUNCTION_EXISTS'
    EXPORTING
      funcname           = c_hdb_dbcon_get
    EXCEPTIONS
      function_not_exist = 1
      OTHERS             = 2.
  IF sy-subrc = 0.
    CALL FUNCTION c_hdb_dbcon_get
      EXPORTING
        i_subappl = c_hdb_subappl
      IMPORTING
        e_dbcon   = dbcon.
    IF dbcon IS NOT INITIAL.
      newsel = 'X'. "set new selection active
    ENDIF.
  ENDIF. "sy-subrc = 0.
* end of secondary database settings                      "n1710852

* check for other settings to activate new selection        "v_n1795093
  IF newsel IS INITIAL.
*  1. check control table for system setting
    SELECT COUNT(*) FROM mmim_control_log WHERE action = 'MB52_NEW_SEL'
                                          AND   repid  = 'RM07MLBS'
                                          AND   status = 'X'.
    IF sy-subrc IS INITIAL.
      newsel = 'X'. "set new selection active
    ENDIF.
* 2. if no system setting, check user setting
    IF newsel IS INITIAL.
      GET PARAMETER ID 'DONTPANIC' FIELD  lv_dontpanic.
      IF sy-subrc IS INITIAL.
        TRANSLATE : lv_dontpanic TO  UPPER CASE.         "#EC TRANSLANG
        FIND FIRST OCCURRENCE OF 'MB52NSEL'  IN  lv_dontpanic.
        IF  sy-subrc IS INITIAL.
          newsel = 'X'. "set new selection active
        ENDIF.
      ENDIF.
    ENDIF.
  ELSE.
    GET PARAMETER ID 'DONTPANIC' FIELD  lv_dontpanic.
    IF sy-subrc IS INITIAL.
      TRANSLATE : lv_dontpanic TO  UPPER CASE.           "#EC TRANSLANG
      FIND FIRST OCCURRENCE OF 'MB52OSEL'  IN  lv_dontpanic.
      IF  sy-subrc IS INITIAL.
        CLEAR: dbcon.
      ENDIF.
    ENDIF.
  ENDIF.
*                                                           "^_n1795093

*ENHANCEMENT-POINT EHP604_RM07MLBS_05 SPOTS ES_RM07MLBS .

ENDFORM.                               " INITIALISIERUNG

************************************************************************
* Main data selection routine
************************************************************************
FORM data_selection.

* Materials to be processed
  TYPES: BEGIN OF ty_mat,
           matnr LIKE mara-matnr,
           werks LIKE marc-werks,
           xchar LIKE marc-xchar,
           mtart LIKE mara-mtart,
           matkl LIKE mara-matkl,
           meins LIKE mara-meins,
           trame LIKE marc-trame,
           umlmc LIKE marc-umlmc,
           glgmg LIKE marc-glgmg,                        "n912093
           bwesb             like  marc-bwesb,           "AC0K020254
           lvorm_mara        like  mara-lvorm,
           lvorm_marc        like  marc-lvorm.
*ENHANCEMENT-POINT EHP604_RM07MLBS_06 SPOTS ES_RM07MLBS STATIC .
  TYPES:  END OF ty_mat.

  DATA: t_mat     TYPE ty_mat OCCURS 0 WITH HEADER LINE,
        t_batch   TYPE ty_mat OCCURS 0 WITH HEADER LINE,
        t_nobatch TYPE ty_mat OCCURS 0 WITH HEADER LINE.
*ENHANCEMENT-POINT EHP604_RM07MLBS_07 SPOTS ES_RM07MLBS STATIC .

* buffer for reading working tables
  data : l_s_mat             type  ty_mat,
         l_f_matnr           like  makt-matnr.

  RANGES: r_sobkz FOR mkol-sobkz.

  DATA: l_cnt_matnr_i_eq     type i.                       "n759412
  DATA: l_cnt_matnr_total    type i.                       "n759412

************************************************************************
* Read material master data (MARA and MARC)
************************************************************************
  REFRESH collector.

* take all matching entries, do not consider the deletion
* indicator

*** BEGIN INSERT n_759412
* analyse the select-option table for material
* numbers
  clear : l_cnt_matnr_total, l_cnt_matnr_i_eq.

  loop at matnr.
    add  1             to  l_cnt_matnr_total.

    if  not matnr-low     is initial  and
            matnr-sign    =  'I'      and
            matnr-option  =  'EQ'     and
            matnr-high    is initial.
*     the table contains single a material number
      add  1           to  l_cnt_matnr_i_eq.
    else.
      exit.
    endif.
  endloop.
* added dynamic break-point ID MMIM_REP_MB52              "n912093
  BREAK-POINT ID MMIM_REP_MB52.                           "n912093
* process SELECT command depending on the
* required material selection
  if  l_cnt_matnr_total  > 0                 and
      l_cnt_matnr_total  = l_cnt_matnr_i_eq.
*ENHANCEMENT-SECTION EHP604_RM07MLBS_08 SPOTS ES_RM07MLBS .
    SELECT mara~matnr werks xchar mtart matkl meins trame umlmc
           bwesb glgmg                       "AC0K020254  "n912093
         mara~lvorm as lvorm_mara
         marc~lvorm as lvorm_marc
         CONNECTION (dbcon)                               "1710852
         INTO CORRESPONDING FIELDS OF TABLE t_mat
         FROM mara INNER JOIN marc
         ON mara~matnr = marc~matnr
         for all entries in matnr
         WHERE mara~matnr = matnr-low
           AND werks IN werks
           AND mtart IN matart
           AND matkl IN matkla
           AND ekgrp IN ekgrup.
*END-ENHANCEMENT-SECTION.
  else.
* END INSERT n_759412
*ENHANCEMENT-SECTION EHP604_RM07MLBS_09 SPOTS ES_RM07MLBS .
    SELECT mara~matnr werks xchar mtart matkl meins trame umlmc
           bwesb glgmg                          "AC0K020254 "912093
         mara~lvorm as lvorm_mara
         marc~lvorm as lvorm_marc
         CONNECTION (dbcon)                               "1710852
         INTO CORRESPONDING FIELDS OF TABLE t_mat
         FROM mara INNER JOIN marc
         ON mara~matnr = marc~matnr
         WHERE mara~matnr IN matnr
           AND werks IN werks
           AND mtart IN matart
           AND matkl IN matkla
           AND ekgrp IN ekgrup.
*END-ENHANCEMENT-SECTION.
  endif.                                                  "n759412
*ENHANCEMENT-POINT EHP604_RM07MLBS_10 SPOTS ES_RM07MLBS .
************************************************************************
* Get "normal" stocks.
* If no detailed batch display is required,
* all data come from MARD. Otherwise, materials with batch
* management are extracted from MCHB, the rest from MARD.
************************************************************************
  REFRESH: t_batch, t_nobatch.
* Split the worklist into the parts for each table...
  IF xmchb IS INITIAL.
    t_nobatch[] = t_mat[].
  ELSE.
    LOOP AT t_mat.
      IF t_mat-xchar IS INITIAL.
        APPEND t_mat TO t_nobatch.
      ELSE.
        APPEND t_mat TO t_batch.
      ENDIF.
    ENDLOOP.
  ENDIF.
*ENHANCEMENT-POINT EHP604_RM07MLBS_11 SPOTS ES_RM07MLBS .
* Access MARD
* I you think that instead of SELECT-APPEND we could have used
* an array fetch, please wait for the table names to become different
* from the internal fields. B.T.W.: The DB-interface also buffers.
  CLEAR collector.
  READ TABLE t_nobatch INDEX 1 TRANSPORTING NO FIELDS.
  IF sy-subrc = 0.
*ENHANCEMENT-SECTION EHP604_RM07MLBS_12 SPOTS ES_RM07MLBS .
    SELECT matnr werks lgort
           labst umlme insme einme speme retme lvorm
           CONNECTION (dbcon)                               "1710852
           INTO (collector-matnr, collector-werks, collector-lgort,
                 collector-labst, collector-umlme, collector-insme,
                 collector-einme, collector-speme, collector-retme,
                 collector-lvorm)
           FROM mard
           FOR ALL ENTRIES IN t_nobatch
           WHERE matnr = t_nobatch-matnr
             AND werks = t_nobatch-werks
             AND lgort IN lgort.

*     save the MARD Key and deletion indicator for later
*     in table G_T_MARD_LV for use with special stocks
      if not pa_sond         is initial  and
         not collector-lvorm is initial.
        move-corresponding  collector
                             to  g_s_mard_lv.
        insert  g_s_mard_lv  into  table  g_t_mard_lv.
      endif.

      perform                f2000_COLLECT_collector.
    ENDSELECT.
*END-ENHANCEMENT-SECTION.
  ENDIF.

* Access MCHB
  CLEAR collector.
  READ TABLE t_batch INDEX 1 TRANSPORTING NO FIELDS.
  IF sy-subrc = 0.
*ENHANCEMENT-SECTION EHP604_RM07MLBS_13 SPOTS ES_RM07MLBS .
    SELECT matnr werks lgort charg
           clabs cumlm cinsm ceinm cspem cretm lvorm
           CONNECTION (dbcon)                               "1710852
           INTO (collector-matnr, collector-werks, collector-lgort,
                 collector-charg,
                 collector-labst, collector-umlme, collector-insme,
                 collector-einme, collector-speme, collector-retme,
                 collector-lvorm)
           FROM mchb
           FOR ALL ENTRIES IN t_batch
           WHERE matnr = t_batch-matnr
             AND werks = t_batch-werks
             AND lgort IN lgort
             AND charg IN charg.

      perform                f2000_COLLECT_collector.
    ENDSELECT.
*END-ENHANCEMENT-SECTION.
  ENDIF.

************************************************************************
* Transfer stocks from MARC (TRAME, UMLMC)
************************************************************************
    CLEAR collector.

*ENHANCEMENT-SECTION EHP604_RM07MLBS_14 SPOTS ES_RM07MLBS .
    LOOP AT t_mat WHERE umlmc <> 0 OR trame <> 0 or      "AC0K020254
                        bwesb <> 0 OR                    "AC0K020254
                        glgmg <> 0.                      "912093

*     there are no lines with stock = zero

*     take the stocks from plant level only when the user   "n577268
*     does not restrict the storage location;               "n577268
      check : g_flag_suppress_init_lgort is initial.        "n577268

      IF negativ = 'X'.
*       ignore entry if all stocks are zero or greater
        IF  t_mat-trame >= 0 AND
            t_mat-umlmc >= 0 AND
            t_mat-glgmg >= 0.                            "n912093
          continue.          "take the next entry
        endif.
      ENDIF.

      collector-matnr = t_mat-matnr.
      collector-werks = t_mat-werks.
      collector-umlme = t_mat-trame + t_mat-umlmc.
      collector-lvorm = t_mat-lvorm_marc.
      collector-bwesb = t_mat-bwesb.                     "AC0K020254
      collector-glgmg = t_mat-glgmg.                      "n912093
      collector-trame = t_mat-trame.                      "n912093
      collector-umlmc = t_mat-umlmc.                      "n912093

      APPEND collector.

    ENDLOOP.
*END-ENHANCEMENT-SECTION.

************************************************************************
* Consignment from vendor (MKOL)
* Read only if requested by one of the
* flags on the selection screen. Absolutely inconsistent, but
* due to compatibility...
* MKOL has a flag for deletion
************************************************************************

  IF  not pa_sond is initial  and
      not t_mat[] is initial.
    if  'K' in so_sobkz  or
        'M' in so_sobkz.
      CLEAR                  collector.

*ENHANCEMENT-SECTION EHP604_RM07MLBS_15 SPOTS ES_RM07MLBS .
      SELECT matnr werks lgort charg sobkz lifnr
             slabs sinsm seinm sspem lvorm
             CONNECTION (dbcon)                               "1710852
             INTO (collector-matnr, collector-werks, collector-lgort,
                   collector-charg, collector-sobkz, collector-lifnr,
                   collector-labst, collector-insme, collector-einme,
                   collector-speme, collector-lvorm)
             FROM mkol
             FOR ALL ENTRIES IN t_mat
             WHERE matnr = t_mat-matnr
               AND werks = t_mat-werks
               AND lgort IN lgort
               AND charg IN charg
               and sobkz in so_sobkz.

        perform              f2000_COLLECT_collector.
      ENDSELECT.
*END-ENHANCEMENT-SECTION.
    ENDIF.
  ENDIF.

************************************************************************
* Special stocks at customer side (MSKU)
* MSKU has no flag for deletion
************************************************************************

  if  not  pa_sond is initial  and
      not  t_mat[] is initial.
    if  'V' in so_sobkz  or
        'W' in so_sobkz.
      CLEAR collector.

*ENHANCEMENT-SECTION EHP604_RM07MLBS_16 SPOTS ES_RM07MLBS .
      SELECT matnr werks charg sobkz kunnr
           kulab kuins kuein kuuml                      " OM EHP604
           CONNECTION (dbcon)                               "1710852
           INTO (collector-matnr, collector-werks, collector-charg,
                 collector-sobkz, collector-kunnr,
                 collector-labst, collector-insme, collector-einme,
                 collector-umlme)                       " OM EHP604
           FROM msku
           FOR ALL ENTRIES IN t_mat
           WHERE matnr = t_mat-matnr
             AND werks = t_mat-werks
             AND charg IN charg
             and sobkz  in  so_sobkz.

        perform              f2000_COLLECT_collector.
      ENDSELECT.
*END-ENHANCEMENT-SECTION.
    endif.
  endif.

************************************************************************
* Special stocks at vendor provision (MSLB)
* MSLB has no flag for deletion
************************************************************************

  if  not  pa_sond is initial  and
      not  t_mat[] is initial  and
           'O'     in so_sobkz.
    CLEAR collector.

*ENHANCEMENT-SECTION EHP604_RM07MLBS_17 SPOTS ES_RM07MLBS .
    SELECT matnr werks charg sobkz lifnr
           lblab lbins lbein lbuml                      " OM EHP604
           CONNECTION (dbcon)                               "1710852
           INTO (collector-matnr, collector-werks, collector-charg,
                 collector-sobkz, collector-lifnr,
                 collector-labst, collector-insme,
                 collector-einme, collector-umlme)      " OM EHP604
           FROM mslb
           FOR ALL ENTRIES IN t_mat
           WHERE matnr = t_mat-matnr
             AND werks = t_mat-werks
             AND charg IN charg
             and sobkz  in  so_sobkz.

      perform                f2000_COLLECT_collector.
    ENDSELECT.
*END-ENHANCEMENT-SECTION.
  endif.

************************************************************************
* Customer order stock (MSKA) and sum segment (MSSA) for valuation.
* Sum on the database and FOR ALL ENTRIES is not allowed from
* release 4.5 onwards, so the summation has to be done
* on the application server (here!).
* MSKA has no flag for deletion
************************************************************************

  if  not  pa_sond is initial  and
      not  t_mat[] is initial  and
           'E'     in so_sobkz.
    CLEAR collector.

*ENHANCEMENT-SECTION EHP604_RM07MLBS_18 SPOTS ES_RM07MLBS .
    SELECT mska~matnr mska~werks lgort charg mska~sobkz
           mska~vbeln mska~posnr
           kalab kains kaspe kaein kzbws
           CONNECTION (dbcon)                               "1710852
           INTO (collector-matnr, collector-werks, collector-lgort,
                 collector-charg, collector-sobkz,
                 collector-vbeln, collector-posnr,
                 collector-labst, collector-insme, collector-speme,
                 collector-einme, collector-kzbws)
           FROM mska INNER JOIN mssa
           ON   mska~matnr = mssa~matnr
            AND mska~werks = mssa~werks
            AND mska~sobkz = mssa~sobkz
            AND mska~vbeln = mssa~vbeln
            AND mska~posnr = mssa~posnr
           FOR ALL ENTRIES IN t_mat
           WHERE mska~matnr = t_mat-matnr
             AND mska~werks = t_mat-werks
             AND mska~lgort IN lgort
             AND mska~charg IN charg.

      perform                f2000_COLLECT_collector.
    ENDSELECT.
*END-ENHANCEMENT-SECTION.

*   Transfer stocks for customer order (SATRA in MSSA)
    CLEAR collector.
*ENHANCEMENT-SECTION EHP604_RM07MLBS_19 SPOTS ES_RM07MLBS .
    SELECT matnr werks sobkz vbeln posnr kzbws satra
           CONNECTION (dbcon)                               "1710852
           INTO (collector-matnr, collector-werks, collector-sobkz,
                 collector-vbeln, collector-posnr,
                 collector-kzbws, collector-umlme)
           FROM mssa
           FOR ALL ENTRIES IN t_mat
           WHERE matnr = t_mat-matnr
             AND werks = t_mat-werks
             and sobkz  in  so_sobkz
             AND satra <> 0.

       perform                f2000_COLLECT_collector.
    ENDSELECT.
*END-ENHANCEMENT-SECTION.
  endif.

************************************************************************
* The same game for project stocks (MSPR/MSSQ).
* MSPR has no flag for deletion
************************************************************************

  if  not  pa_sond is initial  and
      not  t_mat[] is initial  and
           'Q'     in so_sobkz.
    CLEAR collector.

*ENHANCEMENT-SECTION EHP604_RM07MLBS_20 SPOTS ES_RM07MLBS .
    SELECT mspr~matnr mspr~werks lgort charg mspr~sobkz mspr~pspnr
           prlab prins prspe prein kzbws
           CONNECTION (dbcon)                               "1710852
           INTO (collector-matnr, collector-werks, collector-lgort,
                 collector-charg, collector-sobkz,
                 collector-pspnr,
                 collector-labst, collector-insme, collector-speme,
                 collector-einme, collector-kzbws)
           FROM mspr INNER JOIN mssq
           ON   mspr~matnr = mssq~matnr
            AND mspr~werks = mssq~werks
            AND mspr~sobkz = mssq~sobkz
            AND mspr~pspnr = mssq~pspnr
           FOR ALL ENTRIES IN t_mat
           WHERE mspr~matnr = t_mat-matnr
             AND mspr~werks = t_mat-werks
             AND mspr~lgort IN lgort
             AND mspr~charg IN charg.

      perform                f2000_COLLECT_collector.
    ENDSELECT.
*END-ENHANCEMENT-SECTION.

*   Transfer stocks for projects (SQTRA in MSSQ)
    CLEAR collector.
*ENHANCEMENT-SECTION EHP604_RM07MLBS_21 SPOTS ES_RM07MLBS .
    SELECT matnr werks sobkz pspnr kzbws sqtra
           CONNECTION (dbcon)                               "1710852
           INTO (collector-matnr, collector-werks, collector-sobkz,
                 collector-pspnr,
                 collector-kzbws, collector-umlme)
           FROM mssq
           FOR ALL ENTRIES IN t_mat
           WHERE matnr = t_mat-matnr
             AND werks = t_mat-werks
             and sobkz  in  so_sobkz
             AND sqtra <> 0.

      perform                f2000_COLLECT_collector.
    ENDSELECT.
*END-ENHANCEMENT-SECTION.
  ENDIF.
*ENHANCEMENT-POINT RM07MLBS_02 SPOTS ES_RM07MLBS.

************************************************************************
* Extract key-data for other tables.
************************************************************************
  DATA: BEGIN OF t_maktkey OCCURS 0,
          matnr LIKE makt-matnr,
        END OF t_maktkey,

* working area for the material description
        BEGIN OF l_s_makt,
          matnr LIKE makt-matnr,
          maktx LIKE makt-maktx,
        END OF l_s_makt,

        BEGIN OF t_makt OCCURS 0,
          matnr LIKE makt-matnr,
          maktx LIKE makt-maktx,
        END OF t_makt,

        BEGIN OF t_mchakey OCCURS 0,
          matnr LIKE mcha-matnr,
          werks LIKE mcha-werks,
          charg LIKE mcha-charg,
        END OF t_mchakey,
        BEGIN OF t_mcha OCCURS 0,
          matnr LIKE mcha-matnr,
          werks LIKE mcha-werks,
          charg LIKE mcha-charg,
          bwtar LIKE mcha-bwtar,
        END OF t_mcha.

  REFRESH: t_maktkey, t_mchakey, bestand.
* remove all items in bestand with wrong batch number. If we would
* not remove this, report will e.g. show materials which has
* only MARD entries, too.
  LOOP AT collector WHERE charg IN charg.               "note 311770
    MOVE-CORRESPONDING collector TO bestand.

*   fill the key of the special stocks into the field
*   assigment
*ENHANCEMENT-SECTION     RM07MLBS_03 SPOTS ES_RM07MLBS.
    case    collector-sobkz.
      when  'E'.
        move  : 'X'               to  g_flag_sobkz-vbeln.
        WRITE : collector-VBELN   TO  bestand-ssnum.
        MOVE  : '/'               TO  bestand-ssnum+10(01).
        WRITE : collector-posnr   to  bestand-ssnum+12(08)
                                  NO-ZERO.
        condense                  bestand-ssnum.

      when  'K'.
        move  : 'X'               to  g_flag_sobkz-lifnr.
        WRITE : collector-LIFNR   TO  bestand-ssnum.

      when  'M'.
        move  : 'X'               to  g_flag_sobkz-lifnr.
        WRITE : collector-LIFNR   TO  bestand-ssnum.

      when  'O'.
        move  : 'X'               to  g_flag_sobkz-lifnr.
        WRITE : collector-LIFNR   TO  bestand-ssnum.

      when  'Q'.
        move  : 'X'               to  g_flag_sobkz-pspnr.
        WRITE : collector-PSPNR   TO  bestand-ssnum.

      when  'V'.
        move  : 'X'               to  g_flag_sobkz-kunnr.
        WRITE : collector-KUNNR   TO  bestand-ssnum.

      when  'W'.
        move  : 'X'               to  g_flag_sobkz-kunnr.
        WRITE : collector-KUNNR   TO  bestand-ssnum.

      when  others.
        clear                     bestand-ssnum.
    endcase.
*END-ENHANCEMENT-SECTION.

*ENHANCEMENT-POINT RM07MLBS_12 SPOTS ES_RM07MLBS .
    APPEND bestand.

    t_maktkey-matnr = bestand-matnr.
    COLLECT t_maktkey.

    IF bestand-charg <> space.
      t_mchakey-matnr = bestand-matnr.
      t_mchakey-werks = bestand-werks.
      t_mchakey-charg = bestand-charg.
      COLLECT t_mchakey.
    ENDIF.
  ENDLOOP.

  free                       collector.

************************************************************************
* Read additional tables
************************************************************************
  READ TABLE t_maktkey INDEX 1 TRANSPORTING NO FIELDS.
  IF sy-subrc = 0.
    SELECT matnr maktx INTO CORRESPONDING FIELDS OF TABLE t_makt
           FROM makt
           CONNECTION (dbcon)                               "1710852
           FOR ALL ENTRIES IN t_maktkey
           WHERE matnr = t_maktkey-matnr
             AND spras = sy-langu.
    SORT t_makt BY matnr.
  ENDIF.

* Read batch data only if values are requested
  READ TABLE t_mchakey INDEX 1 TRANSPORTING NO FIELDS.
  IF sy-subrc = 0 AND novalues IS INITIAL.
    SELECT matnr werks charg bwtar
           INTO CORRESPONDING FIELDS OF TABLE t_mcha
           FROM mcha
           CONNECTION (dbcon)                               "1710852
           FOR ALL ENTRIES IN t_mchakey
           WHERE matnr = t_mchakey-matnr
             AND werks = t_mchakey-werks
             AND charg = t_mchakey-charg.
    SORT t_mcha BY matnr werks charg.
  ENDIF.

************************************************************************
* Data definitions for the valuation extraction
************************************************************************
  DATA: BEGIN OF t_mbewkey OCCURS 0,
          matnr LIKE mbew-matnr,
          bwkey LIKE mbew-bwkey,
          bwtar LIKE mbew-bwtar,
        END OF t_mbewkey,

*       workin table for the material stock valuation
        BEGIN OF t_mbew OCCURS 0,
          matnr LIKE mbew-matnr,
          bwkey LIKE mbew-bwkey,
          bwtar LIKE mbew-bwtar,
*         consider the valuation of the special stocks E, Q "n531604
          sobkz              like  ebew-sobkz,              "n531604
          vbeln              like  ebew-vbeln,              "n531604
          posnr              like  ebew-posnr,              "n531604
          pspnr              like  qbew-pspnr,              "n531604

          lbkum(12)  TYPE p DECIMALS 3,                          "407810
          salk3(12)  TYPE p DECIMALS 2,                          "388735
          vprsv LIKE mbew-vprsv,                                 "353428
          verpr LIKE mbew-verpr,                                 "353428
          stprs LIKE mbew-stprs,                                 "353428
          peinh LIKE mbew-peinh,                                 "353428
        END OF t_mbew.

  DATA: t_ebewkey LIKE t_mbewkey OCCURS 0 WITH HEADER LINE.
  DATA: t_qbewkey LIKE t_mbewkey OCCURS 0 WITH HEADER LINE.
  DATA: t_ebew    LIKE t_mbew    OCCURS 0 WITH HEADER LINE.
  DATA: t_qbew    LIKE t_mbew    OCCURS 0 WITH HEADER LINE.

  DATA: BEGIN OF t_t134mkey OCCURS 0,
          bwkey LIKE t134m-bwkey,
          mtart LIKE t134m-mtart,
        END OF t_t134mkey,
        BEGIN OF t_t134m OCCURS 0,
          bwkey LIKE t134m-bwkey,
          mtart LIKE t134m-mtart,
          wertu LIKE t134m-wertu,
        END OF t_t134m.

************************************************************************
* Fill in additional data (first round) and extract the data
* for the access to the valuation tables.
************************************************************************
  SORT t_mat BY matnr werks.
  SORT g_t_organ             BY werks.
  clear : g_s_t001l,         g_s_organ.

  LOOP AT bestand.
*   get the information per plant and storage location
*   with buffer
    if  g_flag_t001l = 'X'.
      if  bestand-werks = g_s_t001l-werks  and
          bestand-lgort = g_s_t001l-lgort.
      else.
*       read with plant and storage location
        READ TABLE g_t_t001l   into  g_s_t001l
          with table key werks = bestand-werks
                         lgort = bestand-lgort.

        move : bestand-werks to g_s_t001l-werks,
               bestand-lgort to g_s_t001l-lgort.

        if sy-subrc <> 0.
          clear              g_s_t001l-lgobe.
        endif.
      endif.
    endif.

*   take the storage bin from the buffer
    move : g_s_t001l-lgobe   to  bestand-lgobe.


*   get the information per plant with buffer
    if  bestand-werks  ne  g_s_organ-werks.
      READ TABLE g_t_organ   into  g_s_organ
          WITH KEY werks = bestand-werks
                             BINARY SEARCH.

      if sy-subrc <> 0.
*       sorry nothing found
        clear                g_s_organ.
        move : bestand-werks to  g_s_organ-werks.
      endif.
    endif.

*   take the following fields from the buffer
    move : g_s_organ-name1   to  bestand-name1,
           g_s_organ-waers   to  bestand-waers,
           g_s_organ-bwkey   to  bestand-bwkey.


*   get the information from the material master MARC
*   with buffer
    if  bestand-matnr = l_s_mat-matnr  and
        bestand-werks = l_s_mat-werks.
*     results are in the buffer
    else.
      clear                  l_s_mat.
      move : bestand-matnr   to  l_s_mat-matnr,
             bestand-werks   to  l_s_mat-werks.

      READ TABLE t_mat       into  l_s_mat
         WITH KEY matnr = bestand-matnr
                  werks = bestand-werks
                             BINARY SEARCH.

      IF  sy-subrc <> 0.
*       sorry nothing found
        clear                l_s_mat.
        move : bestand-matnr to  l_s_mat-matnr,
               bestand-werks to  l_s_mat-werks.
      ENDIF.
    endif.

*   take the results the buffer
    move : l_s_mat-mtart     to  bestand-mtart,
           l_s_mat-matkl     to  bestand-matkl,
           l_s_mat-meins     to  bestand-meins.


*   if this entry has no deletion flag, take the
*   deletion flag from a higher level like MARA, MARC,
*   or MARDA
    if  bestand-lvorm is initial.
      if      not  l_S_mat-lvorm_marc is initial.
        move  l_s_mat-lvorm_marc
                             to  bestand-lvorm.
      elseif  not  l_S_mat-lvorm_mara is initial.
        move  l_s_mat-lvorm_mara
                             to  bestand-lvorm.

      elseif  not g_t_mard_lv[] is initial  and
              not bestand-lgort is initial  and
              not bestand-sobkz is initial.
*       look for deletion flag in working table
*       g_t_mard_lv for a line with special stock
        if  bestand-matnr = g_s_mard_lv-matnr  and
            bestand-werks = g_s_mard_lv-werks  and
            bestand-lgort = g_s_mard_lv-lgort.
        else.
*         read table only after the key has changed
          READ TABLE g_t_mard_lv  into  g_s_mard_lv
          with table key matnr = bestand-matnr
                         werks = bestand-werks
                         lgort = bestand-lgort.

          if sy-subrc <> 0.
*           fill the buffer in case the entry does not exist
            move : bestand-matnr  to  g_s_mard_lv-matnr,
                   bestand-werks  to  g_s_mard_lv-werks,
                   bestand-lgort  to  g_s_mard_lv-lgort.
            clear                 g_s_mard_lv-lvorm.
          endif.

*         take the result from the buffer
          move  g_s_mard_lv-lvorm   to  bestand-lvorm.
        endif.
      endif.
    endif.


*   read the material short description after the material
*   number has changed
    if  bestand-matnr ne l_s_makt-matnr.
      READ TABLE t_makt      into  l_s_makt
                   WITH KEY matnr = bestand-matnr
                             BINARY SEARCH.

      IF sy-subrc <> 0.
*       sorry nothing found
        clear                l_s_makt-maktx.
        move  bestand-matnr  to  l_s_makt-matnr.
      ENDIF.
    endif.

*   take the results the buffer
    move : l_s_makt-maktx    to  bestand-maktx.

* added dynamic break-point ID MMIM_REP_MB52              "n912093
  BREAK-POINT ID MMIM_REP_MB52.                           "n912093

    IF bestand-charg <> space AND novalues IS INITIAL.
      READ TABLE t_mcha WITH KEY matnr = bestand-matnr
                                 werks = bestand-werks
                                 charg = bestand-charg
                                 BINARY SEARCH.
      IF sy-subrc = 0.
        bestand-bwtar = t_mcha-bwtar.
      ENDIF.
    ENDIF.
    MODIFY bestand.
* Valuation keys
    IF novalues IS INITIAL.
      IF  bestand-sobkz = ' ' OR bestand-sobkz = 'O' OR
          bestand-sobkz = 'W' OR bestand-sobkz = 'V' OR
          bestand-kzbws = 'A'.
        t_mbewkey-matnr = bestand-matnr.
        t_mbewkey-bwkey = bestand-bwkey.
        t_mbewkey-bwtar = bestand-bwtar.
        COLLECT t_mbewkey.
      ELSEIF bestand-sobkz = 'E' AND bestand-kzbws = 'M'.
        t_ebewkey-matnr = bestand-matnr.
        t_ebewkey-bwkey = bestand-bwkey.
        t_ebewkey-bwtar = bestand-bwtar.
        COLLECT t_ebewkey.
      ELSEIF bestand-sobkz = 'Q' AND bestand-kzbws = 'M'.
        t_qbewkey-matnr = bestand-matnr.
        t_qbewkey-bwkey = bestand-bwkey.
        t_qbewkey-bwtar = bestand-bwtar.
        COLLECT t_qbewkey.
      ENDIF.
*ENHANCEMENT-POINT EHP604_RM07MLBS_44 SPOTS ES_RM07MLBS .
      t_t134mkey-bwkey = bestand-bwkey.
      t_t134mkey-mtart = bestand-mtart.
      COLLECT t_t134mkey.
    ENDIF.                             " novalues is initial
  ENDLOOP.

* release the space of global working tables after use
  free : G_t_mard_lv, g_t_t001l, g_t_organ.

************************************************************************
* Read the valuation tables
************************************************************************
  IF novalues IS INITIAL.
    READ TABLE t_mbewkey INDEX 1 TRANSPORTING NO FIELDS.
    IF sy-subrc = 0.
      SELECT matnr bwkey bwtar lbkum salk3
             vprsv verpr stprs peinh                             "353428
             CONNECTION (dbcon)                               "1710852
             INTO CORRESPONDING FIELDS OF TABLE t_mbew
             FROM mbew
             FOR ALL ENTRIES IN t_mbewkey
             WHERE matnr = t_mbewkey-matnr
               AND bwkey = t_mbewkey-bwkey
               AND bwtar = t_mbewkey-bwtar.
      SORT t_mbew BY matnr bwkey bwtar.
    ENDIF.

    READ TABLE t_ebewkey INDEX 1 TRANSPORTING NO FIELDS.
    IF sy-subrc = 0.
*     "Unfortunately", EBEW and QBEW do not have sum segments over
*     the valuation types. Therefore, without batch data, another
*     SELECT-statement is needed.
      IF xmchb = 'X'.
        SELECT matnr bwkey bwtar sobkz vbeln posnr lbkum salk3
               vprsv verpr stprs peinh
               CONNECTION (dbcon)                               "1710852
               INTO (t_ebew-matnr, t_ebew-bwkey, t_ebew-bwtar,
                     t_ebew-sobkz,  t_ebew-vbeln, t_ebew-posnr,
                     t_ebew-lbkum, t_ebew-salk3,
                     t_ebew-vprsv, t_ebew-verpr,
                     t_ebew-stprs, t_ebew-peinh)
               FROM ebew
               FOR ALL ENTRIES IN t_ebewkey
               WHERE matnr = t_ebewkey-matnr
                 AND bwkey = t_ebewkey-bwkey
                 AND bwtar = t_ebewkey-bwtar.
          COLLECT t_ebew.
        ENDSELECT.
      ELSE.
        SELECT matnr bwkey bwtar sobkz vbeln posnr lbkum salk3
               vprsv verpr stprs peinh
               CONNECTION (dbcon)                               "1710852
               INTO (t_ebew-matnr, t_ebew-bwkey,  t_ebew-bwtar,
                     t_ebew-sobkz,  t_ebew-vbeln, t_ebew-posnr,
                     t_ebew-lbkum, t_ebew-salk3,
                     t_ebew-vprsv, t_ebew-verpr,
                     t_ebew-stprs, t_ebew-peinh)
               FROM ebew
               FOR ALL ENTRIES IN t_ebewkey
               WHERE matnr = t_ebewkey-matnr
                 AND bwkey = t_ebewkey-bwkey.
          COLLECT t_ebew.
        ENDSELECT.
      ENDIF.

      SORT t_ebew BY matnr bwkey bwtar sobkz vbeln posnr.
    ENDIF.

    READ TABLE t_qbewkey INDEX 1 TRANSPORTING NO FIELDS.
    IF sy-subrc = 0.
      IF xmchb = 'X'.
        SELECT matnr bwkey bwtar sobkz pspnr lbkum salk3
               vprsv verpr stprs peinh
               CONNECTION (dbcon)                               "1710852
               INTO (t_qbew-matnr, t_qbew-bwkey, t_qbew-bwtar,
                     t_qbew-sobkz, t_qbew-pspnr,
                     t_qbew-lbkum, t_qbew-salk3,
                     t_qbew-vprsv, t_qbew-verpr,
                     t_qbew-stprs, t_qbew-peinh)
               FROM qbew
               FOR ALL ENTRIES IN t_qbewkey
               WHERE matnr = t_qbewkey-matnr
                 AND bwkey = t_qbewkey-bwkey
                AND bwtar = t_qbewkey-bwtar.
          COLLECT t_qbew.
        ENDSELECT.
      ELSE.
        SELECT matnr bwkey bwtar sobkz pspnr lbkum salk3
               vprsv verpr stprs peinh
               CONNECTION (dbcon)                               "1710852
               INTO (t_qbew-matnr, t_qbew-bwkey, t_qbew-bwtar,
                     t_qbew-sobkz, t_qbew-pspnr,
                     t_qbew-lbkum, t_qbew-salk3,
                     t_qbew-vprsv, t_qbew-verpr,
                     t_qbew-stprs, t_qbew-peinh)
               FROM qbew
               FOR ALL ENTRIES IN t_qbewkey
               WHERE matnr = t_qbewkey-matnr
                 AND bwkey = t_qbewkey-bwkey.
          COLLECT t_qbew.
        ENDSELECT.
      ENDIF.

      SORT t_qbew BY matnr bwkey bwtar sobkz pspnr.
    ENDIF.

    READ TABLE t_t134mkey INDEX 1 TRANSPORTING NO FIELDS.
    IF sy-subrc = 0.
      SELECT bwkey mtart wertu
             INTO CORRESPONDING FIELDS OF TABLE t_t134m
             FROM t134m
             FOR ALL ENTRIES IN t_t134mkey
             WHERE bwkey = t_t134mkey-bwkey
               AND mtart = t_t134mkey-mtart.
      SORT t_t134m BY bwkey mtart.
    ENDIF.
************************************************************************
* Fill the valuation data
************************************************************************
    DATA: factor TYPE f.
    LOOP AT bestand.
      CHECK bestand-waers <> space.  "Do nothing for failed Auth-Checks
      READ TABLE t_t134m WITH KEY bwkey = bestand-bwkey
                                  mtart = bestand-mtart
                                  BINARY SEARCH.
      CHECK sy-subrc = 0 AND t_t134m-wertu = 'X'.
*     Set SY-SUBRC = 4. A successful table read resets it an starts
*     the value filling.
      sy-subrc = 4.
      IF  bestand-sobkz = ' ' OR bestand-sobkz = 'O' OR
          bestand-sobkz = 'W' OR bestand-sobkz = 'V' OR
          bestand-kzbws = 'A'.
        READ TABLE t_mbew WITH KEY matnr = bestand-matnr
                                   bwkey = bestand-bwkey
                                   bwtar = bestand-bwtar
                                   BINARY SEARCH.

      ELSEIF bestand-sobkz = 'E' AND bestand-kzbws = 'M'.
        READ TABLE t_ebew WITH KEY matnr = bestand-matnr
                                   bwkey = bestand-bwkey
                                   bwtar = bestand-bwtar
                                   sobkz = bestand-sobkz
                                   vbeln = bestand-vbeln    "n531604
                                   posnr = bestand-posnr    "n531604
                                   BINARY SEARCH.
        MOVE-CORRESPONDING t_ebew TO t_mbew.

      ELSEIF bestand-sobkz = 'Q' AND bestand-kzbws = 'M'.
        READ TABLE t_qbew WITH KEY matnr = bestand-matnr
                                   bwkey = bestand-bwkey
                                   bwtar = bestand-bwtar
                                   sobkz = bestand-sobkz
                                   pspnr = bestand-pspnr
                                   BINARY SEARCH.
        MOVE-CORRESPONDING t_qbew TO t_mbew.
      ENDIF.
*ENHANCEMENT-POINT EHP604_RM07MLBS_45 SPOTS ES_RM07MLBS .

      IF sy-subrc = 0.
        IF t_mbew-lbkum = 0.
*         Cannot happen, but in R/3 this does not hold in all cases...
          IF t_mbew-peinh = 0.                                   "353428
            t_mbew-peinh = 1.                                    "353428
          ENDIF.                                                 "353428
*         Calculation of value in case of LBKUM = 0 only possible
*         for MBEW. EBEW and QBEW are collected over all subitems
*         (VBELN...), so the data are not available.
          IF bestand-sobkz = 'E' OR bestand-sobkz = 'Q'.         "388735
            factor = 0.                                          "388735
            CLEAR bestand-waers.                                 "388735
          ELSE.                                                  "388735
            CASE t_mbew-vprsv.
              WHEN 'V'. factor = t_mbew-verpr / t_mbew-peinh.
              WHEN 'S'. factor = t_mbew-stprs / t_mbew-peinh.
            ENDCASE.
          ENDIF.                                                 "388735
        ELSE.
          factor = t_mbew-salk3 / t_mbew-lbkum.
        ENDIF.

        bestand-wlabs = bestand-labst * factor.
        bestand-winsm = bestand-insme * factor.
        bestand-wspem = bestand-speme * factor.
        bestand-weinm = bestand-einme * factor.
        bestand-wumlm = bestand-umlme * factor.
        bestand-wbwesb = bestand-bwesb * factor.         "AC0K020254
        bestand-wglgm = bestand-glgmg * factor.                 "n912093
        bestand-wtram = bestand-trame * factor.                 "n912093
        bestand-wumlc = bestand-umlmc * factor.                 "n912093
*ENHANCEMENT-POINT EHP604_RM07MLBS_22 SPOTS ES_RM07MLBS .
        MODIFY bestand.
      ENDIF.
    ENDLOOP.
  ENDIF.                               "novalues is initial
*ENHANCEMENT-POINT EHP604_RM07MLBS_23 SPOTS ES_RM07MLBS .
ENDFORM.

************************************************************************
* Build fieldcatalog for list viewer
************************************************************************
FORM fieldcatalog.

* Header fields
  CLEAR fieldcat.
*ENHANCEMENT-SECTION     RM07MLBS_08 SPOTS ES_RM07MLBS.
  fieldcat-fieldname     = 'MATNR'.
  fieldcat-tabname       = 'HEADER'.
  fieldcat-ref_tabname   = 'MARA'.
*END-ENHANCEMENT-SECTION.
  APPEND fieldcat.
  CLEAR fieldcat.
  fieldcat-fieldname     = 'MAKTX'.
  fieldcat-tabname       = 'HEADER'.
  fieldcat-ref_tabname   = 'MAKT'.
  APPEND fieldcat.
  CLEAR fieldcat.
  fieldcat-fieldname     = 'WERKS'.
  fieldcat-tabname       = 'HEADER'.
  fieldcat-ref_tabname   = 'T001W'.
  APPEND fieldcat.
  CLEAR fieldcat.
  fieldcat-fieldname     = 'NAME1'.
  fieldcat-tabname       = 'HEADER'.
  fieldcat-ref_tabname   = 'T001W'.
  APPEND fieldcat.
*ENHANCEMENT-POINT RM07MLBS_1 SPOTS ES_RM07MLBS .
  CLEAR fieldcat.
  fieldcat-fieldname     = 'MTART'.
  fieldcat-tabname       = 'HEADER'.
  fieldcat-ref_tabname   = 'MARA'.
  fieldcat-no_out        = 'X'.
  APPEND fieldcat.
  CLEAR fieldcat.
  fieldcat-fieldname     = 'MATKL'.
  fieldcat-tabname       = 'HEADER'.
  fieldcat-ref_tabname   = 'MARA'.
  fieldcat-no_out        = 'X'.
  APPEND fieldcat.

* List body
  CLEAR fieldcat.
  fieldcat-fieldname     = 'LGORT'.
  fieldcat-tabname       = 'BESTAND'.
  fieldcat-ref_tabname   = 'MARD'.
  fieldcat-outputlen     = 5.
  APPEND fieldcat.

  CLEAR fieldcat.
  fieldcat-fieldname     = 'LGOBE'.
  fieldcat-tabname       = 'BESTAND'.
  fieldcat-ref_tabname   = 'T001L'.
  fieldcat-no_out        = 'X'.
  APPEND fieldcat.

  IF NOT pa_sond IS INITIAL.
    CLEAR fieldcat.
    fieldcat-fieldname     = 'SOBKZ'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_tabname   = 'MKOL'.
    APPEND fieldcat.

    CLEAR fieldcat.
    fieldcat-fieldname     = 'KZBWS'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_tabname   = 'MSSA'.
    fieldcat-outputlen     = 1.
    APPEND fieldcat.

    CLEAR fieldcat.
    fieldcat-fieldname     = 'SSNUM'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_tabname   = 'BICKEY'.
    APPEND fieldcat.
  ENDIF.

  if  xmchb = 'X'.
    CLEAR fieldcat.
    fieldcat-fieldname     = 'CHARG'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_tabname   = 'MCHB'.
    APPEND fieldcat.
  endif.

  CLEAR fieldcat.
  fieldcat-fieldname     = 'LVORM'.
  fieldcat-tabname       = 'BESTAND'.
  fieldcat-ref_tabname   = 'MARD'.
  fieldcat-outputlen     = 3.
  APPEND fieldcat.

* Quantities
  CLEAR fieldcat.
  fieldcat-fieldname     = 'LABST'.
  fieldcat-tabname       = 'BESTAND'.
  fieldcat-ref_tabname   = 'MARD'.
  fieldcat-qfieldname    = 'MEINS'.
  APPEND fieldcat.

  CLEAR fieldcat.
  fieldcat-fieldname     = 'MEINS'.
  fieldcat-tabname       = 'BESTAND'.
  fieldcat-ref_tabname   = 'MARA'.
  fieldcat-outputlen     = '5'.
  APPEND fieldcat.

  CLEAR fieldcat.                               "n912093
  fieldcat-fieldname     = 'UMLME'.             "n912093
  fieldcat-tabname       = 'BESTAND'.           "n912093
  fieldcat-ref_tabname   = 'AM07M'.             "n912093
  fieldcat-ref_fieldname = 'MB52_TRAUML'.       "n912093
  fieldcat-qfieldname    = 'MEINS'.             "n912093
  APPEND fieldcat.                              "n912093

  CLEAR fieldcat.
  fieldcat-fieldname     = 'INSME'.
  fieldcat-tabname       = 'BESTAND'.
  fieldcat-ref_tabname   = 'MARD'.
  fieldcat-qfieldname    = 'MEINS'.
  APPEND fieldcat.

  CLEAR fieldcat.
  fieldcat-fieldname     = 'EINME'.
  fieldcat-tabname       = 'BESTAND'.
  fieldcat-ref_tabname   = 'MARD'.
  fieldcat-qfieldname    = 'MEINS'.
  APPEND fieldcat.

  CLEAR fieldcat.
  fieldcat-fieldname     = 'SPEME'.
  fieldcat-tabname       = 'BESTAND'.
  fieldcat-ref_tabname   = 'MARD'.
  fieldcat-qfieldname    = 'MEINS'.
  APPEND fieldcat.

  CLEAR fieldcat.
  fieldcat-fieldname     = 'RETME'.
  fieldcat-tabname       = 'BESTAND'.
  fieldcat-ref_tabname   = 'MARD'.
  fieldcat-qfieldname    = 'MEINS'.
  APPEND fieldcat.

* process MARC-BWESB as hidden field                     "AC0K020254
  CLEAR fieldcat.                                        "AC0K020254
  fieldcat-fieldname     = 'BWESB'.                      "AC0K020254
  fieldcat-tabname       = 'BESTAND'.                    "AC0K020254
  fieldcat-ref_tabname   = 'MARC'.                       "AC0K020254
  fieldcat-qfieldname    = 'MEINS'.                      "AC0K020254
  fieldcat-no_out        = 'X'.                          "AC0K020254
  APPEND fieldcat.                                       "AC0K020254

* tied empties stock                                     "n912093
  fieldcat-fieldname     = 'GLGMG'.                      "n912093
  fieldcat-tabname       = 'BESTAND'.                    "n912093
  fieldcat-ref_tabname   = 'MARC'.                       "n912093
  fieldcat-qfieldname    = 'MEINS'.                      "n912093
  fieldcat-no_out      = 'X'.                            "n912093
  APPEND fieldcat.                                       "n912093
  CLEAR fieldcat.                                        "n912093

* stock in transit                                       "n912093
  fieldcat-fieldname     = 'TRAME'.                      "n912093
  fieldcat-tabname       = 'BESTAND'.                    "n912093
  fieldcat-ref_tabname   = 'MARC'.                       "n912093
  fieldcat-qfieldname    = 'MEINS'.                      "n912093
  fieldcat-no_out      = 'X'.                            "n912093
  APPEND fieldcat.                                       "n912093
  CLEAR fieldcat.                                        "n912093

* stock in uml                                           "n912093
  fieldcat-fieldname     = 'UMLMC'.                      "n912093
  fieldcat-tabname       = 'BESTAND'.                    "n912093
  fieldcat-ref_tabname   = 'MARC'.                       "n912093
  fieldcat-qfieldname    = 'MEINS'.                      "n912093
  fieldcat-no_out      = 'X'.                            "n912093
  APPEND fieldcat.                                       "n912093
  CLEAR fieldcat.                                        "n912093


* set the key fields of the special stock as hidden fields
  if  g_flag_sobkz-lifnr = 'X'.
    CLEAR fieldcat.
    fieldcat-fieldname     = 'LIFNR'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_tabname   = 'MKOL'.
    fieldcat-no_out        = 'X'.
    APPEND fieldcat.
  endif.

  if  g_flag_sobkz-kunnr = 'X'.
    CLEAR fieldcat.
    fieldcat-fieldname     = 'KUNNR'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_tabname   = 'MSKU'.
    fieldcat-no_out        = 'X'.
    APPEND fieldcat.
  endif.

  if  g_flag_sobkz-vbeln = 'X'.
    CLEAR fieldcat.
    fieldcat-fieldname     = 'VBELN'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_tabname   = 'MSKA'.
    fieldcat-no_out        = 'X'.
    APPEND fieldcat.

    CLEAR fieldcat.
    fieldcat-fieldname     = 'POSNR'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_tabname   = 'MSKA'.
    fieldcat-no_out        = 'X'.
    APPEND fieldcat.
  endif.

  if  g_flag_sobkz-pspnr = 'X'.
    CLEAR fieldcat.
    fieldcat-fieldname     = 'PSPNR'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_tabname   = 'MSPR'.
    fieldcat-no_out        = 'X'.
    APPEND fieldcat.
  endif.

* here starts the second row
* Values
  IF novalues IS INITIAL.
    CLEAR fieldcat.
    fieldcat-fieldname     = 'DUMMY'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-row_pos       = '2'.
    fieldcat-outputlen     = 9.

*   calculate the length of the dummy field
    IF xmchb = 'X'.
      ADD 11 TO fieldcat-outputlen.
    ENDIF.

    IF not pa_sond is initial.
      ADD 29 TO fieldcat-outputlen.
    ENDIF.

    APPEND fieldcat.

    CLEAR fieldcat.
    fieldcat-fieldname     = 'WLABS'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_fieldname = 'SALK3'.
    fieldcat-ref_tabname   = 'MBEW'.
    fieldcat-cfieldname    = 'WAERS'.
    fieldcat-row_pos       = '2'.
    fieldcat-seltext_s     = text-020.
    fieldcat-seltext_m     = text-020.
    fieldcat-seltext_m     = text-020.
    fieldcat-do_sum        = 'X'.
    APPEND fieldcat.
    CLEAR fieldcat.
    fieldcat-fieldname     = 'WAERS'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_fieldname = 'WAERS'.
    fieldcat-ref_tabname   = 'T001'.
    fieldcat-row_pos       = '2'.
    fieldcat-outputlen     = 5.
    APPEND fieldcat.
    CLEAR fieldcat.
    fieldcat-fieldname     = 'WUMLM'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_fieldname = 'SALK3'.
    fieldcat-ref_tabname   = 'MBEW'.
    fieldcat-cfieldname    = 'WAERS'.
    fieldcat-row_pos       = '2'.
    fieldcat-seltext_s     = text-020.
    fieldcat-seltext_m     = text-020.
    fieldcat-seltext_m     = text-020.
    fieldcat-outputlen     = '18'.
    fieldcat-do_sum        = 'X'.
    APPEND fieldcat.
    CLEAR fieldcat.
    fieldcat-fieldname     = 'WINSM'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_fieldname = 'SALK3'.
    fieldcat-ref_tabname   = 'MBEW'.
    fieldcat-cfieldname    = 'WAERS'.
    fieldcat-row_pos       = '2'.
    fieldcat-seltext_s     = text-020.
    fieldcat-seltext_m     = text-020.
    fieldcat-seltext_m     = text-020.
    fieldcat-outputlen     = '18'.
    fieldcat-do_sum        = 'X'.
    APPEND fieldcat.
    CLEAR fieldcat.
    fieldcat-fieldname     = 'WEINM'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_fieldname = 'SALK3'.
    fieldcat-ref_tabname   = 'MBEW'.
    fieldcat-cfieldname    = 'WAERS'.
    fieldcat-row_pos       = '2'.
    fieldcat-seltext_s     = text-020.
    fieldcat-seltext_m     = text-020.
    fieldcat-seltext_m     = text-020.
    fieldcat-outputlen     = '18'.
    fieldcat-do_sum        = 'X'.
    APPEND fieldcat.
    CLEAR fieldcat.
    fieldcat-fieldname     = 'WSPEM'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_fieldname = 'SALK3'.
    fieldcat-ref_tabname   = 'MBEW'.
    fieldcat-cfieldname    = 'WAERS'.
    fieldcat-row_pos       = '2'.
    fieldcat-seltext_s     = text-020.
    fieldcat-seltext_m     = text-020.
    fieldcat-seltext_m     = text-020.
    fieldcat-outputlen     = '18'.
    fieldcat-do_sum        = 'X'.
    APPEND fieldcat.

    CLEAR fieldcat.
    fieldcat-fieldname     = 'WRETM'.
    fieldcat-tabname       = 'BESTAND'.
    fieldcat-ref_fieldname = 'SALK3'.
    fieldcat-ref_tabname   = 'MBEW'.
    fieldcat-cfieldname    = 'WAERS'.
    fieldcat-row_pos       = '2'.
    fieldcat-seltext_s     = text-020.
    fieldcat-seltext_m     = text-020.
    fieldcat-seltext_m     = text-020.
    fieldcat-outputlen     = '18'.
    fieldcat-do_sum        = 'X'.
    APPEND fieldcat.

*  process estimated value MARC-BWESB as hidden field    "AC0K020254
   CLEAR fieldcat.                                       "AC0K020254
    fieldcat-fieldname     = 'WBWESB'.                   "AC0K020254
    fieldcat-tabname       = 'BESTAND'.                  "AC0K020254
    fieldcat-ref_tabname   = 'MBEW'.                     "AC0K020254
    fieldcat-ref_fieldname = 'SALK3'.                    "AC0K020254
    fieldcat-cfieldname    = 'WAERS'.                    "AC0K020254
    fieldcat-row_pos       = '2'.                        "AC0K020254
    fieldcat-seltext_s     = text-020.                   "AC0K020254
    fieldcat-seltext_m     = text-020.                   "AC0K020254
    fieldcat-seltext_m     = text-020.                   "AC0K020254
    fieldcat-outputlen     = '18'.                       "AC0K020254
    fieldcat-do_sum        = 'X'.                        "AC0K020254
    fieldcat-no_out        = 'X'.                        "AC0K020254
    APPEND fieldcat.                                     "AC0K020254

*   value for tied empties                               "n912093
    fieldcat-no_out      = 'X'.                          "n912093
    fieldcat-fieldname     = 'WGLGM'.                    "n912093
    fieldcat-tabname       = 'BESTAND'.                  "n912093
    fieldcat-ref_fieldname = 'SALK3'.                    "n912093
    fieldcat-ref_tabname   = 'MBEW'.                     "n912093
    fieldcat-cfieldname    = 'WAERS'.                    "n912093
    fieldcat-row_pos       = '2'.                        "n912093
    fieldcat-seltext_s     = text-020.                   "n912093
    fieldcat-seltext_m     = text-020.                   "n912093
    fieldcat-seltext_m     = text-020.                   "n912093
    fieldcat-outputlen     = '18'.                       "n912093
    fieldcat-do_sum        = 'X'.                        "n912093
    APPEND fieldcat.                                     "n912093

*   value for transit                                    "n912093
    fieldcat-no_out      = 'X'.                          "n912093
    fieldcat-fieldname     = 'WTRAM'.                    "n912093
    fieldcat-tabname       = 'BESTAND'.                  "n912093
    fieldcat-ref_fieldname = 'SALK3'.                    "n912093
    fieldcat-ref_tabname   = 'MBEW'.                     "n912093
    fieldcat-cfieldname    = 'WAERS'.                    "n912093
    fieldcat-row_pos       = '2'.                        "n912093
    fieldcat-seltext_s     = text-020.                   "n912093
    fieldcat-seltext_m     = text-020.                   "n912093
    fieldcat-seltext_m     = text-020.                   "n912093
    fieldcat-outputlen     = '18'.                       "n912093
    fieldcat-do_sum        = 'X'.                        "n912093
    APPEND fieldcat.

*   value for uml at plant                               "n912093
    fieldcat-no_out      = 'X'.                          "n912093
    fieldcat-fieldname     = 'WUMLC'.                    "n912093
    fieldcat-tabname       = 'BESTAND'.                  "n912093
    fieldcat-ref_fieldname = 'SALK3'.                    "n912093
    fieldcat-ref_tabname   = 'MBEW'.                     "n912093
    fieldcat-cfieldname    = 'WAERS'.                    "n912093
    fieldcat-row_pos       = '2'.                        "n912093
    fieldcat-seltext_s     = text-020.                   "n912093
    fieldcat-seltext_m     = text-020.                   "n912093
    fieldcat-seltext_m     = text-020.                   "n912093
    fieldcat-outputlen     = '18'.                       "n912093
    fieldcat-do_sum        = 'X'.                        "n912093
    APPEND fieldcat.

  ENDIF.                               "novalues is initial
*ENHANCEMENT-POINT EHP604_RM07MLBS_24 SPOTS ES_RM07MLBS .

ENDFORM.                               " FELDKATALOG_AUFBAUEN

************************************************************************
* Show the result list
************************************************************************
FORM list_output.

*  set pf-status 'STANDARD'.

*ENHANCEMENT-SECTION     RM07MLBS_09 SPOTS ES_RM07MLBS.
  keyinfo-header01 = 'MATNR'.
  keyinfo-header02 = 'WERKS'.
  keyinfo-item01   = 'MATNR'.
  keyinfo-item02   = 'WERKS'.
  keyinfo-item03   = 'LGORT'.

* new sort order
  SORT bestand BY matnr werks lgort
                  sobkz kzbws
                  lifnr kunnr vbeln  posnr  pspnr charg.
*END-ENHANCEMENT-SECTION.


  refresh : sort.
  CLEAR   : sort, g_cnt_spos.

* create the sort table for the ALV depending on the
* list type
  if      not pa_hsq is initial.
*   for hierarchic seq. list

  elseif  not  pa_flt is initial.
*   for flat ( simple ) list
*ENHANCEMENT-SECTION     RM07MLBS_10 SPOTS ES_RM07MLBS.
    perform  f0400_create_sort    using  'MATNR'.
*END-ENHANCEMENT-SECTION.
    perform  f0400_create_sort    using  'WERKS'.
  endif.

  perform  f0400_create_sort      using  'LGORT'.
  perform  f0400_create_sort      using  'SOBKZ'.
  perform  f0400_create_sort      using  'KZBWS'.
  perform  f0400_create_sort      using  'LIFNR'.
  perform  f0400_create_sort      using  'KUNNR'.

  perform  f0400_create_sort      using  'VBELN'.
  perform  f0400_create_sort      using  'POSNR'.
  perform  f0400_create_sort      using  'PSPNR'.


  DEFINE colourize.
    clear color.
    color-fieldname = &1.
    color-color-int = '0'.
    if &2 > 0.
      color-color-col = '5'.
    elseif &2 < 0.
      color-color-col = '6'.
    endif.
    append color.
    case &1.
      when 'LABST'.
        color-fieldname = 'MEINS'.
        append color.
      when 'WLABS'.
        color-fieldname = 'WAERS'.
        append color.
    endcase.
  END-OF-DEFINITION.
*ENHANCEMENT-POINT EHP604_RM07MLBS_26 SPOTS ES_RM07MLBS .

* skip this loop when the user wants a flat list without
* colors
  if  not pa_hsq     is initial  or
      not alv_color  =  'X'.
    LOOP AT bestand.
      if  not pa_hsq is initial.                            "n531604
*       create working table header only if a hierarchic    "n531604
*       list is required                                    "n531604
        ON CHANGE OF bestand-matnr OR bestand-werks.
          MOVE-CORRESPONDING bestand TO header.
*ENHANCEMENT-POINT RM07MLBS_2 SPOTS ES_RM07MLBS .
          APPEND header.
        ENDON.
      endif.                                                "n531604

*     create the table with the colour information          "n531604
*     depending on the customizing settings in table        "n531604
*     V_MMIM_REP_PRINT   'X' = no colors                    "n531604
      if  alv_color ne 'X'.                                 "n531604
        REFRESH color.
        colourize 'LABST' bestand-labst.
        colourize 'UMLME' bestand-umlme.
        colourize 'EINME' bestand-einme.
        colourize 'SPEME' bestand-speme.
        colourize 'RETME' bestand-retme.
        colourize 'INSME' bestand-insme.
        colourize 'WLABS' bestand-wlabs.
        colourize 'WUMLM' bestand-wumlm.
        colourize 'WEINM' bestand-weinm.
        colourize 'WSPEM' bestand-wspem.
        colourize 'WRETM' bestand-wretm.
        colourize 'WINSM' bestand-winsm.
        colourize 'BWESB' bestand-bwesb.                 "AC0K020254
        colourize 'WBWESB' bestand-Wbwesb.               "AC0K020254
        colourize 'GLGMG' bestand-glgmg.                    "n912093
        colourize 'WGLGM' bestand-wglgm.                    "n912093
        colourize 'TRAME' bestand-trame.                    "n912093
        colourize 'WTRAM' bestand-wtram.                    "n912093
        colourize 'UMLMC' bestand-umlmc.                    "n912093
        colourize 'WUMLC' bestand-wumlc.                    "n912093
*ENHANCEMENT-POINT EHP604_RM07MLBS_27 SPOTS ES_RM07MLBS .
        bestand-farbe = color[].
        MODIFY bestand.
      endif.                                                "n531604
    ENDLOOP.
  endif.                                                    "n531604

* set the name for color table when required                "n531604
  if  alv_color = 'X'.                                      "n531604
    clear                    layout-coltab_fieldname.       "n531604
  else.                                                     "n531604
    move  'FARBE'            to  layout-coltab_fieldname.   "n531604
  endif.                                                    "n531604

  layout-group_change_edit = 'X'.

  data : l_f_check(01)       type c.
*ENHANCEMENT-POINT EHP604_RM07MLBS_28 SPOTS ES_RM07MLBS STATIC .

* added dynamic break-point ID MMIM_REP_MB52                "n912093
  BREAK-POINT ID MMIM_REP_MB52.                             "n912093

* process the list according the parameters                 "n531604
    if      not pa_hsq is initial.                          "n531604
*     create a hierarchic list                              "n531604

*     assign form routine for page numbering                "n667256
      gs_events-name         =       'TOP_OF_PAGE'.         "n667256
      gs_events-form         = 'F4000_TOP_OF_PAGE'.         "n667256
      APPEND  gs_events      to gt_events.                  "n667256

*ENHANCEMENT-POINT EHP604_RM07MLBS_29 SPOTS ES_RM07MLBS .


      CALL FUNCTION 'REUSE_ALV_HIERSEQ_LIST_DISPLAY'
       EXPORTING
            I_INTERFACE_CHECK  = l_f_check
            i_callback_program = repid
            is_layout          = layout
            it_fieldcat        = fieldcat[]
            i_default          = 'X'
            i_save             = 'A'
            is_variant         = variante
            it_events          = gt_events[]                "n667256
            i_tabname_header   = 'HEADER'
            i_tabname_item     = 'BESTAND'
            is_keyinfo         = keyinfo
            is_print           = alv_print
            it_sort            = sort[]
            it_excluding       = excluding[]
       TABLES
            t_outtab_header    = header
            t_outtab_item      = bestand
       EXCEPTIONS
            OTHERS             = 2.

  elseif  not  pa_flt is initial.                         "n531604
*   create a flat non-hierarchic list                     "n531604

*   assign form routine for page numbering only for       "n667256
*   classic ALV                                           "n667256
    if  alv_detail_func = 'REUSE_ALV_LIST_DISPLAY'.       "n667256
      gs_events-name       =       'TOP_OF_PAGE'.         "n667256
      gs_events-form       = 'F4000_TOP_OF_PAGE'.         "n667256
      APPEND  gs_events    to gt_events.                  "n667256
    endif.

*ENHANCEMENT-POINT EHP604_RM07MLBS_30 SPOTS ES_RM07MLBS .

    CALL FUNCTION                  alv_detail_func
      EXPORTING
        I_INTERFACE_CHECK          = l_f_check
        I_CALLBACK_PROGRAM         = repid
        IS_LAYOUT                  = layout
        IT_FIELDCAT                = fieldcat[]
        IT_SORT                    = sort[]
        I_DEFAULT                  = 'X'
        I_SAVE                     = 'A'
        IS_VARIANT                 = variante_flat
        it_events                  = gt_events[]          "n667256
        IS_PRINT                   = alv_print
      TABLES
        T_OUTTAB                   = bestand
      EXCEPTIONS
        OTHERS                     = 2.
  endif.                                                  "n531604

  IF SY-SUBRC <> 0.
    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
         WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

ENDFORM.                               " LISTAUSGABE

*&---------------------------------------------------------------------*
*&      Form  F4_FOR_VARIANT
*&---------------------------------------------------------------------*
*       F4-Hilfe fr Reportvariante                                    *
*----------------------------------------------------------------------*
FORM f4_for_variant.

* look for the available display variant depending on the   "n531604
* selected mode of the SAP-LIST-VIEWER
    if      not pa_hsq is initial.
*     for hierarchic seq. list
      MOVE variante          TO  def_variante_f4.

    elseif  not  pa_flt is initial.
*     for flat ( simple ) list
      MOVE variante_flat     TO  def_variante_f4.
    endif.

  CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
       EXPORTING
            is_variant          = def_variante_f4
            i_save              = 'A'
*           it_default_fieldcat =
       IMPORTING
            e_exit              = variant_exit
            es_variant          = def_variante
       EXCEPTIONS
            not_found = 2.

  IF sy-subrc = 2.
    MESSAGE ID sy-msgid TYPE 'S' NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    IF variant_exit = space.
*     save the selected display variant
      p_vari = def_variante-variant.

      if      not pa_hsq is initial.
*       for hierarchic seq. list
        move  p_vari         TO  g_f_vari_hsq.

      elseif  not  pa_flt is initial.
*       for flat ( simple ) list
        MOVE  p_vari         TO  g_f_vari_flt.
      endif.
    ENDIF.
  ENDIF.

ENDFORM.                               " F4_FOR_VARIANT

*-----------------------------------------------------------"n531604
*    f0000_get_print_settings
*-----------------------------------------------------------"n531604

form f0000_get_print_settings.

* get the settings for the SAP-LIST-VIEWER
  select single * from mmim_rep_print
                             where report = sy-repid.

  if sy-subrc <> 0.
*   if this entry is missing, set default values and insert
*   a new entry into database tabel MMIM_REP_PRINT
    clear                    mmim_rep_print.
    move : sy-repid          to  mmim_rep_print-report,
           'X'               to  mmim_rep_print-selinfo.
    insert                   mmim_rep_print.

    if  sy-subrc is initial.
      commit                 work.
    else.
*     error during insert into table MMIN_RE_PRINT
      message  s895          with  text-895.
    endif.
  endif.

* set the parameter for this run
  if mmim_rep_print-selinfo = 'X'.
    alv_print-no_print_selinfos = ' '.
  else.
    alv_print-no_print_selinfos = 'X'.
  endif.

  if mmim_rep_print-coverpage = 'X'.
    alv_print-no_coverpage = ' '.
  else.
    alv_print-no_coverpage = 'X'.
  endif.

  if mmim_rep_print-listinfo = 'X'.
    alv_print-no_print_listinfos = ' '.
  else.
    alv_print-no_print_listinfos = 'X'.
  endif.

  if mmim_rep_print-gridcontrol = 'X'.
    alv_detail_func = 'REUSE_ALV_GRID_DISPLAY'.
  else.
    alv_detail_func = 'REUSE_ALV_LIST_DISPLAY'.
  endif.

  if mmim_rep_print-color = 'X'.
    alv_color       = 'X'.
  else.
    alv_color       = space.
  endif.

endform.                     "f0000_get_print_settings

*-----------------------------------------------------------"n531604
* Initialization of the user defaults for the checkboxes
*    f0100_settings_init.                                   "n531604
*-----------------------------------------------------------"n531604

FORM f0100_settings_init.                                   "n531604

* only in dialog mode
  check : sy-batch is initial.

  IF oref_settings IS INITIAL.
    CREATE OBJECT oref_settings
                             EXPORTING i_action = 'RM07MLBS'.
  ENDIF.

* get the parameters from the last run
  pa_sond   = oref_settings->get( 'PA_SOND' ).
  pa_hsq    = oref_settings->get( 'PA_HSQ' ).

  pa_flt    = oref_settings->get( 'PA_FLT' ).
  negativ   = oref_settings->get( 'NEGATIV' ).
  xmchb     = oref_settings->get( 'XMCHB'   ).
  nozero    = oref_settings->get( 'NOZERO'  ).
  novalues  = oref_settings->get( 'NOVALUES' ).

* check radiobuttons
  if  pa_hsq is initial.
    if  pa_flt is initial.
*     not allowed
      move  'X'              to  pa_hsq.
    endif.
  else.
    if  pa_flt is initial.
    else.
*     not allowed
      clear                  pa_flt.
    endif.
  endif.

ENDFORM.                     "f0100_settings_init           "n531604

************************************************************************
* Save the user settings
************************************************************************

FORM f0200_settings_save.                                   "n531604

* only in dialog mode
  check : sy-batch is initial.

* go on when the FORM routines of INITILIZATION             "n667256
* were processed                                            "n667256
  check : g_flag_initialization = 'X'.                      "n667256

* Save the settings
  CALL METHOD oref_settings->set( i_element = 'PA_SOND'
                                  i_active  =  pa_sond    ).
  CALL METHOD oref_settings->set( i_element = 'PA_HSQ'
                                  i_active  =  pa_hsq ).
  CALL METHOD oref_settings->set( i_element = 'PA_FLT'
                                  i_active  =  pa_flt ).
  CALL METHOD oref_settings->set( i_element = 'NEGATIV'
                                  i_active  =  negativ  ).
  CALL METHOD oref_settings->set( i_element = 'XMCHB'
                                  i_active  =  xmchb    ).

  CALL METHOD oref_settings->set( i_element = 'NOZERO'
                                  i_active  =  nozero   ).
  CALL METHOD oref_settings->set( i_element = 'NOVALUES'
                                  i_active  =  novalues ).

  CALL METHOD oref_settings->flush.

* carry out the database updates only; the normal commit    "n667256
* command does not allow to record this transaction for     "n667256
* a batch input session using transaction SHDB              "n667256
  CALL FUNCTION 'DB_COMMIT'.                                "n667256

ENDFORM.                     "f0200_settings_save           "n531604

*----------------------------------------------------------------------*
*    f0300_fieldcat_flat
*----------------------------------------------------------------------*

form f0300_fieldcat_flat.

* define macro
  DEFINE macro_fill_fieldcat.
    ADD  : 1                 TO  g_cnt_COL_POS.
    move : g_cnt_col_pos     TO  fieldcat-COL_POS,
           &1                to  fieldcat-fieldname,
           'BESTAND'         to  fieldcat-tabname,
           &2                to  fieldcat-ref_tabname,
           &3                to  fieldcat-no_out.

    if  not fieldcat-seltext_l is initial.
      move : fieldcat-seltext_l   to  fieldcat-seltext_m,
             fieldcat-seltext_l   to  fieldcat-seltext_s.
    endif.

    append                   fieldcat.
    clear                    fieldcat.
  END-OF-DEFINITION.

*ENHANCEMENT-SECTION     RM07MLBS_11 SPOTS ES_RM07MLBS.
  macro_fill_fieldcat 'MATNR'  'MARA'   c_out.
*END-ENHANCEMENT-SECTION.
  macro_fill_fieldcat 'MAKTX'  'MAKT'   c_no_out.
  macro_fill_fieldcat 'WERKS'  'T001W'  c_out.
  macro_fill_fieldcat 'NAME1'  'T001W'  c_no_out.
  macro_fill_fieldcat 'MTART'  'MARA'   c_no_out.
  macro_fill_fieldcat 'MATKL'  'MARA'   c_no_out.
  macro_fill_fieldcat 'LGORT'  'MARD'   c_out.
  macro_fill_fieldcat 'LGOBE'  'T001L'  c_no_out.           "1311282

  IF NOT pa_sond IS INITIAL.
    macro_fill_fieldcat 'SOBKZ'  'MKOL'   c_out.

    if  novalues is initial.
      macro_fill_fieldcat 'KZBWS'  'MSSA'   c_out.
    endif.

    MOVE : 'SSNUM'           TO  fieldcat-ref_fieldname.
    macro_fill_fieldcat 'SSNUM' 'BICKEY' c_out.
  ENDIF.

  macro_fill_fieldcat 'LVORM'  'MARD'   c_out.

  IF xmchb = 'X'.
    macro_fill_fieldcat 'CHARG'  'MCHB'   c_out.
  endif.

  macro_fill_fieldcat 'MEINS'  'MARA'   c_out.

* Stock and value for stock unrestrestricted use
  fieldcat-qfieldname    = 'MEINS'.
  macro_fill_fieldcat 'LABST'  'MARD'   c_out.
*ENHANCEMENT-POINT EHP604_RM07MLBS_31 SPOTS ES_RM07MLBS .

  IF novalues IS INITIAL.
    fieldcat-outputlen     = 5.
    macro_fill_fieldcat 'WAERS'  'T001'   c_out.

    fieldcat-ref_fieldname = 'SALK3'.
    fieldcat-cfieldname    = 'WAERS'.
*   'Wert frei verwend.'.
    fieldcat-seltext_l     = text-021.
    fieldcat-do_sum        = 'X'.
    fieldcat-outputlen     = '18'.
    macro_fill_fieldcat 'WLABS'  'MBEW'   c_out.
  endif.

* stock and value for stock in transfer
  fieldcat-qfieldname    = 'MEINS'.
  fieldcat-ref_fieldname = 'MB52_TRAUML'.              "n912093
  macro_fill_fieldcat 'UMLME'  'AM07M'   c_out.        "n912093
*ENHANCEMENT-POINT EHP604_RM07MLBS_32 SPOTS ES_RM07MLBS .

  IF novalues IS INITIAL.
    fieldcat-ref_fieldname = 'SALK3'.
    fieldcat-cfieldname    = 'WAERS'.
*   'Wert in Umlagerung'. // Wert Umlag u. Transit     "n912093
*   'Wert Umlag.Bestand'. // n912093                   "n912093
    fieldcat-seltext_l     = text-030.                 "n912093
    fieldcat-do_sum        = 'X'.
    fieldcat-outputlen     = '18'.
    macro_fill_fieldcat 'WUMLM'  'MBEW'   c_out.
  endif.

* stock and value for stock in quality inspection
  fieldcat-qfieldname    = 'MEINS'.
  macro_fill_fieldcat 'INSME'  'MARD'   c_out.
*ENHANCEMENT-POINT EHP604_RM07MLBS_33 SPOTS ES_RM07MLBS .

  IF novalues IS INITIAL.
    fieldcat-ref_fieldname = 'SALK3'.
    fieldcat-cfieldname    = 'WAERS'.
*   "Wert in QualPrfng'
    fieldcat-seltext_l     = text-023.
    fieldcat-do_sum        = 'X'.
    fieldcat-outputlen     = '18'.
    macro_fill_fieldcat 'WINSM'  'MBEW'   c_out.
  endif.

* stock and value for restricted stock
  fieldcat-qfieldname    = 'MEINS'.
  macro_fill_fieldcat 'EINME'  'MARD'   c_out.
*ENHANCEMENT-POINT EHP604_RM07MLBS_34 SPOTS ES_RM07MLBS .

  IF novalues IS INITIAL.
    fieldcat-ref_fieldname = 'SALK3'.
    fieldcat-cfieldname    = 'WAERS'.
*   "Wert nicht frei'
    fieldcat-seltext_l     = text-024.
    fieldcat-do_sum        = 'X'.
    fieldcat-outputlen     = '18'.
    macro_fill_fieldcat 'WEINM'  'MBEW'   c_out.
  endif.

* stock and value for blocked stock
  fieldcat-qfieldname    = 'MEINS'.
  macro_fill_fieldcat 'SPEME'  'MARD'   c_out.
*ENHANCEMENT-POINT EHP604_RM07MLBS_35 SPOTS ES_RM07MLBS .

  IF novalues IS INITIAL.
    fieldcat-ref_fieldname = 'SALK3'.
    fieldcat-cfieldname    = 'WAERS'.
*   'Wert Sperrbestand'
    fieldcat-seltext_l     = text-025.
    fieldcat-do_sum        = 'X'.
    fieldcat-outputlen     = '18'.
    macro_fill_fieldcat 'WSPEM'  'MBEW'   c_out.
  endif.

* stock and value for blocked returns
  fieldcat-qfieldname    = 'MEINS'.
  macro_fill_fieldcat 'RETME'  'MARD'   c_out.
*ENHANCEMENT-POINT EHP604_RM07MLBS_36 SPOTS ES_RM07MLBS .

* Values
  IF novalues IS INITIAL.
    fieldcat-ref_fieldname = 'SALK3'.
    fieldcat-cfieldname    = 'WAERS'.
*   "Wert RetourenSperr'.
    fieldcat-seltext_l     = text-026.
    fieldcat-do_sum        = 'X'.
    fieldcat-outputlen     = '18'.
    macro_fill_fieldcat 'WRETM'  'MBEW'   c_out.
  ENDIF.                               "novalues is initial

* process valuated block GR stock as hidden field        "AC0K020254
  fieldcat-qfieldname    = 'MEINS'.                      "AC0K020254
  macro_fill_fieldcat 'BWESB'  'MARC'   c_no_out.        "AC0K020254
*ENHANCEMENT-POINT EHP604_RM07MLBS_37 SPOTS ES_RM07MLBS .
                                                         "AC0K020254
* the estimated value for the valuated block GR stock    "AC0K020254
* as hidden field, too                                   "AC0K020254
  IF novalues IS INITIAL.                                "AC0K020254
    fieldcat-ref_fieldname = 'SALK3'.                    "AC0K020254
    fieldcat-cfieldname    = 'WAERS'.                    "AC0K020254
*   value blocked GR stock                               "AC0K020254
    fieldcat-seltext_l     = text-027.                   "AC0K020254
    fieldcat-do_sum        = 'X'.                        "AC0K020254
    fieldcat-outputlen     = '18'.                       "AC0K020254
    macro_fill_fieldcat 'WBWESB'  'MBEW'   c_no_out.     "AC0K020254
  endif.

* stock and value for tied empties                    "n912093
  fieldcat-qfieldname    = 'MEINS'.                   "n912093
  macro_fill_fieldcat 'GLGMG'  'MARC'   c_no_out.     "n912093
*ENHANCEMENT-POINT EHP604_RM07MLBS_38 SPOTS ES_RM07MLBS .
                                                      "n912093
* Values                                              "n912093
  IF novalues IS INITIAL.                             "n912093
    fieldcat-ref_fieldname = 'SALK3'.                 "n912093
    fieldcat-cfieldname    = 'WAERS'.                 "n912093
*   'Wert gebundenes Leergut'                         "n912093
    fieldcat-seltext_l     = text-028.                "n912093
    fieldcat-do_sum        = 'X'.                     "n912093
    fieldcat-outputlen     = '18'.                    "n912093
    macro_fill_fieldcat 'WGLGM'  'MBEW'   c_no_out.   "n912093
  ENDIF.                                              "n912093


* stock and value for stock in transit                "n912093
  fieldcat-qfieldname    = 'MEINS'.                   "n912093
  macro_fill_fieldcat 'TRAME'  'MARC'   c_no_out.     "n912093
*ENHANCEMENT-POINT EHP604_RM07MLBS_39 SPOTS ES_RM07MLBS .
                                                      "n912093
* Values                                              "n912093
  IF novalues IS INITIAL.                             "n912093
    fieldcat-ref_fieldname = 'SALK3'.                 "n912093
    fieldcat-cfieldname    = 'WAERS'.                 "n912093
*   'Wert Transitbestand'                             "n912093
    fieldcat-seltext_l     = text-029.                "n912093
    fieldcat-do_sum        = 'X'.                     "n912093
    fieldcat-outputlen     = '18'.                    "n912093
    macro_fill_fieldcat 'WTRAM'  'MBEW'   c_no_out.   "n912093
  ENDIF.                                              "n912093

* stock and value for stock in transit                "n912093
  fieldcat-qfieldname    = 'MEINS'.                   "n912093
  macro_fill_fieldcat 'UMLMC'  'MARC'   c_no_out.     "n912093
*ENHANCEMENT-POINT EHP604_RM07MLBS_40 SPOTS ES_RM07MLBS .
                                                      "n912093
* Values                                              "n912093
  IF novalues IS INITIAL.                             "n912093
    fieldcat-ref_fieldname = 'SALK3'.                 "n912093
    fieldcat-cfieldname    = 'WAERS'.                 "n912093
*   'Wert Umlagerung an Werk'                         "n912093
    fieldcat-seltext_l     = text-022.                "n912093
    fieldcat-do_sum        = 'X'.                     "n912093
    fieldcat-outputlen     = '18'.                    "n912093
    macro_fill_fieldcat 'WUMLC'  'MBEW'   c_no_out.   "n912093
  ENDIF.                                              "n912093

* set the key fields of the special stock as hidden fields
  if  g_flag_sobkz-lifnr = 'X'.
    macro_fill_fieldcat 'LIFNR'  'MKOL'   c_no_out.
  endif.

  if  g_flag_sobkz-kunnr = 'X'.
    macro_fill_fieldcat 'KUNNR'  'MSKU'   c_no_out.
  endif.

  if  g_flag_sobkz-vbeln = 'X'.
    macro_fill_fieldcat 'VBELN'  'MSKA'   c_no_out.
    macro_fill_fieldcat 'POSNR'  'MSKA'   c_no_out.
  endif.

  if  g_flag_sobkz-pspnr = 'X'.
    macro_fill_fieldcat 'PSPNR'  'MSPR'   c_no_out.
  endif.

endform.                     "f0300_fieldcat_flat

*----------------------------------------------------------------------*
*    f0400_create_sort
*----------------------------------------------------------------------*

form f0400_create_sort
                   using     l_f_fieldname like sort-fieldname.

* create the table with the alv sort information

  if      not pa_hsq is initial.
*   for hierarchic seq. list
*   check whether this is an active field is in the
*   fieldcat
    read table fieldcat with key
                             fieldname = l_f_fieldname
                             no_out    = space.

  elseif  not  pa_flt is initial.
*   for flat ( simple ) list
*   check whether this field is in the fieldcat
    read table fieldcat with key
                             fieldname = l_f_fieldname.
  endif.

  if sy-subrc is initial.
    add  1                   to  g_cnt_spos.
    move : g_cnt_spos        to  sort-spos,
           l_f_fieldname     to  sort-fieldname,
           'X'               to  sort-up,
           'BESTAND'         to  sort-tabname.
    append                   sort.
    clear                    sort.
  endif.

endform.                     "f0400_create_sort

*----------------------------------------------------------------------*
*    f2000_COLLECT_collector.
*----------------------------------------------------------------------*

form f2000_COLLECT_collector.

* does the user want to suppress stock objects from plant   "n577268
* level ?                                                   "n577268
  if  g_flag_suppress_init_lgort = 'X'.                     "n577268
    if  collector-lgort is initial.                         "n577268
*     ignore stock objects without storage location         "n577268
      exit.                  "--> go to exit                "n577268
    endif.                                                  "n577268
  endif.                                                    "n577268

************************************************************************
* process the functions "No zero stocks",
* "Negative stocks only", "Without batches" here
************************************************************************
*ENHANCEMENT-POINT EHP604_RM07MLBS_41 SPOTS ES_RM07MLBS .

  IF negativ = 'X'.
*   ignore entry if all stocks are zero or greater
    IF collector-labst >= 0 AND collector-einme >= 0 AND
       collector-insme >= 0 AND collector-retme >= 0 AND
       collector-speme >= 0 AND collector-umlme >= 0.
      exit.                  "--> go to exit
    endif.
  ENDIF.

  IF nozero = 'X'.
*   ignore all entries without stock
    IF collector-labst = 0 AND collector-einme = 0 AND
       collector-insme = 0 AND collector-retme = 0 AND
       collector-speme = 0 AND collector-umlme = 0.
       exit.                  "--> go to exit
    ENDIF.
  ENDIF.

  IF xmchb IS INITIAL.
    CLEAR collector-charg.
  ENDIF.

  COLLECT                    collector.

endform.                     "f2000_COLLECT_collector.

*-----------------------------------------------------------"n579976
*     F3000_SEND_WARNING_M7_393                             "n579976
*-----------------------------------------------------------"n579976
                                                            "n579976
form F3000_SEND_WARNING_M7_393                              "n579976
         using     l_variant like  disvariant-variant.      "n579976
                                                            "n579976
* check the customising settings : emerge warning 393 ?     "n579976
  CALL FUNCTION              'ME_CHECK_T160M'               "n579976
    EXPORTING                                               "n579976
      I_ARBGB                = 'M7'                         "n579976
      I_MSGNR                = '393'                        "n579976
    EXCEPTIONS                                              "n579976
      NOTHING                = 0                            "n579976
      OTHERS                 = 1.                           "n579976
                                                            "n579976
  IF SY-SUBRC <> 0.                                         "n579976
*   list will be created using the initial layout &         "n579976
    message i393             with  l_variant.               "n667256
  endif.                                                    "n579976
                                                            "n579976
endform.                     "F3000_SEND_WARNING_M7_393     "n579976
                                                            "n579976
*-----------------------------------------------------------"n579976


*-----------------------------------------------------------"n667256
*  F4000_TOP_OF_PAGE.                                       "n667256
*-----------------------------------------------------------"n667256
                                                            "n667256
at selection-screen output.                                 "n667256
                                                            "n667256
*ENHANCEMENT-POINT RM07MLBS_04 SPOTS ES_RM07MLBS.
  if  g_flag_initialization is initial.                     "n667256
*   the process time INITIALIZATION was not done, so        "n667256
*   carry out the functions here                            "n667256
    move  'X'                to g_flag_initialization.      "n667256
                                                            "n667256
    PERFORM                  f0000_get_print_settings.      "n667256
                                                            "n667256
*   look for the setting of the parameters from the         "n667256
*   last run                                                "n667256
    PERFORM                  f0100_settings_init.           "n667256
                                                            "n667256
    PERFORM                  initialisierung.               "n667256
  endif.                                                    "n667256
                                                            "n667256
*-----------------------------------------------------------"n667256

*------------------------- Datenselektion -----------------------------*
form F4000_TOP_OF_PAGE.                                     "n667256
                                                            "n667256
* go on if there is a print destination set                 "n667256
  check   not sy-prdsn is initial.                          "n667256

* go on if it is in print modus, only                       "n960980
  check sy-ucomm = '&RNT' OR sy-ucomm is initial.           "n960980

                                                            "n667256
*     classic ALV : use the simple write command            "n667256
      write : sy-datlo dd/mm/yyyy, sy-title, sy-pagno.      "n667256
                                                            "n667256
                                                            "n667256
endform.                     "F4000_TOP_OF_PAGE.            "n667256
                                                            "n667256
*-----------------------------------------------------------"n667256

*&---------------------------------------------------------------------*
*&      Form  data_selection_new
*&---------------------------------------------------------------------*
FORM data_selection_new.                                    "n1795093

* Materials to be processed
  TYPES: BEGIN OF ty_mat,
           matnr LIKE mara-matnr,
           werks LIKE marc-werks,
           xchar LIKE marc-xchar,
           mtart LIKE mara-mtart,
           matkl LIKE mara-matkl,
           meins LIKE mara-meins,
           trame LIKE marc-trame,
           umlmc LIKE marc-umlmc,
           glgmg LIKE marc-glgmg,                           "n912093
           bwesb             LIKE  marc-bwesb,              "AC0K020254
           lvorm_mara        LIKE  mara-lvorm,
           lvorm_marc        LIKE  marc-lvorm,
           maktx LIKE makt-maktx.                           "n1795093
  TYPES:  END OF ty_mat.

  DATA: t_mat     TYPE ty_mat OCCURS 0 WITH HEADER LINE.

* buffer for reading working tables
  DATA : l_s_mat             TYPE  ty_mat,
         l_f_matnr           LIKE  makt-matnr.

  DATA: sql_ex TYPE REF TO cx_sy_open_sql_db.

  RANGES: r_sobkz FOR mkol-sobkz.

  DATA:
*       workin table for the material stock valuation
        BEGIN OF t_mbew OCCURS 0,
          matnr LIKE mbew-matnr,
          bwkey LIKE mbew-bwkey,
          bwtar LIKE mbew-bwtar,
*         consider the valuation of the special stocks E, Q "n531604
          sobkz              LIKE  ebew-sobkz,              "n531604
          vbeln              LIKE  ebew-vbeln,              "n531604
          posnr              LIKE  ebew-posnr,              "n531604
          pspnr              LIKE  qbew-pspnr,              "n531604

          lbkum(12)  TYPE p DECIMALS 3,                     "407810
          salk3(12)  TYPE p DECIMALS 2,                     "388735
          vprsv LIKE mbew-vprsv,                            "353428
          verpr LIKE mbew-verpr,                            "353428
          stprs LIKE mbew-stprs,                            "353428
          peinh LIKE mbew-peinh,                            "353428
        END OF t_mbew.

  DATA: t_ebew    LIKE t_mbew    OCCURS 0 WITH HEADER LINE.
  DATA: t_qbew    LIKE t_mbew    OCCURS 0 WITH HEADER LINE.

  DATA: lv_subrc LIKE sy-subrc.

  DATA: r_stopt  TYPE RANGE OF labst,
        lsr_stopt LIKE LINE OF r_stopt.

  DATA: factor TYPE f.

  DATA: r_matnr TYPE RANGE OF matnr,
        lsr_matnr LIKE LINE OF r_matnr,
        ls_matnr LIKE LINE OF collector.

  DATA: lv_exists TYPE mandt.

  DATA: lv_xmbew type xfeld,                             "1895376
        lv_xebew type xfeld,                             "1895376
        lv_xqbew type xfeld.                             "1895376

  DATA: lv_dynwhere TYPE string.                            "1917052
************************************************************************
* Read material master data (MARA and MARC)
************************************************************************
  REFRESH collector.

* evaluate check boxes...
  IF ( negativ = 'X' AND nozero = 'X' ) OR negativ = 'X'.
    lsr_stopt-sign   = 'I'.
    lsr_stopt-option = 'LT'. "less than
    lsr_stopt-low    = '0'.
    APPEND lsr_stopt TO r_stopt.
  ELSEIF nozero = 'X'.
    lsr_stopt-sign   = 'I'.
    lsr_stopt-option = 'GT'. "greater than
    lsr_stopt-low    = '0'.
    APPEND lsr_stopt TO r_stopt.
    lsr_stopt-sign   = 'I'.
    lsr_stopt-option = 'LT'. "less than
    lsr_stopt-low    = '0'.
    APPEND lsr_stopt TO r_stopt.
  ELSE.
    REFRESH r_stopt.
  ENDIF.

  BREAK-POINT ID mmim_rep_mb52.
*ENHANCEMENT-POINT RM07MLBS_14 SPOTS ES_RM07MLBS .

  TRY.

* process SELECT command depending on the
* required material selection

* Get MARC stock and master data plus maktx
      CLEAR t_mat[].

      IF lgort[] IS INITIAL.                                                                   "1904998
      SELECT mara~matnr werks xchar mtart matkl meins SUM( trame ) AS trame SUM( umlmc ) AS umlmc
             SUM( bwesb ) AS bwesb SUM( glgmg ) AS glgmg maktx
           mara~lvorm AS lvorm_mara
           marc~lvorm AS lvorm_marc
      CONNECTION (dbcon)
           INTO CORRESPONDING FIELDS OF TABLE t_mat
           FROM mara
           INNER JOIN marc
              ON mara~matnr = marc~matnr
           LEFT JOIN makt
              ON makt~matnr = mara~matnr AND
                 makt~spras = sy-langu
           WHERE mara~matnr IN matnr
             AND werks IN werks
             AND mtart IN matart
             AND matkl IN matkla
             AND ekgrp IN ekgrup
             AND (lv_dynwhere)                                                  "1917052
           GROUP BY mara~matnr werks xchar mtart matkl meins mara~lvorm marc~lvorm maktx.
      ELSE.                                                                                   "v1904998
       SELECT mara~matnr mard~werks xchar mtart matkl meins SUM( trame ) AS trame SUM( umlmc ) AS umlmc
             SUM( bwesb ) AS bwesb SUM( glgmg ) AS glgmg maktx
           mara~lvorm AS lvorm_mara
           marc~lvorm AS lvorm_marc
      CONNECTION (dbcon)
           INTO CORRESPONDING FIELDS OF TABLE t_mat
           FROM mara
           INNER JOIN marc
              ON mara~matnr = marc~matnr
           INNER JOIN mard
             ON  mard~matnr = marc~matnr AND
                 mard~werks = marc~werks
           LEFT JOIN makt
              ON makt~matnr = mara~matnr AND
                 makt~spras = sy-langu
           WHERE mara~matnr IN matnr
             AND mard~werks IN werks
             AND mard~lgort IN lgort
             AND mtart IN matart
             AND matkl IN matkla
             AND ekgrp IN ekgrup
             AND (lv_dynwhere)                                                  "1917052
           GROUP BY mara~matnr mard~werks xchar mtart matkl meins mara~lvorm marc~lvorm maktx.
      ENDIF.                                                                                  "^1904998

************************************************************************
* Get "normal" stocks.
* If no detailed batch display is required,
* all data come from MARD. Otherwise, materials with batch
* management are extracted from MCHB, the rest from MARD.
************************************************************************

* Access MARD
      CLEAR collector_mard.

      FIELD-SYMBOLS:
          <collector_mard> LIKE LINE OF collector.

*     NO BATCH
      READ TABLE t_mat TRANSPORTING NO FIELDS WITH KEY xchar = space." BINARY SEARCH.
      IF sy-subrc = 0.
        SELECT mard~matnr mard~werks lgort
            SUM( mard~labst ) AS labst SUM( umlme ) AS umlme SUM( insme ) AS insme
            SUM( einme ) AS einme SUM( speme ) AS speme SUM( retme ) AS retme
            mard~lvorm
            CONNECTION (dbcon)
            INTO CORRESPONDING FIELDS OF
            TABLE collector_mard
            FROM mard JOIN marc
            ON mard~werks = marc~werks AND
               mard~matnr = marc~matnr
            JOIN mara ON mara~matnr = marc~matnr
            WHERE mard~matnr IN matnr
              AND mard~werks IN werks
              AND lgort IN lgort
              AND xchar EQ space
              AND ( labst IN r_stopt
               OR   umlme IN r_stopt
               OR   insme IN r_stopt
               OR   einme IN r_stopt
               OR   speme IN r_stopt
               OR   retme IN r_stopt )
           AND marc~ekgrp IN ekgrup
           AND mara~matkl IN matkla
           AND mara~mtart IN matart
           AND (lv_dynwhere)                                                  "1917052
            GROUP BY mard~matnr mard~werks mard~lgort mard~lvorm.

        LOOP AT collector_mard ASSIGNING <collector_mard>.
          IF NOT pa_sond IS INITIAL                AND
                 NOT <collector_mard>-lvorm IS INITIAL.
            MOVE-CORRESPONDING <collector_mard> TO g_s_mard_lv.
            INSERT g_s_mard_lv INTO TABLE g_t_mard_lv.
          ENDIF.
        ENDLOOP.
      ENDIF.

      IF collector_mard[] IS NOT INITIAL.                "1895376
        lv_xmbew = 'X'.                                  "1895376
      ENDIF.                                             "1895376
      APPEND LINES OF collector_mard[] TO collector[].
      FREE collector_mard.

* Access MCHB
      CLEAR collector_mchb.
*    BATCH
      READ TABLE t_mat TRANSPORTING NO FIELDS WITH KEY xchar = 'X'." BINARY SEARCH.
      IF sy-subrc = 0.
        IF xmchb = 'X'.
          SELECT mchb~matnr mchb~werks lgort mchb~charg
              SUM( clabs ) AS labst SUM( cumlm ) AS umlme SUM( cinsm ) AS insme
              SUM( ceinm ) AS einme SUM( cspem ) AS speme SUM( cretm ) AS retme
              mchb~lvorm mcha~bwtar
              CONNECTION (dbcon)
              APPENDING CORRESPONDING FIELDS OF TABLE collector_mchb
              FROM mchb INNER JOIN marc
              ON mchb~matnr = marc~matnr AND
                 mchb~werks = marc~werks
              INNER JOIN mara
              ON mara~matnr = mchb~matnr
              LEFT JOIN mcha
              ON mcha~matnr = mchb~matnr AND
                 mcha~werks = mchb~werks AND
                 mcha~charg = mchb~charg
              WHERE mchb~matnr IN matnr
                AND mchb~werks IN werks
                AND lgort IN lgort
                AND mchb~charg IN charg
                AND xchar = 'X'
                AND ( clabs IN r_stopt
                 OR   cumlm IN r_stopt
                 OR   cinsm IN r_stopt
                 OR   ceinm IN r_stopt
                 OR   cspem IN r_stopt
                 OR   cretm IN r_stopt )
             AND marc~ekgrp IN ekgrup
             AND mara~matkl IN matkla
             AND mara~mtart IN matart
             AND (lv_dynwhere)                                                  "1917052
             GROUP BY mchb~matnr mchb~werks mchb~lgort mchb~charg mchb~lvorm mcha~bwtar.
        ELSE.
          "sums without batch from MCHB
          SELECT mchb~matnr mchb~werks lgort
             SUM( clabs ) AS labst SUM( cumlm ) AS umlme SUM( cinsm ) AS insme
             SUM( ceinm ) AS einme SUM( cspem ) AS speme SUM( cretm ) AS retme
             mchb~lvorm
             CONNECTION (dbcon)
             APPENDING CORRESPONDING FIELDS OF TABLE collector_mchb
             FROM mchb INNER JOIN marc
             ON mchb~matnr = marc~matnr AND
                mchb~werks = marc~werks
             INNER JOIN mara
             ON mara~matnr = mchb~matnr
             WHERE mchb~matnr IN matnr
               AND mchb~werks IN werks
               AND lgort IN lgort
               AND charg IN charg
               AND xchar = 'X'
               AND ( clabs IN r_stopt
                OR   cumlm IN r_stopt
                OR   cinsm IN r_stopt
                OR   ceinm IN r_stopt
                OR   cspem IN r_stopt
                OR   cretm IN r_stopt )
            AND marc~ekgrp IN ekgrup
            AND mara~matkl IN matkla
            AND mara~mtart IN matart
            AND (lv_dynwhere)                                                  "1917052
            GROUP BY mchb~matnr mchb~werks mchb~lgort mchb~lvorm.
        ENDIF.
      ENDIF.

      IF collector_mchb[] IS NOT INITIAL.                "1895376
        lv_xmbew = 'X'.                                  "1895376
      ENDIF.                                             "1895376
      APPEND LINES OF collector_mchb[] TO collector[].
      FREE collector_mchb.


************************************************************************
* Transfer stocks from MARC (TRAME, UMLMC)
************************************************************************
      CLEAR collector.

      LOOP AT t_mat WHERE umlmc <> 0 OR trame <> 0 OR
                          bwesb <> 0 OR
                          glgmg <> 0.

*     there are no lines with stock = zero

*     take the stocks from plant level only when the user
*     does not restrict the storage location;
        CHECK : g_flag_suppress_init_lgort IS INITIAL.

        IF negativ = 'X'.
*       ignore entry if all stocks are zero or greater
          IF  t_mat-trame >= 0 AND
              t_mat-umlmc >= 0 AND
              t_mat-glgmg >= 0.
            CONTINUE.          "take the next entry
          ENDIF.
        ENDIF.

        collector-matnr = t_mat-matnr.
        collector-werks = t_mat-werks.
        collector-umlme = t_mat-trame + t_mat-umlmc.
        collector-lvorm = t_mat-lvorm_marc.
        collector-bwesb = t_mat-bwesb.
        collector-glgmg = t_mat-glgmg.
        collector-trame = t_mat-trame.
        collector-umlmc = t_mat-umlmc.

        APPEND collector TO collector_uml.

      ENDLOOP.

      IF collector_uml[] IS NOT INITIAL.                  "1895376
        lv_xmbew = 'X'.                                   "1895376
      ENDIF.                                              "1895376
      APPEND LINES OF collector_uml[] TO collector[].
      FREE collector_uml.

************************************************************************
* Consignment from vendor (MKOL)
* Read only if requested by one of the
* flags on the selection screen. Absolutely inconsistent, but
* due to compatibility...
* MKOL has a flag for deletion
************************************************************************

      IF  NOT pa_sond IS INITIAL  AND
          NOT t_mat[] IS INITIAL.
        IF  'K' IN so_sobkz  OR
            'M' IN so_sobkz.

          SELECT SINGLE mandt FROM mkol CONNECTION (dbcon) INTO lv_exists.
          IF sy-subrc IS INITIAL. "at least one entry found: continue

            CLEAR collector_mkol.
            IF xmchb IS NOT INITIAL.
*           sum with batch
              SELECT mkol~matnr mkol~werks lgort mkol~charg sobkz mkol~lifnr
                     SUM( slabs ) AS labst SUM( sinsm ) AS insme
                     SUM( seinm ) AS einme SUM( sspem ) AS speme
                     mkol~lvorm bwtar
                   CONNECTION (dbcon)
                     INTO CORRESPONDING FIELDS OF TABLE collector_mkol
                     FROM mkol
                     JOIN mara
                     ON mara~matnr = mkol~matnr
                     JOIN marc
                     ON marc~matnr = mkol~matnr
                    AND marc~werks = mkol~werks
                     LEFT JOIN mcha
                     ON mcha~matnr = mkol~matnr AND
                        mcha~werks = mkol~werks AND
                        mcha~charg = mkol~charg
                     WHERE mkol~matnr IN matnr
                       AND mkol~werks IN werks
                       AND lgort IN lgort
                       AND mkol~charg IN charg
                       AND sobkz IN so_sobkz
                       AND ( slabs IN r_stopt
                        OR   sinsm IN r_stopt
                        OR   seinm IN r_stopt
                        OR   sspem IN r_stopt )
                    AND marc~ekgrp IN ekgrup
                    AND mara~matkl IN matkla
                    AND mara~mtart IN matart
                    AND (lv_dynwhere)                                        "1917052
                     GROUP BY mkol~matnr mkol~werks lgort mkol~charg sobkz mkol~lifnr mkol~lvorm bwtar.
            ELSE.
*           sum without batch
              SELECT mkol~matnr mkol~werks lgort sobkz lifnr
                     SUM( slabs ) AS labst SUM( sinsm ) AS insme
                     SUM( seinm ) AS einme SUM( sspem ) AS speme
                     mkol~lvorm
                   CONNECTION (dbcon)
                     INTO CORRESPONDING FIELDS OF TABLE collector_mkol
                     FROM mkol
                     JOIN mara
                     ON mara~matnr = mkol~matnr
                     JOIN marc
                     ON marc~matnr = mkol~matnr
                    AND marc~werks = mkol~werks
                     WHERE mkol~matnr IN matnr
                       AND mkol~werks IN werks
                       AND mkol~lgort IN lgort
                       AND sobkz IN so_sobkz
                       AND ( slabs IN r_stopt
                        OR   sinsm IN r_stopt
                        OR   seinm IN r_stopt
                        OR   sspem IN r_stopt )
                    AND marc~ekgrp IN ekgrup
                    AND mara~matkl IN matkla
                    AND mara~mtart IN matart
                    AND (lv_dynwhere)                                     "1917052
                    GROUP BY mkol~matnr mkol~werks lgort sobkz lifnr mkol~lvorm.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.

      APPEND LINES OF collector_mkol[] TO collector[].
      FREE collector_mkol.

************************************************************************
* Special stocks at customer side (MSKU)
* MSKU has no flag for deletion
************************************************************************

      IF  NOT  pa_sond IS INITIAL  AND
          NOT  t_mat[] IS INITIAL.
        IF  'V' IN so_sobkz  OR
            'W' IN so_sobkz.

          SELECT SINGLE mandt FROM msku CONNECTION (dbcon) INTO lv_exists.
          IF sy-subrc IS INITIAL. "at least one entry found: continue

            CLEAR collector_msku.
            IF xmchb IS NOT INITIAL.
*           sum with batch
              SELECT msku~matnr msku~werks msku~charg msku~sobkz msku~kunnr
                     SUM( kulab ) AS labst SUM( kuins ) AS insme
                     SUM( kuein ) AS einme SUM( kuuml ) AS umlme
                     bwtar
                   CONNECTION (dbcon)
                     INTO CORRESPONDING FIELDS OF TABLE collector_msku
                     FROM msku
                     JOIN mara
                     ON mara~matnr = msku~matnr
                     JOIN marc
                     ON marc~matnr = msku~matnr
                    AND marc~werks = msku~werks
                     LEFT JOIN mcha
                     ON mcha~matnr = msku~matnr AND
                        mcha~werks = msku~werks AND
                        mcha~charg = msku~charg
                     WHERE msku~matnr IN matnr
                       AND msku~werks IN werks
                       AND msku~charg IN charg
                       AND msku~sobkz IN so_sobkz
                       AND ( kulab IN r_stopt
                        OR   kuins IN r_stopt
                        OR   kuein IN r_stopt
                        OR   kuuml IN r_stopt )
                    AND marc~ekgrp IN ekgrup
                    AND mara~matkl IN matkla
                    AND mara~mtart IN matart
                    AND (lv_dynwhere)                                       "1917052
                     GROUP BY msku~matnr msku~werks msku~charg msku~sobkz msku~kunnr bwtar.
            ELSE.
*           sum without batch
              SELECT msku~matnr msku~werks sobkz msku~kunnr
                     SUM( kulab ) AS labst SUM( kuins ) AS insme
                     SUM( kuein ) AS einme SUM( kuuml ) AS umlme
                   CONNECTION (dbcon)
                     INTO CORRESPONDING FIELDS OF TABLE collector_msku
                     FROM msku
                     JOIN mara
                     ON mara~matnr = msku~matnr
                     JOIN marc
                     ON marc~matnr = msku~matnr
                    AND marc~werks = msku~werks
                     WHERE msku~matnr IN matnr
                       AND msku~werks IN werks
                       AND msku~sobkz IN so_sobkz
                       AND ( kulab IN r_stopt
                        OR   kuins IN r_stopt
                        OR   kuein IN r_stopt
                        OR   kuuml IN r_stopt )
                    AND marc~ekgrp IN ekgrup
                    AND mara~matkl IN matkla
                    AND mara~mtart IN matart
                    AND (lv_dynwhere)                                       "1917052
                     GROUP BY msku~matnr msku~werks sobkz msku~kunnr.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.

      IF collector_msku[] IS NOT INITIAL.                 "1895376
        lv_xmbew = 'X'.                                   "1895376
      ENDIF.                                              "1895376
      APPEND LINES OF collector_msku[] TO collector[].
      FREE collector_msku.

************************************************************************
* Special stocks at vendor provision (MSLB)
* MSLB has no flag for deletion
************************************************************************

      IF  NOT  pa_sond IS INITIAL  AND
          NOT  t_mat[] IS INITIAL  AND
               'O'     IN so_sobkz.

        SELECT SINGLE mandt FROM mslb CONNECTION (dbcon) INTO lv_exists.
        IF sy-subrc IS INITIAL. "at least one entry found: continue
          CLEAR collector_mslb.

          IF g_flag_suppress_init_lgort IS INITIAL.
            IF xmchb IS NOT INITIAL.
              SELECT mslb~matnr mslb~werks mslb~charg mslb~sobkz mslb~lifnr
                 SUM( lblab ) AS labst SUM( lbins ) AS insme
                 SUM( lbein ) AS einme SUM( lbuml ) AS umlme
                 bwtar
               CONNECTION (dbcon)
                 INTO CORRESPONDING FIELDS OF TABLE collector_mslb
                 FROM mslb
                 JOIN mara
                 ON mara~matnr = mslb~matnr
                 JOIN marc
                 ON marc~matnr = mslb~matnr
                AND marc~werks = mslb~werks
                 LEFT JOIN mcha
                   ON mcha~matnr = mslb~matnr AND
                      mcha~werks = mslb~werks AND
                      mcha~charg = mslb~charg
                 WHERE mslb~matnr IN matnr
                   AND mslb~werks IN werks
                   AND mslb~charg IN charg
                   AND mslb~sobkz IN so_sobkz
                   AND ( lblab IN r_stopt
                    OR   lbins IN r_stopt
                    OR   lbein IN r_stopt
                    OR   lbuml IN r_stopt )
                    AND marc~ekgrp IN ekgrup
                    AND mara~matkl IN matkla
                    AND mara~mtart IN matart
                    AND (lv_dynwhere)                                         "1917052
                 GROUP BY mslb~matnr mslb~werks mslb~charg mslb~sobkz mslb~lifnr bwtar.

            ELSE.
*           sum without batch
              SELECT mslb~matnr mslb~werks sobkz mslb~lifnr
                 SUM( lblab ) AS labst SUM( lbins ) AS insme
                 SUM( lbein ) AS einme SUM( lbuml ) AS umlme
               CONNECTION (dbcon)
                 APPENDING CORRESPONDING FIELDS OF TABLE collector_mslb
                 FROM mslb
                 JOIN mara
                 ON mara~matnr = mslb~matnr
                 JOIN marc
                 ON marc~matnr = mslb~matnr
                AND marc~werks = mslb~werks
                 WHERE mslb~matnr IN matnr
                   AND mslb~werks IN werks
                   AND mslb~sobkz IN so_sobkz
                   AND ( lblab IN r_stopt
                    OR   lbins IN r_stopt
                    OR   lbein IN r_stopt
                    OR   lbuml IN r_stopt )
                    AND marc~ekgrp IN ekgrup
                    AND mara~matkl IN matkla
                    AND mara~mtart IN matart
                    AND (lv_dynwhere)                                         "1917052
                 GROUP BY mslb~matnr mslb~werks mslb~sobkz mslb~lifnr.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.

      IF collector_mslb[] IS NOT INITIAL.              "1895376
        lv_xmbew = 'X'.                                "1895376
      ENDIF.                                           "1895376
      APPEND LINES OF collector_mslb[] TO collector[].
      FREE collector_mslb.

************************************************************************
* Customer order stock (MSKA) and sum segment (MSSA) for valuation.
* Sum on the database and FOR ALL ENTRIES is not allowed from
* release 4.5 onwards, so the summation has to be done
* on the application server (here!).
* MSKA has no flag for deletion
************************************************************************

      IF  NOT  pa_sond IS INITIAL  AND
          NOT  t_mat[] IS INITIAL  AND
               ( 'E' IN so_sobkz OR 'T' IN so_sobkz ).

        SELECT SINGLE mandt FROM mssa CONNECTION (dbcon) INTO lv_exists.
        IF sy-subrc IS INITIAL. "at least one entry found: continue

          CLEAR collector_mska.

          IF xmchb IS NOT INITIAL.
*         sum of all materials w/ and w/o batch
            SELECT mska~matnr mska~werks lgort mska~charg mska~sobkz
                 mska~vbeln mska~posnr kzbws
                 SUM( kalab ) AS labst SUM( kains ) AS insme
                 SUM( kaspe ) AS speme SUM( kaein ) AS einme
                 bwtar
             CONNECTION (dbcon)
             INTO CORRESPONDING FIELDS OF TABLE collector_mska
                 FROM mska INNER JOIN mssa
                 ON   mska~matnr = mssa~matnr
                  AND mska~werks = mssa~werks
                  AND mska~sobkz = mssa~sobkz
                  AND mska~vbeln = mssa~vbeln
                  AND mska~posnr = mssa~posnr
                 JOIN marc
                   ON marc~werks = mssa~werks AND
                      marc~matnr = mssa~matnr
                 JOIN mara
                   ON mara~matnr = mssa~matnr
                 LEFT JOIN mcha
                   ON mcha~matnr = mska~matnr AND
                      mcha~werks = mska~werks AND
                      mcha~charg = mska~charg
                 WHERE mska~matnr IN matnr
                   AND mska~werks IN werks
                   AND mska~lgort IN lgort
                   AND mska~sobkz IN so_sobkz
                   AND mska~charg IN charg
                   AND ( mska~kalab IN r_stopt
                    OR   mska~kains IN r_stopt
                    OR   mska~kaspe IN r_stopt
                    OR   mska~kaein IN r_stopt )
                    AND marc~ekgrp IN ekgrup
                    AND mara~matkl IN matkla
                    AND mara~mtart IN matart
                    AND (lv_dynwhere)                                       "1917052
                 GROUP BY mska~matnr mska~werks mska~lgort
                          mska~sobkz mska~vbeln mska~posnr
                          kzbws mska~charg bwtar.
          ELSE.
*         sum without batch
            SELECT mska~matnr mska~werks lgort mska~sobkz
                  mska~vbeln mska~posnr kzbws
                  SUM( kalab ) AS labst SUM( kains ) AS insme
                  SUM( kaspe ) AS speme SUM( kaein ) AS einme
              CONNECTION (dbcon)
              APPENDING CORRESPONDING FIELDS OF TABLE collector_mska
                  FROM mska INNER JOIN mssa
                  ON   mska~matnr = mssa~matnr
                   AND mska~werks = mssa~werks
                   AND mska~sobkz = mssa~sobkz
                   AND mska~vbeln = mssa~vbeln
                   AND mska~posnr = mssa~posnr
                 JOIN marc
                   ON marc~werks = mssa~werks AND
                      marc~matnr = mssa~matnr
                 JOIN mara
                   ON mara~matnr = mssa~matnr
                  WHERE mska~matnr IN matnr
                    AND mska~werks IN werks
                    AND mska~lgort IN lgort
                    AND mska~sobkz IN so_sobkz
                    AND ( mska~kalab IN r_stopt
                     OR   mska~kains IN r_stopt
                     OR   mska~kaspe IN r_stopt
                     OR   mska~kaein IN r_stopt )
                    AND marc~ekgrp IN ekgrup
                    AND mara~matkl IN matkla
                    AND mara~mtart IN matart
                    AND (lv_dynwhere)                                       "1917052
                  GROUP BY mska~matnr mska~werks mska~lgort
                           mska~sobkz mska~vbeln mska~posnr
                           kzbws.
          ENDIF.
*         Transfer stocks for customer order (SATRA in MSSA)
          CLEAR collector_mssa.

          SELECT mssa~matnr mssa~werks sobkz vbeln posnr kzbws
             SUM( satra ) AS umlme
           CONNECTION (dbcon)
             INTO CORRESPONDING FIELDS OF TABLE collector_mssa
             FROM mssa
             JOIN marc
             ON   marc~matnr = mssa~matnr
             AND  marc~werks = mssa~werks
             JOIN mara
             ON   mara~matnr = mssa~matnr
             WHERE mssa~matnr IN matnr
               AND mssa~werks IN werks
               AND mssa~sobkz IN so_sobkz
               AND  ( ( satra <> 0
                AND     satra IN r_stopt ) )
               AND marc~ekgrp IN ekgrup
               AND mara~matkl IN matkla
               AND mara~mtart IN matart
               AND (lv_dynwhere)                                            "1917052
             GROUP BY mssa~matnr mssa~werks sobkz vbeln posnr kzbws.
        ENDIF.
      ENDIF.

      IF collector_mska[] IS NOT INITIAL.               "1895376
        lv_xebew = 'X'.                                 "1895376
      ENDIF.                                            "1895376
      APPEND LINES OF collector_mska[] TO collector[].
      FREE collector_mska.
      IF collector_mssa[] IS NOT INITIAL.               "1895376
        lv_xebew = 'X'.                                 "1895376
      ENDIF.                                            "1895376
      APPEND LINES OF collector_mssa[] TO collector[].
      FREE collector_mssa.

************************************************************************
* The same game for project stocks (MSPR/MSSQ).
* MSPR has no flag for deletion
************************************************************************

      IF  NOT  pa_sond IS INITIAL  AND
          NOT  t_mat[] IS INITIAL  AND
               'Q'     IN so_sobkz.

        SELECT SINGLE mandt FROM mssq CONNECTION (dbcon) INTO lv_exists.
        IF sy-subrc IS INITIAL. "at least one entry found: continue
          CLEAR collector_mspr.

          IF xmchb IS NOT INITIAL.
            SELECT mspr~matnr mspr~werks lgort mspr~charg mspr~sobkz mspr~pspnr kzbws
               SUM( prlab ) AS labst  SUM( prins ) AS insme
               SUM( prspe ) AS speme  SUM( prein ) AS einme
               bwtar
             CONNECTION (dbcon)
             INTO CORRESPONDING FIELDS OF TABLE collector_mspr
               FROM mspr INNER JOIN mssq
               ON   mspr~matnr = mssq~matnr
                AND mspr~werks = mssq~werks
                AND mspr~sobkz = mssq~sobkz
                AND mspr~pspnr = mssq~pspnr
                JOIN marc
                 ON marc~matnr = mssq~matnr AND
                    marc~werks = mssq~werks
                JOIN mara
                 ON mara~matnr = mssq~matnr
                LEFT JOIN mcha
                 ON mcha~matnr = mspr~matnr AND
                    mcha~werks = mspr~werks AND
                    mcha~charg = mspr~charg
               WHERE mspr~matnr IN matnr
                 AND mspr~werks IN werks
                 AND mspr~lgort IN lgort
                 AND mspr~charg IN charg
                 AND ( prlab IN r_stopt
                  OR   prins IN r_stopt
                  OR   prspe IN r_stopt
                  OR   prein IN r_stopt )
              AND marc~ekgrp IN ekgrup
              AND mara~matkl IN matkla
              AND mara~mtart IN matart
              AND (lv_dynwhere)                                             "1917052
              GROUP BY mspr~matnr mspr~werks lgort mspr~charg mspr~sobkz mspr~pspnr kzbws bwtar.
          ELSE.
*     sum without batch
            SELECT mspr~matnr mspr~werks lgort mspr~sobkz mspr~pspnr kzbws
               SUM( prlab ) AS labst  SUM( prins ) AS insme
               SUM( prspe ) AS speme  SUM( prein ) AS einme
             CONNECTION (dbcon)
             APPENDING CORRESPONDING FIELDS OF TABLE collector_mspr
               FROM mspr INNER JOIN mssq
               ON   mspr~matnr = mssq~matnr
                AND mspr~werks = mssq~werks
                AND mspr~sobkz = mssq~sobkz
                AND mspr~pspnr = mssq~pspnr
              JOIN marc
               ON   marc~matnr = mssq~matnr AND
                    marc~werks = mssq~werks
              JOIN mara
               ON   mara~matnr = mssq~matnr
               WHERE mspr~matnr IN matnr
                 AND mspr~werks IN werks
                 AND mspr~lgort IN lgort
                 AND ( prlab IN r_stopt
                  OR   prins IN r_stopt
                  OR   prspe IN r_stopt
                  OR   prein IN r_stopt )
              AND marc~ekgrp IN ekgrup
              AND mara~matkl IN matkla
              AND mara~mtart IN matart
              AND (lv_dynwhere)                                             "1917052
              GROUP BY mspr~matnr mspr~werks lgort mspr~sobkz mspr~pspnr kzbws.
          ENDIF.

*   Transfer stocks for projects (SQTRA in MSSQ)
          CLEAR collector_mssq.

          SELECT mssq~matnr mssq~werks sobkz pspnr kzbws
               SUM( sqtra ) AS umlme
               CONNECTION (dbcon)
               INTO CORRESPONDING FIELDS OF TABLE collector_mssq
                FROM mssq
                JOIN marc
                ON   marc~matnr = mssq~matnr AND
                     marc~werks = mssq~werks
                JOIN mara
                ON   mara~matnr = mssq~matnr
                WHERE mssq~matnr IN matnr
                  AND mssq~werks IN werks
                  AND sobkz  IN  so_sobkz
                  AND  ( ( sqtra <> 0
                   AND     sqtra IN r_stopt ) )
                  AND marc~ekgrp IN ekgrup
                  AND mara~matkl IN matkla
                  AND mara~mtart IN matart
                  AND (lv_dynwhere)                                         "1917052
                GROUP BY mssq~matnr mssq~werks sobkz pspnr kzbws.
        ENDIF.
      ENDIF.

      IF collector_mspr[] IS NOT INITIAL.                   "1895376
        lv_xqbew = 'X'.                                     "1895376
      ENDIF.                                                "1895376
      APPEND LINES OF collector_mspr[] TO collector[].
      FREE collector_mspr.
      IF collector_mssq[] IS NOT INITIAL.                   "1895376
        lv_xqbew = 'X'.                                     "1895376
      ENDIF.                                                "1895376
      APPEND LINES OF collector_mssq[] TO collector[].
      FREE collector_mssq.
*ENHANCEMENT-POINT RM07MLBS_15 SPOTS ES_RM07MLBS .

************************************************************************
************************************************************************

      IF nozero = abap_true.
        DELETE collector WHERE labst = 0 AND einme = 0 AND
           insme = 0 AND retme = 0 AND
           speme = 0 AND umlme = 0 AND
           bwesb = 0 AND bwesb = 0 AND
           trame = 0 AND glgmg = 0 AND
           umlmc = 0.
      ENDIF.

      IF collector[] IS INITIAL.
*     nothing found for selection - leave
        RETURN.
      ENDIF.

************************************************************************
* Fill in additional data (first round) and extract the data
* for the access to the valuation tables.
************************************************************************
      SORT t_mat BY matnr werks.
      SORT g_t_organ             BY werks.
      CLEAR : g_s_t001l,         g_s_organ.

*************************************************************************
** Read the valuation tables
*************************************************************************
      BREAK-POINT ID mmim_rep_mb52.

      IF novalues IS INITIAL.
        READ TABLE collector WITH KEY kzbws = 'A' TRANSPORTING NO FIELDS.
        IF sy-subrc = 0 or lv_xmbew = 'X'.                                         "1895376
          SELECT SINGLE mandt FROM mbew CONNECTION (dbcon) INTO lv_exists.
          IF sy-subrc IS INITIAL. "at least one entry found: continue
            IF xmchb = 'X'.
*               batch values / may select zombies, but will be ignored
              SELECT DISTINCT mbew~matnr mbew~bwkey mbew~bwtar lbkum salk3 "distinct because of BWTAR 1:n CHARG
                     vprsv verpr stprs peinh
                     CONNECTION (dbcon)
                     INTO CORRESPONDING FIELDS OF TABLE t_mbew
                     FROM mbew JOIN t001w
                     ON   t001w~bwkey = mbew~bwkey
                     JOIN mcha
                     ON   mcha~werks = t001w~werks
                     AND  mcha~matnr = mbew~matnr
                     AND  mcha~bwtar = mbew~bwtar
                     JOIN mara
                     ON   mara~matnr = mbew~matnr
                     JOIN t134m
                     ON   t134m~bwkey = mbew~bwkey
                     AND  t134m~mtart = mara~mtart
                     WHERE mcha~werks IN werks
                     AND   mcha~matnr IN matnr
                     AND   mcha~charg IN charg
                     AND   mcha~bwtar <> space
                     AND   mbew~lbkum IN r_stopt                                "1904998
                     AND   t134m~wertu = 'X'
                     AND  (lv_dynwhere).                                        "1917052

*               non batch values
              SELECT mbew~matnr mbew~bwkey mbew~bwtar lbkum salk3
                     vprsv verpr stprs peinh
                     CONNECTION (dbcon)
                     APPENDING CORRESPONDING FIELDS OF TABLE t_mbew
                     FROM mbew JOIN t001w
                     ON   mbew~bwkey = t001w~bwkey
                     JOIN marc
                     ON   t001w~werks = marc~werks
                     AND  mbew~matnr = marc~matnr
                     JOIN mara
                     ON   mara~matnr = mbew~matnr
                     JOIN t134m
                     ON   t134m~bwkey = mbew~bwkey
                     AND  t134m~mtart = mara~mtart
                     WHERE marc~werks IN werks
                     AND   marc~matnr IN matnr
                     AND   marc~bwtty = space "no batch/ split valuation
                     AND   t134m~wertu = 'X' "prevent zombies
                     AND   mbew~lbkum IN r_stopt                                "1904998
                     AND  (lv_dynwhere).                                        "1917052
            ELSE.
*               no batch values at all
              IF lgort[] IS INITIAL.                                            "1904998
                SELECT mbew~matnr mbew~bwkey mbew~bwtar lbkum salk3
                     vprsv verpr stprs peinh
                     CONNECTION (dbcon)
                     INTO CORRESPONDING FIELDS OF TABLE t_mbew
                     FROM mbew JOIN t001w
                     ON   mbew~bwkey = t001w~bwkey
                     JOIN marc
                     ON   t001w~werks = marc~werks
                     AND  mbew~matnr = marc~matnr
                       JOIN mara
                       ON   mara~matnr = mbew~matnr
                       JOIN t134m
                       ON   t134m~bwkey = mbew~bwkey
                       AND  t134m~mtart = mara~mtart
                       WHERE marc~werks IN werks
                       AND   marc~matnr IN matnr
                       AND   mbew~lbkum IN r_stopt                              "1904998
                       AND   t134m~wertu = 'X'
                       AND  (lv_dynwhere).                                      "1917052
              ELSE.
                SELECT DISTINCT mbew~matnr mbew~bwkey mbew~bwtar lbkum salk3   "v1904998
                       vprsv verpr stprs peinh
                       CONNECTION (dbcon)
                       INTO CORRESPONDING FIELDS OF TABLE t_mbew
                       FROM mbew JOIN t001w
                       ON   t001w~bwkey = mbew~bwkey                            "1904998
                       JOIN marc
                       ON   marc~werks = t001w~werks                            "1904998
                       AND  marc~matnr = mbew~matnr                             "1904998
                       JOIN mard                                                "1904998
                       ON   mard~matnr = marc~matnr                             "1904998
                       AND  mard~werks = mard~werks                             "1904998
                       JOIN mara
                       ON   mara~matnr = mbew~matnr
                       JOIN t134m
                       ON   t134m~bwkey = mbew~bwkey
                       AND  t134m~mtart = mara~mtart
                       WHERE marc~werks IN werks
                       AND   marc~matnr IN matnr
                       AND   mard~lgort IN lgort                                "1904998
                       AND   mbew~lbkum IN r_stopt                              "1904998
                       AND   t134m~wertu = 'X'
                       AND  (lv_dynwhere).                                      "1917052
              ENDIF.                                                           "^1904998
            ENDIF.
            SORT t_mbew BY matnr bwkey bwtar.
          ENDIF.
        ENDIF.

        IF lv_xebew = 'X'.                                                         "1895376
         READ TABLE collector WITH KEY kzbws = 'M' TRANSPORTING NO FIELDS.         "1895376
         IF sy-subrc = 0.                                                          "1895376
*     "Unfortunately", EBEW and QBEW do not have sum segments over
*     the valuation types. Therefore, without batch data, another
*     SELECT-statement is needed.
          SELECT SINGLE mandt FROM ebew CONNECTION (dbcon) INTO lv_exists.
          IF sy-subrc IS INITIAL. "at least one entry found: continue
            IF xmchb = 'X'.
*       may select zombies, but will be ignored due to later key access (MCHA-Zombies)
              "einzeln bewertete Chargen
              SELECT DISTINCT ebew~matnr ebew~bwkey ebew~bwtar sobkz vbeln posnr "distinct because of BWTAR 1:n CHARG
                     SUM( lbkum ) AS lbkum  SUM( salk3 ) AS salk3
                     vprsv verpr stprs peinh
                     CONNECTION (dbcon)
                     INTO CORRESPONDING FIELDS OF TABLE t_ebew
                     FROM ebew JOIN t001w
                       ON   ebew~bwkey = t001w~bwkey
                       JOIN mcha
                       ON   mcha~werks = t001w~werks
                       AND  mcha~matnr = ebew~matnr
                       AND  mcha~bwtar = ebew~bwtar
                       JOIN mara
                       ON   mara~matnr = ebew~matnr
                       JOIN t134m
                       ON   t134m~bwkey = ebew~bwkey
                       AND  t134m~mtart = mara~mtart
                       WHERE mcha~werks IN werks
                       AND   mcha~matnr IN matnr
                       AND   mcha~charg IN charg
                       AND   t134m~wertu = 'X' "prevent zombies
                       AND  (lv_dynwhere)                                     "1917052
                     GROUP BY ebew~matnr ebew~bwkey ebew~bwtar sobkz vbeln posnr vprsv verpr stprs peinh.
*         normal bewertete chargen & materialien
              SELECT ebew~matnr ebew~bwkey sobkz vbeln posnr SUM( lbkum ) AS lbkum  SUM( salk3 ) AS salk3
                     vprsv verpr stprs peinh
                     CONNECTION (dbcon)
                     APPENDING CORRESPONDING FIELDS OF TABLE t_ebew
                     FROM ebew JOIN t001w
                       ON   ebew~bwkey = t001w~bwkey
                       JOIN marc
                       ON   t001w~werks = marc~werks
                       AND  ebew~matnr = marc~matnr
                       JOIN mara
                       ON   mara~matnr = ebew~matnr
                       JOIN t134m
                       ON   t134m~bwkey = ebew~bwkey
                       AND  t134m~mtart = mara~mtart
                       WHERE marc~werks IN werks
                       AND   marc~matnr IN matnr
                       AND   marc~bwtty = space "no batch/ split valuation
                       AND   t134m~wertu = 'X' "prevent zombies
                       AND  (lv_dynwhere)                                     "1917052
                     GROUP BY ebew~matnr ebew~bwkey sobkz vbeln posnr vprsv verpr stprs peinh.
            ELSE.
*       Bewertung egal
              SELECT ebew~matnr ebew~bwkey sobkz vbeln posnr SUM( lbkum ) AS lbkum  SUM( salk3 ) AS salk3
                      vprsv verpr stprs peinh
                      CONNECTION (dbcon)
                      INTO CORRESPONDING FIELDS OF TABLE t_ebew
                      FROM ebew JOIN t001w
                        ON   ebew~bwkey = t001w~bwkey
                        JOIN marc
                        ON   t001w~werks = marc~werks
                        AND  ebew~matnr = marc~matnr
                        JOIN mara
                        ON   mara~matnr = ebew~matnr
                        JOIN t134m
                        ON   t134m~bwkey = ebew~bwkey
                        AND  t134m~mtart = mara~mtart
                        WHERE marc~werks IN werks
                        AND   marc~matnr IN matnr
                        AND   t134m~wertu = 'X' "prevent zombies
                        AND  (lv_dynwhere)                                    "1917052
                      GROUP BY ebew~matnr ebew~bwkey sobkz vbeln posnr vprsv verpr stprs peinh.
            ENDIF.
            SORT t_ebew BY matnr bwkey bwtar sobkz vbeln posnr.
          ENDIF.
        ENDIF.
       ENDIF.                                                                      "1895376

       IF lv_xqbew = 'X'.                                                          "1895376
         READ TABLE collector WITH KEY kzbws = 'M' TRANSPORTING NO FIELDS.         "1895376
         IF sy-subrc = 0.
          SELECT SINGLE mandt FROM qbew CONNECTION (dbcon) INTO lv_exists.
          IF sy-subrc IS INITIAL. "at least one entry found: continue
            IF xmchb = 'X'.
              "batch valuation
              SELECT DISTINCT qbew~matnr qbew~bwkey qbew~bwtar sobkz pspnr "distinct because of BWTAR 1:n CHARG
                     SUM( lbkum ) AS lbkum  SUM( salk3 ) AS salk3
                     vprsv verpr stprs peinh
                     CONNECTION (dbcon)
                      INTO CORRESPONDING FIELDS OF TABLE t_qbew
                      FROM qbew JOIN t001w
                        ON qbew~bwkey = t001w~bwkey
                      JOIN mcha
                       ON   mcha~werks = t001w~werks
                       AND  mcha~matnr = qbew~matnr
                       AND  mcha~bwtar = qbew~bwtar
                       JOIN mara
                       ON   mara~matnr = qbew~matnr
                       JOIN t134m
                       ON   t134m~bwkey = qbew~bwkey
                       AND  t134m~mtart = mara~mtart
                     WHERE mcha~werks IN werks
                       AND mcha~matnr IN matnr
                       AND mcha~charg IN charg
                       AND   t134m~wertu = 'X' "prevent zombies
                       AND  (lv_dynwhere)                                     "1917052
                  GROUP BY qbew~matnr qbew~bwkey qbew~bwtar sobkz pspnr vprsv verpr stprs peinh.
              "normal valuation
              SELECT qbew~matnr qbew~bwkey qbew~bwtar sobkz pspnr SUM( lbkum ) AS lbkum  SUM( salk3 ) AS salk3
                    vprsv verpr stprs peinh
                    CONNECTION (dbcon)
                     APPENDING CORRESPONDING FIELDS OF TABLE t_qbew
                     FROM qbew JOIN t001w
                       ON qbew~bwkey = t001w~bwkey
                     JOIN marc
                      ON   t001w~werks = marc~werks
                      AND  qbew~matnr = marc~matnr
                      JOIN mara
                      ON   mara~matnr = qbew~matnr
                      JOIN t134m
                      ON   t134m~bwkey = qbew~bwkey
                      AND  t134m~mtart = mara~mtart
                      WHERE marc~werks IN werks
                      AND   marc~matnr IN matnr
                      AND   marc~bwtty = space "no batch/ split valuation
                      AND   t134m~wertu = 'X' "prevent zombies
                      AND  (lv_dynwhere)                                       "1917052
                 GROUP BY qbew~matnr qbew~bwkey qbew~bwtar sobkz pspnr vprsv verpr stprs peinh.
            ELSE.
              SELECT qbew~matnr qbew~bwkey sobkz pspnr SUM( lbkum ) AS lbkum  SUM( salk3 ) AS salk3
                   vprsv verpr stprs peinh
                   CONNECTION (dbcon)
                    INTO CORRESPONDING FIELDS OF TABLE t_qbew
                    FROM qbew JOIN t001w
                     ON   qbew~bwkey = t001w~bwkey
                     JOIN marc
                     ON   t001w~werks = marc~werks
                     AND  qbew~matnr = marc~matnr
                     JOIN mara
                     ON   mara~matnr = qbew~matnr
                     JOIN t134m
                     ON   t134m~bwkey = qbew~bwkey
                     AND  t134m~mtart = mara~mtart
                     WHERE marc~werks IN werks
                     AND   marc~matnr IN matnr
                     AND   t134m~wertu = 'X' "prevent zombies
                     AND  (lv_dynwhere)                                       "1917052
                  GROUP BY qbew~matnr qbew~bwkey sobkz pspnr vprsv verpr stprs peinh.
            ENDIF.
            SORT t_qbew BY matnr bwkey bwtar sobkz pspnr.
          ENDIF.
        ENDIF.
       ENDIF.                                                                      "1895376
      ENDIF. "novalues is initial
************************************************************************
* Extract key-data for other tables.
************************************************************************
      REFRESH:     bestand.

      BREAK-POINT ID mmim_rep_mb52.
      LOOP AT collector  WHERE charg IN charg.                 "2035059
        CLEAR factor.
        CLEAR bestand. "header line!
        MOVE-CORRESPONDING collector TO bestand.

*   fill the key of the special stocks into the field
*   assigment
        CASE    collector-sobkz.
          WHEN  'E'.
            MOVE  : 'X'               TO  g_flag_sobkz-vbeln.
            WRITE : collector-vbeln   TO  bestand-ssnum.
            MOVE  : '/'               TO  bestand-ssnum+10(01).
            WRITE : collector-posnr   TO  bestand-ssnum+12(08)
                                      NO-ZERO.
            CONDENSE                  bestand-ssnum.

          WHEN  'T'.
            MOVE  : 'X'               TO  g_flag_sobkz-vbeln.
            WRITE : collector-vbeln   TO  bestand-ssnum.
            MOVE  : '/'               TO  bestand-ssnum+10(01).
            WRITE : collector-posnr   TO  bestand-ssnum+12(08)
                                      NO-ZERO.
            CONDENSE                  bestand-ssnum.

          WHEN  'K'.
            MOVE  : 'X'               TO  g_flag_sobkz-lifnr.
            WRITE : collector-lifnr   TO  bestand-ssnum.

          WHEN  'M'.
            MOVE  : 'X'               TO  g_flag_sobkz-lifnr.
            WRITE : collector-lifnr   TO  bestand-ssnum.

          WHEN  'O'.
            MOVE  : 'X'               TO  g_flag_sobkz-lifnr.
            WRITE : collector-lifnr   TO  bestand-ssnum.

          WHEN  'Q'.
            MOVE  : 'X'               TO  g_flag_sobkz-pspnr.
            WRITE : collector-pspnr   TO  bestand-ssnum.

          WHEN  'V'.
            MOVE  : 'X'               TO  g_flag_sobkz-kunnr.
            WRITE : collector-kunnr   TO  bestand-ssnum.

          WHEN  'W'.
            MOVE  : 'X'               TO  g_flag_sobkz-kunnr.
            WRITE : collector-kunnr   TO  bestand-ssnum.

          WHEN  OTHERS.
            CLEAR                     bestand-ssnum.
        ENDCASE.
*ENHANCEMENT-POINT RM07MLBS_16 SPOTS ES_RM07MLBS .

        IF  g_flag_t001l = 'X'.
          IF  bestand-werks = g_s_t001l-werks  AND
              bestand-lgort = g_s_t001l-lgort.
          ELSE.
*       read with plant and storage location
            READ TABLE g_t_t001l   INTO  g_s_t001l
              WITH TABLE KEY werks = bestand-werks
                             lgort = bestand-lgort.

            MOVE : bestand-werks TO g_s_t001l-werks,
                   bestand-lgort TO g_s_t001l-lgort.

            IF sy-subrc <> 0.
              CLEAR              g_s_t001l-lgobe.
            ENDIF.
          ENDIF.
        ENDIF.

*   take the storage bin from the buffer
        MOVE : g_s_t001l-lgobe   TO  bestand-lgobe.


*   get the information per plant with buffer
        IF  bestand-werks  NE  g_s_organ-werks.
          READ TABLE g_t_organ   INTO  g_s_organ
              WITH KEY werks = bestand-werks
                                 BINARY SEARCH.

          IF sy-subrc <> 0.
*       sorry nothing found
            CLEAR                g_s_organ.
            MOVE : bestand-werks TO  g_s_organ-werks.
          ENDIF.
        ENDIF.

*   take the following fields from the buffer
        MOVE : g_s_organ-name1   TO  bestand-name1,
               g_s_organ-waers   TO  bestand-waers,
               g_s_organ-bwkey   TO  bestand-bwkey.

*   get the information from the material master MARC
*   with buffer
        IF  bestand-matnr = l_s_mat-matnr  AND
            bestand-werks = l_s_mat-werks.
*     results are in the buffer
        ELSE.
          CLEAR                  l_s_mat.
          MOVE : bestand-matnr   TO  l_s_mat-matnr,
                 bestand-werks   TO  l_s_mat-werks.

          READ TABLE t_mat       INTO  l_s_mat
             WITH KEY matnr = bestand-matnr
                      werks = bestand-werks
                                 BINARY SEARCH.

          IF  sy-subrc <> 0.
*       sorry nothing found
            CLEAR                l_s_mat.
            MOVE : bestand-matnr TO  l_s_mat-matnr,
                   bestand-werks TO  l_s_mat-werks.
          ENDIF.
        ENDIF.

*   take the results the buffer
        MOVE : l_s_mat-mtart     TO  bestand-mtart,
               l_s_mat-matkl     TO  bestand-matkl,
               l_s_mat-meins     TO  bestand-meins,
               l_s_mat-maktx     TO  bestand-maktx.


*   if this entry has no deletion flag, take the
*   deletion flag from a higher level like MARA, MARC,
*   or MARDA
        IF  bestand-lvorm IS INITIAL.
          IF      NOT  l_s_mat-lvorm_marc IS INITIAL.
            MOVE  l_s_mat-lvorm_marc
                                 TO  bestand-lvorm.
          ELSEIF  NOT  l_s_mat-lvorm_mara IS INITIAL.
            MOVE  l_s_mat-lvorm_mara
                                 TO  bestand-lvorm.

          ELSEIF  NOT g_t_mard_lv[] IS INITIAL  AND
                  NOT bestand-lgort IS INITIAL  AND
                  NOT bestand-sobkz IS INITIAL.
*       look for deletion flag in working table
*       g_t_mard_lv for a line with special stock
            IF  bestand-matnr = g_s_mard_lv-matnr  AND
                bestand-werks = g_s_mard_lv-werks  AND
                bestand-lgort = g_s_mard_lv-lgort.
            ELSE.
*         read table only after the key has changed
              READ TABLE g_t_mard_lv  INTO  g_s_mard_lv
              WITH TABLE KEY matnr = bestand-matnr
                             werks = bestand-werks
                             lgort = bestand-lgort.

              IF sy-subrc <> 0.
*           fill the buffer in case the entry does not exist
                MOVE : bestand-matnr  TO  g_s_mard_lv-matnr,
                       bestand-werks  TO  g_s_mard_lv-werks,
                       bestand-lgort  TO  g_s_mard_lv-lgort.
                CLEAR                 g_s_mard_lv-lvorm.
              ENDIF.

*         take the result from the buffer
              MOVE  g_s_mard_lv-lvorm   TO  bestand-lvorm.
            ENDIF.
          ENDIF.
        ENDIF.


*   ***** STOP here if value is not relevant ********************
        IF ( bestand-waers = space ) OR "failed Auth-Checks (value)
           ( novalues IS NOT INITIAL ). "values are not requested
          APPEND bestand.
          CONTINUE.
        ENDIF.

        sy-subrc = 4.
        IF  bestand-sobkz = ' ' OR bestand-sobkz = 'O' OR
            bestand-sobkz = 'W' OR bestand-sobkz = 'V' OR
            bestand-kzbws = 'A'.
          READ TABLE t_mbew WITH KEY matnr = bestand-matnr
                                     bwkey = bestand-bwkey
                                     bwtar = bestand-bwtar
                                     BINARY SEARCH.

        ELSEIF ( bestand-sobkz = 'E' OR bestand-sobkz = 'T' )
             AND bestand-kzbws = 'M'.
          READ TABLE t_ebew WITH KEY matnr = bestand-matnr
                                     bwkey = bestand-bwkey
                                     bwtar = bestand-bwtar
                                     sobkz = bestand-sobkz
                                     vbeln = bestand-vbeln
                                     posnr = bestand-posnr
                                     BINARY SEARCH.
          MOVE-CORRESPONDING t_ebew TO t_mbew.

        ELSEIF bestand-sobkz = 'Q' AND bestand-kzbws = 'M'.
          READ TABLE t_qbew WITH KEY matnr = bestand-matnr
                                     bwkey = bestand-bwkey
                                     bwtar = bestand-bwtar
                                     sobkz = bestand-sobkz
                                     pspnr = bestand-pspnr
                                     BINARY SEARCH.
          MOVE-CORRESPONDING t_qbew TO t_mbew.
        ENDIF.
*ENHANCEMENT-POINT RM07MLBS_17 SPOTS ES_RM07MLBS .

        IF sy-subrc = 0.
          IF t_mbew-lbkum = 0.
*         Cannot happen, but in R/3 this does not hold in all cases...
            IF t_mbew-peinh = 0.                            "353428
              t_mbew-peinh = 1.                             "353428
            ENDIF.                                          "353428
*         Calculation of value in case of LBKUM = 0 only possible
*         for MBEW. EBEW and QBEW are collected over all subitems
*         (VBELN...), so the data are not available.
            IF bestand-sobkz = 'E' OR bestand-sobkz = 'Q'.  "388735
              factor = 0.                                   "388735
              CLEAR bestand-waers.                          "388735
            ELSE.                                           "388735
              CASE t_mbew-vprsv.
                WHEN 'V'. factor = t_mbew-verpr / t_mbew-peinh.
                WHEN 'S'. factor = t_mbew-stprs / t_mbew-peinh.
              ENDCASE.
            ENDIF.                                          "388735
          ELSE.
            factor = t_mbew-salk3 / t_mbew-lbkum.
          ENDIF.

          bestand-wlabs = bestand-labst * factor.
          bestand-winsm = bestand-insme * factor.
          bestand-wspem = bestand-speme * factor.
          bestand-weinm = bestand-einme * factor.
          bestand-wumlm = bestand-umlme * factor.
          bestand-wbwesb = bestand-bwesb * factor.          "AC0K020254
          bestand-wglgm = bestand-glgmg * factor.           "n912093
          bestand-wtram = bestand-trame * factor.           "n912093
          bestand-wumlc = bestand-umlmc * factor.           "n912093
        ENDIF.

        APPEND bestand.

      ENDLOOP.

      FREE                       collector.

    CATCH cx_sy_open_sql_db INTO sql_ex.
      MESSAGE i001(hdb). "no hdb usage
*     use backup for selection
      FREE collector.
      PERFORM data_selection.
  ENDTRY.
ENDFORM.                    "data_selection_new

*ENHANCEMENT-POINT EHP604_RM07MLBS_42 SPOTS ES_RM07MLBS STATIC .
*Text symbol text
*001:Database Selections
*005:Display Options
*006:Hierarchical Representation
*007:Non-Hierarchical Representation
*020:Val.
*021:Value Unrestricted
*022:Value in Stock Tfr
*023:Value in QualInsp.
*024:Value Restricted
*025:Value BlockedStock
*026:Value Rets Blocked
*027:Val. GR Blocked St.
*028:Val. Tied Empties
*029:Value in Transit
*030:Val. in Trans./Tfr
*070:Scope of List
*071:Selection: Special Stocks
*080:Settings
*090:Display Material Flagged for Deletion
*095:Display Negative Stocks
*100:Display Batch Stocks
*101:Special Stocks, Stock in Transfer at Plant Level
*110:Display Consignment Stocks
*111:Display Ret. Transp. Packaging
*160:Material          Valuation Area
*165:Following entries missing from table MBEW:
*200:Read warehouse stocks
*201:Read batch stocks
*202:Read special stocks
*203:Read batch master
*204:Read material valuation
*205:Read material short texts
*206:Read material master
*207:Data being prepared

*895:Error during INSERT in table MMIM_RE_PRINT
*Selection text
*CHARG:        Batch
*EKGRUP:        Purchasing Group
*LGORT:        Storage Location
*MATART:        Material Type
*MATKLA:        Material Group
*MATNR:        Material
*NEGATIV:        Display Negative Stocks Only
*NOVALUES:        Do Not Display Values
*NOZERO:D       .
*PA_SOND:        Also Select Special Stocks
*P_VARI:        Layout
*SO_SOBKZ:        Special Stock Indicator
*WERKS:        Plant
*XMCHB:        Display Batch Stocks
