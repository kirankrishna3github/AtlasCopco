*&---------------------------------------------------------------------*
*& Report  YSE_VISIONAIR_KE24
*&
*&---------------------------------------------------------------------*
*----------------------------------------------------------------------*
* Author  : Anda Wu                                                    *
* Created : 28.09.2016                                                 *
* Dev.nbr: CR3987:SAP data to VA China database                        *
*----------------------------------------------------------------------*
* User administration full access report                               *
************************************************************************
* Mod-xxx | xxxx.xx.xx | xxxx xxxx     | CD1Kxxxxxxxxxx  |   CRXXXX    *
************************************************************************
* Mod-000 | 2016.09.29 | Anda Wu       | CD1K989781      |   CR3987    *
************************************************************************
REPORT yse_visionair_ke24 NO STANDARD PAGE HEADING LINE-SIZE 255.
************************************************************************
* TYPES                                                                *
************************************************************************
TABLES: vbak, veda,ce11000.
************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:

BEGIN OF ty_ce11000,
  paledger  TYPE  ce11000-paledger, "Currency type
  vrgar   TYPE    ce11000-vrgar,    "Record Type
  versi   TYPE    ce11000-versi,    "Plan version
  perio   TYPE    ce11000-perio,    "Period/year
  paobjnr TYPE    ce11000-paobjnr,  "Profitability Segment Number
  pasubnr TYPE    ce11000-pasubnr,  "Profitability segment changes
  belnr   TYPE    ce11000-belnr,    "Document number
  posnr   TYPE    ce11000-posnr,    "Item no. of CO-PA line item
  budat   TYPE    ce11000-budat,    "Posting Date
  kndnr   TYPE    ce11000-kndnr,    "Customer
  artnr   TYPE    ce11000-artnr,    "Product number
  rec_waers TYPE  ce11000-rec_waers,"Currency of the data record
  kaufn   TYPE    ce11000-kaufn,    "Sales Order Number
  kdpos   TYPE    ce11000-kdpos,    "Item Number in Sales Order
  bukrs   TYPE    ce11000-bukrs,    "Company code
  vkorg   TYPE    ce11000-vkorg,    "Sales Organization
  vtweg   TYPE    ce11000-vtweg,    "Distribution Channel
  spart   TYPE    ce11000-spart,    "Division
  auart   TYPE    ce11000-auart,    "Order Type
  vkbur   TYPE    ce11000-vkbur,    "Sales Office
  prctr   TYPE    ce11000-prctr,    "Profit Center
  prodh   TYPE    ce11000-prodh,    "Product hierarchy
  ww002   TYPE    ce11000-ww002,    "PLC
  matkl   TYPE    ce11000-matkl,    "Matl Group
  vv100   TYPE    ce11000-vv100,    "Revenues
  absmg   TYPE    ce11000-absmg,    "Sales quantity
END OF ty_ce11000.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_ce11000      TYPE STANDARD TABLE OF ty_ce11000,
  gt_out_file     TYPE STANDARD TABLE OF yse_outfile_ke24.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_gac(4)     TYPE c,
  gv_pgc(4)     TYPE c,
  gt_vbak       TYPE STANDARD TABLE OF vbak,
  gv_plc        TYPE rkeg_ww002,
  gv_col(10)    TYPE n,
  gv_text       TYPE string,
  gv_save       TYPE c.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x                TYPE c VALUE 'X',
  gc_type_e           TYPE c VALUE 'E'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01 .
SELECT-OPTIONS:
  s_paled     FOR ce11000-paledger,         "Currency type
  s_vrgar     FOR ce11000-vrgar,            "Recode type
  s_budat     FOR ce11000-budat,            "Posting date
  s_bukrs     FOR ce11000-bukrs,            "Company code
  s_ww002     FOR ce11000-ww002.            "PLC
PARAMETERS: p_server   TYPE char50
  DEFAULT '/var/load/xxx/UK/read/VISIONAIR_CTSCHINA/'.
SELECTION-SCREEN END OF BLOCK b1.

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-t02 .
PARAMETERS:
  preadm  LIKE rkb1b-method1 RADIOBUTTON GROUP read
                             MODIF ID pat,
  preadm2 LIKE rkb1b-method2 RADIOBUTTON GROUP read
                             DEFAULT 'X'
                             MODIF ID pat.
SELECTION-SCREEN END OF BLOCK b2.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN ON VALUE-REQUEST                                *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_server.
  PERFORM set_the_path.

************************************************************************
* START-OF-SELECTION                                                     *
************************************************************************
START-OF-SELECTION.
* Get data from table
  PERFORM get_data.
* Generate data
  PERFORM generate_data.

  IF sy-batch IS NOT INITIAL.
*   Send_data
    PERFORM send_data.
    MESSAGE i001(00) WITH text-m04 p_server.
  ELSE.
*   Download the file to local PC
    PERFORM download_file.
    MESSAGE s001(00) WITH text-m09 p_server.
  ENDIF.

*&---------------------------------------------------------------------*
*&      Form  GET_DATA
*&---------------------------------------------------------------------*
*       Get data from table ce11000
*----------------------------------------------------------------------*
FORM get_data .

  DATA: lrg_auart   TYPE RANGE OF vbak-auart,
        ls_auart    LIKE LINE OF lrg_auart.

  SELECT    paledger    "Currency type
            vrgar       "Record Type
            versi       "Plan version
            perio       "Period/year
            paobjnr     "Profitability Segment Number
            pasubnr     "Profitability segment changes
            belnr       "Document number
            posnr       "Item no. of CO-PA line item
            budat       "Posting Date
            kndnr       "Customer
            artnr       "Product number
            rec_waers   "Currency of the data record
            kaufn       "Sales Order Number
            kdpos       "Item Number in Sales Order
            bukrs       "Company code
            vkorg       "Sales Organization
            vtweg       "Distribution Channel
            spart       "Division
            auart       "Order Type
            vkbur       "Sales Office
            prctr       "Profit Center
            prodh       "Product hierarchy
            ww002       "PLC
            matkl       "Matl Group
            vv100       "Revenues
            absmg       "Sales quantity
    FROM ce11000
    INTO TABLE gt_ce11000
    WHERE paledger IN s_paled
      AND vrgar    IN s_vrgar
      AND budat    IN s_budat
      AND bukrs    IN s_bukrs
      AND ww002    IN s_ww002.

  IF gt_ce11000 IS NOT INITIAL.
    SELECT *
      FROM vbak
      INTO TABLE gt_vbak
      FOR ALL ENTRIES IN gt_ce11000
      WHERE vbeln = gt_ce11000-kaufn.
  ENDIF.

ENDFORM.                    " GET_DATA
*&---------------------------------------------------------------------*
*&      Form  GENERATE_DATA
*&---------------------------------------------------------------------*
*       Generate data
*----------------------------------------------------------------------*
FORM generate_data .

  DATA: ls_out_file     TYPE yse_outfile_ke24,
        ls_vbak         TYPE vbak,
        ls_ce11000      TYPE ty_ce11000.

  LOOP AT gt_ce11000 INTO ls_ce11000.
    MOVE-CORRESPONDING ls_ce11000 TO ls_out_file.
    READ TABLE gt_vbak INTO ls_vbak
      WITH KEY vbeln = ls_ce11000-kaufn.
    IF sy-subrc = 0.
      ls_out_file-auart = ls_vbak-auart.
    ENDIF.
    APPEND ls_out_file TO gt_out_file.
  ENDLOOP.

  IF gt_out_file IS INITIAL.
    MESSAGE s001(00) DISPLAY LIKE 'E'
    WITH 'No data generated, please check'(m08).
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " GENERATE_DATA
*&---------------------------------------------------------------------*
*&      Form  SEND_DATA
*&---------------------------------------------------------------------*
*       Send_data
*----------------------------------------------------------------------*
FORM send_data .

  DATA: lv_file     TYPE /sapdmc/ls_filename,
        ls_out_file TYPE yse_outfile_ke24,
        lv_filename TYPE char128,
        lv_directory(50) TYPE c,
        lv_line     TYPE string,
        lv_lines    TYPE char10,
        lv_logsys   TYPE tbdlst-logsys.

  lv_directory = p_server.
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = lv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
  REPLACE 'xxx' IN lv_directory WITH lv_logsys(3).
  CONCATENATE lv_directory 'KE24.CSV' INTO  lv_file.
  OPEN DATASET lv_file FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    MESSAGE s001(00) DISPLAY LIKE 'E'
    WITH 'Open dataset failed for :'(m03) lv_file.
    LEAVE LIST-PROCESSING.
  ENDIF.
  CLEAR: ls_out_file.
  LOOP AT  gt_out_file INTO ls_out_file.
    CALL FUNCTION 'CLOI_PUT_SIGN_IN_FRONT'
      CHANGING
        value         = ls_out_file-vv100.
    CALL FUNCTION 'CLOI_PUT_SIGN_IN_FRONT'
      CHANGING
        value         = ls_out_file-absmg.
    CONCATENATE   ls_out_file-vkorg     "Sales Organization
                  ls_out_file-vtweg     "Distribution Channel
                  ls_out_file-spart     "Division
                  ls_out_file-vkbur     "Sales Office
                  ls_out_file-kaufn     "Sales Order Number
                  ls_out_file-kdpos     "Item Number in Sales Order
                  ls_out_file-matkl     "Matl Group
                  ls_out_file-prodh     "Product hierarchy
                  ls_out_file-prctr     "Profit Center
                  ls_out_file-ww002     "PLC
                  ls_out_file-kndnr     "Customer
                  ls_out_file-artnr     "Product number
                  ls_out_file-auart     "SaTy
                  ls_out_file-budat     "Posting Date
                  ls_out_file-vv100     "Revenues
                  ls_out_file-absmg     "Sales quantity
                  ls_out_file-rec_waers "Currency of the data record
          INTO   lv_line
          SEPARATED BY cl_abap_char_utilities=>horizontal_tab.
    TRANSFER lv_line TO lv_file.
  ENDLOOP.
  CLOSE DATASET lv_file.
  IF sy-subrc <> 0.
    MESSAGE e001(00) WITH 'Close dataset failed for :'(m05)  lv_file.
  ELSE.
    DESCRIBE TABLE gt_out_file LINES lv_lines.
    MESSAGE i001(00)
      WITH 'Totatly '  lv_lines 'records are transferred!'.
  ENDIF.
ENDFORM.                    " SEND_DATA
*&---------------------------------------------------------------------*
*&      Form  DOWNLOAD_FILE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM download_file .
  DATA: lv_filename TYPE string,
        ls_out_file TYPE yse_outfile_ke24,
        lt_line     TYPE STANDARD TABLE OF string,
        lv_line     TYPE string.
  CLEAR: ls_out_file.
  LOOP AT  gt_out_file INTO ls_out_file.
    CONCATENATE   ls_out_file-vkorg     "Sales Organization
                  ls_out_file-vtweg     "Distribution Channel
                  ls_out_file-spart     "Division
                  ls_out_file-vkbur     "Sales Office
                  ls_out_file-kaufn     "Sales Order Number
                  ls_out_file-kdpos     "Item Number in Sales Order
                  ls_out_file-matkl     "Matl Group
                  ls_out_file-prodh     "Product hierarchy
                  ls_out_file-prctr     "Profit Center
                  ls_out_file-ww002     "PLC
                  ls_out_file-kndnr     "Customer
                  ls_out_file-artnr     "Product number
                  ls_out_file-auart     "SaTy
                  ls_out_file-budat     "Posting Date
                  ls_out_file-vv100     "Revenues
                  ls_out_file-absmg     "Sales quantity
                  ls_out_file-rec_waers "Currency of the data record
          INTO   lv_line
          SEPARATED BY cl_abap_char_utilities=>horizontal_tab.
    APPEND lv_line TO lt_line.
  ENDLOOP.
  CONCATENATE p_server
              '\KE24.CSV'
    INTO lv_filename.

  CALL METHOD cl_gui_frontend_services=>gui_download
    EXPORTING
*        bin_filesize              =
       filename                  = lv_filename
*        filetype                  = 'ASC'
*        append                    = SPACE
*        write_field_separator     = SPACE
*        header                    = '00'
*        trunc_trailing_blanks     = SPACE
*        write_lf                  = 'X'
*        col_select                = SPACE
*        col_select_mask           = SPACE
*        dat_mode                  = SPACE
*        confirm_overwrite         = SPACE
*        no_auth_check             = SPACE
*        codepage                  = SPACE
*        ignore_cerr               = ABAP_TRUE
*        replacement               = '#'
*        write_bom                 = SPACE
*        trunc_trailing_blanks_eol = 'X'
*        wk1_n_format              = SPACE
*        wk1_n_size                = SPACE
*        wk1_t_format              = SPACE
*        wk1_t_size                = SPACE
*        show_transfer_status      = 'X'
*        fieldnames                =
*        virus_scan_profile        = '/SCET/GUI_DOWNLOAD'
*        write_lf_after_last_line  = 'X'
*      IMPORTING
*        filelength                =
    CHANGING
      data_tab                  =   lt_line
    EXCEPTIONS
      file_write_error          = 1
      no_batch                  = 2
      gui_refuse_filetransfer   = 3
      invalid_type              = 4
      no_authority              = 5
      unknown_error             = 6
      header_not_allowed        = 7
      separator_not_allowed     = 8
      filesize_not_allowed      = 9
      header_too_long           = 10
      dp_error_create           = 11
      dp_error_send             = 12
      dp_error_write            = 13
      unknown_dp_error          = 14
      access_denied             = 15
      dp_out_of_memory          = 16
      disk_full                 = 17
      dp_timeout                = 18
      file_not_found            = 19
      dataprovider_exception    = 20
      control_flush_error       = 21
      not_supported_by_gui      = 22
      error_no_gui              = 23
      OTHERS                    = 24
          .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " DOWNLOAD_FILE
*&---------------------------------------------------------------------*
*&      Form  SET_THE_PATH
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM set_the_path .

  DATA: lv_folder TYPE string.
  CALL METHOD cl_gui_frontend_services=>directory_browse
*    EXPORTING
*      window_title         =
*      initial_folder       =
    CHANGING
      selected_folder      =  lv_folder
    EXCEPTIONS
      cntl_error           = 1
      error_no_gui         = 2
      not_supported_by_gui = 3
      OTHERS               = 4
          .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    p_server = lv_folder .
  ENDIF.

ENDFORM.                    " SET_THE_PATH

*Text symbol text£º
*M03:Open dataset failed for :
*M04:KE24 data is transferred to
*M05:Close dataset failed for :
*M08:No data generated, please check
*M09:Data is download to the PC with path:
*T01:Select screen

*T02:Mode of access
*Selection text£º
*PREADM:D       .
*PREADM2:D       .
*P_SERVER:        Path
*S_BUDAT:D       .
*S_BUKRS:D       .
*S_PALED:D       .
*S_VRGAR:D       .
*S_WW002:D       .
