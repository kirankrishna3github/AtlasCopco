*&---------------------------------------------------------------------*
*& Report  YSE_UPD_CREDIT_LIMIT_BLOCK
*&
*&---------------------------------------------------------------------*
*&
*& Credit Limit (un)block & Text upload / Credit Limit update
*&
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 18.04.2013
*  Change Request Number : CR2871
*  Transport request Nr. : CD1K975672
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. |   DATE     | NAME               | CORRECT. NR| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                    | CD1K9..... | CR....      *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_upd_credit_limit_block NO STANDARD PAGE HEADING
                                   LINE-SIZE 200.

TABLES: kna1, knb1, knkk,
        t691a.

*----------------------------------------------------------------------*
* BDC Tables                                                           *
*----------------------------------------------------------------------*
DATA: i_bdcdata      LIKE bdcdata OCCURS 0 WITH HEADER LINE,
      struct_bdcdata TYPE bdcdata.

*BDC Structure for Messages
DATA : BEGIN OF i_messtab OCCURS 0.
        INCLUDE STRUCTURE bdcmsgcoll.
DATA : END OF i_messtab.

TYPES truxs_t_text_data(4096)  TYPE c  OCCURS 0.

DATA: gt_raw  TYPE truxs_t_text_data.

DATA: gt_clin  TYPE STANDARD TABLE OF alsmex_tabline,
      gs_clin  TYPE alsmex_tabline.

TYPES: BEGIN OF ty_clup,
         kunnr     TYPE kunnr,
         klimk     TYPE char20,
         ctlpc     TYPE ctlpc_cm,
         klimk_n   TYPE klimk,
         klimk_o   TYPE klimk,
         ctlpc_o   TYPE ctlpc_cm,
         name1     TYPE name1_gp,
         error     TYPE xfeld,
      END OF ty_clup.
DATA: gt_clup  TYPE STANDARD TABLE OF ty_clup
                    WITH HEADER LINE.

TYPES: BEGIN OF ty_clbl,
         kunnr     TYPE kunnr,
         descr     TYPE text120,
         klimk     TYPE klimk,
         name1     TYPE name1_gp,
         error(1)  TYPE c,
       END OF ty_clbl.
DATA: gt_clbl  TYPE STANDARD TABLE OF ty_clbl
                    WITH HEADER LINE.

TYPES: BEGIN OF ty_kna1,
         kunnr     TYPE kunnr,
         name1     TYPE name1_gp,
       END OF ty_kna1.

DATA: gt_kna1  TYPE HASHED TABLE OF ty_kna1
                    WITH UNIQUE KEY kunnr
                    WITH HEADER LINE.

TYPES: BEGIN OF ty_knb1,
         kunnr     TYPE kunnr,
       END OF ty_knb1.

DATA: gt_knb1  TYPE HASHED TABLE OF ty_knb1
                    WITH UNIQUE KEY kunnr
                    WITH HEADER LINE,
      gt_knb1i TYPE TABLE OF ty_knb1
                    WITH HEADER LINE.

TYPES: BEGIN OF ty_knkk,
         kunnr     TYPE kunnr,
         klimk     TYPE klimk,
         ctlpc     TYPE ctlpc_cm,
         crblb     TYPE crblb_cm,
       END OF ty_knkk.

DATA: gt_knkk  TYPE HASHED TABLE OF ty_knkk
                    WITH UNIQUE KEY kunnr
                    WITH HEADER LINE,
      gt_knkki TYPE TABLE OF ty_knkk
                    WITH HEADER LINE.

TYPES: BEGIN OF ty_t691a,
        ctlpc      TYPE ctlpc_cm,
       END OF ty_t691a.

DATA: gt_t691a  TYPE HASHED TABLE OF ty_t691a
                     WITH UNIQUE KEY ctlpc
                     WITH HEADER LINE,
      gt_t691ai TYPE TABLE OF ty_t691a
                     WITH HEADER LINE.

DATA: gv_error      TYPE xfeld,
      gv_dcpfm      TYPE xudcpfm,
      gv_crblb      TYPE crblb_cm,
      gv_klimk      TYPE char20,
      gv_line1      TYPE text60,
      gv_line2      TYPE text60,
      gv_msgtxt     LIKE t100-text,
      gv_errlst     TYPE xfeld     VALUE 'X',
      gv_mode(1)    TYPE c         VALUE 'N'.

*&---------------------------------------------------------------------*
*&  Selection screen                                                   *
*&---------------------------------------------------------------------*

SELECTION-SCREEN  BEGIN OF BLOCK inp  WITH FRAME  TITLE text-s01.
PARAMETERS: p_bukrs  TYPE bukrs  OBLIGATORY
                                 MEMORY ID buk,
            p_kkber  TYPE t014-kkber  OBLIGATORY
                                 MEMORY ID kkb.
* Input file
PARAMETERS: p_infile  TYPE  rlgrap-filename LOWER CASE.
*                 DEFAULT 'C:\VT.MONITOR.xls'.
SELECTION-SCREEN  END OF BLOCK inp.

SELECTION-SCREEN  BEGIN OF BLOCK opt  WITH FRAME  TITLE text-s02.
PARAMETERS: p_updcl   TYPE xfeld  RADIOBUTTON GROUP opt,
            p_blkcl   TYPE xfeld  RADIOBUTTON GROUP opt,
            p_unbcl   TYPE xfeld  RADIOBUTTON GROUP opt.
SELECTION-SCREEN SKIP.
PARAMETERS: p_test  AS CHECKBOX  DEFAULT 'X'.
SELECTION-SCREEN  END OF BLOCK opt.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON p_bukrs.

  PERFORM  check_authorization_bukrs.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON p_kkber.

  PERFORM  check_authorization_kkber.

*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_infile.

* Get the filename
  PERFORM get_filename USING p_infile.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

* Get decimal notation
  SELECT SINGLE dcpfm INTO gv_dcpfm
         FROM usr01
         WHERE bname = sy-uname.

  IF p_updcl = 'X'.
    PERFORM update_cl.
  ELSEIF p_blkcl = 'X'.
    PERFORM block_cl.
  ELSEIF p_unbcl = 'X'.
    PERFORM unblock_cl.
  ENDIF.



*----------------------------------------------------------------------*
TOP-OF-PAGE.

  WRITE: /01 'Date:', sy-datum.
  IF p_updcl = 'X'.
    WRITE:  40 'Credit Limit Update'.
  ELSEIF p_blkcl = 'X'.
    WRITE:  40 'Credit Block'.
  ELSE.
    WRITE:  40 'Credit Unblock'.
  ENDIF.
  WRITE:  80 'Company:', p_bukrs,
         100 'Credit control Area:', p_kkber,
         140 'By:', sy-uname.
  IF NOT p_test IS INITIAL.
    WRITE: 170 '*** TEST ***'.
  ENDIF.
  WRITE: 197(4) sy-pagno.
  ULINE.

  IF NOT gv_errlst IS INITIAL.
    WRITE: /01 'Errors'.
  ELSE.
    IF p_updcl = 'X'.
      WRITE: /01 'Customer',
              13 'Name',
              50 ' Credit Limit before ',
              72 '  Credit Limit after ',
              95 'Risk cat. before',
             115 'Risk cat. after',
             135 'Error'.
    ELSE.
      WRITE: /01 'Customer',
              13 'Name',
              50 '        Credit Limit ',
              72 'Description',
             195 'Error'.
    ENDIF.
  ENDIF.

  ULINE.


*&---------------------------------------------------------------------*
*&  Forms                                                              *
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION_BUKRS
*&---------------------------------------------------------------------*
*       Check authorizations for Company
*----------------------------------------------------------------------*
FORM check_authorization_bukrs .

  AUTHORITY-CHECK OBJECT 'F_BKPF_BUK'
           ID 'ACTVT' DUMMY
           ID 'BUKRS' FIELD p_bukrs.

  IF sy-subrc = 4.
*     No authorisation to display data
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH p_bukrs.
    EXIT.
  ELSEIF sy-subrc <> 0.
*     Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
    EXIT.
  ENDIF.

ENDFORM.                    " CHECK_AUTHORIZATION_BUKRS

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION_KKBER
*&---------------------------------------------------------------------*
*       Check authorizations for Credit Control Area
*----------------------------------------------------------------------*
FORM check_authorization_kkber .

  AUTHORITY-CHECK OBJECT 'F_KNKA_KKB'
           ID 'ACTVT' DUMMY
           ID 'KKBER' FIELD p_kkber.

  IF sy-subrc = 4.
*     No authorisation to display data
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH p_kkber.
    EXIT.
  ELSEIF sy-subrc <> 0.
*     Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
    EXIT.
  ENDIF.

ENDFORM.                    " CHECK_AUTHORIZATION_KKBER

*&---------------------------------------------------------------------*
*&      Form  GET_FILENAME
*&---------------------------------------------------------------------*
*       Get the filename
*----------------------------------------------------------------------*
*      -->P_INFILE : Input file
*----------------------------------------------------------------------*
FORM get_filename  USING    p_infile.

  CALL FUNCTION 'WS_FILENAME_GET'
    EXPORTING
      def_path         = 'C:\'
      mask             = ',*.XLS.'
    IMPORTING
      filename         = p_infile
    EXCEPTIONS
      inv_winsys       = 1
      no_batch         = 2
      selection_cancel = 3
      selection_error  = 4
      OTHERS           = 5.

  IF sy-subrc <> 0.
  ENDIF.

ENDFORM.                    " GET_FILENAME

*&---------------------------------------------------------------------*
*&      Form  UPDATE_CL
*&---------------------------------------------------------------------*
*       Credit Limit Update
*----------------------------------------------------------------------*
FORM update_cl .

* Read input file
  PERFORM read_file_upd.

* Check if error
  IF NOT gv_error IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'Error reading file for Credit Limit Update'(e01).
    EXIT.
  ENDIF.

* Check anything selected
  IF gt_clup[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No records selected for Credit Limit Update'(e02).
    EXIT.
  ENDIF.

* Check input data
  PERFORM check_data_upd.

* Get additional data
  PERFORM get_data_upd.

* Modify credit limit data
  IF p_test IS INITIAL.
    PERFORM cl_upd.
  ENDIF.

* List
  PERFORM list_upd.

ENDFORM.                    " UPDATE_CL

*&---------------------------------------------------------------------*
*&      Form  BLOCK_CL
*&---------------------------------------------------------------------*
*       Credit Block
*----------------------------------------------------------------------*
FORM block_cl .

* Read input file
  PERFORM read_file_bl.

* Check if error
  IF NOT gv_error IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'Error reading file for Credit Block'(e03).
    EXIT.
  ENDIF.

* Check anything selected
  IF gt_clbl[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No records selected for Credit Block'(e04).
    EXIT.
  ENDIF.

* Check input data
  PERFORM check_data_bl.

* Get additional data
  PERFORM get_data_bl.

* Modify credit limit data
  IF p_test IS INITIAL.
    PERFORM cl_blck.
  ENDIF.

* List
  PERFORM list_bl.

ENDFORM.                    " BLOCK_CL

*&---------------------------------------------------------------------*
*&      Form  UNBLOCK_CL
*&---------------------------------------------------------------------*
*       Credit Unblock
*----------------------------------------------------------------------*
FORM unblock_cl .

* Read input file
  PERFORM read_file_bl.

* Check if error
  IF NOT gv_error IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'Error reading file for Credit Unblock'(e05).
    EXIT.
  ENDIF.

* Check anything selected
  IF gt_clbl[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No records selected for Credit Unblock'(e06).
    EXIT.
  ENDIF.

* Check input data
  PERFORM check_data_bl.

* Get additional data
  PERFORM get_data_bl.

* Modify credit limit data
  IF p_test IS INITIAL.
    PERFORM cl_blck.
  ENDIF.

* List
  PERFORM list_bl.

ENDFORM.                    " BLOCK_CL

*&---------------------------------------------------------------------*
*&      Form  READ_FILE_UPD
*&---------------------------------------------------------------------*
*       Read input file
*----------------------------------------------------------------------*
FORM read_file_upd .

* Read EXCEL file
  CALL FUNCTION 'TEXT_CONVERT_XLS_TO_SAP'
    EXPORTING
*     I_FIELD_SEPERATOR        =
      i_line_header            = 'X'
      i_tab_raw_data           = gt_raw       " WORK TABLE
      i_filename               = p_infile
    TABLES
      i_tab_converted_data     = gt_clup[]     "ACTUAL DATA
   EXCEPTIONS
      conversion_failed        = 1
      OTHERS                   = 2.

  IF sy-subrc <> 0.
*    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
*            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    gv_error = 'X'.
  ENDIF.

  LOOP AT gt_clup.
    PERFORM add_zeros USING gt_clup-kunnr.
    MODIFY gt_clup.
  ENDLOOP.

** Read EXCEL file
*  CALL FUNCTION 'ALSM_EXCEL_TO_INTERNAL_TABLE'
*    EXPORTING
*      filename                = p_infile
*      i_begin_col             = '1'
*      i_begin_row             = '2'  "Do not require headings
*      i_end_col               = '3'
*      i_end_row               = '25000'
*    TABLES
*      intern                  = gt_clin
*    EXCEPTIONS
*      inconsistent_parameters = 1
*      upload_ole              = 2
*      OTHERS                  = 3.
*  IF sy-subrc <> 0.
**    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
**            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
*    gv_error = 'X'.
*  ENDIF.
*
** Get the excel sheet values into internal table
*  SORT gt_clin BY row col.
*
*  LOOP AT gt_clin INTO gs_clin.
*    CASE gs_clin-col.
*      WHEN 1.
*        gt_clup-kunnr = gs_clin-value.
*        PERFORM add_zeros USING gt_clup-kunnr.
*      WHEN 2.
*        gt_clup-klimk = gs_clin-value.
*      WHEN 3.
*        gt_clup-ctlpc = gs_clin-value.
*    ENDCASE.
*
*    AT END OF row.
*      APPEND gt_clup.
*      CLEAR gt_clup.
*    ENDAT.
*
*  ENDLOOP.

ENDFORM.                    " READ_FILE_UPD

*&---------------------------------------------------------------------*
*&      Form  READ_FILE_BL
*&---------------------------------------------------------------------*
*       Read input file
*----------------------------------------------------------------------*
FORM read_file_bl .

* Read EXCEL file
  CALL FUNCTION 'TEXT_CONVERT_XLS_TO_SAP'
    EXPORTING
*     I_FIELD_SEPERATOR        =
      i_line_header            = 'X'
      i_tab_raw_data           = gt_raw       " WORK TABLE
      i_filename               = p_infile
    TABLES
      i_tab_converted_data     = gt_clbl[]     "ACTUAL DATA
   EXCEPTIONS
      conversion_failed        = 1
      OTHERS                   = 2.

  IF sy-subrc <> 0.
*    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
*            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    gv_error = 'X'.
  ENDIF.

  LOOP AT gt_clbl.
    PERFORM add_zeros USING gt_clbl-kunnr.
    MODIFY gt_clbl.
  ENDLOOP.

ENDFORM.                    " READ_FILE_BL

*&---------------------------------------------------------------------*
*&      Form  ADD_ZEROS
*&---------------------------------------------------------------------*
*       Add leading zeros
*----------------------------------------------------------------------*
*      -->P_FIELD : field
*----------------------------------------------------------------------*
FORM add_zeros  USING    p_field.

  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
    EXPORTING
      input  = p_field
    IMPORTING
      output = p_field.

ENDFORM.                    " ADD_ZEROS

*&---------------------------------------------------------------------*
*&      Form  CHECK_DATA_UPD
*&---------------------------------------------------------------------*
*       Check input data
*----------------------------------------------------------------------*
FORM check_data_upd .

* Get customer data
  SELECT kunnr
         INTO TABLE gt_knb1i
         FROM knb1
         FOR ALL ENTRIES IN gt_clup
         WHERE kunnr = gt_clup-kunnr
           AND bukrs = p_bukrs.
  SORT gt_knb1i BY kunnr.
  DELETE ADJACENT DUPLICATES FROM gt_knb1i
                             COMPARING kunnr.
  gt_knb1[] = gt_knb1i[].
  FREE gt_knb1i.

  SELECT kunnr name1
      INTO TABLE gt_kna1
      FROM kna1
      FOR ALL ENTRIES IN gt_knb1
      WHERE kunnr = gt_knb1-kunnr.

* Get risk categories
  SELECT ctlpc
         INTO TABLE gt_t691ai
         FROM t691a
         FOR ALL ENTRIES IN gt_clup
         WHERE ctlpc = gt_clup-ctlpc
           AND kkber = p_kkber.
  SORT gt_t691ai BY ctlpc.
  DELETE ADJACENT DUPLICATES FROM gt_t691ai
                             COMPARING ctlpc.
  gt_t691a[] = gt_t691ai[].
  FREE gt_t691ai.

* Check data
  LOOP AT gt_clup.
*   Customer
    READ TABLE gt_knb1 WITH KEY kunnr = gt_clup-kunnr.
    IF sy-subrc NE 0.
      WRITE: /01 'Customer', gt_clup-kunnr,
                 'does not exist for Company', p_bukrs.
      DELETE gt_clup.
      CONTINUE.
    ENDIF.
*   Credit limit
    IF gt_clup-klimk CN '0123456789 ,.'.
      WRITE: /01 'Credit limit', gt_clup-klimk,
                 'has a wrong format'.
      DELETE gt_clup.
      CONTINUE.
    ENDIF.
*   Risk category
    READ TABLE gt_t691a WITH KEY ctlpc = gt_clup-ctlpc.
    IF sy-subrc NE 0.
      WRITE: /01 'Risk category', gt_clup-ctlpc,
                 'does not exist for Credit control area', p_kkber.
      DELETE gt_clup.
      CONTINUE.
    ENDIF.

  ENDLOOP.

ENDFORM.                    " CHECK_DATA_UPD

*&---------------------------------------------------------------------*
*&      Form  CHECK_DATA_BL
*&---------------------------------------------------------------------*
*       Check input data
*----------------------------------------------------------------------*
FORM check_data_bl .

* Get customer data
  SELECT kunnr
         INTO TABLE gt_knb1i
         FROM knb1
         FOR ALL ENTRIES IN gt_clbl
         WHERE kunnr = gt_clbl-kunnr
           AND bukrs = p_bukrs.
  SORT gt_knb1i BY kunnr.
  DELETE ADJACENT DUPLICATES FROM gt_knb1i
                             COMPARING kunnr.
  gt_knb1[] = gt_knb1i[].
  FREE gt_knb1i.

  SELECT kunnr name1
      INTO TABLE gt_kna1
      FROM kna1
      FOR ALL ENTRIES IN gt_knb1
      WHERE kunnr = gt_knb1-kunnr.

* Control area data
  SELECT kunnr klimk ctlpc crblb
         INTO TABLE gt_knkki
         FROM knkk
         FOR ALL ENTRIES IN gt_knb1
         WHERE kunnr = gt_knb1-kunnr
           AND kkber = p_kkber.
  SORT gt_knkki BY kunnr.
  DELETE ADJACENT DUPLICATES FROM gt_knkki
                             COMPARING kunnr.
  gt_knkk[] = gt_knkki[].
  FREE gt_knkki.

* Check data
  LOOP AT gt_clbl.
*   Customer
    READ TABLE gt_knb1 WITH KEY kunnr = gt_clbl-kunnr.
    IF sy-subrc NE 0.
      WRITE: /01 'Customer', gt_clbl-kunnr,
                 'does not exist for Company', p_bukrs.
      DELETE gt_clbl.
      CONTINUE.
    ENDIF.
*   Blocking indicator
    CLEAR gt_knkk.
    READ TABLE gt_knkk WITH KEY kunnr = gt_clbl-kunnr.
*   Unblock
    IF p_unbcl = 'X'.
      IF sy-subrc = 0             AND
         gt_knkk-crblb IS INITIAL.
        WRITE: /01 'Customer', gt_clbl-kunnr,
                   'is NOT blocked'.
        DELETE gt_clbl.
        CONTINUE.
      ENDIF.
    ENDIF.
*   Block
    IF p_blkcl = 'X'.
      IF NOT gt_knkk-crblb IS INITIAL.
        WRITE: /01 'Customer', gt_clbl-kunnr,
                   'is already blocked'.
        DELETE gt_clbl.
        CONTINUE.
      ENDIF.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " CHECK_DATA_BL

*&---------------------------------------------------------------------*
*&      Form  GET_DATA_UPD
*&---------------------------------------------------------------------*
*       Get additional data
*----------------------------------------------------------------------*
FORM get_data_upd .

* Control area data
  SELECT kunnr klimk ctlpc crblb
         INTO TABLE gt_knkki
         FROM knkk
         FOR ALL ENTRIES IN gt_knb1
         WHERE kunnr = gt_knb1-kunnr
           AND kkber = p_kkber.
  SORT gt_knkki BY kunnr.
  DELETE ADJACENT DUPLICATES FROM gt_knkki
                             COMPARING kunnr.
  gt_knkk[] = gt_knkki[].
  FREE gt_knkki.

  LOOP AT gt_clup.
*   Convert input redit limit
    gv_klimk = gt_clup-klimk.
    IF gv_dcpfm = 'X'.
      TRANSLATE gv_klimk USING ', '.
    ELSE.
      TRANSLATE gv_klimk USING '. '.
    ENDIF.
    CONDENSE gv_klimk NO-GAPS.
    IF gv_dcpfm ne 'X'.
      REPLACE ',' IN gv_klimk WITH '.'.
    ENDIF.
    gt_clup-klimk_n = gv_klimk.
*   Old control area data
    READ TABLE gt_knkk WITH KEY kunnr = gt_clup-kunnr.
    IF sy-subrc = 0.
      gt_clup-klimk_o = gt_knkk-klimk.
      gt_clup-ctlpc_o = gt_knkk-ctlpc.
    ENDIF.
*   Customer name
    READ TABLE gt_kna1 WITH KEY kunnr = gt_clup-kunnr.
    IF sy-subrc = 0.
      gt_clup-name1 = gt_kna1-name1.
    ENDIF.

    MODIFY gt_clup.

  ENDLOOP.

ENDFORM.                    " GET_DATA_UPD

*&---------------------------------------------------------------------*
*&      Form  GET_DATA_BL
*&---------------------------------------------------------------------*
*       Get additional data
*----------------------------------------------------------------------*
FORM get_data_bl .

  LOOP AT gt_clbl.
*   Old control area data
    READ TABLE gt_knkk WITH KEY kunnr = gt_clbl-kunnr.
    IF sy-subrc = 0.
      gt_clbl-klimk = gt_knkk-klimk.
    ENDIF.
*   Customer name
    READ TABLE gt_kna1 WITH KEY kunnr = gt_clbl-kunnr.
    IF sy-subrc = 0.
      gt_clbl-name1 = gt_kna1-name1.
    ENDIF.

    MODIFY gt_clbl.

  ENDLOOP.

ENDFORM.                    " GET_DATA_BL

*&---------------------------------------------------------------------*
*&      Form  CL_UPD
*&---------------------------------------------------------------------*
*       Update credit limit data
*----------------------------------------------------------------------*
FORM cl_upd .

  LOOP AT gt_clup.
    CLEAR gv_error.
    PERFORM update_credit_limit.
    IF NOT gv_error IS INITIAL.
      gt_clup-error = 'X'.
      MODIFY gt_clup.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " CL_UPD

*&---------------------------------------------------------------------*
*&      Form  CL_BLCK
*&---------------------------------------------------------------------*
*       Credit (Un)Block
*----------------------------------------------------------------------*
FORM cl_blck .

* Indicator
  CLEAR gv_crblb.
  IF p_blkcl = 'X'.
    gv_crblb = 'X'.
  ENDIF.

  LOOP AT gt_clbl.
    CLEAR gv_error.
*   Credit block
    PERFORM update_credit_block.
    IF NOT gv_error IS INITIAL.
      gt_clbl-error = 'X'.
      MODIFY gt_clbl.
    ELSE.
*     Add description in 'Internal info'
      IF NOT gt_clbl-descr IS INITIAL.
        PERFORM update_text.
        IF NOT gv_error IS INITIAL.
          gt_clbl-error = 'T'.
          MODIFY gt_clbl.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " CL_BLCK

*&---------------------------------------------------------------------*
*&      Form  UPDATE_CREDIT_LIMIT
*&---------------------------------------------------------------------*
*       Update Credit Limit
*----------------------------------------------------------------------*
FORM update_credit_limit .

  REFRESH: i_bdcdata, i_messtab.

* Selection screen
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                 USING    'SAPMF02C'  '0100'  'X' ' ' ' '
                 CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'RF02L-KUNNR' gt_clup-kunnr
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'RF02L-KKBER' p_kkber
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'RF02L-D0210' 'X'
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'BDC_OKCODE'  '/00'
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

* Update credit limit
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                 USING    'SAPMF02C'  '0210'  'X' ' ' ' '
                 CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  IF NOT gt_clup-klimk IS INITIAL.
    WRITE gt_clup-klimk_n TO gv_klimk.
    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    ''  '' ' ' 'KNKK-KLIMK' gv_klimk
       CHANGING struct_bdcdata.
    APPEND struct_bdcdata  TO i_bdcdata.
    CLEAR  struct_bdcdata.
  ENDIF.

* Risk category
  IF NOT gt_clup-ctlpc IS INITIAL.
    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    ''  '' ' ' 'KNKK-CTLPC' gt_clup-ctlpc
       CHANGING struct_bdcdata.
    APPEND struct_bdcdata  TO i_bdcdata.
    CLEAR  struct_bdcdata.
  ENDIF.

* Save
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'BDC_OKCODE'  '=UPDA'
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

* Call transaction FD32
  PERFORM call_fd32.

ENDFORM.                    " UPDATE_CREDIT_LIMIT

*&---------------------------------------------------------------------*
*&      Form  UPDATE_CREDIT_BLOCK
*&---------------------------------------------------------------------*
*       Update Credit Block
*----------------------------------------------------------------------*
FORM update_credit_block .

  REFRESH: i_bdcdata, i_messtab.

* Selection screen
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                 USING    'SAPMF02C'  '0100'  'X' ' ' ' '
                 CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'RF02L-KUNNR' gt_clbl-kunnr
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'RF02L-KKBER' p_kkber
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'RF02L-D0210' 'X'
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'BDC_OKCODE'  '/00'
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

* Update credit bLock
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                 USING    'SAPMF02C'  '0210'  'X' ' ' ' '
                 CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  IF p_blkcl = 'X'.
    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    ''  '' ' ' 'KNKK-CRBLB' 'X'
       CHANGING struct_bdcdata.
  ELSE.
    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    ''  '' ' ' 'KNKK-CRBLB' ' '
       CHANGING struct_bdcdata.
  ENDIF.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

* Save
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'BDC_OKCODE'  '=UPDA'
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

* Call transaction FD32
  PERFORM call_fd32.

ENDFORM.                    " UPDATE_CREDIT_BLOCK

*&---------------------------------------------------------------------*
*&      Form  CALL_FD32
*&---------------------------------------------------------------------*
*       Call transacrtion FD32
*----------------------------------------------------------------------*
FORM call_fd32 .

  CALL TRANSACTION  'FD32'  USING i_bdcdata
                            MODE gv_mode
                            UPDATE 'S'
                            MESSAGES INTO i_messtab.

* Check for errors
  LOOP AT i_messtab WHERE msgtyp = 'E'
                       OR msgtyp = 'A'.

    MESSAGE ID i_messtab-msgid
            TYPE i_messtab-msgtyp
            NUMBER i_messtab-msgnr
            INTO gv_msgtxt
            WITH i_messtab-msgv1 i_messtab-msgv2
                 i_messtab-msgv3 i_messtab-msgv4.

    WRITE: / gv_msgtxt.
    gv_error = 'X'.
  ENDLOOP.

ENDFORM.                    " CALL_FD32

*&---------------------------------------------------------------------*
*&      Form  UPDATE_TEXT
*&---------------------------------------------------------------------*
*       Add description in 'Internal info'
*----------------------------------------------------------------------*
FORM update_text .

  DATA: lv_tdobject  TYPE tdobject  VALUE 'KNKK',
        lv_tdid      TYPE tdid      VALUE '0001',
        lv_tdspras   TYPE spras     VALUE 'E',
        lv_tdname    TYPE tdobname,
        lv_thead     TYPE thead,
        lt_tlines    TYPE TABLE OF tline
                          WITH HEADER LINE.
  DATA: lv_new       TYPE xfeld.


* Split text
  PERFORM split_text.

* Read 'Internal info'
  CONCATENATE gt_clbl-kunnr p_kkber INTO lv_tdname.

  CALL FUNCTION 'READ_TEXT'
    EXPORTING
      id                      = lv_tdid
      language                = lv_tdspras
      name                    = lv_tdname
      object                  = lv_tdobject
    IMPORTING
      header                  = lv_thead
    TABLES
      lines                   = lt_tlines
    EXCEPTIONS
      id                      = 1
      language                = 2
      name                    = 3
      not_found               = 4
      object                  = 5
      reference_check         = 6
      wrong_access_to_archive = 7
      OTHERS                  = 8.
  IF sy-subrc <> 0.
*    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    lv_new = 'X'.
  ENDIF.

* New text: Initialize
  IF lv_new = 'X'.
    CALL FUNCTION 'INIT_TEXT'
      EXPORTING
        id       = lv_tdid
        language = lv_tdspras
        name     = lv_tdname
        object   = lv_tdobject
      IMPORTING
        header   = lv_thead
      TABLES
        lines    = lt_tlines
      EXCEPTIONS
        id       = 1
        language = 2
        name     = 3
        object   = 4
        OTHERS   = 5.
    IF sy-subrc <> 0.
*    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    ENDIF.
  ENDIF.

* Add description
  lt_tlines-tdformat = '*'.
  lt_tlines-tdline = gv_line1.
  INSERT lt_tlines INDEX 1.
  IF NOT gv_line2 IS INITIAL.
    lt_tlines-tdline = gv_line2.
    INSERT lt_tlines INDEX 2.
  ENDIF.

* Write 'Internal info'
  CALL FUNCTION 'SAVE_TEXT'
    EXPORTING
      header                = lv_thead
      insert                = lv_new
      savemode_direct       = 'X'
*    IMPORTING
*      FUNCTION              =
*      NEWHEADER             =
    TABLES
      lines                 = lt_tlines
    EXCEPTIONS
      id                    = 1
      language              = 2
      name                  = 3
      object                = 4
      OTHERS                = 5
            .
  IF sy-subrc <> 0.
*    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    gv_error = 'X'.
  ELSE.
    CALL FUNCTION 'COMMIT_TEXT'.
  ENDIF.

ENDFORM.                    " UPDATE_TEXT

*&---------------------------------------------------------------------*
*&      Form  SPLIT_TEXT
*&---------------------------------------------------------------------*
*       Split description in  2 lines.
*----------------------------------------------------------------------*
FORM split_text .

  DATA: lv_offset1    TYPE i,
        lv_sep(1)     TYPE c  VALUE '#'.


  CLEAR: gv_line1,
         gv_line2.


* Check for separator
  FIND FIRST OCCURRENCE OF lv_sep IN gt_clbl-descr
                                  MATCH OFFSET lv_offset1.
  IF sy-subrc NE 0.
    gv_line1 = gt_clbl-descr(60).
    gv_line2 = gt_clbl-descr+60(60).
  ELSE.
    SPLIT gt_clbl-descr AT lv_sep
                        INTO gv_line1 gv_line2.
  ENDIF.

ENDFORM.                    " SPLIT_TEXT

*&---------------------------------------------------------------------*
*&      Form  LIST_UPD
*&---------------------------------------------------------------------*
*       List Credit limit updates
*----------------------------------------------------------------------*
FORM list_upd .

  CLEAR gv_errlst.
  NEW-PAGE.
  LOOP AT gt_clup.
    WRITE: /01     gt_clup-kunnr,
            13     gt_clup-name1,
            50(20) gt_clup-klimk_o,
            72(20) gt_clup-klimk_n,
            95     gt_clup-ctlpc_o,
           115     gt_clup-ctlpc,
           135     gt_clup-error.
  ENDLOOP.

ENDFORM.                    " LIST_UPD

*&---------------------------------------------------------------------*
*&      Form  LIST_BL
*&---------------------------------------------------------------------*
*       List Credit limit (un)blocks
*----------------------------------------------------------------------*
FORM list_bl .

  CLEAR gv_errlst.
  NEW-PAGE.

  LOOP AT gt_clbl.
    WRITE: /01     gt_clbl-kunnr,
            13     gt_clbl-name1,
            50(20) gt_clbl-klimk,
            72     gt_clbl-descr,
           195     gt_clbl-error.
  ENDLOOP.

ENDFORM.                    " LIST_BL

*Text symbol text£º
*E01:Error reading file for Credit Limit Update
*E02:No records selected for Credit Limit Update
*E03:Error reading file for Credit Block
*E04:No records selected for Credit Block
*E05:Error reading file for Credit Unblock
*E06:No records selected for Credit Unblock
*S01:Input Data

*S02:Options
*Selection text£º
*P_BLKCL:        Credit Block
*P_BUKRS:D       .
*P_INFILE:        Input File
*P_KKBER:D       .
*P_TEST:        Test Run
*P_UNBCL:        Credit Unblock
*P_UPDCL:        Credit Limit Update
