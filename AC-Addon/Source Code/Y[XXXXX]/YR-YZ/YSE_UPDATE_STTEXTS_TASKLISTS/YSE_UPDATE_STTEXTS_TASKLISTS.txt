************************************************************************
* Program ID           : YSE_UPDATE_STTEXTS_TASKLISTS                  *
* Program Title        : Update Standard texts in Task Lists           *
* Author               : Lakshmi Reddy                                 *
* Date                 : 09/AUG/2010                                   *
* Change Request Number: CD1K958487                                    *
* Description          : This program updates Task Lists with changed  *
*                        Standard texts in table PLPO                  *
*======================================================================*
* Copied From          : N/A                                           *
* Title                : N/A                                           *
* Other Related obj    : N/A                                           *
*======================================================================*
* Change History Log                                                   *
*----------------------------------------------------------------------*
* Mod. no.|Date      |Name        |Correction Number|Change Reference  *
*----------------------------------------------------------------------*
* MOD-001 |09/08/2010|LakshmiReddy|                 |CR1348            *
* Description: Initial Development                                     *
*----------------------------------------------------------------------*

REPORT  yse_update_sttexts_tasklists MESSAGE-ID yam_re.

TYPE-POOLS: slis.

************************************************************************
*                  T Y P E S  S T R U C T U R E S
************************************************************************
*Task list groups
TYPES: BEGIN OF ty_tlgroups,
        plnty TYPE plnty,
        plnnr TYPE plnnr,
        plnkn TYPE plnkn,
        zaehl TYPE cim_count,
        ktsch TYPE ktsch,
        ltxa1 TYPE ltxa1,
        plnal TYPE plnal,
       END OF ty_tlgroups.

*Short texts of standard text keys
TYPES:BEGIN OF ty_t435t,
       spras TYPE spras,               "Language Key
       vlsch TYPE ktsch,               "Standard text key
       txt   TYPE vlsch_txt,           "Description of the standard text key
      END OF ty_t435t.

************************************************************************
*                 I N T E R N A L  T A B L E S
************************************************************************
DATA: i_tlgroups    TYPE STANDARD TABLE OF ty_tlgroups,
      i_plpo        TYPE STANDARD TABLE OF plpo,
      i_t435t       TYPE STANDARD TABLE OF ty_t435t,
      i_fieldcatalog TYPE slis_t_fieldcat_alv,
      i_events       TYPE slis_t_event,
      i_header       TYPE slis_t_listheader,
      i_changed_tlgroups TYPE STANDARD TABLE OF ty_tlgroups.


************************************************************************
*                 W O R K  A R E A S
************************************************************************
DATA: wa_tlgroups TYPE ty_tlgroups,
      wa_plpo     TYPE plpo,
      wa_t435t    TYPE ty_t435t.

*----------------------------------------------------------------------*
*                 V A R I A B L E S                                    *
*----------------------------------------------------------------------*
DATA: v_plnnr TYPE plnnr,       "Group
      v_plnal TYPE plnal,       "Group Counter
      v_ktsch TYPE ktsch.       "Standard text key

*----------------------------------------------------------------------*
*                 S E L E C T I O N  S C R E E N                       *
*----------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK s1 WITH FRAME TITLE text-001.
SELECT-OPTIONS: s_plnnr FOR v_plnnr OBLIGATORY,    "Group
                s_plnal FOR v_plnal OBLIGATORY,    "Group Counter
                s_ktsch FOR v_ktsch OBLIGATORY.   "Standard text key
SELECTION-SCREEN END OF BLOCK s1.

************************************************************************
*                 S T A R T - O F - S E L E C T I O N
************************************************************************

PERFORM f100_get_task_lists_plpo.

PERFORM f200_get_shorttexts_from_t435t.

PERFORM f300_update_st_texts_in_plpo.

*&---------------------------------------------------------------------*
*&      Form  F100_GET_TASK_LISTS_PLPO
*&---------------------------------------------------------------------*
*       Get Task Lists and standard texts from PLPO table
*----------------------------------------------------------------------*
FORM f100_get_task_lists_plpo .

  REFRESH: i_tlgroups.
  SELECT plpo~plnty
         plpo~plnnr
         plpo~plnkn
         plpo~zaehl
         plpo~ktsch
         plpo~ltxa1
         plas~plnal
  INTO   CORRESPONDING FIELDS OF TABLE i_tlgroups
  FROM   plas INNER JOIN plpo
  ON     plas~plnty EQ plpo~plnty
  AND    plas~plnnr EQ plpo~plnnr
  AND    plas~plnkn EQ plpo~plnkn
  AND    plas~zaehl EQ plpo~zaehl
  WHERE  plas~plnnr IN s_plnnr
  AND    plas~plnal IN s_plnal
  AND    plpo~ktsch IN s_ktsch
  AND    plpo~loekz EQ ' '.

ENDFORM.                    " F100_GET_TASK_LISTS_PLPO

*&---------------------------------------------------------------------*
*&      Form  F200_GET_SHORTTEXTS_FROM_T435T
*&---------------------------------------------------------------------*
*       Get short texts of Standard text keys from T435T table
*----------------------------------------------------------------------*
FORM f200_get_shorttexts_from_t435t .

  REFRESH: i_t435t.
  IF NOT i_tlgroups IS INITIAL.
    SELECT spras
           vlsch
           txt
    FROM   t435t
    INTO   TABLE i_t435t
    FOR    ALL ENTRIES IN i_tlgroups
    WHERE  vlsch EQ i_tlgroups-ktsch
    AND    spras EQ 'EN'.
  ENDIF.

ENDFORM.                    " F200_GET_SHORTTEXTS_FROM_T435T

*&---------------------------------------------------------------------*
*&      Form  F300_UPDATE_ST_TEXTS_IN_PLPO
*&---------------------------------------------------------------------*
*       Update table PLPO with changed standard text keys
*----------------------------------------------------------------------*
FORM f300_update_st_texts_in_plpo .

  DATA: l_index TYPE sy-tabix.

  CLEAR: wa_tlgroups,
         wa_plpo,
         l_index.
  REFRESH: i_plpo.

  LOOP AT i_tlgroups INTO wa_tlgroups.

    l_index = sy-tabix.

    CLEAR: wa_t435t.
    READ TABLE i_t435t INTO wa_t435t WITH KEY vlsch = wa_tlgroups-ktsch.
    IF sy-subrc EQ 0.
      IF wa_tlgroups-ltxa1 NE wa_t435t-txt.
        wa_tlgroups-ltxa1 = wa_t435t-txt.
        MODIFY i_tlgroups
        INDEX l_index
        FROM wa_tlgroups
        TRANSPORTING ltxa1.
        APPEND wa_tlgroups TO i_changed_tlgroups.
      ENDIF.
    ENDIF.
    CLEAR: wa_tlgroups,
           wa_t435t,
           l_index.
  ENDLOOP.

* SELECT changed task lists FROM plpo table
  SELECT *
  FROM   plpo
  INTO   TABLE i_plpo
  FOR    ALL ENTRIES IN i_changed_tlgroups
  WHERE  plnty EQ i_changed_tlgroups-plnty
  AND    plnnr EQ i_changed_tlgroups-plnnr
  AND    plnkn EQ i_changed_tlgroups-plnkn
  AND    zaehl EQ i_changed_tlgroups-zaehl
  AND    ktsch EQ i_changed_tlgroups-ktsch.

  LOOP AT i_plpo INTO wa_plpo.
    l_index = sy-tabix.

    CLEAR: wa_tlgroups.
    READ TABLE i_changed_tlgroups INTO wa_tlgroups WITH KEY  plnty = wa_plpo-plnty
                                                             plnnr = wa_plpo-plnnr
                                                             plnkn = wa_plpo-plnkn
                                                             zaehl = wa_plpo-zaehl
                                                             ktsch = wa_plpo-ktsch.
    IF sy-subrc EQ 0.
      wa_plpo-ltxa1 = wa_tlgroups-ltxa1.
      MODIFY i_plpo
      INDEX  l_index
      FROM   wa_plpo.
    ENDIF.
    CLEAR: wa_plpo,
           l_index.
  ENDLOOP.

*Update table PLPO with changed standard text key short texts
  IF NOT i_plpo IS INITIAL.
    MODIFY plpo
    FROM TABLE i_plpo.
    IF sy-subrc EQ 0.
      COMMIT WORK.
*ALV Report
      PERFORM f310_display_report.
    ELSE.
      ROLLBACK WORK.
      MESSAGE s320 WITH
             'No Task List Groups have been updated'(002).
    ENDIF.
  ELSE.
    MESSAGE s320 WITH
           'No Task List Groups have been updated'(002).
  ENDIF.

ENDFORM.                    " F300_UPDATE_ST_TEXTS_IN_PLPO
*&---------------------------------------------------------------------*
*&      Form  F310_DISPLAY_REPORT
*&---------------------------------------------------------------------*
*       Display ALV report
*----------------------------------------------------------------------*
FORM f310_display_report .

  PERFORM f320_build_field_catalog.

  PERFORM f330_build_events.

  PERFORM f340_display_alv_report.

ENDFORM.                    " F310_DISPLAY_REPORT
*&---------------------------------------------------------------------*
*&      Form  f320_build_field_catalog
*&---------------------------------------------------------------------*
*       Build field catalog
*----------------------------------------------------------------------*
FORM f320_build_field_catalog .

  PERFORM f321_field_catalog USING 'PLNNR'
                                   'Task List Group'(003).

  PERFORM f321_field_catalog USING 'PLNAL'
                                   'Group Counter'(004).

  PERFORM f321_field_catalog USING 'PLNKN'
                                   'TaskList Node Number'(005).

  PERFORM f321_field_catalog USING 'ZAEHL'
                                   'Internal Counter'(006).

  PERFORM f321_field_catalog USING 'KTSCH'
                                   'Standard text key'(007).

  PERFORM f321_field_catalog USING 'LTXA1'
                                   'Operation Short text'(008).

ENDFORM.                    " f320_build_field_catalog
*&---------------------------------------------------------------------*
*&      Form  F311_FIELD_CATALOG
*&---------------------------------------------------------------------*
*       Prepare field catalog with output fields
*----------------------------------------------------------------------*
FORM f321_field_catalog USING p_fname   TYPE any
                              p_seltext TYPE any.

  DATA: wa_fieldcatalog TYPE slis_fieldcat_alv.

*appending parameters of a particular field of a fieldcatalog
  wa_fieldcatalog-tabname   = 'I_CHANGED_TLGROUPS'.
  wa_fieldcatalog-fieldname = p_fname.
  wa_fieldcatalog-seltext_m = p_seltext.

*appending parameters of a particular field to fieldcatalog
  APPEND wa_fieldcatalog TO i_fieldcatalog.

ENDFORM.                    " f321_field_catalog

*&---------------------------------------------------------------------*
*&      Form  f330_build_events
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM f330_build_events.

  DATA : wa_events TYPE slis_alv_event.

  CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
    EXPORTING
      i_list_type = 0
    IMPORTING
      et_events   = i_events.

  READ TABLE i_events WITH KEY name = 'TOP_OF_PAGE'
                                      INTO wa_events.

  wa_events-form = 'TOP_OF_PAGE'.

  MODIFY i_events
  FROM wa_events
  TRANSPORTING form
  WHERE name EQ wa_events-name.

ENDFORM.                    " f330_build_events

*---------------------------------------------------------------------*
*       FORM top_of_page                                              *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
FORM top_of_page.

  DATA : wa_header TYPE slis_listheader.

  wa_header-typ = 'H'.
  wa_header-info = 'Updated Task List Groups with changed Standard Text Keys'(009).
  APPEND wa_header TO i_header.

  CALL FUNCTION 'REUSE_ALV_COMMENTARY_WRITE'
    EXPORTING
      it_list_commentary = i_header.

ENDFORM.                    "top_of_page

*&---------------------------------------------------------------------*
*&      Form  f340_display_alv_report
*&---------------------------------------------------------------------*
*       Display ALV report
*----------------------------------------------------------------------*
FORM f340_display_alv_report .

  DATA: l_repid TYPE sy-repid.

  l_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program = l_repid
      it_fieldcat        = i_fieldcatalog
      it_events          = i_events
    TABLES
      t_outtab           = i_changed_tlgroups
    EXCEPTIONS
      program_error      = 1
      OTHERS             = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " f340_display_alv_report

*Text symbol text£º
*001:Task List Selection Criteria
*002:No Task List Groups have been updated
*003:Task List Group
*004:Group Counter
*005:TaskList Node Number
*006:Internal Counter
*007:Standard text key
*008:Operation Short text

*009:Updated Task List Groups with changed Standard Text Keys
*Selection text£º
*S_KTSCH:        Standard text key
*S_PLNAL:        Group Counter
*S_PLNNR:        Group
