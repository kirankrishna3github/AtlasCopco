*----------------------------------------------------------------------*
*   INCLUDE CMDC0001                                                   *
*----------------------------------------------------------------------*

*---------------------------------------------------------------------*
*       FORM CONDITIONS_SELECT                                        *
*---------------------------------------------------------------------*
FORM conditions_select.
  DATA: lv_subrc LIKE sy-subrc,
        lr_kotab TYPE REF TO data,
        lv_plain TYPE sdkondtab_long.

* prepare messages
  WRITE xt681-kotabnr TO msg_text-tabnum.
  IF act_kvewe EQ 'A'.
    WRITE '   A' TO msg_text-tabvew.
  ELSE.
    WRITE 'KOTE' TO msg_text-tabvew.
  ENDIF.
  WRITE text-045 TO msg_text-procac.
  PERFORM gui_message USING msg_text.

  FIELD-SYMBOLS: <kotab>  TYPE ANY.

* selection by KNUMH: preparation
  IF NOT r_knumh IS INITIAL.
    CREATE DATA lr_kotab TYPE (xt681-kotab).
    ASSIGN lr_kotab->* TO <kotab>.
  ENDIF.

  REFRESH xvake.
  subrc = 1.
  LOOP AT xt681a.
    vake_low-kappl = xt681a-kappl.
    vake_high-kappl = xt681a-kappl.
    IF r_knumh IS INITIAL.
      PERFORM access_using IN PROGRAM (access_program)
                      USING 'RECEIVE_DATA' con_report_name
                            vake_low vake_high
                            lv_subrc p_vkorg.
    ELSE.
*     selection by KNUMH
      SELECT * FROM (xt681-kotab) INTO <kotab> BYPASSING BUFFER
        WHERE kappl = xt681a-kappl
          AND kschl BETWEEN vake_low-kschl AND vake_high-kschl
          AND knumh BETWEEN vake_low-knumh AND vake_high-knumh.
*       fill XVAKE table...
        CLEAR xvake.
        xvake-kvewe   = xt681-kvewe.
        xvake-kotabnr = xt681-kotabnr.
*       ...fixed fields (dynamic move-corresponding)
        MOVE-CORRESPONDING <kotab> TO xvake.
*       ...variable fields
        MOVE <kotab> TO lv_plain.
        PERFORM get_vakey IN PROGRAM (access_program)
          USING    lv_plain
          CHANGING xvake-vakey.
        PERFORM get_vadat IN PROGRAM (access_program)
          USING    lv_plain
          CHANGING xvake-vadat.
        APPEND xvake.
      ENDSELECT.
      lv_subrc = sy-subrc.
    ENDIF.
    IF lv_subrc EQ 0.
      CLEAR subrc.
    ENDIF.
  ENDLOOP.
  SORT xvake BY kotabnr knumh.
ENDFORM.                    "CONDITIONS_SELECT

*----------------------------------------------------------------------*
*       Form  INITIALIZE
*----------------------------------------------------------------------*
FORM initialize.
  WRITE text-040 TO msg_text-tabtxt.
  WRITE text-041 TO msg_text-errtxt.
  WRITE num_error TO msg_text-errcnt.
  MOVE sy-linsz TO list_width.
  con_low-to = cl_abap_char_utilities=>minchar.
  con_high-to = cl_abap_char_utilities=>maxchar.
  TRANSLATE vake_low  USING con_low.
  TRANSLATE vake_high USING con_high.
  vake_low-datab  = con_dat_0. vake_low-datbi  = con_dat_0.
  vake_high-datab = con_dat_f. vake_high-datbi = con_dat_f.
  vake_low-kotabnr  = con_tab_0.
  vake_high-kotabnr = con_tab_9.

  IF kschl[] IS INITIAL.
    CLEAR r_kschl.
  ENDIF.
  IF NOT r_kschl IS INITIAL.
    READ TABLE kschl INDEX 1.
    vake_low-kschl  = kschl-low.
    IF NOT kschl-high IS INITIAL.
      vake_high-kschl = kschl-high.
    ELSE.
      vake_high-kschl = kschl-low.
    ENDIF.
  ENDIF.
  IF knumh[] IS INITIAL.
    CLEAR r_knumh.
  ENDIF.
  IF NOT r_knumh IS INITIAL.
    READ TABLE knumh INDEX 1.
    vake_low-knumh  = knumh-low.
    vake_high-knumh = knumh-high.
  ENDIF.

  CALL FUNCTION 'GET_ACCESSIBILITY_MODE'
    IMPORTING
      accessibility = gv_acc_flag.


ENDFORM.                    " INITIALIZE

*---------------------------------------------------------------------*
*       FORM RECEIVE_DATA                                             *
*---------------------------------------------------------------------*
FORM receive_data USING rd_vake LIKE vake
                        rd_subrc LIKE sy-subrc.
  rd_subrc = 4.
  xvake = rd_vake.
  APPEND xvake.
ENDFORM.                    "RECEIVE_DATA

*&---------------------------------------------------------------------*
*&      Form  feldauswahl
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM feldauswahl.
  LOOP AT SCREEN.
    IF screen-name(5) = 'KSCHL' AND r_kschl IS INITIAL.
      screen-input = 0.
      MODIFY SCREEN.
      CONTINUE.
    ENDIF.
    IF screen-name(5) = 'KNUMH' AND r_knumh IS INITIAL.
      screen-input = 0.
      MODIFY SCREEN.
      CONTINUE.
    ENDIF.

    IF screen-name(5) = 'KONPE' OR
      screen-name(5) = 'KONHE' OR
      screen-name(5) = 'SCHVA' OR
      screen-name(5) = 'KONAD' OR
      screen-name(5) = 'NUMMC' OR
      screen-name(5) = 'STFBZ' OR
      screen-name(5) = 'TICKA'. " ticka.
      screen-input = 0.
      MODIFY SCREEN.
      CONTINUE.
    ENDIF.


  ENDLOOP.
ENDFORM.                    "feldauswahl
