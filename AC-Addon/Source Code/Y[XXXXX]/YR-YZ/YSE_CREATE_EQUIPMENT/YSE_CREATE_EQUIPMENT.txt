************************************************************************
* Program ID        : YSE_CREATE_EQUIPMENT				     *
* Program Title     : Program title                                    *
* Author            : Anson Sun                                        *
* Date              : 21/12/2011                               	     *
* Change Request Number:2296                                           *
* Description       : The purpose of this program is to Mass load      *
*                     Equipment Master                                 *
*======================================================================*
* Copied From       : (Cloned Program)                        	     *
* Title             : (Program Title)                          	     *
* Other Related obj : (Object names)                 		           *
*======================================================================*
* Change History Log                                             	     *
*----------------------------------------------------------------------*
* Mod. no.|  Date    | Name           | Correction Number  | Change    *
*----------------------------------------------------------------------*
* MOD-001 |dd/mm/yyyy| xxxxxxxxxxxxxx | XXXXxxxxxx         |           *
*                                                                      *
* Description:                                                   	     *
*----------------------------------------------------------------------*
REPORT  yse_create_equipment MESSAGE-ID yse_cs_sd_msg.
*----------------------------------------------------------------------*
*       TABLES
*----------------------------------------------------------------------*
TABLES:
  iloa.                     "PM Object Location and Account Assignment
*----------------------------------------------------------------------*
*       TYPE-POOLS
*----------------------------------------------------------------------*
TYPE-POOLS slis.
*----------------------------------------------------------------------*
*       TYPE
*----------------------------------------------------------------------*
TYPES:
  BEGIN OF ty_equnr,
    material(18)   TYPE c,             "Material Number
    descript(40)   TYPE c,             "Description
    serialno(18)   TYPE c,             "Serial number
    acqdate(10)    TYPE c,             "Acquisition date
    start_from(10) TYPE c,             "Start-up date
    customer(10)   TYPE c,             "Customer
    name(40)       TYPE c,             "Name
  END   OF ty_equnr.
*----------------------------------------------------------------------*
*       DATA
*----------------------------------------------------------------------*
DATA:
  i_log         TYPE TABLE OF yse_create_equipment_log,
  i_fieldcat    TYPE slis_t_fieldcat_alv.
*----------------------------------------------------------------------*
*       SELECTION-SCREEN
*----------------------------------------------------------------------*
SELECT-OPTIONS:
  s_valid FOR sy-datum   NO-EXTENSION NO INTERVALS OBLIGATORY,
  s_tplnr FOR iloa-tplnr NO-EXTENSION NO INTERVALS OBLIGATORY,
  s_vkorg FOR iloa-vkorg NO-EXTENSION NO INTERVALS OBLIGATORY,
  s_vtweg FOR iloa-vtweg NO-EXTENSION NO INTERVALS OBLIGATORY,
  s_spart FOR iloa-spart NO-EXTENSION NO INTERVALS OBLIGATORY,
  s_swerk FOR iloa-swerk NO-EXTENSION NO INTERVALS OBLIGATORY.
PARAMETERS:
  p_file  TYPE rlgrap-filename OBLIGATORY DEFAULT 'c:\equipment.xls'.

*----------------------------------------------------------------------*
*       INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

* initial process
  PERFORM ini_process.
*----------------------------------------------------------------------*
*       AT SELECTION-SCREEN OUTPUT
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_file .
  CALL FUNCTION 'F4_FILENAME'
    IMPORTING
      file_name = p_file.
*----------------------------------------------------------------------*
*       AT SELECTION-SCREEN
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

* Authority check for Sales Organization
  PERFORM authority_check.
*----------------------------------------------------------------------*
*       START-OF-SELECTION
*----------------------------------------------------------------------*
START-OF-SELECTION.

* main process
  PERFORM main_process.
*----------------------------------------------------------------------*
*       END-OF-SELECTION
*----------------------------------------------------------------------*
END-OF-SELECTION.

* log process
  PERFORM display_log.
*&---------------------------------------------------------------------*
*&      Form  ini_process
*&---------------------------------------------------------------------*
*       initial process
*----------------------------------------------------------------------*
FORM ini_process .
  REFRESH:
    i_log,
    i_fieldcat.
ENDFORM.                    " ini_process
*&---------------------------------------------------------------------*
*&      Form  authority_check
*&---------------------------------------------------------------------*
*       Authority check for Sales Organization
*----------------------------------------------------------------------*
FORM authority_check .

* Authority check for Sales Organization
  AUTHORITY-CHECK OBJECT 'V_VBRK_VKO'
           ID 'VKORG' FIELD s_vkorg-low
           ID 'ACTVT' FIELD '03'.
  IF sy-subrc <> 0.
    MESSAGE e002.
  ENDIF.

ENDFORM.                    " authority_check
*&---------------------------------------------------------------------*
*&      Form  main_process
*&---------------------------------------------------------------------*
*       main process
*----------------------------------------------------------------------*
FORM main_process .

  DATA: li_file TYPE TABLE OF ty_equnr.
* Upload file
  PERFORM upload_file TABLES li_file.
* Update End Customer
  PERFORM create_equipment TABLES li_file.

ENDFORM.                    " main_process
*&---------------------------------------------------------------------*
*&      Form  upload_file
*&---------------------------------------------------------------------*
*       Upload file
*----------------------------------------------------------------------*
FORM upload_file TABLES i_file.

*  DATA: filename TYPE string.
*  filename = p_file.
*
*  CALL FUNCTION 'GUI_UPLOAD'
*    EXPORTING
*      filename                      = filename
**   FILETYPE                      = 'ASC'
*      has_field_separator           = 'X'
*    TABLES
*      data_tab                      = i_file
*   EXCEPTIONS
*     file_open_error               = 1
*     file_read_error               = 2
*     no_batch                      = 3
*     gui_refuse_filetransfer       = 4
*     invalid_type                  = 5
*     no_authority                  = 6
*     unknown_error                 = 7
*     bad_data_format               = 8
*     header_not_allowed            = 9
*     separator_not_allowed         = 10
*     header_too_long               = 11
*     unknown_dp_error              = 12
*     access_denied                 = 13
*     dp_out_of_memory              = 14
*     disk_full                     = 15
*     dp_timeout                    = 16
*     OTHERS                        = 17.
*  IF sy-subrc <> 0.
*    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
*           WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
*    EXIT.
*  ELSE.
*    DELETE i_file INDEX 1.
*  ENDIF.

  DATA: filename  TYPE rlgrap-filename,
        li_file   TYPE TABLE OF alsmex_tabline,
        lrec_file TYPE ty_equnr,
        l_index   TYPE i.
  FIELD-SYMBOLS: <fs_file> LIKE LINE OF li_file,
                 <fs_aux>  TYPE ANY.

  filename = p_file.
  CALL FUNCTION 'ALSM_EXCEL_TO_INTERNAL_TABLE'
    EXPORTING
        filename                = filename
        i_begin_col             = '1'
        i_begin_row             = '1'
        i_end_col               = '256'
        i_end_row               = '65536'
    TABLES
        intern                  = li_file
    EXCEPTIONS
        inconsistent_parameters = 1
        upload_ole              = 2
        OTHERS                  = 3.
  IF sy-subrc <> 0.
    MESSAGE s013 DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
  ELSE.
    DELETE li_file WHERE row EQ '0001'.

    SORT li_file BY row col.
    LOOP AT li_file ASSIGNING <fs_file>.
      MOVE <fs_file>-col TO l_index.
      ASSIGN COMPONENT l_index OF STRUCTURE lrec_file TO <fs_aux>.
      MOVE <fs_file>-value TO <fs_aux>.
      AT END OF row.
        APPEND lrec_file TO i_file.
        CLEAR  lrec_file.
      ENDAT.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " upload_file
*&---------------------------------------------------------------------*
*&      Form  create_equipment
*&---------------------------------------------------------------------*
*       Create Equipment
*----------------------------------------------------------------------*
FORM create_equipment TABLES i_file.

  DATA: lrec_file     TYPE ty_equnr.
  DATA: l_matnr       TYPE matnr,
        l_gernr       TYPE gernr,
        l_kunnr       TYPE kunnr,
        l_acqd        TYPE sy-datum,
        l_start       TYPE sy-datum.
  DATA: data_general  TYPE bapi_itob,
        data_specific TYPE bapi_itob_eq_only,
        data_install  TYPE bapi_itob_eq_install,
        equipment     TYPE bapi_itob_parms-equipment,
        return        TYPE bapiret2.

  IF i_file[] IS INITIAL.
    MESSAGE s014.
    LEAVE LIST-PROCESSING.
  ENDIF.

  LOOP AT i_file INTO lrec_file.
*   Internal Material
    PERFORM convert_matnr_input USING lrec_file-material
                             CHANGING l_matnr.
*   Internal Serial number
    PERFORM convert_gernr_input USING lrec_file-serialno
                             CHANGING l_gernr.
*   Internal Date
*   Acquistion date
    PERFORM convert_datum_input USING lrec_file-acqdate
                             CHANGING l_acqd.
*   Start-up date
    PERFORM convert_datum_input USING lrec_file-start_from
                             CHANGING l_start.
*   Internal Customer
    PERFORM convert_alpha_input USING lrec_file-customer
                             CHANGING l_kunnr.

****Set Specific
    data_specific-material = l_matnr.
    data_specific-serialno = l_gernr.
    data_specific-equicatgry = 'Z'.

****Set General
*   Description
    data_general-descript = lrec_file-descript.
*   Object type
    data_general-objecttype = 'EQUIPMENT'.
*   Acquisition date
    data_general-acqdate    = l_acqd.
*   Start-up date
    data_general-start_from = l_start.
*   Construction type material of the object
    data_general-consttype = l_matnr.
*   Sales Org
    data_general-sales_org  = s_vkorg-low.
    data_general-distr_chan = s_vtweg-low.
    data_general-division   = s_spart-low.
*   MaintPlant
    data_general-maintplant = s_swerk-low.

****Set Installation
    data_install-funcloc    = s_tplnr-low.

*   BAPI: Create Equipment
    CALL FUNCTION 'BAPI_EQUI_CREATE'
      EXPORTING
        data_general  = data_general
        data_specific = data_specific
        valid_date    = s_valid-low
        data_install  = data_install
      IMPORTING
        equipment     = equipment
        return        = return.

    IF return-type = 'E' OR return-type = 'A' OR return-type = 'X'.
    ELSE.
      COMMIT WORK AND WAIT.

*     Update Partner
      PERFORM update_partner USING equipment
                                   l_kunnr.
    ENDIF.
    PERFORM set_log USING data_specific-material
                          data_specific-serialno
                          data_specific-equicatgry
                          equipment
                          return-type
                          return-message.
    CLEAR: data_general, data_specific, data_install, equipment, return.
  ENDLOOP.

ENDFORM.                    " create_equipment
*&---------------------------------------------------------------------*
*&      Form  update_partner
*&---------------------------------------------------------------------*
*       Update Partner
*----------------------------------------------------------------------*
FORM update_partner  USING equipment TYPE equnr
                           kunnr     TYPE kunnr.

  DATA: l_objnr TYPE ihpa-objnr.
  DATA: lrec_xihpa TYPE ihpavb,
        li_xihpa   TYPE TABLE OF ihpavb.

  CHECK equipment IS NOT INITIAL
    AND kunnr     IS NOT INITIAL.
* Set Object number
  CONCATENATE 'IE' equipment INTO l_objnr.
  lrec_xihpa-mandt   = sy-mandt.
  lrec_xihpa-objnr   = l_objnr.
  lrec_xihpa-obtyp   = 'IEQ'.
  lrec_xihpa-erdat   = sy-datum.
  lrec_xihpa-erzeit  = sy-uzeit.
  lrec_xihpa-ernam   = sy-uname.
  lrec_xihpa-aedat   = sy-datum.
  lrec_xihpa-aezeit  = sy-uzeit.
  lrec_xihpa-aenam   = sy-uname.
* Insert new partner
  lrec_xihpa-updkz   = 'I'.

* Sold-to
  lrec_xihpa-parvw   = 'AG'.
  lrec_xihpa-counter = lrec_xihpa-counter + 1.
  lrec_xihpa-parnr   = kunnr.
  APPEND lrec_xihpa TO li_xihpa.

* Bill-to
  lrec_xihpa-parvw   = 'RE'.
  lrec_xihpa-counter = lrec_xihpa-counter + 1.
  lrec_xihpa-parnr   = kunnr.
  APPEND lrec_xihpa TO li_xihpa.

* Payer-to
  lrec_xihpa-parvw   = 'RG'.
  lrec_xihpa-counter = lrec_xihpa-counter + 1.
  lrec_xihpa-parnr   = kunnr.
  APPEND lrec_xihpa TO li_xihpa.

* Ship-to
  lrec_xihpa-parvw   = 'WE'.
  lrec_xihpa-counter = lrec_xihpa-counter + 1.
  lrec_xihpa-parnr   = kunnr.
  APPEND lrec_xihpa TO li_xihpa.

* Partner Update
  CALL FUNCTION 'PM_PARTNER_UPDATE'
    TABLES
      fxihpa = li_xihpa.

ENDFORM.                    " update_partner
*&---------------------------------------------------------------------*
*&      Form  display_log
*&---------------------------------------------------------------------*
*       display log
*----------------------------------------------------------------------*
FORM display_log .
  DATA: lrec_layout TYPE slis_layout_alv.

* edit alv fieldcat
  PERFORM edit_alv_fieldcat.

  lrec_layout-colwidth_optimize = 'X'.
  lrec_layout-zebra             = 'X'.
  CALL FUNCTION 'REUSE_ALV_LIST_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'SET_STATUS_10001'
      i_callback_user_command  = 'USER_COMMAND'
      is_layout                = lrec_layout
      it_fieldcat              = i_fieldcat
    TABLES
      t_outtab                 = i_log
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    EXIT.
  ELSE.
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " display_log
*&---------------------------------------------------------------------*
*&      Form  edit_alv_fieldcat
*&---------------------------------------------------------------------*
*       edit alv fieldcat
*----------------------------------------------------------------------*
FORM edit_alv_fieldcat.

  DATA: lrec_fieldcat TYPE slis_fieldcat_alv.
  REFRESH: i_fieldcat.

  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
    EXPORTING
      i_structure_name       = 'YSE_CREATE_EQUIPMENT_LOG'
      i_bypassing_buffer     = 'X'
    CHANGING
      ct_fieldcat            = i_fieldcat
    EXCEPTIONS
      inconsistent_interface = 1
      program_error          = 2
      OTHERS                 = 3.
  IF sy-subrc <> 0.
    EXIT.
  ENDIF.

ENDFORM.                    " edit_alv_fieldcat
*&---------------------------------------------------------------------*
*&      Form  set_status_10001
*&---------------------------------------------------------------------*
*       set screen status
*----------------------------------------------------------------------*
FORM set_status_10001 USING i_extab TYPE slis_t_extab.

  SET PF-STATUS '10001'.
  SET TITLEBAR  '10001'.

ENDFORM.                               " set_status_10001
*&---------------------------------------------------------------------*
*&      Form  user_command
*&---------------------------------------------------------------------*
*       user command
*----------------------------------------------------------------------*
FORM user_command  USING    i_ucomm    LIKE sy-ucomm
                            i_selfield TYPE slis_selfield.

  CASE i_ucomm.
    WHEN '&IC1'.
      PERFORM display_process USING i_selfield.
  ENDCASE.

ENDFORM.                               " user_command
*&---------------------------------------------------------------------*
*&      Form  display_process
*&---------------------------------------------------------------------*
*       display process
*----------------------------------------------------------------------*
FORM display_process  USING    selfield TYPE slis_selfield.

  DATA lrec_log TYPE yse_create_equipment_log.
  READ TABLE i_log INTO lrec_log INDEX selfield-tabindex.
  IF sy-subrc = 0.
    CASE selfield-fieldname.
      WHEN 'EQUNR'.
        SET PARAMETER ID 'EQN' FIELD lrec_log-equnr.
        CALL TRANSACTION 'IE03' AND SKIP FIRST SCREEN.
    ENDCASE.
  ENDIF.

ENDFORM.                    " display_process
*&---------------------------------------------------------------------*
*&      Form  SET_LOG
*&---------------------------------------------------------------------*
*       Set Log
*----------------------------------------------------------------------*
FORM set_log  USING matnr    TYPE matnr
                    gernr    TYPE gernr
                    eqtyp    TYPE eqtyp
                    equnr    TYPE equnr
                    msg_type TYPE bapi_mtype
                    message  TYPE bapi_msg.

  DATA: lrec_log   TYPE yse_create_equipment_log.
  lrec_log-matnr    = matnr.
  lrec_log-gernr    = gernr.
  lrec_log-eqtyp    = eqtyp.
  lrec_log-equnr    = equnr.
  lrec_log-msgty    = msg_type.
  lrec_log-msgtx    = message.
  APPEND lrec_log TO i_log.

ENDFORM.                    " SET_LOG
*&---------------------------------------------------------------------*
*&      Form  CONVERT_MATNR_INPUT
*&---------------------------------------------------------------------*
*       Internal Material
*----------------------------------------------------------------------*
FORM convert_matnr_input  USING    input
                          CHANGING output.
  CLEAR output.
  CALL FUNCTION 'CONVERSION_EXIT_MATN1_INPUT'
    EXPORTING
      input        = input
    IMPORTING
      output       = output
    EXCEPTIONS
      length_error = 1
      OTHERS       = 2.
  IF sy-subrc <> 0.
    EXIT.
  ENDIF.
ENDFORM.                    " CONVERT_MATNR_INPUT
*&---------------------------------------------------------------------*
*&      Form  CONVERT_GERNR_INPUT
*&---------------------------------------------------------------------*
*       Internal Serial
*----------------------------------------------------------------------*
FORM convert_gernr_input  USING    input
                          CHANGING output.
  CLEAR output.
  CALL FUNCTION 'CONVERSION_EXIT_GERNR_INPUT'
    EXPORTING
      input         = input
    IMPORTING
      output        = output.

ENDFORM.                    " CONVERT_GERNR_INPUT
*&---------------------------------------------------------------------*
*&      Form  CONVERT_ALPHA_INPUT
*&---------------------------------------------------------------------*
*       Internal Alpha
*----------------------------------------------------------------------*
FORM convert_alpha_input  USING    input
                          CHANGING output.
  CLEAR output.
  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
    EXPORTING
      input  = input
    IMPORTING
      output = output.
ENDFORM.                    " CONVERT_ALPHA_INPUT
*&---------------------------------------------------------------------*
*&      Form  CONVERT_DATUM_INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_LREC_FILE_ACQDATE  text
*      -->P_L_ACQD  text
*----------------------------------------------------------------------*
FORM convert_datum_input  USING    value(input)
                          CHANGING output.
  CLEAR output.
  IF input IS NOT INITIAL.
    REPLACE ALL OCCURRENCES OF '-' IN input WITH space.
    CONDENSE input NO-GAPS.
    output = input.
  ELSE.
    output = '00000000'.
  ENDIF.
*  CALL FUNCTION 'CONVERT_DATE_TO_INTERNAL'
*    EXPORTING
*      date_external                  = input
*      accept_initial_date            = '-'
*    IMPORTING
*      date_internal                  = output
*    EXCEPTIONS
*      date_external_is_invalid       = 1
*      OTHERS                         = 2.
*  IF sy-subrc <> 0.
*    EXIT.
*  ENDIF.
ENDFORM.                    " CONVERT_DATUM_INPUT

*Selection text£º
*P_FILE:        Input file
*S_SPART:        Division
*S_SWERK:        MaintPlant
*S_TPLNR:        Functional loc.
*S_VALID:        Valid On
*S_VKORG:        Sales Org.
*S_VTWEG:        Distr. Channel
