***********************************************************************************
* Program ID        : yrd_receipt_po
* Author            : Yang Lei
* Date              : 11/05/2017
***********************************************************************************
* Description       :
***********************************************************************************

REPORT  yrd_receipt_po NO STANDARD PAGE HEADING LINE-SIZE 255.


TYPES: BEGIN OF ty_upload,
         v_text(250)  TYPE c,            " FILE UPLOAD TEXT
       END OF ty_upload.
DATA: gt_upload TYPE TABLE OF ty_upload.
DATA: gt_upload2 TYPE TABLE OF ty_upload.

TYPES: BEGIN OF ty_item,
         matnr TYPE ekpo-matnr,
         bwtar TYPE goitem-bwtar,
         gtdno TYPE char30,
         contr TYPE char30,
       END OF ty_item,

       BEGIN OF ty_item2,
         ebeln TYPE ekko-ebeln,
         werks TYPE ekpo-werks,
         bldat TYPE rbkp-bldat,
         budat TYPE rbkp-budat,
         charg TYPE goitem-charg,
         bwtar TYPE goitem-bwtar,
         block TYPE char30,
         ebelp TYPE ekpo-ebelp,
         serial TYPE goserial-serialno,
       END OF ty_item2,

       BEGIN OF ty_down,
         ebeln TYPE ekko-ebeln,
         werks TYPE ekpo-werks,
         bldat TYPE rbkp-bldat,
         budat TYPE rbkp-budat,
         charg TYPE goitem-charg,
         bwtar TYPE goitem-bwtar,
         block TYPE char30,
         ebelp TYPE ekpo-ebelp,
         ermsg TYPE char100,
       END OF ty_down,

       BEGIN OF ty_ekpo,
         ebeln TYPE ekpo-ebeln,
         ebelp TYPE ekpo-ebelp,
         werks TYPE ekpo-werks,
         matnr TYPE ekpo-matnr,
         menge TYPE ekpo-menge,
       END OF ty_ekpo,

       BEGIN OF ty_err,
         serno TYPE char5,
         msg   TYPE char100,
       END OF ty_err.
DATA: gt_item_list  TYPE TABLE OF ty_item.
DATA: gt_item_list2 TYPE TABLE OF ty_item2.
DATA: gt_ekpo       TYPE STANDARD TABLE OF ty_ekpo.

DATA: gv_filename TYPE string.
CONSTANTS: c_filetype(10)   TYPE c VALUE 'ASC',     " FILE TYPE
           c_blanc          TYPE c VALUE ' ',
    con_tab  TYPE c VALUE cl_abap_char_utilities=>horizontal_tab.


PARAMETERS: p_file TYPE ibipparms-path LOWER CASE OBLIGATORY.
PARAMETERS: p_fil2 TYPE ibipparms-path LOWER CASE OBLIGATORY.

*.................. Selection screen validations...................... *

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_file.
  CALL FUNCTION 'F4_FILENAME'
    IMPORTING
      file_name = p_file.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_fil2.
  CALL FUNCTION 'F4_FILENAME'
    IMPORTING
      file_name = p_fil2.


START-OF-SELECTION.
  PERFORM get_item_list.
  PERFORM create_po.

TOP-OF-PAGE.
  WRITE:
    /001 'PO Number',
     015 'Message'.

*&---------------------------------------------------------------------*
*&      Form  get_item_list
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM get_item_list .
  DATA: ls_item    TYPE ty_item,
        ls_item2   TYPE ty_item2,
        lv_string  TYPE ty_upload,
        lt_temp    TYPE STANDARD TABLE OF ty_item2,
        lv_ebelp   TYPE char5.

  "Upload the item list.
  gv_filename = p_file.
  PERFORM get_from_pres IN PROGRAM yam_common_routines
                                    TABLES  gt_upload
                                    USING   gv_filename
                                            c_filetype
                                            c_blanc.

  "Upload the item list.
  gv_filename = p_fil2.
  PERFORM get_from_pres IN PROGRAM yam_common_routines
                                    TABLES  gt_upload2
                                    USING   gv_filename
                                            c_filetype
                                            c_blanc.

  LOOP AT gt_upload INTO lv_string.
    SPLIT lv_string AT con_tab
     INTO ls_item-matnr
          ls_item-bwtar
          ls_item-gtdno
          ls_item-contr.

    CALL FUNCTION 'CONVERSION_EXIT_MATN1_INPUT'
      EXPORTING
        INPUT         = ls_item-matnr
      IMPORTING
        OUTPUT        = ls_item-matnr.

    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        INPUT         = ls_item-bwtar
      IMPORTING
        OUTPUT        = ls_item-bwtar.

    APPEND ls_item TO gt_item_list.
  ENDLOOP.

  SORT gt_item_list
    BY matnr ASCENDING
       bwtar ASCENDING.

  LOOP AT gt_upload2 INTO lv_string.
    SPLIT lv_string AT con_tab
     INTO ls_item2-ebeln
          ls_item2-werks
          ls_item2-bldat
          ls_item2-budat
          ls_item2-charg
          ls_item2-bwtar
          ls_item2-block
          lv_ebelp
          ls_item2-serial.

    ls_item2-ebelp = lv_ebelp.

    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        INPUT         = ls_item2-charg
      IMPORTING
        OUTPUT        = ls_item2-charg.

    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        INPUT         = ls_item2-bwtar
      IMPORTING
        OUTPUT        = ls_item2-bwtar.

    APPEND ls_item2 TO gt_item_list2.
  ENDLOOP.

  SORT gt_item_list2
    BY ebeln ASCENDING
       ebelp ASCENDING.

  IF gt_item_list2 IS NOT INITIAL.
    lt_temp = gt_item_list2.
    SORT lt_temp
      BY ebeln ASCENDING
         ebelp ASCENDING.
    DELETE ADJACENT DUPLICATES FROM lt_temp
                          COMPARING ebeln
                                    ebelp.

    SELECT ebeln
           ebelp
           werks
           matnr
           menge
      FROM ekpo
      INTO TABLE gt_ekpo
       FOR ALL ENTRIES IN lt_temp
     WHERE ebeln = lt_temp-ebeln
       AND ebelp = lt_temp-ebelp.
    SORT gt_ekpo
      BY ebeln ASCENDING
         ebelp ASCENDING.
  ENDIF.

ENDFORM.                    " get_item_list
*&---------------------------------------------------------------------*
*&      Form  create_po
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM create_po .
  DATA: ls_item2  TYPE ty_item2,
        ls_temp   TYPE ty_item2,
        ls_temp2  TYPE ty_item2,
        ls_ekpo   TYPE ty_ekpo,
        ls_item   TYPE ty_item,
        ls_header TYPE bapi2017_gm_head_01,
        lt_item   TYPE TABLE OF bapi2017_gm_item_create WITH HEADER LINE,
        lt_serial TYPE TABLE OF bapi2017_gm_serialnumber WITH HEADER LINE,
        lt_return TYPE TABLE OF bapiret2 WITH HEADER LINE,
        lv_msg    TYPE char100,
        lt_down   TYPE STANDARD TABLE OF ty_down,
        ls_down   TYPE ty_down,
        lv_objkey TYPE BAPI1003_KEY-OBJECT,
        lv_objtbl TYPE BAPI1003_KEY-OBJECTTABLE,
        lv_calnum TYPE BAPI1003_KEY-CLASSNUM,
        lv_clstyp TYPE BAPI1003_KEY-CLASSTYPE,
        lt_num    TYPE STANDARD TABLE OF BAPI1003_ALLOC_VALUES_NUM WITH HEADER LINE,
        lt_char   TYPE STANDARD TABLE OF BAPI1003_ALLOC_VALUES_CHAR WITH HEADER LINE,
        lt_curr   TYPE STANDARD TABLE OF BAPI1003_ALLOC_VALUES_CURR WITH HEADER LINE,
        lt_ret2   TYPE STANDARD TABLE OF BAPIRET2 WITH HEADER LINE,
        lt_temp   TYPE STANDARD TABLE OF ty_item2,
        lv_len    TYPE i,
        lv_path   TYPE string,
        lt_string TYPE stringtab,
        ls_string LIKE LINE OF lt_string,
        lv_docno  TYPE bapi2017_gm_head_ret-mat_doc,
        lv_ebelp  TYPE i.

  LOOP AT gt_item_list2 INTO ls_temp.
    ls_item2 = ls_temp.
    APPEND ls_item2 TO lt_temp.

    CLEAR:
      ls_ekpo,
      ls_item.

    READ TABLE gt_ekpo INTO ls_ekpo
          WITH KEY ebeln = ls_item2-ebeln
                   ebelp = ls_item2-ebelp
          BINARY SEARCH.

    lt_item-material  = ls_ekpo-matnr.
    lt_item-plant     = ls_item2-werks.
    lt_item-batch     = ls_item2-charg.
    lt_item-move_type = '101'.
    lt_item-entry_qnt = ls_ekpo-menge.
    lt_item-po_number = ls_item2-ebeln.
    lt_item-po_item   = ls_item2-ebelp.
    lt_item-mvt_ind   = 'B'.
    APPEND lt_item.

    lv_ebelp = ls_item2-ebelp.
    lt_serial-matdoc_itm = lv_ebelp.
    lt_serial-serialno = ls_item2-serial.
    APPEND lt_serial.

    AT END OF ebeln.
      ls_header-pstng_date = ls_item2-budat.
      ls_header-doc_date   = ls_item2-bldat.

      CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
        EXPORTING
          goodsmvt_header               = ls_header
          goodsmvt_code                 = '01'
        IMPORTING
          materialdocument              = lv_docno
        TABLES
          goodsmvt_item                 = lt_item[]
          goodsmvt_serialnumber         = lt_serial[]
          return                        = lt_return[].
      CALL FUNCTION 'DEQUEUE_ALL'.
      LOOP AT lt_return WHERE TYPE = 'E'.
        EXIT.
      ENDLOOP.
      IF sy-subrc <> 0.
        CONCATENATE 'Material document'
                    lv_docno
                    'posted'
               INTO lv_msg
          SEPARATED BY space.

        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.

        LOOP AT lt_temp INTO ls_temp2.
          READ TABLE gt_ekpo INTO ls_ekpo
                WITH KEY ebeln = ls_temp2-ebeln
                         ebelp = ls_temp2-ebelp
                BINARY SEARCH.
          READ TABLE gt_item_list INTO ls_item
                WITH KEY matnr = ls_ekpo-matnr
                         bwtar = ls_temp2-bwtar
                BINARY SEARCH.

          CONCATENATE ls_ekpo-matnr
                      ls_temp2-werks
                      ls_temp2-charg
                 INTO lv_objkey.

          lv_objtbl = 'MCHA'.
          lv_calnum = 'ZRU_BATCH'.
          lv_clstyp = '022'.

          CLEAR:
            lt_num[],
            lt_char[],
            lt_curr[],
            lt_ret2[].

          CALL FUNCTION 'BAPI_OBJCL_GETDETAIL'
            EXPORTING
              OBJECTKEY              = lv_objkey
              OBJECTTABLE            = lv_objtbl
              CLASSNUM               = lv_calnum
              CLASSTYPE              = lv_clstyp
            TABLES
              ALLOCVALUESNUM         = lt_num[]
              ALLOCVALUESCHAR        = lt_char[]
              ALLOCVALUESCURR        = lt_curr[]
              RETURN                 = lt_ret2[]
                    .

          READ TABLE lt_char WITH KEY CHARACT = 'ZRU_BLOCK_GM'.
          IF SY-SUBRC = 0.
            lt_char-VALUE_CHAR = ls_temp2-block.
            lt_char-VALUE_NEUTRAL = ls_temp2-block.
            MODIFY lt_char INDEX sy-tabix TRANSPORTING VALUE_CHAR VALUE_NEUTRAL.
          ELSE.
            lt_char-CHARACT = 'ZRU_BLOCK_GM'.
            lt_char-VALUE_CHAR = ls_temp2-block.
            lt_char-VALUE_NEUTRAL = ls_temp2-block.
            APPEND lt_char.
          ENDIF.

          READ TABLE lt_char WITH KEY CHARACT = 'ZRU_COUNTRY1'.
          IF SY-SUBRC = 0.
            lt_char-VALUE_CHAR = ls_item-contr.
            lt_char-VALUE_NEUTRAL = ls_item-contr.
            MODIFY lt_char INDEX sy-tabix TRANSPORTING VALUE_CHAR VALUE_NEUTRAL.
          ELSE.
            lt_char-CHARACT = 'ZRU_COUNTRY1'.
            lt_char-VALUE_CHAR = ls_item-contr.
            lt_char-VALUE_NEUTRAL = ls_item-contr.
            APPEND lt_char.
          ENDIF.

          READ TABLE lt_char WITH KEY CHARACT = 'ZRU_GDTNUMBER'.
          IF SY-SUBRC = 0.
            lt_char-VALUE_CHAR = ls_item-gtdno.
            lt_char-VALUE_NEUTRAL = ls_item-gtdno.
            MODIFY lt_char INDEX sy-tabix TRANSPORTING VALUE_CHAR VALUE_NEUTRAL.
          ELSE.
            lt_char-CHARACT = 'ZRU_GDTNUMBER'.
            lt_char-VALUE_CHAR = ls_item-gtdno.
            lt_char-VALUE_NEUTRAL = ls_item-gtdno.
            APPEND lt_char.
          ENDIF.

          CLEAR lt_ret2.
          CALL FUNCTION 'BAPI_OBJCL_CHANGE'
            EXPORTING
              OBJECTKEY                = lv_objkey
              OBJECTTABLE              = lv_objtbl
              CLASSNUM                 = lv_calnum
              CLASSTYPE                = lv_clstyp
            TABLES
              ALLOCVALUESNUMNEW        = lt_num
              ALLOCVALUESCHARNEW       = lt_char
              ALLOCVALUESCURRNEW       = lt_curr
              RETURN                   = lt_ret2.

          CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.
          CALL FUNCTION 'DEQUEUE_ALL'.

        ENDLOOP.

        WRITE:
          /001 ls_item2-ebeln,
           015 lv_msg.

      ELSE.
        CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.

        MESSAGE ID lt_return-ID
              TYPE lt_return-TYPE
            NUMBER lt_return-NUMBER
              WITH lt_return-MESSAGE_V1
                   lt_return-MESSAGE_V2
                   lt_return-MESSAGE_V3
                   lt_return-MESSAGE_V4
              INTO lv_msg.

        LOOP AT lt_temp INTO ls_temp2.
          MOVE-CORRESPONDING ls_temp2 to ls_down.
          ls_down-ermsg = lv_msg.
          APPEND ls_down TO lt_down.
        ENDLOOP.

        WRITE:
          /001 ls_item2-ebeln,
           015 lv_msg.
      ENDIF.

      CLEAR:
        lt_item[],
        lt_serial[],
        lt_return[],
        lt_temp.
    ENDAT.
  ENDLOOP.

  CHECK lt_down IS NOT INITIAL.

  lv_len = strlen( p_file ).
  lv_len = lv_len - 4.
  lv_path = p_file+0(lv_len).

  CONCATENATE lv_path
              '_err.txt'
         INTO lv_path.

  LOOP AT lt_down INTO ls_down.
    CONCATENATE ls_down-ebeln
                ls_down-werks
                ls_down-bldat
                ls_down-budat
                ls_down-charg
                ls_down-bwtar
                ls_down-block
                ls_down-ebelp
                ls_down-ermsg
           INTO ls_string
           SEPARATED BY con_tab.
    APPEND ls_string TO lt_string.
  ENDLOOP.

  CALL FUNCTION 'GUI_DOWNLOAD'
    EXPORTING
      filename                        = lv_path
    TABLES
      data_tab                        = lt_string
    EXCEPTIONS
      file_write_error                = 1
      no_batch                        = 2
      gui_refuse_filetransfer         = 3
      invalid_type                    = 4
      no_authority                    = 5
      unknown_error                   = 6
      header_not_allowed              = 7
      separator_not_allowed           = 8
      filesize_not_allowed            = 9
      header_too_long                 = 10
      dp_error_create                 = 11
      dp_error_send                   = 12
      dp_error_write                  = 13
      unknown_dp_error                = 14
      access_denied                   = 15
      dp_out_of_memory                = 16
      disk_full                       = 17
      dp_timeout                      = 18
      file_not_found                  = 19
      dataprovider_exception          = 20
      control_flush_error             = 21
      OTHERS                          = 22
            .
  IF sy-subrc <> 0.
     MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
             WITH SY-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " create_po

*Text symbol text£º
*BK1:Header Information
*E00:Error opening dataset, return code:
*I01:Session name
*I02:Open session
*I03:Insert transaction
*I04:Close Session
*I05:Return code =
*I06:Error session created
*S01:Session name
*S02:User
*S03:Keep session
*S04:Lock date
*S05:Processing Mode
*S06:Update Mode
*S07:Generate session
*S08:Call transaction
*S09:Error sessn
*S10:Nodata indicator

*S11:Short log
*Selection text£º
*P_FIL2:        Receipt information
*P_FILE:        GTD information
