*&---------------------------------------------------------------------*
*& Report  YSE_FI_REV_POST_AUTOM_ACCNT
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& Reverse Postings (for automatic accounts also)                      *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 30.03.2012
*  Change Request Number : CR2012
*  Transport request Nr. : CD1K971067
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME           | CORRECTION NR. | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 00.00.2012 |                | CD1K......     | CR....      *
*         |                                                            *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_fi_rev_post_autom_accnt.

TABLES: bkpf,
        skb1,
        bseg.

DATA: BEGIN OF it_buk  OCCURS 0,
        bukrs   TYPE bukrs,
      END OF it_buk.

DATA: BEGIN OF it_bkpf  OCCURS 0,
        bukrs   TYPE bukrs,
        belnr   TYPE belnr_d,
        gjahr   TYPE gjahr,
      END OF it_bkpf.

DATA: BEGIN OF it_bseg  OCCURS 0,
        bukrs   TYPE bukrs,
        hkont   TYPE hkont,
      END OF it_bseg.

DATA: BEGIN OF it_skb1  OCCURS 0,
        bukrs   TYPE bukrs,
        saknr   TYPE saknr,
        xintb   TYPE xintb,
      END OF it_skb1.

DATA: i_bdcdata LIKE bdcdata  OCCURS 0  WITH HEADER LINE,
      struct_bdcdata TYPE bdcdata,
      it_err    LIKE bdcmsgcoll  OCCURS 0  WITH HEADER LINE.

DATA: gv_mode(1)     TYPE c      VALUE 'N'.

*----------------------------------------------------------------------*
* Selection criteria and parameters                                    *
*----------------------------------------------------------------------*
SELECT-OPTIONS: s_bukrs FOR bkpf-bukrs OBLIGATORY
                                       NO-EXTENSION
                                       NO INTERVALS,
                s_belnr FOR bkpf-belnr,
                s_gjahr FOR bkpf-gjahr,
                s_rldnr FOR bkpf-rldnr OBLIGATORY
                                       NO-EXTENSION
                                       NO INTERVALS
                                       MATCHCODE OBJECT fagl_rldnr
                                       MEMORY ID fagl_flex.
SELECTION-SCREEN SKIP.
SELECTION-SCREEN BEGIN OF BLOCK gsel WITH FRAME TITLE text-s01.
SELECT-OPTIONS: s_blart FOR bkpf-blart,
                s_budat FOR bkpf-budat,
                s_cpudt FOR bkpf-cpudt,
                s_xblnr FOR bkpf-xblnr,
                s_awtyp FOR bkpf-awtyp,
                s_awkey FOR bkpf-awkey,
                s_awsys FOR bkpf-awsys.
SELECTION-SCREEN END OF BLOCK gsel.

SELECTION-SCREEN BEGIN OF BLOCK revp WITH FRAME TITLE text-s02.
PARAMETERS: p_stogrd  LIKE bkpf-stgrd MEMORY ID fsg OBLIGATORY,
            p_stodat  LIKE bsis-budat  DEFAULT sy-datum,
            p_monat   LIKE bsis-monat,
            p_test    LIKE rfpdo1-allgtest DEFAULT 'X'.
SELECTION-SCREEN END OF BLOCK revp.


*----------------------------------------------------------------------*
* On the selection screen                                              *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON s_bukrs.

  PERFORM  check_authorization.


*----------------------------------------------------------------------*
* Main program                                                         *
*----------------------------------------------------------------------*

INITIALIZATION.

* Propose last day of the month
  CALL FUNCTION 'DATE_GET_MONTH_LASTDAY'
    EXPORTING
      i_date = sy-datum
    IMPORTING
      e_date = p_stodat.

** Propose ledger
*  SELECT SINGLE rldnr INTO s_rldnr-low
*         FROM t881
*         WHERE tab      = 'FAGLFLEXT'
*           AND xleading = 'X'.
*  s_rldnr-option  = 'EQ'.
*  s_rldnr-sign    = 'I'.
*  APPEND s_rldnr.


*----------------------------------------------------------------------*
START-OF-SELECTION.

* Statistics for transaction / program
  CALL METHOD ycl_statistics=>record_transaction.

* Select data
  PERFORM select_data.
* Check anything selected
  IF it_bseg[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No documents selected'(e01).
    RETURN.
  ENDIF.

* Open automatic accounts
  IF p_test IS INITIAL.
    PERFORM open_accounts.
  ENDIF.

* Process data
  PERFORM process_data.

* Close automatic accounts
  IF p_test IS INITIAL.
    PERFORM close_accounts.
  ENDIF.


*----------------------------------------------------------------------*
* Forms                                                                *
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  SELECT bukrs FROM t001
               INTO TABLE it_buk
               WHERE bukrs IN s_bukrs.

  LOOP AT it_buk.
    AUTHORITY-CHECK OBJECT 'F_BKPF_BUK'
             ID 'ACTVT' DUMMY
             ID 'BUKRS' FIELD it_buk-bukrs.

    IF sy-subrc = 4.
*     No authorisation to display data
      MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH it_buk-bukrs.
      EXIT.
    ELSEIF sy-subrc <> 0.
*     Error checking authorization.
      MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
      EXIT.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Select documents
  SELECT bukrs belnr gjahr
         INTO TABLE it_bkpf
         FROM bkpf
         WHERE bukrs IN s_bukrs
           AND belnr IN s_belnr
           AND gjahr IN s_gjahr
*           AND rldnr IN s_rldnr
           AND blart IN s_blart
           AND budat IN s_budat
           AND cpudt IN s_cpudt
           AND xblnr IN s_xblnr
           AND awtyp IN s_awtyp
           AND awkey IN s_awkey
           AND awsys IN s_awsys.

  CHECK NOT it_bkpf[] IS INITIAL.

* Select accounts
  SELECT bukrs hkont
         INTO TABLE it_bseg
         FROM bseg
         FOR ALL ENTRIES IN it_bkpf
         WHERE bukrs = it_bkpf-bukrs
           AND belnr = it_bkpf-belnr
           AND gjahr = it_bkpf-gjahr.

  CHECK NOT it_bseg[] IS INITIAL.

  SORT it_bseg.
  DELETE ADJACENT DUPLICATES FROM it_bseg.

  SELECT bukrs saknr xintb
         INTO TABLE it_skb1
         FROM skb1
         FOR ALL ENTRIES IN it_bseg
         WHERE bukrs = it_bseg-bukrs
           AND saknr = it_bseg-hkont
           AND xintb = 'X'.

  SORT it_skb1.
  DELETE ADJACENT DUPLICATES FROM it_skb1.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  OPEN_ACCOUNTS
*&---------------------------------------------------------------------*
*       Open automatic accounts
*----------------------------------------------------------------------*
FORM open_accounts .

  LOOP AT it_skb1.
    PERFORM call_fss0 USING it_skb1-bukrs it_skb1-saknr ' '.
  ENDLOOP.

ENDFORM.                    " OPEN_ACCOUNTS

*&---------------------------------------------------------------------*
*&      Form  PROCESS_DATA
*&---------------------------------------------------------------------*
*       Process data
*----------------------------------------------------------------------*
FORM process_data .

* Call transaction F.80 (Submit SAPF080)
  SUBMIT sapf080
         WITH br_bukrs IN s_bukrs
         WITH br_gjahr IN s_gjahr
         WITH br_belnr IN s_belnr
         WITH br_rldnr IN s_rldnr
         WITH br_blart IN s_blart
         WITH br_budat IN s_budat
         WITH br_cpudt IN s_cpudt
         WITH br_xblnr IN s_xblnr
         WITH br_awtyp IN s_awtyp
         WITH br_awkey IN s_awkey
         WITH br_awsys IN s_awsys
         WITH stogrd   = p_stogrd
         WITH stodat   = p_stodat
         WITH monat    = p_monat
         WITH testlauf = p_test
         WITH bvor_no  = 'X'
         AND RETURN.

ENDFORM.                    " PROCESS_DATA

*&---------------------------------------------------------------------*
*&      Form  CLOSE_ACCOUNTS
*&---------------------------------------------------------------------*
*       Close automatic accounts
*----------------------------------------------------------------------*
FORM close_accounts .

  LOOP AT it_skb1.
    PERFORM call_fss0 USING it_skb1-bukrs it_skb1-saknr 'X'.
  ENDLOOP.

ENDFORM.                    " CLOSE_ACCOUNTS

*&---------------------------------------------------------------------*
*&      Form  CALL_FSS0
*&---------------------------------------------------------------------*
*       Prepare & Call transaction FSS0
*----------------------------------------------------------------------*
*      -->P_BUKRS : Company Code
*      -->P_SAKNR : G/L Account
*      -->P_XINTB : Indicator: Is account only posted to automatically?
*----------------------------------------------------------------------*
FORM call_fss0  USING  p_bukrs
                       p_saknr
                       p_xintb.

  REFRESH: i_bdcdata, it_err.

  PERFORM bdc_dynpro      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=ACC_MOD'.
  PERFORM bdc_field       USING 'GLACCOUNT_SCREEN_KEY-SAKNR' p_saknr.
  PERFORM bdc_field       USING 'GLACCOUNT_SCREEN_KEY-BUKRS' p_bukrs.

  PERFORM bdc_dynpro      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=TAB02'.

  PERFORM bdc_dynpro      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=SAVE'.
  PERFORM bdc_field       USING 'GLACCOUNT_SCREEN_CCODE-XINTB' p_xintb.

  CALL TRANSACTION 'FSS0' USING i_bdcdata
                          MODE gv_mode
                          UPDATE 'S'
                          MESSAGES INTO it_err.

* Check for errors
  LOOP AT it_err WHERE msgtyp = 'E'
                    OR msgtyp = 'A'.
    MESSAGE ID it_err-msgid TYPE 'E' NUMBER it_err-msgnr
            WITH it_err-msgv1 it_err-msgv2 it_err-msgv3 it_err-msgv4.
  ENDLOOP.

ENDFORM.                    " CALL_FSS0

*----------------------------------------------------------------------*
*        Start new screen                                              *
*----------------------------------------------------------------------*
FORM bdc_dynpro USING program dynpro.

  CLEAR i_bdcdata.
  i_bdcdata-program  = program.
  i_bdcdata-dynpro   = dynpro.
  i_bdcdata-dynbegin = 'X'.
  APPEND i_bdcdata.

ENDFORM.                    "BDC_DYNPRO

*----------------------------------------------------------------------*
*        Insert field                                                  *
*----------------------------------------------------------------------*
FORM bdc_field USING fnam fval.

  CLEAR i_bdcdata.
  i_bdcdata-fnam = fnam.
  i_bdcdata-fval = fval.
  APPEND i_bdcdata.

ENDFORM.                    "bdc_field

*Text symbol text£º
*E01:No documents selected
*S01:General Selections

*S02:Reverse Postings Details
*Selection text£º
*P_MONAT:D       .
*P_STODAT:D       .
*P_STOGRD:        Reason for Reversal
*P_TEST:        Test Run
*S_AWKEY:D       .
*S_AWSYS:D       .
*S_AWTYP:        Reference Transaction
*S_BELNR:D       .
*S_BLART:D       .
*S_BUDAT:D       .
*S_BUKRS:D       .
*S_CPUDT:D       .
*S_GJAHR:D       .
*S_RLDNR:D       .
*S_XBLNR:        Reference Number
