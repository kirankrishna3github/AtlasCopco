************************************************************************
* Program ID           : YSE_REN_SERV_DUE_LIST                         *
* Program Title        : Service Due List                              *
* Author               : Erik Walravens                                *
* Date                 : 26.12.2006                                    *
* Development Number   : D009                                          *
* Change Request Number: CD1K909321                                    *
* Description          : This report displays an overview of rental    *
*                        contract items that need service processing.  *
*                        the user can indicate the relevant items and  *
*                        create the service notification automatically.*
*                        This program must also allow to process all   *
*                        new rental items automatically in a batch job.*
************************************************************************
* Notes:                                                               *
* * Sales office is not copied from the contract to the notification.  *
************************************************************************
* Change History Log                                                   *
*----------------------------------------------------------------------*
* Mod. no.|  Date    | Name           | Correction Number | Change Ref *
*----------------------------------------------------------------------*
* MOD-001 |28/02/2007| Erik Walravens | CD1K911682        | 001        *
* Description: Change material description to service item material.   *
*----------------------------------------------------------------------*
* MOD-002 |28/02/2007| Erik Walravens | CD1K911911        | 002        *
* Description: Include contract messages into notification text.       *
*----------------------------------------------------------------------*
* MOD-003 |28/02/2007| Erik Walravens | CD1K911987        | 003        *
* Description:                                                         *
*----------------------------------------------------------------------*
* MOD-004 |12/03/2007| Erik Walravens | CD1K912158        | 004        *
* Description: Copy actual time and date as notification creation time *
*              and date.                                               *
*----------------------------------------------------------------------*
* MOD-005 |22/03/2007| Erik Walravens | CD1K912838        | 005        *
* Description: Clear internal tables before creating notification.     *
*              Extend Sales Document types.                            *
*----------------------------------------------------------------------*
* MOD-006 |26/06/2007| Erik Walravens | CD1K916849        | 006        *
* Description: Check document flow for existing service notification.  *
*              Optimize contract selection.                            *
*              Fix 'no rows selected' bug.                             *
************************************************************************
REPORT  yse_ren_serv_due_list.

************************************************************************
* CLASSES                                                              *
************************************************************************
************************************************************************
* CLASS lcl_event_handler DEFINITION                                   *
************************************************************************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    METHODS:
*   Double click control
*    handle_double_click
*          FOR EVENT double_click OF cl_gui_alv_grid
*          IMPORTING e_row e_column,

*   Hotspot click control
    handle_hotspot_click
          FOR EVENT hotspot_click OF cl_gui_alv_grid
          IMPORTING e_row_id e_column_id es_row_no.

*   handle_user_command
*         FOR EVENT user_command OF cl_gui_alv_grid
*         IMPORTING e_ucomm.

ENDCLASS.                    "lcl_event_handler DEFINITION

************************************************************************
* CLASS lcl_event_handler IMPLEMENTATION                               *
************************************************************************
CLASS lcl_event_handler IMPLEMENTATION.

* Handle double click
*  METHOD handle_double_click.
*    PERFORM handle_double_click USING e_row e_column.
*  ENDMETHOD.

* Handle hotspot click
  METHOD handle_hotspot_click.
    PERFORM handle_hotspot_click USING e_row_id e_column_id es_row_no.
  ENDMETHOD.                    "handle_hotspot_click

*  METHOD handle_user_command .
*    CASE e_ucomm.
*      WHEN 'BACK'.
*    ENDCASE.
*  ENDMETHOD.                    "handle_user_command

ENDCLASS.                    "lcl_event_handler IMPLEMENTATION
************************************************************************
* FIELD-SYMBOLS                                                        *
************************************************************************
FIELD-SYMBOLS: <fieldcat> TYPE lvc_s_fcat.

************************************************************************
* OBJECTS                                                              *
************************************************************************
DATA : obj_container     TYPE REF TO cl_gui_custom_container.
DATA : obj_alv           TYPE REF TO cl_gui_alv_grid .
DATA : obj_event_handler TYPE REF TO lcl_event_handler.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES: vbak, vbap, qmih.

************************************************************************
* TYPE-POOLS                                                           *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA: BEGIN OF it_servits OCCURS 0.
        INCLUDE STRUCTURE yse_rent_alv_servits.
DATA:   uepos   TYPE vbap-uepos,               " higher level item
        zzequnr TYPE vbap-zzequnr,             " equipment nr
        zzsernr TYPE vbap-zzsernr,             " serial nr
*        vbegdat TYPE veda-vbegdat,             " required start date
      END OF it_servits.

DATA: BEGIN OF it_veda OCCURS 0,
        vbeln   TYPE veda-vbeln,
        vposn   TYPE veda-vposn,
        vbegdat TYPE veda-vbegdat,
      END OF it_veda.

DATA: BEGIN OF it_vbpa OCCURS 0,
        vbeln   TYPE vbpa-vbeln,
        posnr   TYPE vbpa-posnr,
        kunnr   TYPE vbpa-kunnr,
        parvw   TYPE vbpa-parvw,
      END OF it_vbpa.

DATA: BEGIN OF it_kna1 OCCURS 0,
        kunnr   TYPE kna1-kunnr,
        name1   TYPE kna1-name1,
      END OF it_kna1.

DATA: BEGIN OF it_viser02 OCCURS 0,
        sdaufnr TYPE viser02-sdaufnr,
        posnr   TYPE viser02-posnr,
        matnr   TYPE viser02-matnr,
        equnr   TYPE viser02-equnr,
        sernr   TYPE viser02-sernr,
      END OF it_viser02.

DATA: BEGIN OF it_arktx OCCURS 0,
        matnr   TYPE vbap-matnr,
        vbeln   TYPE vbap-vbeln,
        posnr   TYPE vbap-posnr,
        arktx   TYPE vbap-arktx,
      END OF it_arktx.

DATA: BEGIN OF it_vbap OCCURS 0,
        vbeln   TYPE vbap-vbeln,
        posnr   TYPE vbap-posnr,
        matnr   TYPE vbap-matnr,
        zzequnr TYPE vbap-zzequnr,
        zzsernr TYPE vbap-zzsernr,
        arktx   TYPE vbap-arktx,
      END OF it_vbap.

DATA: BEGIN OF it_qmih OCCURS 0,
        kdauf   TYPE qmih-kdauf,
        kdpos   TYPE qmih-kdpos,
        qmnum   TYPE qmih-qmnum,
      END OF it_qmih.

DATA: BEGIN OF it_stxh OCCURS 0,             " Text titles table
        tdname  TYPE stxh-tdname,
        tdspras TYPE stxh-tdspras,
      END OF it_stxh.

DATA:
  it_fieldcat       TYPE lvc_t_fcat,      " ALV grid field catalog
  it_rows           TYPE lvc_t_row,       " table selected rows ALV grid
  it_lines          LIKE tline OCCURS 0,  " Text lines
  it_log_handle     TYPE bal_t_logh,      " Appl. log handle
  it_new_lognumbers TYPE bal_t_lgnm.      " Log numbers

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  lc_true        TYPE char1       VALUE 'X',   " true
  lc_engl        LIKE tvagt-spras VALUE 'E',   " english
  lc_auart_renco LIKE vbak-auart  VALUE 'ZQP', " rental contr.
  lc_pstyv_renit LIKE vbap-pstyv  VALUE 'ZRC1'," service item
  lc_notif_type  TYPE qmart       VALUE 'X1',  " notific. type
  lc_atnam       TYPE cabn-atnam  VALUE 'RE_EQUIPMENT_TYPE',
  lc_id          LIKE thead-tdid  VALUE 'RE02', " Service instr. text id
  lc_obj         LIKE thead-tdobject VALUE 'VBBP', " Text object
  lc_soldto      TYPE vbpa-parvw  VALUE 'AG',  " sold-to party
  lc_shipto      TYPE vbpa-parvw  VALUE 'WE'.  " ship-to party

************************************************************************
* VARIABLES                                                            *
************************************************************************
* User's own data
DATA:  lv_vkorg         TYPE vbak-vkorg,            " Sales organisation
       lv_vtweg         TYPE vbak-vtweg,            " Distribution channel
       lv_spart         TYPE vbak-spart,            " Division
       lv_vkbur         TYPE vbak-vkbur,            " Sales office
       lv_werks         TYPE vbap-werks,            " Plant
       lv_vbeln         TYPE vbak-vbeln,            " Document number
       lv_posnr         TYPE vbap-posnr,            " Document item
       lv_kunnr         TYPE vbak-kunnr.            " Sold-to partner
* Notification
DATA:  wa_notifheader   TYPE bapi2080_nothdre,
       wa_notifhdtext   TYPE bapi2080_nothdtxte,
       wa_notlongtext   TYPE bapi2080_notfulltxte,
       wa_notitem       TYPE bapi2080_notiteme,
       wa_notifcaus     TYPE bapi2080_notcause,
       wa_notifactv     TYPE bapi2080_notactve,
       wa_notiftask     TYPE bapi2080_nottaske,
       wa_notifpartnr   TYPE bapi2080_notpartnre,
       wa_notifheader_i TYPE bapi2080_nothdri,
       wa_notlongtext_i TYPE bapi2080_notfulltxti,
       wa_notitem_i     TYPE bapi2080_notitemi,
       wa_notifcaus_i   TYPE bapi2080_notcausi,
       wa_notifactv_i   TYPE bapi2080_notactvi,
       wa_notiftask_i   TYPE bapi2080_nottaski,
       wa_notifpartnr_i TYPE bapi2080_notpartnri,
       lt_notlongtext   TYPE bapi2080_notfulltxte OCCURS 0,
       lt_notitem       TYPE bapi2080_notiteme OCCURS 0,
       lt_notifcaus     TYPE bapi2080_notcause OCCURS 0,
       lt_notifactv     TYPE bapi2080_notactve OCCURS 0,
       lt_notiftask     TYPE bapi2080_nottaske OCCURS 0,
       lt_notifpartnr   TYPE bapi2080_notpartnre OCCURS 0,
       lt_notlongtext_i TYPE bapi2080_notfulltxti OCCURS 0,
       lt_notitem_i     TYPE bapi2080_notitemi OCCURS 0,
       lt_notifcaus_i   TYPE bapi2080_notcausi OCCURS 0,
       lt_notifactv_i   TYPE bapi2080_notactvi OCCURS 0,
       lt_notiftask_i   TYPE bapi2080_nottaski OCCURS 0,
       lt_notifpartnr_i TYPE bapi2080_notpartnri OCCURS 0,
       lt_return        TYPE bapiret2 OCCURS 0.
* Logging
DATA:  wa_s_log         TYPE bal_s_log,    " BAL_LOG_CREATE - To send application log header
       wa_s_msg         TYPE bal_s_msg,    " BAL_LOG_CREATE - To send application message
       wa_new_lognumbers TYPE bal_s_lgnm,  " BAL_DB_SAVE - to receive the sense-making log numbers
       wa_notif         TYPE yse_rent_tmpsnot,
       wa_lines         LIKE tline,
       lv_log_handle    TYPE balloghndl,   " BAL_LOG_CREATE - Application
       lv_extnr         TYPE balnrext,     " external log number
       wa_return        TYPE bapiret2.     " error message
* Process
DATA:  okcode           LIKE sy-ucomm,     " return param screen 100
       gs_layout        TYPE lvc_s_layo,   " ALV grid layout
       lv_index         TYPE lvc_index,    " index alv grid
       lv_equnr         TYPE vbap-zzequnr, " Equipment number
       lv_atinn         TYPE cabn-atinn,   " Atinn-code for characteristic
       lv_qmnum         TYPE qmnum,        " Notification nr
       lv_atwrt         TYPE atwrt,        " C or G
       lv_tmptxt(20)    TYPE c,
       lv_name          LIKE thead-tdname, " Text name
       lv_lines         TYPE p,            " lines in lt_return
       lv_exists        TYPE c,            " notification flag
       lv_error         TYPE c,            " error has occurred
       it_bdcdata       TYPE TABLE OF bdcdata WITH HEADER LINE,  " to send to slg1
       ls_options       TYPE ctu_params,
       it_sernr         LIKE vbap-sernr OCCURS 0,
       it_sernos        LIKE TABLE OF rserob,
       it_serxx         LIKE TABLE OF rserxx,
       wa_key_data      LIKE rserob,
       wa_sernos        LIKE rserob,
       lv_partner       TYPE c,            " found partner
       lv_row           TYPE lvc_t_row.    " row number in alv grid


************************************************************************
* SELECTION SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-s01.
PARAMETERS:     p_vkorg  TYPE vbak-vkorg OBLIGATORY,  " Sales organisation
                p_vtweg  TYPE vbak-vtweg OBLIGATORY.  " Distribution channel
SELECT-OPTIONS: so_spart FOR  vbak-spart,        " Division
                so_vkbur FOR  vbak-vkbur,        " Sales office
                so_werks FOR  vbap-werks,        " Delivering plant
                so_vbeln FOR  vbak-vbeln,        " Document number
                so_posnr FOR  vbap-posnr,        " Item number
                so_kunnr FOR  vbak-kunnr,        " Sold-to partner
                so_erdat FOR  vbak-erdat,        " Doc. creation date
                so_vdatu FOR  vbak-vdatu.        " Delivery date
SELECTION-SCREEN END OF BLOCK b1.

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-s02.
SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: p_rb1 RADIOBUTTON GROUP r1 USER-COMMAND u1.
SELECTION-SCREEN COMMENT (35) text-p01
                 FOR FIELD p_rb1.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: p_rb2 RADIOBUTTON GROUP r1 DEFAULT 'X'.
SELECTION-SCREEN COMMENT (35) text-p02
                 FOR FIELD p_rb2.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: p_rb3 RADIOBUTTON GROUP r1.
SELECTION-SCREEN COMMENT (35) text-p03
                 FOR FIELD p_rb3.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK b2.


************************************************************************
* INITIALIZATION                                                       *
************************************************************************
INITIALIZATION.

* Load user's parameters
* Sales Organisation
  GET PARAMETER ID 'VKO' FIELD lv_vkorg.
  IF NOT lv_vkorg IS INITIAL.
    p_vkorg = lv_vkorg.
  ENDIF.

* Distribution Channel
  GET PARAMETER ID 'VTW' FIELD lv_vtweg.
  IF NOT lv_vtweg IS INITIAL.
    p_vtweg = lv_vtweg.
  ENDIF.

* Delivery Type
  GET PARAMETER ID 'SPA' FIELD lv_spart.
  IF NOT lv_spart IS INITIAL.
    so_spart-sign = 'I'.
    so_spart-option = 'EQ'.
    so_spart-low = lv_spart.
    APPEND so_spart.
  ENDIF.

* Sales office (depot)
  GET PARAMETER ID 'VKB' FIELD lv_vkbur.
  IF NOT lv_vkbur IS INITIAL.
    so_vkbur-sign = 'I'.
    so_vkbur-option = 'EQ'.
    so_vkbur-low = lv_vkbur.
    APPEND so_vkbur.
  ENDIF.

* Plant
*  GET PARAMETER ID 'WRK' FIELD lv_werks.
*  IF NOT lv_werks IS INITIAL.
*    so_werks-sign = 'I'.
*    so_werks-option = 'EQ'.
*    so_werks-low = lv_werks.
*    APPEND so_werks.
*  ENDIF.

* Document number
*  GET PARAMETER ID 'KTN' FIELD lv_vbeln.
*  IF NOT lv_vbeln IS INITIAL.
*    so_vbeln-sign = 'I'.
*    so_vbeln-option = 'EQ'.
*    so_vbeln-low = lv_vbeln.
*    APPEND so_vbeln.
*  ENDIF.

* Document item
*  GET PARAMETER ID 'APO' FIELD lv_posnr.
*  IF NOT lv_posnr IS INITIAL.
*    so_posnr-sign = 'I'.
*    so_posnr-option = 'EQ'.
*    so_posnr-low = lv_posnr.
*    APPEND so_posnr.
*  ENDIF.

* Sold-To partner
*  GET PARAMETER ID 'VAG' FIELD lv_kunnr.
*  IF NOT lv_kunnr IS INITIAL.
*    so_kunnr-sign = 'I'.
*    so_kunnr-option = 'EQ'.
*    so_kunnr-low = lv_kunnr.
*    APPEND so_kunnr.
*  ENDIF.


************************************************************************
* AT SELECTION-SCREEN                                                  *
************************************************************************
AT SELECTION-SCREEN.

  PERFORM Check_Authorization.


************************************************************************
* START OF SELECTION                                                   *
************************************************************************
START-OF-SELECTION.

* Select all rental contract items with service items
  SELECT *
      INTO CORRESPONDING FIELDS OF TABLE it_servits
      FROM vbak
      JOIN vbap
        ON vbak~vbeln EQ vbap~vbeln
*      JOIN veda
*        ON vbak~vbeln EQ veda~vbeln
*       AND vbap~posnr EQ veda~vposn
     WHERE vbak~vbeln IN so_vbeln
       AND vbap~posnr IN so_posnr
       AND vbak~auart EQ lc_auart_renco
       AND vbap~pstyv EQ lc_pstyv_renit
       AND vbak~vkorg EQ p_vkorg
       AND vbak~vtweg EQ p_vtweg
       AND vbak~spart IN so_spart
       AND vbak~vkbur IN so_vkbur
       AND vbap~werks IN so_werks
       AND vbak~kunnr IN so_kunnr
       AND vbak~erdat IN so_erdat
       AND vbak~vdatu IN so_vdatu.

  IF NOT it_servits[] IS INITIAL.
*   Retrieve contract item start dates
    SELECT vbeln vposn vbegdat
        INTO TABLE it_veda
        FROM veda
         FOR ALL ENTRIES IN it_servits
        WHERE vbeln EQ it_servits-vbeln.

**   Save partner information from all selected contracts
*    SELECT vbpa~vbeln vbpa~posnr vbpa~kunnr
*           kna1~name1 vbpa~parvw
*        FROM vbpa
*       INNER JOIN kna1
*          ON   vbpa~kunnr EQ kna1~kunnr
*        INTO CORRESPONDING FIELDS OF TABLE it_kna1
*       WHERE   vbpa~vbeln IN so_vbeln
*         AND   vbpa~posnr IN so_posnr
*         AND ( vbpa~parvw EQ lc_soldto
*          OR   vbpa~parvw EQ lc_shipto ).
*
**   Preselect all partner names
*    SELECT kunnr name1
*        FROM kna1
*        INTO CORRESPONDING FIELDS OF TABLE it_kna1
*         FOR ALL ENTRIES IN it_servits
*       WHERE kunnr EQ it_servits-kunnr.

*   Save partner information from all selected contracts & Preselect all partner names
    SELECT VBPA~VBELN VBPA~POSNR VBPA~KUNNR
           KNA1~NAME1 VBPA~PARVW
        FROM VBPA
       INNER JOIN KNA1
          ON   VBPA~KUNNR EQ KNA1~KUNNR
        INTO CORRESPONDING FIELDS OF TABLE IT_KNA1
         FOR ALL ENTRIES IN IT_SERVITS
       WHERE kna1~KUNNR EQ IT_SERVITS-KUNNR
       AND  VBPA~VBELN IN SO_VBELN
         AND   VBPA~POSNR IN SO_POSNR
         AND ( VBPA~PARVW EQ LC_SOLDTO
          OR   VBPA~PARVW EQ LC_SHIPTO ).

*   Load material descriptions
    SELECT matnr vbeln posnr arktx
        FROM vbap
        INTO CORRESPONDING FIELDS OF TABLE it_arktx
         FOR ALL ENTRIES IN it_servits
       WHERE vbeln EQ it_servits-vbeln.

*   Preselect equipments from items
    SELECT sdaufnr posnr matnr equnr sernr
        FROM viser02
        INTO TABLE it_viser02
         FOR ALL ENTRIES IN it_servits
       WHERE sdaufnr EQ it_servits-vbeln
         AND   posnr EQ it_servits-posnr.

*   Preselect equipments from higher level items
    SELECT vbeln posnr matnr zzsernr zzequnr arktx
        FROM vbap
        INTO CORRESPONDING FIELDS OF TABLE it_vbap
         FOR ALL ENTRIES IN it_servits
       WHERE vbeln EQ it_servits-vbeln
         AND posnr EQ it_servits-uepos.

*   Preselect all notifications linked to rental contract
    SELECT kdauf kdpos qmnum
        FROM qmih
        INTO CORRESPONDING FIELDS OF TABLE it_qmih
         FOR ALL ENTRIES IN it_servits
       WHERE kdauf EQ it_servits-vbeln
         AND kdpos EQ it_servits-posnr.  "#EC CI_NOFIELD

  ENDIF.     " it_servits[] NOT INITIAL

  LOOP AT it_servits.

    CLEAR lv_qmnum.
    CLEAR it_qmih.

*   Copy corresponding notification if it exists.
    READ TABLE it_qmih
      WITH KEY kdauf = it_servits-vbeln
               kdpos = it_servits-posnr.

*   When found
    IF sy-subrc = 0.
*     copy notification number
      lv_qmnum = it_qmih-qmnum.
*    ELSE.
**     else search for possible notifs through document flow
*      READ TABLE it_vbfa
*        WITH KEY vbelv = it_servits-vbeln.
*      IF sy-subrc = 0.
*        lv_qmnum = it_vbfa-qmnum.
*      ENDIF.
    ENDIF.

*   Check what needs to be displayed
    IF ( p_rb1 EQ lc_true AND lv_qmnum IS INITIAL ) OR
       ( p_rb2 EQ lc_true AND NOT lv_qmnum IS INITIAL ).
      DELETE it_servits.               " delete actual record
    ELSE.

*     Copy corresponding notification (if it exists)
      it_servits-qmnum = lv_qmnum.

*     Copy corresponding sold-to partner
      CLEAR it_kna1.
      READ TABLE it_kna1
        WITH KEY kunnr = it_servits-kunnr.
      it_servits-name1 = it_kna1-name1.

*     Copy service 'material' description
      CLEAR it_arktx.
      READ TABLE it_arktx
        WITH KEY matnr = it_servits-matnr
                 vbeln = it_servits-vbeln
                 posnr = it_servits-posnr.
      it_servits-arktx = it_arktx-arktx.

*     Get material that corresponds to the service item
      READ TABLE it_viser02 WITH KEY sdaufnr = it_servits-vbeln
                                       posnr = it_servits-posnr.
      IF sy-subrc = 0.
*        it_servits-matnr   = it_viser02-matnr.
        it_servits-zzequnr = it_viser02-equnr.
        it_servits-zzsernr = it_viser02-sernr.

*     If no equipment in item, get it from higher level item
      ELSE.
        CLEAR it_vbap.
        READ TABLE it_vbap
          WITH KEY vbeln = it_servits-vbeln
                   posnr = it_servits-uepos.
        IF sy-subrc = 0.
*          it_servits-matnr   = it_vbap-matnr.
          it_servits-zzequnr = it_vbap-zzequnr.
          it_servits-zzsernr = it_vbap-zzsernr.
*          it_servits-arktx   = it_vbap-arktx.
        ENDIF.    " found equipment in higher level item
      ENDIF.    " found equipment in item

*     Update internal table
      MODIFY it_servits FROM it_servits.
    ENDIF.    "  records to be removed.
  ENDLOOP.  " it_servits

* Display
  CALL SCREEN 100.

"END-OF-REPORT


*************************************************************************
** Module STATUS_0100 OUTPUT                                            *
*************************************************************************
MODULE status_0100 OUTPUT.
  SET TITLEBAR '100' .
  SET PF-STATUS 'STATUS100'.
ENDMODULE.                 " STATUS_0100  OUTPUT

*************************************************************************
** Module USER_COMMAND_0100 INPUT                                       *
*************************************************************************
MODULE user_command_0100 INPUT.

  CASE okcode.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN 'EXIT'.
      LEAVE TO SCREEN 0.
    WHEN 'OIW5X'.
      CALL METHOD obj_alv->get_selected_rows
        IMPORTING
          et_index_rows = it_rows.
      IF NOT it_rows[] IS INITIAL.
        PERFORM edit_notification USING it_rows.
      ENDIF.
    WHEN 'OSLG1'.
      CALL METHOD obj_alv->get_selected_rows
        IMPORTING
          et_index_rows = it_rows.
      READ TABLE it_rows INDEX 1 INTO lv_index.
      IF sy-subrc EQ 0.
        ls_options-nobiend = 'X'.
        ls_options-dismode = 'E'.
        ls_options-updmode = 'S'.

        READ TABLE it_servits INDEX lv_index.
        REFRESH it_bdcdata.
        PERFORM bdc_add_screen USING 'SAPLSLG3' '0100'.
        PERFORM bdc_add_field  USING 'BALHDR-OBJECT' 'YSE_REN_SERVINOT'.
        PERFORM bdc_add_field  USING 'BALHDR-EXTNUMBER' it_servits-vbeln.
        PERFORM bdc_add_field  USING 'BDC_OKCODE' '=SELE'.
        CALL TRANSACTION 'SLG1' USING it_bdcdata
                                OPTIONS FROM ls_options.
      ENDIF.    " found row.
  ENDCASE.
ENDMODULE.                 " USER_COMMAND_0100 INPUT

*************************************************************************
** Module PREPARE_SCREEN OUTPUT                                         *
*************************************************************************
MODULE prepare_screen OUTPUT.

  IF obj_container IS INITIAL .
    CREATE OBJECT obj_container
             EXPORTING container_name = 'OVERVIEW' .

    CREATE OBJECT obj_alv
              EXPORTING i_parent = obj_container.

    CREATE OBJECT obj_event_handler.

    PERFORM build_alv.

*   Enable line selection and hotspot clicking
    SET HANDLER obj_event_handler->handle_hotspot_click FOR obj_alv.
  ENDIF.
ENDMODULE.                 " PREPARE_SCREEN  OUTPUT


*************************************************************************
** Form BUILD_ALV                                                       *
*************************************************************************
FORM build_alv.

  REFRESH: it_fieldcat.

  CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
   EXPORTING
    i_buffer_active              = ' '
    i_structure_name             = 'YSE_RENT_ALV_SERVITS'
   CHANGING
    ct_fieldcat                  = it_fieldcat.

  LOOP AT it_fieldcat ASSIGNING <fieldcat>.
    CASE <fieldcat>-fieldname.
      WHEN  'VKORG'.
        <fieldcat>-scrtext_l = 'Sales organization'(001).
        <fieldcat>-scrtext_m = 'Sales org.'(002).
        <fieldcat>-scrtext_s = 'S.Org'(003).
        <fieldcat>-outputlen = 4.
      WHEN  'VTWEG'.
        <fieldcat>-scrtext_l = 'Distribution channel'(004).
        <fieldcat>-scrtext_m = 'Distr.chn'(005).
        <fieldcat>-scrtext_s = 'DC'(006).
        <fieldcat>-outputlen = 3.
      WHEN  'SPART'.
        <fieldcat>-scrtext_l = 'Division'(007).
        <fieldcat>-scrtext_m = 'Divis.'(008).
        <fieldcat>-scrtext_s = 'Div'(009).
        <fieldcat>-outputlen = 3.
      WHEN  'VKBUR'.
        <fieldcat>-scrtext_l = 'Sales office'(010).
        <fieldcat>-scrtext_m = 'Sales off.'(011).
        <fieldcat>-scrtext_s = 'S.Off'(012).
        <fieldcat>-outputlen = 4.
      WHEN  'WERKS'.
        <fieldcat>-scrtext_l = 'Issuing plant'(013).
        <fieldcat>-scrtext_m = 'Iss. plant'(014).
        <fieldcat>-scrtext_s = 'IPl'(015).
        <fieldcat>-outputlen = 4.
      WHEN  'KUNNR'.
        <fieldcat>-scrtext_l = 'Sold-to partner'(016).
        <fieldcat>-scrtext_m = 'Sold-to'(017).
        <fieldcat>-scrtext_s = 'SP'(018).
        <fieldcat>-outputlen = 10.
      WHEN  'NAME1'.
        <fieldcat>-scrtext_l = 'Partner name'(019).
        <fieldcat>-scrtext_m = 'Name'(020).
        <fieldcat>-scrtext_s = 'SP'(021).
        <fieldcat>-outputlen = 20.
      WHEN  'ERDAT'.
        <fieldcat>-scrtext_l = 'Creation date'(022).
        <fieldcat>-scrtext_m = 'Cre.date'(023).
        <fieldcat>-scrtext_s = 'CD'(024).
        <fieldcat>-outputlen = 9.
      WHEN  'VBELN'.
        <fieldcat>-scrtext_l = 'Contract Number'(025).
        <fieldcat>-scrtext_m = 'Contract'(026).
        <fieldcat>-scrtext_s = 'Ctr'(027).
        <fieldcat>-outputlen = 11.
      WHEN  'POSNR'.
        <fieldcat>-scrtext_l = 'Contract item'(028).
        <fieldcat>-scrtext_m = 'Item'(029).
        <fieldcat>-scrtext_s = 'Itm'(030).
        <fieldcat>-outputlen = 4.
      WHEN  'MATNR'.
        <fieldcat>-scrtext_l = 'Material number'(031).
        <fieldcat>-scrtext_m = 'Material'(032).
        <fieldcat>-scrtext_s = 'Mat'(033).
        <fieldcat>-outputlen = 12.
      WHEN  'MAKTX'.
        <fieldcat>-scrtext_l = 'Material description'(034).
        <fieldcat>-scrtext_m = 'Description'(035).
        <fieldcat>-scrtext_s = 'Desc'(036).
        <fieldcat>-outputlen = 30.
      WHEN  'VDATU'.
        <fieldcat>-scrtext_l = 'Delivery date'(037).
        <fieldcat>-scrtext_m = 'Del.date'(038).
        <fieldcat>-scrtext_s = 'DD'(039).
        <fieldcat>-outputlen = 9.
      WHEN  'QMNUM'.
        <fieldcat>-scrtext_l = 'Service notification'(040).
        <fieldcat>-scrtext_m = 'Serv.notif.'(041).
        <fieldcat>-scrtext_s = 'Serv'(042).
        <fieldcat>-outputlen = 10.
    ENDCASE.
  ENDLOOP.

  gs_layout-no_toolbar = lc_true.
  gs_layout-sel_mode   = 'B'.        " single row selectable
  CALL METHOD obj_alv->set_table_for_first_display
    EXPORTING
      i_structure_name              = 'YSE_RENT_ALV_SERVITS'
      is_layout                     = gs_layout
    CHANGING
      it_outtab                     = it_servits[]
      it_fieldcatalog               = it_fieldcat
    EXCEPTIONS
      invalid_parameter_combination = 1
      program_error                 = 2
      too_many_lines                = 3
      OTHERS                        = 4.

  IF sy-subrc <> 0.
*     MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*     WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.
ENDFORM.                    "build_alv


************************************************************************
* Form HANDLE_HOTSPOT_CLICK                                            *
************************************************************************
*      -->P_E_ROW_ID     text                                          *
*      -->P_E_COLUMN_ID  text                                          *
*      -->P_ES_ROW_NO    text                                          *
************************************************************************
FORM handle_hotspot_click  USING    p_e_row_id
                                    p_e_column_id
                                    p_es_row_no STRUCTURE lvc_s_roid.

* Save current row cursor
  CALL METHOD obj_alv->get_selected_rows
    IMPORTING
      et_index_rows = lv_row.

*  READ TABLE it_servits INDEX p_e_row_id.
*
** If Delivery Number clicked
*  IF p_e_column_id EQ 'VBELN'.
*    PERFORM edit_delivery USING it_delinot-vbeln.
*  ENDIF.
*
** If Service Notification clicked
*  IF p_e_column_id EQ 'QMNUM'.
*    PERFORM edit_notification USING it_delinot-qmnum.
*  ENDIF.
*
** If Equipment Number clicked
*  IF p_e_column_id EQ 'ZZEQUNR'.
*    PERFORM edit_equipment USING it_delinot-zzequnr.
*  ENDIF.

* Recall last row cursor position
  CALL METHOD obj_alv->set_selected_rows
    EXPORTING
      it_index_rows = lv_row.

* Refresh alv grid
  CALL METHOD obj_alv->refresh_table_display.

ENDFORM.                    " handle_hotspot_click


************************************************************************
* Form CHECK_AUTHORIZATION                                             *
************************************************************************
* Checks if user is authorized to run this transaction with the        *
* selected parameter(s).                                               *
************************************************************************
FORM Check_Authorization.

  AUTHORITY-CHECK OBJECT 'V_VBAK_VKO'
                      ID 'VKORG' FIELD p_vkorg
                      ID 'VTWEG' FIELD p_vtweg
                      ID 'SPART' DUMMY
                      ID 'ACTVT' DUMMY.

  IF sy-subrc = 4.
*   No authorisation to display data from Sales Organisation p_vkorg
    MESSAGE ID 'YSE_RENTAL' TYPE 'E' NUMBER '041' WITH p_vkorg p_vtweg.
  ELSEIF sy-subrc <> 0.
*   Error checking authorization.
    MESSAGE ID 'YSE_RENTAL' TYPE 'E' NUMBER '046'.
  ENDIF.

ENDFORM.

************************************************************************
* Form EDIT_NOTIFICATION                                               *
************************************************************************
*   argument: arg_rows table of rownumbers                             *
************************************************************************
FORM edit_notification USING arg_rows TYPE lvc_t_row.

  CLEAR lv_exists.

* Save current row cursor
  CALL METHOD obj_alv->get_selected_rows
    IMPORTING
      et_index_rows = lv_row.

  READ TABLE arg_rows INDEX 1 INTO lv_index.
  IF sy-subrc EQ 0.
    READ TABLE it_servits INDEX lv_index.
*   Is there a notification?
    IF NOT it_servits-qmnum IS INITIAL.
      lv_exists = lc_true.
*     Edit the notification
      SET PARAMETER ID 'IQM' FIELD it_servits-qmnum.
      CALL TRANSACTION 'IW52' AND SKIP FIRST SCREEN.
    ENDIF.    " quotation exists
  ENDIF.    " row selected

* Else try to create a notification if there is an equipment
  IF lv_exists IS INITIAL.

*   Check if an equipment was determined.
    IF it_servits-zzequnr IS INITIAL.
      MESSAGE 'No equipment specified. Notification could not be created.'(043) TYPE 'W'.
    ELSE.
*     Create application log
      lv_extnr = it_servits-vbeln.

      CLEAR wa_s_log.
      wa_s_log-extnumber = lv_extnr.
      wa_s_log-object    = 'YSE_REN_SERVINOT'.
      wa_s_log-aldate    = sy-datlo.
      wa_s_log-altime    = sy-timlo.
      wa_s_log-aluser    = sy-uname.
      CALL FUNCTION 'BAL_LOG_CREATE'
        EXPORTING
          i_s_log                       = wa_s_log
        IMPORTING
          e_log_handle                  = lv_log_handle.

*    Find equipment number
     SELECT SINGLE zzequnr
         FROM vbap
        INTO lv_equnr
        WHERE vbeln EQ it_servits-vbeln
          AND posnr EQ it_servits-posnr.

*     Convert characteristic to atinn
      CALL FUNCTION 'CONVERSION_EXIT_ATINN_INPUT'
        EXPORTING
          input         = lc_atnam          " 'RE_EQUIPMENT_TYPE'
        IMPORTING
          output        = lv_atinn.

*     Retrieve characteristic RE_EQUIPMENT_TYPE
      CLEAR lv_atwrt.
      SELECT SINGLE atwrt
          FROM ausp
          INTO lv_atwrt
         WHERE atinn = lv_atinn
           AND objek = lv_equnr.

*     Retrieve template notification number
      SELECT SINGLE *
        FROM yse_rent_tmpsnot
        INTO wa_notif
       WHERE vkorg = it_servits-vkorg
         AND vtweg = it_servits-vtweg
         AND spart = it_servits-spart
         AND atwrt = lv_atwrt
         AND lfart = lc_auart_renco.

*     No corresponding service notification template available
      IF sy-subrc <> 0.
        wa_s_msg-msgty = 'E'.
        wa_s_msg-msgid = 'YSE_RENTAL'.
        wa_s_msg-msgno = 035.
        CONCATENATE it_servits-vbeln
                    '/'
                    it_servits-posnr
                INTO wa_s_msg-msgv1
           SEPARATED BY space.

        CALL FUNCTION 'BAL_LOG_MSG_ADD'
          EXPORTING
            i_log_handle              = lv_log_handle
            i_s_msg                   = wa_s_msg
          EXCEPTIONS
            log_not_found             = 1
            msg_inconsistent          = 2
            log_is_full               = 3
            OTHERS                    = 4.
*       Store the log to be saved later on.
        APPEND lv_log_handle TO it_log_handle.
        lv_error = lc_true.
      ENDIF.
      IF lv_error IS INITIAL.
*       Clear working areas
        CLEAR wa_notifheader_i.
        CLEAR wa_notlongtext.
        CLEAR wa_notitem.
        CLEAR wa_notifcaus.
        CLEAR wa_notifactv.
        CLEAR wa_notiftask.
        CLEAR wa_notifpartnr.

        CLEAR lt_notlongtext.
        REFRESH lt_notlongtext.
        CLEAR lt_notitem.
        REFRESH lt_notitem.
        CLEAR lt_notifcaus.
        REFRESH lt_notifcaus.
        CLEAR lt_notifactv.
        REFRESH lt_notifactv.
        CLEAR lt_notiftask.
        REFRESH lt_notiftask.
        CLEAR lt_notifpartnr.
        REFRESH lt_notifpartnr.
        CLEAR lt_return.
        REFRESH lt_return.
        CLEAR lt_notlongtext_i.
        REFRESH lt_notlongtext_i.
        CLEAR lt_notitem_i.
        REFRESH lt_notitem_i.
        CLEAR lt_notifcaus_i.
        REFRESH lt_notifcaus_i.
        CLEAR lt_notifactv_i.
        REFRESH lt_notifactv_i.
        CLEAR lt_notiftask_i.
        REFRESH lt_notiftask_i.
        CLEAR lt_notifpartnr_i.
        REFRESH lt_notifpartnr_i.

*       Read the template service notification
        CALL FUNCTION 'BAPI_SERVNOT_GET_DETAIL'
          EXPORTING
            number            = wa_notif-qmnum
          IMPORTING
            notifheader       = wa_notifheader
            notifhdtext       = wa_notifhdtext
          TABLES
            notlongtext       = lt_notlongtext
            notitem           = lt_notitem
            notifcaus         = lt_notifcaus
            notifactv         = lt_notifactv
            notiftask         = lt_notiftask
            notifpartnr       = lt_notifpartnr
            return            = lt_return.

*       Log step 1: Read template notification
        CLEAR wa_s_msg.
        wa_s_msg-msgty = 'I'.
        wa_s_msg-msgid = 'YSE_RENTAL'.
        wa_s_msg-msgno = 036.

        CALL FUNCTION 'BAL_LOG_MSG_ADD'
          EXPORTING
            i_log_handle              = lv_log_handle
            i_s_msg                   = wa_s_msg
          EXCEPTIONS
            log_not_found             = 1
            msg_inconsistent          = 2
            log_is_full               = 3
            OTHERS                    = 4.
*       Store the log to be saved later on.
        APPEND lv_log_handle TO it_log_handle.

        DESCRIBE TABLE lt_return LINES lv_lines.

        IF lv_lines > 0.
*         Copy the return message(s) from BAPI_SERVNOT_GET_DETAIL
          LOOP AT lt_return INTO wa_return.
            CLEAR wa_s_msg.
            wa_s_msg-msgty = wa_return-type.
            wa_s_msg-msgid = wa_return-id.
            wa_s_msg-msgno = wa_return-number.
            MOVE wa_return-message_v1 TO wa_s_msg-msgv1.
            MOVE wa_return-message_v2 TO wa_s_msg-msgv2.
            MOVE wa_return-message_v3 TO wa_s_msg-msgv3.
            MOVE wa_return-message_v4 TO wa_s_msg-msgv4.

            CALL FUNCTION 'BAL_LOG_MSG_ADD'
              EXPORTING
                i_log_handle              = lv_log_handle
                i_s_msg                   = wa_s_msg
              EXCEPTIONS
                log_not_found             = 1
                msg_inconsistent          = 2
                log_is_full               = 3
                OTHERS                    = 4.
*           Store the log to be saved later on.
            APPEND lv_log_handle TO it_log_handle.
            IF wa_return-type = 'E'.
              lv_error = lc_true.
            ENDIF.    " Error message
          ENDLOOP.  " lt_return
        ENDIF.    " process return messages
      ENDIF.    " Read template contents

*     Insert data into template notification and create new notification
      IF lv_error IS INITIAL.

*       Description Check-in delivery / check-out delivery ?
*       To be inserted here...

*       Insert equipment and serial
        wa_notifheader_i-material   = it_servits-matnr.
        wa_notifheader_i-equipment  = it_servits-zzequnr.
        wa_notifheader_i-serialno   = it_servits-zzsernr.

*       Insert basic start date
        CLEAR it_veda.
        READ TABLE it_veda WITH KEY vbeln = it_servits-vbeln
                                    vposn = it_servits-posnr.
        IF sy-subrc <> 0.
          READ TABLE it_veda WITH KEY vbeln = it_servits-vbeln
                                      vposn = space.
        ENDIF.
        IF NOT it_veda-vbegdat IS INITIAL.
          wa_notifheader_i-desstdate  = it_veda-vbegdat.
        ENDIF.

*       Insert creation date and time
        wa_notifheader_i-notif_date = sy-datum.
        wa_notifheader_i-notiftime  = sy-uzeit.

*       Insert Sales Area
        wa_notifheader_i-sales_org  = it_servits-vkorg.
        wa_notifheader_i-distr_chan = it_servits-vtweg.
        wa_notifheader_i-division   = it_servits-spart.
*       wa_notifheader_i-sales_office = it_servits-vkbur.   " ????

*       Insert description
        wa_notifheader_i-short_text = it_servits-arktx.

*       Insert Sales doc/item
        wa_notifheader_i-doc_number = it_servits-vbeln.
        wa_notifheader_i-itm_number = it_servits-posnr.

*       Check if more check-out instructions available in contract
        CONCATENATE it_servits-vbeln
                    it_servits-posnr
               INTO lv_tmptxt.

        MOVE lv_tmptxt TO lv_name.

*       Get all text items for this contract
        SELECT *
            FROM stxh CLIENT SPECIFIED
            INTO CORRESPONDING FIELDS OF TABLE it_stxh
           WHERE mandt    EQ sy-mandt
             AND tdobject EQ lc_obj
             AND tdname   EQ lv_name
             AND tdid     EQ lc_id.

        IF sy-subrc <> 0.
*         No new text available
        ELSE.
*         If more text, copy to buffer...
          LOOP AT it_stxh.

            CLEAR it_lines.
            REFRESH it_lines.

            CALL FUNCTION 'READ_TEXT'
              EXPORTING
                client                        = sy-mandt
                id                            = lc_id
                language                      = it_stxh-tdspras
                name                          = it_stxh-tdname
                object                        = lc_obj
              TABLES
                lines                         = it_lines
              EXCEPTIONS
                id                            = 1
                language                      = 2
                name                          = 3
                not_found                     = 4
                object                        = 5
                reference_check               = 6
                wrong_access_to_archive       = 7
                OTHERS                        = 8.

*           If error while reading text in this language
            IF sy-subrc <> 0.

*             Create error text
              wa_s_msg-msgty = 'E'.
              wa_s_msg-msgid = 'YSE_RENTAL'.
              wa_s_msg-msgno = 000.
              wa_s_msg-msgv1 = 'No check-out instructions found for contract/item:'(045).
              CONCATENATE it_servits-vbeln                " xvbap-vbeln
                          '/'
                          it_servits-posnr               " xvbap-posnr
                      INTO wa_s_msg-msgv2
                 SEPARATED BY space.

              CALL FUNCTION 'BAL_LOG_MSG_ADD'
                EXPORTING
                  i_log_handle              = lv_log_handle
                  i_s_msg                   = wa_s_msg
                EXCEPTIONS
                  log_not_found             = 1
                  msg_inconsistent          = 2
                  log_is_full               = 3
                  OTHERS                    = 4.

*             Save application log to database
              APPEND lv_log_handle TO it_log_handle.
              CALL FUNCTION 'BAL_DB_SAVE'
                EXPORTING
                  i_save_all             = 'X'
                  i_t_log_handle         = it_log_handle
                IMPORTING
                  e_new_lognumbers       = it_new_lognumbers
                EXCEPTIONS
                  log_not_found          = 1
                  save_not_allowed       = 2
                  numbering_error        = 3
                  OTHERS                 = 4.

              lv_error = lc_true.
            ELSE.
*           ... and add buffer at end existing lines.
* Note: To do: copy text in correct language code in notification.
              wa_notlongtext_i-objtype = 'QMEL'.
              wa_notlongtext_i-objkey = '00000000'.
              wa_notlongtext_i-format_col = '>X'.
              LOOP AT it_lines INTO wa_lines.
                CONCATENATE '*' wa_lines-tdline
                         INTO wa_notlongtext_i-text_line
                    SEPARATED BY space.
                APPEND wa_notlongtext_i TO lt_notlongtext_i.
              ENDLOOP.  " it_lines
            ENDIF.    " no error in READ_TEXT
          ENDLOOP.  " multi-language texts
        ENDIF.    " if instruction text in contract

*       Copy standard fields from template
        LOOP AT lt_notitem INTO wa_notitem.
          MOVE-CORRESPONDING wa_notitem TO wa_notitem_i.
          APPEND wa_notitem_i TO lt_notitem_i.
        ENDLOOP. " LT_NOTITEM.
        LOOP AT lt_notifcaus INTO wa_notifcaus.
          MOVE-CORRESPONDING wa_notifcaus TO wa_notifcaus_i.
          APPEND wa_notifcaus_i TO lt_notifcaus_i.
        ENDLOOP. " LT_NOTIFCAUS.
        LOOP AT lt_notifactv INTO wa_notifactv.
          MOVE-CORRESPONDING wa_notifactv TO wa_notifactv_i.
          APPEND wa_notifactv_i TO lt_notifactv_i.
        ENDLOOP. " LT_NOTIFACTV.
        LOOP AT lt_notiftask INTO wa_notiftask.
          MOVE-CORRESPONDING wa_notiftask TO wa_notiftask_i.
          APPEND wa_notiftask_i TO lt_notiftask_i.
        ENDLOOP. " LT_NOTIFTASK.

*       Copy template partners
        CLEAR lt_notifpartnr_i.
        REFRESH lt_notifpartnr_i.
*       Sold-to party
        lv_partner = space.
        CLEAR it_vbpa.
*       First look for item partner
        READ TABLE it_vbpa WITH KEY vbeln = it_servits-vbeln
                                    posnr = it_servits-posnr
                                    parvw = lc_soldto.
        IF sy-subrc = 0.
          lv_partner = lc_true.
        ELSE.
*         else look for header partner
          READ TABLE it_vbpa WITH KEY vbeln = it_servits-vbeln
                                      posnr = space
                                      parvw = lc_soldto.
          IF sy-subrc = 0.
            lv_partner = lc_true.
          ENDIF.
        ENDIF.
        IF lv_partner = lc_true.
            wa_notifpartnr_i-partn_role     = lc_soldto.
            wa_notifpartnr_i-partner        = it_kna1-kunnr.   " warning char10 -> char8
            APPEND wa_notifpartnr_i TO lt_notifpartnr_i.
        ENDIF.

*       Ship-to party
        lv_partner = space.
        CLEAR it_vbpa.
*       First look for item partner
        READ TABLE it_vbpa WITH KEY vbeln = it_servits-vbeln
                                    posnr = it_servits-posnr
                                    parvw = lc_shipto.
        IF sy-subrc = 0.
          lv_partner = lc_true.
        ELSE.
*         else look for header partner
          READ TABLE it_vbpa WITH KEY vbeln = it_servits-vbeln
                                      posnr = space
                                      parvw = lc_shipto.
          IF sy-subrc = 0.
            lv_partner = lc_true.
          ENDIF.
        ENDIF.
        IF lv_partner = lc_true.
            wa_notifpartnr_i-partn_role     = lc_shipto.
            wa_notifpartnr_i-partner        = it_kna1-kunnr.   " warning char10 -> char8
            APPEND wa_notifpartnr_i TO lt_notifpartnr_i.
        ENDIF.

*       Create the new service notification
        CALL FUNCTION 'BAPI_SERVNOT_CREATE'
          EXPORTING
*           external_number          =
            notif_type               = lc_notif_type
            notifheader              = wa_notifheader_i
*           task_determination       = ' '
*           SENDER                   =
*           ORDERID                  =
          IMPORTING
            notifheader_export       = wa_notifheader    " This has the templ notif. upto now, it will now be filled with the new notif details
          TABLES
            notitem                  = lt_notitem_i
            notifcaus                = lt_notifcaus_i
            notifactv                = lt_notifactv_i
            notiftask                = lt_notiftask_i
            notifpartnr              = lt_notifpartnr_i
            longtexts                = lt_notlongtext_i
*           key_relationships        =
            return                   = lt_return.

*       Log step 2: Create service notification
        CLEAR wa_s_msg.
        wa_s_msg-msgty = 'I'.
        wa_s_msg-msgid = 'YSE_RENTAL'.
        wa_s_msg-msgno = 037.

        CALL FUNCTION 'BAL_LOG_MSG_ADD'
          EXPORTING
            i_log_handle              = lv_log_handle
            i_s_msg                   = wa_s_msg
          EXCEPTIONS
            log_not_found             = 1
            msg_inconsistent          = 2
            log_is_full               = 3
            OTHERS                    = 4.
*       Store the log to be saved later on.
        APPEND lv_log_handle TO it_log_handle.

        DESCRIBE TABLE lt_return LINES lv_lines.
        IF lv_lines > 0.
          LOOP AT lt_return INTO wa_return.
            wa_s_msg-msgty = wa_return-type.
            wa_s_msg-msgid = wa_return-id.
            wa_s_msg-msgno = wa_return-number.
            MOVE wa_return-message_v1 TO wa_s_msg-msgv1.
            MOVE wa_return-message_v2 TO wa_s_msg-msgv2.
            MOVE wa_return-message_v3 TO wa_s_msg-msgv3.
            MOVE wa_return-message_v4 TO wa_s_msg-msgv4.

            CALL FUNCTION 'BAL_LOG_MSG_ADD'
              EXPORTING
                i_log_handle              = lv_log_handle
                i_s_msg                   = wa_s_msg
              EXCEPTIONS
                log_not_found             = 1
                msg_inconsistent          = 2
                log_is_full               = 3
                OTHERS                    = 4.
*           Store the log to be saved later on
            APPEND lv_log_handle TO it_log_handle.
            IF wa_return-type = 'E'.
              lv_error = lc_true.
            ENDIF.    " error message
          ENDLOOP.  " lt_return
        ENDIF.    " Process create notification return messages
      ENDIF.    " Create new notification
*     Save the new notification
      IF lv_error IS INITIAL.
        CALL FUNCTION 'BAPI_SERVNOT_SAVE'
          EXPORTING
            number            = wa_notifheader-notif_no
          IMPORTING
            notifheader       = wa_notifheader
          TABLES
            return            = lt_return.

        READ TABLE lt_return INTO wa_return WITH KEY type = 'E'.
        IF sy-subrc NE 0.  " success
          wa_s_msg-msgty = 'I'.    " information
          wa_s_msg-msgid = 'YSE_RENTAL'.
          wa_s_msg-msgno = 038.
          wa_s_msg-msgv1 = wa_notifheader-notif_no.
          wa_s_msg-msgv2 = it_servits-vbeln.
          CLEAR wa_s_msg-msgv3.
          CLEAR wa_s_msg-msgv4.
          CALL FUNCTION 'BAL_LOG_MSG_ADD'
            EXPORTING
              i_log_handle              = lv_log_handle
              i_s_msg                   = wa_s_msg
            EXCEPTIONS
              log_not_found             = 1
              msg_inconsistent          = 2
              log_is_full               = 3
              OTHERS                    = 4.
*         Save the log to be saved later on
          APPEND lv_log_handle TO it_log_handle.

*         Commit the new notification so that it can be displayed
          CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.

*         Get index in results table
          READ TABLE it_servits WITH KEY vbeln = it_servits-vbeln
                                         posnr = it_servits-posnr.
*         Update record
          it_servits-qmnum = wa_notifheader-notif_no.
          MODIFY it_servits FROM it_servits INDEX sy-tabix.

*         Redraw the alv grid
          CALL METHOD obj_alv->refresh_table_display.

*         Recall last row cursor position
          CALL METHOD obj_alv->set_selected_rows
              EXPORTING
                 it_index_rows = lv_row.

        ENDIF.    " Check save error messages
      ENDIF.    " Save the new notifiaction

*     Save application log to database
      CALL FUNCTION 'BAL_DB_SAVE'
        EXPORTING
          i_save_all             = 'X'
          i_t_log_handle         = it_log_handle
        IMPORTING
          e_new_lognumbers       = it_new_lognumbers
        EXCEPTIONS
          log_not_found          = 1
          save_not_allowed       = 2
          numbering_error        = 3
          OTHERS                 = 4.

      CLEAR it_log_handle.
      REFRESH it_log_handle.

*     Display a message on screen in case of an error
      IF lv_error = lc_true.
        CLEAR lv_error.
        MESSAGE 'Service notification creation failed. Check Log.'(044) TYPE 'W'.
      ENDIF.    " Creation failed
    ENDIF.    " No equipment
  ENDIF.    " No service notification exists yet
ENDFORM.  " edit_notification

************************************************************************
* Form BCD_ADD_SCREEN                                                  *
************************************************************************
*   arguments:                                                         *
*            P_PROGRAM           Program code of transaction           *
*            P_DYNPRO            Screen number                         *
************************************************************************
FORM bdc_add_screen USING p_program p_dynpro.
  CLEAR it_bdcdata.
  it_bdcdata-program  = p_program.
  it_bdcdata-dynpro   = p_dynpro.
  it_bdcdata-dynbegin = 'X'.
  APPEND it_bdcdata.
ENDFORM.    " BDC_ADD_SCREEN

************************************************************************
* Form BCD_ADD_FIELD                                                   *
************************************************************************
*   arguments:                                                         *
*            P_FNAM              Field name                            *
*            P_FVAL              Value                                 *
************************************************************************
FORM bdc_add_field USING p_fnam p_fval.
  IF p_fval <> '/'.
    CLEAR it_bdcdata.
    it_bdcdata-fnam = p_fnam.
    it_bdcdata-fval = p_fval.
    APPEND it_bdcdata.
  ENDIF.
ENDFORM.    " BDC_ADD_FIELD

*Text symbol text
*001:Sales organization
*002:Sales org.
*003:S.Org
*004:Distribution channel
*005:Distr.chn
*006:DC
*007:Division
*008:Divis.
*009:Div
*010:Sales office
*011:Sales off.
*012:S.Off
*013:Issuing plant
*014:Iss. plant
*015:IPl
*016:Sold-to partner
*017:Sold-to
*018:SP
*019:Partner name
*020:Name
*021:SP
*022:Creation date
*023:Cre.date
*024:CD
*025:Contract Number
*026:Contract
*027:Ctr
*028:Contract item
*029:Item
*030:Itm
*031:Material number
*032:Material
*033:Mat
*034:Material description
*035:Description
*036:Desc
*037:Delivery date
*038:Del.date
*039:DD
*040:Service notification
*041:Serv.notif.
*042:Serv
*043:No equipment specified. Notification could not be created.
*044:Service notification creation failed. Check Log.
*045:No check-out instructions found for contract/item:
*P01:created
*P02:not created
*P03:both
*S01:Selection parameters - Blanc entry = select all for that option

*S02:Service notification
*Selection text
*P_RB1:        created
*P_RB2:        not created
*P_RB3:        both
*P_VKORG:        Sales organisation
*P_VTWEG:        Distribution channel
*SO_ERDAT:        Creation date
*SO_KUNNR:        Sold-to partner
*SO_POSNR:        Item number
*SO_SPART:        Division
*SO_VBELN:        Document number
*SO_VDATU:        Delivery date
*SO_VKBUR:        Sales office
*SO_WERKS:        Issuing plant
