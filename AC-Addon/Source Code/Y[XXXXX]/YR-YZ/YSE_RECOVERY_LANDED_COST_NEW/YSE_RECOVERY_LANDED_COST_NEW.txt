*&---------------------------------------------------------------------------*
*& Report  YSE_RECOVERY_LANDED_COST
*&
*&---------------------------------------------------------------------------*
*&                                                                           *
*& LCR Russia : Report: Recovery of landed costs                             *
*&                                                                           *
*&---------------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 09.08.2010
*  Change Request Number : CR1377
*  Transport request Nr. : CD1K958493
*----------------------------------------------------------------------------*
*                                                                            *
*----------------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                         *
*----------------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME               | CORRECTION NR. | CHANGE REF. # *
*----------------------------------------------------------------------------*
* MOD-001 | 25.02.2011 | Nanda S / Madhu S  | CD1K963236     | CR1901        *
*         | Addition of GAC field at line item level.                        *
*----------------------------------------------------------------------------*
* MOD-002 | 05.01.2012 | Jules Smets        | CD1K969720     | CR2155        *
*         | Addition of freight amount                                       *
*----------------------------------------------------------------------------*
* MOD-003 | 06.12.2012 | Pratap Mada       | CD1K974128  |                   *
*                                            CD1K974131  | CR2155            *
*                                            CD1K974163                      *
*         | Correcting the new line item creation in report                  *
*            output.                                                         *
*            Correcting the value Amount in LC. If the acc doc having more   *
*             than one line item, the value is picking from one line item    *
*             instead of all line items                                      *
*----------------------------------------------------------------------------*
* MOD-004 | 19.02.2013 | Raghavendra D.V.S |  CD1K975114 | CR2155            *
*                              |CD1K975748 |  CD1K975630 | CD1K976449        *
*         | Logic change for GAC and add logic for Freight Cost              *
*----------------------------------------------------------------------------*
* MOD-005 | 02.10.2013 | Raghavendra D.V.S |  CD1K977725 | CR3057            *
*         | CD1K977733 |  CD1K977729       |  CD1K977727 | CD1K977731        *
*         | logic for calculating the values when we have credit memos       *
* MOD-006 |24.09.2014  | Dashmantha        |  CD1K983125 | CR3371            *
*         | To fix the issue related to Profit Center wise Summation         *
*----------------------------------------------------------------------------*
* MOD-007 |24.07.2015  | Shireesha        |  CD1K986306  | CR3475            *
*         | To fix the issue related to frieght charges for IR reversals     *
*----------------------------------------------------------------------------*

REPORT  yse_recovery_landed_cost_new.

TABLES: ekbe,
        bkpf,
        bseg,
        lfa1,
        lfb1,
        yse_prctr_bl,
        ce11000,
        t163c.

* ALV grid
TYPE-POOLS: slis.

* Internal table with PO history categories
DATA: BEGIN OF t_pohc  OCCURS 0,
        bewtp      TYPE bewtp,
        bewtk      TYPE bewtk,
      END OF t_pohc.

* Internal table with return for F4 (value selection)
DATA: BEGIN OF it_return_tab  OCCURS 0.
        INCLUDE STRUCTURE ddshretval.
DATA: END OF it_return_tab.

* Internal table with PO
DATA: BEGIN OF t_po  OCCURS 0,
        ebeln      TYPE ebeln,
        ebelp      TYPE ebelp,
        bewtp      TYPE bewtp,
        budat      TYPE budat,
        mblnr      TYPE mblnr,
        matnr      TYPE matnr, "MOD-001
      END OF t_po.

* Internal table with posting documents
DATA: BEGIN OF t_doc  OCCURS 0,
        belnr      TYPE belnr_d,
        ebeln      TYPE ebeln,
        ebelp      TYPE ebelp,
        bewtp      TYPE bewtp,
        budat      TYPE budat,
        bldat      TYPE bldat,
        blart      TYPE blart,
        hwaer      TYPE hwaer,
        matnr      TYPE matnr,   "MOD-001
        mblnr      TYPE mblnr,                    "MOD-002
      END OF t_doc.

* Internal table with amounts
DATA: BEGIN OF t_amt  OCCURS 0,
        ebeln      TYPE ebeln,
        belnr      TYPE belnr_d,
        segment    TYPE fb_segment,
        prctr      TYPE prctr, "+MOD-006
        bewtp      TYPE bewtp,
        budat      TYPE budat,
        bldat      TYPE bldat,
        blart      TYPE blart,
        lifnr      TYPE lifnr,
        dmbtr      TYPE dmbtr,
        hwaer      TYPE hwaer,
        shkzg      TYPE shkzg,
        matnr      TYPE matnr,   "MOD-001
        mblnr      TYPE mblnr,                    "MOD-002
        gac        TYPE prodh_d,    " MOD-004
        ebelp      TYPE ebelp,      " MOD-005
*        prctr      TYPE prctr,      " MOD-005 "- MOD-006
      END OF t_amt.
DATA: h_amt  LIKE LINE OF t_amt.

* Internal table with output lines
DATA: BEGIN OF t_out  OCCURS 0,
        ebeln      TYPE ebeln,
        ebelp      TYPE ebelp,
        belnr      TYPE belnr_d,
        segment    TYPE fb_segment,
        bewtk      TYPE bewtk,
        budat      TYPE budat,
        bldat      TYPE bldat,
        blart      TYPE blart,
        lifnr      TYPE lifnr,
        name1      TYPE name1,
        akont      TYPE akont,
        dmbtr      TYPE dmbtr,
        hwaer      TYPE hwaer,
        bukrs      TYPE bukrs,
        gjahr      TYPE gjahr,
        wesbs      TYPE wesbs,
        gac        TYPE prodh_d, "MOD-001
       END OF t_out.
DATA: h_out  LIKE LINE OF t_out.

* Start of Mod-001
TYPES: BEGIN OF ty_mara,
        matnr TYPE mara-matnr,
        prdha TYPE mara-prdha,
      END OF ty_mara.

DATA : t_mara TYPE HASHED TABLE OF ty_mara
                   WITH UNIQUE KEY matnr
                   WITH HEADER LINE.
* End of Mod-001

* Internal table with classificaton data
DATA: BEGIN OF t_clobjdat OCCURS 0.
        INCLUDE STRUCTURE clobjdat.
DATA: END OF t_clobjdat.

* Internal table with classes
DATA: BEGIN OF t_class OCCURS 0.
        INCLUDE STRUCTURE sclass.
DATA: END OF t_class.

DATA: xv_variant         LIKE disvariant,
      xv_variant_flag    TYPE c,
      xv_sd_alv_variant  LIKE disvariant,
      xt_fcat            TYPE slis_t_fieldcat_alv,
      ls_fcat            LIKE LINE OF xt_fcat,
      xt_alv_sort        TYPE slis_t_sortinfo_alv,
      xv_user_command    TYPE slis_formname  VALUE 'USER_COMMAND',
      xv_variant_save    TYPE c              VALUE 'U',
      xv_layout          TYPE slis_layout_alv.

DATA: h_text             TYPE char80,
      h_awkey            TYPE awkey,
      h_lifnr            TYPE lifnr,
      h_hkont            TYPE hkont,
      h_dmbtr            TYPE dmbtr,
      h_prctr            TYPE prctr,
      h_segment          TYPE fb_segment,
      h_shkzg            TYPE shkzg,
      h_col(10)          TYPE n,
      h_repid            LIKE sy-repid,
      h_dmbtr1           TYPE dmbtr. " Mod-003

* Selections
SELECTION-SCREEN  BEGIN OF BLOCK sel  WITH FRAME  TITLE text-s01.
PARAMETERS:  p_bukrs  TYPE bukrs  MEMORY ID buk  OBLIGATORY,
             p_gjahr  TYPE gjahr  OBLIGATORY  DEFAULT sy-datum(4).
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_bewtp  FOR ekbe-bewtp  NO INTERVALS,
                s_budat  FOR ekbe-budat,
                s_lifnr  FOR bseg-lifnr,
                s_segm   FOR yse_prctr_bl-segment,
                s_ebeln  FOR ekbe-ebeln.
SELECTION-SCREEN  END OF BLOCK sel.


*----------------------------------------------------------------------*
* On the selection screen                                              *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

  PERFORM check_authorization.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR s_bewtp-low.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield        = 'BEWTP'
      window_title    = text-h01
      value_org       = 'S'
    TABLES
      value_tab       = t_pohc
      return_tab      = it_return_tab
    EXCEPTIONS
      parameter_error = 1
      no_values_found = 2
      OTHERS          = 3.

  IF sy-subrc EQ 0.
    READ TABLE it_return_tab INDEX 1.
    s_bewtp-low = it_return_tab-fieldval.
  ENDIF.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON s_bewtp.

  CHECK NOT s_bewtp-low IS INITIAL.

  READ TABLE t_pohc WITH KEY bewtp = s_bewtp-low.

  IF sy-subrc NE 0.
    MESSAGE e398(00) WITH 'PO history category not valid:'(e01)
                          s_bewtp-low.
  ENDIF.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*
INITIALIZATION.

* Fill PO history categories.
  PERFORM fill_po_histcat.

*----------------------------------------------------------------------*
START-OF-SELECTION.

* Statistics for transaction / program
  CALL METHOD ycl_statistics=>record_transaction.

* Build & display list
  PERFORM select_data.
  PERFORM alv_fcat.
  PERFORM alv_layout.
  PERFORM alv_display.


*&---------------------------------------------------------------------*
*&  Forms                                                              *
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  AUTHORITY-CHECK OBJECT 'YAM_BUKRS'
           ID 'BUKRS' FIELD p_bukrs.

  IF sy-subrc NE 0.
    MESSAGE e101(yam_sls) WITH p_bukrs.
  ENDIF.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      FORM  FILL_PO_HISTCAT
*&---------------------------------------------------------------------*
*       Fill PO history categories
*----------------------------------------------------------------------*
FORM fill_po_histcat .

  REFRESH t_pohc.

  SELECT bewtp bewtk INTO TABLE t_pohc
         FROM t163c
         WHERE bewtp IN ('E','N','Q')
           AND spras = 'E'.
  SORT t_pohc.

ENDFORM.                    " FILL_PO_HISTCAT

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Progress indicator
  h_text = 'Data is being selected'(i01).
  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      text = h_text.

* Limit selection for PO history categories
  IF s_bewtp[] IS INITIAL.
    s_bewtp-sign   = 'I'.
    s_bewtp-option = 'EQ'.
    LOOP AT t_pohc.
      s_bewtp-low = t_pohc-bewtp.
      APPEND s_bewtp.
    ENDLOOP.
  ENDIF.

* Select PO
* Start of Mod-001
*  SELECT DISTINCT EBELN EBELP BEWTP BUDAT BELNR
  SELECT  DISTINCT ebeln ebelp bewtp budat belnr matnr
* End of Mod-001
         INTO TABLE t_po
         FROM ekbe
         WHERE ebeln IN s_ebeln
           AND bewtp IN s_bewtp
           AND budat IN s_budat
           AND gjahr = p_gjahr.
  SORT t_po.
  CHECK NOT t_po[] IS INITIAL.

* Start of Mod-001
  SELECT DISTINCT matnr prdha
                  INTO TABLE t_mara
                  FROM mara
                  FOR ALL ENTRIES IN t_po
                  WHERE matnr = t_po-matnr.
* End of Mod-001

* Select FI documents
  LOOP AT t_po.
    h_awkey(10)   = t_po-mblnr.
    h_awkey+10(4) = p_gjahr.
    SELECT SINGLE belnr bldat blart hwaer
           INTO CORRESPONDING FIELDS OF t_doc
           FROM bkpf
           WHERE awkey = h_awkey
             AND bukrs = p_bukrs
             AND gjahr = p_gjahr.
    IF sy-subrc = 0.
      MOVE-CORRESPONDING t_po TO t_doc.
      APPEND t_doc.
    ENDIF.
  ENDLOOP.
  SORT t_doc.
*  FREE t_po.                               " MOD-004
  CHECK NOT t_doc[] IS INITIAL.

* Select other document data
  LOOP AT t_doc.

*   Progress indicator
    CONCATENATE 'Document'(i02) t_doc-belnr 'is being selected'(i03)
                INTO h_text  SEPARATED BY space.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        text = h_text.

*   Document details
    CLEAR: h_lifnr, h_hkont, h_dmbtr, h_prctr, h_shkzg, h_dmbtr1.
*    begin of mod-003
*    begin of mod-005
    IF t_doc-bewtp EQ 'Q'.
      SELECT lifnr hkont dmbtr prctr shkzg   "mod-003
             INTO (h_lifnr, h_hkont, h_dmbtr1, h_prctr, h_shkzg)
             FROM bseg
             WHERE bukrs = p_bukrs
               AND belnr = t_doc-belnr
               AND gjahr = p_gjahr
               AND ebeln = t_doc-ebeln
               AND ebelp = t_doc-ebelp
               AND koart IN ('A', 'M', 'S')
*            AND shkzg IN ( 'S' ) " mod-003
               AND shkzg IN ('S', 'H'). " MOD-004.
        IF h_shkzg = 'S'.
          h_dmbtr = h_dmbtr + h_dmbtr1.   " mod-003
        ELSEIF h_shkzg = 'H'.
          h_dmbtr = h_dmbtr - h_dmbtr1.
        ENDIF.
      ENDSELECT.  " mod-003
    ELSE.
      SELECT lifnr hkont dmbtr prctr shkzg   "mod-003
       INTO (h_lifnr, h_hkont, h_dmbtr1, h_prctr, h_shkzg)
       FROM bseg
       WHERE bukrs = p_bukrs
         AND belnr = t_doc-belnr
         AND gjahr = p_gjahr
         AND ebeln = t_doc-ebeln
         AND ebelp = t_doc-ebelp
         AND koart IN ('A', 'M', 'S')
*            AND shkzg IN ( 'S' ) " mod-003
         AND shkzg IN ('S', 'H') " MOD-004.
         AND bschl NE '96'.
*  begin of mod-004
        IF h_shkzg = 'S'.
          h_dmbtr = h_dmbtr + h_dmbtr1.   " mod-003
        ELSEIF h_shkzg = 'H'.
          h_dmbtr = h_dmbtr - h_dmbtr1.
        ENDIF.
      ENDSELECT.  " mod-003
*   End of mod-004.
    ENDIF.
*   end of mod-005.

*End of mod-003

    IF sy-subrc NE 0.
      SELECT SINGLE lifnr hkont dmbtr prctr shkzg   "mod-003
            INTO (h_lifnr, h_hkont, h_dmbtr1, h_prctr, h_shkzg)
            FROM bseg
            WHERE bukrs = p_bukrs
              AND belnr = t_doc-belnr
              AND gjahr = p_gjahr
              AND ebeln = t_doc-ebeln
              AND ebelp = t_doc-ebelp
              AND koart IN ('A', 'M', 'S').
    ENDIF.


*   Vendor
    IF h_lifnr IS INITIAL.
      SELECT SINGLE lifnr INTO h_lifnr
             FROM bseg
             WHERE bukrs = p_bukrs
               AND belnr = t_doc-belnr
               AND gjahr = p_gjahr
               AND lifnr NE ' '.
    ENDIF.
*   Get segment
    SELECT SINGLE segment INTO h_segment
           FROM yse_prctr_bl
           WHERE prctr = h_prctr.

    IF sy-subrc = 0.
      CHECK h_segment IN s_segm.
      CHECK h_lifnr   IN s_lifnr.
*     Fill table with amounts
      PERFORM fill_amount.
    ENDIF.
  ENDLOOP.

  SORT t_amt.
  FREE t_doc.

* Fill output table
  PERFORM fill_output.
  SORT t_out BY ebeln bewtk belnr . " MOD-004
  PERFORM freight_logic.            " MOD-004
  FREE t_amt.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  FILL_AMOUNT
*&---------------------------------------------------------------------*
*       Fill table with amounts
*----------------------------------------------------------------------*
FORM fill_amount .

  CHECK h_dmbtr NE 0.

  CLEAR t_amt.

  MOVE-CORRESPONDING t_doc TO t_amt.
  t_amt-segment = h_segment.
  t_amt-lifnr   = h_lifnr.
  t_amt-dmbtr   = h_dmbtr.
  t_amt-shkzg   = h_shkzg.
  t_amt-prctr   = h_prctr. " mod-003
*  Begin of MOD-004.
  t_amt-gac     = h_prctr.
  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
    EXPORTING
      input  = t_amt-gac
    IMPORTING
      output = t_amt-gac.
* End of MOD-004.
  APPEND t_amt.

ENDFORM.                    " FILL_AMOUNT

*&---------------------------------------------------------------------*
*&      Form  FILL_OUTPUT
*&---------------------------------------------------------------------*
*       Fill output table
*----------------------------------------------------------------------*
FORM fill_output .
* Begin of insertion by MOD-006
  SORT t_amt BY prctr
                segment
                ebeln
                belnr ASCENDING.
* End of insertion by MOD-006
  LOOP AT t_amt.
*   New segment
*    AT NEW segment. "-MOD-006
    AT NEW prctr.    "+MOD-006
      CLEAR: t_out,
             h_dmbtr.
    ENDAT.

*   Detail.
    h_amt = t_amt.
*   Debit (S) / Credit (H)
*    Begin of Mod-005.
*    IF t_amt-shkzg = 'H'.
*      t_amt-dmbtr = t_amt-dmbtr * -1.
*    ENDIF.
*    End of Mod-005.
    h_dmbtr = h_dmbtr + t_amt-dmbtr.

*   Total per segment
*    AT END OF segment. "-MOD-006
    AT END OF prctr.   "+MOD-006
      MOVE-CORRESPONDING h_amt TO t_out.
      t_out-dmbtr = h_dmbtr.                     " MOD-005
*      t_out-dmbtr = ABS( h_dmbtr ).               " mod-004 " Commented by MOD-005
      t_out-gjahr = p_gjahr.
      t_out-bukrs = p_bukrs.
*     PO history category
      READ TABLE t_pohc WITH KEY bewtp = h_amt-bewtp.
      IF sy-subrc = 0.
        t_out-bewtk = t_pohc-bewtk.
      ELSE.
        t_out-bewtk = h_amt-bewtp.
      ENDIF.

*     Vendor data.
      SELECT SINGLE lfa1~lifnr lfa1~name1 lfb1~akont
             INTO (t_out-lifnr, t_out-name1, t_out-akont)
             FROM lfa1
             INNER JOIN lfb1
                   ON lfa1~lifnr = lfb1~lifnr
             WHERE lfa1~lifnr = h_amt-lifnr
               AND lfb1~bukrs = p_bukrs.

      APPEND t_out.
    ENDAT.

  ENDLOOP.

* Begin of MOD-004.
*** Start of Mod-001
**  LOOP AT t_out.
**    CLEAR t_amt.
**    READ TABLE t_amt WITH KEY ebeln = t_out-ebeln
**                              ebelp = t_out-ebelp
**                              belnr = t_out-belnr
**                            segment = t_out-segment.
**    IF sy-subrc = 0.
**      CLEAR t_mara.
**      READ TABLE t_mara WITH TABLE KEY matnr = t_amt-matnr.
**      IF sy-subrc = 0.
**        t_out-gac = t_mara-prdha+0(4).
**        MODIFY t_out TRANSPORTING gac.
**        CLEAR t_out.
**      ENDIF.
**    ENDIF.
**  ENDLOOP.
* End of MOD-004.
  SORT t_out BY ebeln ebelp bewtk.
* END OF  Mod-001

ENDFORM.                    " FILL_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  ALV_FCAT
*&---------------------------------------------------------------------*
*       ALV field catalog
*----------------------------------------------------------------------*
FORM alv_fcat .

** Create Fieldcatalogue from internal table
** CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE' ...

  CLEAR h_col.

* Field definitions
  PERFORM add_field USING 'EBELN'   11 'Purch.Order'(t01)          'X'.
  PERFORM add_field USING 'BELNR'   11 'Document Nr'(t02)          'X'.
  PERFORM add_field USING 'SEGMENT' 10 'Segment'(t03)              ' '.
  PERFORM add_field USING 'BEWTK'   07 'PO h.c.'(t04)              ' '.
  PERFORM add_field USING 'BUDAT'   10 'Post. Date'(t05)           ' '.
  PERFORM add_field USING 'BLDAT'   10 'Doc. Date'(t06)            ' '.
  PERFORM add_field USING 'BLART'   05 'Type'(t07)                 ' '.
  PERFORM add_field USING 'LIFNR'   11 'Vendor'(t08)               ' '.
  PERFORM add_field USING 'NAME1'   35 'Name'(t09)                 ' '.
  PERFORM add_field USING 'AKONT'   11 'Rec. Account'(t10)         ' '.
  PERFORM add_field USING 'DMBTR'   20 'Amount LC (excl.VAT)'(t12) ' '.
  PERFORM add_field USING 'HWAER'   05 'Curr.'(t13)                ' '.
  PERFORM add_field USING 'BUKRS'   06 'Comp.'(t14)                ' '.
  PERFORM add_field USING 'GJAHR'   05 'Year'(t15)                 ' '.
* Start of Mod-001
  PERFORM add_field USING 'GAC'   04 'GAC'(t16)                  ' '.       " MOD-001
* End of Mod-001


ENDFORM.                    " ALV_FCAT

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr
                         p_hotsp.

  h_col = h_col + 1.

  CLEAR ls_fcat.
  ls_fcat-col_pos    = h_col.
  ls_fcat-fieldname  = p_field.
  ls_fcat-outputlen  = p_len.
  ls_fcat-seltext_l  = p_descr.
  ls_fcat-seltext_m  = p_descr.
  ls_fcat-seltext_s  = p_descr.
  ls_fcat-fix_column = 'X'.
  ls_fcat-emphasize  = 'X'.
  ls_fcat-hotspot    = p_hotsp.

* Suppress leading zeros
  IF p_field = 'EBELN'   OR
     p_field = 'BELNR'   OR
     p_field = 'SEGMENT' OR
     p_field = 'LIFNR'   OR
     p_field = 'AKONT'.
    ls_fcat-edit_mask = '==ALPHA'.
  ENDIF.

  APPEND ls_fcat TO xt_fcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT
*&---------------------------------------------------------------------*
*       Modify ALV layout
*----------------------------------------------------------------------*
FORM alv_layout .

  xv_layout-zebra = 'X'.
*  xv_layout-colwidth_optimize = 'X'.
*  xv_layout-smalltitle = 'X'.
*  xv_layout-grid_title = 'title'.

ENDFORM.                    " ALV_LAYOUT

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       Display ALV grid
*----------------------------------------------------------------------*
FORM alv_display .

*  xv_sd_alv_variant = xv_variant.
  h_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = h_repid
      i_callback_user_command = xv_user_command
*      i_callback_top_of_page  = 'ALV_TOP'
      i_grid_title            = 'Recovery of landed costs'(h02)
      is_layout               = xv_layout
      it_fieldcat             = xt_fcat
      it_sort                 = xt_alv_sort
      i_default               = 'X'
      i_save                  = 'A'
*      is_variant              = xv_sd_alv_variant
      i_screen_start_column   = 0
      i_screen_start_line     = 0
      i_screen_end_column     = 0
      i_screen_end_line       = 0
    TABLES
      t_outtab                = t_out
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.

  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&   Form  USER_COMMAND
*&---------------------------------------------------------------------*
*    Process user command
*----------------------------------------------------------------------*
FORM user_command USING ucomm    LIKE sy-ucomm
                        selfield TYPE slis_selfield.

* Check function code
  CASE ucomm.
    WHEN '&IC1'.
*     Select hotspot
      IF NOT selfield-value IS INITIAL.
        CASE selfield-fieldname.
          WHEN 'EBELN'.
            SET PARAMETER ID 'BES' FIELD selfield-value.
            CALL TRANSACTION 'ME23N' AND SKIP FIRST SCREEN.
          WHEN 'BELNR'.
*           Show document
            CLEAR h_out.
            READ TABLE t_out INDEX selfield-tabindex INTO h_out.
            SET PARAMETER ID 'BLN' FIELD selfield-value.
            SET PARAMETER ID 'BUK' FIELD h_out-bukrs.
            SET PARAMETER ID 'GJR' FIELD h_out-gjahr.
            CALL TRANSACTION 'FB03' AND SKIP FIRST SCREEN.
        ENDCASE.
      ENDIF.
  ENDCASE.

ENDFORM.                    " USER_COMMAND
*&---------------------------------------------------------------------*
*&      Form  FREIGHT_LOGIC
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM freight_logic .

  TYPES: BEGIN OF ty_rbkp,
            belnr TYPE re_belnr,    "   Document Number of an Invoice Document
            gjahr TYPE gjahr,       "   Fiscal Year
            bukrs TYPE bukrs,       "   Company Code
            bldat TYPE bldat,       "   Document Date in Document
            budat TYPE budat,       "   Posting Date in the Document
            kursf TYPE kursf,       "   Exchange Rate
            beznk TYPE beznk,       "   Unplanned delivery costs
         END OF ty_rbkp.
  DATA: it_rbkp TYPE STANDARD TABLE OF ty_rbkp,
        wa_rbkp TYPE ty_rbkp.
  DATA: t_out_temp LIKE TABLE OF t_out.
  DATA: flag TYPE ebeln, max TYPE i.
  DATA: lv_dmbtr TYPE dmbtr. "+MOD-007
  CLEAR: flag, max.
  SELECT
    belnr
    gjahr
    bukrs
    bldat
    budat
    kursf
    beznk
    FROM rbkp
    INTO TABLE it_rbkp
    FOR ALL ENTRIES IN t_amt
    WHERE belnr = t_amt-mblnr
    AND gjahr = p_gjahr
    AND bukrs = p_bukrs.

  LOOP AT t_out.
    IF t_out-ebeln NE flag AND t_out-ebeln IS NOT INITIAL.
      t_out_temp[] = t_out[].
      flag = t_out-ebeln.
      CLEAR: t_out,t_po, wa_rbkp, max.
      DELETE t_out_temp WHERE ebeln NE flag.
      DESCRIBE TABLE t_out_temp LINES max.
      max = sy-tabix + max.
      CLEAR t_out.
      LOOP AT t_out INTO t_out WHERE ebeln = flag AND bewtk = 'IR-L'.
        READ TABLE t_out INTO t_out WITH KEY belnr = t_out-belnr bewtk = 'FR'.
        IF sy-subrc NE 0.
          READ TABLE t_amt INTO t_amt WITH KEY ebeln = t_out-ebeln belnr = t_out-belnr bewtp = 'Q'.
          READ TABLE it_rbkp INTO wa_rbkp WITH KEY belnr = t_amt-mblnr gjahr = t_out-gjahr bukrs = t_out-bukrs.
          CLEAR: lv_dmbtr. "+MOD-007
          MOVE t_out-dmbtr TO lv_dmbtr. "+MOD-007
          CLEAR: t_out-dmbtr, t_out-ebeln, t_out-bewtk.
          t_out-belnr = t_out-belnr.
          t_out-bewtk = 'FR'.
          t_out-dmbtr =  wa_rbkp-kursf * wa_rbkp-beznk .
*  Begin of insertion MOD-007
*  Check the Frieght cost with negative sign incase of credit memo IR reversals
          IF lv_dmbtr < 0.
            t_out-dmbtr = -1 * t_out-dmbtr.
          ENDIF.
*  End of insertion MOD-007
          IF t_out-dmbtr = 0.
            CONTINUE.
          ELSE.
            INSERT t_out INTO t_out INDEX max.
            max = max + 1.
          ENDIF.
        ENDIF.
      ENDLOOP.
    ENDIF.
  ENDLOOP.
ENDFORM.                    " FREIGHT_LOGIC

*Text symbol text£º
*E01:PO history category not valid:
*H01:PO history categories
*H02:Recovery of landed costs
*I01:Data is being selected
*I02:Document
*I03:is being selected
*S01:Selection
*T01:Purch.Order
*T02:Document Nr
*T03:Segment
*T04:PO h.c.
*T05:Post. Date
*T06:Doc. Date
*T07:Type
*T08:Vendor
*T09:Name
*T10:Rec. Account
*T11:Tax
*T12:Amount LC (excl.VAT)
*T13:Curr.
*T14:Comp.

*T15:Year
*Selection text£º
*P_BUKRS:D       .
*P_GJAHR:D       .
*S_BEWTP:D       .
*S_BUDAT:D       .
*S_EBELN:D       .
*S_LIFNR:D       .
*S_SEGM:D       .
