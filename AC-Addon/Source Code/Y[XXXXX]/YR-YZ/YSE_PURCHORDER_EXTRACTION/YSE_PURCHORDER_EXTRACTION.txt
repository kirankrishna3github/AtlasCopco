*&---------------------------------------------------------------------*
*& Report  YSE_PURCHORDER_EXTRACTION
*&
*&---------------------------------------------------------------------*
*& Author: Luc Mertens
*& Date  : 25.09.2015
*& Devt  : CR5374
*&---------------------------------------------------------------------*
REPORT  YSE_PURCHORDER_EXTRACTION.

************************************************************************
*                   T A B L E S                                        *
************************************************************************
TABLES: ekko, ekpo, eket, ekkn, ekbe, vbak, vbap, adrc, vbfa, mbew,
        t001.

************************************************************************
*                  I N T E R N A L   T A B L E S                       *
************************************************************************
DATA:  BEGIN OF gt_po OCCURS 0,
          ebeln        TYPE ekpo-ebeln,
          ebelp        TYPE ekpo-ebelp,
          bsart        TYPE ekko-bsart,
          lifnr        TYPE ekko-lifnr,
          matnr(18)    TYPE c,
          txz01        TYPE ekpo-txz01,
          menge        TYPE ekpo-menge,
          meins        TYPE ekpo-meins,
          eindt        TYPE eket-eindt,
          netpr        TYPE ekpo-netpr,
          waers        TYPE ekko-waers,
          werks        TYPE ekpo-werks,
          vbeln        TYPE ekkn-vbeln,
          vbelp        TYPE ekkn-vbelp,
          kostl        TYPE ekkn-kostl,
          zzconscode   TYPE ekpo-zzconscode,
          zztranspmofe TYPE ekpo-zztranspmode,
          sakto        TYPE ekkn-sakto,
          lgort        TYPE ekpo-lgort,
          kunnr        TYPE ekpo-kunnr,
          adrnr        TYPE ekpo-adrnr,
          adrn2        TYPE ekpo-adrn2,
          elikz        TYPE ekpo-elikz,
          erekz        TYPE ekpo-erekz,
          bukrs        TYPE ekpo-bukrs,
          ekorg        TYPE ekko-ekorg,
       END OF gt_po.

DATA: BEGIN OF gt_po_h OCCURS 0,          "more fields
          ebeln        TYPE ekpo-ebeln,
          ebelp        TYPE ekpo-ebelp,
          bsart        TYPE ekko-bsart,
          lifnr        TYPE ekko-lifnr,
          matnr(18)    TYPE c,
          txz01        TYPE ekpo-txz01,
          menge        TYPE ekpo-menge,
          meins        TYPE ekpo-meins,
          eindt        TYPE eket-eindt,
          netpr        TYPE ekpo-netpr,
          waers        TYPE ekko-waers,
          werks        TYPE ekpo-werks,
          vbeln        TYPE ekkn-vbeln,
          vbelp        TYPE ekkn-vbelp,
          kostl        TYPE ekkn-kostl,
          zzconscode   TYPE ekpo-zzconscode,
          zztranspmofe TYPE ekpo-zztranspmode,
          sakto        TYPE ekkn-sakto,
          lgort        TYPE ekpo-lgort,
          kunnr        TYPE ekpo-kunnr,
          adrnr        TYPE ekpo-adrnr,
          adrn2        TYPE ekpo-adrn2,
          elikz        TYPE ekpo-elikz,
          erekz        TYPE ekpo-erekz,
          bukrs        TYPE ekpo-bukrs,
          fam_code(04) TYPE c,
          po_status(80) TYPE c,
          ponosc       TYPE vbak-kvgr4,
          hname        TYPE adrc-name1,
          had1         TYPE adrc-street,
          had2         TYPE adrc-str_suppl1,
          had3(40)     TYPE c,
          pqord        TYPE ekpo-menge,
          pqrec        TYPE ekbe-menge,
          pxiqty       TYPE ekbe-menge,
          pxqgit       TYPE ekpo-menge,
          lqord        TYPE vbap-kwmeng,
          lqshp        TYPE vbfa-rfmng,
          open_so      TYPE vbap-kwmeng,
          pcqty        TYPE ekbe-menge,
          pobccd       TYPE t001-waers,
          seed_lgort   TYPE ekpo-lgort,
      END OF gt_po_h.

TYPES: BEGIN OF ty_vbfa,
         vbelv       TYPE vbfa-vbelv,
         posnv       TYPE vbfa-posnv,
         vbeln       TYPE vbfa-vbeln,
         posnn       TYPE vbfa-posnn,
         rfmng       TYPE vbfa-rfmng,
       END OF ty_vbfa,

       BEGIN OF ty_vbak,
         vbeln       TYPE vbak-vbeln,
         kvgr4       TYPE vbak-kvgr4,
         auart       TYPE vbak-auart,
       END OF ty_vbak,

       BEGIN OF ty_vbap,
         vbeln       TYPE vbap-vbeln,
         posnr       TYPE vbap-posnr,
         kwmeng      TYPE vbap-kwmeng,
       END OF ty_vbap,

       BEGIN OF ty_ekkn,
         ebeln       TYPE ekkn-ebeln,
         ebelp       TYPE ekkn-ebelp,
         kostl       TYPE ekkn-kostl,
         sakto       TYPE ekkn-sakto,
       END OF ty_ekkn,

       BEGIN OF ty_ekbe,
         ebeln       TYPE ekbe-ebeln,
         ebelp       TYPE ekbe-ebelp,
         vgabe       TYPE ekbe-vgabe,
         gjahr       TYPE ekbe-gjahr,
         belnr       TYPE ekbe-belnr,
         buzei       TYPE ekbe-buzei,
         menge       TYPE ekbe-menge,
         shkzg       TYPE ekbe-shkzg,
       END OF ty_ekbe.

DATA: gt_vbfa  TYPE STANDARD TABLE OF ty_vbfa,
      gw_vbfa  LIKE LINE OF gt_vbfa,
      gt_vbak  TYPE STANDARD TABLE OF ty_vbak,
      gw_vbak  LIKE LINE OF gt_vbak,
      gt_vbap  TYPE STANDARD TABLE OF ty_vbap,
      gw_vbap  LIKE LINE OF gt_vbap,
      gt_ekbe  TYPE STANDARD TABLE OF ty_ekbe,
      gw_ekbe  LIKE LINE OF gt_ekbe,
      gt_yse_po_class TYPE yse_po_class OCCURS 0 WITH HEADER LINE,
      wa_po    LIKE LINE OF gt_po,
      wa_po_h  LIKE LINE OF gt_po_h.

DATA: BEGIN OF gt_po_h_out OCCURS 0,
          ebeln        TYPE ekpo-ebeln,
          tab01(1)     TYPE c,
          ebelp(5)     TYPE c,
          tab02(1)     TYPE c,
          fam_code(08) TYPE c,
          tab03(1)     TYPE c,
          bsart(8)     TYPE c,
          tab04(1)     TYPE c,
          lifnr        TYPE ekko-lifnr,
          tab05(1)     TYPE c,
          matnr(18)    TYPE c,
          tab06(1)     TYPE c,
          txz01        TYPE ekpo-txz01,
          tab07(1)     TYPE c,
          menge(13)    TYPE c,
          tab08(1)     TYPE c,
          meins(3)     TYPE c,
          tab09(1)     TYPE c,
          eindt(10)    TYPE c,
          tab10(1)     TYPE c,
          netpr(11)    TYPE c,
          tab11(1)     TYPE c,
          waers(5)     TYPE c,
          tab12(1)     TYPE c,
          werks(5)     TYPE c,
          tab13(1)     TYPE c,
          vbeln        TYPE ekkn-vbeln,
          tab14(1)     TYPE c,
          vbelp(6)     TYPE c,
          tab15(1)     TYPE c,
          kostl        TYPE ekkn-kostl,
          tab16(1)     TYPE c,
          zzconscode(6) TYPE c,
          tab17(1)     TYPE c,
          zztranspmode(6) TYPE c,
          tab18(1)     TYPE c,
          sakto        TYPE ekkn-sakto,
          tab19(1)     TYPE c,
          po_status(80) TYPE c,
          tab20(1)     TYPE c,
          ponosc(6)    TYPE c,
          tab21(1)     TYPE c,
          hname        TYPE adrc-name1,
          tab22(1)     TYPE c,
          had1         TYPE adrc-street,
          tab23(1)     TYPE c,
          had2         TYPE adrc-str_suppl1,
          tab24(1)     TYPE c,
          had3(40)     TYPE c,
          tab25(1)     TYPE c,
          pqord(13)    TYPE c,
          tab26(1)     TYPE c,
          pqrec(13)    TYPE c,
          tab27(1)     TYPE c,
          pxiqty(13)   TYPE c,
          tab28(1)     TYPE c,
          pxqgit(13)   TYPE c,
          tab29(1)     TYPE c,
          lqord(13)    TYPE c,
          tab30(1)     TYPE c,
          lqshp(13)    TYPE c,
          tab31(1)     TYPE c,
          open_so(13)  TYPE c,
          tab32(1)     TYPE c,
          pcqty(13)    TYPE c,
          tab33(1)     TYPE c,
          pobccd(6)    TYPE c,
          tab34(1)     TYPE c,
          seed_lgort(10) TYPE c,
       END OF gt_po_h_out.

DATA: BEGIN OF gs_head,
          pord         TYPE ekpo-ebeln VALUE 'pord',
          tab01(1)     TYPE c VALUE '|',
          pline(5)     TYPE c VALUE 'pline',
          tab02(1)     TYPE c VALUE '|',
          fam_code(8)  TYPE c VALUE 'fam_code',
          tab03(1)     TYPE c VALUE '|',
          po_class(8)  TYPE c VALUE 'po_class',
          tab04(1)     TYPE c VALUE '|',
          pvend        TYPE ekko-lifnr VALUE 'pvend',
          tab05(1)     TYPE c VALUE '|',
          pprod        TYPE ekpo-matnr VALUE 'pprod',
          tab06(1)     TYPE c VALUE '|',
          idesc        TYPE ekpo-txz01 VALUE 'idesc',
          tab07(1)     TYPE c VALUE '|',
          open(13)     TYPE c VALUE 'open',
          tab08(1)     TYPE c VALUE '|',
          pum(3)       TYPE c VALUE 'pum',
          tab09(1)     TYPE c VALUE '|',
          lrdte(10)    TYPE c VALUE 'lrdte',
          tab10(1)     TYPE c VALUE '|',
          pscst(11)    TYPE c VALUE 'pscst',
          tab11(1)     TYPE c VALUE '|',
          pocur(5)     TYPE c VALUE 'pocur',
          tab12(1)     TYPE c VALUE '|',
          pwhse(5)     TYPE c VALUE 'pwhse',
          tab13(1)     TYPE c VALUE '|',
          pcord        TYPE ekkn-vbeln VALUE 'pcord',
          tab14(1)     TYPE c VALUE '|',
          pclin(6)     TYPE c VALUE 'pclin',
          tab15(1)     TYPE c VALUE '|',
          hprf         TYPE ekkn-kostl VALUE 'hprf',
          tab16(1)     TYPE c VALUE '|',
          poconc(6)    TYPE c VALUE 'poconc',
          tab17(1)     TYPE c VALUE '|',
          potptc(6)    TYPE c VALUE 'potptc',
          tab18(1)     TYPE c VALUE '|',
          poprf        TYPE ekkn-sakto VALUE 'poprf',
          tab19(1)     TYPE c VALUE '|',
          po_status(80) TYPE c VALUE 'po_status',
          tab20(1)     TYPE c VALUE '|',
          ponosc(6)    TYPE c VALUE 'ponosc',
          tab21(1)     TYPE c VALUE '|',
          hname        TYPE adrc-name1 VALUE 'hname',
          tab22(1)     TYPE c VALUE '|',
          had1         TYPE adrc-street VALUE 'had1',
          tab23(1)     TYPE c VALUE '|',
          had2         TYPE adrc-str_suppl1 VALUE 'had2',
          tab24(1)     TYPE c VALUE '|',
          had3(40)     TYPE c VALUE 'had3',
          tab25(1)     TYPE c VALUE '|',
          pqord(13)    TYPE c VALUE 'pqord',
          tab26(1)     TYPE c VALUE '|',
          pqrec(13)    TYPE c VALUE 'pqrec',
          tab27(1)     TYPE c VALUE '|',
          pxiqty(13)   TYPE c VALUE 'pxiqty',
          tab28(1)     TYPE c VALUE '|',
          pxqgit(13)   TYPE c VALUE 'pxqgit',
          tab29(1)     TYPE c VALUE '|',
          lqord(13)    TYPE c VALUE 'lqord',
          tab30(1)     TYPE c VALUE '|',
          lqshp(13)    TYPE c VALUE 'lqshp',
          tab31(1)     TYPE c VALUE '|',
          open_so(13)  TYPE c VALUE 'open_so',
          tab32(1)     TYPE c VALUE '|',
          pcqty(13)    TYPE c VALUE 'pcqty',
          tab33(1)     TYPE c VALUE '|',
          pobccd(6)    TYPE c VALUE 'pobccd',
          tab34(1)     TYPE c VALUE '|',
          seed_lgort(10) TYPE c VALUE 'seed_lgort',
       END OF gs_head.

************************************************************************
*                   C O N S T A N T S                                  *
************************************************************************
CONSTANTS : c_filen(8)    TYPE c          VALUE 'PURCHORD',
            c_underscore  TYPE c          VALUE '_'.

************************************************************************
*                   V A R I A B L E S                                  *
************************************************************************
DATA: g_ofile         LIKE /sapdmc/lsoinp-filename,
      g_directory(25) TYPE c VALUE '/var/load/xxx/UK/convert/'.

DATA: p_logsys        LIKE tbdlst-logsys,
      g_ersda(12)     type c,                      " YYYYMMDDHHMM
      g_postc         TYPE adrc-post_code1,
      g_city1         TYPE adrc-city1,
      g_status(80)    TYPE c,
      g_bismt         TYPE bismt,
      g_adrnr         TYPE kna1-adrnr.

************************************************************************
*       S E L E C T - O P T I O N S / P A R A M E T E R S              *
************************************************************************
SELECTION-SCREEN: BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.
SELECT-OPTIONS: s_ekorg  FOR  ekko-ekorg OBLIGATORY.
SELECT-OPTIONS: s_ebeln  FOR  ekpo-ebeln.
SELECT-OPTIONS: s_bsart  FOR  ekko-bsart.
SELECTION-SCREEN: END OF BLOCK b1.

************************************************************************
INITIALIZATION.

  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = p_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.

  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

  PERFORM select_data.

  IF NOT gt_po[] IS INITIAL.
    PERFORM data_manipulation.

*   write outputfile to application server
    CONCATENATE sy-datum sy-uzeit(4) INTO g_ersda.
    CONCATENATE c_filen g_ersda
              INTO g_ofile SEPARATED BY c_underscore.
    REPLACE 'xxx' IN g_directory WITH p_logsys(3).
    CONCATENATE g_directory g_ofile INTO g_ofile.

    PERFORM write_outputfile.
  ENDIF.

************************************************************************
*       E N D - O F - S E L E C T I O N    E V E N T                   *
************************************************************************
END-OF-SELECTION .

  WRITE: / 'Job finished'(i02).


*--- S U B R O U T I N E S --------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
FORM select_data.

  SELECT
      ekko~bsart ekpo~ebeln ekpo~ebelp
      ekpo~matnr ekpo~werks ekko~lifnr ekpo~kunnr
      ekpo~menge ekpo~meins ekpo~netpr ekko~waers
      ekpo~zztranspmode ekpo~zzconscode
      ekpo~elikz ekpo~erekz eket~eindt ekpo~adrnr ekpo~adrn2
      ekpo~bukrs ekpo~lgort ekpo~txz01 ekko~ekorg
*      ekkn~vbeln ekkn~vbelp ekkn~kostl ekkn~sakto
  INTO CORRESPONDING FIELDS OF TABLE gt_po
  FROM ekpo
  INNER JOIN  ekko  ON ekpo~ebeln =  ekko~ebeln
  INNER JOIN  eket  ON ekpo~ebeln =  eket~ebeln AND
                       ekpo~ebelp =  eket~ebelp
*  INNER JOIN  ekkn  ON ekpo~ebeln =  ekkn~ebeln AND
*                       ekpo~ebelp =  ekkn~ebelp
  WHERE
          ekpo~retpo    <>  'X'
    AND   ekko~ekorg     IN s_ekorg
    AND   ekko~bsart     IN s_bsart
    AND   ekpo~ebeln     IN s_ebeln
    AND   ekpo~loekz     EQ space
    AND   eket~etenr      = ( SELECT MIN( etenr ) FROM eket
                                                WHERE ebeln =
                                                ekpo~ebeln AND ebelp =
                                                ekpo~ebelp ).

  LOOP AT gt_po INTO wa_po.
*.. Fill Sales order number and sales order item number
    SELECT SINGLE VBELN VBELP KOSTL SAKTO
      FROM EKKN
      INTO (WA_PO-VBELN, WA_PO-VBELP, WA_PO-KOSTL, WA_PO-SAKTO)
      WHERE EBELN = WA_PO-EBELN
        AND EBELP = WA_PO-EBELP
        AND ZEKKN = ( SELECT MAX( ZEKKN ) FROM EKKN
                        WHERE EBELN = WA_PO-EBELN
                          AND EBELP = WA_PO-EBELP ).
    MODIFY gt_po FROM wa_po.
    CLEAR wa_po.
  ENDLOOP.

ENDFORM.                    "SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  DATA_MANIPULATION
*&---------------------------------------------------------------------*
FORM data_manipulation.

* Fetch Referenced Quantity
  SELECT vbelv posnv vbeln posnn rfmng
    FROM vbfa INTO TABLE gt_vbfa
    FOR ALL ENTRIES IN gt_po
    WHERE vbelv   = gt_po-vbeln
      AND posnv   = gt_po-vbelp
      AND vbtyp_n = 'R'
      AND bwart  IN ('261', '601', '   ').

  SORT gt_vbfa by vbelv posnv.

* Fetch inv. and received qty
  SELECT ebeln ebelp vgabe gjahr belnr buzei menge shkzg
    FROM ekbe INTO TABLE gt_ekbe
    FOR ALL ENTRIES IN gt_po
    WHERE ebeln = gt_po-ebeln
      AND ebelp = gt_po-ebelp
      AND vgabe in ('1', '2').

  SORT gt_ekbe by ebeln ebelp vgabe.

* Fetch customer group and order qty in sales units
  SELECT vbeln kvgr4 auart
    FROM vbak INTO TABLE gt_vbak
    FOR ALL ENTRIES IN gt_po
    WHERE vbeln = gt_po-vbeln.

  SORT gt_vbak by vbeln.

  SELECT vbeln posnr kwmeng
    FROM vbap INTO TABLE gt_vbap
    FOR ALL ENTRIES IN gt_po
    WHERE vbeln = gt_po-vbeln
      AND posnr = gt_po-vbelp.

  SORT gt_vbap by vbeln posnr.

* Fetch po_class
  SELECT * INTO TABLE gt_yse_po_class
    FROM yse_po_class.

  SORT gt_yse_po_class by auart.
*
  LOOP AT gt_po INTO wa_po.
    CLEAR wa_po_h.
    MOVE-CORRESPONDING wa_po to wa_po_h.

*.. Get address data
    CLEAR: g_adrnr, g_postc, g_city1.
    IF NOT wa_po-kunnr IS INITIAL.
      SELECT SINGLE adrnr INTO g_adrnr
        FROM kna1 WHERE kunnr = wa_po-kunnr.
    ELSEIF NOT wa_po-adrnr IS INITIAL.
      g_adrnr = wa_po-adrnr.
    ELSE.
      g_adrnr = wa_po-adrn2.
    ENDIF.

    SELECT SINGLE name1 street post_code1 str_suppl1 city1
      INTO (wa_po_h-hname, wa_po_h-had1, g_postc,
            wa_po_h-had2, g_city1)
      FROM adrc
      WHERE addrnumber EQ g_adrnr.

    CONCATENATE g_postc g_city1 INTO wa_po_h-had3 SEPARATED BY space.

*.. Read Reference quantity
    CLEAR gw_vbfa.
    READ TABLE gt_vbfa INTO gw_vbfa WITH KEY
         vbelv = wa_po-vbeln
         posnv = wa_po-vbelp
      BINARY SEARCH.

    if sy-subrc = 0.
      CLEAR gw_vbfa.
      LOOP AT gt_vbfa INTO gw_vbfa FROM sy-tabix.
        if gw_vbfa-vbelv <> wa_po-vbeln or
           gw_vbfa-posnv <> wa_po-vbelp.
          EXIT.
        endif.
        wa_po_h-lqshp = wa_po_h-lqshp + gw_vbfa-rfmng.
      ENDloop.
    ENDIF.

*.. Read inv. and received qty
    CLEAR gw_ekbe.
    READ TABLE gt_ekbe INTO gw_ekbe WITH KEY
         ebeln = wa_po-ebeln
         ebelp = wa_po-ebelp
      BINARY SEARCH.

    if sy-subrc = 0.
      CLEAR gw_ekbe.
      LOOP AT gt_ekbe INTO gw_ekbe FROM sy-tabix.
        if gw_ekbe-ebeln <> wa_po-ebeln or
           gw_ekbe-ebelp <> wa_po-ebelp.
          EXIT.
        endif.
        IF gw_ekbe-shkzg = 'H'.
          if gw_ekbe-vgabe = '1'.
            wa_po_h-pqrec = wa_po_h-pqrec - gw_ekbe-menge.
          else.
            wa_po_h-pxiqty = wa_po_h-pxiqty - gw_ekbe-menge.
            wa_po_h-pcqty = wa_po_h-pcqty - gw_ekbe-menge.
          endif.
        ELSE.
          if gw_ekbe-vgabe = '1'.
            wa_po_h-pqrec = wa_po_h-pqrec + gw_ekbe-menge.
          else.
            wa_po_h-pxiqty = wa_po_h-pxiqty + gw_ekbe-menge.
            wa_po_h-pcqty = wa_po_h-pcqty + gw_ekbe-menge.
          endif.
        ENDIF.
      ENDLOOP.
    ENDIF.

*.. Get currency based on company code
*    SELECT SINGLE waers INTO wa_po_h-pobccd
*      FROM t001 WHERE bukrs = wa_po-bukrs.
*.. Get currency from purchase order  Change on 2015/12/22
    SELECT SINGLE waers INTO wa_po_h-pobccd
      FROM ekko WHERE ebeln = wa_po-ebeln.

*.. Fetch customer group and order qty in sales units
    CLEAR: gw_vbak, gw_vbap.
    READ TABLE gt_vbak INTO gw_vbak WITH KEY
         vbeln = wa_po-vbeln
         BINARY SEARCH.

    IF sy-subrc = 0.
      wa_po_h-ponosc = gw_vbak-kvgr4.
    ENDIF.

    READ TABLE gt_vbap INTO gw_vbap WITH KEY
         vbeln = wa_po-vbeln
         posnr = wa_po-vbelp
         BINARY SEARCH.

    IF sy-subrc = 0.
      wa_po_h-lqord = gw_vbap-kwmeng.
    ENDIF.

*.. PCLIN
    IF wa_po_h-vbelp = '000000'.
      CLEAR wa_po_h-vbelp.
    ENDIF.

*.. PQORD / OPEN
    wa_po_h-pqord = wa_po-menge.
    wa_po_h-menge = wa_po-menge - wa_po_h-pqrec.

*.. PO_CLASS
    IF NOT wa_po-vbeln IS INITIAL.
      READ TABLE gt_yse_po_class WITH KEY
           auart = gw_vbak-auart
           BINARY SEARCH.

      IF sy-subrc = 0.
        wa_po_h-bsart = gt_yse_po_class-po_class.
      ENDIF.
    ENDIF.

*.. PXQGIT
    wa_po_h-pxqgit = wa_po_h-pxiqty - wa_po_h-pqrec.

*.. OPEN_SO
    wa_po_h-open_so = wa_po_h-lqord - wa_po_h-lqshp.

*.. SEED_LGORT
    wa_po_h-seed_lgort = wa_po-lgort.

*   FAMCODE
    CASE wa_po-ekorg.
      WHEN 'PL01'.
        wa_po_h-fam_code = 'PL1'.
      WHEN 'PL02'.
        wa_po_h-fam_code = 'PL2'.
      WHEN 'PL03'.
        wa_po_h-fam_code = 'PLA'.
      WHEN 'PL04'.
        wa_po_h-fam_code = 'PL4'.
    ENDCASE.

*.. Build PO_status
    CLEAR g_status.
    PERFORM build_po_status USING wa_po_h
                         CHANGING g_status.

    wa_po_h-po_status = g_status.

    APPEND wa_po_h TO gt_po_h.
    CLEAR: wa_po.
  ENDLOOP.

ENDFORM.                    "DATA_MANIPULATION

*&---------------------------------------------------------------------*
*&      Form  Write_outputfile
*&---------------------------------------------------------------------*
*       Write outputfile
*----------------------------------------------------------------------*
FORM write_outputfile.

  DATA: l_ir_compl,
        l_gr_compl.

  OPEN DATASET g_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, g_ofile.                "Open dataset failed
    EXIT.
  ENDIF.

  SORT gt_po_h BY ebeln ebelp.

  loop at gt_po_h.
*.. Take only Open PO lines
    CLEAR: l_gr_compl, l_ir_compl.
    IF gt_po_h-elikz = 'X' or ( gt_po_h-pqrec >= gt_po_h-pqord ).
      l_gr_compl = 'X'.
    ENDIF.

    IF gt_po_h-erekz = 'X' or
      ( gt_po_h-pcqty >= gt_po_h-pqord AND gt_po_h-pcqty >= gt_po_h-pqrec ).
      l_ir_compl = 'X'.
    ENDIF.

    IF l_gr_compl = 'X' AND l_ir_compl = 'X'.
      CONTINUE.
    ENDIF.

    move-corresponding gt_po_h to gt_po_h_out.
    MOVE '|' TO: gt_po_h_out-tab01, gt_po_h_out-tab02, gt_po_h_out-tab03,
                 gt_po_h_out-tab04, gt_po_h_out-tab05, gt_po_h_out-tab06,
                 gt_po_h_out-tab07, gt_po_h_out-tab08, gt_po_h_out-tab09,
                 gt_po_h_out-tab10, gt_po_h_out-tab11, gt_po_h_out-tab12,
                 gt_po_h_out-tab13, gt_po_h_out-tab14, gt_po_h_out-tab15,
                 gt_po_h_out-tab16, gt_po_h_out-tab17, gt_po_h_out-tab18,
                 gt_po_h_out-tab19, gt_po_h_out-tab20, gt_po_h_out-tab21,
                 gt_po_h_out-tab22, gt_po_h_out-tab23, gt_po_h_out-tab24,
                 gt_po_h_out-tab25, gt_po_h_out-tab26, gt_po_h_out-tab27,
                 gt_po_h_out-tab28, gt_po_h_out-tab29, gt_po_h_out-tab30,
                 gt_po_h_out-tab31, gt_po_h_out-tab32, gt_po_h_out-tab33,
                 gt_po_h_out-tab34.

    CLEAR g_bismt.
    SELECT SINGLE bismt INTO g_bismt
      FROM mara WHERE matnr = gt_po_h-matnr.

    IF NOT g_bismt IS INITIAL.
      gt_po_h_out-matnr = g_bismt.
    ENDIF.
    append gt_po_h_out.
    clear gt_po_h_out.
  endloop.

* Write heading
  TRANSFER gs_head to g_ofile.

  LOOP AT gt_po_h_out.
    TRANSFER gt_po_h_out TO g_ofile.
  ENDLOOP.

  CLOSE DATASET g_ofile.

ENDFORM.                    " Write_outputfile

*&---------------------------------------------------------------------*
*&      Form  BUILD_PO_STATUS
*&---------------------------------------------------------------------*
FORM BUILD_PO_STATUS  USING    p_wa_po_h LIKE LINE OF gt_po_h
                      CHANGING p_g_status.

  IF p_wa_po_h-pqrec = p_wa_po_h-pqord AND
     p_wa_po_h-pcqty = p_wa_po_h-pqord AND
     p_wa_po_h-pqrec <> 0 AND p_wa_po_h-pcqty <> 0.
    p_g_status =
      'OK: QUANTITY RECEIVED COMPLETELY, QUANTITY COSTED COMPLETELY'.
  ELSEIF p_wa_po_h-pqrec = p_wa_po_h-pqord AND
     p_wa_po_h-pcqty > p_wa_po_h-pqord AND
     p_wa_po_h-pqrec <> 0 AND p_wa_po_h-pcqty <> 0.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED COMPLETELY, QUANTITY COSTED EXCEEDED'.
  ELSEIF p_wa_po_h-pqrec = p_wa_po_h-pqord AND
     p_wa_po_h-pcqty < p_wa_po_h-pqord AND
     p_wa_po_h-pqrec <> 0 AND p_wa_po_h-pcqty <> 0.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED COMPLETELY, QUANTITY COSTED PARTLY'.
  ELSEIF p_wa_po_h-pqrec = p_wa_po_h-pqord AND
     p_wa_po_h-pcqty = 0 AND
     p_wa_po_h-pqrec <> 0.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED COMPLETELY, QUANTITY COSTED ZERO'.
  ELSEIF p_wa_po_h-pqrec > p_wa_po_h-pqord AND
     p_wa_po_h-pcqty = p_wa_po_h-pqord AND
     p_wa_po_h-pqrec <> 0 AND p_wa_po_h-pcqty <> 0.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED EXCEEDED, QUANTITY COSTED COMPLETELY'.
  ELSEIF p_wa_po_h-pqrec > p_wa_po_h-pqord AND
     p_wa_po_h-pcqty > p_wa_po_h-pqord AND
     p_wa_po_h-pqrec <> 0 AND p_wa_po_h-pcqty <> 0.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED EXCEEDED, QUANTITY COSTED EXCEEDED'.
  ELSEIF p_wa_po_h-pqrec > p_wa_po_h-pqord AND
     p_wa_po_h-pcqty < p_wa_po_h-pqord AND
     p_wa_po_h-pqrec <> 0 AND p_wa_po_h-pcqty <> 0.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED EXCEEDED, QUANTITY COSTED PARTLY'.
  ELSEIF p_wa_po_h-pqrec > p_wa_po_h-pqord AND
     p_wa_po_h-pcqty = 0 AND
     p_wa_po_h-pqrec <> 0.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED EXCEEDED, QUANTITY COSTED ZERO'.
  ELSEIF p_wa_po_h-pqrec < p_wa_po_h-pqord AND
     p_wa_po_h-pcqty = p_wa_po_h-pqord AND
     p_wa_po_h-pqrec <> 0 AND p_wa_po_h-pcqty <> 0.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED PARTLY, QUANTITY COSTED COMPLETELY'.
  ELSEIF p_wa_po_h-pqrec < p_wa_po_h-pqord AND
     p_wa_po_h-pcqty > p_wa_po_h-pqord AND
     p_wa_po_h-pqrec <> 0 AND p_wa_po_h-pcqty <> 0.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED PARTLY, QUANTITY COSTED EXCEEDED'.
  ELSEIF p_wa_po_h-pqrec < p_wa_po_h-pqord AND
     p_wa_po_h-pcqty < p_wa_po_h-pqord AND
     p_wa_po_h-pqrec <> 0 AND p_wa_po_h-pcqty <> 0.
    p_g_status =
      'OK: QUANTITY RECEIVED PARTLY, QUANTITY COSTED PARTLY'.
  ELSEIF p_wa_po_h-pqrec < p_wa_po_h-pqord AND
     p_wa_po_h-pcqty = 0 AND
     p_wa_po_h-pqrec <> 0.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED PARTLY, QUANTITY COSTED ZERO'.
  ELSEIF p_wa_po_h-pqrec = 0 AND
     p_wa_po_h-pcqty = p_wa_po_h-pqord AND
     p_wa_po_h-pcqty <> 0.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED ZERO, QUANTITY COSTED COMPLETELY'.
  ELSEIF p_wa_po_h-pqrec = 0 AND
     p_wa_po_h-pcqty > p_wa_po_h-pqord AND
     p_wa_po_h-pcqty <> 0.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED ZERO, QUANTITY COSTED EXCEEDED'.
  ELSEIF p_wa_po_h-pqrec = 0 AND
     p_wa_po_h-pcqty < p_wa_po_h-pqord AND
     p_wa_po_h-pcqty <> 0.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED ZERO, QUANTITY COSTED PARTLY'.
  ELSEIF p_wa_po_h-pqrec = 0 AND
     p_wa_po_h-pcqty = 0.
    p_g_status =
      'OK: QUANTITY RECEIVED ZERO, QUANTITY COSTED ZERO'.
  ELSEIF p_wa_po_h-pqrec < p_wa_po_h-pqord AND
     p_wa_po_h-pcqty < p_wa_po_h-pqord AND
     p_wa_po_h-pqrec <> 0 AND p_wa_po_h-pcqty <> 0 AND
     p_wa_po_h-pqrec < p_wa_po_h-pcqty.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED LESS THAN QUANTITY COSTED'.
  ELSEIF p_wa_po_h-pqrec < p_wa_po_h-pqord AND
     p_wa_po_h-pcqty < p_wa_po_h-pqord AND
     p_wa_po_h-pqrec <> 0 AND p_wa_po_h-pcqty <> 0 AND
     p_wa_po_h-pqrec > p_wa_po_h-pcqty.
    p_g_status =
      'PROBLEM: QUANTITY RECEIVED GREATER THAN QUANTITY COSTED'.
  ENDIF.

ENDFORM.                    " BUILD_PO_STATUS

*Text symbol text£º
*E01:No data found !
*E03:Open dataset failed:

*I02:Job finished
*Selection text£º
*S_BSART:D       .
*S_EBELN:D       .
*S_EKORG:D       .
