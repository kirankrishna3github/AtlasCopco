*----------------------------------------------------------------------*
* PROGRAM ID    : YSE_EQUI_WARRANTY_UPDATE                             *
* PROGRAM TITLE : EQUIPMENT WARRANTY EXPIRE PERIOD UPLOAD              *
* AUTHOR        : Anda Wu                                              *
* DATE          : 15/04/2013                                           *
* DEVELOPMENT ID:                                                      *
*                                                                      *
* CHANGE REQUEST NUMBER:                                               *
*                                                                      *
* Program Description: EQUIPMENT WARRANTY EXPIRE PERIOD UPLOAD         *
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
*  DATE    | NAME     |TRANSPORT  | CR# and short Description          *
*----------------------------------------------------------------------*
*15/04/2013|Anda Wu   |CD1K975612 |CR2618: Initial version             *
*----------------------------------------------------------------------*
REPORT  yse_equi_warranty_update_backu NO STANDARD PAGE HEADING LINE-SIZE 255.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:
  BEGIN OF ty_input_file,
    equnr         TYPE equi-equnr,
    cus_beg(10)   TYPE c,
    cus_end(10)   TYPE c,
    ven_beg(10)   TYPE c,
    ven_end(10)   TYPE c,
  END OF ty_input_file,

  BEGIN OF ty_atinn,
    atinn         TYPE cabn-atinn,
    atnam         TYPE cabn-atnam,
  END OF ty_atinn.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_lfile        TYPE yse_t_lfile,                 "Local file name
  gt_update       TYPE TABLE OF ty_input_file,      "Update file data
  gt_update_log   TYPE TABLE OF string.             "update process log

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_tot_lines    TYPE i,                           "Process lines
  gv_suc_lines    TYPE i,                           "Success Lines
  gv_fai_lines    TYPE i.                           "Failed lines

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_tab          TYPE c VALUE cl_abap_char_utilities=>horizontal_tab,
  gc_type_e       TYPE c VALUE 'E',
  gc_type_s       TYPE c VALUE 'S',
  gc_hifi         TYPE c VALUE '-',
  gc_colon        TYPE c VALUE ':'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.

PARAMETERS:
          p_file(1024) TYPE c LOWER CASE OBLIGATORY.  "Local File Path

SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: cb_hflg    AS CHECKBOX                    "Header text flag
              DEFAULT 'X' MODIF ID m0.
SELECTION-SCREEN COMMENT 4(48) text-c01 FOR FIELD cb_hflg.
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN END OF BLOCK b1.

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-t02.
PARAMETERS: p_edit  AS CHECKBOX DEFAULT 'X'. "space.  wcc "Switch of editable

SELECTION-SCREEN BEGIN OF BLOCK b3 WITH FRAME TITLE text-t03.

SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: cb_cwb AS CHECKBOX DEFAULT 'X'            "Begin guarantee
              MODIF ID m1.
SELECTION-SCREEN COMMENT 4(26) text-c02 FOR FIELD cb_cwb.
PARAMETERS: cb_cwe AS CHECKBOX DEFAULT 'X'            "Warranty end
              MODIF ID m1.
SELECTION-SCREEN COMMENT 50(20) text-c03 FOR FIELD cb_cwe.
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: cb_cwi AS CHECKBOX DEFAULT 'X'            "InheritWarranty
              MODIF ID m1.
SELECTION-SCREEN COMMENT 4(26) text-c04 FOR FIELD cb_cwi.
PARAMETERS: cb_cwp AS CHECKBOX DEFAULT 'X'            "Pass on warranty
              MODIF ID m1.
SELECTION-SCREEN COMMENT 50(30) text-c05 FOR FIELD cb_cwp.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK b3.

SELECTION-SCREEN BEGIN OF BLOCK b4 WITH FRAME TITLE text-t04.

SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: cb_vwb AS CHECKBOX DEFAULT 'X'            "Begin guarantee
              MODIF ID m1.
SELECTION-SCREEN COMMENT 4(26) text-c02 FOR FIELD cb_vwb.
PARAMETERS: cb_vwe AS CHECKBOX DEFAULT 'X'            "Warranty end
              MODIF ID m1.
SELECTION-SCREEN COMMENT 50(20) text-c03 FOR FIELD cb_vwe.
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: cb_vwi AS CHECKBOX DEFAULT 'X'            "InheritWarranty
              MODIF ID m1.
SELECTION-SCREEN COMMENT 4(26) text-c04 FOR FIELD cb_vwi.
PARAMETERS: cb_vwp AS CHECKBOX DEFAULT 'X'            "Pass on warranty
              MODIF ID m1.
SELECTION-SCREEN COMMENT 50(30) text-c05 FOR FIELD cb_vwp.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK b4.

SELECTION-SCREEN END OF BLOCK b2.

SELECTION-SCREEN BEGIN OF BLOCK b5 WITH FRAME TITLE text-t05.

SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS: cb_snor    AS CHECKBOX                    "Show Noraml Log
              DEFAULT 'X' MODIF ID m1.  "space - WCC
SELECTION-SCREEN COMMENT 4(48) text-c06 FOR FIELD cb_hflg.
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN END OF BLOCK b5.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initialization
  PERFORM frm_do_init.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN OUTPUT                                          *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.
* Set selection screen
  PERFORM set_screen.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN                                                 *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.
* Check if the file path and name exist
  PERFORM check_filename.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN  ON VALUE-REQUEST                               *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_file.
* Get local file path
  PERFORM select_file.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* Read local file into a internal table
  PERFORM read_file.
* Write entry to datebase table
  PERFORM write_db.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
  IF gv_tot_lines > 0.
*   WRITE process result message
    PERFORM write_log.
  ENDIF.

*&---------------------------------------------------------------------*
*&      Form  select_file
*&---------------------------------------------------------------------*
*       Get local file path
*----------------------------------------------------------------------*
FORM select_file .

  DATA:
    lv_title  TYPE string,     "Referrence title
    lt_file   TYPE filetable,  "Internal table of dialog filenames
    ls_file   TYPE file_table, "Working area for IT_FILE,
    lv_rc     TYPE i.          "Return Code

  lv_title = text-001.
  CLEAR: lt_file[],
         ls_file.
* Open local file dialog
  CALL METHOD cl_gui_frontend_services=>file_open_dialog
    EXPORTING
      window_title            = lv_title
    CHANGING
      file_table              = lt_file
      rc                      = lv_rc
    EXCEPTIONS
      file_open_dialog_failed = 1
      cntl_error              = 2
      error_no_gui            = 3
      not_supported_by_gui    = 4
      OTHERS                  = 5.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
  IF lv_rc = 1.
    READ TABLE lt_file INDEX 1 INTO ls_file.
    IF sy-subrc EQ 0.
      p_file = ls_file-filename.
    ENDIF.
  ENDIF.

ENDFORM.                    " select_file
*&---------------------------------------------------------------------*
*&      Form  READ_FILE
*&---------------------------------------------------------------------*
*       Read local file into a internal table
*----------------------------------------------------------------------*
FORM read_file .

  DATA: lv_filename TYPE string.
* Read file entires
  lv_filename = p_file.
  PERFORM get_from_pres IN PROGRAM yam_common_routines
                                    TABLES  gt_lfile
                                    USING   lv_filename
                                            ycl_se_utility=>c_ft_asc
                                            ' '.
* Delete the blank lines
  DELETE gt_lfile
    WHERE text IS INITIAL.

ENDFORM.                    " READ_FILE
*&---------------------------------------------------------------------*
*&      Form  WRITE_DB
*&---------------------------------------------------------------------*
*       Write entry to datebase table
*----------------------------------------------------------------------*
FORM write_db .

  TYPES:  BEGIN OF lty_equi,
            equnr TYPE equi-equnr,
            objnr TYPE equi-objnr,
          END OF lty_equi.
  DATA: ls_update     TYPE ty_input_file,
        lv_mess_str   TYPE string,
        lv_mess_str2  TYPE string,
        lv_lastest    TYPE c,
        lv_cus_beg    TYPE sy-datum,
        lv_cus_end    TYPE sy-datum,
        lv_ven_beg    TYPE sy-datum,
        lv_ven_end    TYPE sy-datum,
        lv_num10_1(10) TYPE n,
        lv_num10_2(10) TYPE n,
        ls_bgmkobj    TYPE bgmkobj,
        ls_bgmkobj1   TYPE bgmkobj,
        ls_bgmkobj2   TYPE bgmkobj,
        ls_equi       TYPE lty_equi.

  DATA: lt_equi       TYPE STANDARD TABLE OF lty_equi,
        lt_bgmkobj    TYPE STANDARD TABLE OF bgmkobj,
        lt_bgmkobj_n  TYPE STANDARD TABLE OF bgmkobj,
        lt_bgmkobj_o  TYPE STANDARD TABLE OF bgmkobj.
  FIELD-SYMBOLS: <lfs_update> TYPE ty_input_file.
  CONSTANTS:  lc_wtype_1 TYPE c VALUE '1',
              lc_wtype_2 TYPE c VALUE '2'.

* Do conversation
  PERFORM convert_to_struc.

* Delete header text
  IF cb_hflg IS NOT INITIAL.
    DELETE gt_update INDEX 1.
  ENDIF.

* Check if update entries is empty
  IF gt_update IS INITIAL.
    MESSAGE w000(yse_sales_log) WITH text-002.
  ENDIF.

  IF gt_update IS NOT INITIAL.
*   Make conversion of file equi-equnr
    LOOP AT gt_update ASSIGNING <lfs_update>.
      CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
        EXPORTING
          input  = <lfs_update>-equnr
        IMPORTING
          output = <lfs_update>-equnr.
    ENDLOOP.
    SELECT  equnr
            objnr
      FROM  equi
      INTO TABLE lt_equi
      FOR ALL ENTRIES IN gt_update
      WHERE equnr = gt_update-equnr.
    IF sy-subrc <> 0.
      MESSAGE w000(yse_sales_log) WITH text-003.
      LEAVE LIST-PROCESSING.
    ENDIF.
  ENDIF.

  SORT gt_update BY equnr.
  DESCRIBE TABLE gt_update LINES gv_tot_lines.
* Get old Master Warranty - Object Assignment
  IF NOT lt_equi IS INITIAL.
    SELECT *
      FROM bgmkobj
      INTO TABLE lt_bgmkobj
      FOR ALL ENTRIES IN lt_equi
      WHERE j_objnr = lt_equi-objnr.
  ENDIF.
* Update Master Warranty - Object Assignment by FM
  LOOP AT gt_update INTO ls_update.

    REFRESH:  lt_bgmkobj_n,
              lt_bgmkobj_o.
    CLEAR:  ls_equi, ls_bgmkobj1,lv_lastest,
            ls_bgmkobj2,lv_mess_str , lv_mess_str2.
    READ TABLE lt_equi  INTO ls_equi
      WITH KEY equnr = ls_update-equnr.
*   Check if the equipment exist
    IF    sy-subrc <> 0
        OR ls_equi-objnr IS INITIAL.
      CONCATENATE
            ls_update-equnr
            gc_type_e
        INTO lv_mess_str
        SEPARATED BY gc_hifi.
      lv_mess_str2 = text-013.
      CONCATENATE
            lv_mess_str
            lv_mess_str2
        INTO lv_mess_str
        SEPARATED BY gc_colon.
      APPEND lv_mess_str TO gt_update_log.
      CONTINUE.
    ELSE.
      APPEND LINES OF lt_bgmkobj TO lt_bgmkobj_o.
      DELETE lt_bgmkobj_o WHERE j_objnr <> ls_equi-objnr.
    ENDIF.

    READ TABLE lt_bgmkobj_o INTO ls_bgmkobj1
      WITH KEY j_objnr = ls_equi-objnr
               gaart   = lc_wtype_1.
    READ TABLE lt_bgmkobj_o INTO ls_bgmkobj2
      WITH KEY j_objnr = ls_equi-objnr
               gaart   = lc_wtype_2.

*   Deal with Customer Warranty
    IF   NOT cb_cwb IS INITIAL
      OR NOT cb_cwe IS INITIAL.
      CLEAR: lv_num10_1,lv_num10_2.
      lv_num10_1 = ls_update-cus_beg.
      lv_num10_2 = ls_update-cus_end.
      SHIFT lv_num10_1 LEFT DELETING LEADING '0'.
      SHIFT lv_num10_2 LEFT DELETING LEADING '0'.
      lv_cus_beg = lv_num10_1.
      lv_cus_end = lv_num10_2.

*     Check warranty start date is valid
      CALL FUNCTION 'DATE_CHECK_PLAUSIBILITY'
        EXPORTING
          date                      = lv_cus_beg
        EXCEPTIONS
          plausibility_check_failed = 1
          OTHERS                    = 2.
      IF sy-subrc <> 0.
        CONCATENATE
            ls_update-equnr
            gc_type_s
         INTO lv_mess_str
         SEPARATED BY gc_hifi.
        lv_mess_str2 = text-023.
        REPLACE '&' WITH ls_update-cus_beg
         INTO lv_mess_str2.
        CONCATENATE
              lv_mess_str
              lv_mess_str2
          INTO lv_mess_str
          SEPARATED BY gc_colon.
        APPEND lv_mess_str TO gt_update_log.
        CONTINUE.
      ENDIF.

*     Check warranty end date is valid
      CALL FUNCTION 'DATE_CHECK_PLAUSIBILITY'
        EXPORTING
          date                      = lv_cus_end
        EXCEPTIONS
          plausibility_check_failed = 1
          OTHERS                    = 2.
      IF sy-subrc <> 0.
        CONCATENATE
            ls_update-equnr
            gc_type_s
         INTO lv_mess_str
         SEPARATED BY gc_hifi.
        lv_mess_str2 = text-023.
        REPLACE '&' WITH ls_update-cus_end
         INTO lv_mess_str2.
        CONCATENATE
              lv_mess_str
              lv_mess_str2
          INTO lv_mess_str
          SEPARATED BY gc_colon.
        APPEND lv_mess_str TO gt_update_log.
        CONTINUE.
      ENDIF.
*     Check if end date is before start date
      IF lv_cus_beg > lv_cus_end.
        CONCATENATE
            ls_update-equnr
            gc_type_s
         INTO lv_mess_str
         SEPARATED BY gc_hifi.
        lv_mess_str2 = text-020.
        CONCATENATE
              lv_mess_str
              lv_mess_str2
          INTO lv_mess_str
          SEPARATED BY gc_colon.
        APPEND lv_mess_str TO gt_update_log.
        CONTINUE.
      ENDIF.
*     Check if origal data exist
      IF NOT ls_bgmkobj1 IS  INITIAL.
        CLEAR ls_bgmkobj.
        ls_bgmkobj = ls_bgmkobj1.
        ls_bgmkobj-gwldt = lv_cus_beg.
        ls_bgmkobj-gwlen = lv_cus_end.
        ls_bgmkobj-gaerb = cb_cwi.
        ls_bgmkobj-waget = cb_cwp.
        IF ls_bgmkobj <> ls_bgmkobj1.
          ls_bgmkobj-erdat    = sy-datum.
          ls_bgmkobj-erzei    = sy-uzeit.
          APPEND ls_bgmkobj TO lt_bgmkobj_n.
        ELSE.
          lv_lastest = 'X'.
        ENDIF.
      ELSE.
        ls_bgmkobj-j_objnr  = ls_equi-objnr.
        ls_bgmkobj-gaart    = lc_wtype_1.
        ls_bgmkobj-gwldt    = lv_cus_beg.
        ls_bgmkobj-gwlen    = lv_cus_end.
        ls_bgmkobj-erdat    = sy-datum.
        ls_bgmkobj-erzei    = sy-uzeit.
        ls_bgmkobj-gaerb    = cb_cwi.
        ls_bgmkobj-waget    = cb_cwp.
        APPEND ls_bgmkobj TO lt_bgmkobj_n.
      ENDIF.
    ENDIF.

*   Deal with Vendor/Manufacturer Warranty
    IF   NOT cb_vwb IS INITIAL
      OR NOT cb_vwe IS INITIAL.
      CLEAR: lv_num10_1,lv_num10_2.
      lv_num10_1 = ls_update-ven_beg.
      lv_num10_2 = ls_update-ven_end.
      SHIFT lv_num10_1 LEFT DELETING LEADING '0'.
      SHIFT lv_num10_2 LEFT DELETING LEADING '0'.
      lv_ven_beg = lv_num10_1.
      lv_ven_end = lv_num10_2.
*     Check warranty start date is valid
      CALL FUNCTION 'DATE_CHECK_PLAUSIBILITY'
        EXPORTING
          date                      = lv_ven_beg
        EXCEPTIONS
          plausibility_check_failed = 1
          OTHERS                    = 2.
      IF sy-subrc <> 0.
        CONCATENATE
            ls_update-equnr
            gc_type_s
         INTO lv_mess_str
         SEPARATED BY gc_hifi.
        lv_mess_str2 = text-023.
        REPLACE '&' WITH ls_update-ven_beg
         INTO lv_mess_str2.
        CONCATENATE
              lv_mess_str
              lv_mess_str2
          INTO lv_mess_str
          SEPARATED BY gc_colon.
        APPEND lv_mess_str TO gt_update_log.
        CONTINUE.
      ENDIF.
*     Check warranty end date is valid
      CALL FUNCTION 'DATE_CHECK_PLAUSIBILITY'
        EXPORTING
          date                      = lv_ven_end
        EXCEPTIONS
          plausibility_check_failed = 1
          OTHERS                    = 2.
      IF sy-subrc <> 0.
        CONCATENATE
            ls_update-equnr
            gc_type_s
         INTO lv_mess_str
         SEPARATED BY gc_hifi.
        lv_mess_str2 = text-023.
        REPLACE '&' WITH ls_update-ven_end
         INTO lv_mess_str2.
        CONCATENATE
              lv_mess_str
              lv_mess_str2
          INTO lv_mess_str
          SEPARATED BY gc_colon.
        APPEND lv_mess_str TO gt_update_log.
        CONTINUE.
      ENDIF.

*     Check if end date is before start date
      IF lv_ven_beg > lv_ven_end.
        CONCATENATE
            ls_update-equnr
            gc_type_s
         INTO lv_mess_str
         SEPARATED BY gc_hifi.
        lv_mess_str2 = text-020.
        CONCATENATE
              lv_mess_str
              lv_mess_str2
          INTO lv_mess_str
          SEPARATED BY gc_colon.
        APPEND lv_mess_str TO gt_update_log.
        CONTINUE.
      ENDIF.
*     Check if origal data exist
      IF NOT ls_bgmkobj2 IS  INITIAL.
        CLEAR ls_bgmkobj.
        ls_bgmkobj = ls_bgmkobj2.
        ls_bgmkobj-gwldt = lv_ven_beg.
        ls_bgmkobj-gwlen = lv_ven_end.
        ls_bgmkobj-gaerb = cb_vwi.
        ls_bgmkobj-waget = cb_vwp.
        IF ls_bgmkobj <> ls_bgmkobj2.
          ls_bgmkobj-erdat    = sy-datum.
          ls_bgmkobj-erzei    = sy-uzeit.
          APPEND ls_bgmkobj TO lt_bgmkobj_n.
        ELSE.
          IF lv_lastest IS NOT INITIAL.
            gv_suc_lines = gv_suc_lines + 1.
            IF NOT cb_snor IS INITIAL.
              CONCATENATE
                 ls_update-equnr
                 gc_type_s
              INTO lv_mess_str
              SEPARATED BY gc_hifi.
              lv_mess_str2 = text-014.
              CONCATENATE
                    lv_mess_str
                    lv_mess_str2
                INTO lv_mess_str
                SEPARATED BY gc_colon.
              APPEND lv_mess_str TO gt_update_log.
              CONTINUE.
            ENDIF.
          ENDIF.
        ENDIF.
      ELSE.
        ls_bgmkobj-j_objnr  = ls_equi-objnr.
        ls_bgmkobj-gaart    = lc_wtype_2.
        ls_bgmkobj-gwldt    = lv_ven_beg.
        ls_bgmkobj-gwlen    = lv_ven_end.
        ls_bgmkobj-erdat    = sy-datum.
        ls_bgmkobj-erzei    = sy-uzeit.
        ls_bgmkobj-gaerb    = cb_vwi.
        ls_bgmkobj-waget    = cb_vwp.
        APPEND ls_bgmkobj TO lt_bgmkobj_n.
      ENDIF.
    ENDIF.

    IF lt_bgmkobj_n IS INITIAL.
      CONTINUE.
    ENDIF.

    CALL FUNCTION 'UPDATE_OBJECT_MASTERWARRANTY_A'
      TABLES
        i_e_bgmkobj             = lt_bgmkobj_n
        bgmkobj_old             = lt_bgmkobj_o
      EXCEPTIONS
        invalid_object_number   = 1
        invalid_warranty_number = 2
        no_entry                = 3
        OTHERS                  = 4.
    IF sy-subrc <> 0.
      CLEAR: lv_mess_str , lv_mess_str2.
      CONCATENATE
            ls_update-equnr
            ls_update-cus_beg
            ls_update-cus_end
            ls_update-ven_beg
            ls_update-ven_end
        INTO lv_mess_str
        SEPARATED BY gc_hifi.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
         INTO lv_mess_str2.
      CONCATENATE
            lv_mess_str
            lv_mess_str2
        INTO lv_mess_str
        SEPARATED BY gc_colon.
      APPEND lv_mess_str TO gt_update_log.
    ELSE.
      gv_suc_lines = gv_suc_lines + 1.
      IF NOT cb_snor IS INITIAL .
        CONCATENATE
            ls_update-equnr
            gc_type_s
        INTO lv_mess_str
        SEPARATED BY gc_hifi.
        lv_mess_str2 = text-025.
        CONCATENATE
              lv_mess_str
              lv_mess_str2
          INTO lv_mess_str
          SEPARATED BY gc_colon.
        APPEND lv_mess_str TO gt_update_log.
      ENDIF.

    ENDIF.
    CLEAR: ls_update.
  ENDLOOP.
  gv_fai_lines = gv_tot_lines - gv_suc_lines.

ENDFORM.                    " WRITE_DB

*&---------------------------------------------------------------------*
*&      Form  WRITE_LOG
*&---------------------------------------------------------------------*
*       WRITE process result message
*----------------------------------------------------------------------*
FORM write_log .

  DATA: lv_mess_str  TYPE string.
  WRITE:  'Total Process Entries:'(005) , AT 30(6) gv_tot_lines,
         / 'Successful Update Entries:'(006), AT 30(6) gv_suc_lines,
         / 'Failed Update Entries:'(007), AT 30(6) gv_fai_lines.
  ULINE.

  IF  NOT gt_update_log IS INITIAL.
    LOOP AT gt_update_log INTO lv_mess_str.
      WRITE: / lv_mess_str.
    ENDLOOP.
  ENDIF.

  IF gv_tot_lines = gv_suc_lines.
    MESSAGE s000(yse_sales_log)
      WITH 'Successfully update all equipment'(008).
  ELSEif gv_tot_lines = gv_fai_lines.
    MESSAGE s000(yse_sales_log)
      WITH 'Failed to update all equipment'(009)
      DISPLAY LIKE gc_type_e.
  ELSE.
    MESSAGE s000(yse_sales_log)
      WITH 'Not all equipment are completely update'(011)
      DISPLAY LIKE gc_type_e.
  ENDIF.

ENDFORM.                    " WRITE_LOG
*&---------------------------------------------------------------------*
*&      Form  CONVERT_TO_STRUC
*&---------------------------------------------------------------------*
*       Convert internal table to structure ty_input_file
*----------------------------------------------------------------------*
FORM convert_to_struc .

  DATA:
    ls_lfile TYPE yse_s_lfile,
    lt_field TYPE TABLE OF string,
    lv_field TYPE string,
    lv_line  TYPE string,
    lflg_suc TYPE c,
    lv_mess_str  TYPE string.
  FIELD-SYMBOLS:
    <fs_tab> TYPE STANDARD TABLE,
    <fs_wa> TYPE ANY,
    <fs_field> TYPE ANY.
  DATA: lo_wa TYPE REF TO data,
        lo_itab TYPE REF TO data.
  CREATE DATA:
    lo_wa TYPE ty_input_file,
    lo_itab TYPE TABLE OF ty_input_file INITIAL SIZE 1.

  ASSIGN lo_wa->* TO <fs_wa>.
  ASSIGN lo_itab->* TO <fs_tab>.
  ASSIGN gt_update TO <fs_tab>.

  LOOP AT gt_lfile INTO ls_lfile.
    CLEAR: lt_field,
           lflg_suc.
    lv_line = ls_lfile-text.
    SPLIT lv_line AT gc_tab INTO TABLE lt_field.
    lv_mess_str = lv_line.
    REPLACE ALL OCCURRENCES OF  gc_tab
      IN lv_mess_str
      WITH gc_hifi.
    LOOP AT lt_field INTO lv_field.
      ASSIGN COMPONENT sy-tabix OF STRUCTURE <fs_wa> TO <fs_field>.
      TRY .
          <fs_field> = lv_field.
        CATCH cx_sy_conversion_no_number.
          lflg_suc = gc_type_e.
          CONCATENATE
                text-012
                lv_mess_str+4
            INTO lv_mess_str.
          APPEND lv_mess_str TO gt_update_log.
          EXIT.
      ENDTRY.
    ENDLOOP.
    IF lflg_suc IS NOT INITIAL.
      CONTINUE.
    ENDIF.
    APPEND <fs_wa> TO <fs_tab>.
    CLEAR  <fs_wa>.
  ENDLOOP.

ENDFORM.                    " CONVERT_TO_STRUC

*&---------------------------------------------------------------------*
*&      Form  CHECK_FILENAME
*&---------------------------------------------------------------------*
*       Check if the file path and name exist
*----------------------------------------------------------------------*
FORM check_filename .

  DATA:
    lv_exist TYPE c.
  CALL FUNCTION 'TMP_GUI_GET_FILE_EXIST'
    EXPORTING
      fname          = p_file
    IMPORTING
      exist          = lv_exist
    EXCEPTIONS
      fileinfo_error = 1
      OTHERS         = 2.
  IF sy-subrc <> 0 OR lv_exist IS INITIAL.
    MESSAGE e000(yse_sales_log)
      WITH 'Please enter an invaild filename'(010).
  ENDIF.

ENDFORM.                    " CHECK_FILENAME

*&---------------------------------------------------------------------*
*&      Form  FRM_DO_INIT
*&---------------------------------------------------------------------*
*       Do initialization
*----------------------------------------------------------------------*
FORM frm_do_init .

  CLEAR:
    gv_tot_lines    ,                           "Process lines
    gv_suc_lines    ,                           "Success Lines
    gv_fai_lines    .                           "Failed lines

  REFRESH:
    gt_lfile        ,                           "Local file name
    gt_update       ,                           "Update file data
    gt_update_log   .                           "update process log

ENDFORM.                    " FRM_DO_INIT
*&---------------------------------------------------------------------*
*&      Form  SET_SCREEN
*&---------------------------------------------------------------------*
*       Set selection screen
*----------------------------------------------------------------------*
FORM set_screen .

* Set PARAMETER p_edit invisible
  LOOP AT SCREEN.
    IF screen-name = 'P_EDIT'.
      screen-invisible = '1'.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.
* Set editable for parameter ['ZAM_SKILLS'],[3],[p_hflg]
  IF p_edit IS INITIAL.
    LOOP AT SCREEN.
      IF    screen-name CS '%BT02'
        AND screen-group3 = 'BLK'.
        screen-invisible = '1'.
        MODIFY SCREEN.
      ENDIF.
      IF    screen-name CS '%BT03'
        AND screen-group3 = 'BLK'.
        screen-invisible = '1'.
        MODIFY SCREEN.
      ENDIF.
      IF    screen-name CS '%BT04'
        AND screen-group3 = 'BLK'.
        screen-invisible = '1'.
        MODIFY SCREEN.
      ENDIF.
      IF    screen-name CS '%BT05'
        AND screen-group3 = 'BLK'.
        screen-invisible = '1'.
        MODIFY SCREEN.
      ENDIF.
      IF screen-group1 = 'M1'.
        screen-invisible = '1'.
*       screen-input = '0'.
        MODIFY SCREEN.
      ENDIF.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " SET_SCREEN

*Text symbol text£º
*001:Select a file for upload
*002:[No Input Data] or [Error Happen On Converting]
*003:No vaild equipment. Please check upload file
*005:Total Process Entries:
*006:Successful Update Entries:
*007:Failed Update Entries:
*008:Successfully update all equipment
*009:Failed to update all equipment
*010:Please enter an invaild filename
*011:Not all equipment are completely update
*012:Error to convert record:
*013:The equipment does not exist or object number  is blank!
*014:Now the warranty data is the last version
*020:Warranty end date before wanrranty start date
*023:The date & is not valid
*025:Successful update warranty date
*C01:First line as file header[File Upload]
*C02:Begin Guarantee
*C03:Warranty End
*C04:InheritWarranty[Default]
*C05:Pass on warranty[Default]
*C06:Show normal log(Type 'S') or not?
*T01:Input
*T02:Update Options
*T03:Customer Warranty
*T04:Vendor/Manufacture Warranty

*T05:Output
*Selection text£º
*CB_HFLG:        Characteristic name
*P_EDIT:        Parameters Editable ?
*P_FILE:        Local File
