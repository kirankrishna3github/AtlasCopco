*&---------------------------------------------------------------------*
*& Report  YSE_SEND_IDOC_AC_CONNECT_FR1                                *
*&                                                                     *
*&---------------------------------------------------------------------*
*&                                                                     *
*& AC Connect : AC Connect : Send Idocs for Freight tables             *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 03.03.2011
*  Change Request Number : CR1789
*  Transport request Nr. : CD1K963440
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME              |CORRECT. NR. | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                   | CD1K9.....  | CR....      *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_send_idoc_ac_connect_fr1  MESSAGE-ID yam_inf    .

TABLES : yse_sd_acc_fam,
         yse_sd_freight1,
         yse_sd_freight2,
         yse_sd_freight3,
         yse_sd_freight4.

DATA: gv_rate      LIKE tcurr-ukurs,
      gv_ratef     LIKE tcurr-ukurs,
      gv_typed     LIKE tcurr-kurst,
      gv_ffact(5)  TYPE n,
      gv_tfact(5)  TYPE n,
      gv_datef     TYPE datum,
      gv_dateo     TYPE datum.

DATA: i_edidc_control_comm  LIKE edidc OCCURS 1 WITH HEADER LINE,
      i_edidd_data  LIKE edidd OCCURS 0 WITH HEADER LINE .
DATA: wa_edidc      LIKE edidc.
DATA: created_idocs_fr1  LIKE sy-tabix,
      created_idocs_fr2  LIKE sy-tabix,
      created_idocs_fr3  LIKE sy-tabix,
      created_idocs_fr4  LIKE sy-tabix.

TYPES: BEGIN OF t_fam.
        INCLUDE STRUCTURE yse_sd_acc_fam.
TYPES: END OF t_fam.
DATA: it_fam TYPE HASHED TABLE OF t_fam
                  WITH UNIQUE KEY vkorg vtweg spart
                  WITH HEADER LINE.
DATA: wa_fam TYPE t_fam.

DATA: it_freight1 TYPE TABLE OF yse_sd_freight1
                  WITH HEADER LINE.
DATA: it_fr1 TYPE TABLE OF yse_e1_acc_fr1_type
                  WITH HEADER LINE.
DATA: wa_fr1 TYPE yse_e1_acc_fr1_type.

DATA: it_freight2 TYPE TABLE OF yse_sd_freight2
                  WITH HEADER LINE.
DATA: it_fr2 TYPE TABLE OF yse_e1_acc_fr2_wght
                  WITH HEADER LINE.
DATA: wa_fr2 TYPE yse_e1_acc_fr2_wght.

DATA: it_freight3 TYPE TABLE OF yse_sd_freight3
                  WITH HEADER LINE.
DATA: it_fr3 TYPE TABLE OF yse_e1_acc_fr3_ffv
                  WITH HEADER LINE.
DATA: wa_fr3 TYPE yse_e1_acc_fr3_ffv.

DATA: it_freight4 TYPE TABLE OF yse_sd_freight4
                  WITH HEADER LINE.
DATA: it_fr4 TYPE TABLE OF yse_e1_acc_fr4_fov
                  WITH HEADER LINE.
DATA: wa_fr4 TYPE yse_e1_acc_fr4_fov.

DATA: BEGIN OF it_frchc  OCCURS 0,
        vkorg   TYPE vkorg,
        vtweg   TYPE vtweg,
        spart   TYPE spart,
        frchc   TYPE yfrchc,
      END OF it_frchc.

DATA: BEGIN OF it_charge  OCCURS 0,
        vkorg        TYPE vkorg,
        vtweg        TYPE vtweg,
        spart        TYPE spart,
        frchc        TYPE yfrchc,
        zmethod_fr   TYPE zmethod_fr,
        zbasis_fr    TYPE zbasis_fr,
      END OF it_charge.

CONSTANTS : c_input           TYPE c     VALUE '0',
            c_ls(2)           TYPE c     VALUE 'LS',
            c_mestyp_fr1      LIKE edidc-mestyp VALUE 'YSE_ACC_FR1',
            c_mestyp_fr2      LIKE edidc-mestyp VALUE 'YSE_ACC_FR2',
            c_mestyp_fr3      LIKE edidc-mestyp VALUE 'YSE_ACC_FR3',
            c_mestyp_fr4      LIKE edidc-mestyp VALUE 'YSE_ACC_FR4',
            c_idoc_type_fr1   LIKE edidc-idoctp VALUE 'YSE_ACC_FR1',
            c_idoc_type_fr2   LIKE edidc-idoctp VALUE 'YSE_ACC_FR2',
            c_idoc_type_fr3   LIKE edidc-idoctp VALUE 'YSE_ACC_FR3',
            c_idoc_type_fr4   LIKE edidc-idoctp VALUE 'YSE_ACC_FR4',
            c_segment_fr1(20) TYPE c            VALUE 'YSE_E1_ACC_FR1_TYPE',
            c_segment_fr2(20) TYPE c            VALUE 'YSE_E1_ACC_FR2_WGHT',
            c_segment_fr3(20) TYPE c            VALUE 'YSE_E1_ACC_FR3_FFV',
            c_segment_fr4(20) TYPE c            VALUE 'YSE_E1_ACC_FR4_FOV',
            c_x(1)            TYPE c     VALUE 'X',
            c_v(2)            TYPE c     VALUE 'V ',
            c_004(3)          TYPE c     VALUE '004',
            c_0000(4)         TYPE c     VALUE '0000',
            c_underscore(1)   TYPE c     VALUE '_',    " Underscore
            c_sc1(3)          TYPE c     VALUE 'SC1'.

* Selection
SELECTION-SCREEN: BEGIN OF BLOCK block1 WITH FRAME TITLE text-001.
PARAMETERS: p_fr1    AS CHECKBOX  DEFAULT 'X',
            p_mes_1  LIKE tbdme-mestyp OBLIGATORY DEFAULT 'YSE_ACC_FR1'
                     MODIF ID sc1.
SELECTION-SCREEN SKIP.
PARAMETERS: p_fr2    AS CHECKBOX  DEFAULT 'X',
            p_mes_2  LIKE tbdme-mestyp OBLIGATORY DEFAULT 'YSE_ACC_FR2'
                     MODIF ID sc1.
SELECTION-SCREEN SKIP.
PARAMETERS: p_fr3    AS CHECKBOX  DEFAULT 'X',
            p_mes_3  LIKE tbdme-mestyp OBLIGATORY DEFAULT 'YSE_ACC_FR3'
                     MODIF ID sc1.
SELECTION-SCREEN SKIP.
PARAMETERS: p_fr4    AS CHECKBOX  DEFAULT 'X',
            p_mes_4  LIKE tbdme-mestyp OBLIGATORY DEFAULT 'YSE_ACC_FR4'
                     MODIF ID sc1.
SELECTION-SCREEN: END OF BLOCK block1.
SELECTION-SCREEN SKIP.
SELECTION-SCREEN: BEGIN OF BLOCK block2 WITH FRAME TITLE text-002.
SELECT-OPTIONS: s_vkorg  FOR yse_sd_freight1-vkorg  OBLIGATORY,
                s_vtweg  FOR yse_sd_freight1-vtweg,
                s_spart  FOR yse_sd_freight1-spart.
SELECTION-SCREEN: END OF BLOCK block2.

************************************************************************
*    Disable Message Type Screen Field                                 *
************************************************************************
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-group1 = c_sc1.
      screen-input   = c_input.
      MODIFY SCREEN.
      CONTINUE.
    ENDIF.
  ENDLOOP.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Back-end FAM codes
  SELECT * INTO TABLE it_fam
           FROM yse_sd_acc_fam
           WHERE vkorg IN s_vkorg
             AND vtweg IN s_vtweg
             AND spart IN s_spart.

* Freight Calculation Types
  IF NOT p_fr1 IS INITIAL.
*   Get the information
    PERFORM get_detail_fr1.
    IF it_fr1[] IS INITIAL .
      WRITE: / 'No Idocs created for Freight Calculation Types'(i01).
    ELSE.
*     Generate Idoc's
      PERFORM create_idocs_fr1 USING p_mes_1.
      WRITE : / created_idocs_fr1,
             ' Idocs created for Freight Calculation Types'(i02).
    ENDIF.
    SKIP.
  ENDIF.

* Weights for Freight Calculation
  IF NOT p_fr2 IS INITIAL.
*   Get the information
    PERFORM get_detail_fr2.
    IF it_fr2[] IS INITIAL .
      WRITE: /
         'No Idocs created for Weights for Freight Calculation'(i03).
    ELSE.
*     Generate Idoc's
      PERFORM create_idocs_fr2 USING p_mes_2.
      WRITE : / created_idocs_fr2,
             ' Idocs created for Weights for Freight Calculation'(i04).
    ENDIF.
    SKIP.
  ENDIF.

* Freight Fixed Values
  IF NOT p_fr3 IS INITIAL.
*   Get the information
    PERFORM get_detail_fr3.
    IF it_fr3[] IS INITIAL .
      WRITE: /
         'No Idocs created for Freight Fixed Values'(i05).
    ELSE.
*     Generate Idoc's
      PERFORM create_idocs_fr3 USING p_mes_3.
      WRITE : / created_idocs_fr3,
             ' Idocs created for Freight Fixed Values'(i06).
    ENDIF.
    SKIP.
  ENDIF.

* Freight Order Values
  IF NOT p_fr4 IS INITIAL.
*   Get the information
    PERFORM get_detail_fr4.
    IF it_fr4[] IS INITIAL .
      WRITE: /
         'No Idocs created for Freight Order Values'(i07).
    ELSE.
*     Generate Idoc's
      PERFORM create_idocs_fr4 USING p_mes_4.
      WRITE : / created_idocs_fr4,
             ' Idocs created for Freight Order Values'(i08).
    ENDIF.
    SKIP.
  ENDIF.

************************************************************************
*       E N D - O F - S E L E C T I O N    E V E N T                   *
************************************************************************
END-OF-SELECTION .

  WRITE: / 'Job finished'(i99).
  ULINE.


************************************************************************
*--- S U B R O U T I N E S --------------------------------------------*
************************************************************************
*&---------------------------------------------------------------------*
*&      Form  Validate_ALE_Configuration
*&---------------------------------------------------------------------*
FORM validate_ale_configuration .

  DATA: lv_create_idoc     TYPE     c .

* CHECK IF IDOC CONFIGURATION IS READY AND IDOC CAN BE PROCESSED.
  CALL FUNCTION 'ALE_MODEL_DETERMINE_IF_TO_SEND'
    EXPORTING
      message_type           = p_mes_1
    IMPORTING
      idoc_must_be_sent      = lv_create_idoc
    EXCEPTIONS
      own_system_not_defined = 1
      OTHERS                 = 2.

  IF sy-subrc <> 0.
    MESSAGE e029 WITH p_mes_1.
    EXIT.
  ENDIF.

ENDFORM.                    " Validate_ALE_Configuration

*&---------------------------------------------------------------------*
*&      Form  get_detail_fr1
*&---------------------------------------------------------------------*
*       Get the information for selected Freight Calculation Types
*----------------------------------------------------------------------*
FORM get_detail_fr1.

  CLEAR: it_freight1[], it_fr1[].

* Select Freight data based on Selection screen parameters
  SELECT * INTO TABLE it_freight1
           FROM yse_sd_freight1
           WHERE vkorg IN s_vkorg
             AND vtweg IN s_vtweg
             AND spart IN s_spart.
* Process Error - No data found
  IF sy-subrc NE 0 .
    WRITE: / 'No Freight Calculation Types selected'(e01).
    EXIT.
  ENDIF .

  SORT it_freight1.

  LOOP AT it_freight1.
    MOVE-CORRESPONDING it_freight1 TO it_fr1.
*   Sales organisation (back-end FAM)
    READ TABLE it_fam WITH TABLE KEY vkorg = it_freight1-vkorg
                                     vtweg = it_freight1-vtweg
                                     spart = it_freight1-spart.
    IF sy-subrc = 0.
      it_fr1-orgid = it_fam-famback.
    ENDIF.
    APPEND it_fr1.
  ENDLOOP.

  SORT it_fr1.

ENDFORM.                    " get_detail_fr1

*&---------------------------------------------------------------------*
*&      Form  create_idocs_fr1
*&---------------------------------------------------------------------*
*       Create Idocs for selected Freight Calculation Types
*----------------------------------------------------------------------*
FORM create_idocs_fr1  USING    message_type.

  CLEAR: created_idocs_fr1.

* FIND RECEIVING PARTNER
  SELECT SINGLE rcvprn INTO wa_edidc-rcvprn
         FROM edp13
         WHERE mestyp = c_mestyp_fr1.
* Control Record
  wa_edidc-mestyp =  c_mestyp_fr1.
  wa_edidc-idoctp =  c_idoc_type_fr1.
  wa_edidc-rcvprt =  c_ls.

  LOOP AT it_fr1 INTO wa_fr1.
    CLEAR i_edidd_data[].
    i_edidd_data-segnam  = c_segment_fr1.
    i_edidd_data-sdata   = wa_fr1.
    APPEND i_edidd_data.
    CLEAR i_edidd_data.

    IF NOT i_edidd_data[] IS INITIAL.
      CALL FUNCTION 'MASTER_IDOC_DISTRIBUTE'
        EXPORTING
          master_idoc_control            = wa_edidc
        TABLES
          communication_idoc_control     = i_edidc_control_comm
          master_idoc_data               = i_edidd_data
        EXCEPTIONS
          error_in_idoc_control          = 1
          error_writing_idoc_status      = 2
          error_in_idoc_data             = 3
          sending_logical_system_unknown = 4
          OTHERS                         = 5.

      IF sy-subrc = 0.
        created_idocs_fr1 = created_idocs_fr1 + 1.
      ENDIF.

      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.

      CALL FUNCTION 'EDI_DOCUMENT_DEQUEUE_LATER'
        EXPORTING
          docnum                 = i_edidc_control_comm-docnum
        EXCEPTIONS
          idoc_is_not_to_dequeue = 1
          OTHERS                 = 2.

    ENDIF.
  ENDLOOP.

ENDFORM.                    " create_idocs_fr1

*&---------------------------------------------------------------------*
*&      Form  get_detail_fr2
*&---------------------------------------------------------------------*
*       Get the information for selected Weights for Freight Calculation
*----------------------------------------------------------------------*
FORM get_detail_fr2.

  CLEAR: it_freight2[], it_fr2[].

* Select Freight data based on Selection screen parameters
  SELECT * INTO TABLE it_freight2
           FROM yse_sd_freight2
           WHERE vkorg IN s_vkorg
             AND vtweg IN s_vtweg
             AND spart IN s_spart.
* Process Error - No data found
  IF sy-subrc NE 0 .
    WRITE: / 'No Wheights for Freight Calculation selected'(e02).
    EXIT.
  ENDIF .

  SORT it_freight2.

  LOOP AT it_freight2.
    MOVE-CORRESPONDING it_freight2 TO it_fr2.
*   Sales organisation (back-end FAM)
    READ TABLE it_fam WITH TABLE KEY vkorg = it_freight2-vkorg
                                     vtweg = it_freight2-vtweg
                                     spart = it_freight2-spart.
    IF sy-subrc = 0.
      it_fr2-orgid = it_fam-famback.
    ENDIF.
    APPEND it_fr2.
  ENDLOOP.

  SORT it_fr2.

ENDFORM.                    " get_detail_fr2

*&---------------------------------------------------------------------*
*&      Form  create_idocs_fr2
*&---------------------------------------------------------------------*
*       Create Idocs for selected Weights for Freight Calculation
*----------------------------------------------------------------------*
FORM create_idocs_fr2  USING    message_type.

  CLEAR: created_idocs_fr2.

* FIND RECEIVING PARTNER
  SELECT SINGLE rcvprn INTO wa_edidc-rcvprn
         FROM edp13
         WHERE mestyp = c_mestyp_fr2.
* Control Record
  wa_edidc-mestyp =  c_mestyp_fr2.
  wa_edidc-idoctp =  c_idoc_type_fr2.
  wa_edidc-rcvprt =  c_ls.

  LOOP AT it_fr2 INTO wa_fr2.
    CLEAR i_edidd_data[].
    i_edidd_data-segnam  = c_segment_fr2.
    i_edidd_data-sdata   = wa_fr2.
    APPEND i_edidd_data.
    CLEAR i_edidd_data.

    IF NOT i_edidd_data[] IS INITIAL.
      CALL FUNCTION 'MASTER_IDOC_DISTRIBUTE'
        EXPORTING
          master_idoc_control            = wa_edidc
        TABLES
          communication_idoc_control     = i_edidc_control_comm
          master_idoc_data               = i_edidd_data
        EXCEPTIONS
          error_in_idoc_control          = 1
          error_writing_idoc_status      = 2
          error_in_idoc_data             = 3
          sending_logical_system_unknown = 4
          OTHERS                         = 5.

      IF sy-subrc <> 0.

      ELSE.
        created_idocs_fr2 = created_idocs_fr2 + 1.
      ENDIF.

      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.

      CALL FUNCTION 'EDI_DOCUMENT_DEQUEUE_LATER'
        EXPORTING
          docnum                 = i_edidc_control_comm-docnum
        EXCEPTIONS
          idoc_is_not_to_dequeue = 1
          OTHERS                 = 2.

    ENDIF.
  ENDLOOP.

ENDFORM.                    " create_idocs_fr2

*&---------------------------------------------------------------------*
*&      Form  get_detail_fr3
*&---------------------------------------------------------------------*
*       Get the information for selected Freight Fixed Values
*----------------------------------------------------------------------*
FORM get_detail_fr3.

  CLEAR: it_freight3[], it_fr3[],
         it_frchc[].

* Select Freight data based on Selection screen parameters
  SELECT * INTO TABLE it_freight3
           FROM yse_sd_freight3
           WHERE vkorg IN s_vkorg
             AND vtweg IN s_vtweg
             AND spart IN s_spart.
* Process Error - No data found
  IF sy-subrc NE 0 .
    WRITE: / 'No Freight Fixed Values selected'(e03).
    EXIT.
  ENDIF .

* Get Freight Charge Codes from table with Freight Calculation Types
  SELECT vkorg vtweg spart frchc INTO TABLE it_frchc
         FROM yse_sd_freight1
         WHERE vkorg     IN s_vkorg
           AND vtweg     IN s_vtweg
           AND spart     IN s_spart
           AND zmethod_fr = '2'
           AND zbasis_fr  = '2'.
  SORT it_frchc.
  DELETE ADJACENT DUPLICATES FROM it_frchc.

  SORT it_freight3.

  LOOP AT it_freight3.
    MOVE-CORRESPONDING it_freight3 TO it_fr3.
*   Sales organisation (back-end FAM)
    READ TABLE it_fam WITH TABLE KEY vkorg = it_freight3-vkorg
                                     vtweg = it_freight3-vtweg
                                     spart = it_freight3-spart.
    IF sy-subrc = 0.
      it_fr3-orgid = it_fam-famback.
    ENDIF.
*   Freight charge code
    READ TABLE it_frchc WITH KEY vkorg = it_freight3-vkorg
                                 vtweg = it_freight3-vtweg
                                 spart = it_freight3-spart
                        BINARY SEARCH.
    IF sy-subrc = 0.
      it_fr3-frchc = it_frchc-frchc.
    ENDIF.
*   Charge method and basis
    it_fr3-zmethod_fr = '2'.
    it_fr3-zbasis_fr  = '2'.
    APPEND it_fr3.
  ENDLOOP.

  SORT it_fr3.

ENDFORM.                    " get_detail_fr3

*&---------------------------------------------------------------------*
*&      Form  create_idocs_fr3
*&---------------------------------------------------------------------*
*       Create Idocs for selected Freight Fixed Values
*----------------------------------------------------------------------*
FORM create_idocs_fr3  USING    message_type.

  CLEAR: created_idocs_fr3.

* FIND RECEIVING PARTNER
  SELECT SINGLE rcvprn INTO wa_edidc-rcvprn
         FROM edp13
         WHERE mestyp = c_mestyp_fr3.
* Control Record
  wa_edidc-mestyp =  c_mestyp_fr3.
  wa_edidc-idoctp =  c_idoc_type_fr3.
  wa_edidc-rcvprt =  c_ls.

  LOOP AT it_fr3 INTO wa_fr3.
    CLEAR i_edidd_data[].
    i_edidd_data-segnam  = c_segment_fr3.
    i_edidd_data-sdata   = wa_fr3.
    APPEND i_edidd_data.
    CLEAR i_edidd_data.

    IF NOT i_edidd_data[] IS INITIAL.
      CALL FUNCTION 'MASTER_IDOC_DISTRIBUTE'
        EXPORTING
          master_idoc_control            = wa_edidc
        TABLES
          communication_idoc_control     = i_edidc_control_comm
          master_idoc_data               = i_edidd_data
        EXCEPTIONS
          error_in_idoc_control          = 1
          error_writing_idoc_status      = 2
          error_in_idoc_data             = 3
          sending_logical_system_unknown = 4
          OTHERS                         = 5.

      IF sy-subrc <> 0.

      ELSE.
        created_idocs_fr3 = created_idocs_fr3 + 1.
      ENDIF.

      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.

      CALL FUNCTION 'EDI_DOCUMENT_DEQUEUE_LATER'
        EXPORTING
          docnum                 = i_edidc_control_comm-docnum
        EXCEPTIONS
          idoc_is_not_to_dequeue = 1
          OTHERS                 = 2.

    ENDIF.
  ENDLOOP.

ENDFORM.                    " create_idocs_fr3

*&---------------------------------------------------------------------*
*&      Form  get_detail_fr4
*&---------------------------------------------------------------------*
*       Get the information for selected Freight Order Values
*----------------------------------------------------------------------*
FORM get_detail_fr4.

  CLEAR: it_freight4[], it_fr4[],
         it_charge[].

* Select Freight data based on Selection screen parameters
  SELECT * INTO TABLE it_freight4
           FROM yse_sd_freight4
           WHERE vkorg IN s_vkorg
             AND vtweg IN s_vtweg
             AND spart IN s_spart.
* Process Error - No data found
  IF sy-subrc NE 0 .
    WRITE: / 'No Freight Order Values selected'(e04).
    EXIT.
  ENDIF .

* Get Charge Method & Basis from table with Freight Calculation Types
  SELECT vkorg vtweg spart frchc zmethod_fr zbasis_fr
         INTO TABLE it_charge
         FROM yse_sd_freight1
         WHERE vkorg     IN s_vkorg
           AND vtweg     IN s_vtweg
           AND spart     IN s_spart.
  SORT it_charge.
  DELETE ADJACENT DUPLICATES FROM it_charge.

  SORT it_freight4.

  LOOP AT it_freight4.
    MOVE-CORRESPONDING it_freight4 TO it_fr4.
*   Sales organisation (back-end FAM)
    READ TABLE it_fam WITH TABLE KEY vkorg = it_freight4-vkorg
                                     vtweg = it_freight4-vtweg
                                     spart = it_freight4-spart.
    IF sy-subrc = 0.
      it_fr4-orgid = it_fam-famback.
    ENDIF.
*   Charge method and basis
    READ TABLE it_charge WITH KEY vkorg = it_freight4-vkorg
                                  vtweg = it_freight4-vtweg
                                  spart = it_freight4-spart
                                  frchc = it_freight4-frchc
                        BINARY SEARCH.
    IF sy-subrc = 0.
      it_fr4-zmethod_fr = it_charge-zmethod_fr.
      it_fr4-zbasis_fr  = it_charge-zbasis_fr.
    ENDIF.
    APPEND it_fr4.
  ENDLOOP.

  SORT it_fr4.

ENDFORM.                    " get_detail_fr4

*&---------------------------------------------------------------------*
*&      Form  create_idocs_fr4
*&---------------------------------------------------------------------*
*       Create Idocs for selected Freight Order Values
*----------------------------------------------------------------------*
FORM create_idocs_fr4  USING    message_type.

  CLEAR: created_idocs_fr4.

* FIND RECEIVING PARTNER
  SELECT SINGLE rcvprn INTO wa_edidc-rcvprn
         FROM edp13
         WHERE mestyp = c_mestyp_fr4.
* Control Record
  wa_edidc-mestyp =  c_mestyp_fr4.
  wa_edidc-idoctp =  c_idoc_type_fr4.
  wa_edidc-rcvprt =  c_ls.

  LOOP AT it_fr4 INTO wa_fr4.
    CLEAR i_edidd_data[].
    i_edidd_data-segnam  = c_segment_fr4.
    i_edidd_data-sdata   = wa_fr4.
    APPEND i_edidd_data.
    CLEAR i_edidd_data.

    IF NOT i_edidd_data[] IS INITIAL.
      CALL FUNCTION 'MASTER_IDOC_DISTRIBUTE'
        EXPORTING
          master_idoc_control            = wa_edidc
        TABLES
          communication_idoc_control     = i_edidc_control_comm
          master_idoc_data               = i_edidd_data
        EXCEPTIONS
          error_in_idoc_control          = 1
          error_writing_idoc_status      = 2
          error_in_idoc_data             = 3
          sending_logical_system_unknown = 4
          OTHERS                         = 5.

      IF sy-subrc <> 0.

      ELSE.
        created_idocs_fr4 = created_idocs_fr4 + 1.
      ENDIF.

      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.

      CALL FUNCTION 'EDI_DOCUMENT_DEQUEUE_LATER'
        EXPORTING
          docnum                 = i_edidc_control_comm-docnum
        EXCEPTIONS
          idoc_is_not_to_dequeue = 1
          OTHERS                 = 2.

    ENDIF.
  ENDLOOP.

ENDFORM.                    " create_idocs_fr4

*Text symbol text£º
*001:IDOC Selection
*002:Data Selection
*E01:No Freight Calculation Types selected
*E02:No Wheights for Freight Calculation selected
*E03:No Freight Fixed Values selected
*E04:No Freight Order Values selected
*I01:No Idocs created for Freight Calculation Types
*I02: Idocs created for Freight Calculation Types
*I03:No Idocs created for Weights for Freight Calculation
*I04: Idocs created for Weights for Freight Calculation
*I05:No Idocs created for Freight Fixed Values
*I06: Idocs created for Freight Fixed Values
*I07:No Idocs created for Freight Order Values
*I08: Idocs created for Freight Order Values

*I99:Job finished
*Selection text£º
*P_FR1:        Freight Calculation Types
*P_FR2:        Weights for Freight Calcul.
*P_FR3:        Freight Fixed Values
*P_FR4:        Freight Order Values
*P_MES_1:        Message Type
*P_MES_2:        Message Type
*P_MES_3:        Message Type
*P_MES_4:        Message Type
*S_SPART:D       .
*S_VKORG:D       .
*S_VTWEG:D       .
