*&---------------------------------------------------------------------*
*& Report  YSE_FI_RECLASS_3115_3116
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& Reclass GPS code 3111 to 3115 & 3116 (SEED)                         *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 30.09.2011
*  Change Request Number : CR2012
*  Transport request Nr. : CD1K968166
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME           | CORRECTION NR. | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 27.03.2012 | J. Smets       | CD1K970961     | CR2012      *
*         | Add ProfitCenter                                           *
*----------------------------------------------------------------------*
* MOD-002 | 02.08.2012 | Nanda  S       | CD1K972776     | CR2629      *
*         | Adjust period of posting for SHTA and HKGA                 *
*----------------------------------------------------------------------*
* MOD-003 | 17.08.2012 | Nanda  S       | CD1K972945     | CR2630      *
*         | Correction of missing and wrong profit centers             *
*----------------------------------------------------------------------*
* MOD-004 | 16.03.2015 | Anda Wu        | CD1K984960     | CR3544      *
*         | bugfix -hardcode period unlock for CJAL                    *
*----------------------------------------------------------------------*


************************************************************************

REPORT  YSE_FI_RECLASS_3115_3116 NO STANDARD PAGE HEADING
                                 LINE-SIZE 100.

TABLES: BSIS,
        BSAS.

DATA: BEGIN OF GT_BSIS  OCCURS 0,
         BLART     TYPE BLART,
         PRCTR     TYPE PRCTR,                              "MOD-001
         SEGMENT   TYPE FB_SEGMENT,
         BELNR     TYPE BELNR_D,
         GJAHR     TYPE GJAHR,
         BUZEI     TYPE BUZEI,
         BUDAT     TYPE BUDAT,
         DMBTR     TYPE DMBTR,
         SHKZG     TYPE SHKZG,
       END OF GT_BSIS.

* Document data
DATA: BEGIN OF GT_DOC  OCCURS 0,
         BELNR       TYPE BELNR_D,
         BLART       TYPE BLART,
         PRCTR       TYPE PRCTR,                            "MOD-001
         SEGMENT     TYPE FB_SEGMENT,
         VALUE_LC    TYPE WOGXXX,
      END OF GT_DOC.

DATA: I_BDCDATA LIKE BDCDATA  OCCURS 0  WITH HEADER LINE,
      STRUCT_BDCDATA TYPE BDCDATA,
      GT_ERR    LIKE BDCMSGCOLL  OCCURS 0  WITH HEADER LINE.

DATA: GV_WAERS_CC  TYPE WAERS,
      GV_DATE_EOM  TYPE DATUM,
      GV_VALUE_LC  TYPE WOGXXX,
      GV_XINTB     TYPE XFELD,
      GV_HKONT     TYPE HKONT,
      GV_MSGTXT    LIKE T100-TEXT,
      GV_ERRLST    TYPE XFELD     VALUE 'X',
      GV_DETLST    TYPE XFELD     VALUE ' ',
      GV_MODE(1)   TYPE C         VALUE 'N'.

CONSTANTS: GC_SA       TYPE BLART       VALUE 'SA',
           GC_ZZ       TYPE BLART       VALUE 'ZZ',
           GC_ZS       TYPE MWSKZ       VALUE 'ZS',
           GC_SEG1410  TYPE FB_SEGMENT  VALUE '0000001410'.


* Begin of Change MOD-002
DATA :     GC_SHTA(4)    TYPE C VALUE 'SHTA',
           GC_HKGA(4)    TYPE C VALUE 'HKGA',
* Begin of Change MOD-004  add
           GC_CJAL(4)    TYPE C VALUE 'CJAL',
* End of Change MOD-004    add
           GC_S(1)       TYPE C VALUE 'S',
           GC_3000000(10) TYPE C VALUE '0003000000',
           GC_3499999(10) TYPE C VALUE '0003499999'.


DATA :     GV_FRMON(2)      TYPE N,
           GV_FRYR(4)       TYPE N,
           GV_TOMON(2)      TYPE N,
           GV_TOYR(4)       TYPE N.

DATA : GT_T001B TYPE TABLE OF T001B WITH HEADER LINE.
* End of Change MOD-002


* Begin of Change MOD-003
TYPES: BEGIN OF TY_BSIS_NEW,
         BLART     TYPE BLART,
         PRCTR     TYPE PRCTR,
         SEGMENT   TYPE FB_SEGMENT,
         BELNR     TYPE BELNR_D,
         GJAHR     TYPE GJAHR,
         BUZEI     TYPE BUZEI,
         BUDAT     TYPE BUDAT,
         DMBTR     TYPE DMBTR,
         SHKZG     TYPE SHKZG,
         DOCLN     TYPE FAGLFLEXA-DOCLN,
       END OF TY_BSIS_NEW.

TYPES: BEGIN OF TY_FAGLFLEXA,
        RYEAR TYPE FAGLFLEXA-RYEAR,
        DOCNR TYPE FAGLFLEXA-DOCNR,
        RLDNR TYPE FAGLFLEXA-RLDNR,
        RBUKRS TYPE FAGLFLEXA-RBUKRS,
        DOCLN TYPE FAGLFLEXA-DOCLN,
        PRCTR TYPE FAGLFLEXA-PRCTR,
        SEGMENT TYPE FAGLFLEXA-SEGMENT,
      END OF TY_FAGLFLEXA.

TYPES: BEGIN OF TY_BSAS_PRCTR,
          BUKRS TYPE BSAS-BUKRS,
          HKONT TYPE BSAS-HKONT,
          GJAHR TYPE BSAS-GJAHR,
          BELNR TYPE BSAS-BELNR,
          PRCTR TYPE BSAS-PRCTR,
          SEGMENT TYPE BSAS-SEGMENT,
 END OF TY_BSAS_PRCTR.

TYPES: BEGIN OF TY_BSEG_PRCTR,
        BUKRS TYPE BSEG-BUKRS,
        BELNR TYPE BSEG-BELNR,
        GJAHR TYPE BSEG-GJAHR,
        BUZEI TYPE BSEG-BUZEI,
        VBEL2 TYPE BSEG-VBEL2,
        POSN2 TYPE BSEG-POSN2,
        HKONT TYPE BSEG-HKONT,
END OF TY_BSEG_PRCTR.

TYPES: BEGIN OF TY_VBAP_PRCTR,
        VBELN TYPE VBAP-VBELN,
        POSNR TYPE VBAP-POSNR,
        PRODH TYPE VBAP-PRODH,
END OF TY_VBAP_PRCTR.

TYPES: BEGIN OF TY_FAGL_SEGM,
        SEGMENT TYPE FAGL_SEGM-SEGMENT,
      END OF TY_FAGL_SEGM.




DATA : GT_FAGLFLEXA      TYPE TABLE OF TY_FAGLFLEXA WITH HEADER LINE,
       GT_BSIS_MISSING   TYPE TABLE OF TY_BSIS_NEW   WITH HEADER LINE,
       GT_BSIS_WRNGAB    TYPE TABLE OF TY_BSIS_NEW   WITH HEADER LINE,
       GT_BSIS_WRNGRVZZ  TYPE TABLE OF TY_BSIS_NEW   WITH HEADER LINE,
       GT_BSAS_PRCTR     TYPE TABLE OF TY_BSAS_PRCTR WITH HEADER LINE,
       GT_BSEG_PRCTR     TYPE TABLE OF TY_BSEG_PRCTR WITH HEADER LINE,
       GT_VBAP_PRCTR     TYPE TABLE OF TY_VBAP_PRCTR WITH HEADER LINE,
       GT_FAGL_SEGM      TYPE TABLE OF TY_FAGL_SEGM  WITH HEADER LINE.


DATA:  GC_0L(2)          TYPE C VALUE '0L',
       GV_DOCLN          TYPE FAGLFLEXA-DOCLN,
       GC_AB(2)          TYPE C VALUE 'AB',
       GC_RV(2)          TYPE C VALUE 'RV',
       GV_INDEX          TYPE I.
* End of Change MOD-003

* Selections
SELECTION-SCREEN  BEGIN OF BLOCK SEL  WITH FRAME  TITLE TEXT-S01.
PARAMETERS: P_BUKRS   TYPE BUKRS  OBLIGATORY
                                  MEMORY ID BUK.
SELECT-OPTIONS: S_BUDAT  FOR BSIS-BUDAT  NO-EXTENSION.
SELECTION-SCREEN SKIP.
PARAMETERS: P_TEST AS CHECKBOX  DEFAULT 'X'.
SELECTION-SCREEN  END OF BLOCK SEL.
* G/L Accounts
SELECTION-SCREEN  BEGIN OF BLOCK ACC  WITH FRAME  TITLE TEXT-S02.
PARAMETERS:     P_ACC1     TYPE HKONT  DEFAULT '0002997901'  OBLIGATORY
                                       MODIF ID NUP,
                P_ACC2     TYPE HKONT  DEFAULT '0001780002'  OBLIGATORY
                                       MODIF ID NUP,
                P_ACC3     TYPE HKONT  DEFAULT '0003210911'  OBLIGATORY
                                       MODIF ID NUP,
                P_ACC4     TYPE HKONT  DEFAULT '0003290901'  OBLIGATORY
                                       MODIF ID NUP,
                P_ACC5     TYPE HKONT  DEFAULT '0003292901'  OBLIGATORY
                                       MODIF ID NUP.
SELECTION-SCREEN  END OF BLOCK ACC.


*----------------------------------------------------------------------*
* On the selection screen                                              *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF SCREEN-GROUP1 = 'NUP'.
      SCREEN-INPUT = 0.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON P_BUKRS.

  PERFORM  CHECK_AUTHORIZATION.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*

INITIALIZATION.

  S_BUDAT-LOW = SY-DATUM.
  S_BUDAT-LOW+6(2) = '01'.
  CALL FUNCTION 'DATE_GET_MONTH_LASTDAY'
    EXPORTING
      I_DATE = S_BUDAT-LOW
    IMPORTING
      E_DATE = GV_DATE_EOM.
  S_BUDAT-HIGH = GV_DATE_EOM.
  S_BUDAT-OPTION = 'BT'.
  S_BUDAT-SIGN   = 'I'.
  APPEND S_BUDAT.


*----------------------------------------------------------------------*
START-OF-SELECTION.

  PERFORM INITIAL.

* Select data
  PERFORM SELECT_DATA.

* Check anything selected
  IF GT_BSIS[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No documents selected'(E01).
    EXIT.
  ENDIF.

* Begin of Change MOD-003
  PERFORM CORRECT_DATA.
* End of Change MOD-003

* Begin of Change MOD-002
*           Check the company codes as this is only for SHTA and HKGA company codes.
  IF P_BUKRS = GC_SHTA  OR P_BUKRS = GC_HKGA
* Begin of Change MOD-004
    OR P_BUKRS = GC_CJAL.
* End of Change MOD-004
    PERFORM START_PERIOD_ADJ_FOR_SHTA_HKGA.
  ENDIF.
* End of Change MOD-002

* Process data
  PERFORM PROCESS_DATA.

* Begin of Change MOD-002
*         Check the company codes as this is only for SHTA and HKGA company codes.
  IF P_BUKRS = GC_SHTA  OR P_BUKRS = GC_HKGA
* Begin of Change MOD-004
    OR P_BUKRS = GC_CJAL.
* End of Change MOD-004
    PERFORM END_PERIOD_ADJ_FOR_SHTA_HKGA.
  ENDIF.
* End of Change MOD-002

* List data
  PERFORM LIST_DATA.


*----------------------------------------------------------------------*
TOP-OF-PAGE.

  WRITE: /01 'Date:', SY-DATUM,
          25 'Reclass GPS code 3111 to 3115 & 3116 (SEED)',
          75 'Company:', P_BUKRS,
          97(4) SY-PAGNO.
  ULINE.

  IF NOT GV_ERRLST IS INITIAL.
    WRITE: /01 'Errors'.
  ELSE.
    WRITE: /01 'Document',
            13 'Type',
            22 'Prof.Ctr',
            35 'Segment',
            48 '        Posted Value ',
            70 'Curr.'.
  ENDIF.

  ULINE.


*----------------------------------------------------------------------*
*       FORMS
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM CHECK_AUTHORIZATION .

  AUTHORITY-CHECK OBJECT 'F_BKPF_BUK'
           ID 'ACTVT' DUMMY
           ID 'BUKRS' FIELD P_BUKRS.

  IF SY-SUBRC = 4.
*     No authorisation to display data
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH P_BUKRS.
    EXIT.
  ELSEIF SY-SUBRC <> 0.
*     Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
    EXIT.
  ENDIF.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  INITIAL
*&---------------------------------------------------------------------*
*       Initialization
*----------------------------------------------------------------------*
FORM INITIAL .

* Company currency
  SELECT SINGLE WAERS INTO GV_WAERS_CC
         FROM T001
         WHERE BUKRS = P_BUKRS.
  IF SY-SUBRC NE 0.
  ENDIF.

* Posting date
  IF P_TEST IS INITIAL.
    READ TABLE S_BUDAT INDEX 1.
    IF SY-SUBRC = 0.
      CALL FUNCTION 'DATE_GET_MONTH_LASTDAY'
        EXPORTING
          I_DATE = S_BUDAT-HIGH
        IMPORTING
          E_DATE = GV_DATE_EOM.
    ENDIF.
  ENDIF.

ENDFORM.                    " INITIAL

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM SELECT_DATA .

* Open items (G/L accounts)
*  SELECT blart segment belnr                               "MOD-001
  SELECT BLART PRCTR SEGMENT BELNR                          "MOD-001
         GJAHR BUZEI BUDAT DMBTR SHKZG
         INTO TABLE GT_BSIS
         FROM BSIS
         WHERE BUKRS   =  P_BUKRS
           AND HKONT   IN (P_ACC1, P_ACC2)
           AND BUDAT   IN S_BUDAT
*           AND blart   NE gc_sa
           AND NOT ( BLART = GC_SA AND ZUONR = 'LT/ST Split' )
           AND SEGMENT NE GC_SEG1410.

* Cleared items (G/L accounts)
*  SELECT blart segment belnr                               "MOD-001
  SELECT BLART PRCTR SEGMENT BELNR                          "MOD-001
         GJAHR BUZEI BUDAT DMBTR SHKZG
         APPENDING TABLE GT_BSIS
         FROM BSAS
         WHERE BUKRS   =  P_BUKRS
           AND HKONT   IN (P_ACC1, P_ACC2)
           AND BUDAT   IN S_BUDAT
*           AND blart   NE gc_sa
           AND NOT ( BLART = GC_SA AND ZUONR = 'LT/ST Split' )
           AND SEGMENT NE GC_SEG1410.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  PROCESS_DATA
*&---------------------------------------------------------------------*
*       Process data
*----------------------------------------------------------------------*
FORM PROCESS_DATA .

  SORT GT_BSIS BY BLART
                  PRCTR                                     "MOD-001
                  SEGMENT
                  BELNR.

  LOOP AT GT_BSIS.

*   Begin
    AT FIRST.
*     Open accounts
      GV_XINTB = ' '.
      GV_HKONT = P_ACC3.
      PERFORM CALL_FSS0.
      GV_HKONT = P_ACC4.
      PERFORM CALL_FSS0.
      GV_HKONT = P_ACC5.
      PERFORM CALL_FSS0.
    ENDAT.

*   New segment
    AT NEW SEGMENT.
      CLEAR GV_VALUE_LC.
    ENDAT.

*   Detail.
    IF GT_BSIS-SHKZG = 'S'.
      GV_VALUE_LC = GV_VALUE_LC + GT_BSIS-DMBTR.
    ELSE.
      GV_VALUE_LC = GV_VALUE_LC - GT_BSIS-DMBTR.
    ENDIF.

*   End of segment
    AT END OF SEGMENT.
      IF GV_VALUE_LC NE 0.
        CLEAR GT_DOC.
        IF P_TEST IS INITIAL.
          PERFORM POST_RECLASS.
        ENDIF.
        GT_DOC-BLART    = GT_BSIS-BLART.
        GT_DOC-PRCTR    = GT_BSIS-PRCTR.                    "MOD-001
        GT_DOC-SEGMENT  = GT_BSIS-SEGMENT.
        GT_DOC-VALUE_LC = GV_VALUE_LC.
        APPEND GT_DOC.
      ENDIF.
    ENDAT.

*   End
    AT LAST.
*     Close accounts
      GV_XINTB = 'X'.
      GV_HKONT = P_ACC3.
      PERFORM CALL_FSS0.
      GV_HKONT = P_ACC4.
      PERFORM CALL_FSS0.
      GV_HKONT = P_ACC5.
      PERFORM CALL_FSS0.
    ENDAT.

  ENDLOOP.

ENDFORM.                    " PROCESS_DATA

*&---------------------------------------------------------------------*
*&      Form  CALL_FSS0
*&---------------------------------------------------------------------*
*       Prepare & Call transaction FSS0
*----------------------------------------------------------------------*
FORM CALL_FSS0 .

  REFRESH: I_BDCDATA, GT_ERR.

  PERFORM BDC_DYNPRO      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE' '=ACC_MOD'.
  PERFORM BDC_FIELD       USING 'GLACCOUNT_SCREEN_KEY-SAKNR' GV_HKONT.
  PERFORM BDC_FIELD       USING 'GLACCOUNT_SCREEN_KEY-BUKRS' P_BUKRS.

  PERFORM BDC_DYNPRO      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE' '=TAB02'.

  PERFORM BDC_DYNPRO      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE' '=SAVE'.
  PERFORM BDC_FIELD       USING 'GLACCOUNT_SCREEN_CCODE-XINTB' GV_XINTB.

  CALL TRANSACTION 'FSS0' USING I_BDCDATA
                          MODE GV_MODE
                          UPDATE 'S'
                          MESSAGES INTO GT_ERR.

* Check for errors
  LOOP AT GT_ERR WHERE MSGTYP = 'E'
                    OR MSGTYP = 'A'.
    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*        SPRSL                   = SY-LANGU
        ARBGB                   = GT_ERR-MSGID
        MSGNR                   = GT_ERR-MSGNR
        MSGV1                   = GT_ERR-MSGV1
        MSGV2                   = GT_ERR-MSGV2
        MSGV3                   = GT_ERR-MSGV3
        MSGV4                   = GT_ERR-MSGV4
      IMPORTING
        MSGTEXT                 = GV_MSGTXT
      EXCEPTIONS
        MESSAGE_NOT_FOUND       = 1
        OTHERS                  = 2.

    WRITE: / GV_MSGTXT.
  ENDLOOP.

ENDFORM.                    " CALL_FSS0

*----------------------------------------------------------------------*
*        Start new screen                                              *
*----------------------------------------------------------------------*
FORM BDC_DYNPRO USING PROGRAM DYNPRO.

  CLEAR I_BDCDATA.
  I_BDCDATA-PROGRAM  = PROGRAM.
  I_BDCDATA-DYNPRO   = DYNPRO.
  I_BDCDATA-DYNBEGIN = 'X'.
  APPEND I_BDCDATA.

ENDFORM.                    "BDC_DYNPRO

*----------------------------------------------------------------------*
*        Insert field                                                  *
*----------------------------------------------------------------------*
FORM BDC_FIELD USING FNAM FVAL.

  CLEAR I_BDCDATA.
  I_BDCDATA-FNAM = FNAM.
  I_BDCDATA-FVAL = FVAL.
  APPEND I_BDCDATA.

ENDFORM.                    "bdc_field

*&---------------------------------------------------------------------*
*&      Form  POST_RECLASS
*&---------------------------------------------------------------------*
*       Reclass posting
*----------------------------------------------------------------------*
FORM POST_RECLASS .

  DATA: LV_DATE(10)     TYPE C,
        LV_WRBTR_LC(16) TYPE C,
        LV_ZUONR        TYPE DZUONR,
        LV_XBLNR        TYPE XBLNR,
        LV_HKONT        TYPE HKONT.


  REFRESH: I_BDCDATA, GT_ERR.

  WRITE GV_DATE_EOM TO LV_DATE.
  WRITE GV_VALUE_LC TO LV_WRBTR_LC
                       CURRENCY GV_WAERS_CC NO-SIGN.

  IF GT_BSIS-BLART = GC_SA  OR
     GT_BSIS-BLART = GC_ZZ.
*   Reclass RR Value 3111_3116
    LV_HKONT = P_ACC5.
    LV_XBLNR = 'Recl.3116'.
    LV_ZUONR = 'Recl.RR.Val.3116'.
  ELSE.
*   Reclass Billing Value 3111_3115
    LV_HKONT = P_ACC4.
    LV_XBLNR = 'Recl.3115'.
    LV_ZUONR = 'Recl.Bill.Val.3115'.
  ENDIF.

* Header
  PERFORM BDC_DYNPRO      USING 'SAPMF05A' '0100'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE' '/00'.
  PERFORM BDC_FIELD       USING 'BKPF-BLDAT' LV_DATE.
  PERFORM BDC_FIELD       USING 'BKPF-BLART' GC_SA.
  PERFORM BDC_FIELD       USING 'BKPF-BUKRS' P_BUKRS.
  PERFORM BDC_FIELD       USING 'BKPF-BUDAT' LV_DATE.
  PERFORM BDC_FIELD       USING 'BKPF-WAERS' GV_WAERS_CC.
  PERFORM BDC_FIELD       USING 'BKPF-XBLNR' LV_XBLNR.
  IF GV_VALUE_LC > 0.
    PERFORM BDC_FIELD       USING 'RF05A-NEWBS' '40'.
  ELSE.
    PERFORM BDC_FIELD       USING 'RF05A-NEWBS' '50'.
  ENDIF.
  PERFORM BDC_FIELD       USING 'RF05A-NEWKO' P_ACC3.

* Item 1
  PERFORM BDC_DYNPRO      USING 'SAPMF05A' '0300'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE' '=ZK'.
  PERFORM BDC_FIELD       USING 'BSEG-WRBTR' LV_WRBTR_LC.
  PERFORM BDC_FIELD       USING 'BSEG-ZUONR' LV_ZUONR.
  PERFORM BDC_FIELD       USING 'DKACB-FMORE' 'X'.

* Item 1: more date
  PERFORM BDC_DYNPRO      USING 'SAPLKACB' '0002'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE' 'ENTE'.
  PERFORM BDC_FIELD       USING 'COBL-PRCTR' GT_BSIS-PRCTR.    "MOD-001
  PERFORM BDC_FIELD       USING 'COBL-SEGMENT' GT_BSIS-SEGMENT.

* Item 2
  PERFORM BDC_DYNPRO      USING 'SAPMF05A' '0330'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE' '/00'.
  IF GV_VALUE_LC > 0.
    PERFORM BDC_FIELD       USING 'RF05A-NEWBS' '50'.
  ELSE.
    PERFORM BDC_FIELD       USING 'RF05A-NEWBS' '40'.
  ENDIF.
  PERFORM BDC_FIELD       USING 'RF05A-NEWKO' LV_HKONT.

  PERFORM BDC_DYNPRO      USING 'SAPMF05A' '0300'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE' '=ZK'.
  PERFORM BDC_FIELD       USING 'BSEG-WRBTR' LV_WRBTR_LC.
  PERFORM BDC_FIELD       USING 'BSEG-ZUONR' LV_ZUONR.
  PERFORM BDC_FIELD       USING 'DKACB-FMORE' 'X'.

* Item 2: more date
  PERFORM BDC_DYNPRO      USING 'SAPLKACB' '0002'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE' 'ENTE'.
  PERFORM BDC_FIELD       USING 'COBL-PRCTR' GT_BSIS-PRCTR.    "MOD-001
  PERFORM BDC_FIELD       USING 'COBL-SEGMENT' GT_BSIS-SEGMENT.

* Save
  PERFORM BDC_DYNPRO      USING 'SAPMF05A' '0330'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE' '=BU'.

  CALL TRANSACTION 'FB01' USING I_BDCDATA
                          MODE GV_MODE
                          UPDATE 'S'
                          MESSAGES INTO GT_ERR.

* Check for errors
  LOOP AT GT_ERR WHERE MSGTYP = 'E'
                    OR MSGTYP = 'A'.
    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*        SPRSL                   = SY-LANGU
        ARBGB                   = GT_ERR-MSGID
        MSGNR                   = GT_ERR-MSGNR
        MSGV1                   = GT_ERR-MSGV1
        MSGV2                   = GT_ERR-MSGV2
        MSGV3                   = GT_ERR-MSGV3
        MSGV4                   = GT_ERR-MSGV4
      IMPORTING
        MSGTEXT                 = GV_MSGTXT
      EXCEPTIONS
        MESSAGE_NOT_FOUND       = 1
        OTHERS                  = 2.

    WRITE: / GV_MSGTXT.
  ENDLOOP.

* No errors
  IF SY-SUBRC NE 0.
*   Get document number
*    GET PARAMETER ID 'BLN' FIELD gt_doc-belnr.
    READ TABLE GT_ERR WITH KEY MSGID = 'F5'
                               MSGNR = '312'.
    IF SY-SUBRC NE 0.
      WRITE: / 'No posting made for', GT_BSIS-BLART, GT_BSIS-SEGMENT,
                                      LV_ZUONR,
                                      GV_VALUE_LC, GV_WAERS_CC.
    ELSE.
      CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
        EXPORTING
          INPUT  = GT_ERR-MSGV1
        IMPORTING
          OUTPUT = GT_DOC-BELNR.
    ENDIF.
  ENDIF.

ENDFORM.                    " POST_RECLASS

*&---------------------------------------------------------------------*
*&      Form  LIST_DATA
*&---------------------------------------------------------------------*
*       List data
*----------------------------------------------------------------------*
FORM LIST_DATA .

  CLEAR GV_ERRLST.
  NEW-PAGE.

  IF GT_DOC[] IS INITIAL.
    WRITE: /01 'NO documents posted'.
  ELSE.
*   Posted documents
    LOOP AT GT_DOC.
      WRITE: /01 GT_DOC-BELNR,
              13 GT_DOC-BLART,
              22 GT_DOC-PRCTR,                              "MOD-001
              35 GT_DOC-SEGMENT,
              48(20) GT_DOC-VALUE_LC CURRENCY GV_WAERS_CC,
              70 GV_WAERS_CC.
    ENDLOOP.
  ENDIF.

  ULINE.

ENDFORM.                    " LIST_DATA




*&---------------------------------------------------------------------*
*&      Form  START_PERIOD_ADJ_FOR_SHTA_HKGA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM START_PERIOD_ADJ_FOR_SHTA_HKGA .
* First get the present period values for this line so that we can restore it again.
  SELECT * FROM T001B
          INTO  TABLE   GT_T001B
          WHERE BUKRS = P_BUKRS           "Posting Period Variant
          AND   MKOAR = GC_S              "Account Type or Masking
          AND   VKONT = GC_3000000        "From Account
          AND   BKONT = GC_3499999.       "To Account

* Do not change anything if we do not have that record in OB52
  IF SY-SUBRC = 0.
*         Get the values from the selection screen and Set the values of from and to year and month of period
          GV_FRMON = S_BUDAT-LOW+4(2).
          GV_FRYR  = S_BUDAT-LOW+0(4).
          GV_TOMON = S_BUDAT-HIGH+4(2).
          GV_TOYR  = S_BUDAT-HIGH+0(4).


*         Now call OB52 and store those values.
    PERFORM CALL_OB52_START.
  ENDIF.

ENDFORM.                    " START_PERIOD_ADJ_FOR_SHTA_HKGA



*&---------------------------------------------------------------------*
*&      Form  END_PERIOD_ADJ_FOR_SHTA_HKGA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM END_PERIOD_ADJ_FOR_SHTA_HKGA .

CLEAR : GV_FRMON,
        GV_FRYR,
        GV_TOMON,
        GV_TOYR.


* Read values from our table which was saved previously so that we can restore the same values
IF NOT GT_T001B[] IS INITIAL.
    READ TABLE GT_T001B INDEX 1.
     IF SY-SUBRC = 0.
*         Get the values from the table and Set the values of from and to year and month of period
          GV_FRMON = GT_T001B-FRPE1.
          GV_FRYR  = GT_T001B-FRYE1.
          GV_TOMON = GT_T001B-TOPE1.
          GV_TOYR  = GT_T001B-TOYE1.

*         Now call OB52 and store those values.
          PERFORM CALL_OB52_END.
     ENDIF.
ENDIF.

ENDFORM.                    " END_PERIOD_ADJ_FOR_SHTA_HKGA



*&---------------------------------------------------------------------*
*&      Form  CALL_OB52_START
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM CALL_OB52_START .

    REFRESH: I_BDCDATA, GT_ERR.

  PERFORM BDC_DYNPRO      USING 'SAPL0F00'           '0065'.
  PERFORM BDC_FIELD       USING 'BDC_CURSOR'         'V_T001B-VKONT(01)'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE'         '=POSI'.
  PERFORM BDC_DYNPRO      USING 'SAPLSPO4'           '0300'.
  PERFORM BDC_FIELD       USING 'BDC_CURSOR'         'SVALD-VALUE(03)'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE'         '=FURT'.
  PERFORM BDC_FIELD       USING 'SVALD-VALUE(01)'    P_BUKRS.                   "Position the cursor in OB52
  PERFORM BDC_FIELD       USING 'SVALD-VALUE(02)'    GC_S.
  PERFORM BDC_FIELD       USING 'SVALD-VALUE(03)'    GC_3000000.
  PERFORM BDC_DYNPRO      USING 'SAPL0F00'           '0065'.
  PERFORM BDC_FIELD       USING 'BDC_CURSOR'         'V_T001B-FRYE1(01)'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE'         '=SAVE'.
  PERFORM BDC_FIELD       USING 'VIM_MARKED(01)'     'X'.

  PERFORM BDC_FIELD       USING 'V_T001B-FRPE1(01)'  GV_FRMON.                       "Set the 'from month' value
  PERFORM BDC_FIELD       USING 'V_T001B-FRYE1(01)'  GV_FRYR.                        "Set the 'from year' value
  PERFORM BDC_FIELD       USING 'V_T001B-TOPE1(01)'  GV_TOMON.                       "Set the 'To year' value
  PERFORM BDC_FIELD       USING 'V_T001B-TOYE1(01)'  GV_TOYR.                        "Set the 'To year' value
  PERFORM BDC_DYNPRO      USING 'SAPL0F00'           '0065'.
  PERFORM BDC_FIELD       USING 'BDC_CURSOR'         'V_T001B-FRYE1(01)'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE'         '=ENDE'.

  CALL TRANSACTION 'OB52' USING I_BDCDATA
                          MODE GV_MODE
                          UPDATE 'S'
                          MESSAGES INTO GT_ERR.

* Check for errors
  LOOP AT GT_ERR WHERE MSGTYP = 'E'
                    OR MSGTYP = 'A'.
    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*        SPRSL                   = SY-LANGU
        ARBGB                   = GT_ERR-MSGID
        MSGNR                   = GT_ERR-MSGNR
        MSGV1                   = GT_ERR-MSGV1
        MSGV2                   = GT_ERR-MSGV2
        MSGV3                   = GT_ERR-MSGV3
        MSGV4                   = GT_ERR-MSGV4
      IMPORTING
        MSGTEXT                 = GV_MSGTXT
      EXCEPTIONS
        MESSAGE_NOT_FOUND       = 1
        OTHERS                  = 2.

    WRITE: / GV_MSGTXT.
  ENDLOOP.


ENDFORM.                    " CALL_OB52_START



*&---------------------------------------------------------------------*
*&      Form  CALL_OB52_END
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM CALL_OB52_END .

    REFRESH: I_BDCDATA, GT_ERR.

  PERFORM BDC_DYNPRO      USING 'SAPL0F00'           '0065'.
  PERFORM BDC_FIELD       USING 'BDC_CURSOR'         'V_T001B-VKONT(01)'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE'         '=POSI'.
  PERFORM BDC_DYNPRO      USING 'SAPLSPO4'           '0300'.
  PERFORM BDC_FIELD       USING 'BDC_CURSOR'         'SVALD-VALUE(03)'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE'         '=FURT'.
  PERFORM BDC_FIELD       USING 'SVALD-VALUE(01)'    P_BUKRS.                   "Position the cursor in OB52
  PERFORM BDC_FIELD       USING 'SVALD-VALUE(02)'    GC_S.
  PERFORM BDC_FIELD       USING 'SVALD-VALUE(03)'    GC_3000000.
  PERFORM BDC_DYNPRO      USING 'SAPL0F00'           '0065'.
  PERFORM BDC_FIELD       USING 'BDC_CURSOR'         'V_T001B-FRYE1(01)'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE'         '=SAVE'.
  PERFORM BDC_FIELD       USING 'VIM_MARKED(01)'     'X'.

  PERFORM BDC_FIELD       USING 'V_T001B-FRPE1(01)'  GV_FRMON.                       "Set the 'from month' value
  PERFORM BDC_FIELD       USING 'V_T001B-FRYE1(01)'  GV_FRYR.                        "Set the 'from year' value
  PERFORM BDC_FIELD       USING 'V_T001B-TOPE1(01)'  GV_TOMON.                       "Set the 'To year' value
  PERFORM BDC_FIELD       USING 'V_T001B-TOYE1(01)'  GV_TOYR.                        "Set the 'To year' value
  PERFORM BDC_DYNPRO      USING 'SAPL0F00'           '0065'.
  PERFORM BDC_FIELD       USING 'BDC_CURSOR'         'V_T001B-FRYE1(01)'.
  PERFORM BDC_FIELD       USING 'BDC_OKCODE'         '=ENDE'.

  CALL TRANSACTION 'OB52' USING I_BDCDATA
                          MODE GV_MODE
                          UPDATE 'S'
                          MESSAGES INTO GT_ERR.

* Check for errors
  LOOP AT GT_ERR WHERE MSGTYP = 'E'
                    OR MSGTYP = 'A'.
    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*        SPRSL                   = SY-LANGU
        ARBGB                   = GT_ERR-MSGID
        MSGNR                   = GT_ERR-MSGNR
        MSGV1                   = GT_ERR-MSGV1
        MSGV2                   = GT_ERR-MSGV2
        MSGV3                   = GT_ERR-MSGV3
        MSGV4                   = GT_ERR-MSGV4
      IMPORTING
        MSGTEXT                 = GV_MSGTXT
      EXCEPTIONS
        MESSAGE_NOT_FOUND       = 1
        OTHERS                  = 2.

    WRITE: / GV_MSGTXT.
  ENDLOOP.

ENDFORM.                    " CALL_OB52_END



* Begin of Change MOD-003

*&---------------------------------------------------------------------*
*&      Form  CORRECT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM CORRECT_DATA.

* Correct missing profit centers
PERFORM MISSING_PRCTR.

* Correct wrong profit centers
PERFORM WRONG_PRCTR.

ENDFORM.                    " CORRECT_DATA





*&---------------------------------------------------------------------*
*&      Form  MISSING_PRCTR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM MISSING_PRCTR.

 SORT GT_BSIS BY  BLART
                  PRCTR
                  SEGMENT
                  BELNR.

* Let us get all the records where profit center is missing
LOOP AT GT_BSIS WHERE PRCTR IS INITIAL.
    GT_BSIS_MISSING = GT_BSIS.

    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        INPUT         =  GT_BSIS_MISSING-BUZEI
     IMPORTING
       OUTPUT        =   GT_BSIS_MISSING-DOCLN              .

    APPEND GT_BSIS_MISSING.
    CLEAR GT_BSIS_MISSING.
ENDLOOP.


IF NOT GT_BSIS_MISSING[] IS INITIAL.

*       Let us get the profit centers from the table FAGLFLEXA
      SELECT RYEAR
             DOCNR
             RLDNR
             RBUKRS
             DOCLN
             PRCTR
             SEGMENT
             FROM FAGLFLEXA
             INTO TABLE GT_FAGLFLEXA
             FOR ALL ENTRIES IN GT_BSIS_MISSING
             WHERE  RYEAR = GT_BSIS_MISSING-GJAHR
             AND DOCNR = GT_BSIS_MISSING-BELNR
             AND  RLDNR = GC_0L
             AND  RBUKRS = P_BUKRS
             AND  DOCLN = GT_BSIS_MISSING-DOCLN.




*       Let us insert the profit centers where it is missing
      LOOP AT GT_BSIS WHERE PRCTR IS INITIAL.

         CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
          EXPORTING
            INPUT         =  GT_BSIS-BUZEI
         IMPORTING
           OUTPUT        =   GV_DOCLN.

        CLEAR GT_FAGLFLEXA.
        READ TABLE GT_FAGLFLEXA WITH KEY RYEAR  = GT_BSIS-GJAHR
                                         DOCNR  = GT_BSIS-BELNR
                                         RLDNR  = GC_0L
                                         RBUKRS = P_BUKRS
                                         DOCLN  = GV_DOCLN.
              IF SY-SUBRC = 0.
                GT_BSIS-PRCTR    = GT_FAGLFLEXA-PRCTR.
                GT_BSIS-SEGMENT  = GT_FAGLFLEXA-SEGMENT.

                MODIFY GT_BSIS TRANSPORTING  PRCTR SEGMENT.
              ENDIF.

      ENDLOOP.
ENDIF.

ENDFORM.                    " MISSING_PRCTR


*&---------------------------------------------------------------------*
*&      Form  WRONG_PRCTR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM WRONG_PRCTR.

* Lets get the segments from teh table FAGL_SEGM
  SELECT SEGMENT
         FROM FAGL_SEGM
         INTO TABLE GT_FAGL_SEGM.


SORT GT_BSIS BY   BLART
                  PRCTR
                  SEGMENT
                  BELNR.

* Let us get all the records where profit center is same as segment
LOOP AT GT_BSIS.
  CLEAR GT_FAGL_SEGM.
  READ TABLE GT_FAGL_SEGM WITH KEY SEGMENT = GT_BSIS-PRCTR.
  IF SY-SUBRC = 0.
        IF GT_BSIS-BLART  = GC_AB.
               GT_BSIS_WRNGAB = GT_BSIS.
               APPEND GT_BSIS_WRNGAB.
               CLEAR  GT_BSIS_WRNGAB.


       ELSEIF GT_BSIS-BLART  = GC_RV OR GT_BSIS-BLART  = GC_ZZ.
               GT_BSIS_WRNGRVZZ = GT_BSIS.
               APPEND GT_BSIS_WRNGRVZZ.
               CLEAR  GT_BSIS_WRNGRVZZ.

       ENDIF.
   ENDIF.
ENDLOOP.





* Since we now have the wrong records for AB and RV / ZZ types

* Get data from BSAS for AB type
IF NOT GT_BSIS_WRNGAB[] IS INITIAL.
      SELECT BUKRS
             HKONT
             GJAHR
             BELNR
             PRCTR
             SEGMENT
             FROM BSAS
             INTO TABLE GT_BSAS_PRCTR
             FOR ALL ENTRIES IN GT_BSIS_WRNGAB
             WHERE BUKRS = P_BUKRS
             AND   HKONT IN (P_ACC1, P_ACC2)
             AND   GJAHR =  GT_BSIS_WRNGAB-GJAHR
             AND   BELNR =  GT_BSIS_WRNGAB-BELNR
             AND   SHKZG <> GT_BSIS_WRNGAB-SHKZG.


* Let us insert the correct profit center data into table GT_BSIS_WRNGAB
     LOOP AT GT_BSIS_WRNGAB.
       CLEAR GT_BSAS_PRCTR.
       READ TABLE GT_BSAS_PRCTR WITH KEY BUKRS = P_BUKRS
                                         GJAHR = GT_BSIS_WRNGAB-GJAHR
                                         BELNR = GT_BSIS_WRNGAB-BELNR.
       IF SY-SUBRC = 0.
                GT_BSIS_WRNGAB-PRCTR    = GT_BSAS_PRCTR-PRCTR.
                MODIFY GT_BSIS_WRNGAB TRANSPORTING  PRCTR.
       ENDIF.
    ENDLOOP.
ENDIF.






* Get data for AB and ZZ type from BSEG
 IF NOT GT_BSIS_WRNGRVZZ[] IS INITIAL.
  SELECT  BUKRS
          BELNR
          GJAHR
          BUZEI
          VBEL2
          POSN2
          HKONT
          FROM BSEG
          INTO TABLE GT_BSEG_PRCTR
          FOR ALL ENTRIES IN GT_BSIS_WRNGRVZZ
          WHERE BUKRS = P_BUKRS
          AND   HKONT IN (P_ACC1, P_ACC2)
          AND   BELNR =  GT_BSIS_WRNGRVZZ-BELNR
          AND   GJAHR =  GT_BSIS_WRNGRVZZ-GJAHR
          AND   BUZEI =  GT_BSIS_WRNGRVZZ-BUZEI.


IF NOT GT_BSEG_PRCTR[] IS INITIAL.
*   Get data for AB and ZZ type from VBAP
            SELECT VBELN
                   POSNR
                   PRODH
                   FROM VBAP
                   INTO TABLE GT_VBAP_PRCTR
                   FOR ALL ENTRIES IN GT_BSEG_PRCTR
                   WHERE VBELN = GT_BSEG_PRCTR-VBEL2
                   AND   POSNR = GT_BSEG_PRCTR-POSN2.
ENDIF.



* Let us insert the correct profit center data into table GT_BSIS_WRNGRVZZ
     LOOP AT GT_BSIS_WRNGRVZZ.
       CLEAR GT_BSEG_PRCTR.
       READ TABLE GT_BSEG_PRCTR WITH KEY BUKRS = P_BUKRS
                                         BELNR =  GT_BSIS_WRNGRVZZ-BELNR
                                         GJAHR =  GT_BSIS_WRNGRVZZ-GJAHR
                                         BUZEI =  GT_BSIS_WRNGRVZZ-BUZEI.
      IF SY-SUBRC = 0.
          CLEAR GT_VBAP_PRCTR.
          READ TABLE GT_VBAP_PRCTR WITH KEY VBELN = GT_BSEG_PRCTR-VBEL2
                                            POSNR = GT_BSEG_PRCTR-POSN2.
           IF SY-SUBRC = 0.
                    GT_BSIS_WRNGRVZZ-PRCTR    = GT_VBAP_PRCTR-PRODH+0(4).
                    MODIFY GT_BSIS_WRNGRVZZ TRANSPORTING  PRCTR.
           ENDIF.
      ENDIF.
    ENDLOOP.

ENDIF.





* Lets update the original internal table with the new profit centers
LOOP AT GT_BSIS_WRNGAB.
            CLEAR GT_BSIS.
            READ TABLE GT_BSIS WITH KEY BELNR = GT_BSIS_WRNGAB-BELNR
                                        GJAHR = GT_BSIS_WRNGAB-GJAHR
                                        BUZEI = GT_BSIS_WRNGAB-BUZEI.
            IF SY-SUBRC = 0.
                      GV_INDEX = SY-TABIX.
                      GT_BSIS-PRCTR    = GT_BSIS_WRNGAB-PRCTR.
                      MODIFY GT_BSIS INDEX GV_INDEX TRANSPORTING  PRCTR.
            ENDIF.
ENDLOOP.


LOOP AT GT_BSIS_WRNGRVZZ.
            CLEAR GT_BSIS.
            READ TABLE GT_BSIS WITH KEY BELNR = GT_BSIS_WRNGRVZZ-BELNR
                                        GJAHR = GT_BSIS_WRNGRVZZ-GJAHR
                                        BUZEI = GT_BSIS_WRNGRVZZ-BUZEI.
            IF SY-SUBRC = 0.
                      GV_INDEX = SY-TABIX.
                      GT_BSIS-PRCTR    = GT_BSIS_WRNGRVZZ-PRCTR.
                      MODIFY GT_BSIS INDEX GV_INDEX TRANSPORTING  PRCTR.
            ENDIF.
ENDLOOP.



ENDFORM.                    " WRONG_PRCTR


* End of Change MOD-003
*Text symbol text£º
*S01:Selection

*S02:G/L Accounts
*Selection text£º
*P_ACC3:        G/L Account
*P_ACC4:        G/L Account (Recl.3115)
*P_ACC5:        G/L Account (Recl.3116)
*P_BUKRS:D       .
*P_TEST:        Test run
*S_BUDAT:D       .
