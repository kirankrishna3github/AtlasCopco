*&---------------------------------------------------------------------*
*&
*& Report  YSE_LIST_REPROCESS_67
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& List & Reprocess IDocs with status 67 (GR based invoicing)          *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 30.04.2010
*  Change Request Number : CR1295
*  Transport request Nr. : CD1K956399
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |02.05.2011|          |CD1K964780       |CR1911              *
* MOD-002 |24.03.2016| Anda Wu  |CD1K988533       |CR3832              *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_list_reprocess_67.

TABLES: edidc,
        likp,
        lfa1,
        ekko.

* ALV grid
TYPE-POOLS: slis.

TYPE-POOLS: icon.   "insert MOD-001

* Internal table with IDocs
DATA: BEGIN OF t_idoc  OCCURS 0,
        docnum    TYPE edi_docnum,
      END OF t_idoc.

DATA: its_edidd LIKE edidd  OCCURS 0  WITH HEADER LINE.

* Internal table with output lines
DATA: BEGIN OF t_out  OCCURS 0,
        docnum    TYPE edi_docnum,
        bukrs     TYPE bukrs,
        ekorg     TYPE ekorg,
        bolnr     TYPE bolnr,
        idate     TYPE datum,
**        vbeln     TYPE vbeln_vl,
        lifnr     TYPE lifnr,
        name1     TYPE name1_gp,
        netwr     TYPE netwr,
        waerk     TYPE waerk,
        light     TYPE char4,              "insert MOD-001
        text      TYPE char120,            "insert MOD-001
        vbeln     TYPE likp-vbeln,
        lfdat     TYPE likp-lfdat,
        selkz     TYPE xfeld,
        zzcol     TYPE col_code,
      END OF t_out.
DATA: h_out  LIKE LINE OF t_out.

* Variables
DATA: ls_e1edk01      TYPE e1edk01,
      ls_e1edk02      TYPE e1edk02,
      ls_e1edk03      TYPE e1edk03,
      ls_e1edk14      TYPE e1edk14,
      ls_e1edka1      TYPE e1edka1,
      ls_e1eds01      TYPE e1eds01,
      h_lifnr         TYPE lifnr,
      h_error         TYPE xfeld,
      h_col(10)       TYPE n,
      h_repid         LIKE sy-repid,
      h_text          TYPE char80,
      h_counter       TYPE char10,
      h_total         TYPE char10,
      h_answer        TYPE c.
* ALV
DATA: xv_variant         LIKE disvariant,
      xv_variant_flag    TYPE c,
      xv_sd_alv_variant  LIKE disvariant,
*      xt_fcat            TYPE slis_t_fieldcat_alv,                "comment MOD-001
      xt_alv_sort        TYPE slis_t_sortinfo_alv,
      xv_user_command    TYPE slis_formname  VALUE 'USER_COMMAND',
      xv_variant_save    TYPE c              VALUE 'U',
      xv_layout          TYPE slis_layout_alv,
      xv_grid            TYPE lvc_s_glay,
      xt_events          TYPE slis_t_event,
      ls_events          TYPE slis_alv_event,
      xt_event_exits     TYPE slis_t_event_exit.
DATA: ok_code TYPE sy-ucomm.
* Constants
CONSTANTS: c_invoic   TYPE edi_mestyp   VALUE 'INVOIC',
           c_51       TYPE edi_status   VALUE '51',
**********BEGIN OF INSERT MOD-002
           c_53       TYPE edi_status   VALUE '53',
**********END OF INSERT MOD-002
           c_67       TYPE edi_status   VALUE '67',
           c_x        TYPE c            VALUE 'X'.

*Begin of insert MOD-001
TYPES: BEGIN OF ty_out,
        docnum    TYPE edi_docnum,
        bukrs     TYPE bukrs,
        ekorg     TYPE ekorg,
        bolnr     TYPE bolnr,
        idate     TYPE datum,
        lifnr     TYPE lifnr,
        name1     TYPE name1_gp,
        netwr     TYPE netwr,
        waerk     TYPE waerk,
        light     TYPE char4,
        text      TYPE char120,
        vbeln     TYPE likp-vbeln,
        lfdat     TYPE likp-lfdat,
        selkz     TYPE xfeld,
        zzcol     TYPE col_code,
       END OF ty_out.

TYPES: BEGIN OF ty_likp,
        vbeln TYPE vbeln_vl,
        lfdat TYPE lfdat,
        bolnr TYPE bolnr,
       END OF ty_likp.

TYPES: BEGIN OF ty_vbuk,
        vbeln TYPE vbuk-vbeln,
        wbstk TYPE vbuk-wbstk,
       END OF ty_vbuk.

DATA: it_out    TYPE STANDARD TABLE OF ty_out,
      i_likp    TYPE STANDARD TABLE OF ty_likp,
      it_vbuk   TYPE STANDARD TABLE OF ty_vbuk,
      wa_vbuk   TYPE ty_vbuk,
      wa_likp   TYPE ty_likp.

DATA: go_grid             TYPE REF TO cl_gui_alv_grid,
      go_custom_container TYPE REF TO cl_gui_custom_container,
      gv_container        TYPE scrfname VALUE 'CUSTOM_CONTROL',
      is_layout           TYPE lvc_s_layo,
      gt_selected_rows    TYPE lvc_t_roid,
      gs_selected_rows    TYPE lvc_s_roid,
      xt_fcat             TYPE lvc_t_fcat,
      ls_fcat             LIKE LINE OF xt_fcat,
      ls_stable           TYPE lvc_s_stbl.

FIELD-SYMBOLS: <fs_out> TYPE ANY TABLE,
               <wa_out> TYPE ty_out.
*End of insert MOD-001

* Selections
SELECTION-SCREEN  BEGIN OF BLOCK sel  WITH FRAME  TITLE text-s01.
SELECT-OPTIONS: so_bolnr  FOR likp-bolnr,
                so_verur  FOR likp-verur,
                so_lifnr  FOR likp-lifnr,
                so_traid  FOR likp-traid,
                so_ekorg  FOR ekko-ekorg.
SELECTION-SCREEN SKIP.
PARAMETERS: p_bukrs    TYPE bukrs  OBLIGATORY   MEMORY ID buk
                                   DEFAULT 'MRUA'.
SELECTION-SCREEN  END OF BLOCK sel.

*Begin of insert MOD-001
SELECTION-SCREEN BEGIN OF BLOCK sta WITH FRAME TITLE text-s02.
PARAMETERS: rb_67 RADIOBUTTON GROUP rad1 USER-COMMAND rad1 DEFAULT 'X',
            rb_51 RADIOBUTTON GROUP rad1,
*Begin of insert MOD-002
            rb_53 RADIOBUTTON GROUP rad1.
*End of insert MOD-002
SELECTION-SCREEN END OF BLOCK sta.
*End of insert MOD-001

*----------------------------------------------------------------------*
* On the selection screen                                              *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON p_bukrs.

  PERFORM  check_authorization.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

* Statistics for transaction / program
  CALL METHOD ycl_statistics=>record_transaction.

* Build & display list
*Begin of comment MOD-001
*  PERFORM select_data.
*  PERFORM alv_fcat.
*  PERFORM alv_layout.
*  PERFORM alv_events.
*  PERFORM alv_display.
*End of comment MOD-001

  CALL SCREEN 0100.     "insert MOD-001

*&---------------------------------------------------------------------*
*&      Module  STATUS_0100  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE status_0100 OUTPUT.

  SET PF-STATUS 'YSE_RPI1'.
  SET TITLEBAR  'IDOCS_LIST'.

  PERFORM display_report.

ENDMODULE.                 " STATUS_0100  OUTPUT

*&---------------------------------------------------------------------*
*&  Forms                                                              *
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  AUTHORITY-CHECK OBJECT 'YAM_BUKRS'
           ID 'BUKRS' FIELD p_bukrs.

  IF sy-subrc NE 0.
    MESSAGE e101(yam_sls) WITH p_bukrs.
  ENDIF.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Progress indicator
  h_text = 'Data is being selected'(i01).
  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      text = h_text.

  REFRESH: t_idoc.                    "insert MOD-001
  REFRESH: t_out.                     "insert MOD-001
  IF rb_67 EQ 'X'.                    "insert MOD-001
* Select Idocs with status 67
    SELECT docnum INTO TABLE t_idoc
           FROM edidc
           WHERE status = c_67
             AND mestyp = c_invoic.
*Begin of insert MOD-001
  ELSEIF rb_51 EQ 'X'.
* Select Idocs with status 51
    SELECT docnum INTO TABLE t_idoc
           FROM edidc
           WHERE status = c_51
             AND mestyp = c_invoic.
*Begin of insert MOD-002
  ELSEIF rb_53 EQ 'X'.
    SELECT docnum INTO TABLE t_idoc
           FROM edidc
           WHERE status = c_53
             AND mestyp = c_invoic.
*End of insert MOD-002
  ENDIF.
*End of insert MOD-001
  h_counter = 0.
  DESCRIBE TABLE t_idoc LINES h_total.

* Process segments
  LOOP AT t_idoc.
    ADD 1 TO h_counter.
    h_text = 'IDoc & (& of &) is being processed'(i02).
    REPLACE '&' WITH t_idoc-docnum INTO h_text.
    REPLACE '&' WITH h_counter     INTO h_text.
    REPLACE '&' WITH h_total       INTO h_text.
    CONDENSE h_text.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        text = h_text.
    CLEAR: h_error,
           its_edidd[].
*   Get IDoc data
    CALL FUNCTION 'IDOC_READ_COMPLETELY'
      EXPORTING
        document_number         = t_idoc-docnum
      TABLES
        int_edidd               = its_edidd
      EXCEPTIONS
        document_not_exist      = 1
        document_number_invalid = 2
        OTHERS                  = 3.
    IF sy-subrc <> 0.
*      MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*              WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    ELSE.
      PERFORM fill_output.
    ENDIF.

  ENDLOOP.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  FILL_OUTPUT
*&---------------------------------------------------------------------*
*       Fill output table
*----------------------------------------------------------------------*
FORM fill_output .

  DATA: lt_edids TYPE STANDARD TABLE OF edids,       "insert MOD-001
        lv_edids TYPE edids.

  CLEAR t_out.
  t_out-docnum = t_idoc-docnum.

  LOOP AT its_edidd.
*   Check data
    CASE its_edidd-segnam.

      WHEN 'E1EDK01'.                        "General data (header)
        MOVE its_edidd-sdata TO ls_e1edk01.
        t_out-waerk = ls_e1edk01-curcy.

      WHEN 'E1EDKA1'.                        "Partner data (header)
        MOVE its_edidd-sdata TO ls_e1edka1.
*       Check company and vendor
        IF ls_e1edka1-parvw = 'RE'.          "Bill to
          CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
            EXPORTING
              input  = ls_e1edka1-lifnr
            IMPORTING
              output = h_lifnr.
          IF ls_e1edka1-partn(4) =  p_bukrs  AND
             h_lifnr IN so_lifnr.
            t_out-bukrs = ls_e1edka1-partn(4).
            t_out-lifnr = h_lifnr.
            SELECT SINGLE name1 INTO t_out-name1
                   FROM lfa1
                   WHERE lifnr = h_lifnr.
          ELSE.
            h_error = 'X'.
            EXIT.
          ENDIF.
        ENDIF.

      WHEN 'E1EDK02'.                        "Refer. data (header)
        MOVE its_edidd-sdata TO ls_e1edk02.
        IF ls_e1edk02-qualf = '009'.
          IF ls_e1edk02-belnr IN so_bolnr.
*           Get inbound delivery
            CLEAR likp.
            SELECT SINGLE vbeln traid verur lfdat FROM likp
                   INTO (likp-vbeln, likp-traid, likp-verur, likp-lfdat)
                   WHERE bolnr = ls_e1edk02-belnr.
*            IF sy-subrc = 0            AND
            IF likp-traid IN so_traid  AND
               likp-verur IN so_verur.
              t_out-bolnr = ls_e1edk02-belnr.
**              t_out-vbeln = likp-vbeln.
*Begin of insert MOD-001
              REFRESH: i_likp.
              SELECT vbeln
                     lfdat
                     bolnr
              FROM   likp
              INTO   CORRESPONDING FIELDS OF TABLE i_likp
              WHERE  bolnr EQ t_out-bolnr.
              IF sy-subrc EQ 0.
                SELECT vbeln
                       wbstk
                FROM   vbuk
                INTO   CORRESPONDING FIELDS OF TABLE it_vbuk
                FOR    ALL ENTRIES IN i_likp
                WHERE  vbeln EQ i_likp-vbeln.
                IF sy-subrc EQ 0.
                  CLEAR: wa_vbuk.
                  READ TABLE it_vbuk INTO wa_vbuk WITH KEY wbstk = 'A'.
                  IF sy-subrc EQ 0.
                    t_out-light = icon_red_light.
                  ELSE.
                    CLEAR: wa_vbuk.
                    READ TABLE it_vbuk INTO wa_vbuk WITH KEY wbstk = 'B'.
                    IF sy-subrc EQ 0.
                      t_out-light = icon_red_light.
                    ELSE.
                      t_out-light = icon_green_light.
                    ENDIF.
                  ENDIF.
                ENDIF.
              ENDIF.
*End of insert MOD-001
            ELSE.
              h_error = 'X'.
              EXIT.
            ENDIF.
          ELSE.
            h_error = 'X'.
            EXIT.
          ENDIF.
        ENDIF.

      WHEN 'E1EDK03'.                        "Document date (header)
        MOVE its_edidd-sdata TO ls_e1edk03.
        IF ls_e1edk03-iddat = '012'.
          t_out-idate = ls_e1edk03-datum.
        ENDIF.

      WHEN 'E1EDK14'.                        "Organ. data (header)
        MOVE its_edidd-sdata TO ls_e1edk14.
        IF ls_e1edk14-qualf = '014'.
          IF ls_e1edk14-orgid(4) IN so_ekorg.
            t_out-ekorg = ls_e1edk14-orgid(4).
          ELSE.
            h_error = 'X'.
            EXIT.
          ENDIF.
        ENDIF.

      WHEN 'E1EDS01'.                        "General summary
        MOVE its_edidd-sdata TO ls_e1eds01.
        IF ls_e1eds01-sumid = '011'.
          t_out-netwr = ls_e1eds01-summe.
        ENDIF.

    ENDCASE.
*Begin of insert MOD-001
    IF rb_67 EQ 'X'.
      REFRESH: lt_edids.
      SELECT *
      FROM   edids
      INTO   TABLE lt_edids
      WHERE  docnum = t_idoc-docnum
      AND    status = '67'.
      IF sy-subrc EQ 0.
        SORT lt_edids BY logdat DESCENDING.
        READ TABLE lt_edids INTO lv_edids INDEX 1.
        IF NOT lv_edids-stamid IS INITIAL.
          MESSAGE ID lv_edids-stamid TYPE 'E' NUMBER lv_edids-stamno
                  INTO t_out-text
                  WITH lv_edids-stapa1 lv_edids-stapa2 lv_edids-stapa3 lv_edids-stapa4.
        ENDIF.
      ENDIF.
    ENDIF.
    IF rb_51 EQ 'X'.
      REFRESH: lt_edids.
      SELECT *
      FROM   edids
      INTO   TABLE lt_edids
      WHERE  docnum = t_idoc-docnum
      AND    status = '51'.
      IF sy-subrc EQ 0.
        SORT lt_edids BY logdat DESCENDING.
        READ TABLE lt_edids INTO lv_edids INDEX 1.
        IF NOT lv_edids-stamid IS INITIAL.
          MESSAGE ID lv_edids-stamid TYPE 'E' NUMBER lv_edids-stamno
                  INTO t_out-text
                  WITH lv_edids-stapa1 lv_edids-stapa2 lv_edids-stapa3 lv_edids-stapa4.
        ENDIF.
      ENDIF.
    ENDIF.
*End of inert MOD-001
*Begin of insert MOD-002
    IF rb_53 EQ 'X'.
      REFRESH: lt_edids.
      SELECT *
      FROM   edids
      INTO   TABLE lt_edids
      WHERE  docnum = t_idoc-docnum
      AND    status = '53'.
      IF sy-subrc EQ 0.
        SORT lt_edids BY logdat DESCENDING.
        READ TABLE lt_edids INTO lv_edids INDEX 1.
        IF NOT lv_edids-stamid IS INITIAL.
          MESSAGE ID lv_edids-stamid TYPE 'E' NUMBER lv_edids-stamno
                  INTO t_out-text
                  WITH lv_edids-stapa1 lv_edids-stapa2 lv_edids-stapa3 lv_edids-stapa4.
        ENDIF.
      ENDIF.
    ENDIF.
*End of inert MOD-002

  ENDLOOP.

  IF h_error IS INITIAL.
    APPEND t_out.
  ENDIF.

ENDFORM.                    " FILL_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  ALV_FCAT
*&---------------------------------------------------------------------*
*       ALV field catalog
*----------------------------------------------------------------------*
FORM alv_fcat .

** Create Fieldcatalogue from internal table
** CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE' ...

  CLEAR h_col.
  REFRESH: xt_fcat.    "insert MOD-001

* Field definitions
  PERFORM add_field USING 'DOCNUM' 16 'IDoc number'(t01)       ' '  'X'.
  PERFORM add_field USING 'BUKRS'   7 'Company'(t02)           ' '  ' '.
  PERFORM add_field USING 'EKORG'  10 'Purch.Org.'(t03)        ' '  ' '.
  PERFORM add_field USING 'BOLNR'  35 'Invoice Reference'(t04) ' '  ' '.
  PERFORM add_field USING 'IDATE'  10 'Inv. Date'(t09)         ' '  ' '.
  PERFORM add_field USING 'LIFNR'  10 'Vendor'(t05)            ' '  ' '.
  PERFORM add_field USING 'NAME1'  35 'Name'(t06)              ' '  ' '.
  PERFORM add_field USING 'NETWR'  18 'Invoice Total'(t07)     ' '  ' '.
  PERFORM add_field USING 'WAERK'   5 'Curr.'(t08)             ' '  ' '.
  PERFORM add_field USING 'LIGHT'   4 'GR OK?'                 ' '  ' '.  "insert MOD-001
  PERFORM add_field USING 'TEXT'  120 'Error Message'          ' '  ' '.  "insert MOD-001
**  PERFORM add_field USING 'VBELN' 16 'Inb. Delivery'(txx)   ' '  ' '.

ENDFORM.                    " ALV_FCAT

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr
                         p_hotspot
                         p_key.

  h_col = h_col + 1.

  CLEAR ls_fcat.
  ls_fcat-col_pos    = h_col.
  ls_fcat-fieldname  = p_field.
  ls_fcat-outputlen  = p_len.
*Begin of comment MOD-001
*  ls_fcat-seltext_l  = p_descr.
*  ls_fcat-seltext_m  = p_descr.
*  ls_fcat-seltext_s  = p_descr.
*End of comment MOD-001

*Begin of insert MOD-001
  ls_fcat-seltext  = p_descr.
  ls_fcat-scrtext_l  = p_descr.
  ls_fcat-scrtext_m  = p_descr.
  ls_fcat-scrtext_s  = p_descr.
*End of insert MOD-001
  ls_fcat-fix_column = 'X'.
  ls_fcat-emphasize  = 'X'.
  ls_fcat-hotspot    = p_hotspot.
  ls_fcat-key        = p_key.

  IF ls_fcat-fieldname   = 'LIGHT'.
    ls_fcat-icon         = 'X'.  " Display the field as ICON
    ls_fcat-inttype      = 'C'.
  ENDIF.

  APPEND ls_fcat TO xt_fcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT
*&---------------------------------------------------------------------*
*       Modify ALV layout
*----------------------------------------------------------------------*
FORM alv_layout .

*Begin of comment MOD-001
*  CLEAR: xv_layout.
**.. Define layout
*  xv_layout-zebra               = c_x.
*  xv_layout-get_selinfos        = c_x.
*  xv_layout-detail_popup        = c_x.
*  xv_layout-box_fieldname       = 'SELKZ'.
*  xv_layout-no_keyfix           = c_x.
*  xv_layout-group_change_edit   = c_x.
*  xv_layout-info_fieldname      = 'ZZCOL'.
*End of comment MOD-001
**  xv_layout-totals_before_items = c_x.
**  xv_layout-key_hotspot         = c_x.
**  xv_layout-colwidth_optimize   = c_x.
**  xv_layout-smalltitle          = c_x.
**  xv_layout-grid_title          = 'title'.

*Begin of insert MOD-001
  is_layout-zebra       = c_x.
  is_layout-no_keyfix   = c_x.
  is_layout-info_fname  = 'ZZCOL'.
  is_layout-sel_mode    = 'A'.
  IF rb_51 EQ 'X'.
    is_layout-grid_title = 'List and Reprocess IDoc status 51'(h02).
  ELSEIF rb_67 EQ 'X'.
    is_layout-grid_title = 'List and Reprocess IDoc status 67'(h01).
  ENDIF.
*End of insert MOD-001

*.. Define grid settings
  xv_grid-coll_end_l = c_x.

ENDFORM.                    " ALV_LAYOUT

*&---------------------------------------------------------------------*
*&      Form  ALV_EVENTS
*&---------------------------------------------------------------------*
*       Define ALV events
*----------------------------------------------------------------------*
FORM alv_events .

  REFRESH:xt_events.                               "insert MOD-001
* Fill events
  ls_events-form = ls_events-name = 'USER_COMMAND'.
  APPEND ls_events TO xt_events.
  ls_events-form = ls_events-name = 'PF_STATUS_SET'.
  APPEND ls_events TO xt_events.

ENDFORM.                    " ALV_EVENTS

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       Display ALV grid
*----------------------------------------------------------------------*
FORM alv_display .

*  xv_sd_alv_variant = xv_variant.
  h_repid = sy-repid.

*Begin of comment MOD-001
*  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
*    EXPORTING
*      i_callback_program      = h_repid
*      i_callback_user_command = xv_user_command
**      i_callback_top_of_page  = 'ALV_TOP'
*      i_grid_title            = 'List & Reprocess IDoc status 67'(h01)
*      is_layout               = xv_layout
*      it_fieldcat             = xt_fcat
*      i_grid_settings         = xv_grid
*      it_events               = xt_events
*      it_sort                 = xt_alv_sort
*      i_default               = 'X'
*      i_save                  = 'A'
**      is_variant              = xv_sd_alv_variant
*      i_screen_start_column   = 0
*      i_screen_start_line     = 0
*      i_screen_end_column     = 0
*      i_screen_end_line       = 0
*    TABLES
*      t_outtab                = t_out
*    EXCEPTIONS
*      program_error           = 1
*      OTHERS                  = 2.
*
*  IF sy-subrc NE 0.
**   message id sy-msgid type sy-msgty number sy-msgno
**           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
*  ENDIF.

*End of comment MOD-001
ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  USER_COMMAND
*&---------------------------------------------------------------------*
*       User Command callback routine for ALV
*----------------------------------------------------------------------*
FORM user_command USING r_ucomm LIKE sy-ucomm
                        rs_selfield TYPE slis_selfield.

  CASE r_ucomm.

    WHEN 'RIDC'.
*     Reprocess IDocs
      PERFORM reprocess_idocs.
      rs_selfield-refresh = c_x.

*    WHEN '&IC1'.
**     Selection
*      IF NOT rs_selfield-value IS INITIAL.
*        CASE rs_selfield-fieldname.
**          WHEN 'VBELN'.
***           Display Inbound Delivery
**            SET PARAMETER ID 'VLM' FIELD rs_selfield-value.
**            CALL TRANSACTION 'VL33N' AND SKIP FIRST SCREEN.
*        ENDCASE.
*      ENDIF.
  ENDCASE.

ENDFORM.                    " user_command

*eject
*&---------------------------------------------------------------------*
*&      Form  PF_STATUS_SET
*&---------------------------------------------------------------------*
*       PF-status callback routine for ALV
*----------------------------------------------------------------------*
FORM pf_status_set USING rt_extab TYPE slis_t_extab.

*  SET PF-STATUS 'YSE_RPI'.

ENDFORM.                    " PF_STATUS_SET

*&---------------------------------------------------------------------*
*&      Form  REPROCESS_IDOCS
*&---------------------------------------------------------------------*
*       Reprocess Idocs
*----------------------------------------------------------------------*
FORM reprocess_idocs .

* Internal table with IDoc status
  DATA: lt_edids      TYPE TABLE OF edids  WITH HEADER LINE,
        ls_edids      TYPE edids.

* Internal table with IDoc status counter
  DATA: BEGIN OF lt_count  OCCURS 0,
          docnum      TYPE edi_docnum,
          countr      TYPE edi_countr,
        END OF lt_count.

  DATA: sel_tab       LIKE rsparams  OCCURS 0  WITH HEADER LINE.

  RANGES: lr_docnum   FOR edidc-docnum.


  CLEAR: lt_edids[],
         lr_docnum[].

*Begin of comment MOD-001
** Records selected ?
*  LOOP AT t_out WHERE selkz = c_x.
*    EXIT.
*  ENDLOOP.
*  CHECK sy-subrc = 0.
*End of comment MOD-001

*Begin of insert MOD-001
  REFRESH: gt_selected_rows.
  CLEAR:   gs_selected_rows.

  CALL METHOD go_grid->get_selected_rows
    IMPORTING
      et_row_no = gt_selected_rows.

  IF NOT gt_selected_rows IS INITIAL.
*End of insert MOD-001
* Confirm reprocessing
    CALL FUNCTION 'POPUP_TO_CONFIRM'
      EXPORTING
        titlebar              = text-p01
        text_question         = text-p02
        icon_button_1         = 'ICON_OKAY'
        icon_button_2         = 'ICON_CANCEL'
        default_button        = '2'
        display_cancel_button = space
      IMPORTING
        answer                = h_answer
      EXCEPTIONS
        text_not_found        = 1
        OTHERS                = 2.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.

    CHECK h_answer = '1'.

    lr_docnum-sign   = 'I'.
    lr_docnum-option = 'EQ'.

*Begin of comment MOD-001
** Loop on the selected IDocs
*    LOOP AT t_out WHERE selkz = c_x.
*      SELECT * APPENDING TABLE lt_edids
*               FROM edids
*               WHERE docnum = t_out-docnum
*                 AND status = c_51.
**   Control record
*      lr_docnum-low = t_out-docnum.
*      APPEND lr_docnum.
*    ENDLOOP.
*End of comment MOD-001

*Begin of insert MOD-001
    CLEAR: gs_selected_rows.
    LOOP AT gt_selected_rows INTO gs_selected_rows.
      LOOP AT <fs_out> ASSIGNING <wa_out> CASTING.
        IF sy-tabix EQ gs_selected_rows-row_id.
          SELECT * APPENDING TABLE lt_edids
                   FROM edids
                   WHERE docnum = <wa_out>-docnum
                     AND status = c_51.
*   Control record
          lr_docnum-low = <wa_out>-docnum.
          APPEND lr_docnum.
        ENDIF.
      ENDLOOP.
    ENDLOOP.
*End of insert MOD-001

* IDocs selected ?
    CHECK NOT lr_docnum[] IS INITIAL.

* Get status counters for IDocs
    SELECT docnum countr INTO TABLE lt_count
           FROM edids
           WHERE docnum IN lr_docnum.

* Keep the latest status counter
    SORT lt_count BY docnum ASCENDING
                     countr DESCENDING.

    DELETE ADJACENT DUPLICATES FROM lt_count
           COMPARING docnum.

* Get latest error (status 51 to be reprocessed)
    SORT lt_edids BY docnum ASCENDING
                    logdat DESCENDING logtim DESCENDING.

    DELETE ADJACENT DUPLICATES FROM lt_edids
           COMPARING docnum.

* Set counter
    LOOP AT lt_edids.
      READ TABLE lt_count WITH KEY docnum = lt_edids-docnum
                          BINARY SEARCH.
      lt_edids-countr = lt_count-countr + 1.
      MODIFY lt_edids.
    ENDLOOP.

* Insert status records
    INSERT edids FROM TABLE lt_edids.
    COMMIT WORK AND WAIT.

* Selection table for IDoc reprocessing
    CLEAR sel_tab.
    REFRESH sel_tab.
    sel_tab-selname = 'SO_MESTY'.
    sel_tab-kind    = 'S'.
    sel_tab-sign    = 'I'.
    sel_tab-option  = 'EQ'.
    sel_tab-low     = c_invoic.
    APPEND sel_tab.
    sel_tab-selname = 'SO_DOCNU'.
    sel_tab-kind    = ''.
    sel_tab-sign    = 'I'.
    sel_tab-option  = 'EQ'.
    LOOP AT lr_docnum.
      sel_tab-low     = lr_docnum-low.
      APPEND sel_tab.
    ENDLOOP.

* Update control records
    UPDATE edidc
           SET status = c_51
           WHERE docnum IN lr_docnum.
    COMMIT WORK AND WAIT.

* Reprocess IDocs
    SUBMIT rbdmanin WITH SELECTION-TABLE sel_tab
*                  VIA SELECTION-SCREEN
                    AND RETURN.
  ENDIF.                                          "insert MOD-001
ENDFORM.                    " REPROCESS_IDOCS

*Begin of insert MOD-001
*&---------------------------------------------------------------------*
*&      Form  GOODS_RECEIPT_FROM_IDOC
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM goods_receipt_from_idoc .

  DATA: lv_count    TYPE i,
        lv_lfdatlow TYPE char10.

* Batch input
  DATA: i_bdcdata LIKE bdcdata OCCURS 0 WITH HEADER LINE,
        struct_bdcdata TYPE bdcdata,
        i_bdcmsgcoll   TYPE STANDARD TABLE OF bdcmsgcoll.
  DATA: gv_mode(1)    TYPE c       VALUE 'N'.

  DATA: i_seltab  TYPE STANDARD TABLE OF rsparams,
        wa_seltab TYPE rsparams.

* Records selected ?
  REFRESH: gt_selected_rows.
  CLEAR:   gs_selected_rows,
           lv_count.

  CALL METHOD go_grid->get_selected_rows
    IMPORTING
      et_row_no = gt_selected_rows.

  LOOP AT gt_selected_rows INTO gs_selected_rows.
    lv_count = lv_count + 1.
  ENDLOOP.

  IF lv_count < 1 OR lv_count > 1.
    MESSAGE ID 'YAM_RE' TYPE 'E' NUMBER '320' WITH 'Please select one record'.
  ELSE.
    CLEAR: gs_selected_rows.
    READ TABLE gt_selected_rows INTO gs_selected_rows INDEX 1.
    LOOP AT <fs_out> ASSIGNING <wa_out> CASTING.
      IF sy-tabix EQ gs_selected_rows-row_id.
        EXIT.
      ENDIF.
    ENDLOOP.

    REFRESH: i_likp.
    SELECT vbeln
           lfdat
           bolnr
    FROM   likp
    INTO   CORRESPONDING FIELDS OF TABLE i_likp
    WHERE  bolnr EQ <wa_out>-bolnr.

    IF sy-subrc EQ 0.
      CLEAR wa_seltab.
      REFRESH i_seltab.
      wa_seltab-selname = 'IT_LFDAT'.
      wa_seltab-kind    = 'S'.
      wa_seltab-sign    = 'I'.
      wa_seltab-option  = 'EQ'.
      CLEAR: wa_likp.
      LOOP AT i_likp INTO wa_likp.
        wa_seltab-low     = wa_likp-lfdat.
        APPEND wa_seltab TO i_seltab.
        CLEAR: wa_likp.
      ENDLOOP.

      CLEAR wa_seltab.
      wa_seltab-selname = 'IT_VBELN'.
      wa_seltab-kind    = ''.
      wa_seltab-sign    = 'I'.
      wa_seltab-option  = 'EQ'.
      CLEAR: wa_likp.
      LOOP AT i_likp INTO wa_likp.
        wa_seltab-low     = wa_likp-vbeln.
        APPEND wa_seltab TO i_seltab.
        CLEAR: wa_likp.
      ENDLOOP.

      SUBMIT ws_monitor_inb_del_gdrc
      WITH   SELECTION-TABLE i_seltab
      AND    RETURN.
    ELSE.
      MESSAGE ID 'YAM_RE' TYPE 'S' NUMBER '320' WITH 'No Inbound Deliveries for'
                                                     'Invoice Reference'
                                                     <wa_out>-bolnr.
    ENDIF.
  ENDIF.

ENDFORM.                    " GOODS_RECEIPT_FROM_IDOC
*&---------------------------------------------------------------------*
*&      Form  REFRESH_REPORT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM refresh_report .

  PERFORM display_report.

ENDFORM.                    " REFRESH_REPORT


*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_0100  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE user_command_0100 INPUT.
  CASE ok_code.

    WHEN 'BACK'.
      SET SCREEN 0.
      LEAVE SCREEN.
    WHEN 'RIDC'.
*Reprocess IDocs
      PERFORM reprocess_idocs.
*GR
    WHEN 'GRIDC'.
      PERFORM goods_receipt_from_idoc.
*Refresh Report
    WHEN 'REF'.
      PERFORM refresh_report.
  ENDCASE.

ENDMODULE.                 " USER_COMMAND_0100  INPUT
*End of insert MOD-001
*&---------------------------------------------------------------------*
*&      Form  DISPLAY_REPORT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM display_report .

  PERFORM select_data.

  REFRESH: it_out.
  APPEND LINES OF t_out[] TO it_out.
  ASSIGN it_out TO <fs_out>.

  IF go_custom_container IS INITIAL.

    CREATE OBJECT go_custom_container
      EXPORTING
        container_name              = gv_container
      EXCEPTIONS
        cntl_error                  = 1
        cntl_system_error           = 2
        create_error                = 3
        lifetime_error              = 4
        lifetime_dynpro_dynpro_link = 5.

    CREATE OBJECT go_grid
      EXPORTING
        i_parent = go_custom_container.

    PERFORM alv_layout.
    PERFORM alv_fcat.

*Display the table Contents in ALV format
    CALL METHOD go_grid->set_table_for_first_display
      EXPORTING
        is_layout       = is_layout
        i_default       = 'X'
        i_save          = 'A'
      CHANGING
        it_outtab       = <fs_out>
        it_fieldcatalog = xt_fcat.
  ELSE.
    CALL METHOD go_grid->refresh_table_display
      EXPORTING
        is_stable      = ls_stable
        i_soft_refresh = 'X'.
  ENDIF.
ENDFORM.                    " DISPLAY_REPORT

*Text symbol text£º
*H01:List and Reprocess IDoc status 67
*H02:List and Reprocess IDoc status 51
*I01:Data is being selected
*I02:IDoc & (& of &) is being processed
*P01:Reprocess IDocs ?
*P02:You are about to reprocess the selected IDocs. Are you sure to continue ?
*S01:Selection
*S02:Idoc Status
*T01:IDoc number
*T02:Company
*T03:Purch.Org.
*T04:Invoice Reference
*T05:Vendor
*T06:Name
*T07:Invoice Total
*T08:Curr.

*T09:Inv. Date
*Selection text£º
*P_BUKRS:        Company
*RB_51:        51 - IDocs in Error
*RB_53:        53 - Fully posted
*RB_67:        67 - IDocs waiting for GR
*SO_BOLNR:        Invoice Reference
*SO_EKORG:        Purchase Organization
*SO_LIFNR:        Vendor
*SO_TRAID:        Case
*SO_VERUR:        Shipment
