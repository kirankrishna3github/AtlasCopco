*&---------------------------------------------------------------------*
*& Report  YSE_ACT_EQUIPM_SERV_HIST_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Equipment Service History extraction                         *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 29.11.2012
*  Change Request Number :
*  Transport request Nr. : CD1K974078
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 19.03.2014 | Jules Smets     | CD1K980489    | CR1794 (Act)*
*----------------------------------------------------------------------*
* MOD-002 | 11.09.2014 | Jules Smets     | CD1K982943    |             *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_act_equipm_serv_hist_extr.

TABLES: equi,
        equz,
        iloa,
        jest,
        tj02t,
        tj30t,
        afih,
        aufk,
        afko,
        pmco,
        veda.

* Types

TYPES: BEGIN OF ty_equi,
         equnr      TYPE equnr,
         objnr      TYPE j_objnr,
      END OF ty_equi.

TYPES: BEGIN OF ty_jest,
         objnr      TYPE j_objnr,
         stat       TYPE j_status,
      END OF ty_jest.

TYPES: BEGIN OF ty_aufk,
         equnr      TYPE equnr,
         aufnr      TYPE aufnr,
         objnr      TYPE j_objnr,
         kdauf      TYPE kdauf,
         kdpos      TYPE kdpos,
         waers      TYPE waers,
         bemot      TYPE bemot,
         getri      TYPE co_getri,
      END OF ty_aufk.

TYPES: BEGIN OF ty_pmco,
         objnr      TYPE j_objnr,
         wrt00      TYPE bp_wpl,
         wrt01      TYPE bp_wpl,
         wrt02      TYPE bp_wpl,
         wrt03      TYPE bp_wpl,
         wrt04      TYPE bp_wpl,
         wrt05      TYPE bp_wpl,
         wrt06      TYPE bp_wpl,
         wrt07      TYPE bp_wpl,
         wrt08      TYPE bp_wpl,
         wrt09      TYPE bp_wpl,
         wrt10      TYPE bp_wpl,
         wrt11      TYPE bp_wpl,
         wrt12      TYPE bp_wpl,
         wrt13      TYPE bp_wpl,
         wrt14      TYPE bp_wpl,
         wrt15      TYPE bp_wpl,
         wrt16      TYPE bp_wpl,
      END OF ty_pmco.

TYPES: BEGIN OF ty_veda,
         vbeln      TYPE vbeln_va,
         vposn      TYPE posnr_va,
         venddat    TYPE vndat_veda,
      END OF ty_veda.

* Internal tables
DATA: gt_equi  TYPE TABLE OF ty_equi
                    WITH HEADER LINE.

DATA: gt_jest  TYPE HASHED TABLE OF ty_jest
                    WITH UNIQUE KEY objnr
                    WITH HEADER LINE,
      gt_jesti TYPE TABLE OF ty_jest
                    WITH HEADER LINE.

DATA: gt_aufk  TYPE SORTED TABLE OF ty_aufk
                    WITH NON-UNIQUE KEY equnr
                    WITH HEADER LINE,
      gt_aufki TYPE TABLE OF ty_aufk
                    WITH HEADER LINE.

DATA: gt_pmco  TYPE SORTED TABLE OF ty_pmco
                    WITH NON-UNIQUE KEY objnr
                    WITH HEADER LINE.

DATA: gt_veda  TYPE HASHED TABLE OF ty_veda
                    WITH UNIQUE KEY vbeln vposn
                    WITH HEADER LINE,
      gt_vedai TYPE TABLE OF ty_veda
                    WITH HEADER LINE.

DATA: BEGIN OF gt_tj30t  OCCURS 0,
        estat       TYPE j_estat,
        txt04       TYPE j_txt04,
        txt30       TYPE j_txt30,
      END OF gt_tj30t.

* Internal table with return for F4 (value selection)
DATA: BEGIN OF gt_return_tab  OCCURS 0.
        INCLUDE STRUCTURE ddshretval.
DATA: END OF gt_return_tab.

DATA: BEGIN OF gt_out  OCCURS 0,
        swerk       TYPE swerk,
        tab01(1)    TYPE c,
        equnr       TYPE equnr,
        tab02(1)    TYPE c,
        aufnr       TYPE aufnr,
        tab03(1)    TYPE c,
        getri       TYPE co_getri,
        tab04(1)    TYPE c,
        kdauf       TYPE kdauf,
        tab05(1)    TYPE c,
        kdpos       TYPE kdpos,
        tab06(1)    TYPE c,
        venddat     TYPE vndat_veda,
        tab07(1)    TYPE c,
*        incons      TYPE xfeld,
*        tab08(1)    TYPE c,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        swerk       TYPE fieldname,
        tab01(1)    TYPE c,
        equnr       TYPE fieldname,
        tab02(1)    TYPE c,
        aufnr       TYPE fieldname,
        tab03(1)    TYPE c,
        getri       TYPE fieldname,
        tab04(1)    TYPE c,
        kdauf       TYPE fieldname,
        tab05(1)    TYPE c,
        kdpos       TYPE fieldname,
        tab06(1)    TYPE c,
        venddat     TYPE fieldname,
        tab07(1)    TYPE c,
*        incons      TYPE fieldname,
*        tab08(1)    TYPE c,
      END OF gt_outh.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_ersda(14)     TYPE c,                      "YYYYMMDDHHMMSS
      gv_wrtsum        TYPE bp_wpl,
      gv_datum         TYPE sydatum,
      gv_error         TYPE xfeld.

RANGES:  r_bemot  FOR aufk-bemot.

CONSTANTS: gc_stat_dl  TYPE j_status  VALUE 'I0076',
           gc_stat_in  TYPE j_status  VALUE 'I0320',
           gc_stat_scr TYPE j_status  VALUE 'E0005'.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
PARAMETERS: p_swerk  LIKE iloa-swerk  OBLIGATORY  MEMORY ID swk.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_equnr  FOR equi-equnr,
                s_estat  FOR tj30t-estat  DEFAULT 'E0002'  "ZNOC
                                          NO INTERVALS.
SELECTION-SCREEN SKIP.
PARAMETERS: p_date   LIKE sy-datum  OBLIGATORY  DEFAULT sy-datum,
*** MOD-002 * begin ***
*            p_days(3)  TYPE n  OBLIGATORY  DEFAULT 365.
            p_days(4)  TYPE n  DEFAULT 365.
*** MOD-002 * end ***
SELECTION-SCREEN  END OF BLOCK input.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR s_estat-low.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield        = 'ESTAT'
      window_title    = text-h01
      value_org       = 'S'
    TABLES
      value_tab       = gt_tj30t
      return_tab      = gt_return_tab
    EXCEPTIONS
      parameter_error = 1
      no_values_found = 2
      OTHERS          = 3.

  IF sy-subrc EQ 0.
    READ TABLE gt_return_tab INDEX 1.
    s_estat-low = gt_return_tab-fieldval.
  ENDIF.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON s_estat.

  CHECK NOT s_estat-low IS INITIAL.

  READ TABLE gt_tj30t WITH KEY estat = s_estat-low.

  IF sy-subrc NE 0.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'Wrong User Status'(e03)
                 s_estat-low.
  ENDIF.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Timestamp
  CONCATENATE sy-datum sy-uzeit INTO gv_ersda.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

** Default user status
*  s_estat-sign = 'I'.
*  s_estat-option = 'EQ'.
** ZWWC
*  s_estat-low = 'E0001'.
*  APPEND s_estat.
** ZNOC
*  s_estat-low = 'E0002'.
*  APPEND s_estat.
** ZNOS
*  s_estat-low = 'E0004'.
*  APPEND s_estat.

* User status
  SELECT estat txt04 txt30
         INTO TABLE gt_tj30t
         FROM tj30t
         WHERE stsma = 'ZAM00004'
           AND spras = 'E'.
  SORT gt_tj30t.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Selection date
  gv_datum = p_date - p_days.

* Accounting indicator range
  PERFORM build_accind_range.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_equi[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No equipments selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  BUILD_ACCIND_RANGE
*&---------------------------------------------------------------------*
*       Build selection range for accounting indicator (Revenue Based)
*----------------------------------------------------------------------*
FORM build_accind_range .

  CLEAR r_bemot.

  r_bemot-option = 'EQ'.
  r_bemot-sign   = 'I'.
  r_bemot-low    = '1C'.
  APPEND r_bemot.
  r_bemot-low    = '1E'.
  APPEND r_bemot.
  r_bemot-low    = 'CH'.
  APPEND r_bemot.
  r_bemot-low    = 'CI'.
  APPEND r_bemot.
  r_bemot-low    = 'EX'.
  APPEND r_bemot.
  r_bemot-low    = 'FP'.
  APPEND r_bemot.
  r_bemot-low    = 'HS'.
  APPEND r_bemot.
  r_bemot-low    = 'SB'.
  APPEND r_bemot.
  r_bemot-low    = 'SI'.
  APPEND r_bemot.
  r_bemot-low    = 'SP'.
  APPEND r_bemot.

  SORT r_bemot.

ENDFORM.                    " BUILD_ACCIND_RANGE

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Equipment time segment
  SELECT e~equnr e~objnr
         INTO TABLE gt_equi
         FROM equi AS e
         INNER JOIN equz AS z
                    ON z~equnr = e~equnr
         INNER JOIN iloa AS l
                    ON l~iloan = z~iloan
         INNER JOIN jest AS j
                    ON j~objnr = e~objnr
         WHERE e~equnr IN s_equnr
           AND z~datbi =  '99991231'
           AND l~swerk =  p_swerk
           AND j~stat  IN s_estat
           AND j~inact = ' '.
  SORT gt_equi BY equnr.
  DELETE ADJACENT DUPLICATES FROM gt_equi COMPARING equnr.
  CHECK NOT gt_equi[] IS INITIAL.

*** MOD-001 * begin ***
** Deleted/inactive/scrapped equipments
*  CLEAR: gt_jesti[], gt_jest[].
*  SELECT objnr stat
*         INTO TABLE gt_jesti
*         FROM jest
*         FOR ALL ENTRIES IN gt_equi
*         WHERE objnr = gt_equi-objnr
*           AND ( stat = gc_stat_dl   OR
*                 stat = gc_stat_in   OR
*                 stat = gc_stat_scr )
*           AND inact = ' '.
*  SORT gt_jesti BY objnr.
*  DELETE ADJACENT DUPLICATES FROM gt_jesti COMPARING objnr.
*  gt_jest[] = gt_jesti[].
*  FREE gt_jesti.
*
*  LOOP AT gt_equi.
*    READ TABLE gt_jest WITH TABLE KEY objnr = gt_equi-objnr.
*    IF sy-subrc = 0.
*      DELETE gt_equi.
*    ENDIF.
*  ENDLOOP.
*  FREE gt_jest.
*  CHECK NOT gt_equi[] IS INITIAL.
*** MOD-001 * end ***

* Service orders
  SELECT h~equnr o~aufnr o~objnr o~kdauf o~kdpos
         o~waers o~bemot d~getri
         INTO TABLE gt_aufki
         FROM afih AS h
         INNER JOIN afko AS d
                    ON d~aufnr = h~aufnr
         INNER JOIN aufk AS o
                    ON o~aufnr = h~aufnr
         INNER JOIN jest AS j
                    ON j~objnr = o~objnr
         FOR ALL ENTRIES IN gt_equi
         WHERE h~iphas IN ('0', '1', '2', '3', '5', '6')
           AND h~equnr =  gt_equi-equnr
           AND d~getri >= gv_datum
           AND o~bemot IN r_bemot
           AND ( ( j~stat = 'I0009' AND j~inact = ' ' )    OR
                 ( j~stat = 'I0010' AND j~inact = ' ' ) ).
  SORT gt_aufki BY equnr aufnr.
  DELETE ADJACENT DUPLICATES FROM gt_aufki COMPARING equnr aufnr.
  gt_aufk[] = gt_aufki[].
  FREE gt_aufki.

* Costs (Labour)
  SELECT objnr wrt00 wrt01 wrt02 wrt03 wrt04 wrt05
         wrt06 wrt07 wrt08 wrt09 wrt10 wrt11 wrt12
         wrt13 wrt14 wrt15 wrt16
         INTO TABLE gt_pmco
         FROM pmco
         FOR ALL ENTRIES IN gt_aufk
         WHERE objnr = gt_aufk-objnr
           AND cocur = gt_aufk-waers
           AND beltp = '1'
           AND wrttp = '04'
           AND acpos = 'Z03'
           AND bemot = gt_aufk-bemot.

* Contract data
  SELECT vbeln vposn venddat
         INTO TABLE gt_vedai
         FROM veda
         FOR ALL ENTRIES IN gt_aufk
         WHERE vbeln = gt_aufk-kdauf
           AND ( vposn = gt_aufk-kdpos  OR
                 vposn = 0 ).
  SORT gt_vedai BY vbeln vposn.
  DELETE ADJACENT DUPLICATES FROM gt_vedai COMPARING vbeln vposn.
  gt_veda[] = gt_vedai[].
  FREE gt_vedai.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_equi.

    CLEAR: gt_out.
    MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03,
                 gt_out-tab04, gt_out-tab05, gt_out-tab06,
                 gt_out-tab07.
*                 gt_out-tab08.
    gt_out-swerk = p_swerk.
    gt_out-equnr = gt_equi-equnr.

*   Service orders
    LOOP AT gt_aufk WHERE equnr = gt_equi-equnr.

*     Costs (Labour)
      CLEAR gv_wrtsum.
      LOOP AT gt_pmco WHERE objnr = gt_aufk-objnr.
        gv_wrtsum = gt_pmco-wrt00 + gt_pmco-wrt01 + gt_pmco-wrt02 +
                    gt_pmco-wrt03 + gt_pmco-wrt04 + gt_pmco-wrt05 +
                    gt_pmco-wrt06 + gt_pmco-wrt07 + gt_pmco-wrt08 +
                    gt_pmco-wrt09 + gt_pmco-wrt10 + gt_pmco-wrt11 +
                    gt_pmco-wrt12 + gt_pmco-wrt13 + gt_pmco-wrt14 +
                    gt_pmco-wrt15 + gt_pmco-wrt16.
        CHECK gv_wrtsum = 0.
      ENDLOOP.

      CHECK gv_wrtsum NE 0.

      gt_out-aufnr = gt_aufk-aufnr.
      gt_out-getri = gt_aufk-getri.
      gt_out-kdauf = gt_aufk-kdauf.
      gt_out-kdpos = gt_aufk-kdpos.

*     Contract data
      READ TABLE gt_veda WITH TABLE KEY vbeln = gt_aufk-kdauf
                                        vposn = gt_aufk-kdpos.
      IF sy-subrc NE 0.
        READ TABLE gt_veda WITH TABLE KEY vbeln = gt_aufk-kdauf
                                          vposn = 0.
      ENDIF.
      IF sy-subrc = 0.
        gt_out-venddat = gt_veda-venddat.
      ENDIF.

    ENDLOOP.

**   Check deletion flag / inactive / scrapped
*    READ TABLE gt_jest WITH TABLE KEY objnr = gt_equi-objnr.
*    IF sy-subrc = 0.
*      gt_out-incons = 'X'.
*    ENDIF.

    IF NOT gt_out-aufnr IS INITIAL.
      APPEND gt_out.
    ENDIF.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

* Open file
  CONCATENATE 'EQUIPM_SERV_HIST' p_swerk gv_ersda
             INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM fill_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

* Fill output
  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  FILL_HEADER
*&---------------------------------------------------------------------*
*       Fill header
*----------------------------------------------------------------------*
FORM fill_header .

  CLEAR: gt_outh.
  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03,
               gt_outh-tab04, gt_outh-tab05, gt_outh-tab06,
               gt_outh-tab07.
*               gt_outh-tab08.

  gt_outh-swerk   = 'SWERK'.
  gt_outh-equnr   = 'EQUNR'.
  gt_outh-aufnr   = 'AUFNR'.
  gt_outh-getri   = 'CO_GETRI'.
  gt_outh-kdauf   = 'VBELN'.
  gt_outh-kdpos   = 'POSNR'.
  gt_outh-venddat = 'VNDAT_VEDA'.
*  gt_outh-incons  = 'XFELD'.
  APPEND gt_outh.

  gt_outh-swerk   = 'Maint.Plant'.
  gt_outh-equnr   = 'Equipment'.
  gt_outh-aufnr   = 'Order'.
  gt_outh-getri   = 'Conf. End Date'.
  gt_outh-kdauf   = 'Contract'.
  gt_outh-kdpos   = 'Item'.
  gt_outh-venddat = 'Contr. End Date'.
*  gt_outh-incons  = 'Incons.'.
  APPEND gt_outh.

ENDFORM.                    " FILL_HEADER

*Text symbol text£º
*E01:No equipments selected
*E02:Could not open output file
*E03:Wrong User Status
*H01:User Status
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_DATE:        Calculation date
*P_DAYS:        Number of days in the past
*P_SWERK:D       .
*S_EQUNR:D       .
*S_ESTAT:D       .
