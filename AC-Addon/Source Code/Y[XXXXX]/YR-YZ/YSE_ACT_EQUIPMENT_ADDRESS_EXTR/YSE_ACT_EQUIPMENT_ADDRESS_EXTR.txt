*&---------------------------------------------------------------------*
*& Report  YSE_ACT_EQUIPMENT_ADDRESS_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Equipment Address extraction                                 *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 05.09.2012
*  Change Request Number :
*  Transport request Nr. : CD1K973154
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 19.03.2014 | Jules Smets     | CD1K980489    | CR1794 (Act)*
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_act_equipment_address_extr.

TABLES: equi,
        equz,
        iloa,
        jest,
        adrc,
        adr2,
        adr6,
        adrct.

TYPES: BEGIN OF ty_equi,
         equnr      TYPE equnr,
         adrnr      TYPE ad_addrnum,
         objnr      TYPE j_objnr,
      END OF ty_equi.

TYPES: BEGIN OF ty_dlfl,
         objnr      TYPE j_objnr,
      END OF ty_dlfl.

TYPES: BEGIN OF ty_adrc,
         addrnumber  TYPE ad_addrnum,
         name1       TYPE ad_name1,
         name2       TYPE ad_name2,
         street      TYPE ad_street,
         house_num1  TYPE ad_hsnm1,
         post_code1  TYPE ad_pstcd1,
         city1       TYPE ad_city1,
         country     TYPE land1,
         region      TYPE regio,
         tel_number  TYPE ad_tlnmbr1,
         fax_number  TYPE ad_fxnmbr1,
         langu       TYPE spras,
       END OF ty_adrc.

TYPES: BEGIN OF ty_adr6,
         addrnumber  TYPE ad_addrnum,
         consnumber  TYPE ad_consnum,
         smtp_addr   TYPE ad_smtpadr,
       END OF ty_adr6.

TYPES: BEGIN OF ty_adrct,
         addrnumber  TYPE ad_addrnum,
         remark      TYPE ad_remark1,
       END OF ty_adrct.

TYPES: BEGIN OF ty_adr2,
         addrnumber  TYPE ad_addrnum,
         consnumber  TYPE ad_consnum,
         tel_number  TYPE ad_tlnmbr,
       END OF ty_adr2.

DATA: gt_equi  TYPE TABLE OF ty_equi
                    WITH HEADER LINE.

DATA: gt_dlfl  TYPE HASHED TABLE OF ty_dlfl
                    WITH UNIQUE KEY objnr
                    WITH HEADER LINE,
      gt_dlfli TYPE TABLE OF ty_dlfl
                    WITH HEADER LINE.

DATA: gt_adrc  TYPE HASHED TABLE OF ty_adrc
                    WITH UNIQUE KEY addrnumber
                    WITH HEADER LINE,
      gt_adrci TYPE TABLE OF ty_adrc
                    WITH HEADER LINE.

DATA: gt_adr6   TYPE HASHED TABLE OF ty_adr6
                     WITH UNIQUE KEY addrnumber
                     WITH HEADER LINE,
      gt_adr6i  TYPE TABLE OF ty_adr6
                     WITH HEADER LINE.

DATA: gt_adrct  TYPE HASHED TABLE OF ty_adrct
                     WITH UNIQUE KEY addrnumber
                     WITH HEADER LINE,
      gt_adrcti TYPE TABLE OF ty_adrct
                     WITH HEADER LINE.

DATA: gt_adr2   TYPE HASHED TABLE OF ty_adr2
                     WITH UNIQUE KEY addrnumber
                     WITH HEADER LINE,
      gt_adr2i  TYPE TABLE OF ty_adr2
                     WITH HEADER LINE.

DATA: BEGIN OF gt_out  OCCURS 0,
        equnr         TYPE equnr,
        tab01(1)      TYPE c,
        name1         TYPE ad_name1,
        tab02(1)      TYPE c,
        name2         TYPE ad_name2,
        tab03(1)      TYPE c,
        street        TYPE ad_street,
        tab04(1)      TYPE c,
        house_num1    TYPE ad_hsnm1,
        tab05(1)      TYPE c,
        post_code1    TYPE ad_pstcd1,
        tab06(1)      TYPE c,
        city1         TYPE ad_city1,
        tab07(1)      TYPE c,
        country       TYPE land1,
        tab08(1)      TYPE c,
        region        TYPE regio,
        tab09(1)      TYPE c,
        tel_number    TYPE ad_tlnmbr1,
        tab10(1)      TYPE c,
        fax_number    TYPE ad_fxnmbr1,
        tab11(1)      TYPE c,
        smtp_addr(80) TYPE c,
        tab12(1)      TYPE c,
        remark        TYPE ad_remark1,
        tab13(1)      TYPE c,
        mob_number    TYPE ad_tlnmbr,
        tab14(1)      TYPE c,
        langu         TYPE spras,
        tab15(1)      TYPE c,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        equnr         TYPE fieldname,
        tab01(1)      TYPE c,
        name1         TYPE fieldname,
        tab02(1)      TYPE c,
        name2         TYPE fieldname,
        tab03(1)      TYPE c,
        street        TYPE fieldname,
        tab04(1)      TYPE c,
        house_num1    TYPE fieldname,
        tab05(1)      TYPE c,
        post_code1    TYPE fieldname,
        tab06(1)      TYPE c,
        city1         TYPE fieldname,
        tab07(1)      TYPE c,
        country       TYPE fieldname,
        tab08(1)      TYPE c,
        region        TYPE fieldname,
        tab09(1)      TYPE c,
        tel_number    TYPE fieldname,
        tab10(1)      TYPE c,
        fax_number    TYPE fieldname,
        tab11(1)      TYPE c,
        smtp_addr     TYPE fieldname,
        tab12(1)      TYPE c,
        remark        TYPE fieldname,
        tab13(1)      TYPE c,
        mob_number    TYPE fieldname,
        tab14(1)      TYPE c,
        langu         TYPE fieldname,
        tab15(1)      TYPE c,
      END OF gt_outh.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_error         TYPE xfeld.

CONSTANTS: gc_stat_dl  TYPE j_status  VALUE 'I0076',
           gc_stat_in  TYPE j_status  VALUE 'I0320',
           gc_stat_scr TYPE j_status  VALUE 'E0005'.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
PARAMETERS: p_bukrs  LIKE iloa-bukrs  OBLIGATORY  MEMORY ID buk.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_equnr  FOR equi-equnr.
SELECTION-SCREEN  END OF BLOCK input.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_equi[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No equipments selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Equipment time segment
  SELECT e~equnr l~adrnr e~objnr
         INTO TABLE gt_equi
         FROM equi AS e
         INNER JOIN equz AS z
                    ON z~equnr = e~equnr
         INNER JOIN iloa AS l
                    ON l~iloan = z~iloan
         WHERE e~equnr IN s_equnr
           AND z~datbi =  '99991231'
           AND l~bukrs =  p_bukrs
           AND l~adrnr NE ' '.
  CHECK NOT gt_equi[] IS INITIAL.

*** MOD-001 * begin ***
** Check for deleted/scrapped equipments
*  SELECT objnr
*         INTO TABLE gt_dlfli
*         FROM jest
*         FOR ALL ENTRIES IN gt_equi
*         WHERE objnr = gt_equi-objnr
*           AND ( stat = gc_stat_dl   OR
*                 stat = gc_stat_in   OR
*                 stat = gc_stat_scr )
*           AND inact = ' '.
*  SORT gt_dlfli BY objnr.
*  DELETE ADJACENT DUPLICATES FROM gt_dlfli
*                                  COMPARING objnr.
*  gt_dlfl[] = gt_dlfli[].
*  FREE gt_dlfli.
*
*  LOOP AT gt_equi.
*    READ TABLE gt_dlfl WITH TABLE KEY objnr = gt_equi-objnr.
*    IF sy-subrc = 0.
*      DELETE gt_equi.
*    ENDIF.
*  ENDLOOP.
*  FREE gt_dlfl.
*  CHECK NOT gt_equi[] IS INITIAL.
*** MOD-001 * end ***

* Address data
  SELECT addrnumber name1 name2 street house_num1
         post_code1 city1 country region
         tel_number fax_number langu
         INTO TABLE gt_adrci
         FROM adrc
         FOR ALL ENTRIES IN gt_equi
         WHERE addrnumber = gt_equi-adrnr.
  SORT gt_adrci BY addrnumber.
  DELETE ADJACENT DUPLICATES FROM gt_adrci
                                  COMPARING addrnumber.
  gt_adrc[] = gt_adrci[].
  FREE gt_adrci.

* E-Mail Addresses
  SELECT addrnumber consnumber smtp_addr
         INTO TABLE gt_adr6i
         FROM adr6
         FOR ALL ENTRIES IN gt_equi
         WHERE addrnumber = gt_equi-adrnr.
  SORT gt_adr6i BY addrnumber
                   consnumber DESCENDING.
  DELETE ADJACENT DUPLICATES FROM gt_adr6i
         COMPARING addrnumber.
  gt_adr6[] = gt_adr6i[].
  FREE gt_adr6i.

* Comments
  SELECT addrnumber remark
         INTO TABLE gt_adrcti
         FROM adrct
         FOR ALL ENTRIES IN gt_equi
         WHERE addrnumber = gt_equi-adrnr
           AND langu      = 'E'.
  SORT gt_adrcti BY addrnumber.
  DELETE ADJACENT DUPLICATES FROM gt_adr6i
         COMPARING addrnumber.
  gt_adrct[] = gt_adrcti[].
  FREE gt_adrcti.

* Telephone Numbers (Mobile)
  SELECT addrnumber consnumber tel_number
         INTO TABLE gt_adr2i
         FROM adr2
         FOR ALL ENTRIES IN gt_equi
         WHERE addrnumber = gt_equi-adrnr
           AND r3_user    = '3'.
  SORT gt_adr2i BY addrnumber
                   consnumber DESCENDING.
  DELETE ADJACENT DUPLICATES FROM gt_adr2i
         COMPARING addrnumber.
  gt_adr2[] = gt_adr2i[].
  FREE gt_adr2i.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_equi.

    CLEAR: gt_out.
    MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03,
                 gt_out-tab04, gt_out-tab05, gt_out-tab06,
                 gt_out-tab07, gt_out-tab08, gt_out-tab09,
                 gt_out-tab10, gt_out-tab11, gt_out-tab12,
                 gt_out-tab13, gt_out-tab14, gt_out-tab15.

    gt_out-equnr      = gt_equi-equnr.

*   Address data
    READ TABLE gt_adrc
               WITH TABLE KEY addrnumber = gt_equi-adrnr.
    IF sy-subrc = 0.
      gt_out-name1      = gt_adrc-name1.
      gt_out-name2      = gt_adrc-name2.
      gt_out-street     = gt_adrc-street.
      gt_out-house_num1 = gt_adrc-house_num1.
      gt_out-post_code1 = gt_adrc-post_code1.
      gt_out-city1      = gt_adrc-city1.
      gt_out-country    = gt_adrc-country.
      gt_out-region     = gt_adrc-region.
      gt_out-tel_number = gt_adrc-tel_number.
      gt_out-fax_number = gt_adrc-fax_number.
      gt_out-langu      = gt_adrc-langu.
    ENDIF.

*   E-Mail Address
    READ TABLE gt_adr6
               WITH TABLE KEY addrnumber = gt_equi-adrnr.
    IF sy-subrc = 0.
      gt_out-smtp_addr  = gt_adr6-smtp_addr.
    ENDIF.

*   Comment
    READ TABLE gt_adrct
               WITH TABLE KEY addrnumber = gt_equi-adrnr.
    IF sy-subrc = 0.
      gt_out-remark     = gt_adrct-remark.
    ENDIF.

*   Telephone Number (Mobile)
    READ TABLE gt_adr2
               WITH TABLE KEY addrnumber = gt_equi-adrnr.
    IF sy-subrc = 0.
      gt_out-mob_number = gt_adr2-tel_number.
    ENDIF.

    APPEND gt_out.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

  CONCATENATE 'EQUIPMENT' 'ADDRESS_2' p_bukrs
             INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM fill_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

* Fill output
  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  FILL_HEADER
*&---------------------------------------------------------------------*
*       Fill header
*----------------------------------------------------------------------*
FORM fill_header .

  CLEAR: gt_outh.
  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03,
               gt_outh-tab04, gt_outh-tab05, gt_outh-tab06,
               gt_outh-tab07, gt_outh-tab08, gt_outh-tab09,
               gt_outh-tab10, gt_outh-tab11, gt_outh-tab12,
               gt_outh-tab13, gt_outh-tab14, gt_outh-tab15.

  gt_outh-equnr       = 'EQUNR'.
  gt_outh-name1       = 'AD_NAME1'.
  gt_outh-name2       = 'AD_NAME2'.
  gt_outh-street      = 'AD_STREET'.
  gt_outh-house_num1  = 'AD_HSNM1'.
  gt_outh-post_code1  = 'AD_PSTCD1'.
  gt_outh-city1       = 'AD_CITY1'.
  gt_outh-country     = 'LAND1'.
  gt_outh-region      = 'REGIO'.
  gt_outh-tel_number  = 'AD_TLNMBR1'.
  gt_outh-fax_number  = 'AD_FXNMBR1'.
  gt_outh-smtp_addr   = 'AD_SMTPADR'.
  gt_outh-remark      = 'AD_REMARK1'.
  gt_outh-mob_number  = 'AD_TLNMBR'.
  gt_outh-langu       = 'SPRAS'.
  APPEND gt_outh.

  gt_outh-equnr       = 'Equipment'.
  gt_outh-name1       = 'Name 1'.
  gt_outh-name2       = 'Name 2'.
  gt_outh-street      = 'Street'.
  gt_outh-house_num1  = 'House No.'.
  gt_outh-post_code1  = 'Post.Code'.
  gt_outh-city1       = 'City'.
  gt_outh-country     = 'Country'.
  gt_outh-region      = 'Region'.
  gt_outh-tel_number  = 'Telephone No.'.
  gt_outh-fax_number  = 'Fax No.'.
  gt_outh-smtp_addr   = 'E-mail address'.
  gt_outh-remark      = 'Comments'.
  gt_outh-mob_number  = 'Mobile No.'.
  gt_outh-langu       = 'Language'.
  APPEND gt_outh.

ENDFORM.                    " FILL_HEADER

*Text symbol text£º
*E01:No equipments selected
*E02:Could not open output file
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_BUKRS:D       .
*S_EQUNR:D       .
