************************************************************************
* Program ID           : YSE_MAM_FTFKIT_EXPORT_IMPORT                  *
* Program Title        : Export and Import First-Time-Fix Kit Part     *
*                        assignment to and from input file             *
* Author               : Lakshmi Reddy                                 *
* Date                 : 09/02/2011                                    *
* Change Request Number: CD1K962777                                    *
* Description          : This program Export/Import First-Time-Fix kit *
*                        part assignment data to/from Input file       *
*======================================================================*
* Copied From          : N/A                                           *
* Title                : N/A                                           *
* Other Related obj    : N/A                                           *
*======================================================================*
* Change History Log                                                   *
*----------------------------------------------------------------------*
* Mod. no.|Date      |Name        |Correction Number|Change Reference  *
*----------------------------------------------------------------------*
* MOD-001 |09/02/2011|LakshmiReddy|CD1K962777       |CR1289            *
* Description: Initial Development                                     *
* MOD-002 |07/12/2011|L. Mertens  |CD1K968100       |CR1289            *
* Description: Initial Development                                     *
*----------------------------------------------------------------------*

REPORT  yse_mam_ftfkit_export_import MESSAGE-ID yam_re.

************************************************************************
*                  T Y P E S  S T R U C T U R E S
************************************************************************
TYPE-POOLS: slis.

TYPES: BEGIN OF ty_ftfkit,
        werks         TYPE  werks_d,
        vkorg         TYPE  vkorg,
        arbpl         TYPE  arbpl,
        z_draw_id     TYPE  z_draw_id,
        z_draw_lay    TYPE  z_draw_lay,
        z_draw_des    TYPE  z_draw_des,
        z_draw_box_id TYPE  z_draw_box_id,
        matnr         TYPE  matnr,
        maktx         TYPE  maktx,
        menge         TYPE  menge_d,
        meins         TYPE  meins,
        z_part_type   TYPE  z_part_type,
       END   OF ty_ftfkit.
************************************************************************
*                 I N T E R N A L  T A B L E S                         *
************************************************************************
DATA: i_ftfkit        TYPE STANDARD TABLE OF ty_ftfkit,
      i_modify_ftfkit TYPE STANDARD TABLE OF yse_mam_ftfkit,
      i_fieldcatalog TYPE slis_t_fieldcat_alv,
      i_events       TYPE slis_t_event,
      i_header       TYPE slis_t_listheader.

*----------------------------------------------------------------------*
*                 V A R I A B L E S                                    *
*----------------------------------------------------------------------*
DATA: wa_ftfkit TYPE ty_ftfkit,
      v_vkorg   TYPE vkorg.

*----------------------------------------------------------------------*
*                 S E L E C T I O N  S C R E E N                       *
*----------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK s1 WITH FRAME TITLE text-001.
PARAMETERS: p_werks TYPE werks_d OBLIGATORY.
PARAMETERS:p_export RADIOBUTTON GROUP rad1 USER-COMMAND port DEFAULT 'X'.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN POSITION 5.
SELECTION-SCREEN COMMENT (25) text-002.
SELECTION-SCREEN POSITION 30.
PARAMETERS: p_efile TYPE rlgrap-filename.
SELECTION-SCREEN END OF LINE.
PARAMETERS: p_import RADIOBUTTON GROUP rad1.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN POSITION 5.
SELECTION-SCREEN COMMENT (25) text-003.
SELECTION-SCREEN POSITION 30.
PARAMETERS: p_ifile TYPE rlgrap-filename.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK s1.

*----------------------------------------------------------------------*
*                 A T  S E L E C T I O N  S C R E E N                  *
*----------------------------------------------------------------------*

*F4-Help for Export file
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_efile.
  PERFORM f100_f4_help_for_export_file CHANGING p_efile.

*F4 help for Import File
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_ifile.
  PERFORM f200_f4_help_for_import_file CHANGING p_ifile.

*----------------------------------------------------------------------*
*         A T  S E L E C T I O N  S C R E E N   O U T P U T            *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.
  PERFORM f300_modify_screen_table.

************************************************************************
*                 S T A R T - O F - S E L E C T I O N
************************************************************************
START-OF-SELECTION.

  IF p_export EQ 'X' AND p_efile IS INITIAL.
    MESSAGE s320 WITH 'Select Export File Path and Name'(005).
    EXIT.
  ENDIF.

  IF p_import EQ 'X' AND p_ifile IS INITIAL.
    MESSAGE s320 WITH 'Select Import File Path and Name'(006).
    EXIT.
  ENDIF.

  IF p_export EQ 'X'.
    PERFORM f400_get_data_from_ftfkit.
    PERFORM f500_export_data_to_file.
  ENDIF.

  IF p_import EQ 'X'.
    PERFORM f600_import_data_from_file.
    PERFORM f700_update_ftfkit_table.
  ENDIF.
*---------------------------F O R M S----------------------------------*

*&---------------------------------------------------------------------*
*&      Form  F100_F4_HELP_FOR_EXPORT_FILE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM f100_f4_help_for_export_file  CHANGING f_c_file.

  DATA:lv_title    TYPE string,
       lv_usract   TYPE i,
       lv_deffile  TYPE string,
       lv_filename TYPE string,
       lv_path     TYPE string,
       lv_fullpath TYPE string.

  IF p_export EQ 'X'.
    lv_title = 'Select Path/File for Export'.
    MOVE f_c_file TO lv_deffile.
    CALL METHOD cl_gui_frontend_services=>file_save_dialog
      EXPORTING
        window_title         = lv_title
        default_file_name    = lv_deffile
        file_filter          = '*.*|*.*'
        prompt_on_overwrite  = 'X'
      CHANGING
        filename             = lv_filename
        path                 = lv_path
        fullpath             = lv_fullpath
        user_action          = lv_usract
      EXCEPTIONS
        cntl_error           = 1
        error_no_gui         = 2
        not_supported_by_gui = 3
        OTHERS               = 4.
    IF sy-subrc = 0
          AND lv_usract <>
          cl_gui_frontend_services=>action_cancel.
      MOVE lv_fullpath TO f_c_file.
    ELSEIF lv_usract EQ cl_gui_frontend_services=>action_cancel.
      MOVE lv_deffile TO f_c_file.
    ENDIF.

  ENDIF.

ENDFORM.                    " F100_F4_HELP_FOR_EXPORT_FILE

*&---------------------------------------------------------------------*
*&      Form  F200_F4_HELP_FOR_IMPORT_FILE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM f200_f4_help_for_import_file  CHANGING f_c_ifile.

  DATA: lv_title    TYPE string,
        lv_subrc    TYPE i,
        lv_usract   TYPE i,
        lv_deffile  TYPE string,
        i_files     TYPE filetable,
        wa_file    TYPE file_table.

  IF p_import EQ 'X'.
    lv_title = 'Select Input File for Import'.
    MOVE f_c_ifile TO lv_deffile.
    CALL METHOD cl_gui_frontend_services=>file_open_dialog
      EXPORTING
        window_title            = lv_title
        default_filename        = lv_deffile
        file_filter             = '*.*|*.*'
      CHANGING
        file_table              = i_files
        rc                      = lv_subrc
        user_action             = lv_usract
      EXCEPTIONS
        file_open_dialog_failed = 1
        cntl_error              = 2
        error_no_gui            = 3
        not_supported_by_gui    = 4
        OTHERS                  = 5.
    IF sy-subrc = 0 AND lv_usract <> cl_gui_frontend_services=>action_cancel.
      LOOP AT  i_files INTO wa_file.
        MOVE wa_file-filename TO f_c_ifile.
        EXIT.
      ENDLOOP.
    ELSEIF lv_usract EQ cl_gui_frontend_services=>action_cancel.
      MOVE lv_deffile TO f_c_ifile.
    ENDIF.
  ENDIF.

ENDFORM.                    " F200_F4_HELP_FOR_IMPORT_FILE
*&---------------------------------------------------------------------*
*&      Form  F300_MODIFY_SCREEN_TABLE
*&---------------------------------------------------------------------*
*       Modify SCREEN table
*----------------------------------------------------------------------*
FORM f300_modify_screen_table .

  LOOP AT SCREEN.
    IF screen-name EQ 'P_EFILE'.
      IF p_import EQ 'X'.
        CLEAR: p_efile.
        screen-input = '0'.
        MODIFY SCREEN.
      ENDIF.
    ENDIF.
    IF screen-name EQ 'P_IFILE'.
      IF p_export EQ 'X'.
        CLEAR: p_ifile.
        screen-input = '0'.
        MODIFY SCREEN.
      ENDIF.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " F300_MODIFY_SCREEN_TABLE
*&---------------------------------------------------------------------*
*&      Form  F400_GET_DATA_FROM_FTFKIT
*&---------------------------------------------------------------------*
*       Get data from YSE_MAM_FTFKIT
*----------------------------------------------------------------------*
FORM f400_get_data_from_ftfkit .

  REFRESH: i_ftfkit.
  SELECT  werks
          vkorg
          arbpl
          z_draw_id
          z_draw_lay
          z_draw_des
          z_draw_box_id
          matnr
          maktx
          menge
          meins
          z_part_type
  FROM   yse_mam_ftfkit
  INTO   TABLE i_ftfkit
  WHERE  werks EQ p_werks.

ENDFORM.                    " F400_GET_DATA_FROM_FTFKIT
*&---------------------------------------------------------------------*
*&      Form  F500_EXPORT_DATA_TO_FILE
*&---------------------------------------------------------------------*
*       Export table data to file
*----------------------------------------------------------------------*
FORM f500_export_data_to_file .

  DATA: lv_file TYPE string,
        lv_type TYPE char10.

  MOVE p_efile TO lv_file.
  MOVE 'DAT'  TO lv_type.

  IF NOT i_ftfkit IS INITIAL.
    CALL FUNCTION 'GUI_DOWNLOAD'
      EXPORTING
        filename                = lv_file
        filetype                = lv_type
      TABLES
        data_tab                = i_ftfkit
      EXCEPTIONS
        file_write_error        = 1
        no_batch                = 2
        gui_refuse_filetransfer = 3
        invalid_type            = 4
        no_authority            = 5
        unknown_error           = 6
        header_not_allowed      = 7
        separator_not_allowed   = 8
        filesize_not_allowed    = 9
        header_too_long         = 10
        dp_error_create         = 11
        dp_error_send           = 12
        dp_error_write          = 13
        unknown_dp_error        = 14
        access_denied           = 15
        dp_out_of_memory        = 16
        disk_full               = 17
        dp_timeout              = 18
        file_not_found          = 19
        dataprovider_exception  = 20
        control_flush_error     = 21
        OTHERS                  = 22.
    IF sy-subrc EQ 0.
      SKIP 2.
      WRITE:/ 'A File',lv_file,'has been created with First-Time-Fix Kit Part Assignment Data'.
    ELSE.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.
  ELSE.
    SKIP 2.
    WRITE:/ 'No data has been selected from the table YSE_MAM_FTFKIT for the Plant',p_werks.
    SKIP.
    WRITE:/ 'No data has been exported to the file.'.
  ENDIF.

ENDFORM.                    " F500_EXPORT_DATA_TO_FILE
*&---------------------------------------------------------------------*
*&      Form  F600_IMPORT_DATA_FROM_FILE
*&---------------------------------------------------------------------*
*       Upload Data from file into internal table
*----------------------------------------------------------------------*
FORM f600_import_data_from_file .

  DATA: lv_file TYPE string,
        lv_type TYPE char10.

  MOVE p_ifile TO lv_file.
  MOVE 'DAT'   TO lv_type.

  REFRESH: i_ftfkit.
  CALL FUNCTION 'GUI_UPLOAD'
    EXPORTING
      filename                = lv_file
      filetype                = lv_type
    TABLES
      data_tab                = i_ftfkit
    EXCEPTIONS
      file_open_error         = 1
      file_read_error         = 2
      no_batch                = 3
      gui_refuse_filetransfer = 4
      invalid_type            = 5
      no_authority            = 6
      unknown_error           = 7
      bad_data_format         = 8
      header_not_allowed      = 9
      separator_not_allowed   = 10
      header_too_long         = 11
      unknown_dp_error        = 12
      access_denied           = 13
      dp_out_of_memory        = 14
      disk_full               = 15
      dp_timeout              = 16
      OTHERS                  = 17.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
  IF i_ftfkit IS INITIAL.
    SKIP 2.
    WRITE:/ 'No records have been updated in the table YSE_MAM_FTFKIT.'.
    SKIP.
    WRITE:/ 'The file ',lv_file,'does not contain any records'.
  ENDIF.
ENDFORM.                    " F600_IMPORT_DATA_FROM_FILE
*&---------------------------------------------------------------------*
*&      Form  F700_UPDATE_FTFKIT_TABLE
*&---------------------------------------------------------------------*
*       Update table YSE_MAM_FTFKIT table
*----------------------------------------------------------------------*
FORM f700_update_ftfkit_table .

  DATA: wa_mod_ftfkit TYPE yse_mam_ftfkit,
        lv_file TYPE string.

  MOVE p_ifile TO lv_file.

  IF NOT i_ftfkit IS INITIAL.
*Consider records from input file only for the Plant entered on
*selected screen
    DELETE i_ftfkit WHERE werks NE p_werks.

    IF NOT i_ftfkit IS INITIAL.
*Delete records from YSE_MAM_FTFKIT before updating if the same
*records exist in the file
      REFRESH: i_modify_ftfkit.
      IF NOT i_ftfkit IS INITIAL.
        SELECT *
        FROM   yse_mam_ftfkit
        INTO   TABLE i_modify_ftfkit
        FOR    ALL ENTRIES IN i_ftfkit
        WHERE  werks EQ i_ftfkit-werks.
*        AND    vkorg EQ i_ftfkit-vkorg
*        AND    arbpl EQ i_ftfkit-arbpl
*        AND    z_draw_box_id EQ i_ftfkit-z_draw_box_id.
        IF sy-subrc EQ 0.
          DELETE yse_mam_ftfkit FROM TABLE i_modify_ftfkit.
        ENDIF.
      ENDIF.

      REFRESH: i_modify_ftfkit.
      CLEAR  : wa_ftfkit,
               wa_mod_ftfkit.
      LOOP AT i_ftfkit INTO wa_ftfkit.
        MOVE-CORRESPONDING wa_ftfkit TO wa_mod_ftfkit.
* begin of mod-002
*...... Map the input data into internal format (leading zeroes)
        CALL FUNCTION 'CONVERSION_EXIT_MATN1_INPUT'
          EXPORTING
            input  = wa_mod_ftfkit-matnr
          IMPORTING
            output = wa_mod_ftfkit-matnr.
* end of mod-002
        APPEND wa_mod_ftfkit TO i_modify_ftfkit.
        CLEAR: wa_ftfkit,
               wa_mod_ftfkit.
      ENDLOOP.

      IF NOT i_modify_ftfkit IS INITIAL.
        MODIFY yse_mam_ftfkit FROM TABLE i_modify_ftfkit.
        IF sy-subrc EQ 0.
          COMMIT WORK.
          PERFORM f800_display_report.
        ELSE.
          ROLLBACK WORK.
          SKIP 2.
          WRITE:/ 'No records have been updated in the table YSE_MAM_FTFKIT.'.
          SKIP.
          WRITE:/ 'A Database Error has occurred while updating the table'.
        ENDIF.
      ENDIF.
    ELSE.
      SKIP 2.
      WRITE:/ 'No records have been updated in the table YSE_MAM_FTFKIT.'.
      SKIP.
      WRITE:/ 'The file ',lv_file,' does not contain any records for the Plant',p_werks.
    ENDIF.
  ENDIF.
ENDFORM.                    " F700_UPDATE_FTFKIT_TABLE
*&---------------------------------------------------------------------*
*&      Form  F800_DISPLAY_REPORT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM f800_display_report.

  PERFORM f810_build_field_catalog.

  PERFORM f820_build_events.

  PERFORM f830_display_alv_report.

ENDFORM.                    " F800_DISPLAY_REPORT
*&---------------------------------------------------------------------*
*&      Form  F510_BUILD_FIELD_CATALOG
*&---------------------------------------------------------------------*
*       Build field catalog
*----------------------------------------------------------------------*
FORM f810_build_field_catalog .

  PERFORM f811_field_catalog USING 'WERKS'
                                   'Plant'.

  PERFORM f811_field_catalog USING 'VKORG'
                                   'Sales Organization'.

  PERFORM f811_field_catalog USING 'ARBPL'
                                   'Work center'.

  PERFORM f811_field_catalog USING 'Z_DRAW_ID'
                                   'Drawer Identification'.

  PERFORM f811_field_catalog USING 'Z_DRAW_LAY'
                                   'Drawer Layout'.

  PERFORM f811_field_catalog USING 'Z_DRAW_DES'
                                   'Drawer Description'.

  PERFORM f811_field_catalog USING 'Z_DRAW_BOX_ID'
                                   'Drawer Box Identification'.

  PERFORM f811_field_catalog USING 'MATNR'
                                   'Material Number'.

  PERFORM f811_field_catalog USING 'MAKTX'
                                   'Material Description'.

  PERFORM f811_field_catalog USING 'MENGE'
                                   'Quantity'.

  PERFORM f811_field_catalog USING 'MEINS'
                                   'Unit of Measure'.

  PERFORM f811_field_catalog USING 'Z_PART_TYPE'
                                   'Part Type'.

ENDFORM.                    " F810_BUILD_FIELD_CATALOG

*&---------------------------------------------------------------------*
*&      Form  F811_FIELD_CATALOG
*&---------------------------------------------------------------------*
*       Prepare field catalog with output fields
*----------------------------------------------------------------------*
FORM f811_field_catalog USING p_fname   TYPE any
                              p_seltext TYPE any.

  DATA: wa_fieldcatalog TYPE slis_fieldcat_alv.

*appending parameters of a particular field of a fieldcatalog
  wa_fieldcatalog-tabname   = 'I_MODIFY_FTFKIT'.
  wa_fieldcatalog-fieldname = p_fname.
  wa_fieldcatalog-seltext_m = p_seltext.

*appending parameters of a particular field to fieldcatalog
  APPEND wa_fieldcatalog TO i_fieldcatalog.

ENDFORM.                    " f811_field_catalog

*---------------------------------------------------------------------*
*       FORM top_of_page                                              *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
FORM top_of_page.

  DATA : wa_header TYPE slis_listheader.

  CLEAR wa_header.
  REFRESH i_header.

  wa_header-typ = 'H'.
  wa_header-info = 'List of Records updated in the table YSE_MAM_FTFKIT'.
  APPEND wa_header TO i_header.

  CALL FUNCTION 'REUSE_ALV_COMMENTARY_WRITE'
    EXPORTING
      it_list_commentary = i_header.

ENDFORM.                    "top_of_page

*&---------------------------------------------------------------------*
*&      Form  f820_build_events
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM f820_build_events.

  DATA : wa_events TYPE slis_alv_event.

  CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
    EXPORTING
      i_list_type = 0
    IMPORTING
      et_events   = i_events.

  READ TABLE i_events WITH KEY name = 'TOP_OF_PAGE'
                                      INTO wa_events.

  wa_events-form = 'TOP_OF_PAGE'.

  MODIFY i_events
  FROM wa_events
  TRANSPORTING form
  WHERE name EQ wa_events-name.

ENDFORM.                    " f820_build_events

*&---------------------------------------------------------------------*
*&      Form  f830_display_alv_report
*&---------------------------------------------------------------------*
*       Display ALV report
*----------------------------------------------------------------------*
FORM f830_display_alv_report .

  DATA: l_repid TYPE sy-repid.

  l_repid = sy-repid.
  SORT i_ftfkit BY werks vkorg arbpl z_draw_box_id.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program = l_repid
      it_fieldcat        = i_fieldcatalog
      it_events          = i_events
    TABLES
      t_outtab           = i_modify_ftfkit
    EXCEPTIONS
      program_error      = 1
      OTHERS             = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " f830_display_alv_report

*Text symbol text£º
*001:Selection Criteria
*002:Export File Path and Name

*003:Import File Path and Name
*Selection text£º
*P_EFILE:        Export Data from SAP to File
*P_EXPORT:        Export Data from SAP to File
*P_IFILE:        Import Data from File to SAP
*P_IMPORT:        Import Data from File to SAP
*P_WERKS:        Plant
*S_VKORG:        Sales Organization
