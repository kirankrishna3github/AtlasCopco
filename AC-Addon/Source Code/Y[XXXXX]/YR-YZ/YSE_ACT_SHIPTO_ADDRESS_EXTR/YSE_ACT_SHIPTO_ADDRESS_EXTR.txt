*&---------------------------------------------------------------------*
*& Report  YSE_ACT_SHIPTO_ADDRESS_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Ship-to Address extraction                                   *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 24.07.2012
*  Change Request Number :
*  Transport request Nr. : CD1K972671
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                 | CD1K9.....    |             *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_act_shipto_address_extr.

TABLES: knvp,
        kna1,
        knvv,
        adrc.

TYPES: BEGIN OF ty_knvp,
         kunnr       TYPE kunnr,
         kunn2       TYPE kunn2,
       END OF ty_knvp.

TYPES: BEGIN OF ty_kna1,
         kunnr       TYPE kunnr,
         sortl       TYPE sortl,
         ktokd       TYPE ktokd,
       END OF ty_kna1.

TYPES: BEGIN OF ty_knvv,
         kunnr       TYPE kunnr,
         bzirk       TYPE bzirk,
         vkgrp       TYPE vkgrp,
         vkbur       TYPE vkbur,
       END OF ty_knvv.

TYPES: BEGIN OF ty_adrc,
         kunnr       TYPE kunnr,
         name1       TYPE ad_name1,
         name2       TYPE ad_name2,
         name3       TYPE ad_name3,
         name4       TYPE ad_name4,
         name_co     TYPE ad_name_co,
         street      TYPE ad_street,
         house_num1  TYPE ad_hsnm1,
         post_code1  TYPE ad_pstcd1,
         city1       TYPE ad_city1,
         country     TYPE land1,
         tel_number  TYPE ad_tlnmbr1,
         fax_number  TYPE ad_fxnmbr1,
         region      TYPE regio,
         transpzone  TYPE lzone,
       END OF ty_adrc.

DATA: gt_knvp   TYPE TABLE OF ty_knvp
                     WITH HEADER LINE.

DATA: gt_kna1   TYPE HASHED TABLE OF ty_kna1
                     WITH UNIQUE KEY kunnr
                     WITH HEADER LINE,
      gt_kna1i  TYPE TABLE OF ty_kna1
                     WITH HEADER LINE.

DATA: gt_knvv   TYPE HASHED TABLE OF ty_knvv
                     WITH UNIQUE KEY kunnr
                     WITH HEADER LINE,
      gt_knvvi  TYPE TABLE OF ty_knvv
                     WITH HEADER LINE.

DATA: gt_adrc   TYPE HASHED TABLE OF ty_adrc
                     WITH UNIQUE KEY kunnr
                     WITH HEADER LINE,
      gt_adrci  TYPE TABLE OF ty_adrc
                     WITH HEADER LINE.

DATA: BEGIN OF gt_out  OCCURS 0,
        vkorg       TYPE vkorg,
        tab01(1)    TYPE c,
        kunnr_ag    TYPE kunnr,
        tab02(1)    TYPE c,
        bzirk       TYPE bzirk,
        tab03(1)    TYPE c,
        vkgrp       TYPE vkgrp,
        tab04(1)    TYPE c,
        vkbur       TYPE vkbur,
        tab05(1)    TYPE c,
        ktokd_ag    TYPE ktokd,
        tab23(1)    TYPE c,
        sortl_ag    TYPE sortl,
        tab06(1)    TYPE c,
        kunnr_we    TYPE kunnr,
        tab07(1)    TYPE c,
        sortl_we    TYPE sortl,
        tab08(1)    TYPE c,
        name1       TYPE ad_name1,
        tab09(1)    TYPE c,
        name2       TYPE ad_name2,
        tab10(1)    TYPE c,
        name3       TYPE ad_name3,
        tab11(1)    TYPE c,
        name4       TYPE ad_name4,
        tab12(1)    TYPE c,
        name_co     TYPE ad_name_co,
        tab22(1)    TYPE c,
        street      TYPE ad_street,
        tab13(1)    TYPE c,
        house_num1  TYPE ad_hsnm1,
        tab14(1)    TYPE c,
        post_code1  TYPE ad_pstcd1,
        tab15(1)    TYPE c,
        city1       TYPE ad_city1,
        tab16(1)    TYPE c,
        country     TYPE land1,
        tab17(1)    TYPE c,
        tel_number  TYPE ad_tlnmbr1,
        tab18(1)    TYPE c,
        fax_number  TYPE ad_fxnmbr1,
        tab19(1)    TYPE c,
        region      TYPE regio,
        tab20(1)    TYPE c,
        transpzone  TYPE lzone,
        tab21(1)    TYPE c,
        ktokd_we    TYPE ktokd,
        tab24(1)    TYPE c,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        vkorg       TYPE fieldname,
        tab01(1)    TYPE c,
        kunnr_ag    TYPE fieldname,
        tab02(1)    TYPE c,
        bzirk       TYPE fieldname,
        tab03(1)    TYPE c,
        vkgrp       TYPE fieldname,
        tab04(1)    TYPE c,
        vkbur       TYPE fieldname,
        tab05(1)    TYPE c,
        ktokd_ag    TYPE fieldname,
        tab23(1)    TYPE c,
        sortl_ag    TYPE fieldname,
        tab06(1)    TYPE c,
        kunnr_we    TYPE fieldname,
        tab07(1)    TYPE c,
        sortl_we    TYPE fieldname,
        tab08(1)    TYPE c,
        name1       TYPE fieldname,
        tab09(1)    TYPE c,
        name2       TYPE fieldname,
        tab10(1)    TYPE c,
        name3       TYPE fieldname,
        tab11(1)    TYPE c,
        name4       TYPE fieldname,
        tab12(1)    TYPE c,
        name_co     TYPE fieldname,
        tab22(1)    TYPE c,
        street      TYPE fieldname,
        tab13(1)    TYPE c,
        house_num1  TYPE fieldname,
        tab14(1)    TYPE c,
        post_code1  TYPE fieldname,
        tab15(1)    TYPE c,
        city1       TYPE fieldname,
        tab16(1)    TYPE c,
        country     TYPE fieldname,
        tab17(1)    TYPE c,
        tel_number  TYPE fieldname,
        tab18(1)    TYPE c,
        fax_number  TYPE fieldname,
        tab19(1)    TYPE c,
        region      TYPE fieldname,
        tab20(1)    TYPE c,
        transpzone  TYPE fieldname,
        tab21(1)    TYPE c,
        ktokd_we    TYPE fieldname,
        tab24(1)    TYPE c,
      END OF gt_outh.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_ersda(14)     TYPE c,                      "YYYYMMDDHHMMSS
      gv_error         TYPE xfeld.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
PARAMETERS: p_vkorg  LIKE knvp-vkorg  OBLIGATORY  MEMORY ID vko.
SELECTION-SCREEN  END OF BLOCK input.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Timestamp
  CONCATENATE sy-datum sy-uzeit INTO gv_ersda.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_knvp[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No Ship-to selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Select sales partners
  SELECT kunnr kunn2
         INTO TABLE gt_knvp
         FROM knvp
         WHERE vkorg = p_vkorg
           AND parvw = 'WE'.                      "#EC CI_NOFIRST

  SORT gt_knvp.
  DELETE ADJACENT DUPLICATES FROM gt_knvp.

*  LOOP AT gt_knvp.
*    IF gt_knvp-kunnr = gt_knvp-kunn2.
*      DELETE gt_knvp.
*    ENDIF.
*  ENDLOOP.

  CHECK NOT gt_knvp[] IS INITIAL.

* Search Codes
  SELECT kunnr sortl ktokd
         INTO TABLE gt_kna1i
         FROM kna1
         FOR ALL ENTRIES IN gt_knvp
         WHERE ( kunnr = gt_knvp-kunnr  OR
                 kunnr = gt_knvp-kunn2 )
           AND loevm = ' '.
  SORT gt_kna1i BY kunnr.
  DELETE ADJACENT DUPLICATES FROM gt_kna1i
         COMPARING kunnr.
  gt_kna1[] = gt_kna1i[].
  FREE gt_kna1i.

* Sales info (Sold-to)
  SELECT kunnr bzirk vkgrp vkbur
         INTO TABLE gt_knvvi
         FROM knvv
         FOR ALL ENTRIES IN gt_knvp
         WHERE kunnr = gt_knvp-kunnr
           AND vkorg = p_vkorg.
  SORT gt_knvvi BY kunnr.
  DELETE ADJACENT DUPLICATES FROM gt_knvvi
         COMPARING kunnr.
  gt_knvv[] = gt_knvvi[].
  FREE gt_knvvi.

* Addresses (Ship-to)
  SELECT c~kunnr a~name1 a~name2 a~name3 a~name4 a~name_co
         a~street a~house_num1 a~post_code1 a~city1 a~country
         a~tel_number a~fax_number a~region a~transpzone
         INTO TABLE gt_adrci
         FROM kna1 AS c
         INNER JOIN adrc AS a
                         ON c~adrnr = a~addrnumber
         FOR ALL ENTRIES IN gt_knvp
         WHERE c~kunnr = gt_knvp-kunn2
           AND c~loevm = ' '.
  SORT gt_adrci BY kunnr.
  DELETE ADJACENT DUPLICATES FROM gt_adrci
         COMPARING kunnr.
  gt_adrc[] = gt_adrci[].
  FREE gt_adrci.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_knvp.

    CLEAR gt_out.

    MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03,
                 gt_out-tab04, gt_out-tab05, gt_out-tab06,
                 gt_out-tab07, gt_out-tab08, gt_out-tab09,
                 gt_out-tab10, gt_out-tab11, gt_out-tab12,
                 gt_out-tab13, gt_out-tab14, gt_out-tab15,
                 gt_out-tab16, gt_out-tab17, gt_out-tab18,
                 gt_out-tab19, gt_out-tab20, gt_out-tab21,
                 gt_out-tab22, gt_out-tab23, gt_out-tab24.
    gt_out-vkorg = p_vkorg.

*   Sold-to
    IF gt_knvp-kunnr NE gt_knvp-kunn2.
      gt_out-kunnr_ag = gt_knvp-kunnr.
      READ TABLE gt_kna1 WITH TABLE KEY kunnr = gt_knvp-kunnr.
      IF sy-subrc = 0.
        gt_out-sortl_ag = gt_kna1-sortl.
        gt_out-ktokd_ag = gt_kna1-ktokd.
      ELSE.
        CONTINUE.
      ENDIF.
      READ TABLE gt_knvv WITH TABLE KEY kunnr = gt_knvp-kunnr.
      IF sy-subrc = 0.
        MOVE-CORRESPONDING gt_knvv TO gt_out.
      ENDIF.
    ENDIF.

*   Ship-to
    gt_out-kunnr_we = gt_knvp-kunn2.
    READ TABLE gt_kna1 WITH TABLE KEY kunnr = gt_knvp-kunn2.
    IF sy-subrc = 0.
      gt_out-sortl_we = gt_kna1-sortl.
      gt_out-ktokd_we = gt_kna1-ktokd.
    ELSE.
      CONTINUE.
    ENDIF.
*   Address
    READ TABLE gt_adrc WITH TABLE KEY kunnr = gt_knvp-kunn2.
    IF sy-subrc = 0.
      MOVE-CORRESPONDING gt_adrc TO gt_out.
    ENDIF.

    APPEND gt_out.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

  CONCATENATE 'SHIP-TO' 'ADDRESS' p_vkorg gv_ersda
             INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM build_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

* Fill output
  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  BUILD_HEADER
*&---------------------------------------------------------------------*
*       Build header (in output table)
*----------------------------------------------------------------------*
FORM build_header .

  CLEAR gt_outh.

  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03,
               gt_outh-tab04, gt_outh-tab05, gt_outh-tab06,
               gt_outh-tab07, gt_outh-tab08, gt_outh-tab09,
               gt_outh-tab10, gt_outh-tab11, gt_outh-tab12,
               gt_outh-tab13, gt_outh-tab14, gt_outh-tab15,
               gt_outh-tab16, gt_outh-tab17, gt_outh-tab18,
               gt_outh-tab19, gt_outh-tab20, gt_outh-tab21,
               gt_outh-tab22, gt_outh-tab23, gt_outh-tab24.

  gt_outh-vkorg      = 'VKORG'.
  gt_outh-kunnr_ag   = 'KUNNR_ag'.
  gt_outh-bzirk      = 'BZIRK'.
  gt_outh-vkgrp      = 'VKGRP'.
  gt_outh-vkbur      = 'VKBUR'.
  gt_outh-ktokd_ag   = 'KTOKD_ag'.
  gt_outh-sortl_ag   = 'SORTL_ag'.
  gt_outh-kunnr_we   = 'KUNNR_we'.
  gt_outh-sortl_we   = 'SORTL_we'.
  gt_outh-name1      = 'AD_NAME1'.
  gt_outh-name2      = 'AD_NAME2'.
  gt_outh-name3      = 'AD_NAME3'.
  gt_outh-name4      = 'AD_NAME4'.
  gt_outh-name_co    = 'AD_NAME_CO'.
  gt_outh-street     = 'AD_STREET'.
  gt_outh-house_num1 = 'AD_HSNM1'.
  gt_outh-post_code1 = 'AD_PSTCD1'.
  gt_outh-city1      = 'AD_CITY1'.
  gt_outh-country    = 'LAND1'.
  gt_outh-tel_number = 'AD_TLNMBR1'.
  gt_outh-fax_number = 'AD_FXNMBR1'.
  gt_outh-region     = 'REGIO'.
  gt_outh-transpzone = 'LZONE'.
  gt_outh-ktokd_we   = 'KTOKD_we'.
  APPEND gt_outh.

  gt_outh-vkorg      = 'Sales Org.'.
  gt_outh-kunnr_ag   = 'Sold-to'.
  gt_outh-bzirk      = 'District'.
  gt_outh-vkgrp      = 'Sales Grp.'.
  gt_outh-vkbur      = 'Sales Off.'.
  gt_outh-ktokd_ag   = 'Acc.Grp.(Sold-to)'.
  gt_outh-sortl_ag   = 'Search (Sold-to)'.
  gt_outh-kunnr_we   = 'Ship-to'.
  gt_outh-sortl_we   = 'Search (Ship-to)'.
  gt_outh-name1      = 'Name1'.
  gt_outh-name2      = 'Name2'.
  gt_outh-name3      = 'Name3'.
  gt_outh-name4      = 'Name4'.
  gt_outh-name_co    = 'C/O Name'.
  gt_outh-street     = 'Street'.
  gt_outh-house_num1 = 'House No'.
  gt_outh-post_code1 = 'Postcode'.
  gt_outh-city1      = 'City'.
  gt_outh-country    = 'Country'.
  gt_outh-tel_number = 'Telephone'.
  gt_outh-fax_number = 'Fax'.
  gt_outh-region     = 'Region'.
  gt_outh-transpzone = 'Transp. Zone'.
  gt_outh-ktokd_we   = 'Acc.Grp.(Ship-to)'.
  APPEND gt_outh.

ENDFORM.                    " BUILD_HEADER

*Text symbol text£º
*E01:No Ship-to selected
*E02:Could not open output file
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_VKORG:D       .
