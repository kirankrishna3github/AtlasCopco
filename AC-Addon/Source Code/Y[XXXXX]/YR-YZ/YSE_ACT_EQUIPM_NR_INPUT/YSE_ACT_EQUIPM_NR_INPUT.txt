*&---------------------------------------------------------------------*
*& Report  YSE_ACT_EQUIPM_NR_INPUT
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Read Equipment Numbers (text file) into UNIX file            *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 25.06.2012
*  Change Request Number :
*  Transport request Nr. : CD1K972321
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                 | CD1K9.....    |             *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_act_equipm_nr_input.

DATA: BEGIN OF gt_in  OCCURS 0,
        equnr      TYPE equnr,
      END OF gt_in.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ifile         TYPE string,
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_ersda(14)     TYPE c,                      "YYYYMMDDHHMMSS
      gv_error         TYPE xfeld,
      gv_jobnr         TYPE tbtcjob-jobcount,
      gv_jobname       TYPE tbtcjob-jobname VALUE 'JOB_EQUIPM_ADRS'.


SELECTION-SCREEN  BEGIN OF BLOCK file  WITH FRAME  TITLE text-s01.
PARAMETERS: p_ifile  LIKE rlgrap-filename
                          OBLIGATORY
                          DEFAULT 'C:\SAP\EQUIPMENTS_NRS.txt'.
SELECTION-SCREEN  END OF BLOCK file.

*----------------------------------------------------------------------*
* On the selection screen                                              *
*----------------------------------------------------------------------*

*------------------  F4-Help for filename -----------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_ifile.

  CALL FUNCTION 'WS_FILENAME_GET'
   EXPORTING
     def_filename           = p_ifile
     def_path               = 'C:\SAP\EQUIPMENTS_NRS'
     mask                   = ',*,*.txt.'
     mode                   = 'O'
     title                  = text-s02
   IMPORTING
     filename               = p_ifile
*     RC                     =
   EXCEPTIONS
     inv_winsys             = 1
     no_batch               = 2
     selection_cancel       = 3
     selection_error        = 4
     OTHERS                 = 5
            .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Timestamp
  CONCATENATE sy-datum sy-uzeit INTO gv_ersda.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Input file
  PERFORM upload_data.
  CHECK NOT gt_in[] IS INITIAL.

* Check anything selected
  IF gt_in[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No equipments numbers input'(e01).
    RETURN.
  ENDIF.

* Save output.
  PERFORM save_output.
  CHECK gv_error IS INITIAL.

* Submit job for processing data
  PERFORM submit_job.

  IF gv_error IS INITIAL.
    WRITE: /01 'Job', gv_jobname, gv_jobnr, 'started'.
  ELSE.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  UPLOAD_DATA
*&---------------------------------------------------------------------*
*       Upload input file
*----------------------------------------------------------------------*
FORM upload_data .

  gv_ifile = p_ifile.

  CALL FUNCTION 'GUI_UPLOAD'
    EXPORTING
      filename                = gv_ifile
      filetype                = 'ASC'
    TABLES
      data_tab                = gt_in
    EXCEPTIONS
      file_open_error         = 1
      file_read_error         = 2
      no_batch                = 3
      gui_refuse_filetransfer = 4
      invalid_type            = 5
      no_authority            = 6
      unknown_error           = 7
      bad_data_format         = 8
      header_not_allowed      = 9
      separator_not_allowed   = 10
      header_too_long         = 11
      unknown_dp_error        = 12
      access_denied           = 13
      dp_out_of_memory        = 14
      disk_full               = 15
      dp_timeout              = 16
      OTHERS                  = 17.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " UPLOAD_DATA

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

  CONCATENATE 'EQUIPMENT' 'NRS' gv_ersda
              INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

  LOOP AT gt_in.
    TRANSFER gt_in TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SUBMIT_JOB
*&---------------------------------------------------------------------*
*       Submit job for processing data
*----------------------------------------------------------------------*
FORM submit_job .

  CALL FUNCTION 'JOB_OPEN'
    EXPORTING
      jobname          = gv_jobname
    IMPORTING
      jobcount         = gv_jobnr
    EXCEPTIONS
      cant_create_job  = 1
      invalid_job_data = 2
      jobname_missing  = 3
      OTHERS           = 4.

  IF sy-subrc = 0.
    SUBMIT yse_act_equipm_address_extr
           WITH p_ersda = gv_ersda
           VIA JOB gv_jobname NUMBER gv_jobnr
           AND RETURN.

    IF sy-subrc = 0.
      CALL FUNCTION 'JOB_CLOSE'
        EXPORTING
          jobcount             = gv_jobnr
          jobname              = gv_jobname
          strtimmed            = 'X'
        EXCEPTIONS
          cant_start_immediate = 1
          invalid_startdate    = 2
          jobname_missing      = 3
          job_close_failed     = 4
          job_nosteps          = 5
          job_notex            = 6
          lock_failed          = 7
          OTHERS               = 8.
      IF sy-subrc <> 0.
        gv_error = 'X'.
      ENDIF.
    ENDIF.
  ELSE.
    gv_error = 'X'.
  ENDIF.


ENDFORM.                    " SUBMIT_JOB

*Text symbol text£º
*E01:No equipments numbers input
*E02:Could not open output file
*S01:File

*S02:Input File
*Selection text£º
*P_IFILE:        Input File
