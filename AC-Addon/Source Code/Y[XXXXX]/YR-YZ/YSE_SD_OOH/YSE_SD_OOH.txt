*----------------------------------------------------------------------*
* Author  : Anda Wu                                                    *
* Created : 12.11.2014                                                 *
* Dev.nbr: CR3329-CS/SDMM-OOH Reporting per Sales Order                *
*----------------------------------------------------------------------*
* This report  is CS/SDMM-OOH Reporting per Sales Order                *
* of China MR CC Project                                               *
************************************************************************
* Mod-xxx | xxxx.xx.xx | xxxx xxxx     | CD1Kxxxxxxxxxx  |   CRXXXX    *
************************************************************************
* Mod-001 | 2015.03.17 | Anda Wu       | CD1K984974      |   CR3329    *
*         Fix some issue found by the user
************************************************************************
REPORT yse_sd_ooh NO STANDARD PAGE HEADING LINE-SIZE 255.

************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES:
  vbap,
  vbak,
  t2504,
  ekpo,
  vbfa.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:

BEGIN OF ty_alv_output,
    vbeln    TYPE char10,       "Sales Document
    posnr    TYPE vbap-posnr,   "Sales Document Item
    auart    TYPE vbak-auart,   "Sales Document Type
    erdat    TYPE vbak-erdat,   "Date on Which Record Was Created(H)
    ernam    TYPE vbak-ernam,   "Name of Person who Created(H)
    erdat_sl TYPE vbap-erdat,   "Date on Which Record Was Created(I)
    ernam_sl TYPE vbap-ernam,   "Name of Person who Created(I)
    vkorg    TYPE vbak-vkorg,   "Sales Organization
    vtweg    TYPE vbak-vtweg,   "Distribution Channel
    matnr    TYPE char18,       "Material Number
    maktx    TYPE makt-maktx,   "Material Description
    stprs    TYPE mbew-stprs,   "Standard price
    netwr    TYPE vbap-netwr,   "Net value of the order item
    werks    TYPE vbap-werks,   "Plant
    lgort    TYPE lgort_d,      "Storage Location
    labst    TYPE labst,        "Valuated Unrestricted-Use Stock
    vkbur    TYPE vkbur,        "Sales Office
    vkgrp    TYPE vkgrp,        "Sales Group
    bezei    TYPE bezei20,      "Description
    edatu    TYPE edatu,        "Schedule line date
    wadat    TYPE wadat,        "Planned GI date
    cmgst    TYPE status_bez,   "Credit block status
    lifsk    TYPE lifsk,        "Delivery block
    vsbed    TYPE vsbed,        "Shipping Conditions
    autlf    TYPE autlf,        "Complete delivery status
    kvgr4    TYPE kvgr4,        "Customer group 4
    zterm    TYPE dzterm,       "Terms of Payment Key
    vtext    TYPE dzterm_bez,   "Description of terms of payment
    alava    TYPE zall_avail,   "ALL Available
    avail    TYPE yavail,       "Available
    pstyv    TYPE pstyv,        "Sales document item category
    dtc_sfs  TYPE yse_dtc_sfs,  "DTC/SFS
    pgc      TYPE z_pgc,        "Product Group Code
    target1  TYPE rkeg_ww002,   "PLC
    bstkd    TYPE bstkd,        "Customer purchase order number
    vbeln_po TYPE vbeln_nach,   "Subsequent PO number
    posnn_po TYPE posnr_nach,   "PO line number
    xblnr    TYPE xblnr_long,   "Status in PO line description
    bmeng    TYPE bmeng,        "Confirmed quantity
    kwmeng   TYPE kwmeng,       "Ordered quantity
    kwmeng_open TYPE vbap-kwmeng, "Open quantity
    vrkme    TYPE vrkme,        "Sales unit
    niqty    TYPE yse_niqty,    "Delivered QTY(Not Issued)
    isqty    TYPE yse_isqty,    "Delivered QTY(Issued)
*******Begin of mod-001 add
    open_qty TYPE kwmeng,       " Open QTY
*******Add of mod-001 add
    ivqty    TYPE yse_ivqty,    "Invoiced QTY
    ihrez    TYPE ihrez,        "Service Order
    quotation TYPE yse_quotation,"Quotation number
    grdate   TYPE zgrdate,      "DTC last GR Date
    kunnr    TYPE kunag,        "Sold-to party
    name1    TYPE name1_gp,     "Sold-to name
  END OF ty_alv_output,

BEGIN OF ty_vbak,
    vbeln    TYPE  vbak-vbeln,  "Sales Document
    auart    TYPE  vbak-auart,  "Sales order type
    erdat    TYPE  vbak-erdat,  "Record Create Date
    ernam    TYPE  vbak-ernam,  "Order created by
    vkorg    TYPE  vbak-vkorg,  "Sales Org.
    vtweg    TYPE  vbak-vtweg,  "Distribution channel
    vkbur    TYPE  vbak-vkbur,  "Sales Office
    vkgrp    TYPE  vbak-vkgrp,  "Sales Group
    lifsk    TYPE  vbak-lifsk,  "Delivery block status
    vsbed    TYPE  vbak-vsbed,  "Shipping condition
    autlf    TYPE  vbak-autlf,  "Complete delivery Flag
    kvgr4    TYPE  vbak-kvgr4,  "CPL or not in customer group 4
    kunnr    TYPE  vbak-kunnr,  "Sold-to party
    posnr    TYPE  vbap-posnr,  "Sales Order line
    erdat_sl TYPE  vbap-erdat,  "SO line created on
    ernam_sl TYPE  vbap-ernam,  "SO created by
    matnr    TYPE  vbap-matnr,  "Material
    netwr    TYPE  vbap-netwr,  "SO line net price
    werks    TYPE  vbap-werks,  "Plant
    lgort    TYPE  vbap-lgort,  "Storage Location
    pstyv    TYPE  vbap-pstyv,  "Item category
    kwmeng   TYPE  vbap-kwmeng, "Ordered quantity
    vrkme    TYPE  vbap-vrkme,  "Sales unit
    bwtar    TYPE  vbap-bwtar,  "Valuation Type
  END OF ty_vbak,

BEGIN OF ty_makt,
    matnr    TYPE makt-matnr,   "Material Number
    spras    TYPE makt-spras,   "Language Key
    maktx    TYPE makt-maktx,   "Material Description
  END OF ty_makt,

BEGIN OF ty_mbew,
    matnr	   TYPE mbew-matnr,   "Material Number
    bwkey	   TYPE mbew-bwkey,   "Valuation Area
    bwtar	   TYPE mbew-bwtar,   "Valuation Type
    stprs    TYPE mbew-stprs,   "Standard price
  END OF ty_mbew,

BEGIN OF ty_mard,
    matnr    TYPE mard-matnr,   "Material Number
    werks    TYPE mard-werks,   "Plant
    lgort    TYPE mard-lgort,   "Storage Locatio
    labst    TYPE mard-labst,   "UU Stock
  END OF ty_mard,

BEGIN OF ty_mvke,
    matnr	   TYPE mvke-matnr,   "Material Number
    vkorg	   TYPE mvke-vkorg,   "Sales Organization
    vtweg    TYPE mvke-vtweg,   "Distribution Channel
    prodh    TYPE mvke-prodh,   "Product hierarchy
  END OF ty_mvke,

BEGIN OF ty_tvgrt,
    vkgrp    TYPE tvgrt-vkgrp,  "Sales Group
    bezei    TYPE tvgrt-bezei,  "Sales Group Name
  END OF ty_tvgrt,

BEGIN OF ty_vbep,
    vbeln    TYPE vbep-vbeln,   "Sales Document
    posnr    TYPE vbep-posnr,   "Sales Document Item
    etenr    TYPE vbep-etenr,   "Delivery Schedule Line Number
    ettyp    TYPE vbep-ettyp,   "Schedule line category
    edatu    TYPE vbep-edatu,   "Requested delivery date
    wadat    TYPE vbep-wadat,   "Planned GI date
    bmeng    TYPE vbep-bmeng,   "Confirmed Quantity
  END OF ty_vbep,

BEGIN OF ty_comp,
    rsnum     TYPE resb-rsnum,  "Number
    rspos     TYPE resb-rspos,  "Item Number
    vbeln_adv TYPE vbap-vbeln,  "Sales Document
    posnr_adv TYPE vbap-posnr,  "Sales Document Item
    sobkz     TYPE resb-sobkz,  "Special Stock Indicator
  END OF ty_comp,

BEGIN OF ty_vbkd,
    vbeln     TYPE vbkd-vbeln,  "Sales Document
    posnr     TYPE vbkd-posnr,  "Sales Document Item
    zterm     TYPE vbkd-zterm,  "Terms of Payment Key
    bstkd     TYPE vbkd-bstkd,  "Customer PO number
    ihrez     TYPE vbkd-ihrez,  "Service Order
  END OF ty_vbkd,

BEGIN OF ty_tvzbt,
    zterm     TYPE tvzbt-zterm, "Terms of Payment Key
    vtext     TYPE tvzbt-vtext, "Description of terms of payment
  END OF ty_tvzbt,

BEGIN OF ty_vbfa,
    vbelv     TYPE vbfa-vbelv,  "Preceding sales doc
    posnv     TYPE vbfa-posnv,  "Preceding item
    vbeln     TYPE vbfa-vbeln,  "Subsequent sale doc
    posnn     TYPE vbfa-posnn,  "Subsequent item
*****BEGIN OF MOD-001 ADD
    vbtyp_n   TYPE vbfa-vbtyp_n,
    fkimg     TYPE vbrp-fkimg,  "Invoiced Quantity
    aubel     type vbrp-aubel,  "SO
    aupos     type vbrp-aupos,  "Item
*****END OF MOD-001 ADD
  END OF ty_vbfa,

BEGIN OF ty_likp,
    vbeln     TYPE lips-vbeln,  "Delivery
    posnr     TYPE lips-posnr,  "Delivery item
    lfimg     TYPE lips-lfimg,  "quantity
    vgbel     TYPE lips-vgbel,  "SO
    vgpos     TYPE lips-vgpos,  "Item
  END OF ty_likp,
****begin of mod-001 del
*BEGIN OF ty_vbrk,
*    vbeln     TYPE vbrp-vbeln,  "Billing Document
*    posnr     TYPE vbrp-posnr,  "Billing item
*    fkimg     TYPE vbrp-fkimg,  "Invoiced Quantity
*    aubel     TYPE vbrp-aubel,  "SO
*    aupos     TYPE vbrp-aupos,  "Item
*  END OF ty_vbrk,
****end of mod-001 del
BEGIN OF ty_vbff,
    vbelv     TYPE vbfa-vbelv,  "Preceding sales doc
    vbeln     TYPE vbfa-vbeln,  "Subsequent sale doc
  END OF ty_vbff,

BEGIN OF ty_kna1,
    kunnr     TYPE kna1-kunnr,  "Customer Number
    name1     TYPE kna1-name1,  "Name 1
  END OF ty_kna1,

BEGIN OF ty_vbuk,
    vbeln     TYPE vbuk-vbeln,  "Sales and Distribution Document Number
    cmgst     TYPE vbuk-cmgst,  "Overall status of credit checks
  END OF ty_vbuk,

BEGIN OF ty_tvbst,
    statu     TYPE tvbst-statu, "Status
    bezei     TYPE tvbst-bezei, "Description
  END OF ty_tvbst,

BEGIN OF ty_vbfa1,
    vbelv     TYPE vbfa-vbelv,
    posnv     TYPE vbfa-posnv,
    vbeln     TYPE vbfa-vbeln,
    posnn     TYPE vbfa-posnn,
  END OF ty_vbfa1,

BEGIN OF ty_ekpo,
    ebeln     TYPE ebeln,
    ebelp     TYPE ebelp,
    vbeln     TYPE vbeln_va,
    posnr     TYPE posnr_va,
  END OF ty_ekpo,

BEGIN OF ty_ekes,
    ebeln     TYPE ekes-ebeln,  "Purchasing Document Number
    ebelp     TYPE ekes-ebelp,  "Item Number of Purchasing Document
    xblnr     TYPE ekes-xblnr,  "Reference Document Number
  END OF ty_ekes,

BEGIN OF ty_ekbe,
    ebeln     TYPE ekbe-ebeln,  "Purchasing Documen
    ebelp     TYPE ekbe-ebelp,  "Item Number
    budat     TYPE ekbe-budat,  "Posting Date
  END OF ty_ekbe,

BEGIN OF ty_save,
    vbeln     TYPE vbeln,          "SO
    all_available TYPE zall_avail, "All Available
  END OF ty_save,

BEGIN OF ty_mara ,
    matnr     TYPE marc-matnr,
  END OF ty_mara,

BEGIN OF ty_marc,
    matnr     TYPE marc-matnr,
    werks     TYPE marc-werks,
  END OF ty_marc,

BEGIN OF ty_lips,
    matnr     TYPE lips-matnr,
    werks     TYPE lips-werks,
    vbeln     TYPE lips-vbeln,
    posnr     TYPE lips-posnr,
    vgbel     TYPE lips-vgbel,
    lfimg     TYPE lips-lfimg,
    wbstk     TYPE vbuk-wbstk,
  END OF ty_lips,

BEGIN OF ty_outdel,
    matnr     TYPE lips-matnr,
    werks     TYPE lips-werks,
    lfimg     TYPE lips-lfimg,
  END OF ty_outdel,

BEGIN OF ty_mard_aggr,
    matnr     TYPE mard-matnr,
    werks     TYPE mard-werks,
    labst     TYPE mard-labst,
  END OF ty_mard_aggr,

BEGIN OF ty_vbap_all,
    vbeln       TYPE vbap-vbeln,
    posnr       TYPE vbap-posnr,
    matnr       TYPE vbap-matnr,
    werks       TYPE vbap-werks,
    lgort       TYPE vbap-lgort,
    lprio       TYPE vbap-lprio,
    kwmeng      TYPE vbap-kwmeng,
    kwmeng_open TYPE vbap-kwmeng,
    edatu       TYPE vbep-edatu,
    available(1) TYPE c,
    END OF ty_vbap_all,

BEGIN OF ty_vbep_all,
    vbeln     TYPE vbep-vbeln,
    posnr     TYPE vbep-posnr,
    etenr     TYPE vbep-etenr,
    edatu     TYPE vbep-edatu,
  END OF ty_vbep_all.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_vbak     TYPE STANDARD TABLE OF ty_vbak,
  gt_makt     TYPE STANDARD TABLE OF ty_makt,
  gt_mbew     TYPE STANDARD TABLE OF ty_mbew,
  gt_mard     TYPE STANDARD TABLE OF ty_mard,
  gt_mvke     TYPE STANDARD TABLE OF ty_mvke,
  gt_tvgrt    TYPE STANDARD TABLE OF ty_tvgrt,
  gt_vbep     TYPE STANDARD TABLE OF ty_vbep,
  gt_vbep_tmp TYPE STANDARD TABLE OF ty_vbep,
  gt_comp     TYPE STANDARD TABLE OF ty_comp,
  gt_vbkd     TYPE STANDARD TABLE OF ty_vbkd,
  gt_tvzbt    TYPE STANDARD TABLE OF ty_tvzbt,
  gt_lips     TYPE STANDARD TABLE OF ty_vbfa,
  gt_vbrp     TYPE STANDARD TABLE OF ty_vbfa,
*** being of mod-001 del
*  gt_vbrk     TYPE STANDARD TABLE OF ty_vbrk,
*** del of mod-001 del
  gt_likp1    TYPE STANDARD TABLE OF ty_likp,
  gt_likp2    TYPE STANDARD TABLE OF ty_likp,
  gt_vbff     TYPE STANDARD TABLE OF ty_vbff,
  gt_kna1     TYPE STANDARD TABLE OF ty_kna1,
  gt_vbuk     TYPE STANDARD TABLE OF ty_vbuk,
  gt_tvbst    TYPE STANDARD TABLE OF ty_tvbst,
  gt_vbfa1    TYPE STANDARD TABLE OF ty_vbfa1,
  gt_ekpo     TYPE STANDARD TABLE OF ty_ekpo,
  gt_ekes     TYPE STANDARD TABLE OF ty_ekes,
  gt_ekbe     TYPE STANDARD TABLE OF ty_ekbe,
  gt_alvdata  TYPE STANDARD TABLE OF ty_alv_output,
  gt_fieldcat TYPE slis_t_fieldcat_alv.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_alvdata  TYPE ty_alv_output,
  gs_disvar   TYPE disvariant,
  gs_layout   TYPE slis_layout_alv.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_col(10)  TYPE n,
  gv_text     TYPE string,
  gv_save     TYPE c,
  gv_callback TYPE slis_formname.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x                TYPE c VALUE 'X',
  gc_type_e           TYPE c VALUE 'E',
  gc_callback_routine TYPE slis_formname VALUE 'USER_COMMAND'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
* Alv variants
SELECTION-SCREEN BEGIN OF BLOCK b3 WITH FRAME TITLE text-t03.

PARAMETERS: p_vari TYPE disvariant-variant.       "Dispaly Variant

SELECTION-SCREEN END OF BLOCK b3.

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-t02 .
SELECT-OPTIONS:
  s_vbeln      FOR vbak-vbeln,            "Sales order number
  s_auart      FOR vbak-auart OBLIGATORY, "Sales order type
  s_vkorg      FOR vbak-vkorg OBLIGATORY, "Sales Organisation
  s_vtweg      FOR vbak-vtweg,            "Distribution Channel
  s_vkbur      FOR vbak-vkbur,            "Sales Office
  s_vkgrp      FOR vbak-vkgrp,            "Sales Group
  s_kunnr      FOR vbak-kunnr,            "Sold-to party
  s_pgc        FOR t2504-ww007,           " PGC
  s_matnr      FOR vbap-matnr,            "Material ID
  s_erdat      FOR vbak-erdat OBLIGATORY. "SO creation date
SELECTION-SCREEN END OF BLOCK b2.

SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.

PARAMETERS: rb1 RADIOBUTTON GROUP g1 DEFAULT 'X',
            rb2 RADIOBUTTON GROUP g1.

SELECTION-SCREEN END OF BLOCK b1.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initilization
  PERFORM frm_init.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN                                                 *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.
* selection screen check
  PERFORM check_parameter.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN ON VALUE-REQUEST                                *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
  PERFORM f4_for_variant.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* progress indicator
  PERFORM process_info.
* get data
  PERFORM get_alv_data.
* alv property set
  PERFORM alv_prop_set.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
* ALV display
  PERFORM alv_display.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do initilization
*----------------------------------------------------------------------*
FORM frm_init .

* initialize the internal table,workareas and variables
  REFRESH:
    gt_vbak,
    gt_makt,
    gt_mbew,
    gt_mard,
    gt_mvke,
    gt_tvgrt,
    gt_vbep,
    gt_comp,
    gt_vbkd,
    gt_tvzbt,
    gt_lips,
    gt_vbrp,
****begin of mod-001 del
*    gt_vbrk,
****end of mod-001 del
    gt_likp1,
    gt_likp2,
    gt_vbff,
    gt_kna1,
    gt_vbuk,
    gt_tvbst,
    gt_vbfa1,
    gt_ekpo,
    gt_ekes,
    gt_ekbe,
    gt_alvdata,
    gt_fieldcat.

  CLEAR:
    gs_alvdata,
    gs_disvar,
    gs_layout,
    gv_col,
    gv_text,
    gv_save,
    gv_callback.

  gv_save = 'A'.
* Set default alv layout
  gs_disvar-report = sy-repid.
  CALL FUNCTION 'REUSE_ALV_VARIANT_DEFAULT_GET'
    EXPORTING
      i_save        = gv_save
    CHANGING
      cs_variant    = gs_disvar
    EXCEPTIONS
      wrong_input   = 1
      not_found     = 2
      program_error = 3
      OTHERS        = 4.
  IF sy-subrc = 0.
    p_vari = gs_disvar-variant.
  ENDIF.

ENDFORM.                    " FRM_INIT

*&---------------------------------------------------------------------*
*&      Form  CHECK_PARAMETER
*&---------------------------------------------------------------------*
*       selection screen check
*----------------------------------------------------------------------*
FORM check_parameter .

  DATA: ls_disvar TYPE disvariant.

  IF p_vari IS NOT INITIAL.
    ls_disvar = gs_disvar.
    ls_disvar-variant = p_vari.
    CALL FUNCTION 'REUSE_ALV_VARIANT_EXISTENCE'
      EXPORTING
        i_save        = gv_save
      CHANGING
        cs_variant    = ls_disvar
      EXCEPTIONS
        wrong_input   = 1
        not_found     = 2
        program_error = 3
        OTHERS        = 4.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ELSE.
      gs_disvar = ls_disvar.
    ENDIF.
  ENDIF.

ENDFORM.                    " CHECK_PARAMETER
*&---------------------------------------------------------------------*
*&      Form  PROCESS_INFO
*&---------------------------------------------------------------------*
*       progress indicator
*----------------------------------------------------------------------*
FORM process_info .

  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    CONCATENATE 'Start report:'(t52) sy-repid
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t53.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 1
        text       = gv_text.
  ENDIF.

ENDFORM.                    " PROCESS_INFO
*&---------------------------------------------------------------------*
*&      Form  GET_ALV_DATA
*&---------------------------------------------------------------------*
*       get data
*----------------------------------------------------------------------*
FORM get_alv_data .

* Gain data from table VBAK - Sales Document: Header Data
* Gain data from table VBAP - Sales Document: Item Data
  PERFORM get_data_vbak.
* Gain data from table MAKT - Material Descriptions
* Gain data from table MBEW - Material Valuation
* Gain data from table MARD - Storage Location Data for Material
  PERFORM get_data_makt.
* Gain data from table TVGRT - Sales Group Name
* Gain data from table VBEP  - Schedule Line Data
* Gain data from table VBKD  - Business Data
  PERFORM get_data_tvgrt.
* Gain data from table VBFA  - Sales Document Flow
* Gain data from table VBUP  - Item Status
* Gain data from table KNA1  - Customer Master
  PERFORM get_data_vbfa.
* Gain data from table VBUP  - Header Status
* Gain data from table TVBST - Processing Status
  PERFORM get_data_vbuk.
* Gain data PO data
  PERFORM get_data_vbeln.
* Sort data
  PERFORM get_data_sort.
* Generate ALV data for output
  PERFORM generate_alv_data.
* Edit available
  PERFORM edit_available_data.
* Edit all available
  PERFORM edit_alv_data.
ENDFORM.                    " GET_ALV_DATA
*&---------------------------------------------------------------------*
*&      Form  ALV_PROP_SET
*&---------------------------------------------------------------------*
*       alv property set
*----------------------------------------------------------------------*
FORM alv_prop_set .

* Display ALV process indicator
  PERFORM alv_progress_indicator.

* FIELDCAT set
  PERFORM alv_fieldcat_set.

* LAYOUT set
  PERFORM alv_layout_set.

* Set Others
  PERFORM alv_others_set.

ENDFORM.                    " ALV_PROP_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_lines(10)  TYPE c.

  IF gt_alvdata IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-t61.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  IF sy-batch IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-t63.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 80
        text       = gv_text.
  ELSE.
    CLEAR: gv_text.
    DESCRIBE TABLE gt_alvdata LINES lv_lines.
    CONCATENATE 'Totally'(t62) lv_lines 'entries are generated'(t63)
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ENDIF.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'FRM_MY_STATUS'
      i_callback_user_command  = gv_callback
      is_layout                = gs_layout
      it_fieldcat              = gt_fieldcat
      i_save                   = gv_save
      is_variant               = gs_disvar
    TABLES
      t_outtab                 = gt_alvdata
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  FRM_MY_STATUS
*&---------------------------------------------------------------------*
*       Status Set
*----------------------------------------------------------------------*
*  --> I_EXTAB    STATUS
*----------------------------------------------------------------------*
FORM frm_my_status USING i_extab TYPE slis_t_extab.

  SET PF-STATUS '9100'.

ENDFORM.        "FRM_MY_STATUS

*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM alv_fieldcat_set .

** Create Fieldcatalogue from internal table

  CLEAR gv_col.

* Field definitions
  PERFORM add_field USING 'VBELN'     7  'SD Doc.'(t48)        'X'.
  PERFORM add_field USING 'POSNR'     4  'Item'(t49)           ' '.
  PERFORM add_field USING 'AUART'     10 'SalesDocTy'(t50)     ' '.
  PERFORM add_field USING 'ERDAT'     13 'Created(H) on'(t04)  ' '.
  PERFORM add_field USING 'ERNAM'     13 'Created(H) by'(t05)  ' '.
  PERFORM add_field USING 'ERDAT_SL'  13 'Created(I) on'(t51)  ' '.
  PERFORM add_field USING 'ERNAM_SL'  13 'Created(I) by'(t06)  ' '.
  PERFORM add_field USING 'VKORG'     10 'Sales Org.'(t07)     ' '.
  PERFORM add_field USING 'VTWEG'     14 'Distr. Channel'(t08) ' '.
  PERFORM add_field USING 'MATNR'     10 'Material'(t09)       ' '.
  PERFORM add_field USING 'MAKTX'     11 'Description'(t10)    ' '.
  PERFORM add_field USING 'STPRS'     14 'Standard price'(t11) ' '.
  PERFORM add_field USING 'NETWR'     9  'Net Price'(t12)      ' '.
  PERFORM add_field USING 'WERKS'     5  'Plant'(t13)          ' '.
  PERFORM add_field USING 'LGORT'     10  'Stor. Loc.'(t14)    ' '.
  PERFORM add_field USING 'LABST'     12 'Unrestricted'(t15)   ' '.
  PERFORM add_field USING 'VKBUR'     12 'Sales Office'(t16)   ' '.
  PERFORM add_field USING 'VKGRP'     9  'Sales Grp'(t17)      ' '.
  PERFORM add_field USING 'BEZEI'     9  'Descript.'(t18)      ' '.
  PERFORM add_field USING 'EDATU'     8  'Req.Dlv.Date'(t19)   ' '.
  PERFORM add_field USING 'WADAT'     7  'GI Date'(t20)        ' '.
  PERFORM add_field USING 'CMGST'     12 'Credit block'(t21)   ' '.
  PERFORM add_field USING 'LIFSK'     14 'Delivery block'(t22) ' '.
  PERFORM add_field USING 'VSBED'     14 'Shipping Cond.'(t23) ' '.
  PERFORM add_field USING 'AUTLF'     13 'Complete dlv.'(t24)  ' '.
  PERFORM add_field USING 'KVGR4'     10 'Cust.grp.4'(t25)     ' '.
  PERFORM add_field USING 'ZTERM'     10 'Payt Terms'(t26)     ' '.
  PERFORM add_field USING 'VTEXT'     14 'Payt Descript.'(t56) ' '.
  PERFORM add_field USING 'ALAVA'     7  'All.Ava'(t27)        ' '.
  PERFORM add_field USING 'AVAIL'     9  'Available'(t28)      ' '.
  PERFORM add_field USING 'PSTYV'     9  'Item cat.'(t29)      ' '.
  PERFORM add_field USING 'DTC_SFS'   7  'DTC/SFS'(t30)        ' '.
  PERFORM add_field USING 'PGC'       3  'PGC'(t31)            ' '.
  PERFORM add_field USING 'TARGET1'   3  'PLC'(t32)            ' '.
  PERFORM add_field USING 'BSTKD'     10 'Pur. order'(t33)     ' '.
  PERFORM add_field USING 'VBELN_PO'  13 'Sub.PO number'(t35)  ' '.
  PERFORM add_field USING 'POSNN_PO'  8  'Sub.Item'(t36)       ' '.
  PERFORM add_field USING 'XBLNR'     17 'Status in PO line'(t37)   ' '.
  PERFORM add_field USING 'BMENG'     10 'ConfirmQty'(t38)     ' '.
  PERFORM add_field USING 'KWMENG'    9  'Order qty'(t39)      ' '.
  PERFORM add_field USING 'VRKME'     10 'Sales unit'(t57)     ' '.
  PERFORM add_field USING 'NIQTY'     8  'DN Quantity'(t40)       ' '.
  PERFORM add_field USING 'ISQTY'     8  'Issued Quantity'(t41)       ' '.
***BEGIN OF MOD-001 ADD
  PERFORM add_field USING 'OPEN_QTY'  9  'Open Quantity'(t81)       ' '.
***END OF MOD-001 ADD
  PERFORM add_field USING 'IVQTY'     12 'Invoiced QTY'(t42)   ' '.
  PERFORM add_field USING 'IHREZ'     13 'Service Order'(t43)  ' '.
  PERFORM add_field USING 'QUOTATION' 7  'Quo.No.'(t44)        ' '.
  PERFORM add_field USING 'GRDATE'    14 'Latest GR Date'(t45) ' '.
  PERFORM add_field USING 'KUNNR'     10 'Sold-to pt'(t46)     ' '.
  PERFORM add_field USING 'NAME1'     12 'Sold-to name'(t47)   ' '.

ENDFORM.                    " ALV_FIELDCAT_SET

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr
                         p_hotsp.
  DATA:
     ls_fieldcat TYPE slis_fieldcat_alv.

  gv_col = gv_col + 1.

  CLEAR ls_fieldcat.
  ls_fieldcat-col_pos    = gv_col.
  ls_fieldcat-fieldname  = p_field.
  ls_fieldcat-outputlen  = p_len.
  ls_fieldcat-seltext_l  = p_descr.
  ls_fieldcat-seltext_m  = p_descr.
  ls_fieldcat-seltext_s  = p_descr.
  ls_fieldcat-fix_column = 'X'.
  ls_fieldcat-emphasize  = 'X'.
  ls_fieldcat-hotspot    = p_hotsp.

* Suppress leading zeros for the material number
  IF p_field = 'MATNR'.
    ls_fieldcat-edit_mask = '==MATN1'.
  ENDIF.

  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   "Auto optimize column width
  gs_layout-detail_popup = gc_x.        "Show detail screen

ENDFORM.                    " ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_VBAK
*&---------------------------------------------------------------------*
*       Gain data from table VBAK - Sales Document: Header Data
*       Gain data from table VBAP - Sales Document: Item Data
*----------------------------------------------------------------------*
FORM get_data_vbak .
  DATA:
    lt_vbak TYPE STANDARD TABLE OF ty_vbak,
    lt_vbfa TYPE STANDARD TABLE OF vbfa,
    ls_vbfa TYPE vbfa,
    lv_lfimg  TYPE lips-lfimg,
    lv_sum_lfimg  TYPE lips-lfimg,
    lv_fkimg  TYPE vbrp-fkimg,
*******begin of mod-001 add
    lv_wbsta  TYPE vbup-wbsta,
*******end of mod-001 add
    lv_sum_fkimg  TYPE vbrp-fkimg,
    ls_vbak TYPE ty_vbak.

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to retrieve table VBAK'(t54).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t54.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 10
        text       = gv_text.
  ENDIF.

  SELECT  a~vbeln    "Sales Document
          a~auart    "Sales order type
          a~erdat    "Record Create Date
          a~ernam    "Order created by
          a~vkorg    "Sales Org.
          a~vtweg    "Distribution channel
          a~vkbur    "Sales Office
          a~vkgrp    "Sales Group
          a~lifsk    "Delivery block status
          a~vsbed    "Shipping condition
          a~autlf    "Complete delivery Flag
          a~kvgr4    "CPL or not in customer group 4
          a~kunnr    "Sold-to party
          b~posnr    "Sales Order line
          b~erdat    "SO line created on
          b~ernam    "SO created by
          b~matnr    "Material
          b~netwr    "SO line net price
          b~werks    "Plant
          b~lgort    "Storage Location
          b~pstyv    "Item category
          b~kwmeng   "Ordered quantity
          b~vrkme    "Sales unit
          b~bwtar    "Valuation Type
    FROM  vbak AS a
    INNER JOIN vbap AS b
      ON a~vbeln = b~vbeln
    INTO TABLE gt_vbak
    WHERE a~vbeln IN s_vbeln  "Sales Document
      AND a~erdat IN s_erdat  "Record Create Date
      AND a~auart IN s_auart  "Sales order type
      AND a~vkorg IN s_vkorg  "Sales Organisation
      AND a~vtweg IN s_vtweg  "Distribution Channel
      AND a~vkgrp IN s_vkgrp  "Sales Group
      AND a~vkbur IN s_vkbur  "Sales Office
      AND a~kunnr IN s_kunnr  "Sold-to party
      AND b~matnr IN s_matnr  "Material
      AND b~abgru = space.    " cwu 20150103 add
* No data exist extract by selection screen
  IF sy-subrc <> 0.
    IF sy-batch IS NOT INITIAL.
      CLEAR: gv_text.
      gv_text ='No data exist in table'(t55).
      MESSAGE i000(yse_sales_log) WITH gv_text.
    ELSE.
      MESSAGE e000(yse_sales_log) WITH text-t55.
    ENDIF.
  ENDIF.

  IF rb1 = gc_x.
    SELECT *
      FROM vbfa
      INTO TABLE lt_vbfa
      FOR ALL ENTRIES IN gt_vbak
      WHERE vbelv = gt_vbak-vbeln
        AND posnv = gt_vbak-posnr
        AND ( vbtyp_n = 'T'
            OR vbtyp_n = 'J'
            OR vbtyp_n = 'M'
            OR vbtyp_n = 'O').
    LOOP AT gt_vbak INTO ls_vbak.
      IF ls_vbak-auart = 'ZKA'
      OR ls_vbak-auart = 'ZKB'
      OR ls_vbak-auart = 'ZKT'
      OR ls_vbak-auart = 'ZO03'.
        IF ls_vbak-auart = 'ZKA'.
          READ  TABLE lt_vbfa TRANSPORTING NO FIELDS
            WITH KEY vbelv = ls_vbak-vbeln
                     posnv = ls_vbak-posnr
                     vbtyp_n = 'T'.
          IF sy-subrc <> 0.
            APPEND ls_vbak TO lt_vbak.
            CONTINUE.
          ELSE.
            CLEAR: lv_sum_lfimg.
            LOOP AT lt_vbfa INTO ls_vbfa WHERE vbelv = ls_vbak-vbeln
                                          AND posnv = ls_vbak-posnr
                                          AND vbtyp_n = 'T'.
              CLEAR: lv_lfimg,lv_wbsta.
              SELECT SINGLE lfimg
                INTO lv_lfimg
                FROM lips
                WHERE vbeln = ls_vbfa-vbeln
                  AND posnr = ls_vbfa-posnn.
**************BEING OF MOD-001 MOD
*              lv_sum_lfimg = lv_sum_lfimg + lv_lfimg.
              SELECT SINGLE wbsta
                INTO lv_wbsta
                FROM vbup
                WHERE vbeln = ls_vbfa-vbeln
                  AND posnr = ls_vbfa-posnn.
              IF lv_wbsta = 'C'.
                lv_sum_lfimg = lv_sum_lfimg + lv_lfimg.
              ENDIF.
**************BEING OF MOD-001 MOD
            ENDLOOP.
            DELETE  lt_vbfa WHERE vbelv = ls_vbak-vbeln
                              AND posnv = ls_vbak-posnr
                              AND vbtyp_n = 'T'.
            IF lv_sum_lfimg < ls_vbak-kwmeng.
              APPEND ls_vbak TO lt_vbak.
              CONTINUE.
            ENDIF.
          ENDIF.
        ELSE.
          READ  TABLE lt_vbfa TRANSPORTING NO FIELDS
            WITH KEY vbelv = ls_vbak-vbeln
                     posnv = ls_vbak-posnr
                     vbtyp_n = 'J'.
          IF sy-subrc <> 0.
            APPEND ls_vbak TO lt_vbak.
            CONTINUE.
          ELSE.
            CLEAR: lv_sum_lfimg.
            LOOP AT lt_vbfa INTO ls_vbfa WHERE vbelv = ls_vbak-vbeln
                                          AND posnv = ls_vbak-posnr
                                          AND vbtyp_n = 'J'.
              CLEAR: lv_lfimg,lv_wbsta.
              SELECT SINGLE lfimg
                INTO lv_lfimg
                FROM lips
                WHERE vbeln = ls_vbfa-vbeln
                  AND posnr = ls_vbfa-posnn.
**************BEING OF MOD-001 MOD
*              lv_sum_lfimg = lv_sum_lfimg + lv_lfimg.
              SELECT SINGLE wbsta
                INTO lv_wbsta
                FROM vbup
                WHERE vbeln = ls_vbfa-vbeln
                  AND posnr = ls_vbfa-posnn.
              IF lv_wbsta = 'C'.
                lv_sum_lfimg = lv_sum_lfimg + lv_lfimg.
              ENDIF.
**************BEING OF MOD-001 MOD
            ENDLOOP.
            DELETE  lt_vbfa WHERE vbelv = ls_vbak-vbeln
                              AND posnv = ls_vbak-posnr
                              AND vbtyp_n = 'J'.
            IF lv_sum_lfimg < ls_vbak-kwmeng.
              APPEND ls_vbak TO lt_vbak.
              CONTINUE.
            ENDIF.
          ENDIF.
        ENDIF.
      ELSE.
        IF   ls_vbak-auart = 'ZRE1'
          OR ls_vbak-auart = 'ZRE2'
********* Begin of mod-001 add          .
          OR ls_vbak-auart = 'ZKR'.
********* End of mod-001 add          .
          READ  TABLE lt_vbfa TRANSPORTING NO FIELDS
            WITH KEY vbelv = ls_vbak-vbeln
                     posnv = ls_vbak-posnr
                     vbtyp_n = 'O'.
          IF sy-subrc <> 0.
            APPEND ls_vbak TO lt_vbak.
            CONTINUE.
          ELSE.
            CLEAR: lv_sum_fkimg.
            LOOP AT lt_vbfa INTO ls_vbfa WHERE vbelv = ls_vbak-vbeln
                                          AND posnv = ls_vbak-posnr
                                          AND vbtyp_n = 'O'.
              CLEAR: lv_fkimg.
              SELECT SINGLE fkimg
                INTO lv_fkimg
                FROM vbrp
                WHERE vbeln = ls_vbfa-vbeln
                  AND posnr = ls_vbfa-posnn.
              lv_sum_fkimg = lv_sum_fkimg + lv_fkimg.
            ENDLOOP.
            DELETE  lt_vbfa WHERE vbelv = ls_vbak-vbeln
                              AND posnv = ls_vbak-posnr
                              AND vbtyp_n = 'O'.
***********Begin of mod-001**mod
*            IF lv_sum_lfimg < ls_vbak-kwmeng.
            IF lv_sum_fkimg < ls_vbak-kwmeng.
***********End of mod-001**mod
              APPEND ls_vbak TO lt_vbak.
              CONTINUE.
            ENDIF.
          ENDIF.
        ELSE.
          READ  TABLE lt_vbfa TRANSPORTING NO FIELDS
            WITH KEY vbelv = ls_vbak-vbeln
                     posnv = ls_vbak-posnr
                     vbtyp_n = 'M'.
          IF sy-subrc <> 0.
            APPEND ls_vbak TO lt_vbak.
            CONTINUE.
          ELSE.
            CLEAR: lv_sum_fkimg.
            LOOP AT lt_vbfa INTO ls_vbfa WHERE vbelv = ls_vbak-vbeln
                                          AND posnv = ls_vbak-posnr
                                          AND vbtyp_n = 'M'.
              CLEAR:lv_fkimg.
              SELECT SINGLE fkimg
                INTO lv_fkimg
                FROM vbrp
                WHERE vbeln = ls_vbfa-vbeln
                  AND posnr = ls_vbfa-posnn.
              lv_sum_fkimg = lv_sum_fkimg + lv_fkimg.
            ENDLOOP.
            DELETE  lt_vbfa WHERE vbelv = ls_vbak-vbeln
                              AND posnv = ls_vbak-posnr
                              AND vbtyp_n = 'M'.
***********Begin of mod-001**mod
*            IF lv_sum_lfimg < ls_vbak-kwmeng.
            IF lv_sum_fkimg < ls_vbak-kwmeng.
***********End of mod-001**mod
              APPEND ls_vbak TO lt_vbak.
              CONTINUE.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDLOOP.

    REFRESH gt_vbak.
    gt_vbak[] = lt_vbak[].

* No data exist extract by selection screen
    IF gt_vbak IS INITIAL.
      IF sy-batch IS NOT INITIAL.
        CLEAR: gv_text.
        gv_text ='No data exist in table'(t55).
        MESSAGE i000(yse_sales_log) WITH gv_text.
      ENDIF.
    ENDIF.

  ENDIF.
ENDFORM.                    " GET_DATA_VBAK

*&---------------------------------------------------------------------*
*&      Form  GET_DATA_MAKT
*&---------------------------------------------------------------------*
* Gain data from table MAKT - Material Descriptions
* Gain data from table MBEW - Material Valuation
* Gain data from table MARD - Storage Location Data for Material
*----------------------------------------------------------------------*
FORM get_data_makt .
  DATA:
    lt_vbak1 TYPE STANDARD TABLE OF ty_vbak,
    lt_vbak2 TYPE STANDARD TABLE OF ty_vbak,
    lt_vbak3 TYPE STANDARD TABLE OF ty_vbak,
    lt_vbak4 TYPE STANDARD TABLE OF ty_vbak.
  FIELD-SYMBOLS: <lfs_vbak> TYPE ty_vbak.

  lt_vbak1[] = gt_vbak[].
  SORT lt_vbak1 BY matnr ASCENDING.
  DELETE ADJACENT DUPLICATES FROM lt_vbak1 COMPARING matnr.

* Gain data from table MAKT - Material Descriptions
  IF lt_vbak1 IS NOT INITIAL.
    SELECT matnr  "Material Number
           spras  "Language Key
           maktx  "Material Description
     FROM  makt
     INTO TABLE gt_makt
     FOR ALL ENTRIES IN lt_vbak1
     WHERE matnr = lt_vbak1-matnr
       AND spras = sy-langu.
  ENDIF.

  lt_vbak2[] = gt_vbak[].
  SORT lt_vbak2 BY matnr ASCENDING
                   werks ASCENDING
                   bwtar ASCENDING.
  DELETE ADJACENT DUPLICATES FROM lt_vbak2 COMPARING matnr  "Material
                                                     werks  "Plant
                                                     bwtar. "Va Type
* Gain data from table MBEW - Material Valuation
  IF lt_vbak2 IS NOT INITIAL.
    SELECT matnr   "Material Number
           bwkey   "Valuation Area
           bwtar   "Valuation Type
           stprs   "Standard price
      FROM mbew
      INTO TABLE gt_mbew
       FOR ALL ENTRIES IN lt_vbak2
      WHERE matnr = lt_vbak2-matnr
        AND bwkey = lt_vbak2-werks
        AND bwtar = lt_vbak2-bwtar.
  ENDIF.

  lt_vbak3[] = gt_vbak[].
  SORT lt_vbak3 BY matnr ASCENDING
                   werks ASCENDING
                   lgort ASCENDING.
  DELETE ADJACENT DUPLICATES FROM lt_vbak3 COMPARING matnr  "Material
                                                     werks  "Plant
                                                     lgort. "Storage
* Gain data from table MARD - Storage Location Data for Material
  IF lt_vbak3 IS NOT INITIAL.
    LOOP AT lt_vbak3 ASSIGNING <lfs_vbak>.
      IF <lfs_vbak>-lgort IS INITIAL.
        <lfs_vbak>-lgort = '1000'.
      ENDIF.
    ENDLOOP.
    SELECT matnr "Material Number
           werks "Plant
           lgort "Storage Location
           labst "UU Stock
      FROM mard
      INTO TABLE gt_mard
      FOR ALL ENTRIES IN lt_vbak3
      WHERE matnr = lt_vbak3-matnr  "Material Number
        AND werks = lt_vbak3-werks  "Plant
        AND lgort = lt_vbak3-lgort. "Storage Location
  ENDIF.

  lt_vbak4[] = gt_vbak[].
  SORT lt_vbak4 BY matnr ASCENDING
                   vkorg ASCENDING
                   vtweg ASCENDING.
  DELETE ADJACENT DUPLICATES FROM lt_vbak4 COMPARING matnr  "Material
                                                     vkorg  "Sales Org
                                                     vtweg. "Dis Chan
* Gain data from table MARD - Storage Location Data for Material
  IF lt_vbak4 IS NOT INITIAL.
    SELECT matnr "Material Number
           vkorg "Sales Organization
           vtweg "Distribution Channel
           prodh "Product hierarchy
      FROM mvke
      INTO TABLE gt_mvke
      FOR ALL ENTRIES IN lt_vbak4
      WHERE matnr = lt_vbak4-matnr  "Material Number
        AND vkorg = lt_vbak4-vkorg  "Sales Organization
        AND vtweg = lt_vbak4-vtweg. "Distribution Channel
  ENDIF.

ENDFORM.                    " GET_DATA_MAKT

*&---------------------------------------------------------------------*
*&      Form  GET_DATA_TVGRT
*&---------------------------------------------------------------------*
*       Gain data from table TVGRT - Sales Group Name
*       Gain data from table TVGRT - Schedule Line Data
*       Gain data from table VBKD  - Business Data
*----------------------------------------------------------------------*
FORM get_data_tvgrt .
  DATA:
    lt_vbak1 TYPE STANDARD TABLE OF ty_vbak,
    lt_vbak2 TYPE STANDARD TABLE OF ty_vbak,
    lt_vbak3 TYPE STANDARD TABLE OF ty_vbak,
    lt_vbep  TYPE STANDARD TABLE OF ty_vbep,
    ls_vbep  TYPE ty_vbep,
    ls_old_vbeln  TYPE vbap-vbeln,
    ls_old_posnr  TYPE vbap-posnr,
    lv_bmeng  TYPE vbep-bmeng,
    lt_vbkd  TYPE STANDARD TABLE OF ty_vbkd.
  FIELD-SYMBOLS: <lfs_vbep> TYPE ty_vbep.

  lt_vbak1[] = gt_vbak[].
  SORT lt_vbak1 BY vkgrp ASCENDING.
  DELETE ADJACENT DUPLICATES FROM lt_vbak1 COMPARING vkgrp.

* Gain data from table TVGRT - Sales Group Name
  IF lt_vbak1 IS NOT INITIAL.
    SELECT vkgrp  "Sales Group
           bezei  "Sales Group Name
     FROM  tvgrt
     INTO TABLE gt_tvgrt
     FOR ALL ENTRIES IN lt_vbak1
     WHERE vkgrp = lt_vbak1-vkgrp
       AND spras = sy-langu.
  ENDIF.

  lt_vbak2[] = gt_vbak[].
  SORT lt_vbak2 BY vbeln ASCENDING
                   posnr ASCENDING.

  DELETE ADJACENT DUPLICATES FROM lt_vbak2 COMPARING vbeln
                                                     posnr.

* Gain data from table VBEP - Schedule Line Data
  IF lt_vbak2 IS NOT INITIAL.
    SELECT vbeln  "Sales Document
           posnr  "Sales Document Item
           etenr  "Delivery Schedule Line Number
           ettyp  "Schedule line category
           edatu  "Requested delivery date
           wadat  "Planned GI date
           bmeng  "Confirmed Quantity
     FROM  vbep
     INTO TABLE lt_vbep
     FOR ALL ENTRIES IN lt_vbak2
     WHERE vbeln = lt_vbak2-vbeln
       AND posnr = lt_vbak2-posnr.
*       AND etenr = '0001'.
    SORT lt_vbep BY vbeln posnr.
    LOOP AT lt_vbep INTO ls_vbep.
*     20150205 CWU ADD
      APPEND ls_vbep TO gt_vbep_tmp.
*     20150205 CWU ADD
      IF    ls_vbep-vbeln = ls_old_vbeln
        AND ls_vbep-posnr = ls_old_posnr.
        READ TABLE gt_vbep ASSIGNING <lfs_vbep>
          WITH KEY vbeln = ls_vbep-vbeln
                   posnr = ls_vbep-posnr.
        <lfs_vbep>-bmeng = <lfs_vbep>-bmeng + ls_vbep-bmeng.
        IF ls_vbep-wadat > <lfs_vbep>-wadat.
          <lfs_vbep>-wadat = ls_vbep-wadat.
        ENDIF.
      ELSE.
        APPEND ls_vbep TO gt_vbep.
      ENDIF.
      ls_old_vbeln = ls_vbep-vbeln.
      ls_old_posnr = ls_vbep-posnr.
    ENDLOOP.

* Gain data DTC or SFS
    SELECT resb~rsnum
           resb~rspos
           vbap~vbeln
           vbap~posnr
           resb~sobkz
      INTO TABLE gt_comp
      FROM vbap JOIN resb
        ON resb~rsnum = vbap~zz_aso_rsnum
       AND resb~rspos = vbap~zz_aso_rspos
       FOR ALL entries IN lt_vbak2
     WHERE vbap~vbeln = lt_vbak2-vbeln
       AND vbap~posnr = lt_vbak2-posnr.

  ENDIF.

  lt_vbak3[] = gt_vbak[].
  SORT lt_vbak3 BY vbeln ASCENDING
                   posnr ASCENDING.

  DELETE ADJACENT DUPLICATES FROM lt_vbak3 COMPARING vbeln
                                                     posnr.

* Gain data from table TVGRT - Sales Group Name
  IF lt_vbak3 IS NOT INITIAL.
    SELECT vbeln  "Sales Document
           posnr  "Sales Document Item
           zterm  "Payment Terms
           bstkd  "Customer PO number
           ihrez  "Service Order
     FROM  vbkd
     INTO TABLE gt_vbkd
     FOR ALL ENTRIES IN lt_vbak3
     WHERE vbeln = lt_vbak3-vbeln
       AND posnr = lt_vbak3-posnr.
  ENDIF.

  lt_vbkd[] = gt_vbkd[].
  SORT lt_vbkd BY zterm ASCENDING.

  DELETE ADJACENT DUPLICATES FROM lt_vbkd COMPARING zterm.

* Gain data from table TVZBT - Payment terms description
  IF lt_vbkd IS NOT INITIAL.
    SELECT zterm  "Terms of Payment Key
           vtext  "Description of terms of payment
     FROM  tvzbt
     INTO TABLE gt_tvzbt
     FOR ALL ENTRIES IN lt_vbkd
     WHERE zterm = lt_vbkd-zterm
       AND spras = sy-langu.
  ENDIF.

ENDFORM.                    " GET_DATA_TVGRT

*&---------------------------------------------------------------------*
*&      Form  GET_DATA_VBFA
*&---------------------------------------------------------------------*
*       Gain data from table VBFA - Sales Document Flow
*       Gain data from table VBUP  - Item Status
*----------------------------------------------------------------------*
FORM get_data_vbfa .
  DATA:
    lt_vbak  TYPE STANDARD TABLE OF ty_vbak,
    lt_vbak1 TYPE STANDARD TABLE OF ty_vbak,
    lt_vbak2 TYPE STANDARD TABLE OF ty_vbak,
    lt_vbrp  TYPE STANDARD TABLE OF ty_vbfa,
    lt_lips  TYPE STANDARD TABLE OF ty_vbfa.

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to retrieve table VBFA'(t58).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t58.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 35
        text       = gv_text.
  ENDIF.

  lt_vbak[] = gt_vbak[].
  SORT lt_vbak BY vbeln ASCENDING
                  posnr ASCENDING.

  DELETE ADJACENT DUPLICATES FROM lt_vbak COMPARING vbeln
                                                    posnr.
  IF lt_vbak IS NOT INITIAL.
* Gain data from table VBFA - Sales Document Flow
    SELECT vbelv       "Preceding sales doc
           posnv       "Preceding item
           vbeln       "Subsequent sale doc
           posnn       "Subsequent item
**********Begin of mod-001 add
           vbtyp_n
**********End of mod-001 add
      FROM vbfa
      INTO TABLE gt_lips
      FOR ALL ENTRIES IN lt_vbak
      WHERE vbelv = lt_vbak-vbeln
        AND posnv = lt_vbak-posnr
        AND ( vbtyp_n = 'J'
              OR  vbtyp_n = 'T'
              OR  vbtyp_n = 'R' ) .
    IF sy-subrc <> 0.
      IF sy-batch IS NOT INITIAL.
        CLEAR: gv_text.
        gv_text ='No data exist in table VBFA'(t59).
        MESSAGE i000(yse_sales_log) WITH gv_text.
      ENDIF.
    ENDIF.

* Gain data from table LIPS - Delivery Document: Item Data
    lt_lips[] = gt_lips[].
    SORT lt_lips BY vbeln ASCENDING
                    posnn ASCENDING.

    DELETE ADJACENT DUPLICATES FROM lt_lips COMPARING vbeln
                                                      posnn.
    IF lt_lips IS NOT INITIAL.
      SELECT vbeln  "Delivery Document
             posnr  "Delivery item
             lfimg  "quantity
             vgbel  "SO
             vgpos  "Item
        INTO TABLE gt_likp1
        FROM lips
        FOR ALL ENTRIES IN lt_lips
        WHERE vbeln = lt_lips-vbeln
          AND posnr = lt_lips-posnn.

      SELECT a~vbeln  "Delivery Document
             a~posnr  "Delivery item
             a~lfimg  "quantity
             a~vgbel  "SO
             a~vgpos  "Item
        INTO TABLE gt_likp2
        FROM lips AS a
       INNER JOIN vbup AS b
          ON a~vbeln = b~vbeln
         AND a~posnr = b~posnr
     FOR ALL ENTRIES IN lt_lips
       WHERE a~vbeln = lt_lips-vbeln
         AND a~posnr = lt_lips-posnn
         AND b~wbsta = 'C'.
    ENDIF.

* Gain data from table VBFA - Sales Document Flow
****begin of mod-001 del
*    SELECT vbelv       "Preceding sales doc
*           posnv       "Preceding item
*           vbeln       "Subsequent sale doc
*           posnn       "Subsequent item
    SELECT vbfa~vbelv       "Preceding sales doc
           vbfa~posnv       "Preceding item
           vbfa~vbeln       "Subsequent sale doc
           vbfa~posnn       "Subsequent item
****end of mod-001 del
**********Begin of mod-001 add
           vbfa~vbtyp_n
           vbrp~fkimg  "Invoiced Quantity
           vbrp~aubel  "SO
           vbrp~aupos  "Item
**********End of mod-001 add
      FROM vbfa
**********Begin of mod-001 add
      INNER JOIN vbrp
      on vbfa~vbeln = vbrp~vbeln
        and vbfa~posnn = vbrp~posnr
**********Begin of mod-001 add
      INTO TABLE gt_vbrp
      FOR ALL ENTRIES IN lt_vbak
      WHERE vbfa~vbelv = lt_vbak-vbeln
        AND vbfa~posnv = lt_vbak-posnr
        AND ( vbtyp_n = 'M'
***********BEGIN OF MOD-001 ADD
            OR vbtyp_n = 'N'
            OR vbtyp_n = 'O'
            OR vbtyp_n = 'P' ).
***********END OF MOD-001 ADD
    IF sy-subrc <> 0.
      IF sy-batch IS NOT INITIAL.
        CLEAR: gv_text.
        gv_text ='No data exist in table VBFA'(t59).
        MESSAGE i000(yse_sales_log) WITH gv_text.
      ENDIF.
    ENDIF.

* Gain data from table VBRP - Billing Document: Item Data
****BEGIN OF MOD-001
*    lt_vbrp[] = gt_vbrp[].
*    SORT lt_vbrp BY vbeln ASCENDING
*                    posnn ASCENDING.
*
*    DELETE ADJACENT DUPLICATES FROM lt_vbrp COMPARING vbeln
*                                                      posnn.

*    IF lt_vbrp IS NOT INITIAL.
*      SELECT vbeln  "Billing Document
*             posnr  "Billing item
*             fkimg  "Invoiced Quantity
*             aubel  "SO
*             aupos  "Item
*        INTO TABLE gt_vbrk
*        FROM vbrp
*        FOR ALL ENTRIES IN lt_vbrp
*        WHERE vbeln = lt_vbrp-vbeln
*          AND posnr = lt_vbrp-posnn.
*    ENDIF.
****END OF MOD-001

  ENDIF.

  lt_vbak1[] = gt_vbak[].
  SORT lt_vbak1 BY vbeln ASCENDING.

  DELETE ADJACENT DUPLICATES FROM lt_vbak1 COMPARING vbeln.

  IF lt_vbak1 IS NOT INITIAL.
* Gain data from table VBFA - Sales Document Flow
    SELECT vbelv       "Preceding sales doc
           vbeln       "Subsequent sale doc
      FROM vbfa
      INTO TABLE gt_vbff
      FOR ALL ENTRIES IN lt_vbak1
      WHERE vbelv   = lt_vbak1-vbeln
        AND vbtyp_n = 'B'.
  ENDIF.

  lt_vbak2[] = gt_vbak[].
  SORT lt_vbak2 BY kunnr ASCENDING.

  DELETE ADJACENT DUPLICATES FROM lt_vbak2 COMPARING kunnr.

  IF lt_vbak2 IS NOT INITIAL.
* Gain data from table KNA1 - Customer Master
    SELECT kunnr       "Customer Number
           name1       "Name 1
      FROM kna1
      INTO TABLE gt_kna1
      FOR ALL ENTRIES IN lt_vbak2
      WHERE kunnr   = lt_vbak2-kunnr.
  ENDIF.

ENDFORM.                    " GET_DATA_VBFA

*&---------------------------------------------------------------------*
*&      Form  GET_DATA_VBUK
*&---------------------------------------------------------------------*
*        Gain data from table VBUP  - Header Status
*        Gain data from table TVBST - Processing Status
*----------------------------------------------------------------------*
FORM get_data_vbuk .

  DATA:
    lt_vbak  TYPE STANDARD TABLE OF ty_vbak,
    lt_vbuk  TYPE STANDARD TABLE OF ty_vbuk.
  lt_vbak[] = gt_vbak[].
  SORT lt_vbak BY vbeln ASCENDING.

  DELETE ADJACENT DUPLICATES FROM lt_vbak COMPARING vbeln.

  IF lt_vbak IS NOT INITIAL.
* Gain data from table VBUP  - Header Status
    SELECT vbeln       "Customer Number
           cmgst       "Overall status of credit checks
      FROM vbuk
      INTO TABLE gt_vbuk
      FOR ALL ENTRIES IN lt_vbak
      WHERE vbeln   = lt_vbak-vbeln.
  ENDIF.

  lt_vbuk[] = gt_vbuk[].
  SORT lt_vbuk BY cmgst ASCENDING.

  DELETE ADJACENT DUPLICATES FROM lt_vbuk COMPARING cmgst.

  IF lt_vbuk IS NOT INITIAL.
* Gain data from table TVBST - Processing Status
    SELECT statu       "Status
           bezei       "Description
      FROM tvbst
      INTO TABLE gt_tvbst
      FOR ALL ENTRIES IN lt_vbuk
      WHERE spras = sy-langu
        AND tbnam = 'VBUK'
        AND fdnam = 'CMGST'
        AND statu = lt_vbuk-cmgst.
  ENDIF.

ENDFORM.                    " GET_DATA_VBUK

*&---------------------------------------------------------------------*
*&      Form  GET_DATA_VBELN
*&---------------------------------------------------------------------*
*       Gain data PO data
*----------------------------------------------------------------------*
FORM get_data_vbeln .

  DATA:
    lt_vbak  TYPE STANDARD TABLE OF ty_vbak,
    lh_vbak  TYPE ty_vbak,
    lh_vbkd  TYPE ty_vbkd,
    lt_ekpo  TYPE STANDARD TABLE OF ty_ekpo,
    ls_vbfa1 TYPE ty_vbfa1,
    ls_ekpo  TYPE ty_ekpo,
    lv_tabix TYPE sy-tabix.

  lt_vbak[] = gt_vbak[].
  SORT lt_vbak BY vbeln ASCENDING
                  posnr ASCENDING.

  DELETE ADJACENT DUPLICATES FROM lt_vbak COMPARING vbeln
                                                    posnr.
  IF lt_vbak IS NOT INITIAL.
* Gain data from table VBFA - Sales Document Flow
    SELECT vbelv       "Preceding sales doc
           posnv       "Preceding item
           vbeln       "Subsequent sale doc
           posnn       "Subsequent item
      FROM vbfa
      INTO TABLE gt_vbfa1
      FOR ALL ENTRIES IN lt_vbak
      WHERE vbelv = lt_vbak-vbeln
        AND posnv = lt_vbak-posnr
        AND vbtyp_n = 'V'
        AND vbtyp_v = 'C'.

    SORT gt_vbfa1 BY vbelv posnv.
    REFRESH: gt_ekpo.
    LOOP AT gt_vbfa1 INTO ls_vbfa1.
      CLEAR lv_tabix.
      lv_tabix = sy-tabix.
      SELECT SINGLE loekz INTO ekpo-loekz
        FROM ekpo WHERE ebeln = ls_vbfa1-vbeln
                    AND ebelp = ls_vbfa1-posnn
                    AND loekz = ' '.

      IF sy-subrc <> 0.
        DELETE gt_vbfa1 INDEX lv_tabix.
      ELSE.
        CLEAR ls_ekpo.
        ls_ekpo-ebeln = ls_vbfa1-vbeln.
        ls_ekpo-ebelp = ls_vbfa1-posnn+1(5).
        ls_ekpo-vbeln = ls_vbfa1-vbelv.
        ls_ekpo-posnr = ls_vbfa1-posnv.
        APPEND ls_ekpo TO gt_ekpo.
      ENDIF.
    ENDLOOP.
  ENDIF.

  lt_ekpo[] = gt_ekpo[].
  SORT lt_ekpo BY ebeln ASCENDING
                  ebelp ASCENDING.

  DELETE ADJACENT DUPLICATES FROM lt_ekpo COMPARING ebeln
                                                    ebelp.
  IF lt_ekpo IS NOT INITIAL.
* Gain data from table EKES - Vendor Confirmations
    SELECT ebeln       "Purchasing Document Number
           ebelp       "Item Number of Purchasing Document
           xblnr       "Reference Document Number
      FROM ekes
      INTO TABLE gt_ekes
      FOR ALL ENTRIES IN lt_ekpo
      WHERE ebeln = lt_ekpo-ebeln
        AND ebelp = lt_ekpo-ebelp
        AND ebtyp = 'AB'.
* Gain data from table EKBE - History per Purchasing Document
    SELECT ebeln
           ebelp
           budat
      FROM ekbe
      INTO TABLE gt_ekbe
       FOR ALL ENTRIES IN lt_ekpo
     WHERE ebeln = lt_ekpo-ebeln
       AND ebelp = lt_ekpo-ebelp
       AND bwart = '101'.
  ENDIF.

ENDFORM.                    " GET_DATA_VBELN

*&---------------------------------------------------------------------*
*&      Form  GET_DATA_SORT
*&---------------------------------------------------------------------*
*       Sort Data
*----------------------------------------------------------------------*
FORM get_data_sort .

  SORT gt_vbak  BY vbeln     ASCENDING
                   posnr     ASCENDING.

  SORT gt_makt  BY matnr     ASCENDING.

  SORT gt_mbew  BY matnr     ASCENDING
                   bwkey     ASCENDING
                   bwtar     ASCENDING.

  SORT gt_mard  BY matnr     ASCENDING
                   werks     ASCENDING
                   lgort     ASCENDING.

  SORT gt_tvgrt BY vkgrp     ASCENDING.

  SORT gt_vbep  BY vbeln     ASCENDING
                   posnr     ASCENDING.

  SORT gt_comp  BY vbeln_adv ASCENDING
                   posnr_adv ASCENDING.

  SORT gt_vbuk  BY vbeln     ASCENDING.

  SORT gt_tvbst BY statu     ASCENDING.

  SORT gt_vbkd  BY vbeln     ASCENDING
                   posnr     ASCENDING.

  SORT gt_ekpo  BY vbeln     ASCENDING
                   posnr     ASCENDING.

  SORT gt_vbfa1 BY vbelv     ASCENDING
                   posnv     ASCENDING.


  SORT gt_ekes  BY ebeln     ASCENDING
                   ebelp     ASCENDING.

  SORT gt_vbff  BY vbelv     ASCENDING.

  SORT gt_kna1  BY kunnr     ASCENDING.

  SORT gt_ekbe  BY ebeln     ASCENDING
                   ebelp     ASCENDING
                   budat     DESCENDING.

  SORT gt_mvke  BY matnr     ASCENDING
                   vkorg     ASCENDING
                   vtweg     ASCENDING.
***BEGIN OF MOD-001 DEL
*  SORT gt_vbrk  BY aubel     ASCENDING
*                   aupos     ASCENDING.
***END OF MOD-001 DEL
  SORT gt_likp1 BY vgbel     ASCENDING
                   vgpos     ASCENDING.

  SORT gt_likp2 BY vgbel     ASCENDING
                   vgpos     ASCENDING.

  SORT gt_tvzbt BY zterm     ASCENDING.

ENDFORM.                    " GET_DATA_SORT

*&---------------------------------------------------------------------*
*&      Form  GENERATE_ALV_DATA
*&---------------------------------------------------------------------*
*       Generate ALV data for output
*----------------------------------------------------------------------*
FORM generate_alv_data .

  DATA:
    ls_vbak    TYPE ty_vbak,
    ls_makt    TYPE ty_makt,
    ls_mbew    TYPE ty_mbew,
    ls_mard    TYPE ty_mard,
    ls_tvgrt   TYPE ty_tvgrt,
    ls_vbep    TYPE ty_vbep,
    ls_vbep_tmp   TYPE ty_vbep,
    ls_comp    TYPE ty_comp,
    ls_vbuk    TYPE ty_vbuk,
    ls_tvbst   TYPE ty_tvbst,
    ls_vbkd    TYPE ty_vbkd,
    ls_tvzbt   TYPE ty_tvzbt,
    ls_ekpo    TYPE ty_ekpo,
    ls_ekes    TYPE ty_ekes,
    ls_vbff    TYPE ty_vbff,
    ls_kna1    TYPE ty_kna1,
    ls_ekbe    TYPE ty_ekbe,
    ls_mvke    TYPE ty_mvke,
****beginf of mod-001 mod
*    ls_vbrk    TYPE ty_vbrk,
    ls_vbrp    TYPE ty_vbfa,
****enf of mod-001 mod
    ls_likp1   TYPE ty_likp,
    ls_likp2   TYPE ty_likp,
    lv_prctr   TYPE yse_prctr_deriv-prctr,
    lv_segment TYPE yse_prctr_bl-segment.

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to generate ALV data'(t64).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t64.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 40
        text       = gv_text.
  ENDIF.

  LOOP AT gt_vbak INTO ls_vbak.
    CLEAR gs_alvdata.
    MOVE-CORRESPONDING  ls_vbak TO gs_alvdata.
*       20150205 CWU ADD
    IF ls_vbak-kwmeng = 0.
      gs_alvdata-netwr = ls_vbak-netwr.
    ELSE.
*       20150205 CWU ADD
      gs_alvdata-netwr = ls_vbak-netwr / ls_vbak-kwmeng.
    ENDIF.

    CLEAR ls_comp.
    READ TABLE gt_comp INTO ls_comp WITH KEY vbeln_adv = ls_vbak-vbeln
                                             posnr_adv = ls_vbak-posnr
                                    BINARY SEARCH.
    CLEAR ls_vbep.
    READ TABLE gt_vbep INTO ls_vbep WITH KEY vbeln = ls_vbak-vbeln
                                             posnr = ls_vbak-posnr
                                    BINARY SEARCH.
    IF sy-subrc = 0.
      gs_alvdata-edatu = ls_vbep-edatu.
**       20150205 CWU MOD
*      IF ls_vbep-wadat IS NOT INITIAL.
*      gs_alvdata-wadat = ls_vbep-wadat.
*      ELSE.
      CLEAR ls_vbep_tmp.
      READ TABLE gt_vbep_tmp INTO ls_vbep_tmp
            WITH KEY vbeln = ls_vbak-vbeln
                     posnr = ls_vbak-posnr
                     etenr = '0002'.
      gs_alvdata-wadat = ls_vbep_tmp-wadat.
******Begin of mod-001 add
      IF ls_vbep_tmp-wadat IS INITIAL.
        READ TABLE gt_vbep_tmp INTO ls_vbep_tmp
              WITH KEY vbeln = ls_vbak-vbeln
                       posnr = ls_vbak-posnr
                       etenr = '0001'.
        gs_alvdata-wadat = ls_vbep_tmp-wadat.
      ENDIF.
******End of mod-001 add
*       20150205 CWU MOD
*      ENDIF.

      IF ls_vbep-ettyp = 'Z2'.
        IF ls_comp-sobkz = 'E'.
          gs_alvdata-dtc_sfs = 'DTC'.
        ELSE.
          gs_alvdata-dtc_sfs = 'SFS'.
        ENDIF.
      ELSEIF  ( ls_vbep-ettyp = 'Z5' OR ls_vbep-ettyp = 'Z9' ).
        gs_alvdata-dtc_sfs = 'DTC'.
      ELSEIF  ( ls_vbep-ettyp = 'CP' OR ls_vbep-ettyp = 'E1' ).
        gs_alvdata-dtc_sfs = 'SFS'.
      ELSE.
        IF ls_vbep-ettyp = 'ZB'.
          gs_alvdata-dtc_sfs = 'DTC'.
        ELSE.
          gs_alvdata-dtc_sfs = 'SFS'.
        ENDIF.
      ENDIF.
      IF gs_alvdata-dtc_sfs = 'SFS'.
        gs_alvdata-bmeng = ls_vbep-bmeng.
      ENDIF.
    ELSE.
      CONTINUE.
    ENDIF.

    CLEAR ls_makt.
    READ TABLE gt_makt INTO ls_makt WITH KEY matnr = ls_vbak-matnr
                                    BINARY SEARCH.
    IF sy-subrc = 0.
      gs_alvdata-maktx = ls_makt-maktx.
    ENDIF.

    CLEAR ls_mbew.
    READ TABLE gt_mbew INTO ls_mbew WITH KEY matnr = ls_vbak-matnr
                                             bwkey = ls_vbak-werks
                                             bwtar = ls_vbak-bwtar
                                    BINARY SEARCH.
    IF sy-subrc = 0.
      gs_alvdata-stprs = ls_mbew-stprs.
    ENDIF.

    IF ls_vbak-lgort IS INITIAL.
      LOOP AT gt_mard INTO ls_mard WHERE matnr = ls_vbak-matnr
                                     AND werks = ls_vbak-werks
                                     AND lgort = '1000'.
        gs_alvdata-labst = gs_alvdata-labst + ls_mard-labst.
      ENDLOOP.
    ELSE.
      CLEAR ls_mard.
      READ TABLE gt_mard INTO ls_mard WITH KEY matnr = ls_vbak-matnr
                                               werks = ls_vbak-werks
                                               lgort = ls_vbak-lgort
                                      BINARY SEARCH.
      IF sy-subrc = 0.
        gs_alvdata-labst = ls_mard-labst.
      ENDIF.
    ENDIF.

    CLEAR ls_tvgrt.
    READ TABLE gt_tvgrt INTO ls_tvgrt WITH KEY vkgrp = ls_vbak-vkgrp
                                      BINARY SEARCH.
    IF sy-subrc = 0.
      gs_alvdata-bezei = ls_tvgrt-bezei.
    ENDIF.

    CLEAR ls_vbuk.
    READ TABLE gt_vbuk INTO ls_vbuk WITH KEY vbeln = ls_vbak-vbeln
                                    BINARY SEARCH.
    IF sy-subrc = 0.
      CLEAR ls_tvbst.
      READ TABLE gt_tvbst INTO ls_tvbst WITH KEY statu = ls_vbuk-cmgst
                                        BINARY SEARCH.
      IF sy-subrc = 0.
        gs_alvdata-cmgst = ls_tvbst-bezei.
      ENDIF.
    ENDIF.

    CLEAR ls_vbkd.
    READ TABLE gt_vbkd INTO ls_vbkd WITH KEY vbeln = ls_vbak-vbeln
                                             posnr = ls_vbak-posnr
                                    BINARY SEARCH.
    IF sy-subrc = 0.
      gs_alvdata-zterm = ls_vbkd-zterm.
      READ TABLE  gt_tvzbt INTO ls_tvzbt WITH KEY zterm = ls_vbkd-zterm
                                         BINARY SEARCH.
      IF sy-subrc = 0.
        gs_alvdata-vtext = ls_tvzbt-vtext.
      ENDIF.

      gs_alvdata-bstkd = ls_vbkd-bstkd.
      IF ls_vbak-auart = 'ZO03'.
        gs_alvdata-ihrez = ls_vbkd-ihrez.
      ENDIF.

    ENDIF.

    CLEAR ls_ekpo.
    READ TABLE gt_ekpo INTO ls_ekpo WITH KEY vbeln = ls_vbak-vbeln
                                             posnr = ls_vbak-posnr
                                    BINARY SEARCH.
    IF sy-subrc = 0.
      gs_alvdata-vbeln_po = ls_ekpo-ebeln.
      gs_alvdata-posnn_po = ls_ekpo-ebelp.

      CLEAR ls_ekes.
      READ TABLE gt_ekes INTO ls_ekes WITH KEY ebeln = ls_ekpo-ebeln
                                               ebelp = ls_ekpo-ebelp
                                      BINARY SEARCH.
      IF sy-subrc = 0.
        gs_alvdata-xblnr = ls_ekes-xblnr.
      ENDIF.

      IF   gs_alvdata-pstyv = 'ZDTC'
        OR gs_alvdata-pstyv = 'ZAS2'
        OR gs_alvdata-pstyv = 'ZKB1'.
        CLEAR ls_ekbe.
        READ TABLE gt_ekbe INTO ls_ekbe WITH KEY ebeln = ls_ekpo-ebeln
                                                 ebelp = ls_ekpo-ebelp
                                        BINARY SEARCH.
        IF sy-subrc = 0.
          gs_alvdata-grdate = ls_ekbe-budat.
        ENDIF.
      ENDIF.
    ENDIF.

    IF ls_vbak-auart = 'ZKB'
    OR ls_vbak-auart = 'ZOR'
    OR ls_vbak-auart = 'ZOR1'.

      CLEAR ls_vbff.
      READ TABLE gt_vbff INTO ls_vbff WITH KEY vbelv = ls_vbak-vbeln
                                      BINARY SEARCH.
      IF sy-subrc = 0.
        gs_alvdata-quotation = ls_vbff-vbeln.
      ENDIF.
    ENDIF.

    CLEAR ls_kna1.
    READ TABLE gt_kna1 INTO ls_kna1 WITH KEY kunnr = ls_vbak-kunnr
                                    BINARY SEARCH.
    IF sy-subrc = 0.
      gs_alvdata-name1 = ls_kna1-name1.
    ENDIF.

    CLEAR ls_mvke.
    READ TABLE gt_mvke INTO ls_mvke WITH KEY matnr = ls_vbak-matnr
                                             vkorg = ls_vbak-vkorg
                                             vtweg = ls_vbak-vtweg
                                    BINARY SEARCH.

    IF sy-subrc = 0.
*   Map PGC
      IF ls_mvke-prodh+4(4) NOT IN s_pgc.
*     Skip the item line
        CONTINUE.
      ENDIF.
      gs_alvdata-pgc = ls_mvke-prodh+4(4).
      CLEAR lv_prctr.
      SELECT SINGLE prctr
           FROM yse_prctr_deriv
           INTO lv_prctr
           WHERE vtweg   = ls_mvke-vtweg
             AND rep_pgc = ls_mvke-prodh+4(4)
             AND rep_gac = ls_mvke-prodh+0(4).
      IF sy-subrc = 0.
        CLEAR lv_segment.
        SELECT SINGLE segment
          FROM yse_prctr_bl
          INTO lv_segment
          WHERE prctr = lv_prctr.
        IF sy-subrc = 0.
          SELECT SINGLE target1
            FROM k9rcd11000010
            INTO gs_alvdata-target1
            WHERE sour1_from = lv_segment.
        ENDIF.
      ENDIF.
    ENDIF.
*********Begin of mod-001 mod
*    LOOP AT gt_vbrk INTO ls_vbrk WHERE aubel = ls_vbak-vbeln
*                                   AND aupos = ls_vbak-posnr.
*      gs_alvdata-ivqty = gs_alvdata-ivqty + ls_vbrk-fkimg.
*    ENDLOOP.
    IF ls_vbak-auart = 'ZKR'.
      LOOP AT gt_vbrp INTO ls_vbrp WHERE aubel = ls_vbak-vbeln
                                     AND aupos = ls_vbak-posnr
                                     AND vbtyp_n = 'O'.
        gs_alvdata-ivqty = gs_alvdata-ivqty + ls_vbrp-fkimg.
      ENDLOOP.
      LOOP AT gt_vbrp INTO ls_vbrp WHERE aubel = ls_vbak-vbeln
                                     AND aupos = ls_vbak-posnr
                                     AND vbtyp_n = 'P'.
        gs_alvdata-ivqty = gs_alvdata-ivqty - ls_vbrp-fkimg.
      ENDLOOP.
    ELSE.
      LOOP AT gt_vbrp INTO ls_vbrp WHERE aubel = ls_vbak-vbeln
                                     AND aupos = ls_vbak-posnr
                                     AND vbtyp_n = 'M'.
        gs_alvdata-ivqty = gs_alvdata-ivqty + ls_vbrp-fkimg.
      ENDLOOP.
      LOOP AT gt_vbrp INTO ls_vbrp WHERE aubel = ls_vbak-vbeln
                                     AND aupos = ls_vbak-posnr
                                     AND vbtyp_n = 'N'.
        gs_alvdata-ivqty = gs_alvdata-ivqty - ls_vbrp-fkimg.
      ENDLOOP.
    ENDIF.
*********End of mod-001    mod

    LOOP AT gt_likp1 INTO ls_likp1 WHERE vgbel = ls_vbak-vbeln
                                     AND vgpos = ls_vbak-posnr.
      gs_alvdata-niqty = gs_alvdata-niqty + ls_likp1-lfimg.
    ENDLOOP.

    LOOP AT gt_likp2 INTO ls_likp2 WHERE vgbel = ls_vbak-vbeln
                                     AND vgpos = ls_vbak-posnr.
      gs_alvdata-isqty = gs_alvdata-isqty + ls_likp2-lfimg.
    ENDLOOP.
******Begin of mod-001 add
    gs_alvdata-open_qty = gs_alvdata-kwmeng - gs_alvdata-isqty.
******End of mod-001 add
    APPEND gs_alvdata TO gt_alvdata.

  ENDLOOP.

ENDFORM.                    " GENERATE_ALV_DATA

*&---------------------------------------------------------------------*
*&      Form  ALV_PROGRESS_INDICATOR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM alv_progress_indicator .

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to set ALV property'(t60).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t60.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 75
        text       = gv_text.
  ENDIF.

ENDFORM.                    " ALV_PROGRESS_INDICATOR

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       Set Others
*----------------------------------------------------------------------*
FORM alv_others_set .

  gv_callback = gc_callback_routine.

ENDFORM.                    " ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*&      Form  F4_FOR_VARIANT
*&---------------------------------------------------------------------*
*       F4 help for display variant
*----------------------------------------------------------------------*
FORM f4_for_variant .

  DATA: ls_disvar TYPE disvariant,
        lv_exit   TYPE c.

  ls_disvar-report = sy-repid.

  CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
    EXPORTING
      is_variant    = ls_disvar
      i_save        = gv_save
    IMPORTING
      e_exit        = lv_exit
      es_variant    = gs_disvar
    EXCEPTIONS
      not_found     = 1
      program_error = 2
      OTHERS        = 3.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    IF lv_exit = space.
      p_vari = gs_disvar-variant.
    ENDIF.
  ENDIF.

ENDFORM.                    " F4_FOR_VARIANT

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       USER_COMMAND
*----------------------------------------------------------------------*
FORM user_command USING u_ucomm     LIKE sy-ucomm           "#EC CALLED
                        p_selfield  TYPE slis_selfield.

  CASE u_ucomm.
    WHEN '&IC1'.        " SAP standard code for double-clicking
      IF p_selfield-fieldname = 'VBELN'.
        IF p_selfield-value IS NOT INITIAL.
          SET PARAMETER ID 'AUN' FIELD p_selfield-value.
          CALL TRANSACTION 'VA03'.
        ENDIF.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.

ENDFORM.                    " USER_COMMAND
*&---------------------------------------------------------------------*
*&      Form  EDIT_ALV_DATA
*&---------------------------------------------------------------------*
*       Edit all available
*----------------------------------------------------------------------*
FORM edit_alv_data .
  DATA:
     lv_avail(1) TYPE c,
     lt_save     TYPE STANDARD TABLE OF ty_save,
     ls_save     TYPE ty_save,
     lv_tabix    TYPE sy-tabix.
  IF s_pgc IS INITIAL AND s_matnr IS INITIAL.

    SORT gt_alvdata BY vbeln ASCENDING
                       posnr ASCENDING.
    REFRESH lt_save.
    LOOP AT gt_alvdata INTO gs_alvdata.

      AT NEW vbeln.
        lv_avail = 'X'.
      ENDAT.

      IF gs_alvdata-avail = 'N' OR gs_alvdata-avail = 'GIT'.
        CLEAR lv_avail.
      ENDIF.

      AT END OF vbeln.

        IF lv_avail = 'X'.
          ls_save-all_available = 'Y'.
        ELSE.
          ls_save-all_available = 'N'.
        ENDIF.
        ls_save-vbeln = gs_alvdata-vbeln.
        APPEND ls_save TO lt_save.
      ENDAT.
    ENDLOOP.
    SORT lt_save BY vbeln ASCENDING.

    LOOP AT gt_alvdata INTO gs_alvdata.
      CLEAR:
         ls_save,
         lv_tabix.
      lv_tabix = sy-tabix.
      READ TABLE lt_save INTO  ls_save WITH KEY vbeln = gs_alvdata-vbeln
                                       BINARY SEARCH.
      IF sy-subrc = 0.
        gs_alvdata-alava = ls_save-all_available.
        MODIFY gt_alvdata FROM gs_alvdata INDEX lv_tabix
                                          TRANSPORTING alava.
      ENDIF.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " EDIT_ALV_DATA
*&---------------------------------------------------------------------*
*&      Form  EDIT_AVAILABLE_DATA
*&---------------------------------------------------------------------*
*       Edit available
*----------------------------------------------------------------------*
FORM edit_available_data .
  DATA:
    lv_kalab     TYPE labst,
    ls_vbfa1     TYPE ty_vbfa1,
    lt_mara      TYPE STANDARD TABLE OF ty_mara,
    ls_mara      TYPE ty_mara,
    lt_marc      TYPE STANDARD TABLE OF ty_marc,
    ls_marc      TYPE ty_marc,
    lt_mard      TYPE STANDARD TABLE OF ty_mard,
    ls_mard      TYPE ty_mard,
    lt_lips      TYPE STANDARD TABLE OF ty_lips,
    ls_lips      TYPE ty_lips,
    lt_outdel    TYPE STANDARD TABLE OF ty_outdel,
    ls_outdel    TYPE ty_outdel,
    lt_mard_aggr TYPE STANDARD TABLE OF ty_mard_aggr,
    ls_mard_aggr TYPE ty_mard_aggr,
    lt_vbap_all  TYPE STANDARD TABLE OF ty_vbap_all,
    ls_vbap_all  TYPE ty_vbap_all,
    lt_vbep_all  TYPE STANDARD TABLE OF ty_vbep_all,
    ls_vbep_all  TYPE ty_vbep_all,
    lv_tabix     TYPE sy-tabix,
    lv_tabix1    TYPE sy-tabix,
    lv_tabix2    TYPE sy-tabix,
    lv_tabix3    TYPE sy-tabix,
    lv_tabix4    TYPE sy-tabix,
    lv_tabix5    TYPE sy-tabix,
    lv_tabix6    TYPE sy-tabix,
    lr_lgort     TYPE RANGE OF mard-lgort,
    lh_lgort     LIKE LINE OF lr_lgort,
    it_ekpo      TYPE STANDARD TABLE OF ty_ekpo,
    it_ekes      TYPE STANDARD TABLE OF ty_ekes.
* First build table of unique materials and plant materials based on
* output table
  LOOP AT gt_alvdata INTO gs_alvdata.
    ls_mara-matnr = gs_alvdata-matnr.
    APPEND ls_mara TO lt_mara.
    ls_marc-matnr = gs_alvdata-matnr.
    ls_marc-werks = gs_alvdata-werks.
    APPEND ls_marc TO lt_marc.
  ENDLOOP.

  SORT lt_mara BY matnr.
  SORT lt_marc BY matnr werks.

  DELETE ADJACENT DUPLICATES FROM lt_marc COMPARING ALL FIELDS.
  DELETE ADJACENT DUPLICATES FROM lt_mara COMPARING matnr.

* If no materials found in the order lines, leave them all to
* "not available"
  CHECK:
    NOT lt_mara[] IS INITIAL,
    NOT lt_marc[] IS INITIAL.

* Select unrestricted stock
* First add storage location 1000 and '    '
  lh_lgort-sign   = 'I'.
  lh_lgort-option = 'EQ'.
  lh_lgort-low    = '1000'.
  APPEND lh_lgort TO lr_lgort.
  lh_lgort-sign   = 'I'.
  lh_lgort-option = 'EQ'.
  lh_lgort-low    = '    '.
  APPEND lh_lgort TO lr_lgort.

* Then get stock
  SELECT matnr
         werks
         lgort
         labst
     INTO TABLE lt_mard
     FROM mard
      FOR ALL ENTRIES IN lt_mara
    WHERE matnr = lt_mara-matnr
      AND lgort IN lr_lgort.


  it_ekpo[] = gt_ekpo[].
  SORT it_ekpo BY ebeln ASCENDING
                  ebelp ASCENDING.

  DELETE ADJACENT DUPLICATES FROM it_ekpo COMPARING ebeln
                                                    ebelp.
  IF it_ekpo IS NOT INITIAL.
* Gain data from table EKES - Vendor Confirmations
    SELECT ebeln       "Purchasing Document Number
           ebelp       "Item Number of Purchasing Document
           xblnr       "Reference Document Number
      FROM ekes
      INTO TABLE it_ekes
      FOR ALL ENTRIES IN it_ekpo
      WHERE ebeln = it_ekpo-ebeln
        AND ebelp = it_ekpo-ebelp
        AND ebtyp = 'LA'.
  ENDIF.

  SORT it_ekes BY ebeln ASCENDING
                  ebelp ASCENDING.

* Aggregate the stock to plant level
  LOOP AT lt_mard INTO ls_mard.
    CLEAR ls_marc.
    READ TABLE lt_marc INTO ls_marc WITH KEY matnr = ls_mard-matnr
                                             werks = ls_mard-werks
                                    BINARY SEARCH.
    IF sy-subrc EQ 0.
      MOVE-CORRESPONDING ls_mard TO ls_mard_aggr.
      APPEND ls_mard_aggr TO lt_mard_aggr.
    ENDIF.
  ENDLOOP.

* If no stock found, leave all items to "not available"
*  CHECK NOT lt_mard_aggr[] IS INITIAL.

* Get all order items with any of the previously selected materials
  SELECT vapma~vbeln
         vapma~posnr
         vapma~matnr
         vapma~werks
         lgort lprio
         kwmeng kwmeng AS kwmeng_open
         INTO TABLE lt_vbap_all
         FROM vapma
         JOIN vbap
           ON vapma~vbeln = vbap~vbeln
          AND vapma~posnr = vbap~posnr
         JOIN vbak
           ON vbap~vbeln = vbak~vbeln
         JOIN vbup
           ON vbap~vbeln = vbup~vbeln
          AND vbap~posnr = vbup~posnr
          FOR ALL entries IN lt_mara
        WHERE vapma~matnr EQ lt_mara-matnr
          AND vbak~vbtyp  = 'C'
          AND vbap~abgru  =  '  '
          AND ( vbup~lfsta = 'A' OR vbup~lfsta = 'B' )
          AND (     vbap~pstyv = 'ZTAN'
                OR vbap~pstyv = 'ZAS1'
                OR vbap~pstyv = 'ZAS2'
                OR vbap~pstyv = 'ZDTC'
                OR vbap~pstyv = 'ZKB1'
                OR vbap~pstyv = 'ZKBN'
                OR vbap~pstyv = 'ZTKT').

  LOOP AT lt_vbap_all INTO ls_vbap_all.
*   Check storage location of the order line
    CLEAR lv_tabix6.
    lv_tabix6 = sy-tabix.
    IF NOT ls_vbap_all-lgort IN lr_lgort.
      DELETE lt_vbap_all INDEX lv_tabix6.
      CONTINUE.
    ENDIF.

*   Check plant of the order line
    READ TABLE lt_marc INTO ls_marc WITH KEY matnr = ls_vbap_all-matnr
                                             werks = ls_vbap_all-werks
                                    BINARY SEARCH.
    IF sy-subrc <> 0.
      DELETE lt_vbap_all INDEX lv_tabix6.
    ENDIF.
  ENDLOOP.

* Get schedule lines for the remaining orders
  IF NOT lt_vbap_all[] IS INITIAL.
    SELECT vbeln posnr etenr edatu
      INTO TABLE lt_vbep_all
      FROM vbep
      FOR ALL ENTRIES IN lt_vbap_all
      WHERE vbeln = lt_vbap_all-vbeln.

    SORT lt_vbep_all BY vbeln posnr etenr.

    DELETE ADJACENT DUPLICATES FROM lt_vbep_all
                               COMPARING vbeln posnr.
  ENDIF.

* Add requested delivery date to LT_VBAP_ALL
  LOOP AT lt_vbap_all INTO ls_vbap_all.
    CLEAR lv_tabix.
    lv_tabix = sy-tabix.
    READ TABLE lt_vbep_all INTO ls_vbep_all WITH KEY
                                            vbeln = ls_vbap_all-vbeln
                                            posnr = ls_vbap_all-posnr
                                            BINARY SEARCH.
    IF sy-subrc = 0.
      ls_vbap_all-edatu = ls_vbep_all-edatu.
      MODIFY lt_vbap_all FROM ls_vbap_all INDEX lv_tabix.
    ELSE.
      DELETE lt_vbap_all INDEX lv_tabix.
    ENDIF.
  ENDLOOP.
  SORT:
    lt_vbap_all BY vbeln posnr.

*Get out bound deliveries from LIPS table for all Materials
  REFRESH: lt_lips.
  SELECT lips~matnr
         lips~werks
         lips~vbeln
         lips~posnr
         lips~vgbel
         lips~lfimg
         vbuk~wbstk
  INTO   TABLE lt_lips
  FROM   lips
  INNER  JOIN vbuk
  ON     lips~vbeln = vbuk~vbeln
  FOR    ALL entries IN lt_marc
  WHERE  lips~matnr = lt_marc-matnr
  AND    lips~werks = lt_marc-werks
  AND    lips~vgtyp = 'C'
  AND    vbuk~wbstk = 'A'.

  REFRESH:lt_outdel.
  LOOP AT lt_lips INTO ls_lips.
    MOVE-CORRESPONDING ls_lips TO ls_outdel.
    APPEND ls_outdel TO lt_outdel.
  ENDLOOP.

*Subtract outbound delivery stoc from total Material Stock
  LOOP AT lt_mard_aggr INTO ls_mard_aggr.
    CLEAR lv_tabix1.
    lv_tabix1 = sy-tabix.
    READ TABLE lt_outdel INTO ls_outdel WITH KEY
                                        matnr = ls_mard_aggr-matnr
                                        werks = ls_mard_aggr-werks.
    IF sy-subrc = 0.
      ls_mard_aggr-labst = ls_mard_aggr-labst - ls_outdel-lfimg.
      MODIFY lt_mard_aggr FROM ls_mard_aggr INDEX lv_tabix1.
    ENDIF.
  ENDLOOP.

*Subtract SO outbound delivery from SO stock,
*if the SO has outbound delivery
  LOOP AT lt_vbap_all INTO ls_vbap_all.
    CLEAR lv_tabix2.
    lv_tabix2 = sy-tabix.
    LOOP AT lt_lips INTO ls_lips WHERE vgbel = ls_vbap_all-vbeln.
      ls_vbap_all-kwmeng_open = ls_vbap_all-kwmeng_open - ls_lips-lfimg.
      MODIFY lt_vbap_all FROM ls_vbap_all INDEX lv_tabix2.
    ENDLOOP.
  ENDLOOP.

* Subtract open order qty's from stock and set avly
* flag appropriately for each order line
  LOOP AT lt_vbap_all INTO ls_vbap_all.
    CLEAR lv_tabix4.
    lv_tabix4 = sy-tabix.
*   If item is fully delivered
    IF ls_vbap_all-kwmeng_open LE 0.
*     It should be marked available
      ls_vbap_all-available = '1'.
    ELSE.
*     Get the stock
      READ TABLE lt_mard_aggr INTO ls_mard_aggr  WITH KEY
                                              matnr = ls_vbap_all-matnr
                                              werks = ls_vbap_all-werks
                                              BINARY SEARCH.
      CLEAR lv_tabix3.
      lv_tabix3 = sy-tabix.
*     When stock found
      IF sy-subrc = 0.
*       If stock is enough for current order item
        IF ls_mard_aggr-labst GE ls_vbap_all-kwmeng_open.
          ls_vbap_all-available = '1'.

*         Subtract order open qty from stock
          ls_mard_aggr-labst = ls_mard_aggr-labst
                             - ls_vbap_all-kwmeng_open.
*        ELSE.
*         Leave available mark empty
        ENDIF.
        MODIFY lt_mard_aggr FROM ls_mard_aggr INDEX lv_tabix3.
*     When stock NOT found
*      ELSE.
*       Leave available mark empty
      ENDIF.
    ENDIF.
    MODIFY lt_vbap_all FROM ls_vbap_all INDEX lv_tabix4.
  ENDLOOP.

  SORT lt_vbap_all BY vbeln posnr.

  LOOP AT gt_alvdata INTO gs_alvdata.
    CLEAR lv_tabix5.
    lv_tabix5 = sy-tabix.
    IF gs_alvdata-dtc_sfs = 'SFS'.
*.... Get the corresponding line in ls_vbap_all
      READ TABLE lt_vbap_all INTO  ls_vbap_all WITH KEY
                                               vbeln = gs_alvdata-vbeln
                                               posnr = gs_alvdata-posnr
                                               BINARY SEARCH.
      IF sy-subrc EQ 0.
        IF ls_vbap_all-available IS INITIAL.
          gs_alvdata-avail = 'N'.
        ELSE.
          gs_alvdata-avail = 'Y'.
        ENDIF.
      ELSE.
        gs_alvdata-avail = 'N'.
      ENDIF.
    ELSE.
      CLEAR: ls_vbap_all.
*.... Get the corresponding line in ls_vbap_all
      READ TABLE lt_vbap_all INTO  ls_vbap_all WITH KEY
                                               vbeln = gs_alvdata-vbeln
                                               posnr = gs_alvdata-posnr
                                               BINARY SEARCH.
*.... Check ordered quantity with sales order stock
      CLEAR lv_kalab.
      SELECT SUM( kalab )
         FROM mska
         INTO lv_kalab
         WHERE vbeln = gs_alvdata-vbeln
           AND posnr = gs_alvdata-posnr.

      IF sy-subrc = 0.
        IF lv_kalab = 0.
          gs_alvdata-avail = 'N'.
        ELSE.
          IF lv_kalab <> ls_vbap_all-kwmeng_open.
*  *........ Check if inbound delivery exists
*            READ TABLE gt_vbfa1 INTO ls_vbfa1 WITH KEY
*                                              vbelv = gs_alvdata-vbeln
*                                              posnv = gs_alvdata-posnr
*                                              BINARY SEARCH.
*            IF sy-subrc = 0.
*              READ TABLE it_ekes WITH KEY ebeln = ls_vbfa1-vbeln
*                                          ebelp = ls_vbfa1-posnn+1(5)
*                                 BINARY SEARCH
*                                 TRANSPORTING NO FIELDS.
*              IF sy-subrc = 0.
*                gs_alvdata-avail = 'GIT'.
*              ELSE.
*                gs_alvdata-avail = 'N'.
*              ENDIF.
*            ELSE.
*              gs_alvdata-avail = 'N'.
*            ENDIF.
            IF lv_kalab < ls_vbap_all-kwmeng_open.
              gs_alvdata-avail = 'P'.
            ENDIF.
          ELSE.
            gs_alvdata-avail = 'Y'.
          ENDIF.
        ENDIF.
**...... Check if inbound delivery exists
*        READ TABLE gt_vbfa1 INTO ls_vbfa1 WITH KEY
*                                          vbelv = gs_alvdata-vbeln
*                                          posnv = gs_alvdata-posnr
*                                          BINARY SEARCH.
*
*        IF sy-subrc = 0.
*          READ TABLE it_ekes WITH KEY ebeln = ls_vbfa1-vbeln
*                                      ebelp = ls_vbfa1-posnn+1(5)
*                             BINARY SEARCH
*                             TRANSPORTING NO FIELDS.
*          IF sy-subrc = 0.
*            gs_alvdata-avail = 'GIT'.
*          ELSE.
*            gs_alvdata-avail = 'N'.
*          ENDIF.
*        ELSE.
*          gs_alvdata-avail = 'N'.
*        ENDIF.
      ENDIF.
    ENDIF.
    MODIFY gt_alvdata FROM gs_alvdata INDEX lv_tabix5
                      TRANSPORTING avail.
  ENDLOOP.

ENDFORM.                    " EDIT_AVAILABLE_DATA

*Text symbol text
*T01:Selection Criteria
*T02:Selection Input
*T03:ALV Layout
*T04:Created(H) on
*T05:Created(H) by
*T06:Created(I) by
*T07:Sales Org.
*T08:Distr. Channel
*T09:Material
*T10:Description
*T11:Standard price
*T12:Net Price
*T13:Plant
*T14:Stor. Loc.
*T15:Unrestricted
*T16:Sales Office
*T17:Sales Grp
*T18:Descript.
*T19:Req.Dlv.Date
*T20:GI Date
*T21:Credit block
*T22:Delivery block
*T23:Shipping Cond.
*T24:Complete dlv.
*T25:Cust.grp.4
*T26:Payt Terms
*T27:All.Ava
*T28:Available
*T29:Item cat.
*T30:DTC/SFS
*T31:PGC
*T32:PLC
*T33:Pur. order
*T35:Sub.PO number
*T36:Sub.Item
*T37:Status in PO line
*T38:ConfirmQty
*T39:Order qty
*T40:DN Quantity
*T41:Issued Quantity
*T42:Invoiced QTY
*T43:Service Order
*T44:Quo.No.
*T45:Latest GR Date
*T46:Sold-to pt
*T47:Sold-to name
*T48:SD Doc.
*T49:Item
*T50:SalesDocTy
*T51:Created(I) on
*T52:Start report:
*T53:Start report: YSE_SD_OOH
*T54:Begin to retrieve table VBAK
*T55:No data exist in table
*T56:Payt Descript.
*T57:Sales unit
*T58:Begin to retrieve table VBFA
*T59:No data exist in table VBFA
*T60:Begin to set ALV property
*T61:No data exist
*T62:Totally
*T63:entries are generated
*T64:Begin to generate ALV data

*T81:Open Quantity
*Selection text
*P_VARI:D       .
*RB1:        Open Sales orders
*RB2:        All orders
*S_AUART:D       .
*S_ERDAT:D       .
*S_KUNNR:D       .
*S_MATNR:D       .
*S_PGC:D       .
*S_VBELN:D       .
*S_VKBUR:D       .
*S_VKGRP:D       .
*S_VKORG:D       .
*S_VTWEG:D       .
