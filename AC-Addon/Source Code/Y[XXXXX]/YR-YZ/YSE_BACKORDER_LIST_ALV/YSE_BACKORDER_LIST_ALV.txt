REPORT
    yse_backorder_list_alv

    NO STANDARD PAGE HEADING.

************************************************************************
*Program ID           : yse_backorder_list_alv                         *
*Program Title        : Back-order List                                *
*Author               : Pierre Melin                                   *
*Date                 : 25/02/2007                                     *
*Change Request Number:                                                *
*Description          : Back-order List : compare scheduled date with  *
*                       promised date, required by purchasing          *
*======================================================================*
*Copied From         : (Cloned Program)                                *
*Title               : (Program Title)                                 *
*Other Related obj   : (Object names)                                  *
*======================================================================*
*Change History Log                                                    *
*----------------------------------------------------------------------*
*Mod. no.|  Date    | Name           | Correction Number  | Chge Ref # *
*----------------------------------------------------------------------*
*MOD-001 |dd/mm/yyyy| xxxxxxxxxxxxxx | XXXXxxxxxx         |   XXXXxxxxx*
*                                                                      *
*Description:                                                          *
*----------------------------------------------------------------------*
*MOD-002 |dd/mm/yyyy| xxxxxxxxxxxxxx | XXXXxxxxxx                      *
*
*---------------------------------------------------------------------*
* MOD-016 |16/04/2007| Pieter Jespers | CD1K913810        | 017       *
* Description: Authorisation check                                    *
* some additional requirements (issue 1988) on 02/05/2007
*----------------------------------------------------------------------*
* MOD-017 |16/04/2009| Satyabrata Basu | CD1K947661        | SM#3833   *
* Description: Exclude deleted PO line Items and blocked line Items    *
* some additional requirements (issue 3833) on 16/04/2009
*----------------------------------------------------------------------*
*----------------------------------------------------------------------*
* MOD-018 |22/04/2009| Satyabrata Basu | CD1K947781        | bugfix    *
* Description: Create a real backorder list with possibility to select *
* only non-delivered Order. The old "show all" feature preserved !     *
*----------------------------------------------------------------------*
*----------------------------------------------------------------------*
*MOD-nnn |dd/mm/yyyy| xxxxxxxxxxxxxx | XXXXxxxxxx                      *
*                                                                      *
*----------------------------------------------------------------------*

*Description:                                                          *
************************************************************************
*= TABLES DEFINITIONS
*=======================================================================

TABLES:
    t001, " Companies
    ekko, " Purchasing Document Header
    ekpo, " Purchasing Document Item
    eket, " Scheduling Agreement Schedule Lines
    ekes, " Vendor Confirmations
    ekbe. " History per Purchasing Document



*= DATAS DEFINITIONS
*=======================================================================

"Internal table for display function definition
"-----------------------------------------------------------------------

DATA:
    BEGIN OF t_main_table OCCURS 0,

                 mandant LIKE t001-mandt,
                 aedat   LIKE ekko-aedat,
                 bsart   LIKE ekko-bsart,
                 werks   LIKE ekpo-werks,
                 lifnr   LIKE ekko-lifnr,
                 ebeln   LIKE ekko-ebeln,
                 ebelp   LIKE ekpo-ebelp,
                 ematn   LIKE ekpo-ematn,
                 txz01   LIKE ekpo-txz01,
                 menge   LIKE ekpo-menge,
                 eindt   LIKE eket-eindt,
                 eindt2  LIKE ekes-eindt,
                 eindt3  LIKE ekes-eindt,
                 eindt4  LIKE ekes-eindt,
                 eindt5  LIKE ekes-eindt,
                 budat   LIKE ekbe-budat,
                 budat2  LIKE ekbe-budat,
                 budat3  LIKE ekbe-budat,
                 elikz   LIKE ekpo-elikz,
                 zzeindt LIKE yse_po_confirm-zzeindt,

END OF   t_main_table,

BEGIN OF i_ekes OCCURS 0,
            ebeln LIKE ekes-ebeln,
            ebelp LIKE ekes-ebelp,
            eindt LIKE ekes-eindt,
END OF i_ekes,
BEGIN OF i_ekbe OCCURS 0,
            ebeln LIKE ekbe-ebeln,
            ebelp LIKE ekbe-ebelp,
            budat LIKE ekbe-budat,
END OF i_ekbe.

DATA: it_po_confirm TYPE STANDARD TABLE OF yse_po_confirm WITH
HEADER LINE.

"Necessary types for ALV display management
"-----------------------------------------------------------------------

TYPE-POOLS:
    " Global type for display ALV use

      slis.

FIELD-SYMBOLS:
    " Structure field description

      <data_alv_grid>.

"Structure definition containing field names of the display table
"-------------------------------------------------------------

DATA:
    BEGIN OF t_field OCCURS 0,

                 posnr LIKE vbap-posnr,
                 lib   LIKE dd03t-ddtext,
                 name  LIKE dd03l-fieldname,

    END OF   t_field.


DATA:
    g_repid                 LIKE sy-repid,

    gt_list_top_of_page     TYPE slis_t_listheader,
    g_status_set            TYPE slis_formname VALUE
    'PF_STATUS_SET',
    g_user_command          TYPE slis_formname VALUE
    'USER_COMMAND',
    g_top_of_page           TYPE slis_formname VALUE 'TOP_OF_PAGE'
    ,
    g_top_of_list           TYPE slis_formname VALUE 'TOP_OF_LIST'
    ,
    g_end_of_list           TYPE slis_formname VALUE 'END_OF_LIST'
    ,

    gs_layout               TYPE slis_layout_alv,

    g_exit_caused_by_caller TYPE slis_exit_by_user,
    gs_exit_caused_by_user  TYPE slis_exit_by_user,
    gt_events               TYPE slis_t_event,
    gs_variant              LIKE disvariant,
    g_save.

DATA: s_elikz like RANGE OF ekpo-elikz INITIAL SIZE 0.  "Mod-018
DATA: l_elikz like line OF s_elikz.  "Mod-018


***Moved after selection screen                       "Mod-018
**= INITIALIZATION
**======================================================================
*
*INITIALIZATION.
*
*tit_002 = 'Delivery Complete Status: Yes / No / All'.      "Mod-018
*
*  PERFORM init_alv_field. " Field names initialization
*
*  g_repid           = sy-repid.
*  gs_variant-report = g_repid.
*  g_save            = 'A'.



*= SELECTION-SCREEN
*=======================================================================

  SELECTION-SCREEN: BEGIN OF BLOCK bl1 WITH FRAME.

  SELECT-OPTIONS : s_aedat FOR ekko-aedat.
  PARAMETERS     : p_ekorg LIKE ekko-ekorg OBLIGATORY.
  SELECT-OPTIONS : s_bsart FOR ekko-bsart.
  SELECT-OPTIONS : s_ematn FOR ekpo-ematn.              "Mod-018
*                   s_elikz FOR ekpo-elikz.             "Mod-018

  SELECTION-SCREEN: END OF BLOCK bl1.

*Begin of Mod-018 - Satya
*Create a new block for Users to choose what delivery status they want
selection-screen begin of block bl2 WITH FRAME TITLE tit_002.
parameters: p_con1 radiobutton group sat1,
            p_con2 radiobutton group sat1,
            p_con3 radiobutton group sat1.
selection-screen end of block bl2.

*= INITIALIZATION
*=======================================================================

INITIALIZATION.

tit_002 = 'Delivery Complete Status: Yes / No / All'.      "Mod-018

  PERFORM init_alv_field. " Field names initialization

  g_repid           = sy-repid.
  gs_variant-report = g_repid.
  g_save            = 'A'.



*End of Mod-018


*****************************
* AT SELECTION SCREEN
****************************
AT SELECTION-SCREEN.

  PERFORM check_authorization.




*= START-OF-SELECTION
*=======================================================================

START-OF-SELECTION.

  PERFORM init_main_table. " Display table initialization



*= END-OF-SELECTION
*=======================================================================

END-OF-SELECTION.

*    IF NOT t_main_table[] IS INITIAL. " Check if table contains data

  PERFORM print_alv_report.

*    ENDIF.



*= TOP-OF-PAGE
*=======================================================================

TOP-OF-PAGE.



*= AT USER-COMMANDE
*=======================================================================

AT USER-COMMAND.



************************************************************************
*
*   INTERNAL FORMS
*
************************************************************************



* ----------------------------------------------------------------------
*   FORM
*   EXTRACT FROM DB
*   Fill internal table with SAP data
* ----------------------------------------------------------------------
FORM init_main_table.

  DATA : w_ebelp LIKE ekpo-ebelp,
         w_cpt   TYPE i.

  REFRESH t_main_table.

* Begin of Mod-018    - Satya
* Initialize Variant
 CLEAR s_elikz.
 CLEAR l_elikz.
 REFRESH s_elikz.

IF p_con1 = 'X'.
     l_elikz-sign = 'I'.
     l_elikz-option = 'EQ'.
     l_elikz-low = ' '.
   Append l_elikz to s_elikz.
ELSEIF p_con2 = 'X'.
     l_elikz-sign = 'I'.
     l_elikz-option = 'EQ'.
     l_elikz-low = 'X'.
   Append l_elikz to s_elikz.
ELSEIF p_con3 = 'X'.
*     s_elikz = '*'.
ENDIF.
* End of Mod-018

* data from ekko - ekpo

  SELECT
    a~ebeln a~lifnr a~aedat a~bsart b~ebelp b~werks
    b~ematn b~txz01 b~menge b~elikz
    INTO (t_main_table-ebeln, t_main_table-lifnr, t_main_table-aedat,
          t_main_table-bsart,
          t_main_table-ebelp, t_main_table-werks, t_main_table-ematn,
          t_main_table-txz01, t_main_table-menge, t_main_table-elikz)
    FROM ekko AS a INNER JOIN ekpo AS b
    ON a~ebeln = b~ebeln
    WHERE a~aedat IN s_aedat
    AND   a~ekorg = p_ekorg
    AND   a~bsart IN s_bsart
    AND   b~ematn IN s_ematn
    AND   b~elikz IN s_elikz
    AND   b~loekz = space.         "+MOD-017 - no blocked/deleted line

* get eket

    SELECT SINGLE eindt INTO t_main_table-eindt
      FROM eket
      WHERE ebeln = t_main_table-ebeln
      AND   ebelp = t_main_table-ebelp
      AND   etenr  = '1'.

* get ekes

    SELECT SINGLE eindt INTO t_main_table-eindt2
      FROM ekes
      WHERE ebeln = t_main_table-ebeln
      AND   ebelp = t_main_table-ebelp
      AND   ebtyp = 'AB'.

    APPEND t_main_table.
    CLEAR : w_ebelp, t_main_table-eindt, t_main_table-eindt2.
  ENDSELECT.

* get 3 dates for promised date (LA) - ekes

  SELECT ebeln ebelp eindt
    INTO TABLE i_ekes
    FROM ekes
*    "UP TO 3 ROWS: in comment air22296
    FOR ALL ENTRIES IN t_main_table
    WHERE ebeln = t_main_table-ebeln
    AND   ebelp = t_main_table-ebelp
    AND   ebtyp = 'LA'.

* fill internal table

  LOOP AT t_main_table.
    w_cpt = 0.
      CLEAR : t_main_table-eindt3, t_main_table-eindt4,
      t_main_table-eindt5.
    LOOP AT i_ekes
      WHERE ebeln = t_main_table-ebeln
      AND   ebelp = t_main_table-ebelp.
      w_cpt = w_cpt + 1.
      CASE w_cpt.
        WHEN 1.
          MOVE i_ekes-eindt TO t_main_table-eindt3.
        WHEN 2.
          MOVE i_ekes-eindt TO t_main_table-eindt4.
        WHEN 3.
          MOVE i_ekes-eindt TO t_main_table-eindt5.
          MODIFY t_main_table FROM t_main_table.
          EXIT.
      ENDCASE.
      MODIFY t_main_table FROM t_main_table.
    ENDLOOP.
  ENDLOOP.

* get 3 dates for goods receipt date

  SELECT  ebeln ebelp budat
    INTO TABLE i_ekbe
    FROM ekbe
*    "UP TO 3 ROWS: in comment air22296
    FOR ALL ENTRIES IN t_main_table
    WHERE ebeln = t_main_table-ebeln
    AND   ebelp = t_main_table-ebelp
    AND   vgabe = '1'.

* fill internal table

  LOOP AT t_main_table.
    w_cpt = 0.
      CLEAR : t_main_table-budat, t_main_table-budat2,
      t_main_table-budat3.
    LOOP AT i_ekbe
      WHERE ebeln = t_main_table-ebeln
      AND   ebelp = t_main_table-ebelp.
      w_cpt = w_cpt + 1.
      CASE w_cpt.
        WHEN 1.
          MOVE i_ekbe-budat TO t_main_table-budat.
        WHEN 2.
          MOVE i_ekbe-budat TO t_main_table-budat2.
        WHEN 3.
          MOVE i_ekbe-budat TO t_main_table-budat3.

          MODIFY t_main_table FROM t_main_table.
          EXIT.
      ENDCASE.
      MODIFY t_main_table FROM t_main_table.
    ENDLOOP.
  ENDLOOP.

*get data from table yse_po_confirm
  IF NOT t_main_table[] IS INITIAL.
    SELECT * FROM yse_po_confirm INTO TABLE it_po_confirm
    FOR ALL ENTRIES IN t_main_table
    WHERE ebeln = t_main_table-ebeln
      AND ebelp = t_main_table-ebelp.
  ENDIF.
  IF sy-subrc = 0.
    LOOP AT  t_main_table.
      READ TABLE it_po_confirm WITH KEY ebeln = t_main_table-ebeln
                                        ebelp = t_main_table-ebelp.
      IF sy-subrc = 0.
        t_main_table-zzeindt = it_po_confirm-zzeindt.
        MODIFY t_main_table.
      ENDIF.
    ENDLOOP.
  ENDIF.


ENDFORM.                                                "init_main_table



* ----------------------------------------------------------------------
*   FORM
*   INIT ALV FIELD
*
*   Assignation des noms de champs dans le tableau en correspondance
*   avec ce qui a ¨¦t¨¦ d¨¦clar¨¦ dans la structure de la table.
*
*   Seuls les champs d¨¦crit dans cette proc¨¦dure seront affich¨¦s.
*
*   Pour ajouter une colonne, appelez la proc¨¦dure insert_alv_field en
*   passant les param¨¨tres NomChamp (variable SAP) et Libell¨¦ Colonne
*
*   Pour modifier l'ordre des colonnes, modifier l'ordre d'appel
*   ¨¤ la proc¨¦dure insert_alv_field.
* ----------------------------------------------------------------------
FORM init_alv_field.

  REFRESH t_field.

  PERFORM insert_alv_field USING  'AEDAT'   text-015.
  PERFORM insert_alv_field USING  'BSART'   text-017.
  PERFORM insert_alv_field USING  'WERKS'   text-001.
  PERFORM insert_alv_field USING  'LIFNR'   text-002.
  PERFORM insert_alv_field USING  'EBELN'   text-003.
  PERFORM insert_alv_field USING  'EBELP'   text-016.
  PERFORM insert_alv_field USING  'EMATN'   text-004.
  PERFORM insert_alv_field USING  'TXZ01'   text-005.
  PERFORM insert_alv_field USING  'MENGE'   text-006.
  PERFORM insert_alv_field USING  'EINDT'   text-007.
  PERFORM insert_alv_field USING  'EINDT2'  text-008.
  PERFORM insert_alv_field USING  'EINDT3'  text-019.
  PERFORM insert_alv_field USING  'EINDT4'  text-020.
  PERFORM insert_alv_field USING  'EINDT5'  text-021.
  PERFORM insert_alv_field USING  'BUDAT'   text-022.
  PERFORM insert_alv_field USING  'BUDAT2'  text-023.
  PERFORM insert_alv_field USING  'BUDAT3'  text-024.
  PERFORM insert_alv_field USING  'ELIKZ'   text-011.
  PERFORM insert_alv_field USING  'ZZEINDT' text-018.

ENDFORM.                                                 "init_alv_field


* ----------------------------------------------------------------------
*   FORM
*   INSERT ALV FIELD
*
*   Sub-procedure of INIT ALV FIELD
* ----------------------------------------------------------------------
FORM insert_alv_field USING value(pi_field)
                            value(pi_libel).

  DATA:         l_pos(4) TYPE n,
                l_nbr    TYPE i.

  DESCRIBE TABLE t_field LINES l_nbr.

  l_pos = l_nbr + 1.

  TRANSLATE pi_field TO UPPER CASE.

  CLEAR t_field.
  CONCATENATE l_pos '00' INTO t_field-posnr.
  CONDENSE t_field-posnr.

  t_field-name  = pi_field.
  t_field-lib   = pi_libel.

  APPEND t_field.

ENDFORM.                                               "insert_alv_field



* ----------------------------------------------------------------------
*   FORM
*   PF STATUS SET
*
*   Assign user menu and icons.
* ----------------------------------------------------------------------
FORM pf_status_set USING pi_extab TYPE slis_t_extab.

  SET PF-STATUS 'USER_STATUS' EXCLUDING pi_extab.

ENDFORM.                                                  "pf_status_set



* ----------------------------------------------------------------------
*   FORM
*   USER COMMAND
*
*   User commands management
*
* ----------------------------------------------------------------------
FORM user_command USING pi_ucomm       LIKE sy-ucomm
                        pi_rs_selfield TYPE slis_selfield.  "#EC CALLED

  CASE pi_ucomm.

    WHEN '&IC1'.
      CASE  pi_rs_selfield-fieldname.
        WHEN 'EBELN'.
          SET PARAMETER ID 'BES' FIELD pi_rs_selfield-value.
          CALL TRANSACTION 'ME23N' AND SKIP FIRST SCREEN.
      ENDCASE.

  ENDCASE.

ENDFORM.                                                   "user_command



* ----------------------------------------------------------------------
*   FORM
*   TOP OF PAGE
*
*   Top of page initialization
* ----------------------------------------------------------------------
FORM top_of_page.                                           "#EC CALLED

  CALL FUNCTION 'REUSE_ALV_COMMENTARY_WRITE'
    EXPORTING
      i_logo             = 'Z_IB_LOGO'
      it_list_commentary = gt_list_top_of_page.

ENDFORM.                                                    "top_of_page



* ----------------------------------------------------------------------
*   FORM
*   BUILD TOP OF PAGE
*
* top of page text initialization
* ----------------------------------------------------------------------
FORM build_top_of_page USING lt_top_of_page TYPE slis_t_listheader.

  DATA: ls_line TYPE slis_listheader,
        l_date(10),
        l_heure(8).

  CLEAR ls_line.

  ls_line-typ  = 'S'.
  ls_line-key  = text-012.
  CONCATENATE sy-sysid '|' sy-mandt '|' sy-repid INTO ls_line-info
  SEPARATED BY space.
  APPEND ls_line TO lt_top_of_page.

  ls_line-typ  = 'S'.
  ls_line-key  = 'Nom Utilsateur'(013).
  ls_line-info = sy-uname.
  APPEND ls_line TO lt_top_of_page.

  ls_line-typ  = 'S'.
  ls_line-key  = 'Date | Heure'(014).
  WRITE sy-datum TO l_date.
  WRITE sy-uzeit TO l_heure.
  CONCATENATE l_date '|' l_heure INTO ls_line-info SEPARATED BY space.
  APPEND ls_line TO lt_top_of_page.

  ls_line-typ  = 'S'.
  ls_line-key  = ' '.
  ls_line-info = ' '.
  APPEND ls_line TO lt_top_of_page.

ENDFORM.                                              "build_top_of_page



* ----------------------------------------------------------------------
*   FORM
*   BUILD ALV LAYOUT
*
*   Layout parameters initialization
* ----------------------------------------------------------------------

FORM build_alv_layout USING pi_layout TYPE slis_layout_alv.

  pi_layout-colwidth_optimize   = 'X'. "Optimise les colonnes
  pi_layout-zebra               = 'X'. "Z¨¨brer les lignes
  pi_layout-key_hotspot         = 'X'. "Hotspot sur cl¨¦
  pi_layout-confirmation_prompt = 'X'. "Confirmation en exit

ENDFORM.                    "build_alv_layout



* ----------------------------------------------------------------------
*   FORM
*   BUILD ALV CATALOG
*
*
* ----------------------------------------------------------------------

FORM build_alv_catalog TABLES t_alv_catalog
                              fieldname_tab STRUCTURE t_field.

  TYPE-POOLS:   slis,
                sydes.

  DATA:         s_fieldcat TYPE slis_fieldcat_alv,
                td         TYPE sydes_desc,
                td_type    TYPE sydes_typeinfo,
                td_name    TYPE sydes_nameinfo,
                idx        TYPE i,
                end_flag,
                component(60).

  REFRESH t_alv_catalog.

* Read component table
  DESCRIBE FIELD <data_alv_grid> INTO td.

* loop on components
  LOOP AT td-types INTO td_type.

    IF td_type-idx_name IS INITIAL.
      CONTINUE.
    ENDIF.

    end_flag = ' '.
    idx = td_type-idx_name.

    CLEAR component.

    WHILE ( end_flag = ' ' ).
      READ TABLE td-names INTO td_name INDEX idx.
      CONCATENATE component td_name-name INTO component.

      IF td_name-continue IS INITIAL.
        end_flag = 'X'.
      ENDIF.

      ADD 1 TO idx.
    ENDWHILE.

    READ TABLE fieldname_tab WITH KEY name = component.
    IF sy-subrc <> 0.
      CONTINUE.
    ENDIF.

    CLEAR s_fieldcat.
    s_fieldcat-fieldname = component.
    s_fieldcat-col_pos   = fieldname_tab-posnr.
    s_fieldcat-seltext_l = fieldname_tab-lib.
    s_fieldcat-ddictxt   = 'L'.

*   Allow lowercase letters for fields
    s_fieldcat-lowercase = 'X'.

*   Set output length (necessary for filter)
    s_fieldcat-outputlen = 128.

*   Define content as key
    IF component EQ 'EBELN'.
      s_fieldcat-key        = 'X'.
      s_fieldcat-key_sel    = 'X'.
      s_fieldcat-fix_column = 'X'.
      s_fieldcat-emphasize  = 'C100'.
    ELSE.
      s_fieldcat-key = ' '.
    ENDIF.

    IF component EQ 'BSART'.
      s_fieldcat-outputlen = 5.
    ENDIF.

    IF component EQ 'ZZEINDT'.
      s_fieldcat-outputlen = 10.
    ENDIF.

*   Field conversion
    IF component EQ 'VBELN'.
      s_fieldcat-edit_mask   = '==ALPHA'.
    ENDIF.

*   Sum only these fields
    IF component EQ 'NTGEW'.
      s_fieldcat-do_sum = 'X'.
    ELSE.
*   Prevent sums on other fields
      IF td_type-type = 'P'.
        s_fieldcat-no_sum = 'X'.
      ENDIF.
    ENDIF.

*   Leading zeros for numc fields
    IF td_type-type = 'N'.
      s_fieldcat-lzero = 'X'.
    ENDIF.

    APPEND s_fieldcat TO t_alv_catalog.

  ENDLOOP.
ENDFORM.                                              "build_alv_catalog



* ----------------------------------------------------------------------
*   FORM
*   PRINT ALV REPORT
*
*   Display report.
* ----------------------------------------------------------------------

FORM print_alv_report.

  DATA:         t_alv_catalog   TYPE slis_t_fieldcat_alv,
                t_fieldsel      TYPE slis_t_fieldcat_alv,
                t_exclude       TYPE slis_t_extab,
                s_layout        TYPE slis_layout_alv,
                s_grid_settings TYPE lvc_s_glay.

  SET SCREEN 0.

  ASSIGN t_main_table TO <data_alv_grid>.

* default display options
  PERFORM build_alv_layout USING s_layout.

* Header display.
  s_grid_settings-top_p_only   = 'X'. "Afficher le header standard

* Build top of page (old print_standard_screen)
  PERFORM build_top_of_page USING gt_list_top_of_page[].

* Build catalog.
  PERFORM build_alv_catalog TABLES t_alv_catalog t_field.

* Display ALV
  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
*      i_interface_check           = ' '
*      i_bypassing_buffer          = ' '
*      i_buffer_active             = ' '
       i_callback_program          = g_repid
*       i_callback_pf_status_set    = g_status_set
       i_callback_user_command     = g_user_command
       i_callback_top_of_page      = g_top_of_page
*      i_callback_html_top_of_page =
*      i_callback_html_end_of_list =
*      i_structure_name            =
*      i_background_id             =
*      i_grid_title                =
       i_grid_settings             = s_grid_settings
       is_layout                   = s_layout
       it_fieldcat                 = t_alv_catalog[]
       it_excluding                = t_exclude[]
*      it_special_groups           =
*      it_sort                     =
*      it_filter                   =
*      is_sel_hide                 =
*      i_default                   =
       i_save                      = g_save
       is_variant                  = gs_variant
*      it_events                   =
*      it_event_exit               =
*      is_print                    =
*      is_reprep_id                =
*      i_screen_start_column       = 0
*      i_screen_start_line         = 0
*      i_screen_end_column         = 0
*      i_screen_end_line           = 0
*      it_alv_graphics             =
*      it_hyperlink                =
*      it_add_fieldcat             =
*      it_except_qinfo             =
*      i_html_height_top           =
*      i_html_height_end           =
    IMPORTING
       e_exit_caused_by_caller     = g_exit_caused_by_caller
       es_exit_caused_by_user      = gs_exit_caused_by_user
    TABLES
       t_outtab                    = t_main_table
    EXCEPTIONS
       program_error = 1
       OTHERS        = 2.

  . "end call function REUSE_ALV_GRID_DISPLAY

* Handle errors
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid
          TYPE sy-msgty
        NUMBER sy-msgno
          WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                                               "print_alv_repor
*&---------------------------------------------------------------------*
*&      Form  Check_Authorization
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM check_authorization .

  AUTHORITY-CHECK OBJECT 'V_KONH_EKO'
           ID 'EKORG' FIELD p_ekorg
           ID 'ACTVT' DUMMY.

  IF sy-subrc = 4.
*   No authorisation to display the data
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '002' WITH p_ekorg.
  ELSEIF sy-subrc <> 0.
*   Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '001'.
  ENDIF.

ENDFORM.                    " Check_Authorization

*Text symbol text£º
*001:Plant
*002:Vendor
*003:Purchasing do id
*004:Material number
*005:Short text
*006:Ordered Quantity
*007:Req DelDate
*008:Delivery Date from confirmation line AB
*009:Delivery Date from inbound delivery LA
*010:Posting Date of GR in Pur Order history
*011:Delivery Complete indicator
*012:System
*013:User name
*014:Date | Time
*015:Document creation date
*016:Item
*017:PO type
*018:1st Confirmation date
*019:DelDate Conf LA 1
*020:DelDate Conf LA 2
*021:DelDate Conf LA 3
*022:Posting date GR1
*023:Posting date GR2

*024:Posting date GR3
*Selection text£º
*P_CON1:        Delivery Incomplete
*P_CON2:        Delivery Completed
*P_CON3:        Show all
*P_EKORG:D       .
*S_AEDAT:D       .
*S_BSART:D       .
*S_EMATN:D       .
