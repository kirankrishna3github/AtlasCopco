*&---------------------------------------------------------------------*
*& Report copy of RSAUDIT_SYSTEM_STATUS
*&
*&---------------------------------------------------------------------*
*& 01.03.2010  D033600  ( Note 1433352 )                          7.00+
*& Show client settings and system change options like transactions
*& SCC4 and SE06 in display mode for system auditors
*&---------------------------------------------------------------------*
REPORT yse_system_settings_overview_2.

INCLUDE <icons>.                                           "#EC INCL_OK

TYPE-POOLS: trnsp, slis.

TYPES:
  tv_flag      TYPE boole_d,
  tv_c1        TYPE c LENGTH 1,
  tv_icon      TYPE icon_text,
  tv_descr_tx  TYPE c LENGTH 70,
  tv_status_tx TYPE c LENGTH 25,

  BEGIN OF ts_lst_crtl,
    cnt  TYPE sy-tabix,
    name TYPE tv_status_tx,
  END OF ts_lst_crtl,
  tt_lst_crtl TYPE STANDARD TABLE OF ts_lst_crtl,

  BEGIN OF ts_control,
    return      TYPE tv_c1,
    system_type TYPE tv_c1,
    global_chg  TYPE tv_c1,
    t000_stat   TYPE tv_c1,
    sy_set_stat TYPE tv_c1,
  END OF ts_control,

  BEGIN OF ts_out0, "namespace,
    mandt        TYPE  mandt,
    mtext        TYPE  mtext_d,
    ort01        TYPE  ort01,
    mwaer        TYPE  mwaer,
    adrnr        TYPE  char10,
    cccategory   TYPE  cccategory,
    tx_category  TYPE  tv_descr_tx,
    cccoractiv   TYPE  cccoractiv,
    tx_coractiv  TYPE  tv_descr_tx,
    ccnocliind   TYPE  ccnocliind,
    tx_nocliind  TYPE  tv_descr_tx,
    cccopylock   TYPE  cccopylock,
    tx_copylock  TYPE  tv_descr_tx,
    ccnocascad   TYPE  ccnocascad,
    ti_nocascad  TYPE  tv_icon,
    ccsoftlock   TYPE  ccsoftlock,
    ti_softlock  TYPE  tv_icon,
    ccorigcont   TYPE  ccorigcont,
    ti_origcont  TYPE  tv_descr_tx,
    ccimaildis   TYPE  ccimaildis,
    tx_imaildis  TYPE  tv_descr_tx,
    cctemplock   TYPE  cctemplock,
    ti_templock  TYPE  tv_icon,
    changeuser   TYPE  as4user,
    changedate   TYPE  as4date,
    logsys       TYPE  logsys,
  END OF ts_out0,
  tt_out0 TYPE STANDARD TABLE OF ts_out0,

  ts_trnsp TYPE trnsp_s_nspinfo,
  tt_trnsp TYPE STANDARD TABLE OF ts_trnsp,

  BEGIN OF ts_out1, "namespace,
    namespace     TYPE trnspace-namespace,
    descriptn     TYPE trnspacett-descriptn,
    owner         TYPE trnspacett-owner,
    prefix        TYPE trnspace-namespace,
    role_tx       TYPE tv_status_tx,
    changeable    TYPE trchange,  "2 values:changeable/not change.
    ext_text      TYPE tv_status_tx,
    license_tx    TYPE tv_status_tx,
    sscrflag      TYPE sscrflag,
    sapflag       TYPE sapstandrd,
    gen_only      TYPE gen_only,
    changeuser    TYPE as4user,
    changedate    TYPE as4date,
  END OF ts_out1,
  tt_out1 TYPE STANDARD TABLE OF ts_out1,

*  ts_out2 TYPE trdlvunit,
  BEGIN OF ts_out2,
    dlvunit     TYPE dlvunit,
    changeable  TYPE dlv_change,
    desc_text   TYPE text80,
    extendable  TYPE dlv_extendable,
    ext_text    TYPE tv_status_tx,
  END OF ts_out2,
  tt_out2 TYPE STANDARD TABLE OF ts_out2,

  ts_ttip TYPE alv_s_qinf,
  tt_ttip TYPE STANDARD TABLE OF ts_ttip.
SET EXTENDED CHECK OFF.

CONSTANTS:
  gc_tcd             TYPE tcode        VALUE 'RSAUDIT_SYSTEM_ENV',
  gc_tcd_x           TYPE tcode        VALUE 'RSAUDIT_SYSTEM_ENV_X',
  gc_changeable      TYPE trchange     VALUE 'X',
  gc_not_changeable  TYPE trchange     VALUE 'N',

  gc_chk_failed      TYPE tv_c1        VALUE 'F', "failed
  gc_chk_all         TYPE tv_c1        VALUE 'A', "all data allowed
*  gc_chk_partly      TYPE tv_c1        VALUE 'R', "restricted

  gc_true            TYPE tv_flag      VALUE 'X',
  gc_false           TYPE tv_flag      VALUE '',

  gc_cclass_dummy    TYPE tddat-cclass VALUE '&NC&',
  gc_lst_stru0       TYPE tabname      VALUE 'T000',
  gc_lst_stru1       TYPE tabname      VALUE 'TRNSPACE',
  gc_lst_stru1_out   TYPE tabname      VALUE 'TRNSP_S_NSPINFO',
  gc_lst_stru2       TYPE tabname      VALUE 'TRDLVUNIT',

  gc_lst_client      TYPE tv_status_tx VALUE 'CLIENT',
  gc_lst_nspace      TYPE tv_status_tx VALUE 'NAMESPACE',
  gc_lst_scomp       TYPE tv_status_tx VALUE 'COMPONENTS',

  gc_icon_lock       TYPE tv_icon      VALUE icon_locked,
  gc_icon_unlock     TYPE tv_icon      VALUE icon_unlocked,
  gc_icon_active     TYPE tv_icon      VALUE icon_checked,
  gc_icon_inactive   TYPE tv_icon      VALUE icon_dummy,
  gc_icon_marked     TYPE tv_icon      VALUE icon_checkbox,
  gc_icon_umarked    TYPE tv_icon      VALUE icon_space,

  gc_tx_len          TYPE sy-tabix     VALUE 70,
  gc_icon_len        TYPE sy-tabix     VALUE 46.

INCLUDE z_rsctsdlvu.  "global constants

DATA:
  gt_out0    TYPE tt_out0,
  gt_out1    TYPE tt_out1,
  gt_out2    TYPE tt_out2,
  gt_lst_cnt TYPE tt_lst_crtl,
  gs_crtl    TYPE ts_control,

  gv_tx_c01   TYPE tv_status_tx,
  gv_tx_c02   TYPE tv_status_tx,
  gv_tx_c03   TYPE tv_status_tx,
  gv_tx_c04   TYPE tv_status_tx,
  gv_tx_c05   TYPE tv_status_tx,
  gv_tx_t01   TYPE sy-title,
  gv_tx_t02   TYPE sy-title,
  gv_tx_t03   TYPE sy-title.

FIELD-SYMBOLS:
  <fs_lst_cnt> TYPE ts_lst_crtl.

SET EXTENDED CHECK ON.
*-----------------------------------------------------------------------
PARAMETERS: p_supp TYPE tv_c1 NO-DISPLAY.
"hidden parameter for calling that report with a more technical
"point of view -> extended list for support reasons

SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS p_mandt AS CHECKBOX DEFAULT 'X'.              "#EC SEL_WRONG
SELECTION-SCREEN COMMENT (50) gv_tx001 FOR FIELD p_mandt.
SELECTION-SCREEN END OF LINE.

SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS p_system AS CHECKBOX DEFAULT 'X'.             "#EC SEL_WRONG
SELECTION-SCREEN COMMENT (50) gv_tx002 FOR FIELD p_system.
SELECTION-SCREEN END OF LINE.

*&----------------------------------------------------------------------
*&    INITIALIZATION
*&----------------------------------------------------------------------
INITIALIZATION.
  PERFORM check_report_environment CHANGING gs_crtl.

*&----------------------------------------------------------------------
*&    AT SELECTION-SCREEN OUTPUT
*&----------------------------------------------------------------------
AT SELECTION-SCREEN OUTPUT.
  PERFORM set_screen_settings USING gs_crtl.

*&----------------------------------------------------------------------
*&    START-OF-SELECTION
*&----------------------------------------------------------------------
START-OF-SELECTION.
* 1st - get report environment and convert screen input
  PERFORM check_selection_environment CHANGING gs_crtl
                                               gt_lst_cnt.
  IF gs_crtl-return IS NOT INITIAL.
*   environment check failed -> leave program
    RETURN.
  ELSE.
*   create list hierarchy like the selection screen input
    LOOP AT gt_lst_cnt ASSIGNING <fs_lst_cnt>.
      CASE <fs_lst_cnt>-name.
        WHEN gc_lst_client.
*         load client list like SCC4
          PERFORM get_client_list    CHANGING gt_out0.
        WHEN gc_lst_nspace.
*         load namespace list like RSWBO004
          PERFORM get_namespace_list CHANGING gt_out1.
        WHEN gc_lst_scomp.
*         load software component list like RSWBO004
          PERFORM get_dlvunit_list   CHANGING gt_out2.
      ENDCASE.
    ENDLOOP.
  ENDIF.

*&----------------------------------------------------------------------
*&   END-OF-SELECTION
*&----------------------------------------------------------------------
END-OF-SELECTION.
* create alv based output
  PERFORM show_alv USING gt_lst_cnt.


  INCLUDE z_rsaudit_system_status_alv.


*&---------------------------------------------------------------------*
*&      Form  CHECK_REPORT_ENVIRONMENT
*&---------------------------------------------------------------------*
*  - initial static authority checks
*----------------------------------------------------------------------*
FORM check_report_environment CHANGING ps_crtl TYPE ts_control.

  CASE sy-tcode.
    WHEN gc_tcd OR  gc_tcd_x.
      PERFORM auth_check_for_t000   CHANGING ps_crtl-t000_stat.
      PERFORM auth_check_for_sy_set CHANGING ps_crtl-sy_set_stat.

    WHEN OTHERS.
*     if the report was called directly or via another generic tool
*     check the start authorization for the audit transaction
      CALL FUNCTION 'AUTHORITY_CHECK_TCODE'
        EXPORTING
          tcode  = gc_tcd
        EXCEPTIONS
          ok     = 0
          not_ok = 1
          OTHERS = 2.
      IF sy-subrc <> 0.
        MESSAGE e172(00) WITH gc_tcd.
      ELSE.
        PERFORM auth_check_for_t000   CHANGING ps_crtl-t000_stat.
        PERFORM auth_check_for_sy_set CHANGING ps_crtl-sy_set_stat.
      ENDIF.
  ENDCASE.

  SET EXTENDED CHECK OFF.
* to avoid useless SLIN messages regarding text language differences
  gv_tx001 = 'System client list'(001).
  gv_tx002 = 'System change status'(002).

  gv_tx_c01 = 'Modifiable'(c01).
  gv_tx_c02 = 'Restricted Modifiability'(c02).
  gv_tx_c03 = 'Not Modifiable'(c03).
  gv_tx_c04 = 'Not Modifiable; Enhanceable Only'(c04).
  gv_tx_c05 = 'Not Modifiable; Not Enhanceable'(c05).

  gv_tx_t01 = 'AIS - Current system and client settings'(t01).
  gv_tx_t02 = 'AIS - Current client settings'(t02).
  gv_tx_t03 = 'AIS - Current system settings'(t03).
  SET EXTENDED CHECK ON.

ENDFORM.                    " CHECK_REPORT_ENVIRONMENT

*&---------------------------------------------------------------------*
*&      Form  SET_SCREEN_SETTINGS
*&---------------------------------------------------------------------*
FORM set_screen_settings  USING  ps_crtl TYPE ts_control.
* selection screen control
  IF p_supp = gc_true.
*   if extended mode is active, we do not need the selection screen
    LOOP AT SCREEN.
      screen-input = 0.
      screen-invisible = 1.
      MODIFY SCREEN.
    ENDLOOP.

  ELSE.
*   control the slection screen fields refering to the existing
*   authorizations
    LOOP AT SCREEN.
      CASE screen-name.
        WHEN 'P_MANDT'.
          IF ps_crtl-t000_stat = gc_chk_failed.
            screen-input = 0.
            CLEAR p_mandt.
          ELSE.
            screen-input = 1.
          ENDIF.
          MODIFY SCREEN.

        WHEN 'P_SYSTEM'.
          IF ps_crtl-sy_set_stat = gc_chk_failed.
            screen-input = 0.
            CLEAR p_system.
          ELSE.
            screen-input = 1.
          ENDIF.
          MODIFY SCREEN.
      ENDCASE.
    ENDLOOP.
  ENDIF.
ENDFORM.                    " SET_SCREEN_SETTINGS


*&---------------------------------------------------------------------*
*&      Form  AUTH_CHECK_FOR_T000
*&---------------------------------------------------------------------*
FORM auth_check_for_t000 CHANGING pv_status TYPE tv_c1.

  DATA:
    lv_cclass TYPE tddat-cclass.
*-----------------------------------------------------------------------
* get table class assignment for table T000 (could be changed be
* the customer)
  SELECT SINGLE cclass FROM tddat INTO lv_cclass
     WHERE tabname EQ gc_lst_stru0.
  IF sy-subrc <> 0 OR  lv_cclass = space.
    lv_cclass = gc_cclass_dummy.
  ENDIF.
* check against table display authorization for generic tools
  AUTHORITY-CHECK OBJECT 'S_TABU_DIS'
           ID 'DICBERCLS' FIELD lv_cclass
           ID 'ACTVT'     FIELD '03'.
  IF sy-subrc <> 0.
    pv_status = gc_chk_failed.  "no authorization for that feature
  ELSE.
    pv_status = gc_chk_all.     "like all clients
  ENDIF.

ENDFORM.                    " AUTH_CHECK_FOR_T000

*&---------------------------------------------------------------------*
*&      Form  AUTH_CHECK_FOR_SY_SET
*&---------------------------------------------------------------------*
FORM auth_check_for_sy_set  CHANGING pv_status TYPE tv_c1.

  DATA:
    lv_cclass TYPE tddat-cclass.
*-----------------------------------------------------------------------
* get table class assignment for table TRNSPACE (leading tabele) for NS
* (could be changed be the customer)
  SELECT SINGLE cclass FROM tddat INTO lv_cclass
     WHERE tabname EQ gc_lst_stru1.
  IF sy-subrc <> 0 OR  lv_cclass = space.
    lv_cclass = gc_cclass_dummy.
  ENDIF.
* check against table display authorization for generic tools
  AUTHORITY-CHECK OBJECT 'S_TABU_DIS'
           ID 'DICBERCLS' FIELD lv_cclass
           ID 'ACTVT'     FIELD '03'.
  IF sy-subrc <> 0.
    pv_status = gc_chk_failed.
    "failure, no authorization for that feature
  ELSE.
    pv_status = gc_chk_all .  "authorization okay
  ENDIF.


ENDFORM.                    " AUTH_CHECK_FOR_SY_SET

*&---------------------------------------------------------------------*
*&      Form  CHECK_SELECTION_ENVIRONMENT
*&---------------------------------------------------------------------*
*  - system environment
*  - check input from selection screen
*----------------------------------------------------------------------*
FORM check_selection_environment CHANGING ps_crtl TYPE ts_control
                                          pt_list TYPE tt_lst_crtl.
  DATA:
    ls_list   TYPE ts_lst_crtl,
    ls_tadir  TYPE tadir.
*-----------------------------------------------------------------------
  CLEAR pt_list.
  IF p_mandt IS NOT INITIAL.
    ADD  1 TO ls_list-cnt.
    ls_list-name = gc_lst_client.
    APPEND ls_list TO pt_list.
  ENDIF.

  IF p_system IS NOT INITIAL.
    ADD  1 TO ls_list-cnt.
    ls_list-name = gc_lst_nspace.
    APPEND ls_list TO pt_list.

    ADD  1 TO ls_list-cnt.
    ls_list-name = gc_lst_scomp.
    APPEND ls_list TO pt_list.
  ENDIF.

* get global system settings
  SELECT SINGLE * FROM tadir INTO ls_tadir
                             WHERE pgmid    = 'HEAD'
                               AND object   = 'SYST'
                               AND obj_name = space.
  IF sy-subrc <> 0.
    ps_crtl-global_chg = 'F'.  "failure, system not configured
    MESSAGE e732(tk).
  ELSE.
    CASE ls_tadir-edtflag.
      WHEN 'N'.    "not changable
        ps_crtl-global_chg = gc_not_changeable.
      WHEN 'P'.    "only intern: Hot Package system
        "behavior like RSWBO004 -> changeable
        ps_crtl-global_chg = gc_changeable.
      WHEN OTHERS.
        ps_crtl-global_chg = gc_changeable.
    ENDCASE.

  ENDIF.

ENDFORM.                    " CHECK_SELECTION_ENVIRONMENT

*&---------------------------------------------------------------------*
*&      Form  GET_NAMESPACE_LIST
*&---------------------------------------------------------------------*
FORM get_namespace_list CHANGING pt_out TYPE tt_out1.

  DATA:
    ls_out        TYPE ts_out1,
    ls_loadinfo   TYPE trnsp_namespace,
    lt_trnsp      TYPE tt_trnsp.
  FIELD-SYMBOLS:
    <fs_trnsp>    TYPE ts_trnsp.
*-----------------------------------------------------------------------
* 1st - get the whole list of namespaces
  CALL FUNCTION 'TR_VALID_NAMESPACE_LIST'
    EXPORTING
      iv_edit_only                   = ''
      iv_producer_only               = ''
      iv_licensed_only               = ''
      iv_nongenerated_only           = ' '
      iv_sapstandard_only            = ' '
      iv_show_also_nonprefix_namespc = 'X'
      iv_display_as_f4_list          = ' '
    TABLES
      et_namespace                   = lt_trnsp
    EXCEPTIONS
      OTHERS                         = 0.

* 2nd - complete the namespace attributes
  LOOP AT  lt_trnsp ASSIGNING <fs_trnsp>.
    MOVE-CORRESPONDING <fs_trnsp> TO ls_out.

    CALL FUNCTION 'TRINT_READ_NAMESPACE'
      EXPORTING
        iv_namespace           = ls_out-namespace
        iv_check_license       = 'X'
      IMPORTING
        es_namespace_info      = ls_loadinfo
      EXCEPTIONS
        namespace_not_existing = 1
        OTHERS                 = 2.
    IF sy-subrc = 0.
*     transfer editflag into output table
      IF ls_loadinfo-editflag <> space.
        ls_out-changeable = gc_extendable.
        ls_out-ext_text   =  gv_tx_c01.
      ELSE.
        ls_out-changeable = gc_no_changeability.
        ls_out-ext_text   =  gv_tx_c03.
      ENDIF.

      IF p_supp = gc_true.
*       transfer namespace role into output table
        IF <fs_trnsp>-role = 'P'.
          ls_out-role_tx = 'Producer'(010).              "#EC TEXT_DIFF
        ELSE.
          ls_out-role_tx = 'Recipient'(011).             "#EC TEXT_DIFF
        ENDIF.
*       transfer license status into output table
        IF ls_loadinfo-license_ok <> space.
          ls_out-license_tx   =  'valid license'(015).   "#EC TEXT_DIFF
        ELSE.
          ls_out-license_tx   =  'invalid license'(016). "#EC TEXT_DIFF
        ENDIF.
      ENDIF.
*     transfer further flags for extended technical view
      ls_out-sscrflag = ls_loadinfo-sscrflag.
      ls_out-sapflag  = ls_loadinfo-sapflag.
      ls_out-gen_only = ls_loadinfo-gen_only.
*     get timestamp information about the last change
      PERFORM get_trns_timestamp CHANGING ls_out.

    ENDIF.

    IF  <fs_trnsp>-namespace(2) <> '/0'.
      ls_out-prefix = <fs_trnsp>-namespace.
    ENDIF.

    APPEND ls_out TO pt_out.
    CLEAR ls_out.
  ENDLOOP.

ENDFORM.                    " GET_NAMESPACE_LIST

*&---------------------------------------------------------------------*
*&      Form  GET_TRNS_TIMESTAMP
*&---------------------------------------------------------------------*
FORM get_trns_timestamp  CHANGING ps_out TYPE ts_out1.
  DATA:
    ls_trnspacet TYPE trnspacet.
*-----------------------------------------------------------------------
  SELECT SINGLE * FROM trnspacet INTO ls_trnspacet
    WHERE namespace = ps_out-namespace.
  IF sy-subrc = 0.
    ps_out-changeuser = ls_trnspacet-changeuser.
    ps_out-changedate = ls_trnspacet-changedate.
  ENDIF.

ENDFORM.                    " GET_TRNS_TIMESTAMP

*&---------------------------------------------------------------------*
*&      Form  GET_DLVUNIT_LIST
*&---------------------------------------------------------------------*
* get list of software components and their status
* regarding their local changeability
*-----------------------------------------------------------------------
FORM get_dlvunit_list  CHANGING pt_out TYPE tt_out2.
  DATA:
    ls_out     TYPE ts_out2,
    lt_dlvunit TYPE STANDARD TABLE OF trdlvunit.
  FIELD-SYMBOLS:
    <fs_dlvunit> TYPE trdlvunit.
*-----------------------------------------------------------------------
* 1st get list from database
  CALL FUNCTION 'TR_GET_DLVUNITS'
    EXPORTING
      iv_with_changeability = 'X'
      iv_with_description   = 'X'
    IMPORTING
      et_dlvunits           = lt_dlvunit[].

* merge changeability and extendability
  LOOP AT lt_dlvunit ASSIGNING <fs_dlvunit>.

    IF    <fs_dlvunit>-changeable EQ gc_no_changeability
      AND <fs_dlvunit>-extendable EQ gc_extendable.
      <fs_dlvunit>-changeable = gc_only_extendable.
    ENDIF.
    MOVE-CORRESPONDING <fs_dlvunit> TO ls_out.
* transfer the retrieved technical status into
* readable texts
    CASE ls_out-changeable.
      WHEN gc_full_changeability.
        ls_out-ext_text = gv_tx_c01.
      WHEN gc_reduced_changeability.
        ls_out-ext_text = gv_tx_c02.
      WHEN gc_only_extendable.
        ls_out-ext_text = gv_tx_c04.
      WHEN gc_no_changeability.
        ls_out-ext_text = gv_tx_c03.
      WHEN OTHERS.
    ENDCASE.
    APPEND ls_out TO pt_out.
  ENDLOOP.

ENDFORM.                    " GET_DLVUNIT_LIST



*&---------------------------------------------------------------------*
*&      Form  GET_CLIENT_LIST
*&---------------------------------------------------------------------*
FORM get_client_list  CHANGING pt_out TYPE tt_out0.
  DATA:
    ls_out       TYPE ts_out0,
    lv_tx_return TYPE string,
    lt_t000      TYPE STANDARD TABLE OF t000.
  FIELD-SYMBOLS:
    <fs_t000> TYPE t000.
*-----------------------------------------------------------------------
  SELECT * FROM (gc_lst_stru0) INTO TABLE lt_t000.

  LOOP AT lt_t000 ASSIGNING <fs_t000>.
*    MOVE-CORRESPONDING <fs_t000> TO ls_out.
    MOVE <fs_t000>-MANDT  TO ls_out-MANDT.
    MOVE <fs_t000>-MTEXT  TO ls_out-MTEXT.
    MOVE <fs_t000>-ADRNR  TO ls_out-ADRNR.
    MOVE <fs_t000>-CCCATEGORY  TO ls_out-CCCATEGORY.
    MOVE <fs_t000>-CCCORACTIV  TO ls_out-CCCORACTIV.
    MOVE <fs_t000>-CCNOCLIIND  TO ls_out-CCNOCLIIND.
    MOVE <fs_t000>-CCCOPYLOCK  TO ls_out-CCCOPYLOCK.
*    MOVE <fs_t000>-CCNOCASCAD  TO ls_out-CCNOCASCAD.
    MOVE <fs_t000>-CCSOFTLOCK  TO ls_out-CCSOFTLOCK.
    MOVE <fs_t000>-CCORIGCONT  TO ls_out-CCORIGCONT.
    MOVE <fs_t000>-CCTEMPLOCK  TO ls_out-CCTEMPLOCK.
    MOVE <fs_t000>-CHANGEUSER  TO ls_out-CHANGEUSER.
    MOVE <fs_t000>-CHANGEDATE  TO ls_out-CHANGEDATE.
    MOVE <fs_t000>-LOGSYS  TO ls_out-LOGSYS.

*   get text for: Client Control: category of the client
    CALL FUNCTION 'DDUT_TEXT_FOR_VALUE'
      EXPORTING
        tabname   = gc_lst_stru0
        fieldname = 'CCCATEGORY'
        value     = ls_out-cccategory
*        langu     = sy-langu           "parameter not yet supported
      IMPORTING
        text      = lv_tx_return
      EXCEPTIONS
        OTHERS    = 3.
    IF sy-subrc = 0.
      ls_out-tx_category = lv_tx_return.
    ELSE.
      ls_out-tx_category = ls_out-cccategory.
    ENDIF.

*   get text for: Customizing settings
    CALL FUNCTION 'DDUT_TEXT_FOR_VALUE'
      EXPORTING
        tabname   = gc_lst_stru0
        fieldname = 'CCCORACTIV'
        value     = ls_out-cccoractiv
*        langu     = sy-langu           "parameter not yet supported
      IMPORTING
        text      = lv_tx_return
      EXCEPTIONS
        OTHERS    = 3.
    IF sy-subrc = 0.
      ls_out-tx_coractiv = lv_tx_return.
    ELSE.
      ls_out-tx_coractiv = ls_out-cccoractiv.
    ENDIF.

*   get text for: client independent settings
    CALL FUNCTION 'DDUT_TEXT_FOR_VALUE'
      EXPORTING
        tabname   = gc_lst_stru0
        fieldname = 'CCNOCLIIND'
        value     = ls_out-ccnocliind
*        langu     = sy-langu           "parameter not yet supported
      IMPORTING
        text      = lv_tx_return
      EXCEPTIONS
        OTHERS    = 3.
    IF sy-subrc = 0.
      ls_out-tx_nocliind = lv_tx_return.
    ELSE.
      ls_out-tx_nocliind = ls_out-ccnocliind.
    ENDIF.

*   get text for: client lock against import
    CALL FUNCTION 'DDUT_TEXT_FOR_VALUE'
      EXPORTING
        tabname   = gc_lst_stru0
        fieldname = 'CCCOPYLOCK'
        value     = ls_out-cccopylock
*        langu     = sy-langu           "parameter not yet supported
      IMPORTING
        text      = lv_tx_return
      EXCEPTIONS
        OTHERS    = 3.
    IF sy-subrc = 0.
      ls_out-tx_copylock = lv_tx_return.
    ELSE.
      ls_out-tx_copylock = ls_out-cccopylock.
    ENDIF.

*   get text for: client lock against import
    CALL FUNCTION 'DDUT_TEXT_FOR_VALUE'
      EXPORTING
        tabname   = gc_lst_stru0
        fieldname = 'CCIMAILDIS'
        value     = ls_out-ccimaildis
*        langu     = sy-langu           "parameter not yet supported
      IMPORTING
        text      = lv_tx_return
      EXCEPTIONS
        OTHERS    = 3.
    IF sy-subrc = 0.
      ls_out-tx_imaildis = lv_tx_return.
    ELSE.
      ls_out-tx_imaildis = ls_out-ccimaildis.
    ENDIF.

*   Client Control: client lock flag due to client copy activities
    IF ls_out-cctemplock = space.
      ls_out-ti_templock = gc_icon_unlock.
    ELSE.
      ls_out-ti_templock = gc_icon_lock.
    ENDIF.

* Niet belangrijk.
*    IF ls_out-ccnocascad = space.
*      ls_out-ti_nocascad = gc_icon_umarked.
*    ELSE.
*      ls_out-ti_nocascad = gc_icon_marked.
*    ENDIF.

    IF ls_out-ccorigcont <> space.
      ls_out-ti_origcont = gc_icon_inactive.
    ELSE.
      ls_out-ti_origcont = gc_icon_active.
    ENDIF.

    APPEND ls_out TO pt_out.
  ENDLOOP.

ENDFORM.                    " GET_CLIENT_LIST

*Selection text£º
*P_MANDT:        Client
*P_SYSTEM:        System
