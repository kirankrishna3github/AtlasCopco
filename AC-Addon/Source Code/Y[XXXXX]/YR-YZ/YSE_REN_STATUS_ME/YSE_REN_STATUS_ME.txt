*&---------------------------------------------------------------------*
*& Report  YSE_REN_STATUS_ME                                           *
*&                                                                     *
*&---------------------------------------------------------------------*
*&                                                                     *
*&                                                                     *
*&---------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |03/12/2009|J. Smets  |CD1K951942       | CR0873             *
*         |PO creation out of ASSO for Service Orders                  *
*----------------------------------------------------------------------*
* MOD-002 |15/03/2011|Lakshmi R |CD1K963742       | CR1995             *
************************************************************************

REPORT  yse_ren_status_me                       .

*Developed by Christophe Geerts 13/08/2007
*Issue 1872

TABLES: yse_ren_status_m, ekes.


DATA: wa_ren_status_m LIKE yse_ren_status_m.
DATA: wa_ren_status_m_1 LIKE yse_ren_status_m.
DATA: it_ren_status_m_po LIKE yse_ren_status_m OCCURS 0.
DATA: it_ren_status_m_so LIKE yse_ren_status_m OCCURS 0.
DATA: it_ren_status_m_po_delete LIKE yse_ren_status_m OCCURS 0.
DATA: it_ren_status_m_so_delete LIKE yse_ren_status_m OCCURS 0.

DATA: it_delete_po LIKE yse_ren_status_m OCCURS 0.
DATA: it_delete_so LIKE yse_ren_status_m OCCURS 0.

DATA: wa_ret   TYPE bapiret2,
      wa_sch   TYPE bapimeposchedule,
      wa_schx  TYPE bapimeposchedulx,
      wa_sch2  TYPE bapischdl,
      wa_schx2 TYPE bapischdlx,
      inx      TYPE bapisdh1x.

DATA: it_ret   TYPE TABLE OF bapiret2,
      it_sch   TYPE TABLE OF bapimeposchedule,
      it_schx  TYPE TABLE OF bapimeposchedulx,
      it_sch2  TYPE TABLE OF bapischdl,
      it_schx2 TYPE TABLE OF bapischdlx.

DATA: BEGIN OF it_messages OCCURS 0.
DATA:   text TYPE char20.
DATA:   vbeln TYPE vbeln_va.
        INCLUDE STRUCTURE bapiret2.
DATA: END OF it_messages.

DATA: wa_messages LIKE it_messages.

DATA: ch_ok TYPE i,
      nr_lines TYPE i.

SELECT-OPTIONS: s_vbeln FOR yse_ren_status_m-vbeln.

PERFORM get_po.
PERFORM get_so.

*--------------------
PERFORM process_po.
IF NOT it_ren_status_m_po_delete[] IS INITIAL.
  DELETE yse_ren_status_m FROM TABLE it_ren_status_m_po_delete.
ENDIF.

PERFORM process_so.
IF NOT it_ren_status_m_so_delete[] IS INITIAL.
  DELETE yse_ren_status_m FROM TABLE it_ren_status_m_so_delete.
ENDIF.

*errorlog

IF NOT it_messages[] IS INITIAL.
  SORT it_messages BY text.
  WRITE: /'ERRORS: '.
  WRITE: /'-----------'.
  WRITE: /''.
  LOOP AT it_messages INTO wa_messages.
    WRITE: / wa_messages-text,
             wa_messages-type,
             wa_messages-id,
             wa_messages-number,
             wa_messages-message,
             wa_messages-log_no,
             wa_messages-log_msg_no,
             wa_messages-message_v1,
             wa_messages-message_v2,
             wa_messages-message_v3,
             wa_messages-message_v4,
             wa_messages-parameter,
             wa_messages-row,
             wa_messages-field,
             wa_messages-system.
  ENDLOOP.
ELSE.

  WRITE: /'EVERYTHING WAS SUCCESSFULLY PROCESSED: '.
  WRITE: /'-----------'.
  WRITE: /''.


ENDIF.



*&---------------------------------------------------------------------*
*&      Form  get_po
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM get_po .
*Get the PO lines to be processed
  SELECT * FROM yse_ren_status_m INTO TABLE it_ren_status_m_po
                WHERE doc_type EQ 'P' AND
                      vbeln IN s_vbeln.

ENDFORM.                    " get_po
*&---------------------------------------------------------------------*
*&      Form  get_so
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM get_so .
*Get the PO lines to be processed
  SELECT * FROM yse_ren_status_m INTO TABLE it_ren_status_m_so
                WHERE doc_type EQ 'S' AND
                      vbeln IN s_vbeln.

ENDFORM.                    " get_so
*&---------------------------------------------------------------------*
*&      Form  process_po
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM process_po .
*Process the PO lines
  LOOP AT it_ren_status_m_po INTO wa_ren_status_m_1.
    wa_ren_status_m = wa_ren_status_m_1.

    AT NEW vbeln.
      CLEAR: it_ret.
      REFRESH: it_ret, it_sch, it_schx, it_delete_po.
    ENDAT.

* check if PO still is allowed to be updated
* check confirmation table ekes
    CLEAR ch_ok.
    PERFORM check_confirmation USING wa_ren_status_m_1.

    IF ch_ok EQ 1.
      CLEAR wa_sch.
      wa_sch-po_item       =  wa_ren_status_m-posnr.
      wa_sch-sched_line    = '0001'.
      wa_sch-delivery_date = wa_ren_status_m-del_date.
      APPEND wa_sch TO it_sch.

      CLEAR wa_schx.
      wa_schx-po_item       =  wa_ren_status_m-posnr.
      wa_schx-sched_line    = '0001'.
      wa_schx-delivery_date = 'X'.
      APPEND wa_schx TO it_schx.
      APPEND wa_ren_status_m TO it_delete_po.
    ELSE.
* PO's which cannot be updated because of status, must be delete from Y-table

      APPEND wa_ren_status_m TO  it_ren_status_m_po_delete.
    ENDIF.




    AT END OF vbeln.

      DESCRIBE TABLE it_sch LINES nr_lines.
      IF nr_lines GT 0.
        CALL FUNCTION 'BAPI_PO_CHANGE'
          EXPORTING
            purchaseorder                = wa_ren_status_m-vbeln
*                      POHEADER                     =
*                      POHEADERX                    =
*                      TESTRUN                      =
         TABLES
           return                       = it_ret
*                      POITEM                       =
*                      POITEMX                      =
           poschedule                   = it_sch
           poschedulex                  = it_schx
                  .
*** If there are entries in it_ret with status 'E', then something was wrong -> Fill dbtable to try later?
        LOOP AT it_ret TRANSPORTING NO FIELDS WHERE type = 'E' OR type = 'A'.
        ENDLOOP.
        IF sy-subrc NE 0.
          CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.
          APPEND LINES OF it_delete_po TO it_ren_status_m_po_delete.
        ELSE.
*A problem occured, write to screen
          LOOP AT it_ret INTO wa_ret.
            CLEAR wa_messages.
            wa_messages-text = text-002.
            MOVE-CORRESPONDING wa_ret TO wa_messages.
            APPEND wa_messages TO it_messages.
          ENDLOOP.

        ENDIF.
      ENDIF.

    ENDAT.

  ENDLOOP.

ENDFORM.                    " process_po
*&---------------------------------------------------------------------*
*&      Form  process_so
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM process_so .

  inx-updateflag = 'U'.

  LOOP AT it_ren_status_m_so INTO wa_ren_status_m_1.
    CLEAR wa_ren_status_m.
    wa_ren_status_m = wa_ren_status_m_1.

    AT NEW vbeln.
      REFRESH: it_sch2,
               it_schx2,
               it_ret,
               it_delete_so.
    ENDAT.

    CLEAR: wa_sch2, wa_schx.
    wa_sch2-itm_number = wa_ren_status_m-posnr.
    wa_sch2-sched_line = wa_ren_status_m-sched_line.
*** MOD-001 * begin ***
    IF wa_ren_status_m-req_date < sy-datum.
      wa_sch2-req_date = sy-datum.
    ELSE.
*** MOD-001 * end ***
      wa_sch2-req_date = wa_ren_status_m-req_date.
    ENDIF.                                                  "MOD-001
    APPEND wa_sch2 TO it_sch2.
    wa_schx2-itm_number = wa_ren_status_m-posnr.
    wa_schx2-sched_line = wa_ren_status_m-sched_line.
    wa_schx2-updateflag = 'U'.
    wa_schx2-req_date   = 'X'.
    APPEND wa_schx2 TO it_schx2.

    APPEND wa_ren_status_m TO it_delete_so.

    AT END OF vbeln.
      CALL FUNCTION 'BAPI_SALESORDER_CHANGE'
        EXPORTING
          salesdocument    = wa_ren_status_m-vbeln
          order_header_inx = inx
        TABLES
          return           = it_ret
          schedule_lines   = it_sch2
          schedule_linesx  = it_schx2.
*** If there are entries in it_ret with status 'E', then something was wrong -> Fill dbtable to try later?
      LOOP AT it_ret TRANSPORTING NO FIELDS WHERE type = 'E' OR type = 'A'.
      ENDLOOP.
      IF sy-subrc NE 0.
        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.
        APPEND LINES OF it_delete_so TO it_ren_status_m_so_delete.
      ELSE.
*A problem occured, write to screen
        LOOP AT it_ret INTO wa_ret.
          CLEAR wa_messages.
          wa_messages-text = text-001.
          MOVE-CORRESPONDING wa_ret TO wa_messages.
          APPEND wa_messages TO it_messages.
        ENDLOOP.
      ENDIF.
    ENDAT.



  ENDLOOP.


ENDFORM.                    " process_so
*&---------------------------------------------------------------------*
*&      Form  check_confirmation
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_WA_REN_STATUS_M_1  text
*----------------------------------------------------------------------*
FORM check_confirmation  USING
                     l_wa_ren_status_m_1 STRUCTURE yse_ren_status_m.

  DATA: wa_ekes LIKE ekes,
        wa_nr_xblnr TYPE i,
        it_ekes LIKE ekes OCCURS 0.


  SELECT *
  INTO TABLE it_ekes
  FROM ekes
  WHERE ebeln EQ l_wa_ren_status_m_1-vbeln
  AND   ebelp EQ l_wa_ren_status_m_1-posnr.
  IF sy-subrc EQ 4.
    MOVE 1 TO ch_ok.
  ENDIF.


  LOOP AT it_ekes INTO wa_ekes.
    IF wa_ekes-ebtyp EQ 'LA'.
      MOVE 0 TO ch_ok.
      EXIT.
    ELSEIF wa_ekes-ebtyp EQ 'AB'.
      IF  wa_ekes-xblnr CO '0123456789 '
          AND NOT wa_ekes-xblnr IS INITIAL.
        MOVE wa_ekes-xblnr TO wa_nr_xblnr.
*        IF wa_nr_xblnr < 160.               "comment MOD-002
        IF wa_nr_xblnr < 165.               "insert MOD-002
          MOVE 1 TO ch_ok.
        ELSE.
          MOVE 0 TO ch_ok.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDLOOP.


ENDFORM.                    " check_confirmation

*Text symbol text£º
*001:Sales order

*002:Purchase order
*Selection text£º
*S_VBELN:        Document number
