*&---------------------------------------------------------------------*
*& Report  YSE_CORRECT_CLOSED_PCNF_END
*&
*&---------------------------------------------------------------------*
*&
*& Correct actual enddate for closed orders with status PCNF
*&
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 02.02.2012
*  Change Request Number : CR2334
*  Transport request Nr. : CD1K970207
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. |   DATE     | NAME               | CORRECT. NR| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                    | CD1K9..... | CR....      *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_correct_closed_pcnf_end.

TABLES: aufk,
        afih,
        afko,
        afru,
        jest.

* Types
TYPES: BEGIN OF ty_afru,
         aufnr     TYPE aufnr,
         iedd      TYPE ru_iedd,
       END OF ty_afru.

* Internal tables
DATA: BEGIN OF it_aufk  OCCURS 0,
        aufnr      TYPE aufnr,
      END OF it_aufk.

DATA: it_afru   TYPE HASHED TABLE OF ty_afru
                     WITH UNIQUE KEY aufnr
                     WITH HEADER LINE,
      it_afrui  TYPE STANDARD TABLE OF ty_afru
                     WITH HEADER LINE.


*&---------------------------------------------------------------------*
*&  Selection screen                                                   *
*&---------------------------------------------------------------------*
SELECT-OPTIONS: s_bukrs   FOR aufk-bukrs,
                s_aufnr   FOR aufk-aufnr,
                s_idat3   FOR aufk-idat3.

*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

* Get Service Orders
  SELECT o~aufnr INTO TABLE it_aufk
         FROM aufk AS o
         INNER JOIN afih AS h
                    ON o~aufnr = h~aufnr
         INNER JOIN afko AS d
                    ON o~aufnr = d~aufnr
         INNER JOIN jest AS s
                    ON o~objnr = s~objnr
         WHERE o~aufnr IN s_aufnr
           AND o~bukrs IN s_bukrs
           AND o~idat3 IN s_idat3
           AND h~iphas = '6'
           AND d~getri = '00000000'
           AND s~stat  = 'I0010'
           AND s~inact = ' '.

* Check anything selected
  IF it_aufk[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No Service Orders selected'(e01).
    EXIT.
  ENDIF.

* Get confirmation dates
  SELECT aufnr iedd INTO TABLE it_afrui
         FROM afru
         FOR ALL ENTRIES IN it_aufk
         WHERE aufnr = it_aufk-aufnr
           AND learr = 'ZAM001'.

* Get the latest confirmation date
  SORT it_afrui BY aufnr iedd DESCENDING.
  DELETE ADJACENT DUPLICATES FROM it_afrui
         COMPARING aufnr.
  it_afru[] = it_afrui[].
  FREE it_afrui.

* Process Service Orders
  LOOP AT it_aufk.

    READ TABLE it_afru
               WITH TABLE KEY aufnr = it_aufk-aufnr.

    IF sy-subrc = 0.
*     Update Confirmed Order Finish Date
      TRY.
          UPDATE afko
                 SET getri = it_afru-iedd
                 WHERE aufnr = it_aufk-aufnr.
        CATCH cx_sy_dynamic_osql_error.
          WRITE: /01 'Error in update for Order:'(e02),
                     it_aufk-aufnr.
      ENDTRY.
    ELSE.
      WRITE: /01 'No confirmation date for Order:'(e03),
                 it_aufk-aufnr.
    ENDIF.

  ENDLOOP.

*Text symbol text£º
*E01:No Service Orders selected
*E02:Error in update for Order:

*E03:No confirmation date for Order:
*Selection text£º
*S_AUFNR:        Service Order
*S_BUKRS:        Company Code
*S_IDAT3:        Close Date
