********************************************************************
* Program ID           : YSE_EM_UPDATE_PLANT                       *
* Program Title        : EM: Update Plant Information              *
* Author               : Inge Bruyndonckx / Erik Walravens         *
* Date                 : 20.11.2006                                *
* Change Request Number: 141106-SE-OTH-D168-Exception Management   *
* Description          : Update tables YSE_EM_PLANT and            *
*                        YSE_EM_PL_DISTR                           *
*==================================================================*
* Copied From         : (Cloned Program)                           *
* Title               : (Program Title)                            *
* Other Related obj   : (Object names)                             *
********************************************************************
REPORT  yse_em_update_plant MESSAGE-ID yse_sales_log.

********************************************************************
* CLASSES                                                          *
********************************************************************
*------------------------------------------------------------------*
*       CLASS lcl_event_handler DEFINITION                         *
*------------------------------------------------------------------*
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    METHODS:
*   Double click control
*    handle_double_click
*          FOR EVENT double_click OF cl_gui_alv_grid
*          IMPORTING e_row e_column,

*   Hotspot click control
    handle_hotspot_click
          FOR EVENT hotspot_click OF cl_gui_alv_grid
          IMPORTING e_row_id e_column_id es_row_no.

*   handle_user_command
*         FOR EVENT user_command OF cl_gui_alv_grid
*         IMPORTING e_ucomm.

ENDCLASS.                    "lcl_event_handler DEFINITION

*-----------------------------------------------------------------*
*       CLASS lcl_event_handler IMPLEMENTATION                    *
*-----------------------------------------------------------------*
CLASS lcl_event_handler IMPLEMENTATION.

* Handle double click
*  METHOD handle_double_click.
*    PERFORM handle_double_click USING e_row e_column.
*  ENDMETHOD.

* Handle hotspot click
  METHOD handle_hotspot_click.
    PERFORM handle_hotspot_click USING e_row_id e_column_id es_row_no.
  ENDMETHOD.                    "handle_hotspot_click

*  METHOD handle_user_command .
*    CASE e_ucomm.
*      WHEN 'BACK'.
*    ENDCASE.
*  ENDMETHOD.                    "handle_user_command

ENDCLASS.                    "lcl_event_handler IMPLEMENTATION

********************************************************************
* FIELD-SYMBOLS                                                    *
********************************************************************
FIELD-SYMBOLS: <fieldcat> TYPE lvc_s_fcat.

********************************************************************
* OBJECTS                                                          *
********************************************************************
DATA : obj_container     TYPE REF TO cl_gui_custom_container.
DATA : obj_alv           TYPE REF TO cl_gui_alv_grid .
DATA : obj_event_handler TYPE REF TO lcl_event_handler.

********************************************************************
* TABLES                                                           *
********************************************************************
TABLES: yse_em_pl_distr,
        yse_em_pl_centr,
        yse_em_plant,
        yse_em_whstype,
        t001w.

********************************************************************
* INTERNAL TABLES                                                  *
********************************************************************
DATA:  BEGIN OF it_areas OCCURS 0.
        INCLUDE STRUCTURE yse_em_alv_distr.
DATA:  END OF it_areas.

DATA:  BEGIN OF it_yse_em_pl_distr OCCURS 0.
        INCLUDE STRUCTURE yse_em_pl_distr.
DATA:  END OF it_yse_em_pl_distr.

DATA:  BEGIN OF it_areas_c OCCURS 0.
        INCLUDE STRUCTURE yse_em_alv_centr.
DATA:  END OF it_areas_c.

DATA:  BEGIN OF it_yse_em_pl_centr OCCURS 0.
        INCLUDE STRUCTURE yse_em_pl_centr.
DATA:  END OF it_yse_em_pl_centr.

DATA:  BEGIN OF it_yse_em_plant OCCURS 0.
        INCLUDE STRUCTURE yse_em_plant.
DATA:  END OF it_yse_em_plant.

********************************************************************
* CONSTANTS                                                        *
********************************************************************
CONSTANTS: con_true  TYPE char1 VALUE 'X',
           con_false TYPE char1 VALUE ''.

********************************************************************
* DATA                                                             *
********************************************************************
DATA: okcode LIKE sy-ucomm.
DATA: mrow TYPE lvc_s_row .

DATA: gs_layout TYPE lvc_s_layo ,
      g_behaviour_alv TYPE REF TO cl_dragdrop.

DATA: it_fieldcat TYPE lvc_t_fcat.

DATA: mlines TYPE i.

DATA: loaded_b TYPE char1 VALUE con_false,
      loaded_c TYPE char1 VALUE con_false.
DATA: wa_plant_desc LIKE t001w-name1.

********************************************************************
* CONTROLS                                                         *
********************************************************************
DATA: of_werks TYPE werks,
*      of_plantdesc like T001W-NAME1,
      if_whtype LIKE yse_em_whstype-whtype,
      of_whtypedesc LIKE yse_em_whstype-whtypedesc.

********************************************************************
*                      START OF SELECTION                          *
********************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-s01.
PARAMETERS: p_werks LIKE yse_em_pl_distr-werks OBLIGATORY.    " Plant
SELECTION-SCREEN END OF BLOCK b1.
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.
*----------------------------------------------------------------------*

  PERFORM  check_authorization .
********************************************************************
*                      START OF SELECTION                          *
********************************************************************
START-OF-SELECTION.

  PERFORM get_data.

  CALL SCREEN 100.

*-----------------------------------------------------*
*       FORM GET_DATA                                 *
*-----------------------------------------------------*
FORM get_data.

  CLEAR   it_areas.
  REFRESH it_areas.

* Display labels
  of_werks = p_werks.

  SELECT SINGLE *
      FROM yse_em_plant
     WHERE werks = p_werks.

  if_whtype = yse_em_plant-whtype.
  of_whtypedesc = yse_em_plant-whstdes.

  IF if_whtype = 'B'.
    PERFORM get_b_data.
  ELSEIF if_whtype = 'C'.
    PERFORM get_c_data.
  ENDIF.

ENDFORM .                    "GET_DATA


*-----------------------------------------------------*
*       FORM GET_B_DATA                               *
*-----------------------------------------------------*
* Loads it_area with plants and assigned sales        *
* districts.                                          *
*-----------------------------------------------------*
FORM get_b_data.

  DATA: lv_t001w LIKE t001w .

  IF loaded_b = con_false.
    CLEAR   it_areas.
    REFRESH it_areas.

*   Join plants with sales districts
    SELECT a~bzirk b~bztxt
        INTO CORRESPONDING FIELDS OF TABLE it_areas
        FROM t171 AS a INNER JOIN t171t AS b
          ON a~bzirk = b~bzirk
       WHERE b~spras EQ sy-langu.

    DELETE it_areas WHERE bzirk IS INITIAL.

* RESTRICT SALES DISTRICT LIST FOR THE CHOSEN PLANT
* FIND COUNTRY KEY FOR THE JOINED PLANTS
*DELETE ALL SALES DISTRICT WITH FIRST 2 CHAR NE TO COUNTRY KEY OF CHOSEN
*PLANT


    SELECT SINGLE * FROM t001w
         INTO CORRESPONDING FIELDS OF lv_t001w
         WHERE werks EQ p_werks.
    MOVE lv_t001w-name1 TO wa_plant_desc.

* DON'T DO THIS FOR ZZ PLANTS (DUMMY PLANTS)


    IF NOT p_werks(2) = 'ZZ'.


      LOOP AT it_areas.

        IF it_areas-bzirk(2) NE lv_t001w-land1.
          DELETE it_areas.
        ENDIF.
      ENDLOOP.
    ENDIF.

*   Load internal table with selected plant assigned to sales areas
    SELECT * FROM yse_em_pl_distr
        INTO TABLE it_yse_em_pl_distr
       WHERE werks EQ p_werks.

*   Set checkmarks for assigned sales districts
    LOOP AT it_areas.
      READ TABLE it_yse_em_pl_distr WITH KEY bzirk = it_areas-bzirk.
      IF sy-subrc EQ 0.
        it_areas-sel = con_true.
        MODIFY it_areas.
      ENDIF.
    ENDLOOP.
    loaded_b = con_true.
  ENDIF.                       " Loaded B
ENDFORM .                    "GET_B_DATA

*-----------------------------------------------------*
*       FORM GET_C_DATA                               *
*-----------------------------------------------------*
* Loads it_area with plants and assigned branch       *
* plants.                                             *
*-----------------------------------------------------*
FORM get_c_data.

  IF loaded_c = con_false.
    CLEAR   it_areas_c.
    REFRESH it_areas_c.

* FIND PLANT DESCRIPTION
    SELECT SINGLE * FROM t001w
            WHERE werks EQ p_werks.

    MOVE t001w-name1 TO wa_plant_desc.


*   Load branch plants into it_areas_c
    SELECT *
        FROM yse_em_plant
        INTO TABLE it_yse_em_plant
       WHERE whtype = 'B'.

    LOOP AT it_yse_em_plant.
      CLEAR t001w.
      SELECT SINGLE *
          FROM t001w
         WHERE werks = it_yse_em_plant-werks.

*      IF sy-subrc EQ 0.


      it_areas_c-bwerksdesc = t001w-name1.
*      ENDIF.

      it_areas_c-bwerks = it_yse_em_plant-werks.

      APPEND it_areas_c.
    ENDLOOP.

    DELETE it_areas_c WHERE bwerks IS INITIAL.

*   Load internal table with selected plant assigned to branch plants
    SELECT *
        FROM yse_em_pl_centr
        INTO TABLE it_yse_em_pl_centr
       WHERE cwerks EQ p_werks.

*   Set checkmarks for assigned branch plants
    LOOP AT it_areas_c.
      READ TABLE it_yse_em_pl_centr WITH KEY bwerks = it_areas_c-bwerks.
      IF sy-subrc EQ 0.
        it_areas_c-sel = con_true.
        MODIFY it_areas_c.
      ENDIF.
    ENDLOOP.
    loaded_c = con_true.
  ENDIF.                       " Loaded C
ENDFORM .                    "GET_C_DATA


*&-----------------------------------------------------*
*&      Module  STATUS_0100  OUTPUT                    *
*&-----------------------------------------------------*
* -----------------------------------------------------
* The PF STATUS STATUS100 called from the module below,
* has on the Standard toolbar the buttons
* BACK , EXIT and CANCEL .
* These buttons are provided with function code = 'EXIT'
* -----------------------------------------------------
MODULE status_0100 OUTPUT.
  SET TITLEBAR 'OVERVIEW' .
  SET PF-STATUS 'STATUS100'.
ENDMODULE.                 " STATUS_0100  OUTPUT


*----------------------------------------------------------------------*
*  MODULE USER_COMMAND_0100 INPUT
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
MODULE user_command_0100 INPUT.

* Status BACK pressed
  IF okcode = 'BACK'.
    LEAVE TO SCREEN 0.
  ENDIF.

* REFRESH button pressed
  IF okcode = 'RFSH'.
    IF if_whtype = 'B'.
      PERFORM get_b_data.
      PERFORM build_alv_b.
    ELSEIF if_whtype = 'C'.
      PERFORM get_c_data.
      PERFORM build_alv_c.
    ELSE.
      CALL METHOD obj_alv->set_visible
        EXPORTING
          visible           = ' '
        EXCEPTIONS
          cntl_error        = 1
          cntl_system_error = 2
          OTHERS            = 3.
    ENDIF.
  ENDIF.

* UPDATE button pressed
  IF okcode = 'UPDT'.

*   Update table YSE_EM_PLANT
    IF if_whtype NE space.

*     Does entry exist?
      SELECT SINGLE *
           FROM yse_em_plant
          WHERE werks  = p_werks.
*            AND whtype = if_whtype.
      IF sy-subrc NE 0.  " not found
*       Create a new record
        yse_em_plant-werks  = p_werks.
        yse_em_plant-whtype = if_whtype.
        SELECT SINGLE *
            FROM yse_em_whstype
            WHERE whtype = if_whtype.
        yse_em_plant-whstdes = yse_em_whstype-whtypedesc.
        INSERT yse_em_plant.
      ELSE.
*       Update existing record
        SELECT SINGLE *
            FROM yse_em_whstype
            WHERE whtype = if_whtype.
        UPDATE yse_em_plant
              SET whtype  = if_whtype
                  whstdes = yse_em_whstype-whtypedesc
            WHERE werks   = p_werks.
      ENDIF.
    ENDIF.

    IF if_whtype = 'B'.

*     Update table YSE_EM_PL_DISTR
      LOOP AT it_areas.
*       No checkmark?
        IF it_areas-sel EQ con_false.
          DELETE
              FROM yse_em_pl_distr
              WHERE werks = p_werks
                AND bzirk = it_areas-bzirk.
*       Checkmark?
        ELSEIF it_areas-sel EQ con_true.
*         Does record exist?
          SELECT SINGLE *
               FROM yse_em_pl_distr
              WHERE werks = p_werks
                AND bzirk = it_areas-bzirk.
          IF sy-subrc NE 0.    " not found
            yse_em_pl_distr-werks = p_werks.
            yse_em_pl_distr-bzirk = it_areas-bzirk.
            INSERT yse_em_pl_distr.
          ENDIF.
        ENDIF.                     " checkmark
      ENDLOOP.                   " it_areas

*     Delete entries for this plant in table YSE_EM_PL_CENTR
      DELETE
          FROM yse_em_pl_centr
         WHERE cwerks = p_werks.
      loaded_c = con_false.    " C data not actual anymore

      MESSAGE s003.
    ELSEIF if_whtype = 'C'.
*     Update table YSE_EM_PL_CENTR
      LOOP AT it_areas_c.
*       No checkmark?
        IF it_areas_c-sel EQ con_false.
          DELETE
              FROM yse_em_pl_centr
              WHERE cwerks = p_werks
                AND bwerks = it_areas_c-bwerks.
*       Checkmark?
        ELSEIF it_areas_c-sel EQ con_true.
*         Does record exist?
          SELECT SINGLE *
               FROM yse_em_pl_centr
              WHERE cwerks = p_werks
                AND bwerks = it_areas_c-bwerks.
          IF sy-subrc NE 0.    " not found
            yse_em_pl_centr-cwerks = p_werks.
            yse_em_pl_centr-bwerks = it_areas_c-bwerks.
            INSERT yse_em_pl_centr.
          ENDIF.
        ENDIF.                     " checkmark
      ENDLOOP.                   " it_areas_c
*     Delete entries for this plant in table YSE_EM_PL_DISTR
      DELETE
          FROM yse_em_pl_distr
         WHERE werks = p_werks.
      loaded_b = con_false.    " B data not actual anymore
    ENDIF.                     " B or C plant
  ENDIF.                     " Update button
ENDMODULE.                 " USER_COMMAND_0100 INPUT


*&---------------------------------------------------------------------*
*&      Module  PREPARE_SCREEN  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE prepare_screen OUTPUT.

  IF obj_container IS INITIAL .
    CREATE OBJECT obj_container
             EXPORTING container_name = 'OVERVIEW' .

    CREATE OBJECT obj_alv
              EXPORTING i_parent = obj_container.

    CREATE OBJECT obj_event_handler.

    IF if_whtype = 'B'.
      PERFORM build_alv_b.
    ELSEIF if_whtype = 'C'.
      PERFORM build_alv_c.
    ENDIF.

*   Enable checkbox clicking
    SET HANDLER obj_event_handler->handle_hotspot_click FOR obj_alv.
  ENDIF.
ENDMODULE.                 " PREPARE_SCREEN  OUTPUT


*&---------------------------------------------------------------------*
*&      Form  build_alv_b
*&---------------------------------------------------------------------*
* Prepare alv object to display brach plants with sales districts      *
*&---------------------------------------------------------------------*
FORM build_alv_b.

  REFRESH: it_fieldcat.

  CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
   EXPORTING
    i_buffer_active              = ' '
    i_structure_name             = 'YSE_EM_ALV_DISTR'
*   I_CLIENT_NEVER_DISPLAY       = 'X'
*   I_BYPASSING_BUFFER           =
    CHANGING
      ct_fieldcat                  = it_fieldcat.

  LOOP AT it_fieldcat ASSIGNING <fieldcat>.

    CASE <fieldcat>-fieldname.

      WHEN  'SEL'.
        <fieldcat>-checkbox = con_true.
        <fieldcat>-hotspot  = con_true.
*        <fieldcat>-edit     = con_true.
*        <fieldcat>-input    = con_true.
        <fieldcat>-scrtext_l = 'Chk'.
        <fieldcat>-scrtext_m = 'Chk'.
        <fieldcat>-scrtext_s = 'Chk'.
        <fieldcat>-outputlen = 3.
      WHEN  'BZIRK'.
        <fieldcat>-scrtext_l = 'Sales district'.
        <fieldcat>-scrtext_m = 'Sales district'.
        <fieldcat>-scrtext_s = 'Sales district'.
        <fieldcat>-outputlen = 10.
      WHEN  'BZTXT'.
        <fieldcat>-scrtext_l = 'Description'.
        <fieldcat>-scrtext_m = 'Description'.
        <fieldcat>-scrtext_s = 'Description'.
        <fieldcat>-outputlen = 45.
    ENDCASE.
  ENDLOOP.

  gs_layout-no_toolbar = con_true.
  gs_layout-sel_mode   = con_true.
  gs_layout-box_fname  = 'checkbox'.

  CALL METHOD obj_alv->set_table_for_first_display
    EXPORTING
      i_structure_name              = 'yse_em_alv_distr'
      is_layout                     = gs_layout
    CHANGING
      it_outtab                     = it_areas[]
      it_fieldcatalog               = it_fieldcat
    EXCEPTIONS
      invalid_parameter_combination = 1
      program_error                 = 2
      too_many_lines                = 3
      OTHERS                        = 4.

  IF sy-subrc <> 0.
*     MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*     WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.
ENDFORM.                    "build_alv_b


*&---------------------------------------------------------------------*
*&      Form  build_alv_c
*&---------------------------------------------------------------------*
* Prepare alv object to display brach plants with sales districts      *
*&---------------------------------------------------------------------*
FORM build_alv_c.

  REFRESH: it_fieldcat.

  CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
   EXPORTING
    i_buffer_active              = ' '
    i_structure_name             = 'YSE_EM_ALV_CENTR'
*   I_CLIENT_NEVER_DISPLAY       = 'X'
*   I_BYPASSING_BUFFER           =
    CHANGING
      ct_fieldcat                  = it_fieldcat.

  LOOP AT it_fieldcat ASSIGNING <fieldcat>.

    CASE <fieldcat>-fieldname.

      WHEN  'SEL'.
        <fieldcat>-checkbox = con_true.
        <fieldcat>-hotspot  = con_true.
*        <fieldcat>-edit     = con_true.
*        <fieldcat>-input    = con_true.
        <fieldcat>-scrtext_l = 'Chk'.
        <fieldcat>-scrtext_m = 'Chk'.
        <fieldcat>-scrtext_s = 'Chk'.
        <fieldcat>-outputlen = 3.
      WHEN  'WERKS'.
        <fieldcat>-scrtext_l = 'Branch plant'.
        <fieldcat>-scrtext_m = 'Branch plant'.
        <fieldcat>-scrtext_s = 'Branch plant'.
        <fieldcat>-outputlen = 10.
      WHEN  'NAME1'.
        <fieldcat>-scrtext_l = 'Description'.
        <fieldcat>-scrtext_m = 'Description'.
        <fieldcat>-scrtext_s = 'Description'.
        <fieldcat>-outputlen = 45.
    ENDCASE.
  ENDLOOP.

  gs_layout-no_toolbar = con_true.
  gs_layout-sel_mode   = con_true.
  gs_layout-box_fname  = 'checkbox'.

  CALL METHOD obj_alv->set_table_for_first_display
    EXPORTING
      i_structure_name              = 'yse_em_alv_centr'
      is_layout                     = gs_layout
    CHANGING
      it_outtab                     = it_areas_c[]
      it_fieldcatalog               = it_fieldcat
    EXCEPTIONS
      invalid_parameter_combination = 1
      program_error                 = 2
      too_many_lines                = 3
      OTHERS                        = 4.

  IF sy-subrc <> 0.
*     MESSAGE ...
  ENDIF.
ENDFORM.                          "  build_alv_c

*&---------------------------------------------------------------------*
*&      Form  handle_hotspot_click
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_E_ROW_ID     text
*      -->P_E_COLUMN_ID  text
*      -->P_ES_ROW_NO    text
*----------------------------------------------------------------------*
FORM handle_hotspot_click  USING    p_e_row_id
                                    p_e_column_id
                                    p_es_row_no STRUCTURE lvc_s_roid.

* If checkbox clicked
  IF p_e_column_id EQ 'SEL'.

    IF if_whtype = 'B'.
*     Check checkbox status
      READ TABLE it_areas INDEX p_es_row_no-row_id.
*     Toggle checkbox
      IF it_areas-sel EQ con_true.
        it_areas-sel = con_false.
      ELSE.
        it_areas-sel = con_true.
      ENDIF.
*     Update checkbox status
      MODIFY it_areas INDEX p_es_row_no-row_id.
    ELSEIF if_whtype = 'C'.
*     Check checkbox status
      READ TABLE it_areas_c INDEX p_es_row_no-row_id.
*     Toggle checkbox
      IF it_areas_c-sel EQ con_true.
        it_areas_c-sel = con_false.
      ELSE.
        it_areas_c-sel = con_true.
      ENDIF.
*     Update checkbox status
      MODIFY it_areas_c INDEX p_es_row_no-row_id.
    ENDIF.
  ENDIF.

  CALL METHOD obj_alv->refresh_table_display.

ENDFORM.                    " handle_hotspot_click
*&---------------------------------------------------------------------*
*&      Form  check_authorization
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM check_authorization .

  AUTHORITY-CHECK OBJECT 'M_MATE_WRK'
           ID 'ACTVT' DUMMY
           ID 'WERKS' FIELD p_werks.

  IF sy-subrc = 4.
*   No authorisation to display data from Sales Organisation p_vkorg
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH P_WERKS.
  ELSEIF sy-subrc <> 0.
*   Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
  ENDIF.



ENDFORM.                    " check_authorization

*Text symbol text£º

*S01:
*Selection text£º
*P_WERKS:D       Plant
