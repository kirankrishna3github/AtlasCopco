*----------------------------------------------------------------------*
* PROGRAM ID           : YSE_RENTAL_INFO_EQUI                          *
* PROGRAM TITLE        : Rental : Infomanager Equipments               *
* AUTHOR               : Marc Jacobs                                   *
* DATE                 : 15/09/2008                                    *
* DEVELOPMENT ID       : CR151                                         *
*                                                                      *
* CHANGE REQUEST NUMBER: CD1K943283                                    *
*                                                                      *
* Program Description:  Equipment from Rental to Infomanager           *
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECT NR.| CHANGE REFERENCE #       *
*----------------------------------------------------------------------*
* MOD-001 |05/05/2009|M.Jacobs  |CD1K947976 | NEWGL                    *
************************************************************************
REPORT yse_rental_info_equi MESSAGE-ID yse_rental.

************************************************************************
*                   T A B L E S                                        *
************************************************************************
TABLES: yse_infomanager,          " filenames
        v_equi.            " PM technical objects (EQUI, func.location)

************************************************************************
*                   C O N S T A N T S                                  *
************************************************************************
CONSTANTS :c_equipment  TYPE eqart VALUE 'EQUIPMENT',
           c_object(10) TYPE c     VALUE 'EQUI',
           c_99991231(8)           VALUE '99991231',
           c_n(1)       TYPE c     VALUE 'N',
           c_x(1)       TYPE c     VALUE 'X',
           c_y(1)       TYPE c     VALUE 'Y',
           c_run_hours  TYPE impt-atnam VALUE 'ZAM_RHRSTOTAL_ACT',
           c_underscore(1) TYPE c  VALUE '_',    " Underscore
           c_sc1(3)                VALUE 'SC1',
           c_input                 VALUE '0',
           c_0000(4)    TYPE c     VALUE '0000',
           c_001(3)     TYPE c     VALUE '001',
           c_clnt(4)    TYPE c     VALUE 'CLNT' ,
           c_i0076      LIKE jest-stat    VALUE 'I0076', "stat deleted
           c_i0320      LIKE jest-stat    VALUE 'I0320', "stat inact
           c_tab TYPE c VALUE cl_abap_char_utilities=>horizontal_tab.
CONSTANTS:
  gc_auart_zqp  TYPE vbak-auart VALUE 'ZQP',  " rental contract
  gc_auart_zqp2 TYPE vbak-auart VALUE 'ZQP2', " rental contract
  gc_auart_zrib TYPE vbak-auart VALUE 'ZRIB', " rental contract
  gc_auart_lr   TYPE vbak-auart VALUE 'LR',   " inbound delivery
  gc_auart_zlf  TYPE vbak-auart VALUE 'ZLF',  " outbound delivery
  gc_auart_zsm1 TYPE vbak-auart VALUE 'ZSM1', " service order
  gc_auart_zsm2 TYPE vbak-auart VALUE 'ZSM2', " service order
  gc_auart_zam1 TYPE vbak-auart VALUE 'ZAM1', " service order
  gc_auart_zam2 TYPE vbak-auart VALUE 'ZAM2'. " service order

************************************************************************
*                   V A R I A B L E S                                  *
************************************************************************
DATA:  g_equnr      TYPE equi-equnr,                " Equip Number
       g_vtweg      TYPE v_equi-vtweg,
       g_pfile      LIKE      /sapdmc/lsoinp-filename,
       g_ofile      LIKE      /sapdmc/lsoinp-filename,
       g_retcd      LIKE sy-subrc,                  " return code
       g_directory(25) TYPE c VALUE '/var/load/xxx/UK/convert/',
       g_directory2(23) TYPE c VALUE '/var/load/xxx/UK/read/',
       g_fm_atinn   TYPE ausp-atinn,
       g_point      LIKE imptt-point,               " Measurement point
       g_ersda(12)  TYPE c,                      " YYYYMMDDHHMM
       g_date       LIKE sy-datum.
DATA : gv_status(1) TYPE c,
       gv_ltdparts  LIKE pmco-wrt08,
       gv_ltdlabor  LIKE pmco-wrt08,
       gv_wrt08     LIKE pmco-wrt08,
       gv_objnr     LIKE pmco-objnr,
       gv_year      LIKE pmco-gjahr,
       gv_answl     LIKE anlc-answl,
       gv_nafaz     LIKE anlp-nafaz,
       gv_netbook   LIKE anlc-nafap,
       gv_anlcv     LIKE anlcv,
       gv_deakt     TYPE deakt,
       gv_datum     TYPE sy-datum.
DATA: g_dir_name    LIKE epsf-epsdirnam,
      g_dfile       LIKE epsf-epsfilnam.
DATA : it_outxls(4096)  TYPE c OCCURS 0,
       wa_outxls(4096)  TYPE c.
************************************************************************
*                  I N T E R N A L   T A B L E S                       *
************************************************************************
DATA : BEGIN OF i_prev OCCURS 0.
        INCLUDE STRUCTURE yse_im_equi.
DATA : END OF i_prev.

DATA : BEGIN OF i_equi OCCURS 0.
        INCLUDE STRUCTURE yse_im_equi.
DATA : objnr TYPE j_objnr,
       sernr TYPE gernr,
       matnr TYPE matnr,
       anlun TYPE anln2.
DATA : END OF i_equi.

DATA : BEGIN OF i_delfiles OCCURS 0,
         mandt LIKE yse_infomanager-mandt,
         bukrs LIKE yse_infomanager-bukrs,
         object LIKE yse_infomanager-object,
         ersda LIKE yse_infomanager-ersda,
       END OF i_delfiles.

DATA : BEGIN OF i_equi_delta OCCURS 0.
        INCLUDE STRUCTURE yse_im_equi.
DATA : END OF i_equi_delta.

DATA : BEGIN OF wa_yse_infomanager,
         mandt LIKE yse_infomanager-mandt,
         bukrs LIKE yse_infomanager-bukrs,
         object LIKE yse_infomanager-object,
         ersda LIKE yse_infomanager-ersda,
       END OF wa_yse_infomanager.

DATA: i_wa_point LIKE impt,
      i_wa_value LIKE imrg,
      p_logsys LIKE tbdlst-logsys  .

DATA : BEGIN OF lt_afih OCCURS 0,
         aufnr LIKE afih-aufnr,
       END OF lt_afih.

DATA : BEGIN OF lt_vkorg OCCURS 0,
         vkorg LIKE tvko-vkorg,
       END OF lt_vkorg.

DATA :
it_sme      TYPE yse_rent_sme         OCCURS 0 WITH HEADER LINE,
it_anlc     TYPE anlc                 OCCURS 0 WITH HEADER LINE.
***********************************************************************
* RANGES                                                              *
***********************************************************************
RANGES:
  gr_auart_contract FOR yse_rent_sme-auart,
  gr_auart_delivery FOR yse_rent_sme-auart,
  gr_auart_service  FOR yse_rent_sme-auart.

************************************************************************
*       S E L E C T - O P T I O N S / P A R A M E T E R S              *
************************************************************************

SELECTION-SCREEN : BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.

SELECT-OPTIONS: s_equnr FOR g_equnr.
PARAMETERS: p_bukrs  TYPE bukrs OBLIGATORY.
SELECT-OPTIONS: s_vtweg FOR g_vtweg.
PARAMETERS: p_init TYPE c AS CHECKBOX.
SELECTION-SCREEN: END OF BLOCK b1.

************************************************************************
INITIALIZATION.

  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = p_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  gv_datum = sy-datum.

  PERFORM fill_ranges.
************************************************************************
*    Disable Message Type Screen Field                                 *
************************************************************************
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-group1 = c_sc1.
      screen-input   = c_input.
      MODIFY SCREEN.
      CONTINUE.
    ENDIF.
  ENDLOOP.

************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

** Validate if the ALE Configuration is correctly setup
* PERFORM validate_ale_configuration .

** Get the information for selected plant(s)

  PERFORM get_detail USING p_bukrs.

*   Create delta with new creations/updates by comparing the selected
*   data with this from the previous run. When a new plant
*   has to be loaded, no comparison has to take place.
  IF NOT i_equi[] IS INITIAL.

    IF NOT p_init EQ c_x.

      SORT i_equi BY bukrs equnr.

      CLEAR: g_retcd.
      PERFORM get_previous_file USING p_bukrs
                             CHANGING g_retcd.

      IF g_retcd = 0.
        PERFORM create_delta.
      ENDIF.
    ELSE.
      LOOP AT i_equi.
        MOVE-CORRESPONDING i_equi TO i_equi_delta.
        APPEND i_equi_delta.
        CLEAR i_equi_delta.
      ENDLOOP.
    ENDIF.

*     write outputfile to application server
    CLEAR g_retcd.

    CONCATENATE sy-datum sy-uzeit(4) INTO g_ersda.
    CONCATENATE p_bukrs c_object g_ersda
              INTO g_ofile SEPARATED BY c_underscore.
    REPLACE 'xxx' IN g_directory WITH p_logsys(3).
    CONCATENATE g_directory g_ofile INTO g_ofile.

    PERFORM write_outputfile.

    IF g_retcd IS INITIAL.
*       update custom table YSE_INFOMANAGER
      PERFORM update_custom_table USING p_bukrs.

*       Delete older entries in custom table Yse_infomanager
      PERFORM delete_old_table_entries USING p_bukrs.

*       Delete older files on appl.server
      IF NOT i_delfiles[] IS INITIAL.
        PERFORM delete_old_files.
      ENDIF.

    ENDIF.

    PERFORM overwrite_file_to_compare.

  ENDIF.

************************************************************************
*       E N D - O F - S E L E C T I O N    E V E N T                   *
************************************************************************
END-OF-SELECTION .

  IF i_equi_delta[] IS INITIAL .
    WRITE: / 'No delta found'(i03).
  ELSE.
    SORT i_equi_delta BY equnr.
  ENDIF.

  WRITE: / 'Job finished'(i02).

*&---------------------------------------------------------------------*
*&      Form  Get_Detail
*&---------------------------------------------------------------------*
*       To get the information for selected plant(s)
*----------------------------------------------------------------------*
FORM get_detail USING p_bukrs.

  CLEAR : lt_vkorg.
  REFRESH : lt_vkorg.
  SELECT * INTO CORRESPONDING FIELDS OF TABLE lt_vkorg
     FROM tvko WHERE bukrs = p_bukrs.

  CLEAR i_equi.
  REFRESH i_equi.
  gv_year = sy-datum+0(4).

** Select data based on Selection screen parameters
  SELECT equnr matnr objnr bukrs sernr anlnr anlun
           INTO CORRESPONDING FIELDS OF TABLE i_equi
           FROM v_equi
           WHERE  equnr IN s_equnr
             AND  bukrs = p_bukrs
             AND  vtweg IN s_vtweg
             AND  ( eqtyp =  c_x OR eqtyp = c_y )
             AND  eqart = c_equipment
             AND  datbi = c_99991231.

** Process Error - No data found for the Selected company
  IF sy-subrc NE 0 .
    WRITE: text-e01, p_bukrs.
    EXIT.
  ENDIF .

* no deleted/inactive equipments
  LOOP AT i_equi.
    CONCATENATE 'IE' i_equi-equnr INTO gv_objnr.

    CALL FUNCTION 'STATUS_CHECK'
      EXPORTING
        objnr             = gv_objnr
        status            = c_i0320
      EXCEPTIONS
        object_not_found  = 1
        status_not_active = 2
        OTHERS            = 3.
    IF sy-subrc EQ 0.
      DELETE i_equi.
    ELSE.
      CALL FUNCTION 'STATUS_CHECK'
        EXPORTING
          objnr             = gv_objnr
          status            = c_i0076
        EXCEPTIONS
          object_not_found  = 1
          status_not_active = 2
          OTHERS            = 3.
      IF sy-subrc EQ 0.
        DELETE i_equi.
      ENDIF.
    ENDIF.
  ENDLOOP.
* no divested equipments
  LOOP AT i_equi.
    CLEAR gv_deakt.
    SELECT SINGLE deakt INTO gv_deakt FROM anla
       WHERE bukrs EQ i_equi-bukrs
         AND anln1 EQ i_equi-anlnr
         AND anln2 EQ i_equi-anlun.
    IF NOT gv_deakt IS INITIAL.
      DELETE i_equi.
    ENDIF.
  ENDLOOP.

** Get related info and populate data into Internal Table
  LOOP AT i_equi.
* rental depot
    SELECT SINGLE b_werk INTO i_equi-werks FROM eqbs
      WHERE equnr = i_equi-equnr.
* model
    LOOP AT lt_vkorg.
      SELECT SINGLE pmatn INTO i_equi-typbz
         FROM mvke WHERE matnr = i_equi-matnr
                     AND vkorg = lt_vkorg-vkorg
                     AND vtweg IN s_vtweg.
      IF sy-subrc = 0.
        EXIT.
      ENDIF.
    ENDLOOP.
* bulkitem
    i_equi-bulkitem = c_n.
* status
    CLEAR gv_status.
    PERFORM equipment_status.
    i_equi-status = gv_status.
* company currency
    SELECT SINGLE waers INTO i_equi-waers FROM t001
      WHERE bukrs = i_equi-bukrs.
* original equipment cost(acquisition value)
    CLEAR gv_answl.
*    SELECT SINGLE answl INTO gv_answl FROM anlc
    SELECT SINGLE kansw INTO gv_answl FROM anlc
      WHERE bukrs = i_equi-bukrs
        AND anln1 = i_equi-anlnr
        AND anln2 = i_equi-anlun
        AND gjahr = gv_year
* begin of change MOD-001
*        AND afabe = '30'
        AND afabe = '01'
* end of change MOD-001
        AND zujhr EQ 0
        AND zucod EQ 0.
    i_equi-acquicost = gv_answl.
* book value (= acquisition value - depreciation)
* depreciation (negative value in anlc)
* depreciation
    CLEAR: gv_nafaz, gv_netbook.
*    SELECT SUM( nafaz ) INTO gv_nafaz FROM anlp
*       WHERE bukrs = i_equi-bukrs
*         AND gjahr = gv_year
*         AND anln1 = i_equi-anlnr
*         AND anln2 = i_equi-anlun
*         AND afaber = '30'
*         AND zujhr EQ 0
*         AND zucod EQ 0.
    SELECT SINGLE knafa INTO gv_nafaz FROM anlc
       WHERE bukrs = i_equi-bukrs
         AND anln1 = i_equi-anlnr
         AND anln2 = i_equi-anlun
         AND gjahr = gv_year
* begin of change MOD-001
*        AND afabe = '30'
         AND afabe = '01'
* end of change MOD-001
         AND zujhr EQ 0
         AND zucod EQ 0.
    gv_netbook = gv_answl + gv_nafaz.
    i_equi-netbook = gv_netbook.
* life to date labour
    REFRESH : lt_afih.
    CLEAR : lt_afih,
            gv_ltdlabor.
    SELECT aufnr INTO CORRESPONDING FIELDS OF TABLE lt_afih
       FROM afih WHERE equnr = i_equi-equnr.
    LOOP AT lt_afih.
      CONCATENATE 'OR' lt_afih-aufnr INTO gv_objnr.
      SELECT wrt08 INTO gv_wrt08 FROM pmco
         WHERE objnr = gv_objnr
           AND cocur = i_equi-waers
           AND beltp = '1'
           AND wrttp = '04'
           AND acpos = 'Z03'.
        gv_ltdlabor = gv_ltdlabor + gv_wrt08.
      ENDSELECT.
    ENDLOOP.
    i_equi-ltdlabor = gv_ltdlabor.
* life to date parts
    CLEAR : gv_ltdparts.
    LOOP AT lt_afih.
      CONCATENATE 'OR' lt_afih-aufnr INTO gv_objnr.
      SELECT wrt08 INTO gv_wrt08 FROM pmco
         WHERE objnr = gv_objnr
           AND cocur = i_equi-waers
           AND beltp = '1'
           AND wrttp = '04'
           AND acpos = 'Z04'.
        gv_ltdparts = gv_ltdparts + gv_wrt08.
      ENDSELECT.
    ENDLOOP.
    i_equi-ltdparts = gv_ltdparts.
* life to date revenue
* rpure rent evenue on equipment
* system date
    i_equi-systdate = sy-datum.
* cost of service
* meter reading
**  get running hours
*   convert ZAM_RHRSTOTAL into internal value
    CLEAR g_fm_atinn .
    CALL FUNCTION 'CONVERSION_EXIT_ATINN_INPUT'
      EXPORTING
        input  = c_run_hours
      IMPORTING
        output = g_fm_atinn.

*   get measurement point
    IF sy-subrc = 0.
      CLEAR g_point.
      SELECT point FROM imptt INTO g_point
         WHERE mpobj EQ i_equi-objnr
           AND atinn EQ g_fm_atinn.
      ENDSELECT.

      IF sy-subrc = 0.
*       read last counter
        CALL FUNCTION 'MEASUREM_POINT_LAST_VALUE'
          EXPORTING
            i_point           = g_point
          IMPORTING
            e_wa_point        = i_wa_point
            e_wa_value        = i_wa_value
          EXCEPTIONS
            pointer_not_found = 01.
*
        IF sy-subrc <> 0.
          WRITE:/ text-e04, g_point.    "No value found for measr.point
        ELSE.
*         convert value into display format
          PERFORM unit_conversion USING i_wa_point-msehi
                                        i_wa_value-readg
                                        i_equi-meter
                                        i_wa_point-decim
                                        i_wa_point-expon.
        ENDIF.
      ENDIF.
    ENDIF.

* internal order number
    SELECT SINGLE caufn INTO i_equi-intorder FROM anlz
           WHERE bukrs = i_equi-bukrs
             AND anln1 = i_equi-anlnr
             AND anln2 = i_equi-anlun
             AND bdatu = c_99991231.

    MODIFY i_equi.

  ENDLOOP.

ENDFORM.                    " Get_Detail
*---------------------------------------------------------------------
*  FORM UNIT_CONVERSION
*---------------------------------------------------------------------
FORM unit_conversion USING p_einheit
                           p_fltp_wert
                           p_char_wert
                           p_decimal
                           p_exponent.

  CLEAR p_char_wert.
  CHECK NOT ( p_fltp_wert IS INITIAL ).

  CALL FUNCTION 'FLTP_CHAR_CONVERSION_FROM_SI'
    EXPORTING
      char_unit       = p_einheit
      decimals        = p_decimal
      exponent        = p_exponent
      fltp_value_si   = p_fltp_wert
      indicator_value = c_x
      masc_symbol     = ' '
    IMPORTING
      char_value      = p_char_wert.

ENDFORM.                    "UNIT_CONVERSION
*&---------------------------------------------------------------------*
*&      Form  Create_delta
*&---------------------------------------------------------------------*
*       Create internal table with creations/updates
*----------------------------------------------------------------------*
FORM create_delta.

  LOOP AT i_equi.

    READ TABLE i_prev WITH KEY equnr = i_equi-equnr
                    BINARY SEARCH.

    IF sy-subrc = 0.
***** MUST BE UPDATED when structure yse_im_equi is extended *
      IF i_equi(237) <> i_prev(237).
************************************************************************
        MOVE-CORRESPONDING i_equi TO i_equi_delta.
        APPEND i_equi_delta.
        CLEAR i_equi_delta.
      ENDIF.
    ELSE.
      MOVE-CORRESPONDING i_equi TO i_equi_delta.
      APPEND i_equi_delta.
      CLEAR i_equi_delta.
    ENDIF.

  ENDLOOP.

ENDFORM.                    " Create_delta
*&---------------------------------------------------------------------*
*&      Form  Get_previous_file
*&---------------------------------------------------------------------*
*       Get file from previous run
*----------------------------------------------------------------------*
FORM get_previous_file USING p_bukrs
                    CHANGING p_retcd.

* prepare filename of previous run
  CLEAR g_ersda.
  SELECT ersda INTO g_ersda
      FROM yse_infomanager WHERE bukrs = p_bukrs
                             AND object = c_object.
  ENDSELECT.

  IF sy-subrc <> 0.
    WRITE: / text-e02, p_bukrs, c_object.  "No filename of previous run
*                available in custom table yse_infomanager
    p_retcd = 4.
    EXIT.
  ENDIF.

  CONCATENATE p_bukrs c_object g_ersda
                      INTO g_pfile SEPARATED BY c_underscore.
  REPLACE 'xxx' IN g_directory2 WITH p_logsys(3).
  CONCATENATE g_directory2 g_pfile INTO g_pfile.

* FILE READ FROM APPLICATION SERVER
  PERFORM get_from_appl TABLES  i_prev
                        USING   g_pfile
                                p_retcd.

ENDFORM.                    " Get_previous_file
*&---------------------------------------------------------------------*
*&      Form  Get_from_appl
*&---------------------------------------------------------------------*
*       Get the file from application server into internal table
*----------------------------------------------------------------------*
FORM get_from_appl TABLES i_infile STRUCTURE i_prev
                   USING p_infile p_subrc.

  OPEN DATASET p_infile FOR INPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, p_infile.
    p_subrc = 4.
    EXIT.
  ENDIF.

  REFRESH i_infile.

  DO.
    READ DATASET p_infile INTO i_infile.
    IF sy-subrc <> 0.
      EXIT.
    ENDIF.
    APPEND i_infile.
  ENDDO.
  CLOSE DATASET p_infile.

ENDFORM.                    " GET_FROM_APPL
*&---------------------------------------------------------------------*
*&      Form  Write_outputfile
*&---------------------------------------------------------------------*
*       Write outputfile
*----------------------------------------------------------------------*
FORM write_outputfile.

  OPEN DATASET g_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, g_ofile.
*   message e011(YAM_DC).                      "Open dataset failed
    g_retcd = 4.
    EXIT.
  ENDIF.

  SORT i_equi_delta BY equnr.

  REFRESH it_outxls.
* Make the output table ; delimited
  CALL FUNCTION 'SAP_CONVERT_TO_CSV_FORMAT'
    TABLES
      i_tab_sap_data       = i_equi_delta
    CHANGING
      i_tab_converted_data = it_outxls
    EXCEPTIONS
      conversion_failed    = 1
      OTHERS               = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  CLEAR wa_outxls.
* Process lines
  LOOP AT it_outxls INTO wa_outxls.
    REPLACE ALL OCCURRENCES OF ';' IN wa_outxls WITH c_tab.
    TRANSFER wa_outxls TO g_ofile.
  ENDLOOP.

ENDFORM.                    " Write_outputfile
*&---------------------------------------------------------------------*
*&      Form  Update_custom_table
*&---------------------------------------------------------------------*
*       Update custom table YSE_INFOMANAGER
*----------------------------------------------------------------------*
FORM update_custom_table USING p_swerk.

  wa_yse_infomanager-mandt = sy-mandt.
  wa_yse_infomanager-bukrs = p_bukrs.
  wa_yse_infomanager-object = c_object.
  wa_yse_infomanager-ersda = g_ersda.

  INSERT yse_infomanager FROM wa_yse_infomanager.

ENDFORM.                    " Update_custom_table
*&---------------------------------------------------------------------*
*&      Form  Delete_old_files
*&---------------------------------------------------------------------*
*       Delete files from former runs on application server
*----------------------------------------------------------------------*
FORM delete_old_files.

  LOOP AT i_delfiles.

    CONCATENATE p_bukrs c_object i_delfiles-ersda
                         INTO g_dfile SEPARATED BY c_underscore.

    MOVE g_directory TO g_dir_name.
    REPLACE 'xxx' IN g_dir_name WITH p_logsys(3).
    TRANSLATE g_dir_name(10) TO LOWER CASE.
    TRANSLATE g_dir_name+17(8) TO LOWER CASE.

* change MJ 20090602
*    CALL FUNCTION 'EPS_DELETE_FILE'
    CALL FUNCTION 'YSE_EPS_DELETE_FILE'
      EXPORTING
        file_name              = g_dfile
        dir_name               = g_dir_name
      EXCEPTIONS
        invalid_eps_subdir
        sapgparam_failed
        build_directory_failed
        no_authorization
        build_path_failed
        delete_failed.

    IF sy-subrc <> 0.
      WRITE: / text-e05, g_dfile.      "Could not delete file
    ENDIF.

  ENDLOOP.

ENDFORM.                    "delete_old_files
*&---------------------------------------------------------------------*
*&      Form  Delete_old_table_entries
*&---------------------------------------------------------------------*
*       Delete entries from former runs in custom table YSE_INFOMANAGER
*----------------------------------------------------------------------*
FORM delete_old_table_entries USING p_bukrs.

  g_date = sy-datum - 7.
  CONCATENATE g_date c_0000 INTO g_ersda.

  REFRESH i_delfiles.

  SELECT * FROM yse_infomanager
      WHERE bukrs EQ p_bukrs
        AND object EQ c_object
        AND ersda LT g_ersda.
    MOVE yse_infomanager TO i_delfiles.
    APPEND i_delfiles.
    CLEAR i_delfiles.
  ENDSELECT.

  IF sy-subrc = 0.
    DELETE FROM yse_infomanager WHERE bukrs EQ p_bukrs
                                  AND object EQ c_object
                                  AND ersda LT g_ersda.
    IF sy-subrc <> 0.
      WRITE: / text-e06, p_bukrs.

    ENDIF.
  ENDIF.

ENDFORM.                    "delete_old_table_entries
*&---------------------------------------------------------------------*
*&      Form  EQUIPMENT_STATUS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM equipment_status .

  REFRESH it_sme.
  CLEAR it_sme.
* Get all yse_rent_sme records related to chosen equipment
  SELECT *
      FROM yse_rent_sme
      INTO TABLE it_sme
     WHERE   matnr   EQ i_equi-matnr
       AND   zzequnr EQ i_equi-equnr
       AND   zzsernr EQ i_equi-sernr
       AND   vtweg IN s_vtweg
       AND   angdt LE gv_datum
       AND   bnddt GE gv_datum.

* on rent ?
  LOOP AT it_sme WHERE zzequnr EQ i_equi-equnr
                   AND auart   IN gr_auart_contract.
    gv_status = 'O'.
    EXIT.
  ENDLOOP.

* in service ?
  IF gv_status = ' '.
    LOOP AT it_sme WHERE zzequnr EQ i_equi-equnr
                     AND auart   IN gr_auart_service.
      gv_status = 'N'.
      EXIT.
    ENDLOOP.
  ENDIF.

* in transport
  IF gv_status = ' '.
    LOOP AT it_sme WHERE zzequnr EQ i_equi-equnr
                     AND auart   IN gr_auart_delivery.
      gv_status = 'I'.
      EXIT.
    ENDLOOP.
  ENDIF.

* available
  IF gv_status = ' '.
    gv_status = 'A'.
  ENDIF.

ENDFORM.                    " EQUIPMENT_STATUS
*&---------------------------------------------------------------------*
*&      Form  FILL_RANGES
*&---------------------------------------------------------------------*
FORM fill_ranges.

* Fill global range with document types which represent rental contracts
  gr_auart_contract-sign = 'I'.
  gr_auart_contract-option = 'EQ'.
  gr_auart_contract-low = gc_auart_zqp.
  APPEND gr_auart_contract.
  gr_auart_contract-low = gc_auart_zqp2.
  APPEND gr_auart_contract.
  gr_auart_contract-low = gc_auart_zrib.
  APPEND gr_auart_contract.

* Fill global range with document types which represent deliveries
  gr_auart_delivery-sign = 'I'.
  gr_auart_delivery-option = 'EQ'.
  gr_auart_delivery-low = gc_auart_lr.
  APPEND gr_auart_delivery.
  gr_auart_delivery-low = gc_auart_zlf.
  APPEND gr_auart_delivery.

* Fill global range with document types which represent service
  gr_auart_service-sign = 'I'.
  gr_auart_service-option = 'EQ'.
  gr_auart_service-low = gc_auart_zsm1.
  APPEND gr_auart_service.
  gr_auart_service-low = gc_auart_zsm2.
  APPEND gr_auart_service.
  gr_auart_service-low = gc_auart_zam1.
  APPEND gr_auart_service.
  gr_auart_service-low = gc_auart_zam2.
  APPEND gr_auart_service.

ENDFORM.                    " FILL_RANGES
*&---------------------------------------------------------------------*
*&      Form  OVERWRITE_FILE_TO_COMPARE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM overwrite_file_to_compare .

* delete old file
  CONCATENATE p_bukrs c_object INTO g_dfile SEPARATED BY c_underscore.

  MOVE g_directory2 TO g_dir_name.
  REPLACE 'xxx' IN g_dir_name WITH p_logsys(3).
  TRANSLATE g_dir_name(10) TO LOWER CASE.
  TRANSLATE g_dir_name+17(8) TO LOWER CASE.

* change MJ 20090602
*    CALL FUNCTION 'EPS_DELETE_FILE'
  CALL FUNCTION 'YSE_EPS_DELETE_FILE'
    EXPORTING
      file_name              = g_dfile
      dir_name               = g_dir_name
    EXCEPTIONS
      invalid_eps_subdir
      sapgparam_failed
      build_directory_failed
      no_authorization
      build_path_failed
      delete_failed.

  IF sy-subrc <> 0.
    WRITE: / text-e05, g_dfile.      "Could not delete file
  ENDIF.

* create new file
  CLEAR g_retcd.

  CONCATENATE p_bukrs c_object INTO g_ofile SEPARATED BY c_underscore.
  REPLACE 'xxx' IN g_directory2 WITH p_logsys(3).
  CONCATENATE g_directory2 g_ofile INTO g_ofile.

* write outputfile
  OPEN DATASET g_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, g_ofile.
*   message e011(YAM_DC).                      "Open dataset failed
    g_retcd = 4.
    EXIT.
  ENDIF.

  SORT i_equi BY equnr.

  LOOP AT i_equi.
***** MUST BE UPDATED when structure of segment YSE_IM_EQUI is extended
    TRANSFER i_equi(237) TO g_ofile.
************************************************************************
  ENDLOOP.
ENDFORM.                    " OVERWRITE_FILE_TO_COMPARE

*Text symbol text£º
*001:Selection Screen Input
*E01:No data available for the selected company :
*E02:No previous run entry in table YSE_INFOMANAGER for company :
*E03:Open dataset failed for :
*E04:No counter found for measurement point :
*E05:Could not delete file :
*E06:Could not delete entrie(s) in table YSE_INFOMANAGER for :
*I02:Job finished

*I03:No delta found
*Selection text£º
*P_BUKRS:        Company
*P_INIT:        Initial Load
*S_EQUNR:D       .
*S_VTWEG:        Distribution Channel
