*&---------------------------------------------------------------------*
*& Report  YSE_ACT_EQUIPMENT_TEXT_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Equipment Texts extraction                                   *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 17.10.2012
*  Change Request Number :
*  Transport request Nr. : CD1K973590
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 19.03.2014 | Jules Smets     | CD1K980489    | CR1794 (Act)*
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_act_equipment_text_extr.

TABLES: equi,
        equz,
        iloa,
        jest,
        stxh,
        ttxid.

TYPES: BEGIN OF ty_equi,
         equnr      TYPE equnr,
         objnr      TYPE j_objnr,
      END OF ty_equi.

TYPES: BEGIN OF ty_dlfl,
         objnr      TYPE j_objnr,
      END OF ty_dlfl.

TYPES: BEGIN OF ty_stxh,
         tdname     TYPE tdobname,
         tdid       TYPE tdid,
         tdspras    TYPE spras,
       END OF ty_stxh.

DATA: gt_equi  TYPE TABLE OF ty_equi
                    WITH HEADER LINE.

DATA: gt_dlfl  TYPE HASHED TABLE OF ty_dlfl
                    WITH UNIQUE KEY objnr
                    WITH HEADER LINE,
      gt_dlfli TYPE TABLE OF ty_dlfl
                    WITH HEADER LINE.

DATA: gt_stxh  TYPE SORTED TABLE OF ty_stxh
                    WITH NON-UNIQUE KEY tdname
                    WITH HEADER LINE.

DATA: gt_ttxid TYPE STANDARD TABLE OF ttxid
                    WITH HEADER LINE.

DATA: gt_lines TYPE TABLE OF tline
                    WITH HEADER LINE.

DATA: BEGIN OF gt_out  OCCURS 0,
        equnr       TYPE equnr,
        tab01(1)    TYPE c,
        tdid        TYPE tdid,
        tab02(1)    TYPE c,
        tdspras     TYPE spras,
        tab03(1)    TYPE c,
        tdline      TYPE tdline,
        tab04(1)    TYPE c,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        equnr       TYPE fieldname,
        tab01(1)    TYPE c,
        tdid        TYPE fieldname,
        tab02(1)    TYPE c,
        tdspras     TYPE fieldname,
        tab03(1)    TYPE c,
        tdline      TYPE fieldname,
        tab04(1)    TYPE c,
      END OF gt_outh.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_error         TYPE xfeld.

CONSTANTS: gc_stat_dl  TYPE j_status  VALUE 'I0076',
           gc_stat_in  TYPE j_status  VALUE 'I0320',
           gc_stat_scr TYPE j_status  VALUE 'E0005'.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
PARAMETERS: p_bukrs  LIKE iloa-bukrs  OBLIGATORY  MEMORY ID buk.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_equnr  FOR equi-equnr,
                s_tdid   FOR ttxid-tdid.
SELECTION-SCREEN  END OF BLOCK input.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Text-ID selection
  IF s_tdid[] IS INITIAL.
    PERFORM tdid_selection.
  ENDIF.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_equi[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No equipments selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  TDID_SELECTION
*&---------------------------------------------------------------------*
*       Text-ID selection
*----------------------------------------------------------------------*
FORM tdid_selection .

* Text id's for equipment
  SELECT * INTO TABLE gt_ttxid
           FROM ttxid
           WHERE tdobject = 'EQUI'.

  LOOP AT gt_ttxid.
    s_tdid-low    = gt_ttxid-tdid.
    s_tdid-sign   = 'I'.
    s_tdid-option = 'EQ'.
    APPEND s_tdid.
  ENDLOOP.

  FREE gt_ttxid.

ENDFORM.                    " TDID_SELECTION

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Equipment time segment
  SELECT e~equnr e~objnr
         INTO TABLE gt_equi
         FROM equi AS e
         INNER JOIN equz AS z
                    ON z~equnr = e~equnr
         INNER JOIN iloa AS l
                    ON l~iloan = z~iloan
         WHERE e~equnr IN s_equnr
           AND z~datbi =  '99991231'
           AND l~bukrs =  p_bukrs.
  CHECK NOT gt_equi[] IS INITIAL.

*** MOD-001 * begin ***
** Check for deleted/scrapped equipments
*  SELECT objnr
*         INTO TABLE gt_dlfli
*         FROM jest
*         FOR ALL ENTRIES IN gt_equi
*         WHERE objnr = gt_equi-objnr
*           AND ( stat = gc_stat_dl   OR
*                 stat = gc_stat_in   OR
*                 stat = gc_stat_scr )
*           AND inact = ' '.
*  SORT gt_dlfli BY objnr.
*  DELETE ADJACENT DUPLICATES FROM gt_dlfli
*                                  COMPARING objnr.
*  gt_dlfl[] = gt_dlfli[].
*  FREE gt_dlfli.
*
*  LOOP AT gt_equi.
*    READ TABLE gt_dlfl WITH TABLE KEY objnr = gt_equi-objnr.
*    IF sy-subrc = 0.
*      DELETE gt_equi.
*    ENDIF.
*  ENDLOOP.
*  FREE gt_dlfl.
*  CHECK NOT gt_equi[] IS INITIAL.
*** MOD-001 * end ***

* Equipment texts
  LOOP AT gt_equi.
    SELECT tdname tdid tdspras
           APPENDING TABLE gt_stxh
           FROM stxh
           WHERE tdobject = 'EQUI'
             AND tdname   = gt_equi-equnr
             AND tdid    IN s_tdid.
  ENDLOOP.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_equi.

*   Texts
    LOOP AT gt_stxh WHERE tdname = gt_equi-equnr.

      CLEAR: gt_out.
      MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03,
                   gt_out-tab04.

      gt_out-equnr   = gt_equi-equnr.
      gt_out-tdid    = gt_stxh-tdid.
      gt_out-tdspras = gt_stxh-tdspras.

      CALL FUNCTION 'READ_TEXT'
        EXPORTING
*          CLIENT                  = SY-MANDT
          id                      = gt_stxh-tdid
          language                = gt_stxh-tdspras
          name                    = gt_stxh-tdname
          object                  = 'EQUI'
*          ARCHIVE_HANDLE          = 0
*          LOCAL_CAT               = ' '
*        IMPORTING
*          HEADER                  =
        TABLES
          lines                   = gt_lines
        EXCEPTIONS
          id                      = 1
          language                = 2
          name                    = 3
          not_found               = 4
          object                  = 5
          reference_check         = 6
          wrong_access_to_archive = 7
          OTHERS                  = 8
                .
      IF sy-subrc <> 0.
*        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
      ELSE.
        LOOP AT gt_lines.
          gt_out-tdline = gt_lines-tdline.
          APPEND gt_out.
        ENDLOOP.

      ENDIF.

    ENDLOOP.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

  CONCATENATE 'EQUIPMENT' 'TEXTS' p_bukrs
             INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM fill_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  FILL_HEADER
*&---------------------------------------------------------------------*
*       Fill header
*----------------------------------------------------------------------*
FORM fill_header .

  CLEAR: gt_outh.
  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03,
               gt_outh-tab04.

  gt_outh-equnr       = 'EQUNR'.
  gt_outh-tdid        = 'TDID'.
  gt_outh-tdspras     = 'SPRAS'.
  gt_outh-tdline      = 'TDLINE'.
  APPEND gt_outh.

  gt_outh-equnr       = 'Equipment'.
  gt_outh-tdid        = 'Text ID'.
  gt_outh-tdspras     = 'Lang.'.
  gt_outh-tdline      = 'Text Line'.
  APPEND gt_outh.

ENDFORM.                    " FILL_HEADER

*Text symbol text£º
*E01:No equipments selected
*E02:Could not open output file
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_BUKRS:D       .
*S_EQUNR:D       .
*S_TDID:D       .
