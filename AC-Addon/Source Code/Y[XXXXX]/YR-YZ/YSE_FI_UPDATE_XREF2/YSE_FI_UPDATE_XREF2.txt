*&---------------------------------------------------------------------*
*& Report  YSE_FI_UPDATE_XREF2
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

REPORT  yse_fi_update_xref2.
TABLES: yse_xref2_upd.
*----------------------------------------------------------------------*
*       TYPE-POOLS
*----------------------------------------------------------------------*
TYPE-POOLS: slis,tpit,truxs.

*----------------------------------------------------------------------*
*       Internal table
*----------------------------------------------------------------------*
FIELD-SYMBOLS: <fs_tab> TYPE STANDARD TABLE.
DATA: gt_xref2_upd  TYPE STANDARD TABLE OF yse_xref2_upd,
      gt_error      TYPE STANDARD TABLE OF yse_xref2_upd,
      gv_line       TYPE char10.

*----------------------------------------------------------------------*
*       CONSTANTS
*----------------------------------------------------------------------*
CONSTANTS: gc_struname_xref2 TYPE struname VALUE 'YSE_XREF2_UPDATE',
           gc_bukrs_cona     TYPE bseg-bukrs VALUE 'CONA'.

*----------------------------------------------------------------------*
*       SELECTION-SCREEN
*----------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-b01.
PARAMETERS: rb_upl RADIOBUTTON GROUP g1 USER-COMMAND upl,
            rb_upd RADIOBUTTON GROUP g1.
SELECTION-SCREEN END OF BLOCK b1.

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-b02.
SELECT-OPTIONS:
  s_ernam FOR yse_xref2_upd-ernam DEFAULT sy-uname MODIF ID 01,
  s_erdat FOR yse_xref2_upd-erdat MODIF ID 01.

PARAMETERS:
  p_file  TYPE rlgrap-filename
          DEFAULT 'C:\Users\XREF2_Update.xlsx' MODIF ID 00.

SELECTION-SCREEN END OF BLOCK b2.

*----------------------------------------------------------------------*
*       AT SELECTION-SCREEN OUTPUT
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_file .
  CALL FUNCTION 'F4_FILENAME'
    IMPORTING
      file_name = p_file.

************************************************************************
*  AT SELECTION-SCREEN OUTPUT
************************************************************************
AT SELECTION-SCREEN OUTPUT.
* SELECTION-SCREEN SET
  PERFORM f_screen_output.

START-OF-SELECTION.

  CHECK p_file IS NOT INITIAL.

  IF rb_upl IS NOT INITIAL.
    PERFORM upload_file.
  ELSEIF rb_upd IS NOT INITIAL.
    PERFORM  update_xref2.
  ENDIF.

END-OF-SELECTION.
  CHECK gt_error IS NOT INITIAL.
  PERFORM log_out.

*&---------------------------------------------------------------------*
*&      Form  F_SCREEN_OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM f_screen_output .
  LOOP AT SCREEN.
    IF rb_upd IS NOT INITIAL.
      IF screen-group1 = '00'.
        screen-input = '0'.
        screen-active = '0'.
        MODIFY SCREEN.
      ENDIF.
    ELSE.
      IF screen-group1 = '01'.
        screen-input = '0'.
        screen-active = '0'.
        MODIFY SCREEN.
      ENDIF.
    ENDIF.
  ENDLOOP.
ENDFORM.                    " F_SCREEN_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  upload_file
*&---------------------------------------------------------------------*
*       Upload file
*----------------------------------------------------------------------*
FORM upload_file.

  DATA: filename  TYPE rlgrap-filename,
        lt_raw_data   TYPE truxs_t_text_data,
        ls_xref2_update TYPE yse_xref2_update,
        ls_xref2_upd  TYPE yse_xref2_upd,
        ls_file TYPE REF TO data,
        lr_wa     TYPE REF TO data,
        lr_itab   TYPE REF TO data,
        l_index   TYPE i.
  FIELD-SYMBOLS:  <fs_wa> TYPE ANY,
                  <fs_field> TYPE ANY.

  CREATE DATA ls_file TYPE (gc_struname_xref2).
  CREATE DATA lr_itab TYPE TABLE OF (gc_struname_xref2) INITIAL SIZE 1.
  ASSIGN lr_wa->* TO <fs_wa>.
  ASSIGN lr_itab->* TO <fs_tab>.

  filename = p_file.
* Upload the Material data from the excel file
  CALL FUNCTION 'TEXT_CONVERT_XLS_TO_SAP'
    EXPORTING
      i_line_header        = 'X'
      i_tab_raw_data       = lt_raw_data
      i_filename           = filename
    TABLES
      i_tab_converted_data = <fs_tab>
    EXCEPTIONS
      conversion_failed    = 1
      OTHERS               = 2.
  IF sy-subrc <> 0.
    IF sy-msgty = 'E'.
      MESSAGE ID sy-msgid TYPE 'S' NUMBER sy-msgno DISPLAY LIKE 'E'
          WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      LEAVE LIST-PROCESSING.
    ENDIF.
  ENDIF.

  CHECK <fs_tab> IS NOT INITIAL.
  LOOP AT <fs_tab> INTO ls_xref2_update.
    MOVE-CORRESPONDING ls_xref2_update TO ls_xref2_upd.
    ls_xref2_upd-message = 'No Process'(t01).
    ls_xref2_upd-ernam  = sy-uname.
    ls_xref2_upd-erdat  = sy-datum.
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        input         = ls_xref2_upd-belnr
      IMPORTING
        OUTPUT        = ls_xref2_upd-belnr
              .

    IF ls_xref2_upd-bukrs = gc_bukrs_cona.
      APPEND ls_xref2_upd TO gt_xref2_upd.
    ENDIF.
  ENDLOOP.

  IF gt_xref2_upd IS NOT INITIAL.
    MODIFY yse_xref2_upd FROM TABLE gt_xref2_upd.
    IF sy-subrc <> 0.
      MESSAGE s001(00)
        WITH 'Uploaded successfully in table YSE_XREF2_UPD'(t04).
    ENDIF.
  ENDIF.
ENDFORM.                    " upload_file
*&---------------------------------------------------------------------*
*&      Form  UPDATE_XREF2
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM update_xref2 .
  TYPES: BEGIN OF ty_bseg,
            bukrs TYPE bseg-bukrs,
            belnr TYPE bseg-belnr,
            gjahr TYPE bseg-gjahr,
            buzei TYPE bseg-buzei,
            buzid TYPE bseg-buzid,
            koart TYPE bseg-koart,
            xref2 TYPE bseg-xref2,
         END OF ty_bseg.

  DATA: ls_xref2_upd  TYPE yse_xref2_upd,
        ls_bseg_chg   TYPE bseg,
        ls_buztab     TYPE tpit_buztab,
        ls_fldtab     TYPE tpit_fname,
        lv_dynpro_01(4) TYPE n,
        lt_errdoc     TYPE tpit_t_errdoc,
        lt_buztab     TYPE tpit_t_buztab,
        lt_fldtab     TYPE tpit_t_fname,
        lt_bseg       TYPE STANDARD TABLE OF ty_bseg,
        ls_bseg       TYPE ty_bseg,
        lt_det_xref   TYPE STANDARD TABLE OF yse_xref2_upd.
  DATA: lt_accchg TYPE STANDARD TABLE OF accchg,
        ls_accchg TYPE accchg.

  REFRESH: gt_xref2_upd.
  SELECT *
    FROM yse_xref2_upd
    INTO TABLE gt_xref2_upd
    WHERE ernam IN s_ernam
      AND erdat IN s_erdat
      AND message <> space.
  IF sy-subrc <> 0.
    MESSAGE s001(00) DISPLAY LIKE 'E' WITH 'No process data'(t07).
    LEAVE LIST-PROCESSING.
  ELSE.
    DESCRIBE TABLE gt_xref2_upd LINES gv_line.
  ENDIF.
  SORT gt_xref2_upd.
  SELECT    bukrs
            belnr
            gjahr
            buzei
            buzid
            koart
            xref2
    FROM bseg
    INTO TABLE lt_bseg
    FOR ALL ENTRIES IN gt_xref2_upd
    WHERE bukrs = gt_xref2_upd-bukrs
      AND belnr = gt_xref2_upd-belnr
      AND gjahr = gt_xref2_upd-gjahr
      AND buzei = gt_xref2_upd-buzei.

  SORT lt_bseg.
  LOOP AT gt_xref2_upd INTO ls_xref2_upd.
    CLEAR: lv_dynpro_01,ls_bseg.
    IF ls_xref2_upd-bukrs <> gc_bukrs_cona.
      CONCATENATE 'Company code'(t65)
                  ls_xref2_upd-bukrs
                  'is not allowed for mass updating'(t66)
              INTO ls_xref2_upd-message
              SEPARATED BY space.
*      ls_xref2_upd-message = 'Company code'(t15).
      ls_xref2_upd-aenam   = sy-uname.
      ls_xref2_upd-aedat   = sy-datum.
      APPEND ls_xref2_upd TO lt_det_xref.
      CONTINUE.
    ENDIF.
    READ TABLE lt_bseg INTO ls_bseg
      WITH KEY bukrs = ls_xref2_upd-bukrs
               belnr = ls_xref2_upd-belnr
               gjahr = ls_xref2_upd-gjahr
               buzei = ls_xref2_upd-buzei.
    IF sy-subrc <> 0.
      ls_xref2_upd-message = 'Document Item not exist!'(t15).
      ls_xref2_upd-aenam   = sy-uname.
      ls_xref2_upd-aedat   = sy-datum.
      APPEND ls_xref2_upd TO lt_det_xref.
    ELSE.
      ls_xref2_upd-xref2_old = ls_bseg-xref2.
      ls_xref2_upd-aenam   = sy-uname.
      ls_xref2_upd-aedat   = sy-datum.
      IF ls_xref2_upd-xref2_new = ls_bseg-xref2.
        CLEAR ls_xref2_upd-message.
        APPEND ls_xref2_upd TO lt_det_xref.
      ELSE.
        IF ( ls_bseg-koart = 'D' OR ls_bseg-koart = 'K' ).
          CLEAR: ls_accchg.
          REFRESH: lt_accchg.
          ls_accchg-fdname  = 'XREF2'.
          ls_accchg-oldval  = ls_xref2_upd-xref2_old.
          ls_accchg-newval  = ls_xref2_upd-xref2_new.
          APPEND ls_accchg TO lt_accchg.

          CALL FUNCTION 'FI_DOCUMENT_CHANGE'
           EXPORTING
*           I_AWTYP                    =
*           I_AWREF                    =
*           I_AWORG                    = ' '
*           I_AWSYS                    = ' '
*           I_KUNNR                    = ' '
*           I_LIFNR                    = ' '
*           I_OBZEI                    = ' '
           i_buzei                    = ls_xref2_upd-buzei
*           I_BSEGC                    =
*           X_LOCK                     = 'X'
            i_bukrs                    =  ls_xref2_upd-bukrs
            i_belnr                    =  ls_xref2_upd-belnr
            i_gjahr                    =  ls_xref2_upd-gjahr
          TABLES
            t_accchg                   = lt_accchg
          EXCEPTIONS
            no_reference               = 1
            no_document                = 2
            many_documents             = 3
            wrong_input                = 4
            overwrite_creditcard       = 5
            OTHERS                     = 6
                  .
          IF sy-subrc <> 0.
            MESSAGE ID sy-msgid TYPE 'S' NUMBER sy-msgno
                    WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
            INTO ls_xref2_upd-message.
          ELSE.
            CLEAR ls_xref2_upd-message.
          ENDIF.
        ELSEIF ls_bseg-koart = 'S'.
          lv_dynpro_01 = '0300'.
          IF ls_bseg-buzid = 'T'.
            lv_dynpro_01 = '0312'.
          ENDIF.
          PERFORM update_xref2_gl USING ls_xref2_upd-bukrs
                                        ls_xref2_upd-belnr
                                        ls_xref2_upd-gjahr
                                        ls_xref2_upd-buzei
                                        ls_xref2_upd-xref2_new
                                        lv_dynpro_01
                                   CHANGING ls_xref2_upd-message.
        ENDIF.
        APPEND ls_xref2_upd TO lt_det_xref.
      ENDIF.
    ENDIF.
  ENDLOOP.

  IF lt_det_xref IS NOT INITIAL.
    MODIFY yse_xref2_upd FROM TABLE lt_det_xref.
    LOOP AT lt_det_xref INTO ls_xref2_upd WHERE message <> space.
      APPEND ls_xref2_upd TO  gt_error.
    ENDLOOP.
    IF sy-subrc = 0.
      MESSAGE s001(00)
        WITH 'Update done, please check further log in YSE_XREF2_UPD'(t11).
    ENDIF.
  ENDIF.

ENDFORM.                    " UPDATE_XREF2
*&---------------------------------------------------------------------*
*&      Form  UPDATE_XREF2_GL
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_LS_XREF2_UPD_BUKRS  text
*      -->P_LS_XREF2_UPD_BELNR  text
*      -->P_LS_XREF2_UPD_GJAHR  text
*      -->P_LS_XREF2_UPD_BUZEI  text
*      -->P_LS_XREF2_UPD_XREF2_NEW  text
*----------------------------------------------------------------------*
FORM update_xref2_gl  USING    p_bukrs
                               p_belnr
                               p_gjahr
                               p_buzei
                               p_xref2_new
                               p_dynpro01
                      CHANGING c_message.
  DATA: lt_bdcdata      TYPE STANDARD TABLE OF bdcdata,
        ls_bdcdata      TYPE bdcdata,
        ls_option       TYPE ctu_params,
        lt_bdcmsgcoll   TYPE STANDARD TABLE OF bdcmsgcoll,
        ls_bdcmsg       TYPE bdcmsgcoll.

  ls_option-dismode   = 'N'.
  ls_option-updmode   = 'S'.
  ls_option-racommit  = 'X'.
  ls_option-nobinpt   = 'X'.


  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    'SAPMF05L'  '0102'  'X'  ''  ''
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    ''  ''  ''  'BDC_CURSOR'  'RF05L-BELNR'
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    ''  ''  ''  'BDC_OKCODE'  '/00'
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    ''  ''  ''  'RF05L-BELNR'  p_belnr
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    ''  ''  ''  'RF05L-BUKRS'  p_bukrs
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    ''  ''  ''  'RF05L-GJAHR'  p_gjahr
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    ''  ''  ''  'RF05L-BUZEI'  p_buzei
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    'SAPMF05L'  p_dynpro01  'X'  ''  ''
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    ''  ''  ''  'BDC_OKCODE'  '=ZK'
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    'SAPMF05L'  '1300'  'X'  ''  ''
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    ''  ''  ''  'BDC_CURSOR'  'BSEG-XREF2'
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    ''  ''  ''  'BDC_OKCODE'  '=ENTR'
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    ''  ''  ''  'BSEG-XREF2'  p_xref2_new
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    'SAPMF05L'  p_dynpro01  'X'  ''  ''
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    ''  ''  ''  'BDC_OKCODE'  '=AE'
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CALL TRANSACTION 'FB09'
    USING lt_bdcdata
    OPTIONS FROM ls_option
    MESSAGES INTO lt_bdcmsgcoll.

  READ TABLE lt_bdcmsgcoll INTO ls_bdcmsg
        WITH KEY  msgtyp = 'S'
                  msgid  = 'F5'
                  msgnr  = '300'.
  IF sy-subrc <> 0.
    MESSAGE ID ls_bdcmsg-msgid TYPE 'S'
        NUMBER ls_bdcmsg-msgnr
        WITH ls_bdcmsg-msgv1 ls_bdcmsg-msgv2
             ls_bdcmsg-msgv3 ls_bdcmsg-msgv4
        INTO c_message.
  ELSE.
    CLEAR: c_message.
  ENDIF.


ENDFORM.                    " UPDATE_XREF2_GL
*&---------------------------------------------------------------------*
*&      Form  LOG_OUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM log_out .
  DATA: ls_err TYPE yse_xref2_upd.
  CONDENSE: gv_line.
  WRITE: 'Totally' , 9 gv_line , 'items are processed.'.
  WRITE: / 'Items that process with error as below'.
  ULINE.
  WRITE: /1 'Company Code'(t43),sy-vline,
         15 'Accounting Document Number'(t45),sy-vline,
         44 'Fiscal Year'(t47),sy-vline,
         58 'Line Item'(t49),70 sy-vline,
         72 'Message'(t51), 132 sy-vline.

  LOOP AT gt_error INTO ls_err.
    WRITE: / ls_err-bukrs UNDER text-t43, 14 sy-vline,
             ls_err-belnr UNDER text-t45, 42 sy-vline,
             ls_err-gjahr UNDER text-t47, 56 sy-vline,
             ls_err-buzei UNDER text-t49, 70 sy-vline,
             ls_err-message UNDER text-t51, 132 sy-vline.
  ENDLOOP.
ENDFORM.                    " LOG_OUT

*Text symbol text£º
*B01:Process option
*B02:Parametes
*T04:Uploaded successfully in table YSE_XREF2_UPD
*T07:No process data
*T11:Update done, please check further log in YSE_XREF2_UPD
*T43:Company Code
*T45:Accounting Document Number
*T47:Fiscal Year
*T49:Line Item
*T51:Message
*T65:Company code

*T66:is not allowed for mass updating
*Selection text£º
*P_FILE:D       .
*RB_UPD:        Update
*RB_UPL:        Upload
*S_ERDAT:D       .
*S_ERNAM:D       .
