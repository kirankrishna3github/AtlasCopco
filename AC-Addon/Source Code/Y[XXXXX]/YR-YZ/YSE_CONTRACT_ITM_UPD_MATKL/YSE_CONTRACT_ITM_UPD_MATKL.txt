*&---------------------------------------------------------------------*
*& Report  YSE_CONTRACT_ITM_UPD_MATKL
*&
*&---------------------------------------------------------------------*
*&
*& Update Material Group of Contract Line
*&
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 08.01.2013
*  Change Request Number : CR2653
*  Transport request Nr. : CD1K974571
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. |   DATE     | NAME               | CORRECT. NR| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                    | CD1K9..... | CR....      *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_contract_itm_upd_matkl
        LINE-SIZE 132.

* ALV grid
TYPE-POOLS: slis.

*----------------------------------------------------------------------*
* BDC Tables                                                           *
*----------------------------------------------------------------------*
DATA: i_bdcdata      LIKE bdcdata OCCURS 0 WITH HEADER LINE,
      struct_bdcdata TYPE bdcdata.

*BDC Structure for Messages
DATA : BEGIN OF i_messtab OCCURS 0.
        INCLUDE STRUCTURE bdcmsgcoll.
DATA : END OF i_messtab.

* Return
DATA: lt_return   TYPE STANDARD TABLE OF bapiret2 WITH HEADER LINE,
      ls_return   LIKE bapiret2.

DATA: BEGIN OF gt_input  OCCURS 0,
        line(50)   TYPE c,
      END OF gt_input.

DATA: BEGIN OF gt_in  OCCURS 0,
        vbeln        TYPE vbeln_va,
        posnr        TYPE posnr_va,
        matkl_n      TYPE matkl,
      END OF gt_in.

DATA: BEGIN OF gt_out  OCCURS 0,
        vbeln        TYPE vbeln_va,
        posnr        TYPE posnr_va,
        matkl_o      TYPE matkl,
        matkl_n      TYPE matkl,
        selkz        TYPE xfeld,
        zzcol        TYPE col_code,
      END OF gt_out.

DATA: BEGIN OF gt_list  OCCURS 0,
        vbeln        TYPE vbeln_va,
        posnr        TYPE posnr_va,
        matkl_o      TYPE matkl,
        matkl_n      TYPE matkl,
        update       TYPE xfeld,
        message(80)  TYPE c,
      END OF gt_list.

TYPES: BEGIN OF ty_vbap,
         vbeln       TYPE vbeln_va,
         posnr       TYPE posnr_va,
         matkl       TYPE matkl,
       END OF ty_vbap.

DATA: gt_vbap  TYPE HASHED TABLE OF ty_vbap
                    WITH UNIQUE KEY vbeln posnr
                    WITH HEADER LINE,
      gt_vbapi TYPE TABLE OF ty_vbap
                    WITH HEADER LINE.

* ALV
DATA: xv_variant         LIKE disvariant,
      xv_variant_flag    TYPE c,
      xv_sd_alv_variant  LIKE disvariant,
      xt_fcat            TYPE slis_t_fieldcat_alv,
      ls_fcat            LIKE LINE OF xt_fcat,
      xt_alv_sort        TYPE slis_t_sortinfo_alv,
      xv_user_command    TYPE slis_formname  VALUE 'USER_COMMAND',
      xv_variant_save    TYPE c              VALUE 'U',
      xv_layout          TYPE slis_layout_alv,
      xv_grid            TYPE lvc_s_glay,
      xt_events          TYPE slis_t_event,
      ls_events          TYPE slis_alv_event,
      xt_event_exits     TYPE slis_t_event_exit.

DATA: gv_col(10)         TYPE n,
      gv_repid           LIKE sy-repid,
      gv_answer          TYPE c,
      gv_text            TYPE char80,
      gv_error           TYPE xfeld,
      gv_cnt_upd         TYPE i,
      gv_mode(1)         TYPE c  VALUE 'N'.

CONSTANTS: gc_x                         VALUE 'X',
           gc_msgty_e                   VALUE 'E',
           gc_msgty_s                   VALUE 'S',
           gc_msgty_w                   VALUE 'W',
           gc_blank                     VALUE ' '.


*&---------------------------------------------------------------------*
*&  Selection screen                                                   *
*&---------------------------------------------------------------------*
* Input file
PARAMETERS: p_infile TYPE  rlgrap-filename LOWER CASE.
*                 DEFAULT 'C:\VT.MONITOR.xls'.


AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_infile.

* Get the filename
  PERFORM get_filename USING p_infile.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

* Read input file
  PERFORM read_file.

* Load data (for ALV)
  PERFORM load_data.

* Display data
  IF gt_out[] IS INITIAL.
    MESSAGE i001(00) WITH 'No contract items to update'(i01).
  ELSE.
    PERFORM alv_fcat.
    PERFORM alv_layout.
    PERFORM alv_events.
    PERFORM alv_display.
  ENDIF.

*&---------------------------------------------------------------------*
*&  Forms                                                              *
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  GET_FILENAME
*&---------------------------------------------------------------------*
*       Get the filename
*----------------------------------------------------------------------*
*      -->P_INFILE : Input file
*----------------------------------------------------------------------*
FORM get_filename  USING    p_infile.

  CALL FUNCTION 'WS_FILENAME_GET'
    EXPORTING
      def_path         = 'C:\'
      mask             = ',*.XLS.'
    IMPORTING
      filename         = p_infile
    EXCEPTIONS
      inv_winsys       = 1
      no_batch         = 2
      selection_cancel = 3
      selection_error  = 4
      OTHERS           = 5.

  IF sy-subrc <> 0.
  ENDIF.

ENDFORM.                    " GET_FILENAME

*&---------------------------------------------------------------------*
*&      Form  READ_FILE
*&---------------------------------------------------------------------*
*       Read input file
*----------------------------------------------------------------------*
FORM read_file .

* Read EXCEL file
  CALL FUNCTION 'FAA_FILE_UPLOAD_EXCEL'
    EXPORTING
      i_filename                 = p_infile
*      I_TEST                     =
*      I_UCOMM                    =
*      I_MAXCOLS                  =
      i_delimiter                = '|'
    TABLES
      et_filecontent             = gt_input
    EXCEPTIONS
      error_accessing_file       = 1
      OTHERS                     = 2
            .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    EXIT.
  ENDIF.

* Store in an internal table
  LOOP AT gt_input.
    CLEAR gt_in.
    SPLIT gt_input AT '|' INTO gt_in-vbeln gt_in-posnr gt_in-matkl_n.
*   Convert contract number & operation
    PERFORM add_zeros USING gt_in-vbeln.
    PERFORM add_zeros USING gt_in-posnr.
    APPEND gt_in.
  ENDLOOP.
  CHECK NOT gt_in[] IS INITIAL.

* Get (old) material types
  SELECT vbeln posnr matkl
         INTO TABLE gt_vbapi
         FROM vbap
         FOR ALL ENTRIES IN gt_in
         WHERE vbeln = gt_in-vbeln
           AND posnr = gt_in-posnr.
  SORT gt_vbapi BY vbeln posnr.
  DELETE ADJACENT DUPLICATES FROM gt_vbapi
                                  COMPARING vbeln posnr.
  gt_vbap[] = gt_vbapi[].
  FREE gt_vbapi.

ENDFORM.                    " READ_FILE

*&---------------------------------------------------------------------*
*&      Form  LOAD_DATA
*&---------------------------------------------------------------------*
*       Load data (for ALV)
*----------------------------------------------------------------------*
FORM load_data .

  LOOP AT gt_in.
    CLEAR gt_out.
*   Get old material group
    CLEAR gt_vbap.
    READ TABLE gt_vbap WITH KEY vbeln = gt_in-vbeln
                                posnr = gt_in-posnr.
    IF sy-subrc = 0.
      gt_out-matkl_o    = gt_vbap-matkl.
    ELSE.
      gt_list-vbeln = gt_in-vbeln.
      gt_list-posnr = gt_in-posnr.
      gt_list-message = 'Item does not exist!'(e01).
      APPEND gt_list.
      CONTINUE.
    ENDIF.

*   Fill internal table for output
    MOVE-CORRESPONDING gt_in TO gt_out.
    APPEND gt_out.

  ENDLOOP.

ENDFORM.                    " LOAD_DATA

*&---------------------------------------------------------------------*
*&      Form  ADD_ZEROS
*&---------------------------------------------------------------------*
*       Add leading zeros
*----------------------------------------------------------------------*
*      -->P_FIELD : field
*----------------------------------------------------------------------*
FORM add_zeros  USING    p_field.

  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
    EXPORTING
      input  = p_field
    IMPORTING
      output = p_field.

ENDFORM.                    " ADD_ZEROS

*&---------------------------------------------------------------------*
*&      Form  ALV_FCAT
*&---------------------------------------------------------------------*
*       ALV field catalog
*----------------------------------------------------------------------*
FORM alv_fcat .

** Create Fieldcatalogue from internal table
** CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE' ...

  CLEAR gv_col.

* Field definitions
  PERFORM add_field USING 'VBELN'      12 'Contract'(t01)       'X'.
  PERFORM add_field USING 'POSNR'       8 'Item'(t02)           ' '.
  PERFORM add_field USING 'MATKL_O'    14 'Mat.Grp (Old)'(t03)  ' '.
  PERFORM add_field USING 'MATKL_N'    14 'Mat.Grp (New)'(t04)  ' '.

ENDFORM.                    " ALV_FCAT

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr
                         p_key.

  gv_col = gv_col + 1.

  CLEAR ls_fcat.
  ls_fcat-col_pos    = gv_col.
  ls_fcat-fieldname  = p_field.
  ls_fcat-outputlen  = p_len.
  ls_fcat-seltext_l  = p_descr.
  ls_fcat-seltext_m  = p_descr.
  ls_fcat-seltext_s  = p_descr.
  ls_fcat-fix_column = 'X'.
  ls_fcat-emphasize  = 'X'.
  ls_fcat-hotspot    = p_key.
  ls_fcat-key        = p_key.

* Suppress leading zeros
  IF p_field = 'VBELN' OR
     p_field = 'POSNR'.
    ls_fcat-edit_mask = '==ALPHA'.
  ENDIF.

  APPEND ls_fcat TO xt_fcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT
*&---------------------------------------------------------------------*
*       Modify ALV layout
*----------------------------------------------------------------------*
FORM alv_layout .

* Define layout
  xv_layout-zebra               = gc_x.
  xv_layout-get_selinfos        = gc_x.
  xv_layout-detail_popup        = gc_x.
  xv_layout-box_fieldname       = 'SELKZ'.
  xv_layout-no_keyfix           = gc_x.
  xv_layout-group_change_edit   = gc_x.
  xv_layout-info_fieldname      = 'ZZCOL'.
*  xv_layout-totals_before_items = c_x.
*  xv_layout-key_hotspot         = c_x.
*  xv_layout-colwidth_optimize   = c_x.
*  xv_layout-smalltitle          = c_x.
*  xv_layout-grid_title          = 'title'.

* Define grid settings
  xv_grid-coll_end_l = gc_x.

ENDFORM.                    " ALV_LAYOUT

*&---------------------------------------------------------------------*
*&      Form  ALV_EVENTS
*&---------------------------------------------------------------------*
*       Define ALV events
*----------------------------------------------------------------------*
FORM alv_events .

* Fill events
  ls_events-form = ls_events-name = 'USER_COMMAND'.
  APPEND ls_events TO xt_events.
  ls_events-form = ls_events-name = 'PF_STATUS_SET'.
  APPEND ls_events TO xt_events.

ENDFORM.                    " ALV_EVENTS

*&---------------------------------------------------------------------*
*&      Form  USER_COMMAND
*&---------------------------------------------------------------------*
*       User Command callback routine for ALV
*----------------------------------------------------------------------*
FORM user_command USING r_ucomm LIKE sy-ucomm
                        rs_selfield TYPE slis_selfield.

  CASE r_ucomm.

    WHEN 'UPD_CONTR'.
*     Update Contract Items
      PERFORM update_contract_items.
      rs_selfield-refresh = gc_x.

    WHEN 'PRT_LOG'.
*     Print Update Log
      PERFORM print_log.

    WHEN '&IC1'.
*     Selection
      IF NOT rs_selfield-value IS INITIAL.
        CASE rs_selfield-fieldname.
          WHEN 'VBELN'.
*           Display Contract
            SET PARAMETER ID 'KTN' FIELD rs_selfield-value.
            CALL TRANSACTION 'VA43' AND SKIP FIRST SCREEN.
        ENDCASE.
      ENDIF.

  ENDCASE.

ENDFORM.                    " user_command

*&---------------------------------------------------------------------*
*&      Form  PF_STATUS_SET
*&---------------------------------------------------------------------*
*       PF-status callback routine for ALV
*----------------------------------------------------------------------*
FORM pf_status_set USING rt_extab TYPE slis_t_extab.

  SET PF-STATUS 'YSE_MON'.

ENDFORM.                    " PF_STATUS_SET

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       Display ALV grid
*----------------------------------------------------------------------*
FORM alv_display .

*  xv_sd_alv_variant = xv_variant.
  gv_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = gv_repid
      i_callback_user_command = xv_user_command
*      i_callback_top_of_page  = 'ALV_TOP'
      i_grid_title            = 'Update Material Group of Contract Items'(h01)
      is_layout               = xv_layout
      it_fieldcat             = xt_fcat
      i_grid_settings         = xv_grid
      it_events               = xt_events
      it_sort                 = xt_alv_sort
      i_default               = 'X'
      i_save                  = 'A'
*      is_variant              = xv_sd_alv_variant
      i_screen_start_column   = 0
      i_screen_start_line     = 0
      i_screen_end_column     = 0
      i_screen_end_line       = 0
    TABLES
      t_outtab                = gt_out
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.

  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  UPDATE_CONTRACT_ITEMS
*&---------------------------------------------------------------------*
*       Update Contract Items
*----------------------------------------------------------------------*
FORM update_contract_items .

* Records selected ?
  LOOP AT gt_out WHERE selkz = gc_x.
    EXIT.
  ENDLOOP.
  CHECK sy-subrc = 0.

* Confirm reprocessing
  CALL FUNCTION 'POPUP_TO_CONFIRM'
    EXPORTING
      titlebar              = text-h01
      text_question         = text-q01
      icon_button_1         = 'ICON_OKAY'
      icon_button_2         = 'ICON_CANCEL'
      default_button        = '2'
      display_cancel_button = space
    IMPORTING
      answer                = gv_answer
    EXCEPTIONS
      text_not_found        = 1
      OTHERS                = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  CHECK gv_answer = '1'.

  CLEAR gv_cnt_upd.

* Loop on the selected IDocs
  LOOP AT gt_out WHERE selkz = gc_x.

*   Progress indicator
    gv_text = 'Item & of Contract & is being processed'(i02).
    REPLACE '&' WITH gt_out-posnr INTO gv_text.
    REPLACE '&' WITH gt_out-vbeln INTO gv_text.
    CONDENSE gv_text.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        text = gv_text.

*   Update contract item data
    PERFORM update_item.

*   Update OK ?
    IF gv_error IS INITIAL.
*     Count
      gv_cnt_upd = gv_cnt_upd + 1.
*     List
      CLEAR gt_list.
      MOVE-CORRESPONDING gt_out TO gt_list.
      gt_list-update = gc_x.
      APPEND gt_list.
*     Remove from table
      DELETE gt_out.
    ENDIF.

  ENDLOOP.

  MESSAGE i001(00) WITH 'Number of Contract Items updated :'(i03)
                        gv_cnt_upd.

ENDFORM.                    " UPDATE_CONTRACT_ITEMS

*&---------------------------------------------------------------------*
*&      Form  UPDATE_ITEM
*&---------------------------------------------------------------------*
*       Update contract item data
*----------------------------------------------------------------------*
FORM update_item .

  REFRESH: i_bdcdata, i_messtab.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                 USING    'SAPMV45A'  '0102'  'X' ' ' ' '
                 CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'VBAK-VBELN' gt_out-vbeln
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'BDC_OKCODE'  '/00'
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

*----------------------------------------------------------------------*
*   Items
*----------------------------------------------------------------------*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
               USING    'SAPMV45A'  '4001'  'X' ''  ''
               CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
               USING    ''  ''  '' 'BDC_OKCODE'  '=POPO'
               CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

*----------------------------------------------------------------------*
*   Position at item
*----------------------------------------------------------------------*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    'SAPMV45A'  '0251'  'X' ''  ''
       CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'RV45A-POSNR' gt_out-posnr
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
      USING    ''  ''  '' 'BDC_OKCODE' '=POSI'
      CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

*----------------------------------------------------------------------*
*   Select item
*----------------------------------------------------------------------*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    'SAPMV45A'  '4001'  'X' ''  ''
       CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'RV45A-VBAP_SELKZ(01)' gc_x
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
      USING    ''  ''  '' 'BDC_OKCODE' '=ITEM'
      CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

*----------------------------------------------------------------------*
*   Go to tab 'Sales B'
*----------------------------------------------------------------------*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    'SAPMV45A'  '4003'  'X' ''  ''
       CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
      USING    ''  ''  '' 'BDC_OKCODE' '=T\02'
      CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

*----------------------------------------------------------------------*
*   Update the field 'Material Group' & Save
*----------------------------------------------------------------------*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    'SAPMV45A'  '4003'  'X' ''  ''
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
      USING    ''  ''  '' 'VBAP-MATKL' gt_out-matkl_n
      CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
        USING    ''  ''  '' 'BDC_OKCODE' '=SICH'
        CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

*----------------------------------------------------------------------*
*   Call transaction VA42
*----------------------------------------------------------------------*
  CALL TRANSACTION  'VA42'  USING i_bdcdata
                            MODE gv_mode
                            UPDATE 'S'
                            MESSAGES INTO i_messtab.

* Check if BDC error
  IF NOT i_messtab[] IS INITIAL.
    LOOP AT i_messtab WHERE msgtyp = 'E'  OR
                            msgtyp = 'A'.
      gv_error = 'X'.
*     List
      CLEAR gt_list.
      MOVE-CORRESPONDING gt_out TO gt_list.
      MESSAGE ID i_messtab-msgid
              TYPE i_messtab-msgtyp
              NUMBER i_messtab-msgnr
              INTO gt_list-message
              WITH i_messtab-msgv1 i_messtab-msgv2
                   i_messtab-msgv3 i_messtab-msgv4.
      APPEND gt_list.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " UPDATE_ITEM

*&---------------------------------------------------------------------*
*&      Form  PRINT_LOG
*&---------------------------------------------------------------------*
*       Print Update Log
*----------------------------------------------------------------------*
FORM print_log .

  CHECK NOT gt_list[] IS INITIAL.

  NEW-PAGE PRINT ON.

  SORT gt_list BY vbeln posnr.

  LOOP AT gt_list.
    WRITE: /01 gt_list-vbeln,
            13 gt_list-posnr,
            22 gt_list-matkl_o,
            34 gt_list-matkl_n,
            46 gt_list-update,
            52 gt_list-message.
  ENDLOOP.

  ULINE.

  NEW-PAGE PRINT OFF.

ENDFORM.                    " PRINT_LOG

*List header: Column headings£º

*001:Contract    Item     Mat.Grp(O)  Mat.Grp(N)  Upd.  Message
*Text symbol text£º
*E01:Item does not exist!
*H01:Update Material Group of Contract Items
*I01:No contract items to update
*I02:Item & of Contract & is being processed
*I03:Number of Contract Items updated :
*Q01:You are about to update the selected Contract items. Are you sure to continue ?
*T01:Contract
*T02:Item
*T03:Mat.Grp (Old)

*T04:Mat.Grp (New)
*Selection text£º
*P_INFILE:        Input file
