*&---------------------------------------------------------------------*
*& Report  YSE_ADV_SHIP_LIST
*&
*&---------------------------------------------------------------------*
*&
*& List SO / ASSO
*&
*&---------------------------------------------------------------------*

REPORT  yse_adv_ship_list.

TABLES: resb,
        vbfa,
        vbap.

* ----- ALV GRID ---------------------------------------------------*
TYPE-POOLS: slis.

DATA: BEGIN OF t_resb  OCCURS 0,
        rsnum  TYPE rsnum,
        aufnr  TYPE aufnr,
        werks  TYPE werks_d,
        matnr  TYPE matnr,
        rspos  TYPE rspos,
        lgort  TYPE lgort_d,
        kdauf  TYPE kdauf,
        kdpos  TYPE kdpos,
      END OF t_resb.
DATA: h_resb  LIKE LINE OF t_resb.

DATA: BEGIN OF t_vbfa  OCCURS 0,
        vbelv  TYPE vbeln_va,
        posnv  TYPE posnr_va,
        vbeln  TYPE vbeln_va,
        posnn  TYPE posnr_va,
        werks  TYPE werks_d,
        matnr  TYPE matnr,
      END OF t_vbfa.

DATA: BEGIN OF t_out  OCCURS 0,
        aufnr     TYPE aufnr,
        rsnum     TYPE rsnum,
        matnr     TYPE matnr,
        werks     TYPE werks_d,
        lgort     TYPE lgort_d,
        cnt_r(6)  TYPE n,
        vbeln     TYPE vbeln_va,
        cnt_v(6)  TYPE n,
      END OF t_out.

DATA: BEGIN OF t_wrk  OCCURS 0,
        werks     TYPE werks_d,
      END OF t_wrk.

DATA: xv_variant         LIKE disvariant,
      xv_variant_flag    TYPE c,
      xv_sd_alv_variant  LIKE disvariant,
      xt_fcat            TYPE slis_t_fieldcat_alv,   " lvc_t_fcat,
      ls_fcat            LIKE LINE OF xt_fcat,
      xt_alv_sort        TYPE slis_t_sortinfo_alv,
      xv_user_command    TYPE slis_formname  VALUE 'USER_COMMAND',
      xv_variant_save    TYPE c              VALUE 'U',
      xv_layout          TYPE slis_layout_alv.    "lvc_s_layo.

DATA: h_cnt_v(6)   TYPE n,
      h_cnt_r(6)   TYPE n,
      h_col(10)    TYPE n.

SELECT-OPTIONS:  so_werks  FOR resb-werks,    "Plant
                 so_aufnr  FOR resb-aufnr.    "Order


*----------------------------------------------------------------------*
* Checks on the selection screen                                       *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

  PERFORM  check_authorization.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*

START-OF-SELECTION.

  PERFORM select_data.
  PERFORM alv_fcat.
  PERFORM alv_layout.
  PERFORM alv_display.


*&---------------------------------------------------------------------*
*&  Forms                                                              *
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  SELECT werks FROM t001w
               INTO TABLE t_wrk
               WHERE werks IN so_werks.

  LOOP AT t_wrk.
    AUTHORITY-CHECK OBJECT 'M_MATE_WRK'
             ID 'ACTVT' DUMMY
             ID 'WERKS' FIELD t_wrk-werks.
    IF sy-subrc = 4.
*     No authorisation to display data
      MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '010' WITH t_wrk-werks.
      EXIT.
    ELSEIF sy-subrc <> 0.
*     Error checking authorization.
      MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
      EXIT.
    ENDIF.
  ENDLOOP.


ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

  REFRESH: t_resb, t_vbfa, t_out.

  SELECT rsnum aufnr werks matnr rspos
         lgort kdauf kdpos
         FROM resb
         INTO TABLE t_resb
         WHERE werks IN so_werks
           AND aufnr IN so_aufnr
           AND sobkz = 'E'
           AND xloek = ' '.

  SORT t_resb.

  SELECT vbfa~vbelv vbfa~posnv vbfa~vbeln posnn
         vbap~werks vbap~matnr
         INTO TABLE t_vbfa
         FROM vbfa
              INNER JOIN vbap
                         ON vbfa~vbeln = vbap~vbeln  AND
                            vbfa~posnn = vbap~posnr
         FOR ALL entries IN t_resb
             WHERE vbfa~vbelv   = t_resb-kdauf
               AND vbfa~posnv   = t_resb-kdpos
               AND vbfa~vbtyp_n = 'C'
               AND vbap~sobkz   = 'E'.

  SORT t_vbfa.

*  LOOP AT t_resb.
*    LOOP AT t_vbfa WHERE vbelv = t_resb-kdauf
*                     AND posnv = t_resb-kdpos
*                     AND werks = t_resb-werks
*                     AND matnr = t_resb-matnr.
*      IF t_resb-bdmng NE t_vbfa-kwmeng.
*        PERFORM fill_outtab.
*      ENDIF.
*    ENDLOOP.
*  ENDLOOP.

  LOOP AT t_resb.
    h_resb = t_resb.
    h_cnt_r = h_cnt_r + 1.

    AT END OF matnr.
      CLEAR h_cnt_v.
      CLEAR t_vbfa.
      LOOP AT t_vbfa WHERE vbelv = h_resb-kdauf
                       AND posnv = h_resb-kdpos
                       AND werks = h_resb-werks
                       AND matnr = h_resb-matnr.
        h_cnt_v = h_cnt_v + 1.
      ENDLOOP.
      IF h_cnt_v NE h_cnt_r.
        PERFORM fill_outtab.
      ENDIF.
      CLEAR: h_cnt_r.
    ENDAT.
  ENDLOOP.

  SORT t_out.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  FILL_OUTTAB
*&---------------------------------------------------------------------*
*       Fill output table
*----------------------------------------------------------------------*
FORM fill_outtab .

  CLEAR t_out.
  t_out-werks  = h_resb-werks.
  t_out-lgort  = h_resb-lgort.
  t_out-aufnr  = h_resb-aufnr.
  t_out-rsnum  = h_resb-rsnum.
*  t_out-rspos  = h_resb-rspos.
  t_out-matnr  = h_resb-matnr.
  t_out-cnt_r  = h_cnt_r.
  t_out-vbeln  = t_vbfa-vbeln.
*  t_out-posnr  = t_vbfa-posnn.
  t_out-cnt_v = h_cnt_v.
  APPEND t_out.

ENDFORM.                    " FILL_OUTTAB

*&---------------------------------------------------------------------*
*&      Form  ALV_FCAT
*&---------------------------------------------------------------------*
*       ALV field catalog
*----------------------------------------------------------------------*
FORM alv_fcat .

  DATA:lv_repid LIKE sy-repid.

  FIELD-SYMBOLS: <l_fcat> TYPE slis_fieldcat_alv.  "lvc_s_fcat.

  CLEAR: xt_fcat.
  REFRESH: xt_fcat.

*  lv_repid = sy-repid.
*
** Create Fieldcatalogue from internal table
*  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
*       EXPORTING
*            i_program_name         = lv_repid
*            i_internal_tabname     = 'T_OUT'
**            I_STRUCTURE_NAME       = 'YSE_AM_TRACY_LI3'
*            i_inclname             = lv_repid
*       CHANGING
*            ct_fieldcat            = xt_fcat[]
*       EXCEPTIONS
*            inconsistent_interface = 1
*            program_error          = 2
*            OTHERS                 = 3.

  CLEAR h_col.

  PERFORM add_field USING 'AUFNR'  12 'Order'         'X'.
  PERFORM add_field USING 'RSNUM'  10 'Reserv.'       ' '.
  PERFORM add_field USING 'MATNR'  18 'Material'      ' '.
  PERFORM add_field USING 'WERKS'   6 'Plant'         ' '.
  PERFORM add_field USING 'LGORT'   9 'Stor.Loc.'     ' '.
  PERFORM add_field USING 'CNT_R'  12 '# Items SO'      ' '.
  PERFORM add_field USING 'VBELN'  10 'ASSO'          'X'.
  PERFORM add_field USING 'CNT_V'  12 '# Items ASSO'  ' '.

ENDFORM.                    " ALV_FCAT

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr
                         p_hotsp.

  h_col = h_col + 1.

  CLEAR ls_fcat.
  ls_fcat-col_pos    = h_col.
  ls_fcat-fieldname  = p_field.
  ls_fcat-outputlen  = p_len.
  ls_fcat-seltext_l  = p_descr.
  ls_fcat-seltext_m  = p_descr.
  ls_fcat-seltext_s  = p_descr.
  ls_fcat-fix_column = 'X'.
  ls_fcat-emphasize  = 'X'.
  ls_fcat-hotspot    = p_hotsp.

  APPEND ls_fcat TO xt_fcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT
*&---------------------------------------------------------------------*
*       Modify ALV layout
*----------------------------------------------------------------------*
FORM alv_layout .

  xv_layout-zebra = ''.
*  xv_layout-smalltitle = 'X'.
*  xv_layout-grid_title = 'title'.

ENDFORM.                    " ALV_LAYOUT

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_repid LIKE sy-repid.

*  xv_sd_alv_variant = xv_variant.
  lv_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = lv_repid
      i_callback_user_command = xv_user_command
*      i_callback_top_of_page  = 'ALV_TOP'
      i_grid_title            = 'Sales reporting'
      is_layout               = xv_layout
      it_fieldcat             = xt_fcat
      it_sort                 = xt_alv_sort
      i_default               = 'X'
      i_save                  = 'A'
*      is_variant              = xv_sd_alv_variant
      i_screen_start_column   = 0
      i_screen_start_line     = 0
      i_screen_end_column     = 0
      i_screen_end_line       = 0
    TABLES
      t_outtab                = t_out
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.
  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


ENDFORM.                    " ALV_DISPLAY

*------------------------------------------------------------------*
*   Form  USER_COMMAND                                             *
*------------------------------------------------------------------*
*   --> R_UCOMM                                                    *
*   --> RS_SELFIELD                                                *
*------------------------------------------------------------------*
FORM user_command USING ucomm    LIKE sy-ucomm
                        selfield TYPE slis_selfield.

*-- Check function code
  CASE ucomm.
    WHEN '&IC1'.
      IF NOT selfield-value IS INITIAL.
        CASE selfield-fieldname.
*          WHEN 'MATNR'.
*            SET PARAMETER ID 'MAT' FIELD selfield-value.
*            CALL TRANSACTION 'MM03' AND SKIP FIRST SCREEN.
          WHEN 'AUFNR'.
            SET PARAMETER ID 'ANR' FIELD selfield-value.
            CALL TRANSACTION 'IW33' AND SKIP FIRST SCREEN.
          WHEN 'VBELN'.
            SET PARAMETER ID 'AUN' FIELD selfield-value.
            CALL TRANSACTION 'VA03' AND SKIP FIRST SCREEN.
        ENDCASE.
      ENDIF.
  ENDCASE.

ENDFORM.                    " USER_COMMAND

*Selection text£º
*SO_AUFNR:D       .
*SO_WERKS:D       .
