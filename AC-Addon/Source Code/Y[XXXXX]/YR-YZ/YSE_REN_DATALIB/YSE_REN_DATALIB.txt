***********************************************************************
* Program ID           : YSE_REN_DATALIB                              *
* Program Title        : Rental - Data Library                        *
* Author               : Tom Van Oevelen                              *
* Date                 : 03.10.2007                                   *
* Development Number   : D029                                         *
* Description          : A data library gives an historical overview  *
*                        the status and location of an equipment and  *
*                        rental contracts.                            *
*  Completely reviewed by TVO (02/10/2007). Original version in
*  YSE_REN_DATALIB_BACKUP
***********************************************************************
* Notes:                                                              *
***********************************************************************
* MOD-001 |  /  /    |                |                   | 001       *
* Description:                                                        *
*---------------------------------------------------------------------*
* MOD-002 |  /  /    |                |                   | 002       *
* Description:                                                        *
*                                                                     *
***********************************************************************
REPORT YSE_REN_DATALIB MESSAGE-ID YSE_RENTAL.

***********************************************************************
* CONSTANTS                                                           *
***********************************************************************
CONSTANTS:
  GC_AUART_ZQP  TYPE VBAK-AUART VALUE 'ZQP',  " rental contract
  GC_AUART_ZQP1 TYPE VBAK-AUART VALUE 'ZQP1', " rental contract
  GC_AUART_ZQP2 TYPE VBAK-AUART VALUE 'ZQP2', " rental contract
  GC_AUART_ZRIB TYPE VBAK-AUART VALUE 'ZRIB', " rental contract
  GC_AUART_LR   TYPE VBAK-AUART VALUE 'LR',   " inbound delivery
  GC_AUART_ZLF  TYPE VBAK-AUART VALUE 'ZLF',  " outbound delivery
  GC_AUART_ZSM1 TYPE VBAK-AUART VALUE 'ZSM1', " service order
  GC_AUART_ZSM2 TYPE VBAK-AUART VALUE 'ZSM2', " service order
  GC_AUART_ZAM1 TYPE VBAK-AUART VALUE 'ZAM1', " service order
  GC_AUART_ZAM2 TYPE VBAK-AUART VALUE 'ZAM2', " service order
  GC_ENGLISH    TYPE SPRAS      VALUE 'E',
  GC_TRUE       TYPE C          VALUE 'X',
  GC_BLUISH(4)  TYPE C          VALUE 'C400',
  GC_BLUE(4)    TYPE C          VALUE 'C100',
  GC_SOLDTO     TYPE VBPA-PARVW VALUE 'AG',      " SoldTo partner
  GC_SHIPTO     TYPE VBPA-PARVW VALUE 'WE'.      " ShipTo partner

***********************************************************************
* TABLES                                                              *
***********************************************************************
TABLES:
  YSE_RENT_SME,
  MARA,
  MVKE,
  EQUI.

***********************************************************************
* TYPE-POOLS                                                          *
***********************************************************************
TYPE-POOLS:
  SLIS.

***********************************************************************
* GLOBAL VARIABLES                                                    *
***********************************************************************
DATA:
  GV_BUKRS        TYPE VKBUK,        " Company code from sales org
  GV_ANGDT        TYPE DATS,         " Start date for eq (taking acq date into account)
  GV_BNDDT        TYPE DATS,         " End date for eq (taking deact date into account)
  GV_DATUM        TYPE DATS,         " Date used in loop
  GV_COLOR(4)     TYPE C,            " Line color for an equipment
  GV_BUKS         TYPE BUKRS,        " Company Code

***********************************************************************
* INTERNAL TABLES                                                     *
***********************************************************************
  IT_SME      TYPE YSE_RENT_SME         OCCURS 0 WITH HEADER LINE,
  BEGIN OF IT_MATNR                     OCCURS 0,
    MATNR     TYPE MATNR,
  END OF IT_MATNR,
  BEGIN OF IT_MAKT                      OCCURS 0,
    MATNR        TYPE MAKT-MATNR,
    MAKTX        TYPE MAKT-MAKTX,
  END OF IT_MAKT,
  BEGIN OF IT_MARA                      OCCURS 0,
    MATNR        TYPE MARA-MATNR,
    PRDHA        TYPE MARA-PRDHA,
  END OF IT_MARA,
  BEGIN OF IT_MVKE                      OCCURS 0,
    MATNR     TYPE MATNR,
    PMATN     TYPE MVKE-PMATN,
  END OF IT_MVKE,
  BEGIN OF IT_EQUI                      OCCURS 0,
    EQUNR        TYPE EQUI-EQUNR,
    SERNR        TYPE EQUI-SERNR,
    MATNR        TYPE EQUI-MATNR,
  END OF IT_EQUI,
  BEGIN OF IT_ILOA                      OCCURS 0,
    EQUNR     TYPE EQUZ-EQUNR,
    BUKRS     TYPE ILOA-BUKRS,
  END OF IT_ILOA,
  BEGIN OF IT_ANLA                      OCCURS 0,
    EQUNR     TYPE EQUZ-EQUNR,
    ZUGDT     TYPE ANLA-ZUGDT,
    DEAKT     TYPE ANLA-DEAKT,
  END OF IT_ANLA,
  BEGIN OF IT_VBAK                       OCCURS 0,
    VBELN        TYPE VBAK-VBELN,
    BUKRS_VF     TYPE VBAK-BUKRS_VF,
    VKORG        TYPE VBAK-VKORG,
    VKBUR        TYPE VBAK-VKBUR,
    WERKS        TYPE YSE_RENT_DEPOT-WERKS,
    LAND1        TYPE T001W-LAND1,
    LANDX        TYPE T005T-LANDX,
  END OF IT_VBAK,
  BEGIN OF IT_VBAP                       OCCURS 0,
    VBELN        TYPE VBAP-VBELN,
    POSNR        TYPE VBAP-POSNR,
    PRODH        TYPE VBAP-PRODH,
    WERKS        TYPE VBAP-WERKS,
    VKAUS        TYPE VBAP-VKAUS,
  END OF IT_VBAP,
  BEGIN OF IT_VBPA                         OCCURS 0,
    VBELN         TYPE VBPA-VBELN,
    POSNR         TYPE VBPA-POSNR,
    PARVW         TYPE VBPA-PARVW,
    KUNNR         TYPE VBPA-KUNNR,
    ADRNR         TYPE VBPA-ADRNR,
    LAND1         TYPE VBPA-LAND1,
    NAME1         TYPE ADRC-NAME1,
    LANDX         TYPE T005T-LANDX,
  END OF IT_VBPA,
  BEGIN OF IT_AUFK                       OCCURS 0,
    AUFNR         TYPE AUFK-AUFNR,
  END OF IT_AUFK,
  BEGIN OF IT_IHPA                       OCCURS 0,
    AUFNR         TYPE AUFK-AUFNR,
    OBJNR         TYPE IHPA-OBJNR,
    PARVW         TYPE IHPA-PARVW,
    COUNTER       TYPE IHPA-COUNTER,
    PARNR         TYPE IHPA-PARNR,
    ADRNR         TYPE IHPA-ADRNR,
  END OF IT_IHPA,
  BEGIN OF IT_ADRC                       OCCURS 0,
    ADDRNUMBER    TYPE ADRC-ADDRNUMBER,
    COUNTRY       TYPE ADRC-COUNTRY,
    NAME1         TYPE ADRC-NAME1,
    LANDX         TYPE T005T-LANDX,
  END OF IT_ADRC,
  BEGIN OF IT_KNA1                       OCCURS 0,
    KUNNR         TYPE KNA1-KUNNR,
    LAND1         TYPE KNA1-LAND1,
    NAME1         TYPE KNA1-NAME1,
    LANDX         TYPE T005T-LANDX,
  END OF IT_KNA1,
  IT_DATALIB  TYPE  YSE_REN_DATALIB_OUT  OCCURS 0 WITH HEADER LINE,

***********************************************************************
* WORKING AREAS                                                       *
***********************************************************************
  WA_LAYOUT       TYPE SLIS_LAYOUT_ALV,    " ALV GRID LAYOUT
  WA_DATALIB      LIKE LINE OF IT_DATALIB.

***********************************************************************
* RANGES                                                              *
***********************************************************************
RANGES:
  GR_AUART_CONTRACT FOR YSE_RENT_SME-AUART,
  GR_AUART_DELIVERY FOR YSE_RENT_SME-AUART,
  GR_AUART_SERVICE  FOR YSE_RENT_SME-AUART.

***********************************************************************
* SELECTION SCREEN                                                    *
***********************************************************************
SELECTION-SCREEN BEGIN OF BLOCK B1 WITH FRAME TITLE TEXT-S01.
  PARAMETERS:
    P_ANGDT TYPE DATS OBLIGATORY,                     " Startdate
    P_BNDDT TYPE DATS OBLIGATORY,                     " Enddate
*    P_BUKRS TYPE BUKRS MEMORY ID BUK,                 " Company Code
    P_VKORG TYPE YSE_RENT_SME-VKORG   MEMORY ID VKO OBLIGATORY,  " Sales Org
    P_VTWEG TYPE YSE_RENT_SME-VTWEG   MEMORY ID VTW OBLIGATORY.  " Distr Chan
  SELECT-OPTIONS:
*    SO_VKORG FOR YSE_RENT_SME-VKORG   MEMORY ID VKO OBLIGATORY,  " Sales Org
*    SO_VTWEG FOR YSE_RENT_SME-VTWEG   MEMORY ID VTW OBLIGATORY,  " Distr Chan
    SO_MODEL FOR MVKE-PMATN,                          " Model
    SO_MATNR FOR MARA-MATNR           MEMORY ID MAT,  " Material Nr
    SO_SERNR FOR YSE_RENT_SME-ZZSERNR MEMORY ID SER,  " Material Nr
    SO_EQUNR FOR EQUI-EQUNR           MEMORY ID EQN.  " Equipment
SELECTION-SCREEN END OF BLOCK B1.

* *******************************************************
* INITIALIZATION.
* *******************************************************
INITIALIZATION.

  PERFORM FILL_RANGES.

* *******************************************************
* AT SELECTION-SCREEN.
* *******************************************************
AT SELECTION-SCREEN.

  PERFORM CHECK_AUTHORIZATION.

* *******************************************************
* AT SELECTION-SCREEN ON P_VKORG
* *******************************************************
AT SELECTION-SCREEN ON P_VKORG.

  IF NOT P_VKORG IS INITIAL.
    SELECT SINGLE BUKRS
             FROM TVKO
             INTO GV_BUKS
            WHERE VKORG EQ P_VKORG.
    IF SY-SUBRC NE 0.
      MESSAGE E076.
    ENDIF.
  ENDIF.

***********************************************************************
* START OF SELECTION                                                  *
***********************************************************************
START-OF-SELECTION.

  DATA:
    LV_SUBRC TYPE SY-SUBRC.

  PERFORM SELECT_MATERIALS.

  PERFORM SELECT_SMEDATA CHANGING LV_SUBRC.
  IF LV_SUBRC NE 0.
    EXIT.
  ENDIF.

  PERFORM SELECT_DEPDATA.

  PERFORM PROCESS_DATA.

  PERFORM ALV_DISPLAY.

*-------------------------------------------------------------------*
* Form  SELECT_MATERIALS                                            *
*-------------------------------------------------------------------*
FORM SELECT_MATERIALS.

  DATA:
    BEGIN OF LT_MATNR OCCURS 0,
      MATNR TYPE MATNR,
    END OF LT_MATNR.

* If models provided
  IF NOT SO_MODEL IS INITIAL.

*   Note: if materials in so_matnr should also be included, remove
*         next CLEAR and append materials from model instead.
    CLEAR SO_MATNR.

*   Get all material numbers for selected models...
    SELECT MATNR
        INTO TABLE LT_MATNR
        FROM MVKE
       WHERE PMATN IN SO_MODEL
*         AND VKORG IN SO_VKORG
         AND VKORG EQ P_VKORG
*         AND VTWEG IN SO_VTWEG.
         AND VTWEG EQ P_VTWEG.
    SORT LT_MATNR.
    DELETE ADJACENT DUPLICATES FROM LT_MATNR.

*   Fill SO_MATNR based on model(s)
    SO_MATNR-SIGN = 'I'.
    SO_MATNR-OPTION = 'EQ'.
    SO_MATNR-HIGH = SPACE.
    LOOP AT LT_MATNR.
      SO_MATNR-LOW = LT_MATNR-MATNR.
      APPEND SO_MATNR.
    ENDLOOP.

    SORT SO_MATNR.
    DELETE ADJACENT DUPLICATES FROM SO_MATNR.
  ENDIF.    " SO_MODEL NOT INITIAL

ENDFORM.    " SELECT_MATERIALS

*&---------------------------------------------------------------------*
*&      Form  SELECT_SMEDATA
*&---------------------------------------------------------------------*
FORM SELECT_SMEDATA CHANGING P_SUBRC.

  DATA:
    LV_RECORDS TYPE I.

  P_SUBRC = 0.

* Get all yse_rent_sme records related to chosen materials
  SELECT *
      FROM YSE_RENT_SME
      INTO TABLE IT_SME
     WHERE   MATNR   IN SO_MATNR
       AND   ZZEQUNR IN SO_EQUNR    " Equipment nr
       AND   ZZEQUNR NE SPACE       " extra check
       AND   ZZSERNR IN SO_SERNR    " Serial number
*       AND   VKORG   IN SO_VKORG    " Sales org
       AND   VKORG   EQ P_VKORG    " Sales org
*       AND   VTWEG   IN SO_VTWEG    " Distr chan
       AND VTWEG EQ P_VTWEG     " Distr chan
       AND ( ( ANGDT GE P_ANGDT     " Begin within selected period
       AND     ANGDT LE P_BNDDT )
        OR (   BNDDT GE P_ANGDT     " End within selected period
       AND     BNDDT LE P_BNDDT )
        OR (   ANGDT LT P_ANGDT     " Completely overlapping period
       AND     BNDDT GT P_BNDDT ) ).

* Don't continue if no records found
  DESCRIBE TABLE IT_SME LINES LV_RECORDS.
  IF LV_RECORDS = 0.
    MESSAGE S072.
    P_SUBRC = 4.
  ENDIF.

ENDFORM.                    " SELECT_SMEDATA

*&---------------------------------------------------------------------*
*&      Form  SELECT_DEPDATA
*&---------------------------------------------------------------------*
FORM SELECT_DEPDATA .

  DATA:
    BEGIN OF IT_KNA1_TEMP OCCURS 0,
      KUNNR TYPE KNA1-KUNNR,
    END OF IT_KNA1_TEMP.

* Create list of unique material numbers
  LOOP AT IT_SME WHERE NOT ZZSERNR IS INITIAL.
    IT_MATNR-MATNR = IT_SME-MATNR.
    APPEND IT_MATNR.
  ENDLOOP.
  SORT IT_MATNR BY MATNR.
  DELETE ADJACENT DUPLICATES FROM IT_MATNR.

* Get material descriptions
  SELECT MATNR MAKTX
      INTO TABLE IT_MAKT
      FROM MAKT
       FOR ALL ENTRIES IN IT_MATNR
     WHERE MATNR = IT_MATNR-MATNR
       AND SPRAS = GC_ENGLISH.

* Determine set of models
  SELECT MATNR PMATN
      INTO TABLE IT_MVKE
      FROM MVKE
      FOR ALL ENTRIES IN IT_MATNR
     WHERE MATNR EQ IT_MATNR-MATNR
       AND PMATN NE SPACE.
  SORT IT_MVKE.
  DELETE ADJACENT DUPLICATES FROM IT_MVKE.

* Get material description for selected models
  SELECT MATNR MAKTX
      APPENDING TABLE IT_MAKT
      FROM MAKT
       FOR ALL ENTRIES IN IT_MVKE
     WHERE MATNR = IT_MVKE-PMATN
       AND SPRAS = GC_ENGLISH.
  SORT IT_MAKT BY MATNR.

* Get material's GAC and PGC
  SELECT MATNR PRDHA
         FROM MARA
         INTO TABLE IT_MARA
          FOR ALL ENTRIES IN IT_MATNR
        WHERE MATNR EQ IT_MATNR-MATNR.
  SORT IT_MARA BY MATNR.

* Creat list of unique equipment numbers based on loaded SME data
  SELECT      EQUNR SERNR MATNR
         INTO TABLE IT_EQUI
         FROM EQUI
          FOR ALL ENTRIES IN IT_SME
        WHERE EQUNR = IT_SME-ZZEQUNR.
  SORT IT_EQUI BY EQUNR.
  DELETE ADJACENT DUPLICATES FROM IT_EQUI.

* If any equipments in the table
  IF NOT IT_EQUI[] IS INITIAL.
*   Retrieve acquisition and deactivation dates
    SELECT EQUZ~EQUNR ZUGDT DEAKT
        INTO TABLE IT_ANLA
        FROM EQUZ
        JOIN ILOA
          ON EQUZ~ILOAN EQ ILOA~ILOAN
        JOIN ANLA
          ON ILOA~ANLNR EQ ANLA~ANLN1
         FOR ALL ENTRIES IN IT_EQUI
       WHERE EQUZ~EQUNR EQ IT_EQUI-EQUNR
*         AND ANLA~BUKRS EQ P_BUKRS.
        AND ANLA~BUKRS EQ GV_BUKRS.

*   Retrieve company code of the equipment
    SELECT EQUNR BUKRS
        INTO TABLE IT_ILOA
        FROM EQUZ
        JOIN ILOA
          ON EQUZ~ILOAN EQ ILOA~ILOAN
         FOR ALL ENTRIES IN IT_EQUI
       WHERE EQUZ~EQUNR EQ IT_EQUI-EQUNR.

  ENDIF.  " NOT IT_EQUI[] IS INITIAL

* Get all VBAP records related to yse_rent_sme documents
  SELECT VBELN POSNR PRODH WERKS VKAUS
      FROM VBAP
      INTO TABLE IT_VBAP
       FOR ALL ENTRIES IN IT_SME
     WHERE VBELN = IT_SME-VBELN.
  SORT IT_VBAP BY VBELN POSNR.

* Get all VBAK records related to yse_rent_sme documents
  SELECT VBELN BUKRS_VF VBAK~VKORG VBAK~VKBUR YSE_RENT_DEPOT~WERKS T001W~LAND1 LANDX
      FROM VBAK
      JOIN YSE_RENT_DEPOT
        ON VBAK~VKORG = YSE_RENT_DEPOT~VKORG
       AND VBAK~VKBUR = YSE_RENT_DEPOT~VKBUR
      JOIN T001W
        ON YSE_RENT_DEPOT~WERKS = T001W~WERKS
      JOIN T005T
        ON T001W~LAND1 = T005T~LAND1
      INTO TABLE IT_VBAK
       FOR ALL ENTRIES IN IT_SME
     WHERE VBELN EQ IT_SME-VBELN
       AND T005T~SPRAS EQ GC_ENGLISH .
  SORT IT_VBAK BY VBELN.
  DELETE ADJACENT DUPLICATES FROM IT_VBAK.

* Select partner information for Contracts and Deliveries
  SELECT      VBELN POSNR PARVW KUNNR ADRNR VBPA~LAND1 NAME1 LANDX
         INTO TABLE IT_VBPA
         FROM VBPA
         JOIN ADRC
           ON VBPA~ADRNR = ADRC~ADDRNUMBER
         JOIN T005T
           ON VBPA~LAND1 = T005T~LAND1
          FOR ALL ENTRIES IN IT_SME
        WHERE VBELN EQ IT_SME-VBELN
          AND (    PARVW EQ GC_SOLDTO
                OR PARVW EQ GC_SHIPTO )
          AND SPRAS EQ GC_ENGLISH.
  SORT IT_VBPA BY VBELN POSNR PARVW.
  LOOP AT IT_SME WHERE AUART IN GR_AUART_SERVICE.
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        INPUT         = IT_SME-VBELN
      IMPORTING
        OUTPUT        = IT_AUFK-AUFNR.
    APPEND IT_AUFK.
  ENDLOOP.
* Select partner information for Service
  SELECT      AUFNR AUFK~OBJNR PARVW COUNTER PARNR ADRNR
         INTO TABLE IT_IHPA
         FROM AUFK
         JOIN IHPA
           ON AUFK~OBJNR = IHPA~OBJNR
          FOR ALL ENTRIES IN IT_AUFK
        WHERE AUFNR EQ IT_AUFK-AUFNR
          AND (    PARVW EQ GC_SOLDTO
                OR PARVW EQ GC_SHIPTO ).
  SORT IT_IHPA BY AUFNR PARVW.
* Get adres information for service
  SELECT      ADDRNUMBER COUNTRY NAME1 LANDX
         INTO TABLE IT_ADRC
         FROM ADRC
         JOIN T005T
           ON ADRC~COUNTRY = T005T~LAND1
          FOR ALL ENTRIES IN IT_IHPA
        WHERE ADDRNUMBER NE SPACE
          AND ADDRNUMBER EQ IT_IHPA-ADRNR
          AND SPRAS EQ GC_ENGLISH.
  SORT IT_ADRC BY ADDRNUMBER.
  LOOP AT IT_IHPA WHERE ADRNR EQ SPACE.
    IT_KNA1_TEMP-KUNNR = IT_IHPA-PARNR.
    APPEND IT_KNA1_TEMP.
  ENDLOOP.
  SELECT      KUNNR KNA1~LAND1 NAME1 LANDX
         INTO TABLE IT_KNA1
         FROM KNA1
         JOIN T005T
           ON KNA1~LAND1 = T005T~LAND1
          FOR ALL ENTRIES IN IT_KNA1_TEMP
        WHERE KUNNR EQ IT_KNA1_TEMP-KUNNR
          AND T005T~SPRAS EQ GC_ENGLISH.
  SORT IT_KNA1 BY KUNNR.

ENDFORM.                    " SELECT_DEPDATA

*&---------------------------------------------------------------------*
*&      Form  PROCESS_DATA
*&---------------------------------------------------------------------*
FORM PROCESS_DATA .

* For every equipment
  LOOP AT IT_EQUI.

*   Fill template working area for all lines for current equipment
    PERFORM FILL_EQUI_TEMPLATE.

*   Get the period (taking into account acquisit/deactiv date)
    PERFORM GET_ASSET_PERIOD.

*   Go through the period for the equipment
    GV_DATUM = GV_ANGDT.
    WHILE NOT GV_DATUM > GV_BNDDT.

*     Add all contracts which follow each other up starting on the current date
      LOOP AT IT_SME WHERE ZZEQUNR EQ IT_EQUI-EQUNR     " Current equipment
                       AND AUART   IN GR_AUART_CONTRACT " Contract
                       AND ANGDT   LE GV_DATUM          " Start date less or equal to day of period
                       AND BNDDT   GE GV_DATUM          " End date greater or equal to day of period
                       AND ANGDT   LE GV_BNDDT.         " Start date less or equal to enddate of period
*       Add the contract
        PERFORM ADD_CONTRACT.
      ENDLOOP.

*     By adding contracts, it's possible that the working date has passed end of period
      CHECK GV_DATUM LE GV_BNDDT.

*     Check if there is delivery for this date
      LOOP AT IT_SME WHERE ZZEQUNR EQ IT_EQUI-EQUNR     " Current equipment
                       AND AUART   IN GR_AUART_DELIVERY " Delivery
                       AND ANGDT   LE GV_DATUM          " Start date less or equal to day of period
                       AND BNDDT   GE GV_DATUM.         " End date greater or equal to day of period
        EXIT.
      ENDLOOP.
*     If there is
      IF SY-SUBRC EQ 0.
*       Add the delivery, only for one day
        PERFORM ADD_UNAVAIL.
*       Force a new check on contract for the new date (ie start at the top of the WHILE)
        CONTINUE.
      ENDIF.

*     Check if there is service for this date
      LOOP AT IT_SME WHERE ZZEQUNR EQ IT_EQUI-EQUNR     " Current equipment
                       AND AUART   IN GR_AUART_SERVICE  " Service
                       AND ANGDT   LE GV_DATUM          " Start date less or equal to day of period
                       AND BNDDT   GE GV_DATUM.         " End date greater or equal to day of period
        EXIT.
      ENDLOOP.
*     If there is
      IF SY-SUBRC EQ 0.
*       Add the delivery, only for one day
        PERFORM ADD_UNAVAIL.
*       Force a new check on contract for the new date (ie start at the top of the WHILE)
        CONTINUE.
      ENDIF.

*     If for today, no contract, nor delivery nor service, add an availability line
      PERFORM ADD_AVAIL.

    ENDWHILE.

  ENDLOOP.  " IT_EQUI

ENDFORM.                    " PROCESS_DATA

*&---------------------------------------------------------------------*
*&      Form  GET_ASSET_PERIOD
*&---------------------------------------------------------------------*
FORM GET_ASSET_PERIOD .

* Get Acquisition date and Deactivation date
  CLEAR IT_ANLA.
  READ TABLE IT_ANLA WITH KEY EQUNR = IT_EQUI-EQUNR.
* Determine latest start date
  IF SY-SUBRC <> 0.
*   If no record found...
    GV_ANGDT = P_ANGDT.
    GV_BNDDT = P_BNDDT.
  ELSE.
*   If acquisition date is '00000000'...
    IF IT_ANLA-ZUGDT IS INITIAL.
      GV_ANGDT = P_ANGDT.
    ELSE.
      IF P_ANGDT > IT_ANLA-ZUGDT.
        GV_ANGDT = P_ANGDT.
      ELSE.
        GV_ANGDT = IT_ANLA-ZUGDT.
      ENDIF.
    ENDIF.
*   If deactivation date is '00000000'...
    IF IT_ANLA-DEAKT IS INITIAL.
      GV_BNDDT = P_BNDDT.
    ELSE.
*     Determine earliest end date
      IF P_BNDDT < IT_ANLA-DEAKT.
        GV_BNDDT = P_BNDDT.
      ELSE.
        GV_BNDDT = IT_ANLA-DEAKT.
      ENDIF.
    ENDIF.
  ENDIF.

ENDFORM.                    " GET_ASSET_PERIOD

*&---------------------------------------------------------------------*
*&      Form  FILL_EQUI_TEMPLATE
*&---------------------------------------------------------------------*
FORM FILL_EQUI_TEMPLATE .

* Set the color for this equipment
  IF GV_COLOR EQ GC_BLUE.
    GV_COLOR = GC_BLUISH.
  ELSE.
    GV_COLOR = GC_BLUE.
  ENDIF.

  CLEAR WA_DATALIB.

  WA_DATALIB-SERNR     = IT_EQUI-SERNR. " Serial number
  WA_DATALIB-EQUNR     = IT_EQUI-EQUNR.
  WA_DATALIB-LINECOLOR = GV_COLOR.

* Get prod hier for GAC / PGC
  READ TABLE IT_MARA WITH KEY MATNR = IT_EQUI-MATNR BINARY SEARCH.
  IF SY-SUBRC EQ 0.
    WA_DATALIB-GAC = IT_MARA-PRDHA+0(4).
    WA_DATALIB-PGC = IT_MARA-PRDHA. "+4(4).
  ENDIF.

* Get model descr
  READ TABLE IT_MVKE WITH KEY MATNR = IT_EQUI-MATNR BINARY SEARCH.
  IF SY-SUBRC EQ 0.
    READ TABLE IT_MAKT WITH KEY MATNR = IT_MVKE-PMATN BINARY SEARCH.
    IF SY-SUBRC EQ 0.
      WA_DATALIB-MODEL = IT_MAKT-MAKTX.
    ENDIF.
  ENDIF.

* Get material descr
  READ TABLE IT_MAKT WITH KEY MATNR = IT_EQUI-MATNR BINARY SEARCH.
  IF SY-SUBRC EQ 0.
    WA_DATALIB-MAKTX = IT_MAKT-MAKTX.
  ENDIF.

* Get equipment company code
  READ TABLE IT_ILOA WITH KEY EQUNR = IT_EQUI-EQUNR.
  IF SY-SUBRC EQ 0.
    WA_DATALIB-BUKRS = IT_ILOA-BUKRS.
  ENDIF.

ENDFORM.                    " FILL_EQUI_TEMPLATE

*&---------------------------------------------------------------------*
*&      Form  ADD_CONTRACT
*&---------------------------------------------------------------------*
FORM ADD_CONTRACT .

* Create a new line from equipment template
  IT_DATALIB = WA_DATALIB.

* Determine dates
  IT_DATALIB-ANGDT = GV_DATUM.
  IF IT_SME-BNDDT GT GV_BNDDT.
    IT_DATALIB-BNDDT = GV_BNDDT.
  ELSE.
    IT_DATALIB-BNDDT = IT_SME-BNDDT.
  ENDIF.

* Determine number of days in this contract
  IT_DATALIB-PERIOD = IT_DATALIB-BNDDT - IT_DATALIB-ANGDT + 1.

* Add some details
  IT_DATALIB-VBELN    = IT_SME-VBELN.   " Document number
  IT_DATALIB-LOCATION = IT_SME-ZZSE_LOC." Location
  IT_DATALIB-STATUS   = 'On rent'(003). " Status is On Rent

* Add rental plant
  READ TABLE IT_VBAK WITH KEY VBELN = IT_SME-VBELN BINARY SEARCH.
  IF SY-SUBRC EQ 0.
    IT_DATALIB-WERKS = IT_VBAK-WERKS.
    IT_DATALIB-COUNTRY = IT_VBAK-LANDX.
  ENDIF.

* Add regime
  READ TABLE IT_VBAP WITH KEY VBELN = IT_SME-VBELN
                              POSNR = IT_SME-POSNR
                     BINARY SEARCH.
  IF SY-SUBRC EQ 0.
*   Regime
    IT_DATALIB-VKAUS = IT_VBAP-VKAUS.
**  And overwrite GAC / PGC with document GAC / PGC
*   ...
  ENDIF.

* Add partner information
  PERFORM GET_PARTNER_INFO_SALES.

* Move date forward for this equipment
  GV_DATUM = IT_DATALIB-BNDDT + 1.

* Append the line to the result table
  APPEND IT_DATALIB.

ENDFORM.                    " ADD_CONTRACT

*&---------------------------------------------------------------------*
*&      Form  ADD_UNAVAIL
*&---------------------------------------------------------------------*
FORM ADD_UNAVAIL .

  DATA:
    LV_DATUM TYPE DATUM.

* Check if a line already exists for this document with noted enddate the previous day
  LV_DATUM = GV_DATUM - 1.
  READ TABLE IT_DATALIB WITH KEY VBELN   = IT_SME-VBELN
                                 SERNR   = IT_SME-ZZSERNR
                                 BNDDT   = LV_DATUM.

* If so, extend it with a day
  IF SY-SUBRC EQ 0.
    IT_DATALIB-BNDDT  = GV_DATUM.
    IT_DATALIB-PERIOD = IT_DATALIB-PERIOD + 1.
    MODIFY IT_DATALIB INDEX SY-TABIX.

* If not, create a new line
  ELSE.
*   Create a new line from equipment template
    IT_DATALIB = WA_DATALIB.
*   Fill out document (SME line) specifics
    IT_DATALIB-ANGDT     = GV_DATUM.
    IT_DATALIB-BNDDT     = GV_DATUM.
    IT_DATALIB-PERIOD    = 1.
    IT_DATALIB-VBELN     = IT_SME-VBELN.   " Document number
    IT_DATALIB-LOCATION  = IT_SME-ZZSE_LOC." Location

*   Add more details depending on unavailability type

*   --> For deliveries <-- *
    IF IT_SME-AUART IN GR_AUART_DELIVERY.
*     Add status
      IT_DATALIB-STATUS    = 'In transport'(002). " Status is In transport
*     Add partner information
      PERFORM GET_PARTNER_INFO_SALES.

*   --> For service <-- *
    ELSE.
*     Add status
      IT_DATALIB-STATUS    = 'Service'(004).      " Service
*     Add partner information
      PERFORM GET_PARTNER_INFO_SERVICE.

    ENDIF.

*   Append the line to the result table
    APPEND IT_DATALIB.

  ENDIF.

* Move date forward for this equipment
  GV_DATUM = IT_DATALIB-BNDDT + 1.

ENDFORM.                    " ADD_UNAVAIL

*&---------------------------------------------------------------------*
*&      Form  ADD_AVAIL
*&---------------------------------------------------------------------*
FORM ADD_AVAIL .

  DATA:
    LV_DATUM TYPE DATUM.

* Check if a line already exists for availability with enddate the previous day
  LV_DATUM = GV_DATUM - 1.
  READ TABLE IT_DATALIB WITH KEY VBELN   = SPACE
                                 SERNR   = IT_EQUI-SERNR
                                 BNDDT   = LV_DATUM.

* If so, extend it with a day
  IF SY-SUBRC EQ 0.
    IT_DATALIB-BNDDT  = GV_DATUM.
    IT_DATALIB-PERIOD = IT_DATALIB-PERIOD + 1.
    MODIFY IT_DATALIB INDEX SY-TABIX.

* If not, create a new line
  ELSE.
*   Create a new line from equipment template
    IT_DATALIB           = WA_DATALIB.
*   Fill out availability details
    IT_DATALIB-ANGDT     = GV_DATUM.
    IT_DATALIB-BNDDT     = GV_DATUM.
    IT_DATALIB-PERIOD    = 1.

*   Add status available
    IT_DATALIB-STATUS    = 'Available'(001).

*   Append the line to the result table
    APPEND IT_DATALIB.

  ENDIF.

* Move date forward for this equipment
  GV_DATUM = IT_DATALIB-BNDDT + 1.

ENDFORM.                    " ADD_AVAIL

*&---------------------------------------------------------------------*
*&      Form  GET_PARTNER_INFO_SALES
*&---------------------------------------------------------------------*
FORM GET_PARTNER_INFO_SALES .

* Add soldto partner information
  READ TABLE IT_VBPA WITH KEY VBELN = IT_SME-VBELN
                              POSNR = IT_SME-POSNR
                              PARVW = GC_SOLDTO
                     BINARY SEARCH.
  IF SY-SUBRC NE 0.
    READ TABLE IT_VBPA WITH KEY VBELN = IT_SME-VBELN
                                POSNR = SPACE
                                PARVW = GC_SOLDTO
                       BINARY SEARCH.
  ENDIF.
  IF SY-SUBRC EQ 0.
    IT_DATALIB-SOLDTO = IT_VBPA-NAME1.
  ENDIF.
* Add shiptto partner information
  READ TABLE IT_VBPA WITH KEY VBELN = IT_SME-VBELN
                              POSNR = IT_SME-POSNR
                              PARVW = GC_SHIPTO
                     BINARY SEARCH.
  IF SY-SUBRC NE 0.
    READ TABLE IT_VBPA WITH KEY VBELN = IT_SME-VBELN
                                POSNR = SPACE
                                PARVW = GC_SHIPTO
                       BINARY SEARCH.
  ENDIF.
  IF SY-SUBRC EQ 0.
    IT_DATALIB-SHIPTO = IT_VBPA-LANDX.
  ENDIF.

ENDFORM.                    " GET_PARTNER_INFO_SALES

*&---------------------------------------------------------------------*
*&      Form  GET_PARTNER_INFO_SERVICE
*&---------------------------------------------------------------------*
FORM GET_PARTNER_INFO_SERVICE .

  DATA:
    LV_AUFNR  TYPE AUFK-AUFNR.

  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
    EXPORTING
      INPUT         = IT_SME-VBELN
    IMPORTING
      OUTPUT        = LV_AUFNR.

* Add soldto partner information
  READ TABLE IT_IHPA WITH KEY AUFNR = LV_AUFNR
                              PARVW = GC_SOLDTO
                     BINARY SEARCH.
* If found
  IF SY-SUBRC EQ 0.
*   If special address to be used
    IF IT_IHPA-ADRNR NE SPACE.
*     Read special address
      READ TABLE IT_ADRC WITH KEY ADDRNUMBER = IT_IHPA-ADRNR BINARY SEARCH.
*     If found, set data
      IF SY-SUBRC EQ 0.
        IT_DATALIB-SOLDTO = IT_ADRC-NAME1.
      ENDIF.
*   If no special address for the partner
    ELSE.
*     Read partner master data
      READ TABLE IT_KNA1 WITH KEY KUNNR = IT_IHPA-PARNR BINARY SEARCH.
*     If found, set data
      IF SY-SUBRC EQ 0.
        IT_DATALIB-SOLDTO = IT_KNA1-NAME1.
      ENDIF.
    ENDIF.
  ENDIF.

* Add shipto partner information
  READ TABLE IT_IHPA WITH KEY AUFNR = LV_AUFNR
                              PARVW = GC_SHIPTO
                     BINARY SEARCH.
* If found
  IF SY-SUBRC EQ 0.
*   If special address to be used
    IF IT_IHPA-ADRNR NE SPACE.
*     Read special address
      READ TABLE IT_ADRC WITH KEY ADDRNUMBER = IT_IHPA-ADRNR BINARY SEARCH.
*     If found, set data
      IF SY-SUBRC EQ 0.
        IT_DATALIB-SHIPTO = IT_ADRC-LANDX.
      ENDIF.
*   If no special address for the partner
    ELSE.
*     Read partner master data
      READ TABLE IT_KNA1 WITH KEY KUNNR = IT_IHPA-PARNR BINARY SEARCH.
*     If found, set data
      IF SY-SUBRC EQ 0.
        IT_DATALIB-SHIPTO = IT_KNA1-LANDX.
      ENDIF.
    ENDIF.
  ENDIF.

ENDFORM.                    " GET_PARTNER_INFO_SERVICE

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
FORM ALV_DISPLAY.

  WA_LAYOUT-COLWIDTH_OPTIMIZE = GC_TRUE.
  WA_LAYOUT-INFO_FIELDNAME = 'LINECOLOR'.   " Name of the field that holds color information

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
         I_BACKGROUND_ID          = 'ALV_BACKGROUND'
         I_CALLBACK_PROGRAM       = SY-REPID
         I_CALLBACK_TOP_OF_PAGE   = 'ALV_TOP-OF-PAGE'
         I_STRUCTURE_NAME         = 'YSE_REN_DATALIB_OUT'
         IS_LAYOUT                = WA_LAYOUT
    TABLES
         T_OUTTAB                 = IT_DATALIB
    EXCEPTIONS
         PROGRAM_ERROR            = 1
         OTHERS                   = 2.

  IF SY-SUBRC <> 0.
*   MESSAGE ...
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*-------------------------------------------------------------------*
* FORM  ALV_TOP-OF-PAGE                                             *
*-------------------------------------------------------------------*
FORM ALV_TOP-OF-PAGE.

  DATA:
    LT_HEADER     TYPE SLIS_T_LISTHEADER,
    WA_HEADER     TYPE SLIS_LISTHEADER,
    LT_LINE1      LIKE WA_HEADER-INFO,
    LT_LINE2      LIKE WA_HEADER-INFO.

* Company code
  WA_HEADER-TYP  = 'S'.
  WA_HEADER-KEY = 'Sales Organization:'(005).
*  WA_HEADER-INFO = P_BUKRS.
  WA_HEADER-INFO = P_VKORG.
  APPEND WA_HEADER TO LT_HEADER.
  CLEAR WA_HEADER.

* Period
  WA_HEADER-TYP  = 'S'.
  WA_HEADER-KEY = 'Period:'(006).
  CONCATENATE  P_ANGDT+6(2) '.'
               P_ANGDT+4(2) '.'
               P_ANGDT(4)
         INTO  LT_LINE1.

  CONCATENATE  P_BNDDT+6(2) '.'
               P_BNDDT+4(2) '.'
               P_BNDDT(4)
         INTO LT_LINE2.

  CONCATENATE LT_LINE1 'until' LT_LINE2
         INTO WA_HEADER-INFO
    SEPARATED BY SPACE.

  APPEND WA_HEADER TO LT_HEADER.
  CLEAR: WA_HEADER.

* Blank line
  WA_HEADER-TYP  = 'S'.
  WA_HEADER-KEY = SPACE.
  WA_HEADER-INFO = SPACE.
  APPEND WA_HEADER TO LT_HEADER.
  CLEAR WA_HEADER.

  CALL FUNCTION 'REUSE_ALV_COMMENTARY_WRITE'
       EXPORTING
            IT_LIST_COMMENTARY = LT_HEADER.

ENDFORM.

*&---------------------------------------------------------------------*
*&      Form  FILL_RANGES
*&---------------------------------------------------------------------*
FORM FILL_RANGES.

* Fill global range with document types which represent rental contracts
  GR_AUART_CONTRACT-SIGN = 'I'.
  GR_AUART_CONTRACT-OPTION = 'EQ'.
  GR_AUART_CONTRACT-LOW = GC_AUART_ZQP.
  APPEND GR_AUART_CONTRACT.
  GR_AUART_CONTRACT-LOW = GC_AUART_ZQP1.
  APPEND GR_AUART_CONTRACT.
  GR_AUART_CONTRACT-LOW = GC_AUART_ZQP2.
  APPEND GR_AUART_CONTRACT.
  GR_AUART_CONTRACT-LOW = GC_AUART_ZRIB.
  APPEND GR_AUART_CONTRACT.

* Fill global range with document types which represent deliveries
  GR_AUART_DELIVERY-SIGN = 'I'.
  GR_AUART_DELIVERY-OPTION = 'EQ'.
  GR_AUART_DELIVERY-LOW = GC_AUART_LR.
  APPEND GR_AUART_DELIVERY.
  GR_AUART_DELIVERY-LOW = GC_AUART_ZLF.
  APPEND GR_AUART_DELIVERY.

* Fill global range with document types which represent service
  GR_AUART_SERVICE-SIGN = 'I'.
  GR_AUART_SERVICE-OPTION = 'EQ'.
  GR_AUART_SERVICE-LOW = GC_AUART_ZSM1.
  APPEND GR_AUART_SERVICE.
  GR_AUART_SERVICE-LOW = GC_AUART_ZSM2.
  APPEND GR_AUART_SERVICE.
  GR_AUART_SERVICE-LOW = GC_AUART_ZAM1.
  APPEND GR_AUART_SERVICE.
  GR_AUART_SERVICE-LOW = GC_AUART_ZAM2.
  APPEND GR_AUART_SERVICE.

ENDFORM.                    " FILL_RANGES

*&---------------------------------------------------------------------*
*&      Form  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
FORM CHECK_AUTHORIZATION .

*  AUTHORITY-CHECK OBJECT 'F_KNA1_BUK'
*                      ID 'BUKRS' FIELD P_BUKRS
*                      ID 'ACTVT' DUMMY.
*
*  IF SY-SUBRC = 4.
**   No authorisation to display the data
*    MESSAGE ID 'YSE_RENTAL' TYPE 'E' NUMBER '040' WITH P_BUKRS.
*  ELSEIF SY-SUBRC <> 0.
**   Error checking authorization.
*    MESSAGE ID 'YSE_RENTAL' TYPE 'E' NUMBER '046'.
*  ENDIF.

ENDFORM.                    " CHECK_AUTHORIZATION

*Text symbol text£º
*001:Available
*002:In transport
*003:On rent
*004:Service
*005:Sales Organization:
*006:Period:

*S01:Parameters
*Selection text£º
*P_ANGDT:        Startdate
*P_BNDDT:        Enddat
*P_BUKRS:D       Company Code
*P_VKORG:D       Sales Organization
*P_VTWEG:D       Distribution Channel
*SO_EQUNR:D       Equipment
*SO_MATNR:D       Material
*SO_MODEL:D       Pricing ref.material
*SO_SERNR:D       Serial number
