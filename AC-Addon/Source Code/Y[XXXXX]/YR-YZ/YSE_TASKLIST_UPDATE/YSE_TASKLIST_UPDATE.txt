*&---------------------------------------------------------------------*
*& Report  YSE_TASKLIST_UPDATE
*&---------------------------------------------------------------------*
*& Maintenance package for operation line 95 in GMIX task list
*&---------------------------------------------------------------------*
*  Author                : Anda Wu
*  Date                  : 02.04.2014
*  Change Request Number : CR3056
*  Transport request Nr. : CD1K980649
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. |   DATE     | NAME               | CORRECT. NR| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                    | CD1K9..... | CR....      *
************************************************************************
REPORT  yse_tasklist_update.
*&---------------------------------------------------------------------*
*&                    TYPES
*&---------------------------------------------------------------------*
TYPES:  BEGIN OF ty_package,
          plnnr   TYPE plko-plnnr,
          plnal   TYPE plko-plnal,
        END OF ty_package,
        BEGIN OF ty_plko,
          plnty   TYPE plko-plnty,
          plnnr   TYPE plko-plnnr,
          plnal   TYPE plko-plnal,
        END OF ty_plko,
        BEGIN OF ty_plas,
          plnty   TYPE plas-plnty,
          plnnr   TYPE plas-plnnr,
          plnal   TYPE plas-plnal,
          plnkn   TYPE plas-plnkn,
          zaehl   TYPE plas-zaehl,
          datuv   TYPE plas-datuv,
        END OF ty_plas,
        BEGIN OF ty_plpo,
          plnty   TYPE plpo-plnty,
          plnnr   TYPE plpo-plnnr,
          plnkn   TYPE plpo-plnkn,
          zaehl   TYPE plpo-zaehl,
          vornr   TYPE plpo-vornr,
        END OF ty_plpo.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA: gt_plko         TYPE STANDARD TABLE OF ty_plko,
      gt_plpo         TYPE STANDARD TABLE OF ty_plpo,
      gt_plas         TYPE STANDARD TABLE OF ty_plas,
      gt_lfile        TYPE yse_t_lfile,             "Local file name
      gt_package      TYPE TABLE OF ty_package,     "Update file data
      gt_update_log   TYPE TABLE OF string.         "update process log

************************************************************************
* WORKEARE                                                             *
************************************************************************

************************************************************************
* VARIABLES                                                            *
************************************************************************
*DATA: gv_tot_lines  TYPE sy-tabix.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_tab(1)     TYPE c VALUE cl_bcs_convert=>gc_tab,
  gc_type_e(1)  TYPE c VALUE 'E',
  gc_colon(1)   TYPE c VALUE ':',
  gc_hifi(1)    TYPE c VALUE '-'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
PARAMETERS:
  p_file(1024)  TYPE c LOWER CASE OBLIGATORY.  "Local File Path

PARAMETERS:
  cb_hflg       TYPE c  AS CHECKBOX MODIF ID m1 DEFAULT 'X'.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initialization
  PERFORM frm_do_init.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN OUTPUT                                          *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.
* Set selection screen
  PERFORM set_screen.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN                                                 *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.
* Check if the file path and name exist
  PERFORM check_filename.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN  ON VALUE-REQUEST                               *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_file.
* Get local file path
  PERFORM select_file.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* Read local file into a internal table
  PERFORM read_file.
* Get the package, task list data
  PERFORM get_tasklist_data.
* Update the 0095 tasklist
  PERFORM update_095.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
*   WRITE process result message
  PERFORM write_log.

*&---------------------------------------------------------------------*
*&      Form  FRM_DO_INIT
*&---------------------------------------------------------------------*
*      Do initialization
*----------------------------------------------------------------------*
FORM frm_do_init .

  CLEAR: gt_plko,
         gt_plpo,
         gt_plas,
         gt_lfile ,
         gt_package ,
         gt_update_log.

ENDFORM.                    " FRM_DO_INIT
*&---------------------------------------------------------------------*
*&      Form  SET_SCREEN
*&---------------------------------------------------------------------*
*       Set selection screen
*----------------------------------------------------------------------*
FORM set_screen .

  LOOP AT SCREEN.
    IF screen-group1 = 'M1'.
      screen-invisible = '1'.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " SET_SCREEN
*&---------------------------------------------------------------------*
*&      Form  CHECK_FILENAME
*&---------------------------------------------------------------------*
*       Check if the file path and name exis
*----------------------------------------------------------------------*
FORM check_filename .

  DATA:
    lv_exist TYPE c.
  CALL FUNCTION 'TMP_GUI_GET_FILE_EXIST'
    EXPORTING
      fname          = p_file
    IMPORTING
      exist          = lv_exist
    EXCEPTIONS
      fileinfo_error = 1
      OTHERS         = 2.
  IF sy-subrc <> 0 OR lv_exist IS INITIAL.
    MESSAGE e000(yse_sales_log)
      WITH 'Please enter an invaild filename'(010).
  ENDIF.

ENDFORM.                    " CHECK_FILENAME
*&---------------------------------------------------------------------*
*&      Form  SELECT_FILE
*&---------------------------------------------------------------------*
*       Get local file path
*----------------------------------------------------------------------*
FORM select_file .

  DATA:
    lv_title  TYPE string,     "Referrence title
    lt_file   TYPE filetable,  "Internal table of dialog filenames
    ls_file   TYPE file_table, "Working area for IT_FILE,
    lv_rc     TYPE i.          "Return Code

  lv_title = text-001.
  CLEAR: lt_file[],
         ls_file.
* Open local file dialog
  CALL METHOD cl_gui_frontend_services=>file_open_dialog
    EXPORTING
      window_title            = lv_title
    CHANGING
      file_table              = lt_file
      rc                      = lv_rc
    EXCEPTIONS
      file_open_dialog_failed = 1
      cntl_error              = 2
      error_no_gui            = 3
      not_supported_by_gui    = 4
      OTHERS                  = 5.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
  IF lv_rc = 1.
    READ TABLE lt_file INDEX 1 INTO ls_file.
    IF sy-subrc EQ 0.
      p_file = ls_file-filename.
    ENDIF.
  ENDIF.

ENDFORM.                    " SELECT_FILE

*&---------------------------------------------------------------------*
*&      Form  READ_FILE
*&---------------------------------------------------------------------*
*       Read local file into a internal table
*----------------------------------------------------------------------*
FORM read_file .

  DATA: lv_filename TYPE string.
* Read file entires
  lv_filename = p_file.
  PERFORM get_from_pres IN PROGRAM yam_common_routines
                                    TABLES  gt_lfile
                                    USING   lv_filename
                                            ycl_se_utility=>c_ft_asc
                                            ' '.
* Delete the blank lines
  DELETE gt_lfile
    WHERE text IS INITIAL.

ENDFORM.                    " READ_FILE
*&---------------------------------------------------------------------*
*&      Form  GET_TASKLIST_DATA
*&---------------------------------------------------------------------*
*      Get the package, task list data
*----------------------------------------------------------------------*
FORM get_tasklist_data .

  FIELD-SYMBOLS: <lfs_package> TYPE ty_package.
* Do conversation
  PERFORM convert_to_struc.

* Delete header text
  IF cb_hflg IS NOT INITIAL.
    DELETE gt_package INDEX 1.
  ENDIF.
  SORT gt_package.
  DELETE ADJACENT DUPLICATES FROM gt_package
    COMPARING ALL FIELDS.

* Check if update entries is empty
  IF gt_package IS INITIAL.
    MESSAGE s000(yse_sales_log) WITH text-002
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  LOOP AT gt_package ASSIGNING <lfs_package>.
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        input  = <lfs_package>-plnnr
      IMPORTING
        output = <lfs_package>-plnnr.
  ENDLOOP.
  SELECT  plnty
          plnnr
          plnal
    FROM  plko
    INTO TABLE gt_plko
    FOR ALL ENTRIES IN gt_package
    WHERE plnnr = gt_package-plnnr.
  IF sy-subrc <> 0.
    MESSAGE s000(yse_sales_log) WITH text-021
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  SELECT  plnty
          plnnr
          plnal
          plnkn
          zaehl
          datuv
    INTO TABLE  gt_plas
    FROM plas
    FOR ALL ENTRIES IN gt_plko
    WHERE plnnr = gt_plko-plnnr
      AND plnal = gt_plko-plnal.
  IF sy-subrc <> 0.
    MESSAGE s000(yse_sales_log) WITH text-022
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  SELECT  plnty
          plnnr
          plnkn
          zaehl
          vornr
    INTO TABLE gt_plpo
    FROM plpo
    FOR ALL ENTRIES IN gt_plas
    WHERE plnty = gt_plas-plnty
      AND plnnr = gt_plas-plnnr
      AND plnkn = gt_plas-plnkn
      AND zaehl = gt_plas-zaehl
      AND vornr = '0095'.

ENDFORM.                    " GET_TASKLIST_DATA
*&---------------------------------------------------------------------*
*&      Form  WRITE_LOG
*&---------------------------------------------------------------------*
*       Write entry to datebase table
*----------------------------------------------------------------------*
FORM write_log .

  DATA: lv_mess_str  TYPE string,
        lv_tot_lines TYPE i,
        lv_suc_lines TYPE i.
  SORT gt_plko.
  DELETE ADJACENT DUPLICATES FROM gt_plko
    COMPARING plnnr.
  DESCRIBE TABLE gt_plko LINES lv_tot_lines.
  DESCRIBE TABLE gt_update_log LINES lv_suc_lines.
* Dispaly process log
  WRITE: 'Total Process Package:'(005) , AT 30(6) lv_tot_lines, /.
  WRITE: 'Updated package:'(015), AT 30(6) lv_suc_lines.
* Under line and blank line
  ULINE.
  SKIP.

  LOOP AT gt_update_log INTO lv_mess_str.
    WRITE: lv_mess_str , /.
  ENDLOOP.

ENDFORM.                    " WRITE_LOG
*&---------------------------------------------------------------------*
*&      Form  CONVERT_TO_STRUC
*&---------------------------------------------------------------------*
*       Do conversation
*----------------------------------------------------------------------*
FORM convert_to_struc .

  DATA:
    ls_lfile TYPE yse_s_lfile,
    lt_field TYPE TABLE OF string,
    lv_field TYPE string,
    lv_line  TYPE string,
    lflg_suc TYPE c,
    lv_mess_str  TYPE string.
  FIELD-SYMBOLS:
    <fs_tab> TYPE STANDARD TABLE,
    <fs_wa> TYPE ANY,
    <fs_field> TYPE ANY.
  DATA: lo_wa TYPE REF TO data,
        lo_itab TYPE REF TO data.
  CREATE DATA:
    lo_wa TYPE ty_package,
    lo_itab TYPE TABLE OF ty_package INITIAL SIZE 1.

  ASSIGN lo_wa->* TO <fs_wa>.
  ASSIGN lo_itab->* TO <fs_tab>.
  ASSIGN gt_package TO <fs_tab>.

  LOOP AT gt_lfile INTO ls_lfile.
    CLEAR: lt_field,
           lflg_suc.
    lv_line = ls_lfile-text.
    SPLIT lv_line AT gc_tab INTO TABLE lt_field.
    lv_mess_str = lv_line.
    REPLACE ALL OCCURRENCES OF  gc_tab
      IN lv_mess_str
      WITH gc_hifi.
    LOOP AT lt_field INTO lv_field.
      ASSIGN COMPONENT sy-tabix OF STRUCTURE <fs_wa> TO <fs_field>.
      TRY .
          <fs_field> = lv_field.
        CATCH cx_sy_conversion_no_number.
          lflg_suc = gc_type_e.
          CONCATENATE
                text-012
                lv_mess_str+4
            INTO lv_mess_str.
          APPEND lv_mess_str TO gt_update_log.
          EXIT.
      ENDTRY.
    ENDLOOP.
    IF lflg_suc IS NOT INITIAL.
      CONTINUE.
    ENDIF.
    APPEND <fs_wa> TO <fs_tab>.
    CLEAR  <fs_wa>.
  ENDLOOP.

ENDFORM.                    " CONVERT_TO_STRUC
*&---------------------------------------------------------------------*
*&      Form  UPDATE_095
*&---------------------------------------------------------------------*
*       Update the 0095 tasklist
*----------------------------------------------------------------------*
FORM update_095 .

  DATA: ls_plko     TYPE ty_plko,
        ls_plpo     TYPE ty_plpo,
        ls_plwp     TYPE plwp,
        lt_plwp     TYPE STANDARD TABLE OF plwp,
        ls_plas     TYPE ty_plas,
        lv_mess_str TYPE string,
        lv_sub_str  TYPE string.
  SORT gt_plko.
* Update Package one by one
  LOOP AT gt_plko INTO ls_plko.
    AT NEW plnnr.
      CLEAR: lv_mess_str,
             lv_sub_str.
      CONCATENATE 'Update package'(009)
                  ls_plko-plnnr
                  'of Group Counter'(013)
                  gc_colon
             INTO
                  lv_mess_str
              SEPARATED BY space.
    ENDAT.
    LOOP AT gt_plas INTO ls_plas
      WHERE plnty = ls_plko-plnty
        AND plnnr = ls_plko-plnnr
        AND plnal = ls_plko-plnal.
      CLEAR: ls_plwp.
      READ TABLE gt_plpo INTO ls_plpo
          WITH KEY plnty = ls_plas-plnty
                   plnnr = ls_plas-plnnr
                   plnkn = ls_plas-plnkn.
      IF sy-subrc <> 0.
        CONTINUE.
      ENDIF.
      SELECT *
        FROM plwp
        INTO TABLE lt_plwp
        WHERE plnty = ls_plpo-plnty
          AND plnnr = ls_plpo-plnnr
          AND plnkn = ls_plpo-plnkn
          AND plnal = ls_plko-plnal.
      IF sy-subrc = 0.
        SORT lt_plwp by zaehl DESCENDING.
        READ TABLE lt_plwp INTO ls_plwp INDEX 1.
        IF ls_plwp-loekz IS NOT INITIAL.
          CLEAR ls_plwp-loekz.
          ls_plwp-aedat = sy-datum.
          ls_plwp-aenam = sy-uname.
          UPDATE plwp FROM ls_plwp.
          IF sy-subrc = 0.
            CONCATENATE lv_sub_str ls_plas-plnal
              INTO  lv_sub_str SEPARATED BY space.
          ENDIF.
        ENDIF.
      ELSE.
        SELECT *
          FROM plwp
          INTO ls_plwp UP TO 1 ROWS
          WHERE plnty = ls_plpo-plnty
            AND plnnr = ls_plpo-plnnr
            AND plnal = ls_plko-plnal.
        ENDSELECT.
        IF sy-subrc = 0.
          ls_plwp-plnkn = ls_plas-plnkn.
          ls_plwp-zaehl = ls_plas-zaehl.
          ls_plwp-datuv = sy-datum.
          ls_plwp-andat = sy-datum.
          ls_plwp-annam = sy-uname.
          ls_plwp-aedat = sy-datum.
          ls_plwp-aenam = sy-uname.
          INSERT plwp FROM ls_plwp.
          IF sy-subrc = 0.
            CONCATENATE lv_sub_str ls_plas-plnal
              INTO  lv_sub_str SEPARATED BY space.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDLOOP.

    AT END OF plnnr.
      IF lv_sub_str IS NOT INITIAL.
        CONCATENATE lv_mess_str lv_sub_str
          INTO lv_mess_str
          SEPARATED BY space.
        APPEND lv_mess_str TO gt_update_log.
      ENDIF.
    ENDAT.
  ENDLOOP.
ENDFORM.                    " UPDATE_095

*Text symbol text£º
*001:Select a file for upload
*002:[No Input Data] or [Error Happen On Converting]
*005:Total Process Package:
*009:Update package
*010:Please enter an invaild filename
*012:Error to convert record:
*013:of Group Counter
*015:Updated package:
*021:No package exist, please check your file!!

*022:No  Task list - operation/activity exist!
*Selection text£º
*CB_HFLG:        First line not involved
*P_FILE:        Filename
