************************************************************************
* Program ID        : YSE_UPD_IT_EQWTYDATE                             *
* Program Title     : Program title                                    *
* Author            : Anson Sun                                        *
* Date              : 08/09/2011                               	       *
* Change Request Number:2214                                           *
* Description       : Update Warranty for ITBA                         *
*======================================================================*
* Copied From       : (Cloned Program)                                 *
* Title             : (Program Title)                          	       *
* Other Related obj : (Object names)                                   *
*======================================================================*
* Change History Log                                             	     *
*----------------------------------------------------------------------*
* Mod. no.|  Date      | Name           | Correction Nr  | Change Req. *
*----------------------------------------------------------------------*
* MOD-001 | 03/10/2011 |J. Smets        | CD1K967012     | CR2177      *
*----------------------------------------------------------------------*
* MOD-002 | 29/10/2011 |Anson Sun       | CD1K968664     | CR2214      *
*----------------------------------------------------------------------*

REPORT  yse_upd_it_eqwtydate MESSAGE-ID yse_cs_sd_msg.
*----------------------------------------------------------------------*
*       TABLES
*----------------------------------------------------------------------*
TABLES:
  t001,                      "Company Code
  equi,                      "Equipment master data
  equz,                      "Equipment time segment
  yse_p025_parms.            "SE: P025 Parameters          "MOD-001

*----------------------------------------------------------------------*
*       TYPE-POOLS
*----------------------------------------------------------------------*
TYPE-POOLS slis.

*----------------------------------------------------------------------*
*       TYPES
*----------------------------------------------------------------------*
TYPES:
  BEGIN OF ty_equnr,
    iwerk TYPE equz-iwerk,                      "Plant
    equnr TYPE equz-equnr,                      "Equipment number
    datbi TYPE equz-datbi,                      "Valid To Date
    eqlfn TYPE equz-eqlfn,                      "Consecutive numberin
    erdat TYPE equi-erdat,                      "Date created
    matnr TYPE equi-matnr,                      "Material
    objnr TYPE equi-objnr,                      "Object number
    wadat_ist          TYPE likp-wadat_ist,     "Actual GI date
    cus_warr_months    TYPE yse_cus_warr_months,"customer warr period
    ven_warr_months    TYPE yse_ven_warr_months,"vendor   warr period
    cus_warr_sta_old   TYPE gwldt,              "customer Guar old
    cus_warr_end_old   TYPE gwlen,              "customer warr ends old
    ven_warr_sta_old   TYPE gwldt,              "vendor   Guar old
    ven_warr_end_old   TYPE gwlen,              "vendor   warr ends old
    cus_warr_sta_new   TYPE gwldt,              "customer Guar new
    cus_warr_end_new   TYPE gwlen,              "customer warr ends new
    ven_warr_sta_new   TYPE gwldt,              "vendor   Guar new
    ven_warr_end_new   TYPE gwlen,              "vendor   warr ends new
  END   OF ty_equnr.

*----------------------------------------------------------------------*
*       DATA
*----------------------------------------------------------------------*
DATA:
  i_log         TYPE TABLE OF yse_upd_it_eqwtydate_log,
  i_fieldcat    TYPE slis_t_fieldcat_alv,
  rec_bdc       TYPE bdcdata,
  gv_plc        TYPE yse_plc.                              "MOD-001

*----------------------------------------------------------------------*
*       SELECTION-SCREEN
*----------------------------------------------------------------------*
SELECTION-SCREEN: BEGIN OF BLOCK b_sel1 WITH FRAME TITLE text-b01.
SELECT-OPTIONS:
  s_bukrs FOR t001-bukrs  NO-EXTENSION NO INTERVALS OBLIGATORY,
  s_iwerk FOR equz-iwerk  OBLIGATORY.
SELECTION-SCREEN: END   OF BLOCK b_sel1.

SELECTION-SCREEN: BEGIN OF BLOCK b_sel2 WITH FRAME TITLE text-b02.
SELECT-OPTIONS:
  s_erdat FOR equi-erdat  NO-EXTENSION OBLIGATORY,
  s_equnr FOR equi-equnr.
SELECTION-SCREEN: END   OF BLOCK b_sel2.

PARAMETERS: p_vendor AS CHECKBOX.

*----------------------------------------------------------------------*
*       AT SELECTION-SCREEN
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

* Authority check for Maintain Plant
  PERFORM authority_check.

*----------------------------------------------------------------------*
*       INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

* initial process
  PERFORM ini_process.

*----------------------------------------------------------------------*
*       START-OF-SELECTION
*----------------------------------------------------------------------*
START-OF-SELECTION.

* main process
  PERFORM main_process.

*----------------------------------------------------------------------*
*       END-OF-SELECTION
*----------------------------------------------------------------------*
END-OF-SELECTION.

* log process
  PERFORM display_log.


*&---------------------------------------------------------------------*
*&      Form  ini_process
*&---------------------------------------------------------------------*
*       initial process
*----------------------------------------------------------------------*
FORM ini_process .

  DATA: lrh_erdat LIKE LINE  OF s_erdat.

  REFRESH:
    i_log,
    i_fieldcat.

* Set Default Date-to
  IF s_erdat-high IS INITIAL.
    lrh_erdat-sign   = 'I'.
    lrh_erdat-option = 'BT'.
    lrh_erdat-high   = sy-datum.
    APPEND lrh_erdat TO s_erdat.
  ENDIF.

ENDFORM.                    " ini_process

*&---------------------------------------------------------------------*
*&      Form  authority_check
*&---------------------------------------------------------------------*
*       Authority check for Maintain Plant
*----------------------------------------------------------------------*
FORM authority_check .

  DATA: li_werks   TYPE TABLE OF t001w,
        lrec_werks TYPE t001w.

  SELECT werks INTO CORRESPONDING FIELDS OF TABLE li_werks
         FROM t001w
         WHERE werks IN s_iwerk.

  LOOP AT li_werks INTO lrec_werks.
*   Authority check for Maintain Plant
    AUTHORITY-CHECK OBJECT 'I_IWERK'
             ID 'TCD'   FIELD sy-tcode
             ID 'IWERK' FIELD lrec_werks-werks.
    IF sy-subrc <> 0.
      MESSAGE e002.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " authority_check

*&---------------------------------------------------------------------*
*&      Form  main_process
*&---------------------------------------------------------------------*
*       main process
*----------------------------------------------------------------------*
FORM main_process .

  DATA: li_werks   TYPE TABLE OF t001w,
        li_equnr   TYPE TABLE OF ty_equnr,
        lrec_equnr TYPE ty_equnr.
  DATA: l_msgtx    TYPE bapi_msg,
        l_subrc    TYPE sy-subrc.

  SELECT werks INTO CORRESPONDING FIELDS OF TABLE li_werks
         FROM t001w
         WHERE werks IN s_iwerk.

  IF li_werks IS NOT INITIAL.
    SELECT equz~iwerk                       "Plant
           equz~equnr                       "Equipment number
           equz~datbi                       "Valid To Date
*           equz~eqlfn                       "Consecutive numberin
           equi~erdat                       "Date created
           equi~matnr                       "Material
           equi~objnr                       "Object number
      INTO CORRESPONDING FIELDS OF TABLE li_equnr
      FROM equi INNER JOIN equz
        ON ( equi~equnr = equz~equnr )      "Equipment number
       FOR ALL entries IN li_werks
     WHERE equi~equnr  IN s_equnr           "Equipment number
       AND equi~erdat  IN s_erdat           "Date created
       AND equz~datbi   = '99991231'        "Valid To Date
       AND equz~iwerk   = li_werks-werks.   "Main Planning Plant
  ENDIF.

* no data process.
  IF li_equnr[] IS INITIAL.
    MESSAGE s001.
    LEAVE LIST-PROCESSING.
  ENDIF.

  LOOP AT li_equnr[] INTO lrec_equnr.
*   Get Old warranty date
    PERFORM read_warr  USING lrec_equnr-objnr
                    CHANGING lrec_equnr-cus_warr_sta_old
                             lrec_equnr-cus_warr_end_old
                             lrec_equnr-ven_warr_sta_old
                             lrec_equnr-ven_warr_end_old.

*   Get actual goods issue date
    PERFORM get_gi_date USING lrec_equnr-equnr
                     CHANGING lrec_equnr-wadat_ist.
*   Check actual goods issue date
    IF lrec_equnr-wadat_ist IS INITIAL.
      MESSAGE s009(yse_cs_sd_msg) INTO l_msgtx.
      PERFORM set_log USING lrec_equnr 'E' l_msgtx.
      CONTINUE.
    ENDIF.

*   Get warranty period
    PERFORM get_warr_period USING lrec_equnr-iwerk
                                  lrec_equnr-matnr
                                  s_bukrs-low
                         CHANGING lrec_equnr-cus_warr_months
                                  lrec_equnr-ven_warr_months
                                  l_subrc.
*   Check warranty period
    IF l_subrc <> 0.
      MESSAGE s010(yse_cs_sd_msg) INTO l_msgtx.
      PERFORM set_log USING lrec_equnr 'E' l_msgtx.
      CONTINUE.
    ENDIF.

*   Get New warranty date
    PERFORM get_warr_date USING lrec_equnr-wadat_ist
                                lrec_equnr-cus_warr_months
                                lrec_equnr-ven_warr_months
                       CHANGING lrec_equnr-cus_warr_sta_new
                                lrec_equnr-cus_warr_end_new
                                lrec_equnr-ven_warr_sta_new
                                lrec_equnr-ven_warr_end_new.
*   Check warranty date changes
    IF p_vendor IS INITIAL.
      IF  lrec_equnr-cus_warr_sta_old = lrec_equnr-cus_warr_sta_new
      AND lrec_equnr-cus_warr_end_old = lrec_equnr-cus_warr_end_new.
*       clear new warranty date
        CLEAR: lrec_equnr-cus_warr_sta_new, lrec_equnr-cus_warr_end_new.
        MESSAGE s011(yse_cs_sd_msg) WITH lrec_equnr-equnr INTO l_msgtx.
        PERFORM set_log USING lrec_equnr 'S' l_msgtx.
        CONTINUE.
      ENDIF.
    ELSE.
      IF  lrec_equnr-cus_warr_sta_old = lrec_equnr-cus_warr_sta_new
      AND lrec_equnr-cus_warr_end_old = lrec_equnr-cus_warr_end_new
      AND lrec_equnr-ven_warr_sta_old = lrec_equnr-ven_warr_sta_new
      AND lrec_equnr-ven_warr_end_old = lrec_equnr-ven_warr_end_new.
*       clear new warranty date
        CLEAR: lrec_equnr-cus_warr_sta_new, lrec_equnr-cus_warr_end_new,
               lrec_equnr-ven_warr_sta_new, lrec_equnr-ven_warr_end_new.
        MESSAGE s011(yse_cs_sd_msg) WITH lrec_equnr-equnr INTO l_msgtx.
        PERFORM set_log USING lrec_equnr 'S' l_msgtx.
        CONTINUE.
      ENDIF.
    ENDIF.

*   Update warranty date
    PERFORM upd_warr_date USING lrec_equnr.
  ENDLOOP.

* Sort Log
  SORT i_log BY bukrs iwerk equnr erdat.

ENDFORM.                    " main_process

*&---------------------------------------------------------------------*
*&      Form  get_gi_date
*&---------------------------------------------------------------------*
*       Get actual goods issue date
*----------------------------------------------------------------------*
FORM get_gi_date  USING    equnr
               CHANGING wadat_ist.

  DATA: l_lief_nr TYPE ser01-lief_nr.

  CLEAR wadat_ist.

  SELECT ser01~lief_nr INTO l_lief_nr UP TO 1 ROWS
    FROM objk INNER JOIN ser01
      ON objk~obknr  = ser01~obknr
   WHERE objk~equnr  = equnr
     AND objk~taser  = 'SER01'
     AND ser01~vbtyp = 'J'.
  ENDSELECT.

  SELECT SINGLE wadat_ist INTO wadat_ist
    FROM likp WHERE vbeln = l_lief_nr.

ENDFORM.                    " get_gi_date

*&---------------------------------------------------------------------*
*&      Form  get_warr_period
*&---------------------------------------------------------------------*
*       Get warranty period
*----------------------------------------------------------------------*
FORM get_warr_period  USING iwerk matnr bukrs
                   CHANGING cus_warr_months ven_warr_months
                            subrc.

  DATA : l_prctr   TYPE marc-prctr,
         l_segment TYPE yse_prctr_bl-segment.

  CLEAR: cus_warr_months, ven_warr_months, subrc.

* Get Profit Center
  SELECT SINGLE prctr INTO l_prctr
         FROM marc
         WHERE werks = iwerk
           AND matnr = matnr
           AND lvorm = space.

* Get Segment
  SELECT SINGLE segment INTO l_segment
         FROM yse_prctr_bl
         WHERE prctr = l_prctr.

* Get warranty period
  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
    EXPORTING
      input  = l_segment
    IMPORTING
*      output = l_segment.                                 "MOD-001
      output = gv_plc.                                     "MOD-001

  IF gv_plc CO '0123456789'.                               "MOD-001
    gv_plc+2(1) = ' '.                                     "MOD-001
  ENDIF.                                                   "MOD-001

  SELECT SINGLE cus_warr_months ven_warr_months
         INTO (cus_warr_months, ven_warr_months)
         FROM yse_p025_parms
         WHERE bukrs = bukrs
*           AND plc   = l_segment+0(2).                    "MOD-001
           AND plc   = gv_plc.                             "MOD-001

  subrc = sy-subrc.

ENDFORM.                    " get_warr_period

*&---------------------------------------------------------------------*
*&      Form  get_warr_date
*&---------------------------------------------------------------------*
*       Get New warranty date
*----------------------------------------------------------------------*
FORM get_warr_date  USING    wadat_ist
                                 cus_warr_months  ven_warr_months
                        CHANGING cus_warr_sta_new cus_warr_end_new
                                 ven_warr_sta_new ven_warr_end_new.

  CLEAR: cus_warr_sta_new, cus_warr_end_new,
         ven_warr_sta_new, ven_warr_end_new.

  IF p_vendor IS INITIAL.
*   Get Customer Warranty Date
*   Customer
    cus_warr_sta_new = wadat_ist.
    PERFORM calc_date_in_interval USING cus_warr_sta_new
                                        cus_warr_months
                                        '+'
                               CHANGING cus_warr_end_new.
  ELSE.
*   Get Customer&Vendor Warranty Date
*   Customer
    cus_warr_sta_new = wadat_ist.
    PERFORM calc_date_in_interval USING cus_warr_sta_new
                                        cus_warr_months
                                        '+'
                               CHANGING cus_warr_end_new.
*   Vendor
    ven_warr_sta_new = wadat_ist.
    PERFORM calc_date_in_interval USING ven_warr_sta_new
                                        ven_warr_months
                                        '+'
                               CHANGING ven_warr_end_new.
  ENDIF.

ENDFORM.                    " get_warr_date

*&---------------------------------------------------------------------*
*&      Form  CALC_DATE_IN_INTERVAL
*&---------------------------------------------------------------------*
*       Date +/- year/month/day (into the future/past)
*----------------------------------------------------------------------*
FORM calc_date_in_interval  USING    date
                                     months
                                     signum
                            CHANGING calc_date.

  DATA: l_days    TYPE t5a4a-dlydy,
        l_months  TYPE t5a4a-dlymo,
        l_years   TYPE t5a4a-dlyyr.

  CLEAR calc_date.

  l_months = months.
  CALL FUNCTION 'RP_CALC_DATE_IN_INTERVAL'
    EXPORTING
      date      = date
      days      = l_days
      months    = l_months
      signum    = signum
      years     = l_years
    IMPORTING
      calc_date = calc_date.

ENDFORM.                    " CALC_DATE_IN_INTERVAL

*&---------------------------------------------------------------------*
*&      Form  read_warr
*&---------------------------------------------------------------------*
*       Read warranty date
*----------------------------------------------------------------------*
FORM read_warr  USING    objnr
                CHANGING cus_warr_sta cus_warr_end
                         ven_warr_sta ven_warr_end.

  DATA : rihbgmd TYPE rihbgmd.

  CLEAR: cus_warr_sta, cus_warr_end,
         ven_warr_sta, ven_warr_end.

  CALL FUNCTION 'IREP1_WARRANTY_READ'
    EXPORTING
      i_objnr   = objnr
    IMPORTING
      e_rihbgmd = rihbgmd.
  IF p_vendor IS INITIAL.
    cus_warr_sta = rihbgmd-gwldt_k.
    cus_warr_end = rihbgmd-gwlen_k.
  ELSE.
    cus_warr_sta = rihbgmd-gwldt_k.
    cus_warr_end = rihbgmd-gwlen_k.
    ven_warr_sta = rihbgmd-gwldt_l.
    ven_warr_end = rihbgmd-gwlen_l.
  ENDIF.

ENDFORM.                    " read_warr

*&---------------------------------------------------------------------*
*&      Form  upd_warr_date
*&---------------------------------------------------------------------*
*       Update warranty date
*----------------------------------------------------------------------*
FORM upd_warr_date  USING lrec_equnr TYPE ty_equnr.

  DATA: li_bdc       TYPE TABLE OF bdcdata,
        li_message   TYPE TABLE OF bdcmsgcoll,
        lrec_message TYPE bdcmsgcoll.
  DATA: l_mode       TYPE ctu_mode,
        l_update     TYPE ctu_update,
        l_msgtx      TYPE bapi_msg.

  PERFORM set_bdcdata TABLES li_bdc
                       USING lrec_equnr.

  l_mode   = 'N'.
  l_update = 'S'.
  CALL TRANSACTION 'IE02' USING li_bdc
                           MODE l_mode
                         UPDATE l_update
                  MESSAGES INTO li_message.
  READ TABLE li_message INTO lrec_message WITH KEY msgid  = 'IS'
                                                   msgnr  = '817'
                                                   msgtyp = 'S'.
  IF sy-subrc = 0.
*   Update Successfully
    MESSAGE ID     lrec_message-msgid
            TYPE   lrec_message-msgtyp
            NUMBER lrec_message-msgnr
       WITH lrec_message-msgv1 lrec_message-msgv2
            lrec_message-msgv3 lrec_message-msgv4
       INTO l_msgtx.
    PERFORM set_log USING lrec_equnr lrec_message-msgtyp l_msgtx.
  ELSE.
    LOOP AT li_message INTO lrec_message WHERE msgtyp = 'E'
                                            OR msgtyp = 'A'
                                            OR msgtyp = 'X'.
*     Update Failure, clear new warranty date
      CLEAR: lrec_equnr-cus_warr_sta_new, lrec_equnr-cus_warr_end_new,
             lrec_equnr-ven_warr_sta_new, lrec_equnr-ven_warr_end_new.
      MESSAGE ID     lrec_message-msgid
              TYPE   lrec_message-msgtyp
              NUMBER lrec_message-msgnr
         WITH lrec_message-msgv1 lrec_message-msgv2
              lrec_message-msgv3 lrec_message-msgv4
         INTO l_msgtx.
      PERFORM set_log USING lrec_equnr lrec_message-msgtyp l_msgtx.
    ENDLOOP.
  ENDIF.

  MESSAGE s012(yse_cs_sd_msg).

ENDFORM.                    " upd_warr_date

*&---------------------------------------------------------------------*
*&      Form  SET_BDCDATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM set_bdcdata TABLES li_bdc STRUCTURE rec_bdc
                  USING lrec_equnr TYPE ty_equnr.

  DATA: l_cus_warr_sta_new(10) TYPE c,                     "MOD-002
        l_cus_warr_end_new(10) TYPE c,                     "MOD-002
        l_ven_warr_sta_new(10) TYPE c,                     "MOD-002
        l_ven_warr_end_new(10) TYPE c.                     "MOD-002

  PERFORM set_bdc_dynpro TABLES li_bdc
                          USING 'SAPMIEQ0'
                                '0100'.
  PERFORM set_bdc_field  TABLES li_bdc
                          USING 'RM63E-EQUNR'
                                lrec_equnr-equnr.
  PERFORM set_bdc_field  TABLES li_bdc
                          USING 'BDC_OKCODE'
                                '/00'.

  PERFORM set_bdc_dynpro TABLES li_bdc
                          USING 'SAPMIEQ0'
                                '0101'.
  PERFORM set_bdc_field  TABLES li_bdc
                          USING 'BDC_OKCODE'
                                '=T\05'.

  PERFORM set_bdc_dynpro TABLES li_bdc
                          USING 'SAPMIEQ0'
                                '0101'.
  IF p_vendor IS INITIAL.
    IF lrec_equnr-cus_warr_sta_old <> lrec_equnr-cus_warr_sta_new.
      PERFORM convert_date_to_external
                                 USING lrec_equnr-cus_warr_sta_new
                              CHANGING l_cus_warr_sta_new.
      PERFORM set_bdc_field  TABLES li_bdc
                              USING 'WCHECK_V_H-GWLDT_O'
*                                    lrec_equnr-cus_warr_sta_new.
                                    l_cus_warr_sta_new.    "MOD-002
    ENDIF.
    IF lrec_equnr-cus_warr_end_old <> lrec_equnr-cus_warr_end_new.
      PERFORM convert_date_to_external
                                 USING lrec_equnr-cus_warr_end_new
                              CHANGING l_cus_warr_end_new.
      PERFORM set_bdc_field  TABLES li_bdc
                              USING 'WCHECK_V_H-GWLEN_O'
*                                    lrec_equnr-cus_warr_end_new.
                                     l_cus_warr_end_new.   "MOD-002
    ENDIF.
  ELSE.
    IF lrec_equnr-cus_warr_sta_old <> lrec_equnr-cus_warr_sta_new.
      PERFORM convert_date_to_external
                                 USING lrec_equnr-cus_warr_sta_new
                              CHANGING l_cus_warr_sta_new.
      PERFORM set_bdc_field  TABLES li_bdc
                              USING 'WCHECK_V_H-GWLDT_O'
*                                    lrec_equnr-cus_warr_sta_new.
                                    l_cus_warr_sta_new.    "MOD-002
    ENDIF.
    IF lrec_equnr-cus_warr_end_old <> lrec_equnr-cus_warr_end_new.
      PERFORM convert_date_to_external
                                 USING lrec_equnr-cus_warr_end_new
                              CHANGING l_cus_warr_end_new.
      PERFORM set_bdc_field  TABLES li_bdc
                              USING 'WCHECK_V_H-GWLEN_O'
*                                    lrec_equnr-cus_warr_end_new.
                                     l_cus_warr_end_new.   "MOD-002
    ENDIF.
    IF lrec_equnr-ven_warr_sta_old <> lrec_equnr-ven_warr_sta_new.
      PERFORM convert_date_to_external
                                 USING lrec_equnr-ven_warr_sta_new
                              CHANGING l_ven_warr_sta_new.
      PERFORM set_bdc_field  TABLES li_bdc
                              USING 'WCHECK_V_H-GWLDT_I'
*                                    lrec_equnr-ven_warr_sta_new.
                                    l_ven_warr_sta_new.    "MOD-002
    ENDIF.
    IF lrec_equnr-ven_warr_end_old <> lrec_equnr-ven_warr_end_new.
      PERFORM convert_date_to_external
                                 USING lrec_equnr-ven_warr_end_new
                              CHANGING l_ven_warr_end_new.
      PERFORM set_bdc_field  TABLES li_bdc
                              USING 'WCHECK_V_H-GWLEN_I'
*                                    lrec_equnr-ven_warr_end_new.
                                    l_ven_warr_end_new.    "MOD-002
    ENDIF.
  ENDIF.
  PERFORM set_bdc_field  TABLES li_bdc
                          USING 'BDC_OKCODE'
                                '=BU'.

ENDFORM.                    " SET_BDCDATA

*&---------------------------------------------------------------------*
*&      Form  SET_BDC_DYNPRO
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM set_bdc_dynpro TABLES li_bdc STRUCTURE rec_bdc
                     USING program dynpro.

  DATA: lrec_bdc TYPE bdcdata.

  lrec_bdc-program  = program.
  lrec_bdc-dynpro   = dynpro.
  lrec_bdc-dynbegin = 'X'.
  APPEND lrec_bdc TO li_bdc.

ENDFORM.                    " SET_BDC_DYNPRO

*&---------------------------------------------------------------------*
*&      Form  SET_BDC_FIELD
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM set_bdc_field TABLES li_bdc STRUCTURE rec_bdc
                    USING fnam fval.

  DATA: lrec_bdc TYPE bdcdata.

  lrec_bdc-fnam  = fnam.
  lrec_bdc-fval  = fval.
  APPEND lrec_bdc TO li_bdc.

ENDFORM.                    " SET_BDC_FIELD

*&---------------------------------------------------------------------*
*&      Form  SET_LOG
*&---------------------------------------------------------------------*
*       Set Log
*----------------------------------------------------------------------*
FORM set_log USING lrec_equnr TYPE ty_equnr
                   msgty msgtx.

  DATA: lrec_log   TYPE yse_upd_it_eqwtydate_log.

  MOVE-CORRESPONDING lrec_equnr TO lrec_log.
  lrec_log-bukrs  = s_bukrs-low.
  lrec_log-msgty  = msgty.
  lrec_log-msgtx  = msgtx.
  APPEND lrec_log TO i_log.

ENDFORM.                    " SET_LOG

*&---------------------------------------------------------------------*
*&      Form  display_log
*&---------------------------------------------------------------------*
*       display log
*----------------------------------------------------------------------*
FORM display_log .

  DATA: lrec_layout TYPE slis_layout_alv.

* edit alv fieldcat
  PERFORM edit_alv_fieldcat.

  lrec_layout-colwidth_optimize = 'X'.
  lrec_layout-zebra             = 'X'.

  CALL FUNCTION 'REUSE_ALV_LIST_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'SET_STATUS_10001'
      i_callback_user_command  = 'USER_COMMAND'
      is_layout                = lrec_layout
      it_fieldcat              = i_fieldcat
    TABLES
      t_outtab                 = i_log
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    EXIT.
  ELSE.
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " display_log

*&---------------------------------------------------------------------*
*&      Form  edit_alv_fieldcat
*&---------------------------------------------------------------------*
*       edit alv fieldcat
*----------------------------------------------------------------------*
FORM edit_alv_fieldcat.

  DATA: lrec_fieldcat TYPE slis_fieldcat_alv.

  REFRESH: i_fieldcat.

  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
    EXPORTING
      i_structure_name       = 'YSE_UPD_IT_EQWTYDATE_LOG'
      i_bypassing_buffer     = 'X'
    CHANGING
      ct_fieldcat            = i_fieldcat
    EXCEPTIONS
      inconsistent_interface = 1
      program_error          = 2
      OTHERS                 = 3.
  IF sy-subrc <> 0.
    EXIT.
  ENDIF.
*  LOOP AT i_fieldcat INTO lrec_fieldcat.
*    CASE lrec_fieldcat-fieldname.
*      WHEN ''.
*    ENDCASE.
*    MODIFY i_fieldcat FROM lrec_fieldcat.
*    CLEAR  lrec_fieldcat.
*  ENDLOOP.

ENDFORM.                    " edit_alv_fieldcat

*&---------------------------------------------------------------------*
*&      Form  set_status_10001
*&---------------------------------------------------------------------*
*       set screen status
*----------------------------------------------------------------------*
FORM set_status_10001 USING i_extab TYPE slis_t_extab.

  SET PF-STATUS '10001'.
  SET TITLEBAR  '10001'.

ENDFORM.                               " set_status_10001

*&---------------------------------------------------------------------*
*&      Form  user_command
*&---------------------------------------------------------------------*
*       user command
*----------------------------------------------------------------------*
FORM user_command  USING    i_ucomm    LIKE sy-ucomm
                            i_selfield TYPE slis_selfield.

  CASE i_ucomm.
    WHEN '&IC1'.
      PERFORM display_process USING i_selfield.
  ENDCASE.

ENDFORM.                               " user_command

*&---------------------------------------------------------------------*
*&      Form  display_process
*&---------------------------------------------------------------------*
*       display process
*----------------------------------------------------------------------*
FORM display_process  USING    selfield TYPE slis_selfield.

  DATA lrec_log TYPE yse_upd_it_eqwtydate_log.

  READ TABLE i_log INTO lrec_log INDEX selfield-tabindex.
  IF sy-subrc = 0.
    CASE selfield-fieldname.
      WHEN 'EQUNR'.
        SET PARAMETER ID 'EQN' FIELD lrec_log-equnr.
        CALL TRANSACTION 'IE03' AND SKIP FIRST SCREEN.

    ENDCASE.
  ENDIF.

ENDFORM.                    " display_process
*&---------------------------------------------------------------------*
*&      Form  convert_date_to_external
*&---------------------------------------------------------------------*
*       Conversion: Internal to external date (like screen conversion)
*----------------------------------------------------------------------*
FORM convert_date_to_external  USING    in_date
                               CHANGING out_date.
  CLEAR out_date.
  CALL FUNCTION 'CONVERT_DATE_TO_EXTERNAL'
   EXPORTING
     date_internal                  = in_date
   IMPORTING
     date_external                  = out_date
   EXCEPTIONS
     date_internal_is_invalid       = 1
     OTHERS                         = 2.
  IF sy-subrc <> 0.
    EXIT.
  ENDIF.

ENDFORM.                    " convert_date_to_external

*Text symbol text£º
*B01:Org. Data

*B02:Equipment Data
*Selection text£º
*P_VENDOR:        Update Vendor Warranty
*S_BUKRS:        Company Code
*S_EQUNR:        Equipment
*S_ERDAT:        Equipment creation
*S_IWERK:        Maintenance Plant
