*----------------------------------------------------------------------*
* PROGRAM ID    : YSE_UPLOAD_PIR_SL                                    *
* PROGRAM TITLE : Mass upload PIR and sourcing list for Nanjing CC     *
* AUTHOR        : Johnny Wu                                            *
* DATE          : 17/01/2012                                           *
* DEVELOPMENT ID:                                                      *
*                                                                      *
* CHANGE REQUEST NUMBER: CR2392                                        *
*                                                                      *
* Program Description: Mass upload PIR and sourcing list for Nanjing CC*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
*  DATE    | NAME     |TRANSPORT  | CR# and short Description          *
*----------------------------------------------------------------------*
*
*17/01/2012|Johnny Wu |CD1K969899 |CR2392: Initial version             *
*18/01/2012|Johnny Wu |CD1K969907 |CR2392: Buf Fix                     *
*18/01/2012|Johnny Wu |CD1K969909 |CR2392: Buf Fix                     *
*19/01/2012|Johnny Wu |CD1K969925 |CR2392: Buf Fix                     *
*20/01/2012|Johnny Wu |CD1K969961 |CR2392: Enable background upload    *
*21/01/2012|Johnny Wu |CD1K969993 |CR2392: Enable background upload    *
*03/07/2013|Anda   Wu |CD1K976744 |
*----------------------------------------------------------------------*
report yse_upload_pir_sl
       no standard page heading line-size 255.

*---------------------------------------------------------------------*
* TYPES                                                               *
*---------------------------------------------------------------------*
types: begin of ty_item,
          lifnr type eord-lifnr,
          matnr type eord-matnr,
          ekorg type eine-ekorg,
          werks type eord-werks,
          seperator(1) type c,
          kbetr(11) type c, "like konp-kbetr,"100
          konwa like konp-konwa,"RMB
          kpein(5) type c," like konp-kpein,"1
          kmein type konp-kmein,"PC
          datab type rv13a-datab,"Date Begin
       end of ty_item.

data: gt_item_list type table of ty_item,
      gt_lfile type yse_t_lfile.                     "Local file name

data: gv_process_code type char1.

constants: gc_x type c value 'X'.

*include bdcrecx1.
***********************************************************************
load-of-program.
  include yse_bdcrecx1.

*----------------------------------------------------------------------*
*                          SELECTION SCREEN VARIABLES                  *
*----------------------------------------------------------------------*
  selection-screen begin of block bk1 with frame title text-t01.
  parameters: rb_upld radiobutton group g1 default 'X',      "Do upload
              rb_updt radiobutton group g1.                  "Do update
  selection-screen end of block bk1.

  selection-screen begin of block bk2 with frame title text-t02.
  parameters:
     p_appl type yse_up_appl-appl no-display default '02',"App Type
     p_path(1024) type c lower case,  "Local File Path,
     p_jname  type btcjob no-display default 'YSE_UPLOAD_PIR_SL' .
  selection-screen end of block bk2.

  selection-screen begin of block bk3 with frame title text-bk1.
  parameters: p_pir_up  as checkbox default 'X' modif id m1,
              p_sl_del  as checkbox default 'X' modif id m1,
              p_sl_cre  as checkbox default 'X' modif id m1.
*  PARAMETERS: cb_back AS CHECKBOX DEFAULT 'X' MODIF ID m1.
  selection-screen end of block bk3.


at selection-screen on value-request for p_path.
* Get local file path
  perform select_file.

at selection-screen output.
  perform set_screen.

start-of-selection.
  perform check_selection.
  perform set_bdc_para.
* If the function of  'UPLOAD' is choosed
  if rb_upld = gc_x.
    perform read_file.
*   Write entry to datebase table
    perform write_db.
  endif.

  if rb_updt = gc_x.
    perform mass_update.
  endif.

*&---------------------------------------------------------------------*
*&      Form  SET_BDC_PARA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form set_bdc_para .
*   Set default call transaction settings
  user = sy-uname.

  ctu = 'X'.
  ctumode = 'N'.
  cupdate = 'L'.

  smalllog = ''.
  nodata = '/'.
endform.                    " SET_BDC_PARA
*&---------------------------------------------------------------------*
*&      Form  CHECK_SELECTION
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form check_selection .
  if    rb_upld is not initial.
    if p_path is initial.
      message s000(yse_general) with 'Please enter the file path'(081)
       display like 'E'.
      leave list-processing.
    endif.
*   Check if the file path and name exist
    perform check_filename.
  endif.
* Applicatin Type Check
  perform check_appl.

endform.                    " CHECK_SELECTION
*&---------------------------------------------------------------------*
*&      Form  SELECT_FILE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form select_file .
  data:
    lv_title  type string,     "Referrence title
    lt_file   type filetable,  "Internal table of dialog filenames
    ls_file   type file_table, "Working area for IT_FILE,
    lv_rc     type i.          "Return Code

  lv_title = 'Select a file for upload'(001).
  clear: lt_file[],
         ls_file.

  call method cl_gui_frontend_services=>file_open_dialog
    exporting
      window_title            = lv_title
    changing
      file_table              = lt_file
      rc                      = lv_rc
    exceptions
      file_open_dialog_failed = 1
      cntl_error              = 2
      error_no_gui            = 3
      not_supported_by_gui    = 4
      others                  = 5.
  if sy-subrc <> 0.
    message id sy-msgid type sy-msgty number sy-msgno
              with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  endif.
  if lv_rc = 1.
    read table lt_file index 1 into ls_file.
    if sy-subrc eq 0.
      p_path = ls_file-filename.
    endif.
  endif.
endform.                    " SELECT_FILE
*&---------------------------------------------------------------------*
*&      Form  CHECK_FILENAME
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form check_filename .
  data:
    lv_exist type c.
  call function 'TMP_GUI_GET_FILE_EXIST'
    exporting
      fname          = p_path
    importing
      exist          = lv_exist
    exceptions
      fileinfo_error = 1
      others         = 2.
  if sy-subrc <> 0 or lv_exist is initial.
    message s000(yse_general)
      with 'Please enter an invaild filename'(010)
    display like 'E'.
  endif.
endform.                    " CHECK_FILENAME
*&---------------------------------------------------------------------*
*&      Form  READ_FILE
*&---------------------------------------------------------------------*
*       Read local file into a internal table
*----------------------------------------------------------------------*
form read_file .
  data: lv_filename type string.

  lv_filename = p_path.
  perform get_from_pres in program yam_common_routines
                                    tables  gt_lfile
                                    using   lv_filename
                                            ycl_se_utility=>c_ft_asc
                                            ' '.
  if gt_lfile is not initial.
    delete gt_lfile index 1.
    delete gt_lfile
      where text is initial.
  endif.
endform.                    " READ_FILE
*&---------------------------------------------------------------------*
*&      Form  CHECK_APPL
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form check_appl .
  data: lt_up_appl type table of yse_up_appl,
        ls_up_appl type yse_up_appl.

  select * from yse_up_appl into table lt_up_appl
    where appl = p_appl.                              "#EC CI_SGLSELECT

  if sy-subrc <> 0 .
    if p_appl = '02'.
      ls_up_appl-appl  = '02'.
      ls_up_appl-description = 'Upload PIR and SL for Nanjing CC'.
      insert yse_up_appl from ls_up_appl.
    else.
      message s000(yse_general)
        with 'Applicaiton'(014) p_appl
             ' does not exist!'(005)
      display like 'E'.
      leave list-processing.
    endif.
  endif.
endform.                    " CHECK_APPL
*&---------------------------------------------------------------------*
*&      Form  SET_SCREEN
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form set_screen .

  loop at screen.
    if screen-group1 = 'M1'.
      screen-input = '0'.
      screen-invisible = '1'.
      modify screen.
    endif.
  endloop.

  loop at screen.
    if    screen-name cs 'P_PATH'
      and  rb_upld is  initial .
      screen-input = '0'.
      screen-invisible = '1'.
      modify screen.
    endif.
    if    rb_updt is  initial
      and screen-group1 = 'M1'.
      screen-input = '0'.
      screen-invisible = '1'.
      modify screen.
    endif.
  endloop.
endform.                    " SET_SCREEN
*&---------------------------------------------------------------------*
*&      Form  WRITE_DB
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form write_db .
  data: lv_xml type string,
        ls_up_file type yse_up_file.

  if gt_lfile is initial.
    message w000(yse_general) with 'No Input Data'(002).
  else.
    call transformation yse_trans_file source lfile = gt_lfile result xml lv_xml.
  endif.

  clear: gt_lfile[].
  call transformation yse_trans_file source xml lv_xml result lfile = gt_lfile.
  call function 'SYSTEM_UUID_CREATE'
    importing
      uuid = ls_up_file-guid.
* Prepare entry for insert
  ls_up_file-appl = p_appl.
  ls_up_file-create_date = sy-datum.
  ls_up_file-create_time = sy-uzeit.
  ls_up_file-create_by   = sy-uname.
  ls_up_file-xml = lv_xml.
  insert yse_up_file from ls_up_file.

  if sy-subrc = 0.
    message s000(yse_general) with 'File uploaded successfully'(003)
                                     'for applicaiton'(080)
                                     p_appl.
  endif.
endform.                    " WRITE_DB
*&---------------------------------------------------------------------*
*&      Form  MASS_UPDATE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form mass_update .
  perform get_xml_data.
  perform convert_to_struc.
  if p_pir_up is not initial.
    perform handle_pir.
  endif.
  if p_sl_del is not initial.
    perform delete_sourcing_list."Delete Source List
  endif.
  if p_sl_cre is not initial.
    perform create_sourcing_list.
  endif.
endform.                    " MASS_UPDATE

*&---------------------------------------------------------------------*
*&      Form  CONVERT_TO_STRUC
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form convert_to_struc .
  constants: c_tab type c value cl_abap_char_utilities=>horizontal_tab.

  data: ls_lfile type yse_s_lfile,
        lv_line type string,
        lt_field type table of string,
        lv_field type string.

  data: lo_wa type ref to data,
        lo_itab type ref to data.

  field-symbols: <fs_tab> type standard table,
                 <fs_wa> type any,
                 <fs_field> type any,
                 <fs_item> type ty_item.

  create data lo_wa type ty_item.
  create data lo_itab type table of  ty_item initial size 1 .

  assign lo_wa->* to <fs_wa>.
  assign lo_itab->* to <fs_tab>.
  assign gt_item_list to <fs_tab>.

  loop at gt_lfile into ls_lfile.
    clear: lt_field.
    lv_line = ls_lfile-text.
    split lv_line at c_tab into table lt_field.

    loop at lt_field into lv_field.
      assign component sy-tabix of structure <fs_wa> to <fs_field>.
      <fs_field> = lv_field.
    endloop.
    append <fs_wa> to <fs_tab>.
  endloop.
  sort gt_item_list.
  delete adjacent duplicates from gt_item_list.
  if gt_item_list is initial.
    message s000(yse_general)
      with 'No data to be processed in table yse_up_file'(040)
           'Please upload in advance'(041)
    display like 'E'.
    leave list-processing.
  endif.
  update yse_up_file set processed = 'X' where processed = ''
                                          and  appl = p_appl.
  loop at gt_item_list assigning <fs_item>.
    call function 'CONVERSION_EXIT_ALPHA_INPUT'
      exporting
        input  = <fs_item>-lifnr
      importing
        output = <fs_item>-lifnr.

    call function 'CONVERSION_EXIT_MATN1_INPUT'
      exporting
        input        = <fs_item>-matnr
      importing
        output       = <fs_item>-matnr
      exceptions
        length_error = 1
        others       = 2.
    if sy-subrc <> 0.
      message id sy-msgid type sy-msgty number sy-msgno
              with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    endif.
  endloop.
endform.                    " CONVERT_TO_STRUC

*&---------------------------------------------------------------------*
*&      Form  CREATE_SOURCING_LIST
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form create_sourcing_list .
  data: ls_item type ty_item,
        lv_fval type bdcdata-fval.

  loop at gt_item_list into ls_item.
    clear: bdcdata[].
    perform bdc_dynpro      using 'SAPLMEOR' '0200'.
    perform bdc_field       using 'BDC_CURSOR'
                                  'EORD-MATNR'.
    perform bdc_field       using 'BDC_OKCODE'
                                  '/00'.
    perform bdc_field       using 'EORD-MATNR'
                                  ls_item-matnr.
    perform bdc_field       using 'EORD-WERKS'
                                  ls_item-werks.

    perform bdc_dynpro      using 'SAPLMEOR' '0205'.
    perform bdc_field       using 'BDC_CURSOR'
                                  'EORD-AUTET(01)'.
    perform bdc_field       using 'BDC_OKCODE'
                                  '=BU'.
    write sy-datum to lv_fval dd/mm/yyyy.
    perform bdc_field       using 'EORD-VDATU(01)'
                                  lv_fval.
    perform bdc_field       using 'EORD-BDATU(01)'
                                  '31.12.9999'.
    perform bdc_field       using 'EORD-LIFNR(01)'
                                  ls_item-lifnr.
    perform bdc_field       using 'EORD-EKORG(01)'
                                  ls_item-ekorg.
    perform bdc_field       using 'RM06W-FESKZ(01)'
                                  'X'.
    perform bdc_field       using 'EORD-AUTET(01)'
                                  '1'.

    perform bdc_transaction using 'ME01'.
  endloop.
endform.                    " CREATE_SOURCING_LIST

*&---------------------------------------------------------------------*
*&      Form  HANDLE_PIR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form handle_pir .
  data: ls_item type ty_item,
        lv_exist(1) type c.

  loop at gt_item_list into ls_item.

    perform check_pir_exist using ls_item changing lv_exist.

    if lv_exist = ''.
      "Create
      perform create_pir using ls_item.
    else.
      "Change
      perform change_pir using ls_item.
    endif.

  endloop.
endform.                    " HANDLE_PIR

*&---------------------------------------------------------------------*
*&      Form  DELETE_SOURCING_LIST
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form delete_sourcing_list .
  data: ls_item type ty_item.

  loop at gt_item_list into ls_item.

    select count(*) from eord where matnr = ls_item-matnr
                                and werks = ls_item-werks.
    if sy-subrc <> 0 ."Not Sourcing List
      continue.
    endif.

    clear: bdcdata[].
    perform bdc_dynpro      using 'SAPLMEOR' '0200'.
    perform bdc_field       using 'BDC_CURSOR'
                                  'EORD-MATNR'.
    perform bdc_field       using 'BDC_OKCODE'
                                  '/00'.
    perform bdc_field       using 'EORD-MATNR'
                                  ls_item-matnr.
    perform bdc_field       using 'EORD-WERKS'
                                  ls_item-werks.
    perform bdc_dynpro      using 'SAPLMEOR' '0205'.
    perform bdc_field       using 'BDC_CURSOR'
                                  'EORD-EBELP(01)'.
    perform bdc_field       using 'BDC_OKCODE'
                                  '=SA'.
    perform bdc_dynpro      using 'SAPLMEOR' '0205'.
    perform bdc_field       using 'BDC_CURSOR'
                                  'EORD-EBELP(01)'.
    perform bdc_field       using 'BDC_OKCODE'
                                  '=LOES'.

    perform bdc_dynpro      using 'SAPLSPO1' '0200'.
    perform bdc_field       using 'BDC_OKCODE'
                                  '=YES'.

    perform bdc_dynpro      using 'SAPLMEOR' '0205'.
    perform bdc_field       using 'BDC_CURSOR'
                                  'EORD-EBELP(01)'.
    perform bdc_field       using 'BDC_OKCODE'
                                  '=BU'.
    perform bdc_transaction using 'ME01'.
  endloop.
endform.                    " DELETE_SOURCING_LIST

*&---------------------------------------------------------------------*
*&      Form  CHANGE_PIR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_LS_ITEM  text
*----------------------------------------------------------------------*
form change_pir  using    p_item type ty_item.
  data: lv_fval type bdcdata-fval.

  clear: bdcdata[].
  perform bdc_dynpro      using 'SAPMM06I' '0100'.
  perform bdc_field       using 'BDC_CURSOR'
                                'EINA-LIFNR'.
  perform bdc_field       using 'BDC_OKCODE'
                                '/00'.
  perform bdc_field       using 'EINA-LIFNR'
                                p_item-lifnr.
  perform bdc_field       using 'EINA-MATNR'
                                p_item-matnr.
  perform bdc_field       using 'EINE-EKORG'
                                p_item-ekorg.
  perform bdc_field       using 'EINE-WERKS'
                                p_item-werks.
  perform bdc_field       using 'RM06I-NORMB'
                                'X'.

  perform bdc_dynpro      using 'SAPMM06I' '0101'.
  perform bdc_field       using 'BDC_CURSOR'
                                'EINA-MAHN1'.
  perform bdc_field       using 'BDC_OKCODE'
                                '=KO'.
  perform bdc_field       using 'EINA-MAHN1'
                                '7'.
  perform bdc_field       using 'EINA-MAHN2'
                                  '14'.
  perform bdc_field       using 'EINA-MAHN3'
                                  '28'.

  perform bdc_dynpro      using 'SAPLV14A' '0102'.
  perform bdc_field       using 'BDC_CURSOR'
                                'VAKE-DATAB(01)'.
  perform bdc_field       using 'BDC_OKCODE'
                                '=NEWD'.

  perform bdc_dynpro      using 'SAPMV13A' '0201'.
  perform bdc_field       using 'BDC_CURSOR'
                                'KONP-KPEIN(01)'.
  perform bdc_field       using 'BDC_OKCODE'
                                '=SICH'.
  write sy-datum to lv_fval dd/mm/yyyy.
  perform bdc_field       using 'RV13A-DATAB'
                                lv_fval.
  perform bdc_field       using 'RV13A-DATBI'
                                '31.12.9999'.
  perform bdc_field       using 'KONP-KBETR(01)'
                                p_item-kbetr."100
  perform bdc_field       using 'KONP-KONWA(01)'
                                p_item-konwa."RMB
  perform bdc_field       using 'KONP-KPEIN(01)'
                                p_item-kpein. "1
  perform bdc_field       using 'KONP-KMEIN(01)'
                                p_item-kmein. "PC

  perform bdc_transaction using 'ME12'.
endform.                    " CHANGE_PIR

*&---------------------------------------------------------------------*
*&      Form  CHECK_PIR_EXIST
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_LS_ITEM  text
*----------------------------------------------------------------------*
form check_pir_exist  using    p_item type ty_item changing p_exist type c.
  data: ls_meico type meico,
        ls_eina type eina.
*        ls_eine TYPE eine.
*        ls_meicr TYPE meicr.

  ls_meico-lifnr = p_item-lifnr.
  ls_meico-matnr = p_item-matnr.
  ls_meico-leina = 'X'.

  p_exist = 'X'.
  "EINA
  call function 'ME_READ_INFORECORD'
    exporting
      incom          = ls_meico
      i_no_other_org = 'X'
      i_really_exist = 'X'
    importing
      einadaten      = ls_eina
*      excom          = ls_meicr
    exceptions
      not_found      = 1.

  if sy-subrc <> 0 .
    p_exist = ''.
  else.
    "EINE
    ls_meico-infnr = ls_eina-infnr.
    ls_meico-esokz = 0.
    ls_meico-ekorg = p_item-ekorg.
    ls_meico-werks = p_item-werks.
    ls_meico-onlye = 'X'.
    ls_meico-onlyw = 'X'.
    ls_meico-skipdl = 'X'.
    call function 'ME_READ_INFORECORD'
      exporting
        incom          = ls_meico
        i_no_other_org = ' '
        i_really_exist = 'X'
*      IMPORTING
*        einedaten      = ls_eine
*        excom          = ls_meicr
      exceptions
        not_found      = 1.
    if sy-subrc <> 0.
      p_exist = ''.
    endif.
  endif.
endform.                    " CHECK_PIR_EXIST

*&---------------------------------------------------------------------*
*&      Form  CREATE_PIR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form create_pir using    p_item type ty_item .
  data: lv_fval   type bdcdata-fval,
        lv_ekgrp  type eine-ekgrp.

  clear: bdcdata[].
  perform bdc_dynpro      using 'SAPMM06I' '0100'.
  perform bdc_field       using 'BDC_CURSOR'
                                'EINA-LIFNR'.
  perform bdc_field       using 'BDC_OKCODE'
                                '/00'.
  perform bdc_field       using 'EINA-LIFNR'
                                p_item-lifnr.
  perform bdc_field       using 'EINA-MATNR'
                                p_item-matnr.
  perform bdc_field       using 'EINE-EKORG'
                                p_item-ekorg.
  perform bdc_field       using 'EINE-WERKS'
                                p_item-werks.
  perform bdc_field       using 'RM06I-NORMB'
                                'X'.

  perform bdc_dynpro      using 'SAPMM06I' '0101'.
  perform bdc_field       using 'BDC_CURSOR'
                                'EINA-MAHN3'.
  perform bdc_field       using 'BDC_OKCODE'
                                '/00'.
  perform bdc_field       using 'EINA-MAHN1'
                                '7'.
  perform bdc_field       using 'EINA-MAHN2'
                                  '14'.
  perform bdc_field       using 'EINA-MAHN3'
                                  '28'.

  perform bdc_dynpro      using 'SAPMM06I' '0102'.
  perform bdc_field       using 'BDC_CURSOR'
                                'EINE-NETPR'.
  perform bdc_field       using 'BDC_OKCODE'
                                '=KO'.
  if p_item-ekorg = 'CD01'.
    lv_ekgrp = '200'.
  else.
    lv_ekgrp = '002'.
  endif.

  perform bdc_field       using 'EINE-EKGRP'
                                lv_ekgrp.

  perform bdc_field       using 'EINE-NORBM'
                                '1'.

  perform bdc_field       using 'EINE-NETPR'
                                  p_item-kbetr.
  perform bdc_field       using 'EINE-WAERS'
                                  p_item-konwa.

  perform bdc_field       using 'EINE-PEINH'
                                  p_item-kpein.
  perform bdc_field       using 'EINE-BPRME'
                                  p_item-kmein.


  perform bdc_dynpro      using 'SAPMV13A' '0201'.
  perform bdc_field       using 'BDC_CURSOR'
                                'RV13A-DATAB'.
  perform bdc_field       using 'BDC_OKCODE'
                                '=SICH'.
  write sy-datum to lv_fval dd/mm/yyyy.
  perform bdc_field       using 'RV13A-DATAB'
                                lv_fval.
  perform bdc_field       using 'RV13A-DATBI'
                                '31.12.9999'.

  perform bdc_transaction using 'ME11'.
endform.                    " CREATE_PIR

*&---------------------------------------------------------------------*
*&      Form  GET_XML_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form get_xml_data .
  data: lt_lfile_tmp type yse_t_lfile,
        lt_up_file type table of yse_up_file,
        ls_up_file type yse_up_file.

  select * from yse_up_file into table lt_up_file where appl = p_appl "#EC CI_NOFIELD
                                                    and processed = abap_false.

  loop at lt_up_file into ls_up_file.
    clear: lt_lfile_tmp.
    if ls_up_file-xml is initial.
      continue.
    endif.

    call transformation yse_trans_file source xml ls_up_file-xml
                                       result lfile = lt_lfile_tmp.

    append lines of lt_lfile_tmp to gt_lfile.
  endloop.
endform.                    " GET_XML_DATA

*Text symbol text£º
*001:Select a file for upload
*002:No Input Data
*003:File uploaded successfully
*005: does not exist!
*010:Please enter an invaild filename
*014:Applicaiton
*020:Batch job & has been started...
*080:for applicaiton
*081:Please enter the file path
*BK1:Upload Selection
*E00:Error opening dataset, return code:
*I01:Create group
*I02:BDC_OPEN_GROUP
*I03:BDC_INSERT
*I04:BDC_CLOSE_GROUP
*I05:returncode:
*I06:Fehlermappe wurde erzeugt
*T01:Functions

*T02:Selection Criterial
*Selection text£º
*P_PATH:        Local file path
*P_PIR_UP:        PIR Update
*P_SL_CRE:        Source List Creation
*P_SL_DEL:        Source List Deletion
*RB_UPDT:        Update
*RB_UPLD:        Upload
