*&---------------------------------------------------------------------*
*&  Include           YSE_REN_PL_DEPOT_S0200                           *
*&---------------------------------------------------------------------*

************************************************************************
* MODULE STATUS_0200 OUTPUT                                            *
************************************************************************
MODULE STATUS_0200 OUTPUT.
  CASE GV_RTYPE.
    WHEN '01'.
      SET TITLEBAR '200'.
    WHEN '03'.
      SET TITLEBAR '201'.
    WHEN '04'.
      SET TITLEBAR '202'.
  ENDCASE.
  SET PF-STATUS 'STATUS200'.
ENDMODULE.                 " STATUS_0200  OUTPUT

************************************************************************
* MODULE PREPARE_SCREEN_0200 OUTPUT                                    *
************************************************************************
MODULE PREPARE_SCREEN_0200 OUTPUT.

* If container is still initial
  IF GO_CONT_0200 IS INITIAL.

*   Create the container
    CREATE OBJECT GO_CONT_0200
      EXPORTING
        REPID           = SY-REPID
        DYNNR           = SY-DYNNR
        LIFETIME        = CNTL_LIFETIME_DYNPRO
        EXTENSION       = 280
        SIDE            = CL_GUI_DOCKING_CONTAINER=>DOCK_AT_BOTTOM.

*   Create the ALV control
    CREATE OBJECT GO_ALV_0200
      EXPORTING
        I_PARENT  =  GO_CONT_0200.

*   Build fieldcatalog
    CLEAR: IT_FIELDCAT, IT_FIELDCAT[].

*   Fetch the field catalog for detail output
    CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
      EXPORTING
        I_BUFFER_ACTIVE              = ' '
        I_STRUCTURE_NAME             = 'YSE_RENT_COPA_DET_OUT'
      CHANGING
        CT_FIELDCAT                  = IT_FIELDCAT[].

*    Adapt fieldcatalog to chosen line
     LOOP AT IT_FIELDCAT.
       CASE IT_FIELDCAT-FIELDNAME.
         WHEN 'WAERS'.
           IT_FIELDCAT-NO_OUT = 'X'.
         WHEN 'WW006' OR 'WW007'.
           IF GV_RTYPE NE '01'.
             IT_FIELDCAT-NO_OUT = 'X'.
           ENDIF.
         WHEN 'VREVU'.
           IF GV_RTYPE = '03'.
             IT_FIELDCAT-COLTEXT = 'Depreciation'(013).
             IT_FIELDCAT-TOOLTIP = 'Depreciation Costs'(014).
           ELSEIF GV_RTYPE = '04'.
             IT_FIELDCAT-COLTEXT = 'Service'(015).
             IT_FIELDCAT-TOOLTIP = 'Service Costs'(016).
           ENDIF.
       ENDCASE.
       MODIFY IT_FIELDCAT.
     ENDLOOP.

    CALL METHOD GO_ALV_0200->SET_TABLE_FOR_FIRST_DISPLAY
      EXPORTING
          I_SAVE        =  'A'
*         IS_VARIANT    =  LS_VARIANT
*         IS_LAYOUT     =  WA_LAYOUT
      CHANGING
          IT_OUTTAB         =  IT_DETAIL[]
          IT_FIELDCATALOG   =  IT_FIELDCAT[].

  ENDIF.

* Set screen variables
  SV_BUKRS = P_BUKRS.
  SV_FYEAR = P_GJAHR.

ENDMODULE.                 " PREPARE_SCREEN_200  OUTPUT

************************************************************************
* MODULE USER_COMMAND_0200 INPUT                                       *
************************************************************************
MODULE USER_COMMAND_0200 INPUT.

  CASE GV_OKCOD.
    WHEN 'BACK' OR 'EXIT'.
      CALL METHOD GO_ALV_0200->FREE.
      FREE GO_ALV_0200.
      CALL METHOD GO_CONT_0200->FREE.
      FREE GO_CONT_0200.
      LEAVE TO SCREEN 0.

  ENDCASE.
ENDMODULE.                 " USER_COMMAND_0100 INPUT
