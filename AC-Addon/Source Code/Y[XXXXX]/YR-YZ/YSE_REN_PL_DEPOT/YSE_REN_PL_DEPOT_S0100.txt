*&---------------------------------------------------------------------*
*&  Include           YSE_REN_PL_DEPOT_S0100                           *
*&---------------------------------------------------------------------*

************************************************************************
* Module STATUS_0100 OUTPUT                                            *
************************************************************************
MODULE STATUS_0100 OUTPUT.
  SET TITLEBAR '100'.
  SET PF-STATUS 'STATUS100'.
ENDMODULE.                 " STATUS_0100  OUTPUT

************************************************************************
* Module PREPARE_SCREEN_0100 OUTPUT                                    *
************************************************************************
MODULE PREPARE_SCREEN_0100 OUTPUT.

* If container is still initial
  IF GO_CONT_0100 IS INITIAL.

*   Create the container
    CREATE OBJECT GO_CONT_0100
      EXPORTING
        REPID           = SY-REPID
        DYNNR           = SY-DYNNR
        LIFETIME        = CNTL_LIFETIME_DYNPRO
        EXTENSION       = 280
        SIDE            = CL_GUI_DOCKING_CONTAINER=>DOCK_AT_BOTTOM.

*   Create the ALV control
    CREATE OBJECT GO_ALV_0100
      EXPORTING
        I_PARENT  =  GO_CONT_0100.

*   Create Event handler control
    CREATE OBJECT GO_EVTH_0100.

*   Enable events on ALV
    SET HANDLER GO_EVTH_0100->HANDLE_DOUBLE_CLICK FOR GO_ALV_0100.

    WA_LAYOUT-CTAB_FNAME = 'CELLCOLOR'.
    WA_LAYOUT-CWIDTH_OPT = GC_TRUE.

*   Fieldcatalog is already built (was needed to create dynamic table) but now arrange sorting
*   General
    WA_SORT_100-UP        = GC_TRUE.  " sort ascending
*   Period
    WA_SORT_100-SPOS      = '1'.
    WA_SORT_100-FIELDNAME = 'PERNR'.
    APPEND WA_SORT_100 TO IT_SORT_100.
*   Description
    WA_SORT_100-SPOS      = '2'.
    WA_SORT_100-FIELDNAME = 'COTYP'.
    APPEND WA_SORT_100 TO IT_SORT_100.
*   GAC
    WA_SORT_100-SPOS      = '3'.
    WA_SORT_100-FIELDNAME = 'GAC'.
    APPEND WA_SORT_100 TO IT_SORT_100.
*   PGC
    WA_SORT_100-SPOS      = '4'.
    WA_SORT_100-FIELDNAME = 'PGC'.
    APPEND WA_SORT_100 TO IT_SORT_100.

*   Set for display
    CALL METHOD GO_ALV_0100->SET_TABLE_FOR_FIRST_DISPLAY
      EXPORTING
        I_SAVE        =  'A'
*       IS_VARIANT    =  LS_VARIANT
        IS_LAYOUT     =  WA_LAYOUT
      CHANGING
        IT_OUTTAB         = <IT_RESULT>
        IT_FIELDCATALOG   = IT_FCAT_100[]
        IT_SORT           = IT_SORT_100.

  ENDIF.    " GO_CONT_0100 IS INITIAL

* Set screen variables
  SV_BUKRS = P_BUKRS.
  SV_FYEAR = P_GJAHR.
  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
    EXPORTING
      INPUT  = P_START
    IMPORTING
      OUTPUT = SV_ANGDT.
  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
    EXPORTING
      INPUT  = P_END
    IMPORTING
      OUTPUT = SV_BNDDT.

ENDMODULE.                 " PREPARE_SCREEN_0100  OUTPUT

************************************************************************
* Module USER_COMMAND_0100 INPUT                                       *
************************************************************************
MODULE USER_COMMAND_0100 INPUT.

  CASE GV_OKCOD.
    WHEN 'BACK' OR 'EXIT'.
      CALL METHOD GO_ALV_0100->FREE.
      FREE GO_ALV_0100.
      CALL METHOD GO_CONT_0100->FREE.
      FREE GO_CONT_0100.
      LEAVE TO SCREEN 0.
  ENDCASE.
ENDMODULE.                 " USER_COMMAND_0100 INPUT
