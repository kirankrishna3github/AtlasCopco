*&---------------------------------------------------------------------*
*& Report  YSE_FI_RR_CHECK
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& Report for Revenue Recognition overview per Contract Line Item      *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 18.07.2011
*  Change Request Number : CR1738
*  Transport request Nr. : CD1K966686
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME           | CORRECTION NR. | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 14.10.2011 | Nanda . S      | CD1K968392     | CR 2268     *
*----------------------------------------------------------------------*
* MOD-002 | 20.11.2011 | J. Smets       | CD1K968392     | BUGFIX      *
*         | Start- & enddate at item level                             *
*----------------------------------------------------------------------*
* MOD-003 | 29.12.2011 | Nanda . S      | CD1K969638     | CR 2268     *
*----------------------------------------------------------------------*
* MOD-004 | 31.12.2011 | Nanda . S      | CD1K969648     | CR 2268     *
*         |  Reversal of Changes Mod-002|                              *
*----------------------------------------------------------------------*
* MOD-005 | 29.06.2012 | J. Smets       | CD1K972406     | BUGFIX      *
*         | Additional fields                                          *
*----------------------------------------------------------------------*
* MOD-006 | 07.04.2014 | D. Shireesha   | CD1K980773     | CR 3177     *
*         | To add Contract type in the report output                  *
************************************************************************

REPORT  yse_fi_rr_check.

TABLES: vbrevk,
        vbreve,
        vbrevr,
        vbak,
        vbap,
        viser02,                                            "MOD-005
        veda,
        vbkd,
        fplt,
        fpla,
        coep,
        cobk,
        konv,
        tcurr.

* ALV grid
TYPE-POOLS: slis.

TYPES: BEGIN OF ty_vbap,
         vbeln     TYPE vbeln,
         posnr     TYPE posnr,
         netwr     TYPE netwr,
         matnr     TYPE matnr,
         prctr     TYPE prctr,
         prodh     TYPE prodh_d,
         objnr     TYPE j_objnr,
         waerk     TYPE waers,
         vkorg     TYPE vkorg,
         vtweg     TYPE vtweg,
         spart     TYPE spart,
         vkbur     TYPE vkbur,
         knumv     TYPE knumv,
         vbegdat   TYPE vbdat_veda,
         venddat   TYPE vndat_veda,
*** MOD-005 * begin ***
         matkl     TYPE matkl,
         vkgrp     TYPE vkgrp,
         equnr     TYPE equnr,
         obknr     TYPE objknr,
         obzae     TYPE objza,
*** MOD-005 * end ***
*** MOD-006 * Begin ***
         auart     TYPE auart,
*** MOD-006 * End ***
       END OF ty_vbap.

TYPES: BEGIN OF ty_veda,
         vbeln     TYPE vbeln,
         vposn     TYPE posnr,
         vbegdat   TYPE datum,
         venddat   TYPE datum,
         vkuegru   TYPE kuegru,
       END OF ty_veda.

TYPES: BEGIN OF ty_vbrevk,
         vbeln     TYPE vbeln_va,
         posnr     TYPE posnr_va,
         sakrr     TYPE saknr,
         sakrrk    TYPE saknr,
         waerk     TYPE waers,
         wrbtr     TYPE wrbtr,
       END OF ty_vbrevk.

TYPES: BEGIN OF ty_vbreve,
         vbeln     TYPE vbeln_va,
         posnr     TYPE posnr_va,
         sakrv     TYPE saknr,
         bdjpoper  TYPE rr_bdjpoper,
         popupo    TYPE rr_popupo,
         rrsta     TYPE rr_status,
         wrbtr     TYPE wrbtr,
       END OF ty_vbreve.

TYPES: BEGIN OF ty_vbrevr,
         vbeln     TYPE vbeln_va,
         posnr     TYPE posnr_va,
         vbeln_n   TYPE vbeln,
         posnr_n   TYPE posnr,
         wrbtr     TYPE wrbtr,
       END OF ty_vbrevr.

TYPES: BEGIN OF ty_fplt,
         vbeln     TYPE vbeln_va,
         posnr     TYPE posnr_va,
         fplnr     TYPE fplnr,
*         bedat     TYPE bedat_fp,                          "MOD-002
*         endat     TYPE endat_fp,                          "MOD-002
*         lodat     TYPE tadat,                             "MOD-002
*         tndat     TYPE tbdat,                             "MOD-002
         bedat     TYPE bedat_fp,                          "MOD-004
         endat     TYPE endat_fp,                          "MOD-004
*         lodat     TYPE tadat,                             "MOD-004
*         tndat     TYPE tbdat,                             "MOD-004
         afdat     TYPE fkdat,
         fakwr     TYPE fakwr,
         fksaf     TYPE fksaf,
         fkdat     TYPE bfdat,
         nfdat     TYPE nfdat,
         autte     TYPE autte,
       END OF ty_fplt.

TYPES: BEGIN OF ty_coep,
         objnr     TYPE j_objnr,
         twaer     TYPE twaer,
         wtgbtr    TYPE wtgxxx,
         belnr     TYPE co_belnr,
         buzei     TYPE co_buzei,
       END OF ty_coep.

TYPES: BEGIN OF ty_konv,
         knumv     TYPE knumv,
         kposn     TYPE kposn,
         kschl     TYPE kschl,
         zaehk     TYPE dzaehk,
         kwert     TYPE kwert,
         waers     TYPE waers,
       END OF ty_konv.

TYPES: BEGIN OF ty_curr,
         kurst     TYPE kurst,
         fcurr     TYPE fcurr,
         tcurr     TYPE tcurr_curr,
         gdatu     TYPE gdatu_inv,
         ukurs     TYPE ukurs_curr,
       END OF ty_curr.

*** MOD-005 * begin ***
TYPES: BEGIN OF ty_eqkt,
         equnr     TYPE equnr,
         eqktx     TYPE ktx01,
       END OF ty_eqkt.

TYPES: BEGIN OF ty_tvgrt,
         vkgrp     TYPE vkgrp,
         vkgrpt    TYPE bezei20,
       END OF ty_tvgrt.
*** MOD-005 * end ***

* Internal table with Contract Items
DATA: gt_vbap    TYPE HASHED TABLE OF ty_vbap
                      WITH UNIQUE KEY vbeln posnr
                      WITH HEADER LINE.
*** MOD-005 * end ***
DATA: gt_vbapi TYPE TABLE OF ty_vbap
               WITH HEADER LINE.
*** MOD-005 * end ***
* Begin of Mod-004
*      wa_vbap    TYPE ty_vbap.                              "MOD-002
* End of Mod-004

* Internal table with Contract Data
DATA: gt_veda    TYPE TABLE OF ty_veda
                      WITH HEADER LINE.

* Internal table with Control Lines
DATA: gt_vbrevk  TYPE SORTED TABLE OF ty_vbrevk
                      WITH NON-UNIQUE KEY vbeln posnr
                      WITH HEADER LINE.

* Internal table with Revenue Recognition Lines
DATA: gt_vbreve  TYPE SORTED TABLE OF ty_vbreve
                      WITH NON-UNIQUE KEY vbeln posnr
                      WITH HEADER LINE.

* Internal table with Reference Document Lines
DATA: gt_vbrevr  TYPE SORTED TABLE OF ty_vbrevr
                      WITH NON-UNIQUE KEY vbeln posnr
                      WITH HEADER LINE.

* Internal table with Billing Plan Lines
DATA: gt_fplt    TYPE SORTED TABLE OF ty_fplt
                      WITH NON-UNIQUE KEY vbeln posnr
                      WITH HEADER LINE.

* Internal table with CO Data (Cost / Revenue)
DATA: gt_coep    TYPE SORTED TABLE OF ty_coep
                      WITH NON-UNIQUE KEY objnr
                      WITH HEADER LINE.

* Internal table with Conditions
DATA: gt_konv    TYPE HASHED TABLE OF ty_konv
                      WITH UNIQUE KEY knumv kposn kschl
                      WITH HEADER LINE,
      gt_konvi   TYPE TABLE OF ty_konv
                      WITH HEADER LINE.

* Internal table with Exchange Rates
DATA: gt_curr    TYPE HASHED TABLE OF ty_curr
                      WITH UNIQUE KEY kurst fcurr tcurr
                      WITH HEADER LINE,
      gt_curri   TYPE TABLE OF ty_curr
                      WITH HEADER LINE.

*** MOD-005 * begin ***
* Internal table with Equipment Descriptions
DATA: gt_eqkt    TYPE HASHED TABLE OF ty_eqkt
                      WITH UNIQUE KEY equnr
                      WITH HEADER LINE,
      gt_eqkti   TYPE TABLE OF ty_eqkt
                      WITH HEADER LINE.

* Internal table with Sales Groups Texts
DATA: gt_tvgrt   TYPE HASHED TABLE OF ty_tvgrt
                      WITH UNIQUE KEY vkgrp
                      WITH HEADER LINE,
      gt_tvgrti  TYPE TABLE OF ty_tvgrt
                      WITH HEADER LINE.
*** MOD-005 * end ***

* Outout table (for ALV)
DATA: BEGIN OF gt_out  OCCURS 0,
         vbeln     TYPE vbeln_va,
         posnr     TYPE posnr_va,
         waerk     TYPE waers,
         netwr     TYPE netwr,
         def_rev   TYPE wrbtr,
         unb_rec   TYPE wrbtr,
         rec_rev   TYPE wrbtr,
         unr_rev   TYPE wrbtr,
         inv_rev   TYPE wrbtr,
         frdat     TYPE bfdat,
         todat     TYPE bfdat,
         fakwr     TYPE fakwr,
         fksaf     TYPE fksaf,
         tobill    TYPE xfeld,
         bedat     TYPE bedat_fp,
         endat     TYPE endat_fp,
         days_tot(4) TYPE p,
         days_cur(4) TYPE p,
* Begin of Mod-003
*         cdef_rev  TYPE wrbtr,
          cdef_rev  TYPE netwr,
* End of Mod-003
* Begin of Mod-001
*         perc_dr   TYPE psc_percent,
* Begin of Mod-003
*         perc_dr   TYPE wrbtr,
          perc_dr   TYPE netwr,
* End of Mod-003
* End of Mod-001
         cunb_rec  TYPE wrbtr,
* Begin of Mod-001
*         perc_ur   TYPE psc_percent,
* Begin of Mod-003
*         perc_ur   TYPE wrbtr,
          perc_ur   TYPE  netwr,
* End of Mod-003
* End of Mod-001
         act_cost  TYPE wtgxxx,
         pl_cost   TYPE wtgxxx,
         rr_poc    TYPE wtgxxx,
         diff_rr   TYPE wtgxxx,
         vkorg     TYPE vkorg,
         vtweg     TYPE vtweg,
         spart     TYPE spart,
         vkbur     TYPE vkbur,
         matnr     TYPE matnr,
         prctr     TYPE prctr,
         prodh     TYPE prodh_d,
*** MOD-005 * begin ***
         matkl     TYPE matkl,
         vkgrp     TYPE vkgrp,
         vkgrpt    TYPE bezei20,
         equnr     TYPE equnr,
         eqktx     TYPE ktx01,
*** MOD-005 * end ***
*** MOD-006 * Begin ***
         auart     TYPE auart,
*** MOD-006 * Eegin ***
      END OF gt_out.

DATA: xv_variant         LIKE disvariant,
      xv_variant_flag    TYPE c,
      xv_sd_alv_variant  LIKE disvariant,
      xt_fcat            TYPE slis_t_fieldcat_alv,
      ls_fcat            LIKE LINE OF xt_fcat,
      xt_alv_sort        TYPE slis_t_sortinfo_alv,
      xv_user_command    TYPE slis_formname  VALUE 'USER_COMMAND',
      xv_variant_save    TYPE c              VALUE 'U',
      xv_layout          TYPE slis_layout_alv.

DATA: gv_end_month    TYPE datum,
      gv_waers_cc     TYPE waers,
      gv_waers_int    TYPE waers,
      gv_gdatu        TYPE gdatu_inv,
      gv_kurst        TYPE kurst,
      gv_text         TYPE char80,
      gv_bedat        TYPE bedat_fp,
      gv_endat        TYPE endat_fp,
      gv_frdat        TYPE bfdat,
      gv_todat        TYPE bfdat,
      gv_afdat        TYPE fkdat,
      gv_fakwr        TYPE fakwr,
      gv_fakwr_b      TYPE fakwr,
      gv_fksaf        TYPE fksaf,
      gv_days_settl(4)   TYPE p,
      gv_days_curr(4)    TYPE p,
      gv_days_endm(4)    TYPE p,
      gv_act_cost_so  TYPE wtgxxx,
      gv_act_cost_lc  TYPE wogxxx,
      gv_pl_cost      TYPE wtgxxx,
      gv_col(10)      TYPE n,
      gv_repid        LIKE sy-repid.

CONSTANTS: gc_1780    TYPE saknr  VALUE '0001780%',
           gc_2997    TYPE saknr  VALUE '0002997%',
           gc_3210    TYPE saknr  VALUE '0003210%',
           gc_zek2    TYPE kschl  VALUE 'ZEK2',
           gc_zciq    TYPE kschl  VALUE 'ZCIQ',
           gc_a(1)    TYPE c      VALUE 'A',
           gc_c(1)    TYPE c      VALUE 'C',
           gc_g(1)    TYPE c      VALUE 'G',
           gc_x(1)    TYPE c      VALUE 'X'.

* Selections
SELECTION-SCREEN  BEGIN OF BLOCK sel  WITH FRAME  TITLE text-s01.
PARAMETERS: p_bukrs   TYPE bukrs  OBLIGATORY
                                  MEMORY ID buk.
SELECT-OPTIONS: so_vbeln  FOR vbrevk-vbeln.
PARAMETERS:     p_kdate   TYPE datum OBLIGATORY  DEFAULT sy-datum.
SELECTION-SCREEN SKIP.
SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS:     p_seed RADIOBUTTON GROUP comp.
SELECTION-SCREEN COMMENT 03(5) text-s02 FOR FIELD p_seed.
SELECTION-SCREEN COMMENT 14(15) text-s03 FOR FIELD so_matkl.
SELECT-OPTIONS: so_matkl   FOR vbap-matkl.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
PARAMETERS:     p_ctam RADIOBUTTON GROUP comp.
SELECTION-SCREEN COMMENT 03(5) text-s04 FOR FIELD p_ctam.
SELECTION-SCREEN COMMENT 14(15) text-s05 FOR FIELD so_auart.
SELECT-OPTIONS: so_auart   FOR vbak-auart.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN SKIP.
PARAMETERS:     p_open RADIOBUTTON GROUP cntr,
                p_all  RADIOBUTTON GROUP cntr.
SELECTION-SCREEN  END OF BLOCK sel.


*----------------------------------------------------------------------*
* On the selection screen                                              *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON p_bukrs.

  PERFORM  check_authorization.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

*  IF NOT p_seed IS INITIAL.
*    IF so_matkl[] IS INITIAL.
*      MESSAGE e001(00) WITH 'For Seed: select Material Group(s)'(e02).
*    ENDIF.
*  ENDIF.
*  IF NOT p_ctam IS INITIAL.
*    IF so_auart[] IS INITIAL.
*      MESSAGE e001(00) WITH 'For CT-AM: select Contract Type(s)'(e03).
*    ENDIF.
*  ENDIF.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

  PERFORM initial.

* Statistics for transaction / program
  CALL METHOD ycl_statistics=>record_transaction.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_vbap[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No contracts selected'(e01).
    EXIT.
  ENDIF.

* Process data
  PERFORM process_data.

* Build & display list
  PERFORM alv_fcat.
  PERFORM alv_layout.
  PERFORM alv_display.


*&---------------------------------------------------------------------*
*&  Forms                                                              *
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  AUTHORITY-CHECK OBJECT 'F_BKPF_BUK'
           ID 'ACTVT' DUMMY
           ID 'BUKRS' FIELD p_bukrs.

  IF sy-subrc = 4.
*     No authorisation to display data
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH p_bukrs.
    EXIT.
  ELSEIF sy-subrc <> 0.
*     Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
    EXIT.
  ENDIF.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  INITIAL
*&---------------------------------------------------------------------*
*       Initialization
*----------------------------------------------------------------------*
FORM initial .

* Company currency
  SELECT SINGLE waers INTO gv_waers_cc
         FROM t001
         WHERE bukrs = p_bukrs.
  IF sy-subrc NE 0.
  ENDIF.

* Invert key date
  CONVERT DATE p_kdate INTO INVERTED-DATE gv_gdatu.

* Exchange rate type
  CASE p_bukrs.
    WHEN 'MRUA'.
      gv_kurst = 'DRU'.
    WHEN 'POLA'.
      gv_kurst = 'D'.
    WHEN OTHERS.
      gv_kurst = 'EURX'.
  ENDCASE.

* Intermediate currency
  CASE p_bukrs.
    WHEN 'MRUA'.
      gv_waers_int = gv_waers_cc.
    WHEN 'POLA'.
      gv_waers_int = gv_waers_cc.
    WHEN OTHERS.
      gv_waers_int = 'EUR'.
  ENDCASE.

* Get last day of the month
  CALL FUNCTION 'DATE_GET_MONTH_LASTDAY'
    EXPORTING
      i_date = p_kdate
    IMPORTING
      e_date = gv_end_month.

ENDFORM.                    " INITIAL

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Progress indicator
  gv_text = 'Data is being selected'(i01).
  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      text = gv_text.

* Contract Items
  PERFORM select_contracts.

* Check anything selected
  CHECK NOT gt_vbap[] IS INITIAL.
  SORT gt_vbap BY vbeln posnr.

* Control Lines
  SELECT vbeln posnr sakrr sakrrk waerk wrbtr
         INTO TABLE gt_vbrevk
         FROM vbrevk
         FOR ALL ENTRIES IN gt_vbap
         WHERE vbeln = gt_vbap-vbeln
           AND posnr = gt_vbap-posnr
           AND bukrs =  p_bukrs
           AND ( sakrr LIKE gc_1780 OR sakrr LIKE gc_2997 ).

* Revenue Recognition Lines
  SELECT vbeln posnr sakrv bdjpoper popupo rrsta wrbtr
         INTO TABLE gt_vbreve
         FROM vbreve
         FOR ALL ENTRIES IN gt_vbap
         WHERE vbeln = gt_vbap-vbeln
           AND posnr = gt_vbap-posnr
           AND sakrv LIKE gc_3210
           AND ( rrsta = gc_a OR rrsta = gc_c ).

* Reference Document Lines
  SELECT vbeln posnr vbeln_n posnr_n wrbtr
         INTO TABLE gt_vbrevr
         FROM vbrevr
         FOR ALL ENTRIES IN gt_vbap
         WHERE vbeln = gt_vbap-vbeln
           AND posnr = gt_vbap-posnr.

* Billing plan lines
  SELECT so~vbeln so~posnr so~fplnr
*         bp~bedat bp~endat                                 "MOD-002
*         bp~lodat bp~tndat                                 "MOD-002
         bp~bedat bp~endat                                 "MOD-004
*         bp~lodat bp~tndat                                 "MOD-004
         bd~afdat bd~fakwr bd~fksaf
         bd~fkdat bd~nfdat bp~autte
         INTO TABLE gt_fplt
         FROM vbkd AS so
         INNER JOIN fpla AS bp
                    ON so~fplnr = bp~fplnr
         INNER JOIN fplt AS bd
                    ON bp~fplnr = bd~fplnr
         FOR ALL ENTRIES IN gt_vbap
         WHERE so~vbeln =  gt_vbap-vbeln
           AND so~posnr =  gt_vbap-posnr
           AND bd~afdat LE p_kdate.

* Costs
  SELECT ci~objnr ci~twaer ci~wtgbtr ci~belnr ci~buzei
         INTO TABLE gt_coep
         FROM coep AS ci
         INNER JOIN cobk AS ch
                    ON ci~kokrs = ch~kokrs  AND
                       ci~belnr = ch~belnr
         FOR ALL ENTRIES IN gt_vbap
         WHERE ci~objnr =  gt_vbap-objnr
           AND ci~wrttp =  '04'            "Actual costs
           AND ci~lednr =  '00'
           AND ci~versn =  '000'
           AND ci~beknz IN ('H', 'S')
           AND ci~beltp IN ('0', '1')
           AND ch~budat LE p_kdate.

* Conditions
  SELECT knumv kposn kschl zaehk kwert waers INTO TABLE gt_konvi
         FROM konv
         FOR ALL ENTRIES IN gt_vbap
         WHERE knumv = gt_vbap-knumv
           AND kposn = gt_vbap-posnr
           AND kschl IN (gc_zek2, gc_zciq)
           AND kinak = ' '.
  SORT gt_konvi BY knumv kposn kschl zaehk DESCENDING.
  DELETE ADJACENT DUPLICATES FROM gt_konvi COMPARING knumv kposn kschl.
  gt_konv[] = gt_konvi[].

* Exchange rates
  SELECT kurst fcurr tcurr gdatu ukurs INTO TABLE gt_curri
         FROM tcurr
         WHERE kurst =  gv_kurst
           AND tcurr =  gv_waers_int
           AND gdatu GE gv_gdatu.
  SORT gt_curri BY kurst fcurr tcurr gdatu.
  DELETE ADJACENT DUPLICATES FROM gt_curri COMPARING kurst fcurr tcurr.
  gt_curr[] = gt_curri[].
  FREE gt_curri.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  SELECT_CONTRACTS
*&---------------------------------------------------------------------*
*       Select contract items
*----------------------------------------------------------------------*
FORM select_contracts .

* Seed
  IF NOT p_seed IS INITIAL.
*   Open contracts
    IF NOT p_open IS INITIAL.
      SELECT h~vbeln i~posnr i~netwr i~matnr i~prctr i~prodh i~objnr
             h~waerk h~vkorg h~vtweg h~spart h~vkbur h~knumv
             c~vbegdat c~venddat
*** MOD-005 * begin ***
             i~matkl h~vkgrp o~equnr o~obknr o~obzae
*** MOD-006 * Begin ***
             h~auart
*** MOD-006 * End ***
*             INTO TABLE gt_vbap
             INTO TABLE gt_vbapi
*** MOD-005 * end ***
             FROM vbak AS h
             INNER JOIN vbap AS i
                        ON h~vbeln = i~vbeln
*** MOD-005 * begin ***
             INNER JOIN viser02 AS o
                        ON i~vbeln = o~sdaufnr  AND
                           i~posnr = o~posnr
*** MOD-005 * end ***
             INNER JOIN veda AS c
                        ON h~vbeln = c~vbeln
             WHERE h~vbeln    IN so_vbeln
               AND h~bukrs_vf =  p_bukrs
               AND h~vbtyp    =  gc_g
               AND h~auart    IN so_auart
               AND i~matkl    IN so_matkl
               AND c~vposn    = 0
               AND c~vbegdat  LE p_kdate
               AND c~venddat  GE p_kdate
               AND c~vkuegru  =  '  '.
    ENDIF.
*   All contracts
    IF NOT p_all IS INITIAL.
      SELECT h~vbeln i~posnr i~netwr i~matnr i~prctr i~prodh i~objnr
             h~waerk h~vkorg h~vtweg h~spart h~vkbur h~knumv
             c~vbegdat c~venddat
*** MOD-005 * begin ***
             i~matkl h~vkgrp o~equnr o~obknr o~obzae
*** MOD-006 * Begin ***
             h~auart
*** MOD-006 * End ***
*             INTO TABLE gt_vbap
             INTO TABLE gt_vbapi
*** MOD-005 * end ***
             FROM vbak AS h
             INNER JOIN vbap AS i
                        ON h~vbeln = i~vbeln
*** MOD-005 * begin ***
             INNER JOIN viser02 AS o
                        ON i~vbeln = o~sdaufnr  AND
                           i~posnr = o~posnr
*** MOD-005 * end ***
             INNER JOIN veda AS c
                        ON h~vbeln = c~vbeln
             WHERE h~vbeln    IN so_vbeln
               AND h~bukrs_vf =  p_bukrs
               AND h~vbtyp    =  gc_g
               AND h~auart    IN so_auart
               AND i~matkl    IN so_matkl
               AND c~vposn    = 0
               AND c~vkuegru  =  '  '.
    ENDIF.
  ENDIF.

* CT-AM
  IF NOT p_ctam IS INITIAL.
*   Open contracts
    IF NOT p_open IS INITIAL.
      SELECT h~vbeln i~posnr i~netwr i~matnr i~prctr i~prodh i~objnr
             h~waerk h~vkorg h~vtweg h~spart h~vkbur h~knumv
             c~vbegdat c~venddat
*** MOD-005 * begin ***
             i~matkl h~vkgrp o~equnr o~obknr o~obzae
*** MOD-006 * Begin ***
             h~auart
*** MOD-006 * End ***
*             INTO TABLE gt_vbap
             INTO TABLE gt_vbapi
*** MOD-005 * end ***
             FROM vbak AS h
             INNER JOIN vbap AS i
                        ON h~vbeln = i~vbeln
*** MOD-005 * begin ***
             INNER JOIN viser02 AS o
                        ON i~vbeln = o~sdaufnr  AND
                           i~posnr = o~posnr
*** MOD-005 * end ***
             INNER JOIN veda AS c
                        ON h~vbeln = c~vbeln
             WHERE h~vbeln    IN so_vbeln
               AND h~bukrs_vf =  p_bukrs
               AND h~vbtyp    =  gc_g
               AND h~auart    IN so_auart
               AND i~matkl    IN so_matkl
               AND c~vposn    = 0
               AND c~vbegdat  LE p_kdate
               AND c~venddat  GE p_kdate
               AND c~vkuegru  =  '  '.
    ENDIF.
*   All contracts
    IF NOT p_all IS INITIAL.
      SELECT h~vbeln i~posnr i~netwr i~matnr i~prctr i~prodh i~objnr
             h~waerk h~vkorg h~vtweg h~spart h~vkbur h~knumv
             c~vbegdat c~venddat
*** MOD-005 * begin ***
             i~matkl h~vkgrp o~equnr o~obknr o~obzae
*** MOD-006 * Begin ***
             h~auart
*** MOD-006 * End ***
*             INTO TABLE gt_vbap
             INTO TABLE gt_vbapi
*** MOD-005 * end ***
             FROM vbak AS h
             INNER JOIN vbap AS i
                        ON h~vbeln = i~vbeln
*** MOD-005 * begin ***
             INNER JOIN viser02 AS o
                        ON i~vbeln = o~sdaufnr  AND
                           i~posnr = o~posnr
*** MOD-005 * end ***
             INNER JOIN veda AS c
                        ON h~vbeln = c~vbeln
             WHERE h~vbeln    IN so_vbeln
               AND h~bukrs_vf =  p_bukrs
               AND h~vbtyp    =  gc_g
               AND h~auart    IN so_auart
               AND i~matkl    IN so_matkl
               AND c~vposn    = 0
               AND c~vkuegru  =  '  '.
    ENDIF.
  ENDIF.

*** MOD-005 * begin ***
  SORT gt_vbapi BY vbeln posnr obknr DESCENDING obzae DESCENDING.
  DELETE ADJACENT DUPLICATES FROM gt_vbapi COMPARING vbeln posnr.
  gt_vbap[] = gt_vbapi[].
  FREE gt_vbapi.
*** MOD-005 * end ***
  CHECK NOT gt_vbap[] IS INITIAL.

* Check contract data
  SELECT vbeln vposn vbegdat venddat vkuegru
         INTO TABLE gt_veda
         FROM veda
         FOR ALL ENTRIES IN gt_vbap
         WHERE vbeln = gt_vbap-vbeln
           AND vposn = gt_vbap-posnr.
* Canceled
  LOOP AT gt_veda WHERE vkuegru NE space.
    DELETE gt_vbap WHERE vbeln = gt_veda-vbeln
                     AND posnr = gt_veda-vposn.
  ENDLOOP.

*   Open contracts
  IF NOT p_open IS INITIAL.
* Not yet started
    LOOP AT gt_veda WHERE vbegdat > p_kdate.
      DELETE gt_vbap WHERE vbeln = gt_veda-vbeln
                       AND posnr = gt_veda-vposn.
    ENDLOOP.
* Finished
    LOOP AT gt_veda WHERE venddat < p_kdate.
      DELETE gt_vbap WHERE vbeln = gt_veda-vbeln
                       AND posnr = gt_veda-vposn.
    ENDLOOP.
  ENDIF.

* Begin of Mod-004  Reversal of Mod-002
**** MOD-002 * begin ***
** Check if altered start- or enddate
*  SORT gt_veda BY vbeln vposn.
*  LOOP AT gt_veda.
*    READ TABLE gt_vbap WITH TABLE KEY vbeln = gt_veda-vbeln
*                                      posnr = gt_veda-vposn
*                       INTO wa_vbap.
*    IF sy-subrc = 0.
*      wa_vbap-vbegdat = gt_veda-vbegdat.
*      wa_vbap-venddat = gt_veda-venddat.
*      MODIFY TABLE gt_vbap FROM wa_vbap.
*    ENDIF.
*  ENDLOOP.
**** MOD-002 * end ***
* End of Mod-004
  FREE gt_veda.

*** MOD-005 * begin ***
* Select Equipment dDescriptions
  SELECT equnr eqktx INTO TABLE gt_eqkti
         FROM eqkt
         FOR ALL ENTRIES IN gt_vbap
         WHERE equnr = gt_vbap-equnr.
  SORT gt_eqkti BY equnr.
  DELETE ADJACENT DUPLICATES FROM gt_eqkti COMPARING equnr.
  gt_eqkt[] = gt_eqkti[].
  FREE gt_eqkti.

* Select Sales Groups Texts
  SELECT vkgrp bezei INTO TABLE gt_tvgrti
         FROM tvgrt
         FOR ALL ENTRIES IN gt_vbap
         WHERE vkgrp = gt_vbap-vkgrp.
  SORT gt_tvgrti BY vkgrp.
  DELETE ADJACENT DUPLICATES FROM gt_tvgrti COMPARING vkgrp.
  gt_tvgrt[] = gt_tvgrti[].
  FREE gt_tvgrti.
*** MOD-005 * end ***

ENDFORM.                    " SELECT_CONTRACTS

*&---------------------------------------------------------------------*
*&      Form  PROCESS_DATA
*&---------------------------------------------------------------------*
*       Process data
*----------------------------------------------------------------------*
FORM process_data .

  LOOP AT gt_vbap.

    gv_text = 'Contract &/& is being processed'(i02).
    REPLACE '&' WITH gt_vbap-vbeln INTO gv_text.
    REPLACE '&' WITH gt_vbap-posnr INTO gv_text.
    CONDENSE gv_text.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        text = gv_text.

    CLEAR gt_out.
    MOVE-CORRESPONDING gt_vbap TO gt_out.
* Begin of Mod-004  Reversal of MOD-002
*** MOD-002 * begin ***
**   Start- & enddate
*    gt_out-bedat = gt_vbap-vbegdat.
*    gt_out-endat = gt_vbap-venddat.
**** MOD-002 * end ***
* End of Mod-004

*** MOD-005 * begin ***
*   Equipment dDescription
    READ TABLE gt_eqkt WITH TABLE KEY equnr = gt_vbap-equnr.
    IF sy-subrc = 0.
      gt_out-eqktx = gt_eqkt-eqktx.
    ENDIF.

*   Sales Groups Text
    READ TABLE gt_tvgrt WITH TABLE KEY vkgrp = gt_vbap-vkgrp.
    IF sy-subrc = 0.
      gt_out-vkgrpt = gt_tvgrt-vkgrpt.
    ENDIF.
*** MOD-005 * end ***

*   Sum Deferred Revenues & Sum Unbilled Receivables
    LOOP AT gt_vbrevk WHERE vbeln = gt_vbap-vbeln
                        AND posnr = gt_vbap-posnr.
      CASE gt_vbrevk-sakrr(7).
        WHEN gc_2997(7).
          gt_out-def_rev = gt_out-def_rev + gt_vbrevk-wrbtr.
        WHEN gc_1780(7).
          gt_out-unb_rec = gt_out-unb_rec - gt_vbrevk-wrbtr.
      ENDCASE.
    ENDLOOP.

*   Sum Recognized Revenues & Sum Unrecognized Revenues
    LOOP AT gt_vbreve WHERE vbeln = gt_vbap-vbeln
                        AND posnr = gt_vbap-posnr.
      CASE gt_vbreve-rrsta.
        WHEN gc_c.
          gt_out-rec_rev = gt_out-rec_rev - gt_vbreve-wrbtr.
        WHEN gc_a.
          gt_out-unr_rev = gt_out-unr_rev - gt_vbreve-wrbtr.
      ENDCASE.
    ENDLOOP.

*   Sum Invoiced Revenues
    LOOP AT gt_vbrevr WHERE vbeln = gt_vbap-vbeln
                        AND posnr = gt_vbap-posnr.
      gt_out-inv_rev = gt_out-inv_rev + gt_vbrevr-wrbtr.
    ENDLOOP.

*   Billing Plan Data
*    CLEAR: gv_bedat,                                       "MOD-002
*           gv_endat,                                       "MOD-002
    CLEAR: gv_bedat,                                       "MOD-004
           gv_endat.                                       "MOD-004
    CLEAR: gv_frdat,
           gv_todat,
           gv_afdat,
           gv_fakwr,
           gv_fakwr_b,
           gv_fksaf.
    LOOP AT gt_fplt WHERE vbeln = gt_vbap-vbeln
                      AND posnr = gt_vbap-posnr.
* Begin of Mod-004  Reversal of Mod-002
*** MOD-002 * begin ***
**     Start- & enddate
*      gv_bedat = gt_fplt-bedat.
*      gv_endat = gt_fplt-endat.
**     Modified start- or enddate
*      IF NOT gt_fplt-lodat IS INITIAL.
*        gv_bedat = gt_fplt-lodat.
*      ENDIF.
*      IF NOT gt_fplt-tndat IS INITIAL.
*        gv_endat = gt_fplt-tndat.
*      ENDIF.
*** MOD-002 * end ***

      gv_bedat = gt_fplt-bedat.
      gv_endat = gt_fplt-endat.

**     Modified start- or enddate
*      IF NOT gt_fplt-lodat IS INITIAL.
*        gv_bedat = gt_fplt-lodat.
*      ENDIF.
*      IF NOT gt_fplt-tndat IS INITIAL.
*        gv_endat = gt_fplt-tndat.
*      ENDIF.

* End of Mod-004

*     Open periods
      IF gt_fplt-afdat LE p_kdate.
        gv_afdat = gt_fplt-afdat.
*       Status
        IF gt_fplt-fksaf = gc_a.
          gv_fksaf = gt_fplt-fksaf.
*         First unbilled day & Billing date
          IF gv_frdat IS INITIAL.
            gv_frdat = gt_fplt-afdat.
          ELSEIF gt_fplt-afdat < gv_frdat.
            gv_frdat = gt_fplt-afdat.
          ENDIF.
*         Last unbilled day
          IF gt_fplt-afdat > gv_todat.
            gv_todat = gt_fplt-afdat.
          ENDIF.
*         Unbilled value
          gv_fakwr = gv_fakwr + gt_fplt-fakwr.
        ENDIF.
*       Billing values
        gv_fakwr_b = gv_fakwr_b + gt_fplt-fakwr.
      ENDIF.
    ENDLOOP.

* Begin of Mod-004   Reversal of MOD-002
*** MOD-002 * begin ***
*    IF gv_bedat IS INITIAL.
*      gv_bedat = gt_vbap-vbegdat.
*    ENDIF.
*    IF gv_endat IS INITIAL.
*      gv_endat = gt_vbap-venddat.
*    ENDIF.
*
*    gt_out-bedat = gv_bedat.
*    gt_out-endat = gv_endat.
*** MOD-002 * end ***

    IF gv_bedat IS INITIAL.
      gv_bedat = gt_vbap-vbegdat.
    ENDIF.
    IF gv_endat IS INITIAL.
      gv_endat = gt_vbap-venddat.
    ENDIF.

    gt_out-bedat = gv_bedat.
    gt_out-endat = gv_endat.

* End of Mod-004

    gt_out-fakwr = gv_fakwr.
    gt_out-fksaf = gv_fksaf.
    gt_out-frdat = gv_frdat.
    gt_out-todat = gv_todat.

*   To be billed ?
    IF gv_afdat < p_kdate  AND
       gv_fksaf = gc_a.
      gt_out-tobill = 'X'.
    ENDIF.

* Begin of Mod-004
** Begin of Mod-003
*    gv_bedat = gt_out-bedat.
*    gv_endat = gt_out-endat.
** End of Mod-003
* End of Mod-004

*   Number of days in settlement
    gv_days_settl = gv_endat - gv_bedat.
    gt_out-days_tot = gv_days_settl.

*   Number of days (until current day)
    gv_days_curr = p_kdate - gv_bedat.
    gt_out-days_cur = gv_days_curr.

*   Number of days until end of month
    gv_days_endm = gv_end_month - gv_bedat.

*   Check Deferred Revenues
    IF gt_out-def_rev NE 0.
      IF gv_days_settl = 0.
        gv_days_settl = 1.
      ENDIF.
      gt_out-cdef_rev = ( gv_fakwr_b - ( ( gt_out-netwr * gv_days_endm ) / gv_days_settl ) ).
      gt_out-perc_dr =  ( gt_out-def_rev - gt_out-cdef_rev ) / gt_out-def_rev.
    ENDIF.

*   Check Unbilled Receivables
    IF gt_out-unb_rec NE 0.
      gt_out-cunb_rec =  gt_out-unb_rec - gv_fakwr.
      gt_out-perc_ur =  ( gt_out-unb_rec - gt_out-cunb_rec ) / gt_out-unb_rec.
    ENDIF.

*   Actual Cost
    PERFORM calc_act_cost USING gt_out-act_cost.

*   Planned Cost
    PERFORM calc_plan_cost USING gt_out-pl_cost.

*   Revenue Recognition POC
    gv_pl_cost = gt_out-pl_cost.
    IF gv_pl_cost = 0.
      gv_pl_cost = 1.
    ENDIF.
    gt_out-rr_poc = ( gt_out-act_cost / gv_pl_cost ) * gt_out-netwr.
    IF ABS( gt_out-rr_poc ) > ABS( gt_out-netwr ).
      gt_out-rr_poc = gt_out-netwr.
    ENDIF.

*   Difference Revenue & Revenue POC
    gt_out-diff_rr = gt_out-rec_rev - gt_out-rr_poc.

    APPEND gt_out.

  ENDLOOP.

ENDFORM.                    " PROCESS_DATA

*&---------------------------------------------------------------------*
*&      Form  CALC_ACT_COST
*&---------------------------------------------------------------------*
*       Calculate actual cost
*----------------------------------------------------------------------*
*      -->P_ACT_COST : Actual Cost (in Sales Order Currency)
*----------------------------------------------------------------------*
FORM calc_act_cost  USING    p_act_cost.

  CLEAR: gv_act_cost_so.

  LOOP AT gt_coep WHERE objnr = gt_vbap-objnr.
*   Get actual values
    IF gt_coep-twaer = gt_vbap-waerk.
*     Transaction Currency = SO Currency
      gv_act_cost_so = gv_act_cost_so + gt_coep-wtgbtr.
    ELSE.
*     Convert to SO Currency (via intermediate currency)
      CLEAR gv_act_cost_lc.
      PERFORM convert_amount_int USING gt_coep-wtgbtr
                                       gt_coep-twaer
                                       gv_act_cost_lc
                                       gt_vbap-waerk.
      gv_act_cost_so = gv_act_cost_so + gv_act_cost_lc.
    ENDIF.
  ENDLOOP.

  p_act_cost = gv_act_cost_so.

ENDFORM.                    " CALC_ACT_COST

*&---------------------------------------------------------------------*
*&      Form  CONVERT_AMOUNT_INT
*&---------------------------------------------------------------------*
*       Convert amount via intermediate currency
*----------------------------------------------------------------------*
*      -->P_KWERT_IN  : Given Value
*      -->P_WAERS_IN  : Given Currency
*      -->P_KWERT_OUT : Converted Value
*      -->P_WAERS_RES : Result Currency
*----------------------------------------------------------------------*
FORM convert_amount_int  USING    p_kwert_in
                                  p_waers_in
                                  p_kwert_out
                                  p_waers_res.

  DATA: lv_value_int   TYPE wtgxxx.

* Input Currency = Result currency -> NO conversion
  IF p_waers_in = p_waers_res.
    p_kwert_out = p_kwert_in.
    RETURN.
  ENDIF.

* Intermediate exchange rate
  IF p_waers_in = gv_waers_int.
    lv_value_int = p_kwert_in.
  ELSE.
    CLEAR gt_curr.
    READ TABLE gt_curr WITH TABLE KEY kurst = gv_kurst
                                      fcurr = p_waers_in
                                      tcurr = gv_waers_int.
    CHECK sy-subrc = 0.
*     Calculate ammount in intermediate currency
    IF gt_curr-ukurs = 0.
      gt_curr-ukurs = 1.
    ENDIF.
    lv_value_int = p_kwert_in * gt_curr-ukurs.
  ENDIF.

* Result exchange rate
  IF p_waers_res = gv_waers_int.
    p_kwert_out = lv_value_int.
  ELSE.
    CLEAR gt_curr.
    READ TABLE gt_curr WITH TABLE KEY kurst = gv_kurst
                                      fcurr = p_waers_res
                                      tcurr = gv_waers_int.
    CHECK sy-subrc = 0.
*     Calculate ammount in result currency
    IF gt_curr-ukurs = 0.
      gt_curr-ukurs = 1.
    ENDIF.
    p_kwert_out = lv_value_int / gt_curr-ukurs.
  ENDIF.

ENDFORM.                    " CONVERT_AMOUNT_INT

*&---------------------------------------------------------------------*
*&      Form  CALC_PLAN_COST
*&---------------------------------------------------------------------*
*       Calculate actual cost
*----------------------------------------------------------------------*
*      -->P_PL_COST : Planned Cost (in Sales Order Currency)
*----------------------------------------------------------------------*
FORM calc_plan_cost  USING    p_pl_cost.

* Seed
  IF NOT p_seed IS INITIAL.
*   Get condition data
    READ TABLE gt_konv WITH TABLE KEY knumv = gt_vbap-knumv
                                      kposn = gt_vbap-posnr
                                      kschl = gc_zek2.

    IF sy-subrc = 0.
      p_pl_cost = gt_konv-kwert.
    ENDIF.
  ENDIF.

* CT-AM
  IF NOT p_ctam IS INITIAL.
*   Get condition data
    READ TABLE gt_konv WITH TABLE KEY knumv = gt_vbap-knumv
                                      kposn = gt_vbap-posnr
                                      kschl = gc_zciq.

    IF sy-subrc = 0.
      p_pl_cost = ( gt_konv-kwert * gv_days_settl ) / 365.
    ENDIF.
  ENDIF.

ENDFORM.                    " CALC_PLAN_COST

*&---------------------------------------------------------------------*
*&      Form  ALV_FCAT
*&---------------------------------------------------------------------*
*       ALV field catalog
*----------------------------------------------------------------------*
FORM alv_fcat .

** Create Fieldcatalogue from internal table
** CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE' ...

  CLEAR gv_col.

* Field definitions
  PERFORM add_field USING 'VBELN'    10 'Contract'(t01)        'X'  ' '.
  PERFORM add_field USING 'POSNR'    06 'Item'(t02)            ' '  ' '.
  PERFORM add_field USING 'WAERK'    05 'Curr.'(t03)           ' '  ' '.
  PERFORM add_field USING 'NETWR'    15 'Billing Value'(t12)   ' '  ' '.
  PERFORM add_field USING 'DEF_REV'  15 'Deferred Rev.'(t04)   ' '  ' '.
  PERFORM add_field USING 'UNB_REC'  15 'Unbilled Rec.'(t05)   ' '  ' '.
  PERFORM add_field USING 'REC_REV'  15 'Recognized Rev.'(t06) ' '  ' '.
  PERFORM add_field USING 'UNR_REV'  15 'Unrecogn. Rev.'(t07)  ' '  ' '.
  PERFORM add_field USING 'INV_REV'  15 'Invoiced Rev.'(t08)   ' '  ' '.
  PERFORM add_field USING 'FRDAT'    14 'First Unbilled'(t09)  ' '  ' '.
  PERFORM add_field USING 'TODAT'    14 'Last Unbilled'(t10)   ' '  ' '.
  PERFORM add_field USING 'FAKWR'    15 'Unbilled Value'(t19)  ' '  ' '.
  PERFORM add_field USING 'FKSAF'    09 'Bill.Stat.'(t13)      ' '  ' '.
  PERFORM add_field USING 'TOBILL'   05 'ToBill'(t14)          ' '  'X'.
  PERFORM add_field USING 'BEDAT'    10 'Setll. From'(t31)     ' '  ' '.
  PERFORM add_field USING 'ENDAT'    10 'Setll. To'(t32)       ' '  ' '.
  PERFORM add_field USING 'DAYS_TOT' 08 'Tot. Days'(t33)       ' '  ' '.
  PERFORM add_field USING 'DAYS_CUR' 08 'Cur. Days'(t34)       ' '  ' '.
  PERFORM add_field USING 'CDEF_REV' 15 'Check Def. Rev.'(t15) ' '  ' '.
  PERFORM add_field USING 'PERC_DR'  10 '% Def. Rev.'(t16)     ' '  ' '.
  PERFORM add_field USING 'CUNB_REC' 15 'Check Unb. Rec.'(t17) ' '  ' '.
  PERFORM add_field USING 'PERC_UR'  10 '% Unb. Rec.'(t18)     ' '  ' '.
  PERFORM add_field USING 'ACT_COST' 15 'Actual Cost'(t20)     ' '  ' '.
  PERFORM add_field USING 'PL_COST'  15 'Planned Cost'(t21)    ' '  ' '.
  PERFORM add_field USING 'RR_POC'   15 'Rev.Rec. POC'(t22)    ' '  ' '.
  PERFORM add_field USING 'DIFF_RR'  15 'Diff. R.R. POC'(t23)  ' '  ' '.
  PERFORM add_field USING 'VKORG'    06 'S.Org.'(t24)          ' '  ' '.
  PERFORM add_field USING 'VTWEG'    04 'D.Ch.'(t25)           ' '  ' '.
  PERFORM add_field USING 'SPART'    04 'Div.'(t26)            ' '  ' '.
  PERFORM add_field USING 'VKBUR'    06 'S.Off.'(t27)          ' '  ' '.
  PERFORM add_field USING 'MATNR'    18 'Material'(t28)        ' '  ' '.
  PERFORM add_field USING 'PRCTR'    12 'Profitcenter'(t29)    ' '  ' '.
  PERFORM add_field USING 'PRODH'    18 'Product Hierarchy'(t30) ' '  ' '.
*** MOD-005 * begin ***
  PERFORM add_field USING 'MATKL'    09 'Mat. Group'(t35)      ' '  ' '.
  PERFORM add_field USING 'VKGRP'    03 'SGrp'(t36)            ' '  ' '.
  PERFORM add_field USING 'VKGRPT'   20 'Sales Group'(t37)     ' '  ' '.
  PERFORM add_field USING 'EQUNR'    18 'Equipment'(t38)       ' '  ' '.
  PERFORM add_field USING 'EQKTX'    40 'Description'(t39)     ' '  ' '.
*** MOD-005 * end ***
*** MOD-006 * Begin***
  PERFORM add_field USING 'AUART'    13 'Contract Type'(t40)   ' '  ' '.
*** MOD-006 * End***
ENDFORM.                    " ALV_FCAT

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field   USING  p_field
                        p_len
                        p_descr
                        p_hotsp
                        p_check.

  gv_col = gv_col + 1.

  CLEAR ls_fcat.
  ls_fcat-col_pos    = gv_col.
  ls_fcat-fieldname  = p_field.
  ls_fcat-outputlen  = p_len.
  ls_fcat-seltext_l  = p_descr.
  ls_fcat-seltext_m  = p_descr.
  ls_fcat-seltext_s  = p_descr.
  ls_fcat-fix_column = gc_x.
  ls_fcat-emphasize  = gc_x.
  ls_fcat-hotspot    = p_hotsp.
  ls_fcat-checkbox   = p_check.
* Suppress leading zeros for contract number & profitcenter
  IF p_field = 'VBELN'  OR
     p_field = 'EQUNR'  OR                                  "MOD-005
     p_field = 'PRCTR'.
    ls_fcat-edit_mask = '==ALPHA'.
  ENDIF.
* Suppress leading zeros for the material number
  IF p_field = 'MATNR'.
    ls_fcat-edit_mask = '==MATN1'.
  ENDIF.

  APPEND ls_fcat TO xt_fcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT
*&---------------------------------------------------------------------*
*       Modify ALV layout
*----------------------------------------------------------------------*
FORM alv_layout .

  xv_layout-zebra = gc_x.
*  xv_layout-colwidth_optimize = gc_x.
*  xv_layout-smalltitle = gc_x.
*  xv_layout-grid_title = 'title'.

ENDFORM.                    " ALV_LAYOUT

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       Display ALV grid
*----------------------------------------------------------------------*
FORM alv_display .

*  xv_sd_alv_variant = xv_variant.
  gv_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = gv_repid
      i_callback_user_command = xv_user_command
*      i_callback_top_of_page  = 'ALV_TOP'
      i_grid_title            = 'Revenue Recognition'(h01)
      is_layout               = xv_layout
      it_fieldcat             = xt_fcat
      it_sort                 = xt_alv_sort
      i_default               = gc_x
      i_save                  = gc_a
*      is_variant              = xv_sd_alv_variant
      i_screen_start_column   = 0
      i_screen_start_line     = 0
      i_screen_end_column     = 0
      i_screen_end_line       = 0
    TABLES
      t_outtab                = gt_out
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.

  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&   Form  USER_COMMAND
*&---------------------------------------------------------------------*
*    Process user command
*----------------------------------------------------------------------*
FORM user_command USING ucomm    LIKE sy-ucomm
                        selfield TYPE slis_selfield.

*-- Check function code
  CASE ucomm.
    WHEN '&IC1'.
*     Select hotspot
      IF NOT selfield-value IS INITIAL.
        CASE selfield-fieldname.
          WHEN 'VBELN'.
            SET PARAMETER ID 'KTN' FIELD selfield-value.
            CALL TRANSACTION 'VA43' AND SKIP FIRST SCREEN.
        ENDCASE.
      ENDIF.
  ENDCASE.

ENDFORM.                    " USER_COMMAND

*Text symbol text£º
*E01:No contracts selected
*E02:For Seed: select Material Group(s)
*E03:For CT-AM: select Contract Type(s)
*H01:Revenue Recognition
*I01:Data is being selected
*I02:Contract &/& is being processed
*S01:Selection
*S02:Seed
*S03:Material Group
*S04:CT-AM
*S05:Contract Type
*T01:Contract
*T02:Item
*T03:Curr.
*T04:Deferred Rev.
*T05:Unbilled Rec.
*T06:Recognized Rev.
*T07:Unrecogn. Rev.
*T08:Invoiced Rev.
*T09:First Unbilled
*T10:Last Unbilled
*T11:Bill. Date
*T12:Billing Value
*T13:Bill.Stat.
*T14:ToBill
*T15:Check Def. Rev.
*T16:% Def. Rev.
*T17:Check Unb. Rec.
*T19:Unbilled Value
*T20:Actual Cost
*T21:Planned Cost
*T22:Rev.Rec. POC
*T23:Diff. R.R. POC
*T24:S.Org.
*T25:D.Ch.
*T26:Div.
*T27:S.Off.
*T28:Material
*T29:Profitcenter
*T30:Product Hierarchy
*T31:Setll. From
*T32:Setll. To
*T33:Tot. Days
*T34:Cur. Days
*T35:Mat. Group
*T36:SGrp
*T37:Sales Group
*T38:Equipment
*T39:Description

*T40:Contract Type
*Selection text£º
*P_ALL:        All Contracts
*P_BUKRS:D       .
*P_CTAM:        CT-AM
*P_KDATE:        Key date
*P_OPEN:        Open Contracts
*P_SEED:        Seed
*SO_AUART:        Contract Type
*SO_MATKL:D       .
*SO_VBELN:        Contract
