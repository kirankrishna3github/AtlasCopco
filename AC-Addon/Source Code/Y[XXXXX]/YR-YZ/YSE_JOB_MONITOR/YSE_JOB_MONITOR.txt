*----------------------------------------------------------------------*
* PROGRAM ID    : YSE_JOB_MONITOR                                      *
* PROGRAM TITLE : Job Monitor Tool                                     *
* AUTHOR        : Johnny Wu                                            *
* DATE          : 05/07/2012                                           *
* DEVELOPMENT ID:                                                      *
*                                                                      *
* CHANGE REQUEST NUMBER: NO                                            *
*                                                                      *
* Program Description: Tool to monitor job                             *
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
*  DATE    | NAME     |TRANSPORT  | CR# and short Description          *
*----------------------------------------------------------------------*
*
*05/07/2012|Johnny Wu | CD1K972456 |Initial version                    *
*10/30/2012|Johnny Wu | CD1K973677 |Job Monitor based on job status    *
*----------------------------------------------------------------------*

report  yse_job_monitor.
*----------------------------------------------------------------------*
*                            TABLES                                    *
*----------------------------------------------------------------------*
tables: tbtco.

*----------------------------------------------------------------------*
*                            TYPES                                     *
*----------------------------------------------------------------------*
types: begin of ty_job,
          jobname   like tbtco-jobname,
          jobcount  like tbtco-jobcount,
          sdlstrtdt like tbtco-sdlstrtdt,
          sdlstrttm like tbtco-sdlstrttm,
          status    like tbtco-status,
       end of ty_job.
*----------------------------------------------------------------------*
*                            VARIABLES                                 *
*----------------------------------------------------------------------*
data: gt_job type table of ty_job.
*----------------------------------------------------------------------*
*                          SELECTION SCREEN VARIABLES                  *
*----------------------------------------------------------------------*
selection-screen begin of block b01 with frame title text-001.
parameters: p_urgrp like yse_job_list-urgrp.
select-options:
        s_date  for tbtco-sdlstrtdt obligatory,
        s_time  for tbtco-sdlstrttm.

parameters: p_output as checkbox default 'X',
            p_mail   as checkbox default 'X'.
selection-screen end of block b01.

at selection-screen on value-request for p_urgrp.
  perform f4_urgrp.

start-of-selection.
  perform get_job.

  if p_output = 'X'.
    perform list_display.
  endif.

  if p_mail = 'X' and gt_job is not initial.
    perform mail_sending.
  endif.
*&---------------------------------------------------------------------*
*&      Form  get_job
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form get_job .
  data: lt_job_list type table of yse_job_list,
        lt_job_list1 type table of yse_job_list,
        lt_job_list2 type table of yse_job_list,
        ls_job_list type yse_job_list.
  "Get the job list.

  select * from yse_job_list into table lt_job_list where urgrp = p_urgrp. "#EC CI_SGLSELECT

  if lt_job_list is initial.
    return.
  endif.

  loop at lt_job_list into ls_job_list.
    if ls_job_list-jobname cs '*'.
      translate ls_job_list-jobname using '*%'.
      append ls_job_list to lt_job_list1.
    else.
      append ls_job_list to lt_job_list2.
    endif.
  endloop.
  select jobname jobcount sdlstrtdt sdlstrttm status into table gt_job
    from tbtco
    for all entries in lt_job_list2
    where jobname = lt_job_list2-jobname
    and sdlstrtdt in s_date
    and sdlstrttm in s_time
    and status = lt_job_list2-status."'A'.

  loop at lt_job_list1 into ls_job_list.
    select jobname jobcount sdlstrtdt sdlstrttm status appending table gt_job
          from tbtco
          where jobname like ls_job_list-jobname
          and sdlstrtdt in s_date
          and sdlstrttm in s_time
          and status = ls_job_list-status." 'A'.
  endloop.
*
*  loop at gt_job into ls_job.
*    if ls_job-status <> 'A'.
*      delete gt_job from ls_job.
*    endif.
*  endloop.
endform.                    " get_job
*&---------------------------------------------------------------------*
*&      Form  f4_urgrp
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form f4_urgrp .

  data: lt_urgrp type table of yse_job_urgrp.
  data: lt_return type table of ddshretval,
          ls_return type ddshretval.

  select * from yse_job_urgrp into table lt_urgrp.    "#EC CI_SGLSELECT

  call function 'F4IF_INT_TABLE_VALUE_REQUEST'
    exporting
      retfield        = 'URGRP'
      dynpprog        = sy-repid
      dynprofield     = 'P_URGRP'
      value_org       = 'S'
    tables
      value_tab       = lt_urgrp
      return_tab      = lt_return
    exceptions
      parameter_error = 1
      no_values_found = 2
      others          = 3.
  read table lt_return into ls_return index 1.
  if sy-subrc = 0.
    p_urgrp = ls_return-fieldval.
  endif.
endform.                    " f4_urgrp
*&---------------------------------------------------------------------*
*&      Form  List_display
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form list_display .
  data: ls_job       type ty_job.

  write:'The below jobs were monitored under your criteria:'.

  loop at gt_job into ls_job.
    write: /.
    write: at 10 ls_job-jobname.
    write: at 40 'Start Date:', ls_job-sdlstrtdt.
    write: at 70 'Start Time:', ls_job-sdlstrttm .
    write: at 100 'Status: '.
    if ls_job-status = 'A'.
      write: 'Canceled'.
    elseif ls_job-status = 'F'.
      write: 'Finished'.
    endif.
*    concatenate ls_job-jobname ':' cl_abap_char_utilities=>cr_lf
*                'Start Date:' ls_job-sdlstrtdt cl_abap_char_utilities=>cr_lf
*                'Start Time:' ls_job-sdlstrttm into lv_string.
*    WRITE: / lv_string.
  endloop.
endform.                    " List_display
*&---------------------------------------------------------------------*
*&      Form  mail_sending
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form mail_sending.
  data: lv_mail_subject type so_obj_des,
        lv_sender       type adr6-smtp_addr,
        lt_mail_to      type bcsy_smtpa,
        lt_body type bcsy_text,
        lt_attach type table of string,
        lt_att_name type standard table of sood-objdes,
        lt_att_types type standard table of so_obj_tp.

  data: lt_mail_list type table of yse_mail_list,
        ls_mail_list type yse_mail_list,
        ls_job       type ty_job,
        lv_string    type string,
        lv_result     type bcs_cxerr.

  concatenate sy-datum '-' sy-uzeit ' JOB Monitor for Group' '-' p_urgrp
                     into lv_mail_subject.


  "Get Mail to
  select * from yse_mail_list into table lt_mail_list where urgrp = p_urgrp. "#EC CI_SGLSELECT
  loop at lt_mail_list into ls_mail_list.
    append ls_mail_list-mail to lt_mail_to.
  endloop.

  append 'The enclosed list contains the jobs that you want to monitor during execution.' to lt_body.
  append 'Please check the list.' to lt_body.
  append '' to lt_body.
  append 'Best Regards,' to lt_body.
  append 'Job Monitor' to lt_body.

  append '' to lt_body.
  append 'This is an automatic mail. Do not reply.' to lt_body.

  loop at gt_job into ls_job.
    at first.
      lv_string = 'The jobs which were monitored:'.
      concatenate lv_string cl_abap_char_utilities=>cr_lf
                  'System: ' sy-sysid '/' sy-mandt into lv_string.
*      append 'The jobs which were canceled:' to lt_attach.
*      append '' to lt_attach.
    endat.

    concatenate lv_string   cl_abap_char_utilities=>cr_lf cl_abap_char_utilities=>cr_lf
                ls_job-jobname ':' cl_abap_char_utilities=>horizontal_tab
                'Start Date:' ls_job-sdlstrtdt cl_abap_char_utilities=>horizontal_tab
                'Start Time:' ls_job-sdlstrttm  into lv_string.

    if ls_job-status = 'A'."Cancel.
      concatenate lv_string cl_abap_char_utilities=>horizontal_tab
                'Status: ' 'Canceled' into lv_string.
    elseif ls_job-status = 'F'."Finished.
      concatenate lv_string cl_abap_char_utilities=>horizontal_tab
              'Status: ' 'Finished' into lv_string.
    endif.
  endloop.

  append lv_string to lt_attach.

  append lv_mail_subject to lt_att_name.
  append 'TXT' to lt_att_types.
  call method ycl_se_utility=>send_mail
    exporting
      iv_mail_subject = lv_mail_subject
      iv_sender       = lv_sender
      it_mail_to      = lt_mail_to
*    it_mail_cc      =
*    it_mail_bc      =
      it_body         = lt_body
      it_attachment   = lt_attach
      it_att_names    = lt_att_name
      it_att_types    = lt_att_types
  importing
    ev_mail_result  = lv_result.

  if lv_result is not initial.
    write: 'The mail sending failed with error: ', lv_result.
  endif.

endform.                    "mail_sending

*Text symbol text£º

*001:Selction Criteria
*Selection text£º
*P_MAIL:        Mail Sending
*P_OUTPUT:        List Display
*P_URGRP:D       .
*S_DATE:D       .
*S_TIME:D       .
