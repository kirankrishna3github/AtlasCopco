*----------------------------------------------------------------------*
***INCLUDE YSE_STOCK_OVERVIEW_ALVSUPP .
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  prepare_alv
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM PREPARE_ALV.

* If container is still initial
  IF MY_CONTAINER IS INITIAL.
*   Create container
    CREATE OBJECT MY_CONTAINER
      EXPORTING
          REPID           =  SY-REPID
          DYNNR           =  SY-DYNNR
          LIFETIME        =  CNTL_LIFETIME_DYNPRO
          EXTENSION       =  5000.

*   Create the ALV control
    CREATE OBJECT MY_ALV
      EXPORTING
          I_PARENT  =  MY_CONTAINER.

*   Create the handler object
    CREATE OBJECT MY_HANDLER.
*   Set handler for ALV
    SET HANDLER MY_HANDLER->HANDLE_HOTSPOT_CLICK FOR MY_ALV.

*   Set parameters regarding layout, etc.
    WA_LAYOUT-NO_TOOLBAR = ' '.
*   Make sure layout variants can be saved (by setting repid
    WA_VARIANT-REPORT = SY-REPID.
*   Create the fieldcatalog e.g. which fields to display
    PERFORM CREATE_FIELDCAT.

*   Display the data in the ALV control
    CALL METHOD MY_ALV->SET_TABLE_FOR_FIRST_DISPLAY
      EXPORTING
        I_STRUCTURE_NAME  = 'YSE_STOCK_OVERVIEW'
        IS_LAYOUT         = WA_LAYOUT
        IS_PRINT          = WA_PRINT
        I_SAVE            = 'A'
        IS_VARIANT        = WA_VARIANT
      CHANGING
        IT_OUTTAB         = IT_OUTPUT
        IT_FIELDCATALOG   = IT_FIELDCAT
      EXCEPTIONS
        INVALID_PARAMETER_COMBINATION = 1
        PROGRAM_ERROR                 = 2
        TOO_MANY_LINES                = 3
        OTHERS                        = 4.

* If container already exists
  ELSE.

*   Refresh data without changing the layout -> 'soft'
    CALL METHOD MY_ALV->REFRESH_TABLE_DISPLAY
      EXPORTING
          I_SOFT_REFRESH  =  C_X.
  ENDIF.

ENDFORM.                    " PREPARE_ALV

*&---------------------------------------------------------------------*
*&      Form  create_fieldcat
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM CREATE_FIELDCAT .

  FIELD-SYMBOLS: <WA_FC> LIKE LINE OF IT_FIELDCAT.

* Fetch the field catalog for structure YSE_RENT_EHRSP_OUTPUT
  CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
    EXPORTING
      I_BUFFER_ACTIVE              = ' '
      I_STRUCTURE_NAME             = 'YSE_STOCK_OVERVIEW'
    CHANGING
      CT_FIELDCAT                  = IT_FIELDCAT.

* Update field catalog (depending on e.g. selection parameters)
  LOOP AT IT_FIELDCAT ASSIGNING <WA_FC>.

    CASE <WA_FC>-FIELDNAME.
      WHEN 'LGORT'.
        IF P_PLANT EQ C_X.
          <WA_FC>-NO_OUT = C_X.
        ENDIF.
      WHEN 'UMLMC' OR 'TRAME' OR 'BACK'.
        IF P_PLANT EQ SPACE.
          <WA_FC>-NO_OUT = C_X.
        ENDIF.
    ENDCASE.

  ENDLOOP.

ENDFORM.                    " CREATE_FIELDCAT
