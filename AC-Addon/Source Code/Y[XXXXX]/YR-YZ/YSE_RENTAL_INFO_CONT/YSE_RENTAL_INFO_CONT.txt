*----------------------------------------------------------------------*
* PROGRAM ID           : YSE_RENTAL_INFO_CONT                          *
* PROGRAM TITLE        : Rental : Infomanager Contracts                *
* AUTHOR               : Marc Jacobs                                   *
* DATE                 : 23/09/2008                                    *
* DEVELOPMENT ID       : CR151                                         *
*                                                                      *
* CHANGE REQUEST NUMBER: CD1K943283                                    *
*                                                                      *
* Program Description:  Contracts from Rental to Infomanager           *
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECT NR.| CHANGE REFERENCE #       *
*----------------------------------------------------------------------*
*         |          |          |           |                          *
************************************************************************
REPORT yse_rental_info_cont MESSAGE-ID yse_rental.

************************************************************************
*                   T A B L E S                                        *
************************************************************************
TABLES: yse_infomanager,          " filenames
        v_equi.            " PM technical objects (EQUI, func.location)

************************************************************************
*                   C O N S T A N T S                                  *
************************************************************************
CONSTANTS :c_object(10) TYPE c     VALUE 'CONT',
           c_99991231(8)           VALUE '99991231',
           c_n(1)       TYPE c     VALUE 'C',
           c_x(1)       TYPE c     VALUE 'X',
           c_y(1)       TYPE c     VALUE 'Y',
           c_run_hours  TYPE impt-atnam VALUE 'ZAM_RHRSTOTAL_ACT',
           c_underscore(1) TYPE c  VALUE '_',    " Underscore
           c_sc1(3)                VALUE 'SC1',
           c_input                 VALUE '0',
           c_0000(4)    TYPE c     VALUE '0000',
           c_001(3)     TYPE c     VALUE '001',
           c_ag(2)      TYPE c     VALUE 'AG',
           c_day(8)     TYPE c     VALUE 'Day',
           c_month(10)  TYPE c     VALUE 'Month',
           c_zbp2(4)    TYPE c     VALUE 'ZBP2',
           c_clnt(4)    TYPE c     VALUE 'CLNT' ,
           c_tab TYPE c VALUE cl_abap_char_utilities=>horizontal_tab.
CONSTANTS:
  gc_pgc_5900(4)   TYPE c          VALUE '5900',  " pgc
  gc_pgc_5910(4)   TYPE c          VALUE '5910',  " pgc
  gc_pgc_5911(4)   TYPE c          VALUE '5911',  " pgc
  gc_pgc_5920(4)   TYPE c          VALUE '5920',  " pgc
  gc_pgc_5930(4)   TYPE c          VALUE '5930',  " pgc
  gc_pgc_5940(4)   TYPE c          VALUE '5940',  " pgc
  gc_pgc_5949(4)   TYPE c          VALUE '5949',  " pgc
  gc_pgc_59re(4)   TYPE c          VALUE '59RE',  " pgc
  gc_pgc_59ri(4)   TYPE c          VALUE '59RI',  " pgc
  gc_pgc_59fu(4)   TYPE c          VALUE '59FU',  " pgc
  gc_pgc_59lo(4)   TYPE c          VALUE '59LO',  " pgc
  gc_pgc_59sl(4)   TYPE c          VALUE '59SL',  " pgc
  gc_pgc_59sp(4)   TYPE c          VALUE '59SP',  " pgc
  gc_pgc_59sn(4)   TYPE c          VALUE '59SN',  " pgc
  gc_pgc_59ue(4)   TYPE c          VALUE '59UE',  " pgc
  gc_pgc_59so(4)   TYPE c          VALUE '59SO',  " pgc
  gc_pgc_59sm(4)   TYPE c          VALUE '59SM',  " pgc
  gc_auart_zqp     TYPE vbak-auart VALUE 'ZQP',  " rental contract
  gc_auart_zqp2    TYPE vbak-auart VALUE 'ZQP2', " rental contract
  gc_auart_zrib    TYPE vbak-auart VALUE 'ZRIB'. " rental contract

************************************************************************
*                   V A R I A B L E S                                  *
************************************************************************
DATA:  g_vbeln      TYPE vbak-vbeln,
       g_vtweg      TYPE vbak-vtweg,
       g_pfile      LIKE      /sapdmc/lsoinp-filename,
       g_ofile      LIKE      /sapdmc/lsoinp-filename,
       g_retcd      LIKE sy-subrc,                  " return code
       g_directory(25) TYPE c VALUE '/var/load/xxx/UK/convert/',
       g_directory2(23) TYPE c VALUE '/var/load/xxx/UK/read/',
       g_fm_atinn   TYPE ausp-atinn,
       g_point      LIKE imptt-point,               " Measurement point
       g_ersda(12)  TYPE c,                      " YYYYMMDDHHMM
       g_date       LIKE sy-datum.
DATA : gv_status(1) TYPE c,
       gv_ltdparts  LIKE pmco-wrt08,
       gv_ltdlabor  LIKE pmco-wrt08,
       gv_ytdparts  LIKE pmco-wrt08,
       gv_ytdlabor  LIKE pmco-wrt08,
       gv_wrt08     LIKE pmco-wrt08,
       gv_objnr     LIKE pmco-objnr,
       gv_year      LIKE pmco-gjahr,
       gv_answl     LIKE anlc-answl,
       gv_nafap     LIKE anlc-nafap,
       gv_obknr     LIKE objk-obknr,
       gv_netbook   LIKE anlc-nafap,
       gv_yearper   TYPE jahrper,
       gv_vbegdat   LIKE veda-vbegdat,
       gv_venddat   LIKE veda-venddat,
       gv_rvamt     LIKE vbrevk-rvamt,
       gv_zero      LIKE vbrevk-rvamt,
       gv_gbstk     LIKE vbuk-gbstk,
       gv_fksak     LIKE vbuk-fksak,
       gv_anlcv     LIKE anlcv,
       gv_datum     TYPE sy-datum.
DATA: g_dir_name    LIKE epsf-epsdirnam,
      g_dfile       LIKE epsf-epsfilnam.
************************************************************************
*                  I N T E R N A L   T A B L E S                       *
************************************************************************
DATA : BEGIN OF i_prev OCCURS 0.
        INCLUDE STRUCTURE yse_im_cont.
DATA : END OF i_prev.

DATA : BEGIN OF i_cont_init OCCURS 0,
        vbeln TYPE vbeln,
        vkgrp TYPE vkgrp,
        vkbur TYPE vkbur,
        vkorg TYPE vkorg,
        auart TYPE auart,
       END OF i_cont_init.

DATA : BEGIN OF i_cont OCCURS 0.
        INCLUDE STRUCTURE yse_im_cont.
DATA : objnr TYPE j_objnr,
       sernr TYPE gernr.
DATA : END OF i_cont.

DATA : BEGIN OF i_delfiles OCCURS 0,
         mandt LIKE yse_infomanager-mandt,
         bukrs LIKE yse_infomanager-bukrs,
         object LIKE yse_infomanager-object,
         ersda LIKE yse_infomanager-ersda,
       END OF i_delfiles.

DATA : BEGIN OF i_cont_delta OCCURS 0.
        INCLUDE STRUCTURE yse_im_cont.
DATA : END OF i_cont_delta.

DATA : BEGIN OF wa_yse_infomanager,
         mandt LIKE yse_infomanager-mandt,
         bukrs LIKE yse_infomanager-bukrs,
         object LIKE yse_infomanager-object,
         ersda LIKE yse_infomanager-ersda,
       END OF wa_yse_infomanager.

DATA: i_wa_point LIKE impt,
      i_wa_value LIKE imrg,
      p_logsys LIKE tbdlst-logsys  .

DATA : BEGIN OF lt_afih OCCURS 0,
         aufnr LIKE afih-aufnr,
       END OF lt_afih.
DATA :
it_sme      TYPE yse_rent_sme         OCCURS 0 WITH HEADER LINE,
it_anlc     TYPE anlc                 OCCURS 0 WITH HEADER LINE,
it_vbap     TYPE vbap                 OCCURS 0 WITH HEADER LINE.
DATA : it_outxls(4096)  TYPE c OCCURS 0,
       wa_outxls(4096)  TYPE c.
***********************************************************************
* RANGES                                                              *
***********************************************************************
RANGES:
  gr_auart_contract FOR yse_rent_sme-auart,
  gr_pgc_rent       FOR ce41000_acct-ww006,
  gr_pgc_insurance  FOR ce41000_acct-ww006,
  gr_pgc_rerent     FOR ce41000_acct-ww006,
  gr_pgc_fuel       FOR ce41000_acct-ww006,
  gr_pgc_transport  FOR ce41000_acct-ww006,
  gr_pgc_workorder  FOR ce41000_acct-ww006,
  gr_pgc_sales      FOR ce41000_acct-ww006,
  gr_pgc_misc       FOR ce41000_acct-ww006.

************************************************************************
*       S E L E C T - O P T I O N S / P A R A M E T E R S              *
************************************************************************

SELECTION-SCREEN : BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.

SELECT-OPTIONS: s_vbeln FOR g_vbeln.
PARAMETERS: p_bukrs  TYPE bukrs OBLIGATORY.
SELECT-OPTIONS: s_vtweg FOR g_vtweg.
PARAMETERS: p_init TYPE c AS CHECKBOX.
SELECTION-SCREEN: END OF BLOCK b1.

************************************************************************
INITIALIZATION.

  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = p_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  gv_datum = sy-datum .

  gv_yearper = ( sy-datum+0(4) * 1000 ) + sy-datum+4(2).

  PERFORM fill_ranges.
************************************************************************
*    Disable Message Type Screen Field                                 *
************************************************************************
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-group1 = c_sc1.
      screen-input   = c_input.
      MODIFY SCREEN.
      CONTINUE.
    ENDIF.
  ENDLOOP.

************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

** Validate if the ALE Configuration is correctly setup
* PERFORM validate_ale_configuration .

** Get the information for selected plant(s)

  PERFORM get_detail USING p_bukrs.

*   Create delta with new creations/updates by comparing the selected
*   data with this from the previous run. When a new plant
*   has to be loaded, no comparison has to take place.
  IF NOT i_cont[] IS INITIAL.

    IF NOT p_init EQ c_x.

      SORT i_cont BY vbeln posnum.

      CLEAR: g_retcd.
      PERFORM get_previous_file USING p_bukrs
                             CHANGING g_retcd.

      IF g_retcd = 0.
        PERFORM create_delta.
      ENDIF.
    ELSE.
      LOOP AT i_cont.
        MOVE-CORRESPONDING i_cont TO i_cont_delta.
        APPEND i_cont_delta.
        CLEAR i_cont_delta.
      ENDLOOP.
    ENDIF.

*     write outputfile to application server
    CLEAR g_retcd.

    CONCATENATE sy-datum sy-uzeit(4) INTO g_ersda.
    CONCATENATE p_bukrs c_object g_ersda
              INTO g_ofile SEPARATED BY c_underscore.
    REPLACE 'xxx' IN g_directory WITH p_logsys(3).
    CONCATENATE g_directory g_ofile INTO g_ofile.

    PERFORM write_outputfile.

    IF g_retcd IS INITIAL.
*       update custom table YSE_INFOMANAGER
      PERFORM update_custom_table USING p_bukrs.

*       Delete older entries in custom table Yse_infomanager
      PERFORM delete_old_table_entries USING p_bukrs.

*       Delete older files on appl.server
      IF NOT i_delfiles[] IS INITIAL.
        PERFORM delete_old_files.
      ENDIF.

    ENDIF.

    PERFORM overwrite_file_to_compare.

  ENDIF.

************************************************************************
*       E N D - O F - S E L E C T I O N    E V E N T                   *
************************************************************************
END-OF-SELECTION .

  IF i_cont_delta[] IS INITIAL .
    WRITE: / 'No delta found'(i03).
  ELSE.
    SORT i_cont_delta BY vbeln posnum.
  ENDIF.

  WRITE: / 'Job finished'(i02).

*&---------------------------------------------------------------------*
*&      Form  Get_Detail
*&---------------------------------------------------------------------*
*       To get the information for selected plant(s)
*----------------------------------------------------------------------*
FORM get_detail USING p_bukrs.

  CLEAR : i_cont , i_cont_init.
  REFRESH : i_cont, i_cont_init.
  gv_year = sy-datum+0(4).

** Select data based on Selection screen parameters
* contract data + sales data
  SELECT vbeln vkgrp vkbur vkorg auart
     INTO CORRESPONDING FIELDS OF TABLE i_cont_init
       FROM vbak WHERE vbeln IN s_vbeln
                   AND auart IN gr_auart_contract
                   AND vtweg IN s_vtweg
                   AND bukrs_vf = p_bukrs.

** Process Error - No data found for the Selected company
  IF sy-subrc NE 0 .
    WRITE: text-e01, p_bukrs.
    EXIT.
  ENDIF .
* check overall status or bill status
  LOOP AT i_cont_init.
    CLEAR : gv_gbstk, gv_fksak.
    SELECT SINGLE gbstk fksak INTO (gv_gbstk, gv_fksak) FROM
          vbuk WHERE vbeln = i_cont_init-vbeln.
    IF gv_gbstk = 'C' OR gv_fksak = 'C'.
      DELETE i_cont_init.
    ENDIF.
  ENDLOOP.

* distinction between contract and sales is made by auart
** Get related info and populate data into Internal Table
  LOOP AT i_cont_init.
    IF i_cont_init-auart IN gr_auart_contract.
* get contract lines
      CLEAR : it_vbap.
      REFRESH : it_vbap.
      SELECT * INTO CORRESPONDING FIELDS OF TABLE it_vbap
        FROM vbap WHERE vbeln = i_cont_init-vbeln.
* loop over contract lines
      LOOP AT it_vbap.
        CLEAR i_cont.
        MOVE-CORRESPONDING i_cont_init TO i_cont.
* company
        i_cont-bukrs = p_bukrs.
* get revenue-pgc
        SELECT SINGLE ww007 INTO i_cont-revpgc FROM ce41000_acct
          WHERE aktbo = c_x
            AND paobjnr = it_vbap-paobjnr.
* item line
        MOVE it_vbap-posnr TO i_cont-posnum.
* customer number = sold-to party
        SELECT SINGLE kunnr INTO i_cont-kunnr
          FROM vbpa WHERE vbeln = i_cont-vbeln
                      AND posnr = 0
                      AND parvw = c_ag.
* fill different fields depending on item category
        CASE it_vbap-pstyv.
          WHEN 'ZMVN'.
* lookup equipment
            SELECT SINGLE obknr INTO gv_obknr FROM ser02
               WHERE sdaufnr = it_vbap-vbeln
                 AND posnr   = it_vbap-posnr.
            IF sy-subrc = 0.
              SELECT SINGLE equnr INTO i_cont-equnr
                FROM objk WHERE obknr = gv_obknr
                            AND obzae = 1.
            ENDIF.
* model
            SELECT SINGLE pmatn INTO i_cont-model
               FROM mvke WHERE matnr = it_vbap-matnr
                           AND vkorg = i_cont_init-vkorg
                           AND vtweg IN s_vtweg.
          WHEN OTHERS.
        ENDCASE.
* start and end date item level
        CLEAR : gv_vbegdat, gv_venddat.
        SELECT SINGLE vbegdat venddat INTO (gv_vbegdat, gv_venddat)
          FROM veda WHERE vbeln = it_vbap-vbeln
                      AND vposn = it_vbap-posnr.
        IF sy-subrc <> 0.
          SELECT SINGLE vbegdat venddat INTO (gv_vbegdat, gv_venddat)
          FROM veda WHERE vbeln = it_vbap-vbeln
                      AND vposn = 0.
        ENDIF.
        MOVE gv_vbegdat TO i_cont-startd.
        MOVE gv_venddat TO i_cont-endd.
* rental revenue
        i_cont-rentrev = gv_zero.
        IF i_cont-revpgc IN gr_pgc_rent.
          i_cont-rentrev = it_vbap-netwr.
        ENDIF.
* insurance revenue
        i_cont-damwrev = gv_zero.
        IF i_cont-revpgc IN gr_pgc_insurance.
          IF it_vbap-pstyv = c_zbp2.
            i_cont-damwrev = it_vbap-netwr.
          ENDIF.
        ENDIF.
* re-rent revenue
        i_cont-rerentrev = gv_zero.
        IF i_cont-revpgc IN gr_pgc_rerent.
          i_cont-rerentrev = it_vbap-netwr.
        ENDIF.
* sales revenue
        i_cont-salesrev = gv_zero.
        IF i_cont-revpgc IN gr_pgc_sales.
          i_cont-salesrev = it_vbap-netwr.
        ENDIF.
* fuel revenue
        i_cont-fuelrev = gv_zero.
        IF i_cont-revpgc IN gr_pgc_fuel.
          i_cont-fuelrev = it_vbap-netwr.
        ENDIF.
* workorder revenue
        i_cont-servrev = gv_zero.
        IF i_cont-revpgc IN gr_pgc_workorder.
          i_cont-servrev = it_vbap-netwr.
        ENDIF.
* transport revenue
        i_cont-tranrev = gv_zero.
        IF i_cont-revpgc IN gr_pgc_transport.
          i_cont-tranrev = it_vbap-netwr.
        ENDIF.
* miscelaneous revenue
        i_cont-miscrev = gv_zero.
        IF i_cont-revpgc IN gr_pgc_misc.
          IF it_vbap-pstyv <> c_zbp2.
            i_cont-miscrev = it_vbap-netwr.
          ENDIF.
        ENDIF.
* revenue recognition
        i_cont-revrecog = gv_zero.
        CLEAR gv_rvamt.
        SELECT SUM( wrbtr ) INTO gv_rvamt FROM vbreve
           WHERE vbeln = it_vbap-vbeln
             AND posnr = it_vbap-posnr
             AND bdjpoper = gv_yearper
             AND wrbtr <> 0.
        IF gv_rvamt <> 0.
          i_cont-revrecog = gv_rvamt * -1 .
        ENDIF.
* material description
        i_cont-matdesc = it_vbap-arktx.
* uom + bookrate
        i_cont-bookrate = gv_zero.
        CASE it_vbap-kmein.
          WHEN 'TAG'.
            i_cont-uom = c_day.
            i_cont-bookrate = it_vbap-netpr.
          WHEN 'MON'.
            i_cont-uom = c_month.
            i_cont-bookrate = it_vbap-netpr.
          WHEN OTHERS.
        ENDCASE.
* currency
        MOVE it_vbap-waerk TO i_cont-waers.

        i_cont-systdate = sy-datum.

        APPEND i_cont.
      ENDLOOP.

    ELSE.
* sales

* get sales lines
      CLEAR : it_vbap.
      REFRESH : it_vbap.
      SELECT * INTO CORRESPONDING FIELDS OF TABLE it_vbap
        FROM vbap WHERE vbeln = i_cont_init-vbeln.
* loop over contract lines
      LOOP AT it_vbap.
        CLEAR i_cont.
        MOVE-CORRESPONDING i_cont_init TO i_cont.
* get revenue-pgc
        SELECT SINGLE ww007 INTO i_cont-revpgc FROM ce41000_acct
          WHERE aktbo = c_x
            AND paobjnr = it_vbap-paobjnr.
* item line
        MOVE it_vbap-posnr TO i_cont-posnum.
* customer number = sold-to party
        SELECT SINGLE kunnr INTO i_cont-kunnr
          FROM vbpa WHERE vbeln = i_cont-vbeln
                      AND posnr = 0
                      AND parvw = c_ag.

* lookup equipment
        SELECT SINGLE obknr INTO gv_obknr FROM ser02
           WHERE sdaufnr = it_vbap-vbeln
             AND posnr   = it_vbap-posnr.
        IF sy-subrc = 0.
          SELECT SINGLE equnr INTO i_cont-equnr
            FROM objk WHERE obknr = gv_obknr
                        AND obzae = 1.
        ENDIF.
* model
        SELECT SINGLE pmatn INTO i_cont-model
           FROM mvke WHERE matnr = it_vbap-matnr
                       AND vkorg = i_cont_init-vkorg
                       AND vtweg IN s_vtweg.
* start date (= purchase order date)
        CLEAR : gv_vbegdat.
        SELECT SINGLE bstdk INTO gv_vbegdat FROM vbak
           WHERE vbeln  = i_cont-vbeln.
        MOVE gv_vbegdat TO i_cont-startd.
* sales revenue
        IF i_cont-revpgc IN gr_pgc_sales.
          i_cont-salesrev = it_vbap-netwr.
        ENDIF.
* material description
        i_cont-matdesc = it_vbap-arktx.

        i_cont-systdate = sy-datum.

        APPEND i_cont.
      ENDLOOP.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " Get_Detail
*---------------------------------------------------------------------
*  FORM UNIT_CONVERSION
*---------------------------------------------------------------------
FORM unit_conversion USING p_einheit
                           p_fltp_wert
                           p_char_wert
                           p_decimal
                           p_exponent.

  CLEAR p_char_wert.
  CHECK NOT ( p_fltp_wert IS INITIAL ).

  CALL FUNCTION 'FLTP_CHAR_CONVERSION_FROM_SI'
    EXPORTING
      char_unit       = p_einheit
      decimals        = p_decimal
      exponent        = p_exponent
      fltp_value_si   = p_fltp_wert
      indicator_value = c_x
      masc_symbol     = ' '
    IMPORTING
      char_value      = p_char_wert.

ENDFORM.                    "UNIT_CONVERSION
*&---------------------------------------------------------------------*
*&      Form  Create_delta
*&---------------------------------------------------------------------*
*       Create internal table with creations/updates
*----------------------------------------------------------------------*
FORM create_delta.

  LOOP AT i_cont.

    READ TABLE i_prev WITH KEY kunnr = i_cont-vbeln
                               posnum = i_cont-posnum
                    BINARY SEARCH.

    IF sy-subrc = 0.
***** MUST BE UPDATED when structure yse_im_cont is extended *
      IF i_cont(409) <> i_prev(409).
************************************************************************
        MOVE-CORRESPONDING i_cont TO i_cont_delta.
        APPEND i_cont_delta.
        CLEAR i_cont_delta.
      ENDIF.
    ELSE.
      MOVE-CORRESPONDING i_cont TO i_cont_delta.
      APPEND i_cont_delta.
      CLEAR i_cont_delta.
    ENDIF.

  ENDLOOP.

ENDFORM.                    " Create_delta
*&---------------------------------------------------------------------*
*&      Form  Get_previous_file
*&---------------------------------------------------------------------*
*       Get file from previous run
*----------------------------------------------------------------------*
FORM get_previous_file USING p_bukrs
                    CHANGING p_retcd.

* prepare filename of previous run
  CLEAR g_ersda.
  SELECT ersda INTO g_ersda
      FROM yse_infomanager WHERE bukrs = p_bukrs
                             AND object = c_object.
  ENDSELECT.

  IF sy-subrc <> 0.
    WRITE: / text-e02, p_bukrs, c_object.  "No filename of previous run
*                available in custom table yse_infomanager
    p_retcd = 4.
    EXIT.
  ENDIF.

  CONCATENATE p_bukrs c_object g_ersda
                      INTO g_pfile SEPARATED BY c_underscore.
  REPLACE 'xxx' IN g_directory2 WITH p_logsys(3).
  CONCATENATE g_directory2 g_pfile INTO g_pfile.

* FILE READ FROM APPLICATION SERVER
  PERFORM get_from_appl TABLES  i_prev
                        USING   g_pfile
                                p_retcd.

ENDFORM.                    " Get_previous_file
*&---------------------------------------------------------------------*
*&      Form  Get_from_appl
*&---------------------------------------------------------------------*
*       Get the file from application server into internal table
*----------------------------------------------------------------------*
FORM get_from_appl TABLES i_infile STRUCTURE i_prev
                   USING p_infile p_subrc.

  OPEN DATASET p_infile FOR INPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, p_infile.
    p_subrc = 4.
    EXIT.
  ENDIF.

  REFRESH i_infile.

  DO.
    READ DATASET p_infile INTO i_infile.
    IF sy-subrc <> 0.
      EXIT.
    ENDIF.
    APPEND i_infile.
  ENDDO.
  CLOSE DATASET p_infile.

ENDFORM.                    " GET_FROM_APPL
*&---------------------------------------------------------------------*
*&      Form  Write_outputfile
*&---------------------------------------------------------------------*
*       Write outputfile
*----------------------------------------------------------------------*
FORM write_outputfile.

  OPEN DATASET g_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, g_ofile.
*   message e011(YAM_DC).                      "Open dataset failed
    g_retcd = 4.
    EXIT.
  ENDIF.

  SORT i_cont_delta BY vbeln posnum.

  REFRESH it_outxls.
* Make the output table ; delimited
  CALL FUNCTION 'SAP_CONVERT_TO_CSV_FORMAT'
    TABLES
      i_tab_sap_data       = i_cont_delta
    CHANGING
      i_tab_converted_data = it_outxls
    EXCEPTIONS
      conversion_failed    = 1
      OTHERS               = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  CLEAR wa_outxls.
* Process lines
  LOOP AT it_outxls INTO wa_outxls.
    REPLACE ALL OCCURRENCES OF ';' IN wa_outxls WITH c_tab.
    TRANSFER wa_outxls TO g_ofile.
  ENDLOOP.

ENDFORM.                    " Write_outputfile
*&---------------------------------------------------------------------*
*&      Form  Update_custom_table
*&---------------------------------------------------------------------*
*       Update custom table YSE_INFOMANAGER
*----------------------------------------------------------------------*
FORM update_custom_table USING p_swerk.

  wa_yse_infomanager-mandt = sy-mandt.
  wa_yse_infomanager-bukrs = p_bukrs.
  wa_yse_infomanager-object = c_object.
  wa_yse_infomanager-ersda = g_ersda.

  INSERT yse_infomanager FROM wa_yse_infomanager.

ENDFORM.                    " Update_custom_table
*&---------------------------------------------------------------------*
*&      Form  Delete_old_files
*&---------------------------------------------------------------------*
*       Delete files from former runs on application server
*----------------------------------------------------------------------*
FORM delete_old_files.

  LOOP AT i_delfiles.

    CONCATENATE p_bukrs c_object i_delfiles-ersda
                         INTO g_dfile SEPARATED BY c_underscore.

    MOVE g_directory TO g_dir_name.
    REPLACE 'xxx' IN g_dir_name WITH p_logsys(3).
    TRANSLATE g_dir_name(10) TO LOWER CASE.
    TRANSLATE g_dir_name+17(8) TO LOWER CASE.

* change MJ 20090602
*    CALL FUNCTION 'EPS_DELETE_FILE'
     call function 'YSE_EPS_DELETE_FILE'
      EXPORTING
        file_name              = g_dfile
        dir_name               = g_dir_name
      EXCEPTIONS
        invalid_eps_subdir
        sapgparam_failed
        build_directory_failed
        no_authorization
        build_path_failed
        delete_failed.

    IF sy-subrc <> 0.
      WRITE: / text-e05, g_dfile.      "Could not delete file
    ENDIF.

  ENDLOOP.

ENDFORM.                    "delete_old_files
*&---------------------------------------------------------------------*
*&      Form  Delete_old_table_entries
*&---------------------------------------------------------------------*
*       Delete entries from former runs in custom table YSE_INFOMANAGER
*----------------------------------------------------------------------*
FORM delete_old_table_entries USING p_bukrs.

  g_date = sy-datum - 7.
  CONCATENATE g_date c_0000 INTO g_ersda.

  REFRESH i_delfiles.

  SELECT * FROM yse_infomanager
      WHERE bukrs EQ p_bukrs
        AND object EQ c_object
        AND ersda LT g_ersda.
    MOVE yse_infomanager TO i_delfiles.
    APPEND i_delfiles.
    CLEAR i_delfiles.
  ENDSELECT.

  IF sy-subrc = 0.
    DELETE FROM yse_infomanager WHERE bukrs EQ p_bukrs
                                  AND object EQ c_object
                                  AND ersda LT g_ersda.
    IF sy-subrc <> 0.
      WRITE: / text-e06, p_bukrs.

    ENDIF.
  ENDIF.

ENDFORM.                    "delete_old_table_entries

*&---------------------------------------------------------------------*
*&      Form  OVERWRITE_FILE_TO_COMPARE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM overwrite_file_to_compare .

* delete old file
  CONCATENATE p_bukrs c_object INTO g_dfile SEPARATED BY c_underscore.

  MOVE g_directory2 TO g_dir_name.
  REPLACE 'xxx' IN g_dir_name WITH p_logsys(3).
  TRANSLATE g_dir_name(10) TO LOWER CASE.
  TRANSLATE g_dir_name+17(8) TO LOWER CASE.

* change MJ 20090602
*    CALL FUNCTION 'EPS_DELETE_FILE'
     call function 'YSE_EPS_DELETE_FILE'
    EXPORTING
      file_name              = g_dfile
      dir_name               = g_dir_name
    EXCEPTIONS
      invalid_eps_subdir
      sapgparam_failed
      build_directory_failed
      no_authorization
      build_path_failed
      delete_failed.

  IF sy-subrc <> 0.
    WRITE: / text-e05, g_dfile.      "Could not delete file
  ENDIF.

* create new file
  CLEAR g_retcd.

  CONCATENATE p_bukrs c_object INTO g_ofile SEPARATED BY c_underscore.
  REPLACE 'xxx' IN g_directory2 WITH p_logsys(3).
  CONCATENATE g_directory2 g_ofile INTO g_ofile.

* write outputfile
  OPEN DATASET g_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, g_ofile.
*   message e011(YAM_DC).                      "Open dataset failed
    g_retcd = 4.
    EXIT.
  ENDIF.

  SORT i_cont BY vbeln posnum.

  LOOP AT i_cont.
***** MUST BE UPDATED when structure of segment YSE_IM_CONT is extended
    TRANSFER i_cont(409) TO g_ofile.
************************************************************************
  ENDLOOP.
ENDFORM.                    " OVERWRITE_FILE_TO_COMPARE
*&---------------------------------------------------------------------*
*&      Form  FILL_RANGES
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM fill_ranges .

* Fill global range with document types which represent rental contracts
  gr_auart_contract-sign = 'I'.
  gr_auart_contract-option = 'EQ'.
  gr_auart_contract-low = gc_auart_zqp.
  APPEND gr_auart_contract.
  gr_auart_contract-low = gc_auart_zqp2.
  APPEND gr_auart_contract.
  gr_auart_contract-low = gc_auart_zrib.
  APPEND gr_auart_contract.

* rental pgc's
  gr_pgc_rent-sign = 'I'.
  gr_pgc_rent-option = 'EQ'.
  gr_pgc_rent-low = gc_pgc_5900.
  APPEND gr_pgc_rent.
  gr_pgc_rent-low = gc_pgc_5910.
  APPEND gr_pgc_rent.
  gr_pgc_rent-low = gc_pgc_5911.
  APPEND gr_pgc_rent.
  gr_pgc_rent-low = gc_pgc_5920.
  APPEND gr_pgc_rent.
  gr_pgc_rent-low = gc_pgc_5930.
  APPEND gr_pgc_rent.
  gr_pgc_rent-low = gc_pgc_5940.
  APPEND gr_pgc_rent.
  gr_pgc_rent-low = gc_pgc_5949.
  APPEND gr_pgc_rent.

* insurance pgc
  gr_pgc_insurance-sign = 'I'.
  gr_pgc_insurance-option = 'EQ'.
  gr_pgc_insurance-low = gc_pgc_59sm.
  APPEND gr_pgc_insurance.
  gr_pgc_insurance-low = gc_pgc_59so.
  APPEND gr_pgc_insurance.

* re-rent pgc
  gr_pgc_rerent-sign = 'I'.
  gr_pgc_rerent-option = 'EQ'.
  gr_pgc_rerent-low = gc_pgc_59re.
  APPEND gr_pgc_rerent.
  gr_pgc_rerent-low = gc_pgc_59ri.
  APPEND gr_pgc_rerent.

* fuel
  gr_pgc_fuel-sign = 'I'.
  gr_pgc_fuel-option = 'EQ'.
  gr_pgc_fuel-low = gc_pgc_59fu.
  APPEND gr_pgc_fuel.

* transport
  gr_pgc_transport-sign = 'I'.
  gr_pgc_transport-option = 'EQ'.
  gr_pgc_transport-low = gc_pgc_59lo.
  APPEND gr_pgc_transport.

* workorder
  gr_pgc_workorder-sign = 'I'.
  gr_pgc_workorder-option = 'EQ'.
  gr_pgc_workorder-low = gc_pgc_59sl.
  APPEND gr_pgc_workorder.

* sales
  gr_pgc_sales-sign = 'I'.
  gr_pgc_sales-option = 'EQ'.
  gr_pgc_sales-low = gc_pgc_59sp.
  APPEND gr_pgc_sales.
  gr_pgc_sales-low = gc_pgc_59sn.
  APPEND gr_pgc_sales.
  gr_pgc_sales-low = gc_pgc_59ue.
  APPEND gr_pgc_sales.

* misc
  gr_pgc_misc-sign = 'I'.
  gr_pgc_misc-option = 'EQ'.
  gr_pgc_misc-low = gc_pgc_59so.
  APPEND gr_pgc_misc.

ENDFORM.                    " FILL_RANGES

*Text symbol text£º
*001:Selection Screen Input
*E01:No data available for the selected company :
*E02:No previous run entry in table YSE_INFOMANAGER for company :
*E03:Open dataset failed for :
*E04:No counter found for measurement point :
*E05:Could not delete file :
*E06:Could not delete entrie(s) in table YSE_INFOMANAGER for :
*I02:Job finished

*I03:No delta found
*Selection text£º
*P_BUKRS:        Company
*P_INIT:        Initial Load
*S_VBELN:        Contract Number
*S_VTWEG:        Distribution Channel
