*&---------------------------------------------------------------------*
*& Report  YSE_ACT_EQUIPM_WARRANTY_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Equipment Warranty extraction                                *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 05.07.2012
*  Change Request Number :
*  Transport request Nr. : CD1K972470
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 19.03.2014 | Jules Smets     | CD1K980489    | CR1794 (Act)*
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_act_equipm_warranty_extr.

TABLES: equz,
        equi,
        bgmkobj,
        jest.

TYPE-POOLS: ibco2.

TYPES: BEGIN OF ty_equz,
         equnr      TYPE equnr,
         hequi      TYPE hequi,
         objnr      TYPE j_objnr,
      END OF ty_equz.

TYPES: BEGIN OF ty_dlfl,
         objnr      TYPE j_objnr,
      END OF ty_dlfl.

TYPES: BEGIN OF ty_equi,
         equnr      TYPE equnr,
         objnr      TYPE j_objnr,
         cuobj      TYPE cuobj,
         kmatn      TYPE kmatn,
      END OF ty_equi.

TYPES: BEGIN OF ty_bgmkobj,
         objnr      TYPE j_objnr,
         gaart      TYPE gaart,
         gwldt      TYPE gwldt,
         gwlen      TYPE gwlen,
         waget      TYPE waget,
         gaerb      TYPE gaerb,
       END OF ty_bgmkobj.

DATA: gt_equz  TYPE TABLE OF ty_equz
                    WITH HEADER LINE,
      gt_equh  TYPE TABLE OF ty_equz
                    WITH HEADER LINE.

DATA: gt_dlfl  TYPE HASHED TABLE OF ty_dlfl
                    WITH UNIQUE KEY objnr
                    WITH HEADER LINE,
      gt_dlfli TYPE TABLE OF ty_dlfl
                    WITH HEADER LINE.

DATA: gt_equi  TYPE HASHED TABLE OF ty_equi
                    WITH UNIQUE KEY equnr
                    WITH HEADER LINE.

DATA: gt_bgmkobj  TYPE HASHED TABLE OF ty_bgmkobj
                       WITH UNIQUE KEY objnr gaart
                       WITH HEADER LINE,
      gt_bgmkobji TYPE TABLE OF ty_bgmkobj
                       WITH HEADER LINE.

DATA: BEGIN OF gt_out  OCCURS 0,
        equnr      TYPE equnr,
        tab01(1)   TYPE c,
        hequi      TYPE hequi,
        tab02(1)   TYPE c,
        gwldt_c    TYPE gwldt,
        tab03(1)   TYPE c,
        gwlen_c    TYPE gwlen,
        tab04(1)   TYPE c,
        waget_c    TYPE waget,
        tab05(1)   TYPE c,
        gaerb_c    TYPE gaerb,
        tab06(1)   TYPE c,
        gwldt_v    TYPE gwldt,
        tab07(1)   TYPE c,
        gwlen_v    TYPE gwlen,
        tab08(1)   TYPE c,
        waget_v    TYPE waget,
        tab09(1)   TYPE c,
        gaerb_v    TYPE gaerb,
        tab10(1)   TYPE c,
        kmatn      TYPE kmatn,
        tab11(1)   TYPE c,
        atwrt      TYPE atwrt,
        tab12(1)   TYPE c,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        equnr      TYPE fieldname,
        tab01(1)   TYPE c,
        hequi      TYPE fieldname,
        tab02(1)   TYPE c,
        gwldt_c    TYPE fieldname,
        tab03(1)   TYPE c,
        gwlen_c    TYPE fieldname,
        tab04(1)   TYPE c,
        waget_c    TYPE fieldname,
        tab05(1)   TYPE c,
        gaerb_c    TYPE fieldname,
        tab06(1)   TYPE c,
        gwldt_v    TYPE fieldname,
        tab07(1)   TYPE c,
        gwlen_v    TYPE fieldname,
        tab08(1)   TYPE c,
        waget_v    TYPE fieldname,
        tab09(1)   TYPE c,
        gaerb_v    TYPE fieldname,
        tab10(1)   TYPE c,
        kmatn      TYPE fieldname,
        tab11(1)   TYPE c,
        atwrt      TYPE fieldname,
        tab12(1)   TYPE c,
      END OF gt_outh.

DATA: gt_chval        TYPE ibco2_value_tab.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_atinn         TYPE atinn,
      gv_equnr         TYPE equnr,
      gv_sub           TYPE xfeld,
      gv_error         TYPE xfeld.

CONSTANTS: gc_atnam_c  TYPE atnam     VALUE 'CH_EQUIPMENT_COUNTRY',
           gc_stat_dl  TYPE j_status  VALUE 'I0076',
           gc_stat_in  TYPE j_status  VALUE 'I0320',
           gc_stat_scr TYPE j_status  VALUE 'E0005'.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
PARAMETERS: p_iwerk  LIKE equz-iwerk  OBLIGATORY  MEMORY ID iwk,
            p_ingrp  LIKE equz-ingrp  OBLIGATORY. "MEMORY ID ihg.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_equnr  FOR equz-equnr.
SELECTION-SCREEN  END OF BLOCK input.

FIELD-SYMBOLS: <chval>  TYPE ibco2_value_rec.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

* Convert characteristic
  CALL FUNCTION 'CONVERSION_EXIT_ATINN_INPUT'
    EXPORTING
      input  = gc_atnam_c
    IMPORTING
      output = gv_atinn.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_equz[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No equipments selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Equipment time segment
  SELECT z~equnr z~hequi i~objnr
         INTO TABLE gt_equz
         FROM equz AS z
         INNER JOIN equi AS i
                    ON i~equnr = z~equnr
         WHERE z~equnr IN s_equnr
           AND z~iwerk =  p_iwerk
           AND z~ingrp =  p_ingrp
           AND z~datbi =  '99991231'.
  CHECK NOT gt_equz[] IS INITIAL.

*** MOD-001 * begin ***
** Check for deleted/scrapped equipments
*  SELECT objnr
*         INTO TABLE gt_dlfli
*         FROM jest
*         FOR ALL ENTRIES IN gt_equz
*         WHERE objnr = gt_equz-objnr
*           AND ( stat = gc_stat_dl   OR
*                 stat = gc_stat_in   OR
*                 stat = gc_stat_scr )
*           AND inact = ' '.
*  SORT gt_dlfli BY objnr.
*  DELETE ADJACENT DUPLICATES FROM gt_dlfli
*                                  COMPARING objnr.
*  gt_dlfl[] = gt_dlfli[].
*  FREE gt_dlfli.
*
*  LOOP AT gt_equz.
*    READ TABLE gt_dlfl WITH TABLE KEY objnr = gt_equz-objnr.
*    IF sy-subrc = 0.
*      DELETE gt_equz.
*    ENDIF.
*  ENDLOOP.
*  FREE gt_dlfl.
*** MOD-001 * end ***

* Main equipments
  gt_equh[] = gt_equz[].
  DELETE gt_equh WHERE NOT hequi IS INITIAL.

* Equipment master data
  CHECK NOT gt_equh[] IS INITIAL.
  SELECT equnr objnr cuobj kmatn
         INTO TABLE gt_equi
         FROM equi
         FOR ALL ENTRIES IN gt_equh
         WHERE equnr = gt_equh-equnr.
  FREE gt_equh.

* Master Warranty - Object Assignment
  CHECK NOT gt_equi[] IS INITIAL.
  SELECT j_objnr gaart gwldt gwlen waget gaerb
         INTO TABLE gt_bgmkobj
         FROM bgmkobj
         FOR ALL ENTRIES IN gt_equi
         WHERE j_objnr = gt_equi-objnr
           AND ( gaart = '1' OR
                 gaart = '2' ).

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_equz.

    CLEAR: gv_sub, gt_out.
    MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03,
                 gt_out-tab04, gt_out-tab05, gt_out-tab06,
                 gt_out-tab07, gt_out-tab08, gt_out-tab09,
                 gt_out-tab10, gt_out-tab11, gt_out-tab12.

*   Main equipment
    IF NOT gt_equz-hequi IS INITIAL.
      gv_equnr = gt_equz-hequi.
      gv_sub = 'X'.
    ELSE.
      gv_equnr = gt_equz-equnr.
    ENDIF.
    READ TABLE gt_equi WITH TABLE KEY equnr = gv_equnr.
    CHECK sy-subrc = 0.
    gt_out-equnr = gt_equz-equnr.
    gt_out-hequi = gt_equz-hequi.
    IF gv_sub IS INITIAL.
      gt_out-kmatn = gt_equi-kmatn.
    ENDIF.

*   Warranty data
*   Customer
    READ TABLE gt_bgmkobj WITH TABLE KEY objnr = gt_equi-objnr
                                         gaart = '1'.
    IF sy-subrc = 0.
      gt_out-gwldt_c = gt_bgmkobj-gwldt.
      gt_out-gwlen_c = gt_bgmkobj-gwlen.
      gt_out-waget_c = gt_bgmkobj-waget.
      gt_out-gaerb_c = gt_bgmkobj-gaerb.
    ENDIF.
*   Vendor
    READ TABLE gt_bgmkobj WITH TABLE KEY objnr = gt_equi-objnr
                                         gaart = '2'.
    IF sy-subrc = 0.
      gt_out-gwldt_v = gt_bgmkobj-gwldt.
      gt_out-gwlen_v = gt_bgmkobj-gwlen.
      gt_out-waget_v = gt_bgmkobj-waget.
      gt_out-gaerb_v = gt_bgmkobj-gaerb.
    ENDIF.

*   Characteristics
    IF gv_sub IS INITIAL.
      CLEAR gt_chval[].
      CALL FUNCTION 'CUCB_GET_VALUES_FROM_INSTANCE'
        EXPORTING
          iv_instance                  = gt_equi-cuobj
        IMPORTING
          et_values                    = gt_chval
        EXCEPTIONS
          invalid_instance             = 1
          instance_is_a_classification = 2
          OTHERS                       = 3.
      IF sy-subrc = 0.
        LOOP AT gt_chval ASSIGNING <chval>
                         WHERE atinn = gv_atinn.
          gt_out-atwrt = <chval>-atwrt.
          EXIT.
        ENDLOOP.
      ELSE.
*        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
      ENDIF.
    ENDIF.

    APPEND gt_out.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

  CONCATENATE 'EQUIPMENT' 'WARRANTY' p_iwerk p_ingrp
             INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM fill_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

* Fill output
  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  FILL_HEADER
*&---------------------------------------------------------------------*
*       Fill header
*----------------------------------------------------------------------*
FORM fill_header .

  CLEAR: gt_outh.
  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03,
               gt_outh-tab04, gt_outh-tab05, gt_outh-tab06,
               gt_outh-tab07, gt_outh-tab08, gt_outh-tab09,
               gt_outh-tab10, gt_outh-tab11, gt_outh-tab12.

  gt_outh-equnr   = 'EQUNR'.
  gt_outh-hequi   = 'HEQUI'.
  gt_outh-gwldt_c = 'GWLDT_c'.
  gt_outh-gwlen_c = 'GWLEN_c'.
  gt_outh-waget_c = 'WAGET_c'.
  gt_outh-gaerb_c = 'GAERB_c'.
  gt_outh-gwldt_v = 'GWLDT_v'.
  gt_outh-gwlen_v = 'GWLEN_v'.
  gt_outh-waget_v = 'WAGET_v'.
  gt_outh-gaerb_v = 'GAERB_v'.
  gt_outh-kmatn   = 'KMATN'.
  gt_outh-atwrt   = 'ATWRT'.
  APPEND gt_outh.

  gt_outh-equnr   = 'Equipment'.
  gt_outh-hequi   = 'Main Equipment'.
  gt_outh-gwldt_c = 'Warranty begin (Cust.)'.
  gt_outh-gwlen_c = 'Warranty end (Cust.)'.
  gt_outh-waget_c = 'Inherit (Cust.)'.
  gt_outh-gaerb_c = 'Pass on (Cust.)'.
  gt_outh-gwldt_v = 'Warranty begin (Vend.)'.
  gt_outh-gwlen_v = 'Warranty end (Vend.)'.
  gt_outh-waget_v = 'Inherit (Vend.)'.
  gt_outh-gaerb_v = 'Pass on (Vend.)'.
  gt_outh-kmatn   = 'Configurable Mat.'.
  gt_outh-atwrt   = 'Charact. Value'.
  APPEND gt_outh.

ENDFORM.                    " FILL_HEADER

*Text symbol text£º
*E02:Could not open output file
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_INGRP:D       .
*P_IWERK:D       .
*S_EQUNR:D       .
