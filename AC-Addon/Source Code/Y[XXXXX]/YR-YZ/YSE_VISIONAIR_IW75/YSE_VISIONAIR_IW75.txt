*&---------------------------------------------------------------------*
*----------------------------------------------------------------------*
* Author  : Anda Wu                                                    *
* Created : 23.09.2016                                                 *
* Dev.nbr: CR3987:SAP data to VA China database                        *
*----------------------------------------------------------------------*
* User administration full access report                               *
************************************************************************
* Mod-xxx | xxxx.xx.xx | xxxx xxxx     | CD1Kxxxxxxxxxx  |   CRXXXX    *
************************************************************************
* Mod-000 | 2016.09.23 | Anda Wu       | CD1K989781      |   CR3987    *
************************************************************************
REPORT yse_visionair_iw75 NO STANDARD PAGE HEADING LINE-SIZE 255.
************************************************************************
* TYPES                                                                *
************************************************************************
TABLES: vbak, veda.
************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:

BEGIN OF ty_vbak,
  vbeln   TYPE    vbap-vbeln,       "Sales Doc.
  auart   TYPE    vbak-auart,       "Order type
  vkgrp   TYPE    vbak-vkgrp,       "Sales Grp
  vkbur   TYPE    vbak-vkbur,       "Sales Off.
  vkorg   TYPE    vbak-vkorg,       "Sales Org
END OF ty_vbak,

BEGIN OF ty_veda,
  vbeln   TYPE    veda-vbeln,       "Sales Document
  vposn   TYPE    veda-vposn,       "Sales Document Item
  vbegdat TYPE    veda-vbegdat,     "Contract start date
  venddat TYPE    veda-venddat,     "Contract end date
END OF ty_veda,

BEGIN OF ty_ser_objk,
  obknr   TYPE    ser02-obknr,      "Object list number
  sdaufnr TYPE    ser02-sdaufnr,    "Sales Document
  posnr   TYPE    ser02-posnr,      "Sales Document Item
  equnr   TYPE    objk-equnr,       "Equipment Number
  sernr   TYPE    objk-sernr,       "Serial number
  matnr   TYPE    objk-matnr,       "Material Number
END OF ty_ser_objk,

BEGIN OF ty_vbap,
  vbeln   TYPE    vbap-vbeln,       "Sales Doc.
  posnr   TYPE    vbap-posnr,       "Sales item
  prodh   TYPE    vbap-prodh,       "product  hierarchy
  prctr   TYPE    vbap-prctr,       "Profit Center
  matkl   TYPE    vbap-matkl,       "Matl Group
END OF ty_vbap.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_vbak         TYPE STANDARD TABLE OF ty_vbak,
  gt_vbap         TYPE STANDARD TABLE OF ty_vbap,
  gt_veda         TYPE STANDARD TABLE OF ty_veda,
  gt_ser_objk     TYPE STANDARD TABLE OF ty_ser_objk,
  gt_out_file     TYPE STANDARD TABLE OF yse_outfile_iw75.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_gac(4)     TYPE c,
  gv_pgc(4)     TYPE c,
  gv_plc        TYPE rkeg_ww002,
  gv_col(10)    TYPE n,
  gv_text       TYPE string,
  gv_save       TYPE c.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x                TYPE c VALUE 'X',
  gc_type_e           TYPE c VALUE 'E'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01 .
SELECT-OPTIONS:
  s_vkorg     FOR vbak-vkorg OBLIGATORY,   "Sales Org
  s_begdat    FOR veda-vbegdat,            "Contract start date
  s_enddat    FOR veda-venddat.            "Contract end date
PARAMETERS: p_server   TYPE char50
  DEFAULT '/var/load/xxx/UK/read/VISIONAIR_CTSCHINA/'.
SELECTION-SCREEN END OF BLOCK b1.

************************************************************************
* START-OF-SELECTION                                                     *
************************************************************************
START-OF-SELECTION.
* Get data from table
  PERFORM get_data.
* Generate data
  PERFORM generate_data.

  IF sy-batch IS NOT INITIAL.
*   Send_data
    PERFORM send_data.
    MESSAGE i001(00) WITH text-m04 p_server.
  ELSE.
*   Download the file to local PC
    PERFORM download_file.
    MESSAGE s001(00) WITH text-m09 p_server.
  ENDIF.

*&---------------------------------------------------------------------*
*&      Form  GET_DATA
*&---------------------------------------------------------------------*
*       Get data from table
*----------------------------------------------------------------------*
FORM get_data .

  DATA: lrg_auart   TYPE RANGE OF vbak-auart,
        ls_auart    LIKE LINE OF lrg_auart.

  ls_auart-sign    = 'I'.
  ls_auart-option  = 'EQ'.
  ls_auart-low      = 'ZC01'.
  APPEND ls_auart TO lrg_auart.

  ls_auart-sign    = 'I'.
  ls_auart-option  = 'EQ'.
  ls_auart-low      = 'ZC02'.
  APPEND ls_auart TO lrg_auart.

  SELECT  vbeln               "Sales Doc.
          auart               "Order type
          vkgrp               "Sales Grp
          vkbur               "Sales Off.
          vkorg               "Sales Org
    FROM  vbak
    INTO TABLE gt_vbak
    WHERE vkorg IN s_vkorg
      AND auart IN lrg_auart.

  CHECK gt_vbak IS NOT INITIAL.

  SELECT  vbeln               "Sales Doc.
          posnr               "Sales item
          prodh               "product  hierarchy
          prctr               "Profit Center
          matkl               "Matl Group
    INTO  TABLE gt_vbap
    FROM vbap
    FOR ALL ENTRIES IN gt_vbak
    WHERE vbeln = gt_vbak-vbeln.

  CHECK gt_vbap IS NOT INITIAL.

  SELECT  vbeln               "Sales Document
          vposn               "Sales Document Item
          vbegdat             "Contract start date
          venddat             "Contract end date
    INTO TABLE gt_veda
    FROM veda
    FOR ALL ENTRIES IN gt_vbap
    WHERE vbeln = gt_vbap-vbeln
*      AND vposn = gt_vbap-posnr
      AND vbegdat IN s_begdat
      AND venddat IN s_enddat.

  CHECK gt_vbap IS NOT INITIAL AND gt_veda IS NOT INITIAL.

  SELECT  ser02~obknr         "Object list number
          ser02~sdaufnr       "Sales Document
          ser02~posnr         "Sales Document Item
          objk~equnr          "Equipment Number
          objk~sernr          "Serial number
          objk~matnr          "Material Number
    FROM ser02
    INNER JOIN objk
    ON ser02~obknr = objk~obknr
    INTO TABLE gt_ser_objk
    FOR ALL ENTRIES IN gt_vbap
    WHERE ser02~sdaufnr  = gt_vbap-vbeln
      AND ser02~posnr    = gt_vbap-posnr.

ENDFORM.                    " GET_DATA
*&---------------------------------------------------------------------*
*&      Form  GENERATE_DATA
*&---------------------------------------------------------------------*
*       Generate data
*----------------------------------------------------------------------*
FORM generate_data .

  DATA: ls_out_file     TYPE yse_outfile_iw75,
        ls_veda         TYPE ty_veda,
        ls_vbak         TYPE ty_vbak,
        ls_vbap         TYPE ty_vbap,
        ls_ser_objk     TYPE ty_ser_objk.

*  LOOP AT gt_veda INTO ls_veda.
  loop at gt_vbap INTO ls_vbap.
    CLEAR: ls_vbak , ls_veda , ls_ser_objk.
    READ TABLE gt_vbak   INTO ls_vbak
      WITH KEY vbeln = ls_vbap-vbeln.
    READ TABLE gt_veda   INTO ls_veda
      WITH KEY vbeln = ls_vbap-vbeln.
    IF sy-subrc <> 0.
      CONTINUE.
    ENDIF.
    READ TABLE gt_ser_objk INTO ls_ser_objk
      WITH KEY sdaufnr = ls_vbap-vbeln
               posnr   = ls_vbap-posnr.

    ls_out_file-vkbur   =    ls_vbak-vkbur.     "Sales Off.
    ls_out_file-prodh   =    ls_vbap-prodh.     "product  hierarchy
    ls_out_file-prctr   =    ls_vbap-prctr.     "Profit Center
    ls_out_file-vkgrp   =    ls_vbak-vkgrp.     "Sales Grp
    ls_out_file-matkl   =    ls_vbap-matkl.     "Matl Group
    ls_out_file-vbeln   =    ls_vbap-vbeln.     "Sales Doc.
    ls_out_file-equnr   =    ls_ser_objk-equnr. "Equipment
    ls_out_file-vbegdat =    ls_veda-vbegdat.   "Start dt
    ls_out_file-venddat =    ls_veda-venddat.   "End date
    ls_out_file-sernr   =    ls_ser_objk-sernr. "SerialNumber
    ls_out_file-posnr   =    ls_vbap-posnr.     "So item
    APPEND ls_out_file TO gt_out_file.
  ENDLOOP.
  IF gt_out_file IS INITIAL.
    MESSAGE s001(00) DISPLAY LIKE 'E'
    WITH 'No data generated, please check'(m08).
    LEAVE LIST-PROCESSING.
  ENDIF.
ENDFORM.                    " GENERATE_DATA
*&---------------------------------------------------------------------*
*&      Form  SEND_DATA
*&---------------------------------------------------------------------*
*       Send_data
*----------------------------------------------------------------------*
FORM send_data .

  DATA: lv_file     TYPE /sapdmc/ls_filename,
        ls_out_file TYPE yse_outfile_iw57,
        lv_filename TYPE char128,
        lv_directory(50) TYPE c,
        lv_line     TYPE string,
        lv_lines    TYPE char10,
        lv_logsys   TYPE tbdlst-logsys.

  lv_directory = p_server.
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = lv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
  REPLACE 'xxx' IN lv_directory WITH lv_logsys(3).
  CONCATENATE lv_directory 'IW75.CSV' INTO  lv_file.
  OPEN DATASET lv_file FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    MESSAGE s001(00) DISPLAY LIKE 'E'
    WITH 'Open dataset failed for :'(m03) lv_file.
    LEAVE LIST-PROCESSING.
  ENDIF.
  CLEAR: ls_out_file.
  LOOP AT  gt_out_file INTO ls_out_file.
    CONCATENATE   ls_out_file-vkbur         "Sales Off.
                  ls_out_file-prodh         "product  hierarchy
*                  ls_out_file-prctr         "Profit Center
                  ls_out_file-vkgrp         "Sales Grp
                  ls_out_file-matkl         "Matl Group
                  ls_out_file-vbeln         "Sales Doc.
                  ls_out_file-equnr         "Equipment
                  ls_out_file-vbegdat       "Start dt
                  ls_out_file-venddat       "End date
                  ls_out_file-sernr         "SerialNumber
                  ls_out_file-posnr         "Posnr
          INTO   lv_line
          SEPARATED BY cl_abap_char_utilities=>horizontal_tab.
    TRANSFER lv_line TO lv_file.
  ENDLOOP.
  CLOSE DATASET lv_file.
  IF sy-subrc <> 0.
    MESSAGE s001(00) DISPLAY LIKE 'E'
      WITH 'Close dataset failed for :'(m05)  lv_file.
    LEAVE LIST-PROCESSING.
  ELSE.
    DESCRIBE TABLE gt_out_file LINES lv_lines.
    MESSAGE i001(00)
      WITH 'Totatly '  lv_lines 'records are transferred!'.
  ENDIF.
ENDFORM.                    " SEND_DATA
*&---------------------------------------------------------------------*
*&      Form  DOWNLOAD_FILE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM download_file .
  DATA: lv_filename TYPE string,
          ls_out_file TYPE yse_outfile_iw75,
          lt_line     TYPE STANDARD TABLE OF string,
          lv_line     TYPE string.
  CLEAR: ls_out_file.
  LOOP AT  gt_out_file INTO ls_out_file.
    CONCATENATE   ls_out_file-vkbur     "Sales Off.
                  ls_out_file-prodh     "product  hierarchy
                  ls_out_file-prctr     "Profit Center
                  ls_out_file-vkgrp     "Sales Grp
                  ls_out_file-matkl     "Matl Group
                  ls_out_file-vbeln     "Sales Doc.
                  ls_out_file-equnr     "Equipment
                  ls_out_file-vbegdat   "Start dt
                  ls_out_file-venddat   "End date
                  ls_out_file-sernr     "SerialNumber
                  ls_out_file-posnr     " so number
          INTO   lv_line
          SEPARATED BY cl_abap_char_utilities=>horizontal_tab.
    APPEND lv_line TO lt_line.
  ENDLOOP.
  CONCATENATE p_server
              '\IW75.CSV'
    INTO lv_filename.

  CALL METHOD cl_gui_frontend_services=>gui_download
    EXPORTING
*        bin_filesize              =
       filename                  = lv_filename
*        filetype                  = 'ASC'
*        append                    = SPACE
*        write_field_separator     = SPACE
*        header                    = '00'
*        trunc_trailing_blanks     = SPACE
*        write_lf                  = 'X'
*        col_select                = SPACE
*        col_select_mask           = SPACE
*        dat_mode                  = SPACE
*        confirm_overwrite         = SPACE
*        no_auth_check             = SPACE
*        codepage                  = SPACE
*        ignore_cerr               = ABAP_TRUE
*        replacement               = '#'
*        write_bom                 = SPACE
*        trunc_trailing_blanks_eol = 'X'
*        wk1_n_format              = SPACE
*        wk1_n_size                = SPACE
*        wk1_t_format              = SPACE
*        wk1_t_size                = SPACE
*        show_transfer_status      = 'X'
*        fieldnames                =
*        virus_scan_profile        = '/SCET/GUI_DOWNLOAD'
*        write_lf_after_last_line  = 'X'
*      IMPORTING
*        filelength                =
    CHANGING
      data_tab                  =   lt_line
    EXCEPTIONS
      file_write_error          = 1
      no_batch                  = 2
      gui_refuse_filetransfer   = 3
      invalid_type              = 4
      no_authority              = 5
      unknown_error             = 6
      header_not_allowed        = 7
      separator_not_allowed     = 8
      filesize_not_allowed      = 9
      header_too_long           = 10
      dp_error_create           = 11
      dp_error_send             = 12
      dp_error_write            = 13
      unknown_dp_error          = 14
      access_denied             = 15
      dp_out_of_memory          = 16
      disk_full                 = 17
      dp_timeout                = 18
      file_not_found            = 19
      dataprovider_exception    = 20
      control_flush_error       = 21
      not_supported_by_gui      = 22
      error_no_gui              = 23
      OTHERS                    = 24
          .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
ENDFORM.                    " DOWNLOAD_FILE

*Text symbol text£º
*M03:Open dataset failed for :
*M04:IW75 data is transferred to
*M05:Close dataset failed for :
*M08:No data generated, please check
*M09:Data is download to the PC with path:

*T01:Selection screen
*Selection text£º
*P_SERVER:        Path
*S_BEGDAT:D       .
*S_ENDDAT:D       .
*S_VKORG:D       .
