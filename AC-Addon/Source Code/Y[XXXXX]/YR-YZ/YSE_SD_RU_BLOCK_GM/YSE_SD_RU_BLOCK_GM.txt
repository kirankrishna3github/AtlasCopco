*----------------------------------------------------------------------*
* PROGRAM ID           : YSE_SD_RU_BLOCK_GM                            *
* PROGRAM TITLE        : Block / Unblock goods movements (after GR)    *
* AUTHOR               : Jules Smets                                   *
* DATE                 : 31/08/2009                                    *
* DEVELOPMENT ID       : CR0977                                        *
* CHANGE REQUEST NUMBER: CD1K955734                                    *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG
*----------------------------------------------------------------------*
* MOD. NR.|  DATE      | NAME             | CORR. NR.  | CHANGE REF. # *
*----------------------------------------------------------------------*
* MOD-001 | 25/01/2011 | Jules Smets      | CD1K9xxxxx | CR1840        *
*         | Modify selection criteria                                  *
************************************************************************
REPORT yse_sd_ru_block_gm
*       NO STANDARD PAGE HEADING
       LINE-SIZE 120.

TABLES: likp.

*---------------------------------------------------------------------*
* INTERNAL TABLE DECLARATIONS                                         *
*---------------------------------------------------------------------*
DATA: BEGIN OF gt_likp OCCURS 0,
        vbeln TYPE vbeln_vl,
      END OF gt_likp.

DATA: BEGIN OF gt_output OCCURS 0,
        vbeln   TYPE vbeln_vl,
        posnr   TYPE posnr_vl,
        vgbel   TYPE vgbel,
        block   TYPE xfeld,                                 "MOD-001
        type    LIKE bapiret2-type,
        id      LIKE bapiret2-id,
        number  LIKE bapiret2-number,
        message LIKE bapiret2-message,
      END OF gt_output.

DATA: gt_return    TYPE STANDARD TABLE OF bapiret2,
      wa_return    LIKE LINE OF gt_return,
      gt_lips      LIKE lips OCCURS 0 WITH HEADER LINE,
      wa_lips      LIKE LINE OF gt_lips,
      gt_prot      LIKE prott OCCURS 0 WITH HEADER LINE,
      gt_del_items LIKE lips OCCURS 100 WITH HEADER LINE,
      gs_batchctrl LIKE bapibatchctrl,
      gv_batch     LIKE bapibatchkey-batch,
      gv_retcd     LIKE sy-subrc,
      gv_mode(1)   TYPE c VALUE 'N',
      gt_valnum    LIKE TABLE OF bapi1003_alloc_values_num,
      wa_valnum    LIKE LINE OF gt_valnum,
      gt_valchar   LIKE TABLE OF bapi1003_alloc_values_char,
      wa_valchar   LIKE LINE OF gt_valchar,
      gt_valcurr   LIKE TABLE OF bapi1003_alloc_values_curr,
      wa_valcurr   LIKE LINE OF gt_valcurr.

*---------------------------------------------------------------------*
* VARIABLE DECLARATIONS                                               *
*---------------------------------------------------------------------*
DATA: gv_cnt(2)    TYPE n,
      gv_bukrs     TYPE bukrs,
      gv_wbstk     TYPE wbstk,
      gv_vbeln     TYPE vbeln,
      gv_mtart     TYPE mtart,
      gv_block     TYPE xfeld,                              "MOD-001
      gv_text(50)  TYPE c,
      gv_objkey    TYPE bapi1003_key-object.

DATA: gt_err    LIKE bdcmsgcoll OCCURS 0 WITH HEADER LINE,
      wa_err    LIKE LINE OF gt_err.

*---------------------------------------------------------------------*
* CONSTANT DECLARATIONS                                               *
*---------------------------------------------------------------------*
CONSTANTS: c_class         TYPE klasse_d VALUE 'ZRU_BATCH',
           c_block_gm      TYPE atnam    VALUE 'ZRU_BLOCK_GM',
           c_022(3)        TYPE c        VALUE '022',
           c_mcha          TYPE tabelle  VALUE 'MCHA'.

*---------------------------------------------------------------------*
* Selection screen
*---------------------------------------------------------------------*
SELECTION-SCREEN : BEGIN OF BLOCK sel  WITH FRAME  TITLE text-t01.
*** MOD-001 * begin ***
*PARAMETERS: p_vbeln  TYPE vbeln_vl.
SELECT-OPTIONS: s_vbeln  FOR likp-vbeln.  "NO INTERVALS.
*** MOD-001 * end ***
PARAMETERS: p_bolnr  TYPE bolnr.
*** MOD-001 * begin ***
SELECT-OPTIONS: s_lifex  FOR likp-lifex.  "NO INTERVALS.
*** MOD-001 * end ***
SELECTION-SCREEN BEGIN OF LINE.
**** MOD-001 * begin ***
*SELECTION-SCREEN COMMENT (31)  text-sc2 FOR FIELD p_lifex.
*PARAMETERS: p_lifex TYPE lifex.
*SELECTION-SCREEN COMMENT 75(10) text-sc1 FOR FIELD p_lifnr.
SELECTION-SCREEN COMMENT 22(10) text-sc1 FOR FIELD p_lifnr.
*** MOD-001 * end ***
PARAMETERS: p_lifnr TYPE lifnr.
SELECTION-SCREEN END OF LINE.
PARAMETERS: p_traid  TYPE traid.
SELECTION-SCREEN END OF BLOCK sel.

SELECTION-SCREEN SKIP 1.

SELECTION-SCREEN : BEGIN OF BLOCK act  WITH FRAME  TITLE text-t02.
PARAMETERS: p_unblck  RADIOBUTTON GROUP blk  DEFAULT 'X',
            p_block   RADIOBUTTON GROUP blk.
SELECTION-SCREEN END OF BLOCK act.

*-----------------------------------------------------------------*
AT SELECTION-SCREEN ON BLOCK sel.

*** MOD-001 * begin ***
*  IF ( p_vbeln IS INITIAL AND p_bolnr IS INITIAL AND
*       p_lifex IS INITIAL AND p_traid IS INITIAL ).
  IF ( s_vbeln[] IS INITIAL AND p_bolnr IS INITIAL AND
       s_lifex[] IS INITIAL AND p_traid IS INITIAL ).
*** MOD-001 * end ***
    MESSAGE e001(00) WITH text-e24.
  ELSE.
    CLEAR gv_cnt.
*** MOD-001 * begin ***
*    IF NOT p_vbeln IS INITIAL.
    IF NOT s_vbeln[] IS INITIAL.
*** MOD-001 * end ***
      ADD 1 TO gv_cnt.
    ENDIF.
    IF NOT p_bolnr IS INITIAL.
      ADD 1 TO gv_cnt.
    ENDIF.
*** MOD-001 * begin ***
*    IF NOT p_lifex IS INITIAL.
    IF NOT s_lifex[] IS INITIAL.
*** MOD-001 * end ***
      ADD 1 TO gv_cnt.
    ENDIF.
    IF NOT p_traid IS INITIAL.
      ADD 1 TO gv_cnt.
    ENDIF.

    IF gv_cnt > 1.
      MESSAGE e001(00) WITH text-e25.
    ENDIF.
  ENDIF.

*** MOD-001 * begin ***
*  IF NOT p_lifex IS INITIAL.
  IF NOT s_lifex[] IS INITIAL.
*** MOD-001 * end ***
    IF p_lifnr IS INITIAL.
      MESSAGE e001(00) WITH text-e23.
    ENDIF.
  ENDIF.


*--------- S T A R T   O F   M A I N   P R O C E S S I N G -----------*

*---------------------------------------------------------------------*
* START-OF-SELECTION                                                  *
*---------------------------------------------------------------------*
START-OF-SELECTION.

* Statistics for transaction / program
  CALL METHOD ycl_statistics=>record_transaction.

* Select inbound deliveries
  REFRESH: gt_likp,
           gt_output.

*** MOD-001 * begin ***
*  IF NOT p_vbeln IS INITIAL.
  IF NOT s_vbeln[] IS INITIAL.
*** MOD-001 * end ***
    SELECT vbeln INTO  TABLE gt_likp
           FROM likp
*** MOD-001 * begin ***
*           WHERE vbeln = p_vbeln.
           WHERE vbeln IN s_vbeln.
*** MOD-001 * end ***
  ELSEIF NOT p_bolnr IS INITIAL.
    SELECT vbeln INTO TABLE gt_likp
           FROM likp
           WHERE bolnr = p_bolnr.
  ELSEIF NOT p_traid IS INITIAL.
    SELECT vbeln INTO TABLE gt_likp
           FROM likp
           WHERE traid = p_traid.
*** MOD-001 * begin ***
*  ELSEIF NOT p_lifex IS INITIAL.
  ELSEIF NOT s_lifex[] IS INITIAL.
*** MOD-001 * end ***
    SELECT vbeln INTO TABLE gt_likp
           FROM likp
           WHERE lifnr = p_lifnr
*** MOD-001 * begin ***
*             AND lifex = p_lifex.
             AND lifex IN s_lifex.
*** MOD-001 * end ***
  ENDIF.

  CHECK NOT gt_likp[] IS INITIAL.

  SELECT * INTO TABLE gt_lips
    FROM lips
    FOR ALL ENTRIES IN gt_likp
    WHERE vbeln = gt_likp-vbeln.

  SORT gt_lips BY vbeln posnr.

* Check if all related PO's are not locked
  LOOP AT gt_lips.
    CALL FUNCTION 'ENQUEUE_EMEKKOE'
      EXPORTING
        mode_ekko      = 'E'
        mode_ekpo      = 'E'
        mandt          = sy-mandt
        ebeln          = gt_lips-vgbel
*        ebelp          = gt_lips-vgpos
      EXCEPTIONS
        foreign_lock   = 1
        system_failure = 2
        OTHERS         = 3.

    IF sy-subrc <> 0.
      CLEAR gv_text.
      CONCATENATE text-e01 gt_lips-vgbel text-e02 INTO gv_text
                  SEPARATED BY space.
      MESSAGE s001(00) WITH gv_text.
      STOP.
    ELSE.
      CALL FUNCTION 'DEQUEUE_EMEKKOE'
        EXPORTING
          mode_ekko = 'E'
          mode_ekpo = 'E'
          mandt     = sy-mandt
          ebeln     = gt_lips-vgbel.
    ENDIF.
  ENDLOOP.

* Do the necessary changes
  LOOP AT gt_lips.
    SELECT SINGLE bukrs INTO gv_bukrs
           FROM t001k
           WHERE bwkey = gt_lips-werks.

    IF sy-subrc = 0.
*     Check Company
      SELECT SINGLE bukrs INTO gv_bukrs
             FROM yse_cc_act_cost
             WHERE bukrs = gv_bukrs.

      IF sy-subrc = 0.
*       Check material type
        CLEAR gv_mtart.
        SELECT SINGLE mtart INTO gv_mtart
               FROM mara
               WHERE matnr = gt_lips-matnr.
        CHECK gv_mtart = 'ZMAT'.
*       If batch does not exist, create it  and
*                         change the inbound delivery
        IF gt_lips-charg IS INITIAL.
          CLEAR gv_retcd.
          PERFORM sub_create_batch CHANGING gv_retcd.

          IF gv_retcd = 0.
            MOVE gv_batch TO gt_lips-charg.
            MODIFY gt_lips TRANSPORTING charg.
          ELSE.
            CONTINUE.
          ENDIF.
        ENDIF.

*       Update characteristic ZRU_BLOCK_GM
        PERFORM sub_update_block.

        LOOP AT gt_return INTO wa_return
                          WHERE type = 'E' OR type = 'A'.
        ENDLOOP.

        IF sy-subrc = 0.
          MOVE gt_lips-vbeln     TO gt_output-vbeln.
          MOVE gt_lips-posnr     TO gt_output-posnr.
          MOVE gt_lips-vgbel     TO gt_output-vgbel.
          MOVE gv_block          TO gt_output-block.
          MOVE wa_return-type    TO gt_output-type.
          MOVE wa_return-id      TO gt_output-id.
          MOVE wa_return-number  TO gt_output-number.
          MOVE wa_return-message TO gt_output-message.
          APPEND gt_output.
          CLEAR gt_output.
        ELSE.
          COMMIT WORK AND WAIT.
          MOVE gt_lips-vbeln TO gt_output-vbeln.
          MOVE gt_lips-posnr TO gt_output-posnr.
          MOVE gt_lips-vgbel TO gt_output-vgbel.
          MOVE gv_block      TO gt_output-block.
          APPEND gt_output.
          CLEAR gt_output.
        ENDIF.

      ENDIF.
    ENDIF.
  ENDLOOP.


*----------------------------------------------------------------------*
* END-OF-SELECTION                                                     *
*----------------------------------------------------------------------*
END-OF-SELECTION.

* Give overview of updated inbound deliveries
  LOOP AT gt_output.
    WRITE: /   gt_output-vbeln,
            13 gt_output-posnr NO-ZERO,
            24 gt_output-vgbel.
*** MOD-001 * begin ***
    IF gt_output-block IS INITIAL.
      WRITE: 40 'Not blocked'(i01).
    ELSE.
      WRITE: 40 'Blocked'(i02).
    ENDIF.
*** MOD-001 * end ***

    IF NOT gt_output-type IS INITIAL.
*** MOD-001 * begin ***
*      WRITE:  40 'E R R O R  ! ! ! '.
      WRITE: 70 'E R R O R  ! ! ! '.
*** MOD-001 * end ***
      FORMAT COLOR COL_NEGATIVE.
      WRITE: /05 gt_output-type,
              09 gt_output-id(5),
              15 gt_output-number,
              20 gt_output-message(72).
      FORMAT RESET.
    ENDIF.
  ENDLOOP.


*-S U B R O U T I N E S------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  SUB_UPDATE_BLOCK
*&---------------------------------------------------------------------*
*       Update characteristic ZRU_BLOCK_GM
*----------------------------------------------------------------------*
FORM sub_update_block.

  MOVE gt_lips-matnr TO gv_objkey+00(18).
  MOVE gt_lips-werks TO gv_objkey+18(04).
  MOVE gt_lips-charg TO gv_objkey+22(10).

* Get characteristics
  CALL FUNCTION 'BAPI_OBJCL_GETDETAIL'
    EXPORTING
      objectkey              = gv_objkey
      objecttable            = c_mcha
      classnum               = c_class
      classtype              = c_022
*      KEYDATE                = SY-DATUM
*      UNVALUATED_CHARS       = ' '
*      LANGUAGE               = SY-LANGU
*    IMPORTING
*      STATUS                 =
*      STANDARDCLASS          =
    TABLES
      allocvaluesnum         = gt_valnum
      allocvalueschar        = gt_valchar
      allocvaluescurr        = gt_valcurr
      return                 = gt_return.
  SORT gt_valchar.

* Update characteristic ZRU_BLOCK_GM
  CLEAR gv_block.                                           "MOD-001
  CLEAR wa_valchar.
  READ TABLE gt_valchar INTO wa_valchar
             WITH KEY charact = c_block_gm.
  IF sy-subrc = 0.
    MOVE wa_valchar-value_char TO gv_block.                 "MOD-001
    MOVE p_block      TO: wa_valchar-value_char,
                          wa_valchar-value_neutral.
    MODIFY gt_valchar INDEX sy-tabix
                      FROM wa_valchar.
  ELSE.
    MOVE c_block_gm   TO wa_valchar-charact.
    MOVE p_block      TO wa_valchar-value_char.
    APPEND wa_valchar TO gt_valchar.
  ENDIF.

* Update characteristics
  CALL FUNCTION 'BAPI_OBJCL_CHANGE'
    EXPORTING
      objectkey          = gv_objkey
      objecttable        = c_mcha
      classnum           = c_class
      classtype          = c_022
*      STATUS             = '1'
*      STANDARDCLASS      =
*      CHANGENUMBER       =
*      KEYDATE            = SY-DATUM
*      NO_DEFAULT_VALUES  = ' '
*    IMPORTING
*      CLASSIF_STATUS     =
    TABLES
      allocvaluesnumnew  = gt_valnum
      allocvaluescharnew = gt_valchar
      allocvaluescurrnew = gt_valcurr
      return             = gt_return.

ENDFORM.                    " SUB_UPDATE_BLOCK

*&---------------------------------------------------------------------*
*&      Form  SUB_CREATE_BATCH
*&---------------------------------------------------------------------*
*       Create batch and change inbound delivery
*----------------------------------------------------------------------*
*  <--- r_subrc   Returncode
*----------------------------------------------------------------------*
FORM sub_create_batch CHANGING r_subrc.

  DATA: transaction_id  LIKE arfctid,
        commit_work(15) VALUE 'COMMIT_WORK',
        status_of_transaction(15).


  REFRESH gt_return.

  CALL FUNCTION 'TRANSACTION_BEGIN'
    IMPORTING
      transaction_id = transaction_id
    EXCEPTIONS
      OTHERS         = 1.

  gs_batchctrl-batchlevel = '0'.
  gs_batchctrl-class_num  = c_class.
  gs_batchctrl-doclassify = 'X'.

  CALL FUNCTION 'BAPI_BATCH_CREATE'
    EXPORTING
      material             = gt_lips-matnr
*     BATCH                =
      plant                = gt_lips-werks
*      BATCHATTRIBUTES      =
      batchcontrolfields   = gs_batchctrl
*      BATCHSTORAGELOCATION =
*      INTERNALNUMBERCOM    =
*      EXTENSION1           =
*      MATERIAL_EVG         =
    IMPORTING
      batch                = gv_batch
*      BATCHATTRIBUTES      =
    TABLES
      return               = gt_return.

  CALL FUNCTION 'TRANSACTION_STATUS'
    IMPORTING
      status = status_of_transaction
    EXCEPTIONS
      OTHERS = 1.

  IF status_of_transaction = commit_work.
    COMMIT WORK AND WAIT.
  ENDIF.

  CALL FUNCTION 'TRANSACTION_END'
    EXPORTING
      transaction_id = transaction_id
    EXCEPTIONS
      OTHERS         = 1.

  LOOP AT gt_return INTO wa_return
                    WHERE type = 'E'
                       OR type = 'A'.
  ENDLOOP.

  IF sy-subrc = 0.
    r_subrc = 4.
    MOVE gt_lips-vbeln     TO gt_output-vbeln.
    MOVE gt_lips-posnr     TO gt_output-posnr.
    MOVE gt_lips-vgbel     TO gt_output-vgbel.
    MOVE wa_return-type    TO gt_output-type.
    MOVE wa_return-id      TO gt_output-id.
    MOVE wa_return-number  TO gt_output-number.
    MOVE wa_return-message TO gt_output-message.
    APPEND gt_output.
    CLEAR gt_output.
  ELSE.
    r_subrc = 0.
    REFRESH: gt_prot,
             gt_del_items.
    MOVE-CORRESPONDING gt_lips TO gt_del_items.
    MOVE gv_batch              TO gt_del_items-charg.
    APPEND gt_del_items.
    CLEAR gt_del_items.

    CALL FUNCTION 'LE_MOB_DELIVERY_UPDATE'
      EXPORTING
        do_commit                = ' '
      TABLES
        t_delivery_items         = gt_del_items
        prot                     = gt_prot
      EXCEPTIONS
        conversion_overflow      = 1
        essential_data_missing   = 2
        error                    = 3
        nothing_to_update        = 4
        lock_after_update_failed = 5
        error_in_delivery_update = 6
        OTHERS                   = 7.

    IF sy-subrc <> 0.
      r_subrc = sy-subrc.
      READ TABLE gt_prot INDEX 1.
      MOVE gt_lips-vbeln  TO gt_output-vbeln.
      MOVE gt_lips-posnr  TO gt_output-posnr.
      MOVE gt_lips-vgbel  TO gt_output-vgbel.
      MOVE gt_prot-msgty  TO gt_output-type.
      MOVE gt_prot-msgid  TO gt_output-id.
      MOVE gt_prot-msgno  TO gt_output-number.
      MOVE gt_prot-msgv1  TO gt_output-message.
      APPEND gt_output.
      CLEAR gt_output.
    ELSE.
      COMMIT WORK AND WAIT.
    ENDIF.
  ENDIF.

ENDFORM.                    " SUB_CREATE_BATCH

*&---------------------------------------------------------------------*
*&      Form  SUB_WRITE_MESSAGE
*&---------------------------------------------------------------------*
*       Write error messages from B-I sessions
*----------------------------------------------------------------------*
FORM sub_write_message .

  DATA:  lv_mestx   LIKE t100-text,
         lv_msgnr   LIKE t100-msgnr.

  LOOP AT gt_err.
    lv_msgnr = gt_err-msgnr.
    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*        SPRSL             = SY-LANGU
        arbgb             = sy-msgid
        msgnr             = lv_msgnr
        msgv1             = sy-msgv1
        msgv2             = sy-msgv2
        msgv3             = sy-msgv3
        msgv4             = sy-msgv4
      IMPORTING
        msgtext           = lv_mestx
      EXCEPTIONS
        message_not_found = 1
        OTHERS            = 2.

    IF sy-subrc = 0.
      MOVE lv_mestx TO gt_output-message.
      APPEND gt_output.
      CLEAR gt_output.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " SUB_WRITE_MESSAGE

*List header: Column headings£º

*001:Inb.Delivery  Item     PO number       Previous state
*Text symbol text£º
*E01:PO
*E02:is locked
*E03:Inb.delivery
*E04:is already goods receipted
*E10:No characteristics exist for
*E11:Characteristic not in class
*E12:Value is not valid
*E16:Fill in Old Service Product number
*E17:Fill in Item cat. group, MRP Type and Mat. Group
*E23:Please fill also vendor number
*E24:At least one parameter has to be filled in !
*E25:Not more than 1 parameter may be filled in
*E26:Posting is only possible in open periods
*SC1:Vendor
*SC2:Shipment number
*T01:Selection

*T02:Action
*Selection text£º
*P_BLOCK:        Block batches
*P_BOLNR:        Invoice reference number
*P_LIFEX:        Shipment number
*P_LIFNR:D       .
*P_TRAID:        Case number
*P_UNBLCK:        Unblock batches
*P_VBELN:        Inbound delivery number
*S_LIFEX:        Shipment number

*S_VBELN:        Inbound delivery number
*List Title: Titlebar£º
*:Updated inbound deliveries
