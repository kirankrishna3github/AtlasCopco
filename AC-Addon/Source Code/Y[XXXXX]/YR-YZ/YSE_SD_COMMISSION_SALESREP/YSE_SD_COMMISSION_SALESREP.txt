************************************************************************
* Report  : YSE_SD_COMMISSION_SALESREP                                 *
*----------------------------------------------------------------------*
* Author  : Kurt Clement                                               *
* Company : Atlas Copco                                                *
* Created : 27.12.2006                                                 *
* Dev.nbr : D41F - Sales rep reporting for commission                  *
*----------------------------------------------------------------------*
* This program gives an overview of all the commissions for the sales  *
* reps                                                                 *
************************************************************************
REPORT yse_sd_commission_salesrep NO STANDARD PAGE HEADING.

*----------------------------------------------------------------------*
* Data declarations                                                    *
*----------------------------------------------------------------------*


INCLUDE yse_sd_commission_top.
INCLUDE yse_sd_rep_include.

*----------------------------------------------------------------------*
* Selection screen                                                     *
*----------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK b01 WITH FRAME TITLE text-s01.
PARAMETERS: p_fdate LIKE sy-datum OBLIGATORY.
PARAMETERS: p_tdate LIKE sy-datum OBLIGATORY.
PARAMETERS: p_vkorg LIKE vbak-vkorg OBLIGATORY.
SELECT-OPTIONS: s_vtweg FOR vbak-vtweg OBLIGATORY.
SELECT-OPTIONS: s_spart FOR vbak-spart OBLIGATORY.
SELECT-OPTIONS: s_prctr FOR ce41000_acct-prctr.
SELECT-OPTIONS: s_ww002 FOR ce41000_acct-ww002.
SELECT-OPTIONS: s_ww006 FOR ce41000_acct-ww006.
SELECT-OPTIONS: s_ww007 FOR ce41000_acct-ww007.
SELECT-OPTIONS: s_werks FOR vbap-werks.
SELECT-OPTIONS: s_matnr FOR vbap-matnr.
SELECT-OPTIONS: s_kunag FOR vbak-kunnr.
SELECT-OPTIONS: s_pernr FOR vbpa-pernr.
SELECT-OPTIONS: s_vbeln FOR vbak-vbeln.
SELECT-OPTIONS: s_posnr FOR vbap-posnr.
SELECTION-SCREEN END OF BLOCK b01.
SELECTION-SCREEN BEGIN OF BLOCK b02 WITH FRAME TITLE text-s02.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (30) text-s03.
SELECTION-SCREEN POSITION POS_LOW.
PARAMETERS: rb_sel1 RADIOBUTTON GROUP sel DEFAULT 'X'.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (30) text-s04.
SELECTION-SCREEN POSITION POS_LOW.
PARAMETERS: rb_sel2 RADIOBUTTON GROUP sel.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN SKIP.
SELECTION-SCREEN END OF BLOCK b02.
SELECTION-SCREEN BEGIN OF BLOCK b03 WITH FRAME TITLE text-s06.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (25) text-s07.
SELECTION-SCREEN POSITION POS_LOW.
PARAMETERS: p_var   LIKE disvariant-variant.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK b03.


*----------------------------------------------------------------------*
* Initialization of the selection screen                               *
*----------------------------------------------------------------------*
INITIALIZATION.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_var.
  PERFORM variant_inputhelp USING p_var.

AT SELECTION-SCREEN OUTPUT.

  PERFORM variant_init.
  IF p_var IS INITIAL AND g_variant_flag IS INITIAL.
    PERFORM get_default_variant USING p_var.
    g_variant_flag = 'X'.
  ENDIF.

AT SELECTION-SCREEN.

  PERFORM check_authorization.

  PERFORM existence_variant USING p_var.



************************************************************************
* START MAIN PROGRAM                                                   *
************************************************************************
START-OF-SELECTION.

  PERFORM initialize_data.

  PERFORM select_data.

  PERFORM process_data.

  PERFORM display_data.



************************************************************************
* SUBROUTINES  LEVEL 01                                                *
************************************************************************









*----------------------------------------------------------------------*
*   Form  INITIALIZE_DATA                                              *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM initialize_data.

  CLEAR: it_acct, it_ce41000, it_adrct.
  CLEAR: it_vbak, it_vbap, it_vbpa1, it_vbpa2, it_vbfa1, it_vbfa2.
  CLEAR: it_likp, it_lips, it_vbrk, it_vbrp.
  CLEAR: it_data.
  REFRESH: it_acct, it_ce41000, it_adrct.
  REFRESH: it_vbak, it_vbap, it_vbpa1, it_vbpa2, it_vbfa1, it_vbfa2.
  REFRESH: it_likp, it_lips, it_vbrk, it_vbrp.
  REFRESH: it_data.

  CLEAR: r_dtype1, r_dtype2.
  REFRESH: r_dtype1, r_dtype2.
  SELECT * FROM yse_sr_doctype WHERE vkorg = p_vkorg
                                 AND vtweg IN s_vtweg
                                 AND spart IN s_spart
                                 AND ( vbtyp EQ c_vbtyp_c
                                    OR vbtyp EQ c_vbtyp_m
                                    OR vbtyp EQ c_vbtyp_n
                                    OR vbtyp EQ c_vbtyp_o ).
    IF yse_sr_doctype-vbtyp EQ c_vbtyp_c.
      CLEAR r_dtype1.
      r_dtype1-sign   = 'I'.
      r_dtype1-option = 'EQ'.
      r_dtype1-low    = yse_sr_doctype-auart.
      APPEND r_dtype1.
    ELSE.
      CLEAR r_dtype2.
      r_dtype2-sign   = 'I'.
      r_dtype2-option = 'EQ'.
      r_dtype2-low    = yse_sr_doctype-auart.
      APPEND r_dtype2.
    ENDIF.
  ENDSELECT.

  CLEAR: r_parvw.
  REFRESH: r_parvw.
  r_parvw-sign   = 'I'.
  r_parvw-option = 'EQ'.
  r_parvw-low    = c_parvw_ve.  APPEND r_parvw.
  r_parvw-low    = c_parvw_zx.  APPEND r_parvw.
  r_parvw-low    = c_parvw_zy.  APPEND r_parvw.

  CLEAR: r_date1, r_date2.
  REFRESH: r_date1, r_date2.
  IF NOT rb_sel1 IS INITIAL.
    r_date1-sign   = 'I'.
    r_date1-option = 'BT'.
    r_date1-low    = p_fdate.
    r_date1-high   = p_tdate.
    APPEND r_date1.
  ELSE.
    r_date2-sign   = 'I'.
    r_date2-option = 'BT'.
    r_date2-low    = p_fdate.
    r_date2-high   = p_tdate.
    APPEND r_date2.
  ENDIF.

ENDFORM.                    " INITIALIZE_DATA


*----------------------------------------------------------------------*
*   Form  SELECT_DATA                                                  *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM select_data.

  SELECT * FROM vbak INTO TABLE it_vbak
                     WHERE vbeln IN s_vbeln
                       AND erdat IN r_date1
                       AND vbtyp EQ c_vbtyp_c
                       AND auart IN r_dtype1
                       AND vkorg = p_vkorg
                       AND vtweg IN s_vtweg
                       AND spart IN s_spart
                       AND kunnr IN s_kunag.
  IF NOT it_vbak[] IS INITIAL.
    SELECT * FROM vbap INTO TABLE it_vbap
                       FOR ALL ENTRIES IN it_vbak
                       WHERE vbeln EQ it_vbak-vbeln
                         AND posnr IN s_posnr
                         AND matnr IN s_matnr
                         AND werks IN s_werks.
    IF NOT it_vbap[] IS INITIAL.
      SELECT aktbo paobjnr pasubnr ce4key
             FROM ce41000_acct
             INTO TABLE it_acct
             FOR ALL ENTRIES IN it_vbap
             WHERE aktbo   EQ 'X'
               AND paobjnr EQ it_vbap-paobjnr.
      DELETE ADJACENT DUPLICATES FROM it_acct.
      IF NOT it_acct[] IS INITIAL.
        SELECT aktbo paobjnr pasubnr prctr ww002 ww006 ww007
               FROM ce41000 INTO TABLE it_ce41000
               FOR ALL ENTRIES IN it_acct
               WHERE aktbo EQ 'X'
                 AND paobjnr EQ it_acct-ce4key
                 AND prctr   IN s_prctr
                 AND ww002   IN s_ww002
                 AND ww006   IN s_ww006
                 AND ww007   IN s_ww007.                "#EC CI_NOFIRST
        DELETE ADJACENT DUPLICATES FROM it_ce41000.
      ENDIF.
    ENDIF.
    SELECT * FROM vbpa INTO TABLE it_vbpa1
                       FOR ALL ENTRIES IN it_vbak
                       WHERE vbeln EQ it_vbak-vbeln AND
                           ( parvw EQ c_parvw_ag    OR
                             parvw EQ c_parvw_re ).
    DELETE ADJACENT DUPLICATES FROM it_vbpa1.
    IF NOT it_vbpa1[] IS INITIAL.
      SELECT kunnr name1 FROM kna1 INTO TABLE it_kna1
                         FOR ALL ENTRIES IN it_vbpa1
                         WHERE kunnr EQ it_vbpa1-kunnr.
    ENDIF.
    SELECT * FROM vbpa INTO TABLE it_vbpa2
                       FOR ALL ENTRIES IN it_vbak
                       WHERE vbeln EQ it_vbak-vbeln
                         AND parvw IN r_parvw
                         AND pernr IN s_pernr.
    DELETE ADJACENT DUPLICATES FROM it_vbpa2.
    IF NOT it_vbpa2[] IS INITIAL.
      SELECT * FROM adrct INTO TABLE it_adrct
                          FOR ALL ENTRIES IN it_vbpa2
                          WHERE addrnumber EQ it_vbpa2-adrnr.
      DELETE ADJACENT DUPLICATES FROM it_adrct.
*     Load all relevant personel names
      SELECT pernr ename FROM pa0001 INTO TABLE it_pa0001
                         FOR ALL ENTRIES IN it_vbpa2
                         WHERE pernr EQ it_vbpa2-pernr.
      DELETE ADJACENT DUPLICATES FROM it_pa0001.
    ENDIF.
    SELECT * FROM vbfa INTO TABLE it_vbfa1
                       FOR ALL ENTRIES IN it_vbak
                       WHERE vbelv   EQ it_vbak-vbeln
                         AND vbtyp_n EQ c_vbtyp_j
                         AND vbtyp_v EQ it_vbak-vbtyp.
    IF NOT it_vbfa1[] IS INITIAL.
      SELECT * FROM likp INTO TABLE it_likp
                         FOR ALL ENTRIES IN it_vbfa1
                         WHERE vbeln EQ it_vbfa1-vbeln.
      DELETE ADJACENT DUPLICATES FROM it_likp.
      IF NOT it_likp[] IS INITIAL.
        SELECT * FROM lips INTO TABLE it_lips
                           FOR ALL ENTRIES IN it_likp
                           WHERE vbeln EQ it_likp-vbeln.
        SELECT * FROM vbfa INTO TABLE it_vbfa2
                           FOR ALL ENTRIES IN it_likp
                           WHERE ( vbelv   EQ it_likp-vbeln )
                             AND ( vbtyp_n EQ c_vbtyp_m
                                OR vbtyp_n EQ c_vbtyp_n
                                OR vbtyp_n EQ c_vbtyp_o )
                             AND ( vbtyp_v EQ it_likp-vbtyp ).
        IF NOT it_vbfa2[] IS INITIAL.
          SELECT * FROM vbrk INTO TABLE it_vbrk
                             FOR ALL ENTRIES IN it_vbfa2
                             WHERE vbeln EQ it_vbfa2-vbeln
                               AND fkart IN r_dtype2
                               AND erdat IN r_date2.
          DELETE ADJACENT DUPLICATES FROM it_vbrk.
          IF NOT it_vbrk[] IS INITIAL.
            SELECT * FROM vbrp INTO TABLE it_vbrp
                               FOR ALL ENTRIES IN it_vbrk
                               WHERE vbeln EQ it_vbrk-vbeln.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDIF.

ENDFORM.                    " SELECT_DATA


*----------------------------------------------------------------------*
*   Form  PROCESS_DATA                                                 *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM process_data.

  CLEAR str_data.
  LOOP AT it_vbak.

    LOOP AT it_vbap WHERE vbeln EQ it_vbak-vbeln.

      READ TABLE it_acct WITH KEY paobjnr = it_vbap-paobjnr.
      IF sy-subrc NE 0.
        CLEAR it_acct.
      ELSE.
        READ TABLE it_ce41000 WITH KEY paobjnr = it_acct-ce4key.
        IF sy-subrc NE 0.
          CLEAR it_ce41000.
        ENDIF.
      ENDIF.

      READ TABLE it_vbpa1 WITH KEY vbeln = it_vbap-vbeln
                                   posnr = c_posnr_init
                                   parvw = c_parvw_ag.
      IF sy-subrc NE 0.
        CLEAR it_vbpa1.
      ENDIF.

      LOOP AT it_vbfa1 WHERE vbelv   EQ it_vbap-vbeln
                         AND posnv   EQ it_vbap-posnr
                         AND vbtyp_n EQ c_vbtyp_j
                         AND vbtyp_v EQ it_vbak-vbtyp.

        READ TABLE it_likp WITH KEY vbeln = it_vbfa1-vbeln.
        IF sy-subrc NE 0.
          CLEAR it_likp.
        ENDIF.

        LOOP AT it_lips WHERE vbeln EQ it_likp-vbeln
                          AND posnr EQ it_vbfa1-posnn.

          LOOP AT it_vbfa2 WHERE ( vbelv   EQ it_lips-vbeln )
                             AND ( posnv   EQ it_lips-posnr )
                             AND ( vbtyp_n EQ c_vbtyp_m
                                OR vbtyp_n EQ c_vbtyp_n
                                OR vbtyp_n EQ c_vbtyp_o )
                             AND ( vbtyp_v EQ it_likp-vbtyp ).

            READ TABLE it_vbrk WITH KEY vbeln = it_vbfa2-vbeln.
            IF sy-subrc NE 0.
              CLEAR it_vbrk.
            ENDIF.
            LOOP AT it_vbrp WHERE vbeln EQ it_vbrk-vbeln
                              AND posnr EQ it_vbfa2-posnn.

              PERFORM get_order_data.
              PERFORM get_atlascopco_data.
              PERFORM get_soldto_data.
              PERFORM get_billto_data.
              PERFORM get_delivery_data.
              PERFORM get_invoice_data.
              PERFORM get_sales_rep_data USING c_parvw_ve
                                          CHANGING x_found_ve.
              IF NOT x_found_ve IS INITIAL.
                PERFORM save_all_data.
              ENDIF.
              PERFORM get_sales_rep_data USING c_parvw_zx
                                          CHANGING x_found_zx.
              IF NOT x_found_zx IS INITIAL.
                PERFORM save_all_data.
              ENDIF.
              PERFORM get_sales_rep_data USING c_parvw_zy
                                          CHANGING x_found_zy.
              IF NOT x_found_zy IS INITIAL.
                PERFORM save_all_data.
              ENDIF.
              IF x_found_ve IS INITIAL AND
                 x_found_zx IS INITIAL AND
                 x_found_zy IS INITIAL AND
                 s_pernr IS INITIAL.
                PERFORM save_all_data.
              ENDIF.
              PERFORM reset_data.
            ENDLOOP.
          ENDLOOP.
          IF sy-subrc NE 0 AND NOT rb_sel1 IS INITIAL.
            PERFORM get_order_data.
            PERFORM get_atlascopco_data.
            PERFORM get_soldto_data.
            PERFORM get_billto_data.
            PERFORM get_delivery_data.
            PERFORM get_sales_rep_data USING c_parvw_ve
                                        CHANGING x_found_ve.
            IF NOT x_found_ve IS INITIAL.
              PERFORM save_all_data.
            ENDIF.
            PERFORM get_sales_rep_data USING c_parvw_zx
                                        CHANGING x_found_zx.
            IF NOT x_found_zx IS INITIAL.
              PERFORM save_all_data.
            ENDIF.
            PERFORM get_sales_rep_data USING c_parvw_zy
                                        CHANGING x_found_zy.
            IF NOT x_found_zy IS INITIAL.
              PERFORM save_all_data.
            ENDIF.
            IF x_found_ve IS INITIAL AND
               x_found_zx IS INITIAL AND
               x_found_zy IS INITIAL AND
                  s_pernr IS INITIAL.
              PERFORM save_all_data.
            ENDIF.
            PERFORM reset_data.
          ENDIF.
        ENDLOOP.
      ENDLOOP.
      IF sy-subrc NE 0.
        IF NOT rb_sel1 IS INITIAL.
          PERFORM get_order_data.
          PERFORM get_atlascopco_data.
          PERFORM get_soldto_data.
          PERFORM get_billto_data.
          PERFORM get_sales_rep_data USING c_parvw_ve
                                      CHANGING x_found_ve.
          IF NOT x_found_ve IS INITIAL.
            PERFORM save_all_data.
          ENDIF.
          PERFORM get_sales_rep_data USING c_parvw_zx
                                      CHANGING x_found_zx.
          IF NOT x_found_zx IS INITIAL.
            PERFORM save_all_data.
          ENDIF.
          PERFORM get_sales_rep_data USING c_parvw_zy
                                      CHANGING x_found_zy.
          IF NOT x_found_zy IS INITIAL.
            PERFORM save_all_data.
          ENDIF.
          IF x_found_ve IS INITIAL AND
             x_found_zx IS INITIAL AND
             x_found_zy IS INITIAL AND
             s_pernr IS INITIAL.
            PERFORM save_all_data.
          ENDIF.
          PERFORM reset_data.
        ENDIF.
      ENDIF.
    ENDLOOP.
  ENDLOOP.

ENDFORM.                    " PROCESS_DATA


*----------------------------------------------------------------------*
*   Form  DISPLAY_DATA                                                 *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM display_data.

  PERFORM fill_field_catalog.
  PERFORM prepare_sort.
  PERFORM change_catalog.
  PERFORM alv_output.

ENDFORM.                    " DISPLAY_DATA



************************************************************************
* SUBROUTINES  LEVEL 02                                                *
************************************************************************

*----------------------------------------------------------------------*
*   Form  GET_ORDER_DATA                                               *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM get_order_data.

  str_data-werks   = it_vbap-werks.
  str_data-matnr   = it_vbap-matnr.
  str_data-vbeln_c = it_vbak-vbeln.
  str_data-posnr_c = it_vbap-posnr.
  str_data-netwr   = it_vbap-netwr.
  IF it_vbak-vbtyp EQ c_vbtyp_h OR it_vbak-vbtyp EQ c_vbtyp_k.
    str_data-netwr = str_data-netwr * -1.
  ENDIF.
  str_data-waerk   = it_vbap-waerk.
  str_data-kwmeng  = it_vbap-kwmeng.
  str_data-vrkme_c = it_vbap-vrkme.
  str_data-erdat   = it_vbak-erdat.

ENDFORM.                    " GET_ORDER_DATA


*----------------------------------------------------------------------*
*   Form  GET_ATLASCOPCO_DATA                                          *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM get_atlascopco_data.

* str_data-prctr = it_acct-prctr.
  str_data-prctr = it_ce41000-prctr.
* str_data-ww002 = it_acct-ww002.
  str_data-ww002 = it_ce41000-ww002.
* str_data-ww006 = it_acct-ww006.
  str_data-ww006 = it_ce41000-ww006.
* str_data-ww007 = it_acct-ww007.
  str_data-ww007 = it_ce41000-ww007.

ENDFORM.                    " GET_ATLASCOPCO_DATA


*----------------------------------------------------------------------*
*   Form  GET_SOLDTO_DATA                                              *
*----------------------------------------------------------------------*
*   Copy Sold-to partner number and name to output structure.          *
*----------------------------------------------------------------------*
FORM get_soldto_data.

  READ TABLE it_vbpa1 WITH KEY vbeln = it_vbak-vbeln
                               parvw = c_parvw_ag.
  IF sy-subrc = 0.
    str_data-kunag = it_vbpa1-kunnr.
    READ TABLE it_kna1 WITH KEY kunnr = str_data-kunag.
    IF sy-subrc = 0.
      str_data-kunag_n = it_kna1-name1.
    ENDIF.    " found name
  ENDIF.    " found partner

ENDFORM.                    " GET_SOLDTO_DATA


*----------------------------------------------------------------------*
*   Form  GET_BILLTO_DATA                                              *
*----------------------------------------------------------------------*
*   Copy Bill-to partner number and name to output structure.          *
*----------------------------------------------------------------------*
FORM get_billto_data.

  READ TABLE it_vbpa1 WITH KEY vbeln = it_vbak-vbeln
                               parvw = c_parvw_re.
  IF sy-subrc = 0.
    str_data-kunre = it_vbpa1-kunnr.
    READ TABLE it_kna1 WITH KEY kunnr = str_data-kunre.
    IF sy-subrc = 0.
      str_data-kunre_n = it_kna1-name1.
    ENDIF.    " found name
  ENDIF.    " found partner

ENDFORM.                    " GET_BILLTO_DATA


*----------------------------------------------------------------------*
*   Form  GET_DELIVERY_DATA                                            *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM get_delivery_data.

  str_data-vbeln_j   = it_likp-vbeln.
  str_data-posnr_j   = it_lips-posnr.
  str_data-lfimg     = it_lips-lfimg.
  str_data-vrkme_j   = it_lips-vrkme.

ENDFORM.                    " GET_DELIVERY_DATA


*----------------------------------------------------------------------*
*   Form  GET_INVOICE_DATA                                             *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM get_invoice_data.

  str_data-vbeln_m = it_vbrk-vbeln.
  str_data-posnr_m = it_vbrp-posnr.
  str_data-fkimg   = it_vbrp-fkimg.
  str_data-vrkme_m = it_vbrp-vrkme.
  str_data-kzwi3   = it_vbrp-kzwi3.
  IF it_vbrk-vbtyp EQ c_vbtyp_n OR it_vbrk-vbtyp EQ c_vbtyp_o.
    str_data-kzwi3 = str_data-kzwi3 * -1.
  ENDIF.
  str_data-fkdat   = it_vbrk-fkdat.

ENDFORM.                    " GET_INVOICE_DATA


*----------------------------------------------------------------------*
*   Form  GET_SALES_REP_DATA                                           *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM get_sales_rep_data USING partnerfunction
                         CHANGING found.

  CLEAR found.
  READ TABLE it_vbpa2 WITH KEY vbeln = it_vbap-vbeln
                               posnr = it_vbap-posnr
                               parvw = partnerfunction.
  IF sy-subrc EQ 0.
    found = 'X'.
  ELSE.
    READ TABLE it_vbpa2 WITH KEY vbeln = it_vbap-vbeln
                                 posnr = c_posnr_init
                                 parvw = partnerfunction.
    IF sy-subrc EQ 0.
      found = 'X'.
    ENDIF.
  ENDIF.
  IF found EQ 'X'.
    MOVE it_vbpa2-parvw TO str_data-parvw.
    MOVE it_vbpa2-pernr TO str_data-pernr.
    READ TABLE it_adrct WITH KEY addrnumber = it_vbpa2-adrnr.
    IF sy-subrc EQ 0.
      MOVE it_adrct-remark TO str_data-remark.
    ENDIF.
*   Get Sales Employee's name
    READ TABLE it_pa0001 WITH KEY pernr = it_vbpa2-pernr.
    IF sy-subrc EQ 0.
      MOVE it_pa0001-ename TO str_data-pernr_n.
    ENDIF.
  ENDIF.

ENDFORM.                    " GET_SALES_REP_DATA


*----------------------------------------------------------------------*
*   Form  SAVE_ALL_DATA                                                *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM save_all_data.

  MOVE str_data TO it_data.
  APPEND it_data.

ENDFORM.                    " SAVE_ALL_DATA


*----------------------------------------------------------------------*
*   Form  RESET_DATA                                                   *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM reset_data.

  CLEAR str_data.

ENDFORM.                    " RESET_DATA


*----------------------------------------------------------------------*
*       Form  FILL_FIELD_CATALOG                                       *
*----------------------------------------------------------------------*
*       text                                                           *
*----------------------------------------------------------------------*
FORM fill_field_catalog.

  x_repid = sy-repid.
  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
       EXPORTING
            i_program_name         = x_repid
            i_internal_tabname     = 'IT_DATA'
*           i_structure_name       =
*           i_client_never_display = 'X'
            i_inclname             = x_repid
*           i_bypassing_buffer     =
*           i_buffer_active        =
       CHANGING
            ct_fieldcat            = it_fieldcat
       EXCEPTIONS
            inconsistent_interface = 1
            program_error          = 2
            OTHERS                 = 3.
  IF sy-subrc <> 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " FILL_FIELD_CATALOG


*----------------------------------------------------------------------*
*       Form  PREPARE_SORT                                             *
*----------------------------------------------------------------------*
*       text                                                           *
*----------------------------------------------------------------------*
FORM prepare_sort.

  CLEAR str_sort.
  str_sort-spos      = '1'.
  str_sort-fieldname = 'PRCTR'.
  str_sort-up        = 'X'.
  APPEND str_sort TO it_sort.

ENDFORM.                    " PREPARE_SORT


*----------------------------------------------------------------------*
*       Form  CHANGE_CATALOG                                           *
*----------------------------------------------------------------------*
*       text                                                           *
*----------------------------------------------------------------------*
FORM change_catalog.

  CLEAR: ls_fieldcat.
  CLEAR: lt_fieldcat.
  REFRESH: lt_fieldcat.

*        prctr      like ce41000-prctr,
*        ww002      like ce41000-ww002,
*        ww006      like ce41000-ww006,
*        ww007      like ce41000-ww007,
*        werks      like vbap-werks,
*        matnr      like vbap-matnr,
*        kunag      like vbak-kunnr,        " Sold-to party
*        kunag_n    like kna1-name1,        " Sold-to party name
*        vbeln_c    like vbak-vbeln,
*        posnr_c    like vbap-posnr,
*        parvw      like vbpa-parvw,
*        pernr      like vbpa-pernr,        " Employee personalnr
*        pernr_n    like kna1-name1,        " Sales Rep name
*        remark     like adrct-remark,
*        netwr      like vbap-netwr,
*        waerk      like vbap-waerk,
*        kwmeng     like vbap-kwmeng,
*        vrkme_c    like vbap-vrkme,
*        vbeln_j    like likp-vbeln,
*        posnr_j    like lips-posnr,
*        lfimg      like lips-lfimg,
*        vrkme_j    like lips-vrkme,
*        vbeln_m    like vbrk-vbeln,
*        posnr_m    like vbrp-posnr,
*        fkimg      like vbrp-fkimg,
*        vrkme_m    like vbrp-vrkme,
*        kzwi3      like vbrp-kzwi3,
*        kunre      like vbpa-kunnr,        " Bill-to party
*        kunre_n    like kna1-name1.        " Bill-to party name


  LOOP AT it_fieldcat INTO ls_fieldcat.
    CASE ls_fieldcat-fieldname.
      WHEN 'KUNAG_N'.
        ls_fieldcat-seltext_l = 'Sold-to Party name'.
        modify it_fieldcat from ls_fieldcat.
      WHEN 'PERNR_N'.
        ls_fieldcat-seltext_l = 'Sales Rep name'.
        modify it_fieldcat from ls_fieldcat.
      WHEN 'KUNRE'.
        ls_fieldcat-seltext_l = 'Bill-to Party'.
        modify it_fieldcat from ls_fieldcat.
      WHEN 'KUNRE_N'.
        ls_fieldcat-seltext_l = 'Bill-to Party name'.
        modify it_fieldcat from ls_fieldcat.
      WHEN 'VBELN_C'.
        ls_fieldcat-seltext_l = 'Sales Document'.
        ls_fieldcat-hotspot = 'X'.
        modify it_fieldcat from ls_fieldcat.
      WHEN 'VBELN_J'.
        ls_fieldcat-seltext_l = 'Delivery document'.
        ls_fieldcat-hotspot = 'X'.
        modify it_fieldcat from ls_fieldcat.
      WHEN 'VBELN_M'.
        ls_fieldcat-seltext_l = 'Billing document'.
        ls_fieldcat-hotspot = 'X'.
        modify it_fieldcat from ls_fieldcat.
      WHEN 'NETWR'.
        ls_fieldcat-seltext_s = 'Order Net Value'.
        ls_fieldcat-seltext_m = 'Order Net Value'.
        ls_fieldcat-seltext_l = 'Order Net Value'.
        ls_fieldcat-reptext_ddic = 'Order Net Value'.
        modify it_fieldcat from ls_fieldcat.
      WHEN 'KZWI3'.
        ls_fieldcat-seltext_s = 'Invoice Net Value'.
        ls_fieldcat-seltext_m = 'Invoice Net Value'.
        ls_fieldcat-seltext_l = 'Invoice Net Value'.
        ls_fieldcat-reptext_ddic = 'Invoice Net Value'.
        modify it_fieldcat from ls_fieldcat.
    ENDCASE.
  ENDLOOP.

  gs_layout-colwidth_optimize   = 'X'.
*  gs_layout-confirmation_prompt = 'X'.
*  gs_layout-key_hotspot         = 'X'.
  gs_layout-detail_titlebar     = 'SALES REP REPORTING FOR COMMISSIONS'.

ENDFORM.                    " CHANGE_CATALOG


*----------------------------------------------------------------------*
*       Form  ALV_OUTPUT                                               *
*----------------------------------------------------------------------*
*       text                                                           *
*----------------------------------------------------------------------*
FORM alv_output.

* variant-report    = 'YSE_SD_COMMISSION_SALESREP'.
* variant-variant   = variant.
  gs_sd_alv-variant = g_variant.

  w_callback_program    = sy-repid.
  w_callback_subroutine = 'HOTSPOT_CLICK'.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
       EXPORTING
*           i_interface_check           = ' '
*           i_bypassing_buffer          =
*           i_buffer_active             = ' '
            i_callback_program          = w_callback_program
*           i_callback_program          = 'YSE_SD_COMMISSION_SALESREP'
*           i_callback_pf_status_set    = ' '
            i_callback_user_command     = w_callback_subroutine
*           i_callback_top_of_page      = ' '
*           i_callback_html_top_of_page = ' '
*           i_callback_html_end_of_list = ' '
*           i_structure_name            =
*           i_background_id             = ' '
            i_grid_title                = 'Sales rep reporting'
*           i_grid_settings             =
            is_layout                   = gs_layout
            it_fieldcat                 = it_fieldcat
*           it_excluding                =
*           it_special_groups           =
            it_sort                     = it_sort
*           it_filter                   =
*           is_sel_hide                 =
            i_default                   = 'X'
            i_save                      = 'A'
            is_variant                  = gs_sd_alv-variant
*           it_events                   =
*           it_event_exit               =
*           is_print                    =
*           is_reprep_id                =
            i_screen_start_column       = 0
            i_screen_start_line         = 0
            i_screen_end_column         = 0
            i_screen_end_line           = 0
*           it_alv_graphics             =
*           it_add_fieldcat             =
*           it_hyperlink                =
*      importing
*           e_exit_caused_by_caller     =
*           es_exit_caused_by_user      =
       TABLES
            t_outtab                    = it_data
       EXCEPTIONS
            program_error               = 1
            OTHERS                      = 2.
  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_OUTPUT


*----------------------------------------------------------------------*
*  Form  ALV_OUTPUT                                                    *
*----------------------------------------------------------------------*
*  Handles calling of transactions to edit Sales Orders, Deliveries    *
*  and Invoices when clicking on a hotspot.                            *
*----------------------------------------------------------------------*
FORM hotspot_click USING p_ucomm    LIKE sy-ucomm
                         p_selfield TYPE slis_selfield.

  IF NOT p_selfield-value IS INITIAL.
    IF p_ucomm EQ '&IC1'.    " SAP standard code for clicking
      CASE p_selfield-fieldname.
        WHEN 'VBELN_C'.
*         Display Sales Order
          SET PARAMETER ID 'AUN' FIELD p_selfield-value.   " 'KTN'
          CALL TRANSACTION 'VA03' AND SKIP FIRST SCREEN.
        WHEN 'VBELN_J'.
*         Display Delivery
          SET PARAMETER ID 'VL' FIELD p_selfield-value.
          CALL TRANSACTION 'VL03N' AND SKIP FIRST SCREEN.
        WHEN 'VBELN_M'.
*         Display Billing document
          SET PARAMETER ID 'VF' FIELD p_selfield-value.
          CALL TRANSACTION 'VF03' AND SKIP FIRST SCREEN.
      ENDCASE.
    ENDIF.    " hotspot click
  ENDIF.    " value not initial
ENDFORM.   " hotspot_click


*&---------------------------------------------------------------------*
*&      Form  check_authorization
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form check_authorization .

  AUTHORITY-CHECK OBJECT 'V_VBAK_VKO'
                      ID 'VKORG' FIELD p_vkorg
                      ID 'VTWEG' DUMMY
                      ID 'SPART' DUMMY
                      ID 'ACTVT' DUMMY.

  IF sy-subrc = 4.
*   No authorisation to display data from Sales Organisation p_vkorg
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '006' WITH p_vkorg.
  ELSEIF sy-subrc <> 0.
*   Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
  ENDIF.

endform.                    " check_authorization

*Text symbol text£º
*F01:Field 1
*S01:Selections
*S02:Entered / Invoiced ?
*S03:Select sales orders entered
*S04:Select sales orders invoiced
*S05:Only sales orders delivered
*S06:ALV

*S07:Variant to use ALV-output
*Selection text£º
*P_FDATE:        From date
*P_TDATE:        To date
*P_VKORG:        Sales organization
*S_KUNAG:D       Sold-to party
*S_MATNR:D       Material
*S_PERNR:        Sales representative
*S_POSNR:D       Sales Document Item
*S_PRCTR:D       Profit Center
*S_SPART:D       Division
*S_VBELN:D       Sales document
*S_VKORG:D       Sales Organization
*S_VTWEG:D       Distribution Channel
*S_WERKS:D       Plant
*S_WW002:D       PLC
*S_WW006:D       GAC
*S_WW007:D       PGC
