************************************************************************
* Program ID        : YSE_TRANSFER_STOCK					     *
* Program Title     : Program title                                    *
* Author            : Anson Sun                                        *
* Date              : 20/12/2011                               	     *
* Change Request Number:2296                                           *
* Description       : The purpose of this program is to transfer stock *
*======================================================================*
* Copied From       : (Cloned Program)                        	     *
* Title             : (Program Title)                          	     *
* Other Related obj : (Object names)                 		           *
*======================================================================*
* Change History Log                                             	     *
*----------------------------------------------------------------------*
* Mod. no.|  Date    | Name           | Correction Number  | Change    *
*----------------------------------------------------------------------*
* MOD-001 |dd/mm/yyyy| xxxxxxxxxxxxxx | XXXXxxxxxx         |           *
*                                                                      *
* Description:                                                   	     *
*----------------------------------------------------------------------*

REPORT  yse_transfer_stock MESSAGE-ID yse_cs_sd_msg.
*----------------------------------------------------------------------*
*       TABLES
*----------------------------------------------------------------------*
TABLES:
  mara,                      "General Material Data
  marc,                      "Plant Data for Material
  mard,                      "Storage Location Data for Material
  mchb,                      "Batch Stocks
  mkpf.                      "Header: Material Document
*----------------------------------------------------------------------*
*       TYPE-POOLS
*----------------------------------------------------------------------*
TYPE-POOLS slis.
*----------------------------------------------------------------------*
*       TYPE
*----------------------------------------------------------------------*
TYPES:
  BEGIN OF ty_stock,
    stock_typ TYPE char04,             "Stock Type(Internal Use)
    werks TYPE marc-werks,             "Plant
    lgort TYPE mard-lgort,             "Storage Location

    matnr TYPE marc-matnr,             "Material Number
    charg TYPE mchb-charg,             "Batch Number
    sernp TYPE marc-sernp,             "Serial Number Profile
    sernr TYPE equi-sernr,             "Serial Number
    xchar TYPE marc-xchar,             "Batch management indicator
    labst TYPE mard-labst,             "w/o batch Un-Use Stock
    clabs TYPE mchb-clabs,             "batch Un-Use Stock
    menge TYPE mard-labst,             "Stock
    meins TYPE mara-meins,             "Base Unit
    bwart TYPE bwart,                  "Movement Type
  END   OF ty_stock,

  BEGIN OF ty_sernr,
    b_werk   TYPE eqbs-b_werk,         "Plant
    b_lager  TYPE eqbs-b_lager,        "Storage Location
    b_charge TYPE eqbs-b_charge,       "Batch Number
    matnr    TYPE equi-matnr,          "Material Number
    sernr    TYPE equi-sernr,          "Serial number
  END   OF ty_sernr.
*----------------------------------------------------------------------*
*       DATA
*----------------------------------------------------------------------*
DATA:
  rec_stock     TYPE ty_stock,
  rec_sernr     TYPE ty_sernr,
  rec_bdc       TYPE bdcdata,
  i_log         TYPE TABLE OF yse_transfer_stock_log,
  i_fieldcat    TYPE slis_t_fieldcat_alv.
*---------------------------------------------------------------------*
*       CONSTANT
*---------------------------------------------------------------------*
CONSTANTS: c_class TYPE klasse_d VALUE 'ZRU_BATCH'.
*----------------------------------------------------------------------*
*       SELECTION-SCREEN
*----------------------------------------------------------------------*
SELECTION-SCREEN: BEGIN OF BLOCK b_sel1 WITH FRAME TITLE text-b01.
SELECT-OPTIONS:
  s_werks  FOR marc-werks OBLIGATORY,
  s_lgort  FOR mard-lgort OBLIGATORY,
  s_matnr  FOR marc-matnr.
SELECTION-SCREEN: END   OF BLOCK b_sel1.

SELECTION-SCREEN: BEGIN OF BLOCK b_sel2 WITH FRAME TITLE text-b02.
SELECT-OPTIONS:
  t_werks  FOR marc-werks NO-EXTENSION NO INTERVALS OBLIGATORY,
  t_lgort  FOR mard-lgort NO-EXTENSION NO INTERVALS OBLIGATORY.
SELECTION-SCREEN: END   OF BLOCK b_sel2.

SELECTION-SCREEN: BEGIN OF BLOCK b_sel3 WITH FRAME TITLE text-b03.
SELECT-OPTIONS:
  s_budat  FOR mkpf-budat NO-EXTENSION NO INTERVALS OBLIGATORY,
  s_bktxt  FOR mkpf-bktxt NO-EXTENSION NO INTERVALS OBLIGATORY.
SELECTION-SCREEN: END   OF BLOCK b_sel3.
PARAMETERS:
  p_maxpo  TYPE maxpo OBLIGATORY,
  p_zeile  TYPE maxpo NO-DISPLAY.
*----------------------------------------------------------------------*
*       INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

* initial process
  PERFORM ini_process.
*----------------------------------------------------------------------*
*       AT SELECTION-SCREEN OUTPUT
*----------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.
  LOOP AT SCREEN.
    IF screen-name = 'P_ZEILE'.
      p_zeile = p_maxpo * 2.
      screen-input = 0.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.
*----------------------------------------------------------------------*
*       START-OF-SELECTION
*----------------------------------------------------------------------*
START-OF-SELECTION.

* main process
  PERFORM main_process.
*----------------------------------------------------------------------*
*       END-OF-SELECTION
*----------------------------------------------------------------------*
END-OF-SELECTION.

* log process
  PERFORM display_log.
*&---------------------------------------------------------------------*
*&      Form  ini_process
*&---------------------------------------------------------------------*
*       initial process
*----------------------------------------------------------------------*
FORM ini_process .

  CLEAR: rec_stock,
         rec_sernr.
  REFRESH:
    i_log,
    i_fieldcat.
ENDFORM.                    " ini_process
*&---------------------------------------------------------------------*
*&      Form  main_process
*&---------------------------------------------------------------------*
*       main process
*----------------------------------------------------------------------*
FORM main_process .

  DATA: li_stock TYPE TABLE OF ty_stock,
        li_sernr TYPE TABLE OF ty_sernr.

* Get Serial Number
  PERFORM get_sernr TABLES li_sernr.
* Get Stock
  PERFORM get_stock TABLES li_stock.
* Transfer Stock
  PERFORM transfer_stock TABLES li_stock
                              li_sernr.

ENDFORM.                    " main_process
*&---------------------------------------------------------------------*
*&      Form  display_log
*&---------------------------------------------------------------------*
*       display log
*----------------------------------------------------------------------*
FORM display_log .
  DATA: lrec_layout TYPE slis_layout_alv.

* edit alv fieldcat
  PERFORM edit_alv_fieldcat.

  lrec_layout-colwidth_optimize = 'X'.
  lrec_layout-zebra             = 'X'.
  CALL FUNCTION 'REUSE_ALV_LIST_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'SET_STATUS_10001'
      i_callback_user_command  = 'USER_COMMAND'
      is_layout                = lrec_layout
      it_fieldcat              = i_fieldcat
    TABLES
      t_outtab                 = i_log
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    EXIT.
  ELSE.
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " display_log
*&---------------------------------------------------------------------*
*&      Form  edit_alv_fieldcat
*&---------------------------------------------------------------------*
*       edit alv fieldcat
*----------------------------------------------------------------------*
FORM edit_alv_fieldcat.

  DATA: lrec_fieldcat TYPE slis_fieldcat_alv.
  REFRESH: i_fieldcat.

  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
    EXPORTING
      i_structure_name       = 'YSE_TRANSFER_STOCK_LOG'
      i_bypassing_buffer     = 'X'
    CHANGING
      ct_fieldcat            = i_fieldcat
    EXCEPTIONS
      inconsistent_interface = 1
      program_error          = 2
      OTHERS                 = 3.
  IF sy-subrc <> 0.
    EXIT.
  ENDIF.

ENDFORM.                    " edit_alv_fieldcat
*&---------------------------------------------------------------------*
*&      Form  set_status_10001
*&---------------------------------------------------------------------*
*       set screen status
*----------------------------------------------------------------------*
FORM set_status_10001 USING i_extab TYPE slis_t_extab.

  SET PF-STATUS '10001'.
  SET TITLEBAR  '10001'.

ENDFORM.                               " set_status_10001
*&---------------------------------------------------------------------*
*&      Form  user_command
*&---------------------------------------------------------------------*
*       user command
*----------------------------------------------------------------------*
FORM user_command  USING    i_ucomm    LIKE sy-ucomm
                            i_selfield TYPE slis_selfield.

  CASE i_ucomm.
    WHEN '&IC1'.
      PERFORM display_process USING i_selfield.
  ENDCASE.

ENDFORM.                               " user_command
*&---------------------------------------------------------------------*
*&      Form  display_process
*&---------------------------------------------------------------------*
*       display process
*----------------------------------------------------------------------*
FORM display_process  USING    selfield TYPE slis_selfield.

  DATA lrec_log TYPE yse_transfer_stock_log.
  READ TABLE i_log INTO lrec_log INDEX selfield-tabindex.
  IF sy-subrc = 0.
    CASE selfield-fieldname.
      WHEN 'MBLNR'.
        SET PARAMETER ID 'MBN' FIELD lrec_log-mblnr.
        SET PARAMETER ID 'MJA' FIELD lrec_log-mjahr.
        CALL TRANSACTION 'MB03' AND SKIP FIRST SCREEN.
    ENDCASE.
  ENDIF.

ENDFORM.                    " display_process
*&---------------------------------------------------------------------*
*&      Form  SET_LOG
*&---------------------------------------------------------------------*
*       Set Log
*----------------------------------------------------------------------*
FORM set_log  USING mat_doc  TYPE bapi2017_gm_head_ret
                    msg_type TYPE bapi_mtype
                    msg      TYPE bapi_msg.

  DATA: lrec_log   TYPE yse_transfer_stock_log.
  lrec_log-mblnr    = mat_doc-mat_doc.
  lrec_log-mjahr    = mat_doc-doc_year.
  lrec_log-type     = msg_type.
  lrec_log-message  = msg.
  APPEND lrec_log TO i_log.

ENDFORM.                    " SET_LOG
*&---------------------------------------------------------------------*
*&      Form  GET_STOCK
*&---------------------------------------------------------------------*
*       Get Stock
*----------------------------------------------------------------------*
FORM get_stock TABLES i_stock STRUCTURE rec_stock.

  DATA: lrec_stock         TYPE ty_stock.
  DATA: li_stock_no_batch  TYPE TABLE OF ty_stock,
        li_stock_batch     TYPE TABLE OF ty_stock.
  DATA: l_tabix            TYPE sy-tabix.

* Get w/o batch Un-Use Stock
  SELECT marc~werks                    "Plant
         mard~lgort                    "Storage Location
         marc~matnr                    "Material Number
         marc~sernp                    "Serial Number Profile
         marc~xchar                    "Batch management indicator
         mard~labst                    "w/o batch Un-Use Stock
    INTO CORRESPONDING FIELDS OF TABLE li_stock_no_batch
    FROM marc INNER JOIN mard
      ON marc~matnr = mard~matnr AND marc~werks = mard~werks
   WHERE marc~matnr IN s_matnr   AND marc~werks IN s_werks
     AND marc~lvorm = space      AND marc~xchar = space
     AND mard~lgort IN s_lgort   AND mard~lvorm = space
     AND mard~labst > 0.
  IF sy-subrc = 0.
    LOOP AT li_stock_no_batch INTO lrec_stock.
      l_tabix              = sy-tabix.
      lrec_stock-bwart     = '301'.
      lrec_stock-menge     = lrec_stock-labst.
      lrec_stock-stock_typ = 'MARD'.
*     Get Base Unit
      PERFORM get_meins USING lrec_stock-matnr
                     CHANGING lrec_stock-meins.
      APPEND lrec_stock TO i_stock.
      MODIFY li_stock_no_batch FROM lrec_stock INDEX l_tabix.
    ENDLOOP.
  ENDIF.

* Get batch Un-Use Stock
  SELECT marc~werks                    "Plant
         mchb~lgort                    "Storage Location
         mchb~charg                    "Batch Number
         marc~matnr                    "Material Number
         marc~sernp                    "Serial Number Profile
         marc~xchar                    "Batch management indicator
         mchb~clabs                    "batch Un-Use Stock
    INTO CORRESPONDING FIELDS OF TABLE li_stock_batch
    FROM marc INNER JOIN mchb
      ON marc~matnr = mchb~matnr AND marc~werks = mchb~werks
   WHERE marc~matnr IN s_matnr   AND marc~werks IN s_werks
     AND marc~lvorm = space      AND marc~xchar = 'X'
     AND mchb~lgort IN s_lgort   AND mchb~lvorm = space
     AND clabs > 0.
  IF sy-subrc = 0.
    LOOP AT li_stock_batch INTO lrec_stock.
      l_tabix              = sy-tabix.
      lrec_stock-bwart     = '301'.
      lrec_stock-menge     = lrec_stock-clabs.
      lrec_stock-stock_typ = 'MCHB'.
*     Get Base Unit
      PERFORM get_meins USING lrec_stock-matnr
                     CHANGING lrec_stock-meins.
      APPEND lrec_stock TO i_stock.
      MODIFY li_stock_batch FROM lrec_stock INDEX l_tabix.
    ENDLOOP.
  ENDIF.

* no data process.
  IF i_stock[] IS INITIAL.
    MESSAGE s001.
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " GET_STOCK
*&---------------------------------------------------------------------*
*&      Form  TRANSFER_STOCK
*&---------------------------------------------------------------------*
*       Transfer Stock
*----------------------------------------------------------------------*
FORM transfer_stock TABLES i_stock STRUCTURE rec_stock
                           i_sernr STRUCTURE rec_sernr.

  DATA: lrec_stock        TYPE ty_stock,
        li_stock_bak      TYPE TABLE OF ty_stock.
  DATA: l_subrc           TYPE sy-subrc.

  SORT i_stock[] BY stock_typ werks lgort matnr charg.
  LOOP AT i_stock[] INTO lrec_stock.
*   Batch Management
    PERFORM batch_management USING lrec_stock
                          CHANGING l_subrc.
    CHECK l_subrc = 0.

*   Serial Number Profile
    IF lrec_stock-sernp   = 'Z001'.
      PERFORM set_sernr TABLES li_stock_bak
                               i_sernr
                         USING lrec_stock.
    ELSE.
      APPEND lrec_stock TO li_stock_bak.
    ENDIF.

********** Begin Goods Movement Create **********
    AT END OF lgort.
      PERFORM goodsmvt_create TABLES li_stock_bak.
      REFRESH li_stock_bak.
    ENDAT.
********** End   Goods Movement Create **********
    CLEAR lrec_stock.
  ENDLOOP.

ENDFORM.                    " TRANSFER_STOCK
*&---------------------------------------------------------------------*
*&      Form  GOODSMVT_CREATE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM goodsmvt_create  TABLES i_stock STRUCTURE rec_stock.

  DATA: lrec_stock        TYPE ty_stock,
        gm_head           TYPE bapi2017_gm_head_01,
        gm_code           TYPE bapi2017_gm_code,
        lrec_item         TYPE bapi2017_gm_item_create,
        lrec_gm_sernr     TYPE bapi2017_gm_serialnumber,
        mat_doc           TYPE bapi2017_gm_head_ret,
        lrec_return       TYPE bapiret2.

  DATA: gm_item           TYPE TABLE OF bapi2017_gm_item_create,
        gm_sernr          TYPE TABLE OF bapi2017_gm_serialnumber,
        return            TYPE TABLE OF bapiret2.

  DATA: l_lines           TYPE i,
        l_maxpo           TYPE maxpo,
        l_matdoc_itm      TYPE i,
        lflg_last_proc    TYPE c,
        l_msg             TYPE bapi_msg.

* GMCODE Table T158G - 01 - MB01 - Goods Receipts for Purchase Order
*                      02 - MB31 - Goods Receipts for Prod Order
*                      03 - MB1A - Goods Issue
*                      04 - MB1B - Transfer Posting
*                      05 - MB1C - Enter Other Goods Receipt
*                      06 - MB11 - Enter Goods Movement

  CHECK i_stock[] IS NOT INITIAL.

  l_maxpo = p_maxpo.

  gm_head-pstng_date = s_budat-low.
  gm_head-doc_date   = sy-datum.
  gm_head-pr_uname   = sy-uname.
  gm_head-header_txt = s_bktxt-low.
  gm_code-gm_code    = '04'.

  LOOP AT i_stock INTO lrec_stock.
    lrec_item-material        = lrec_stock-matnr.
    lrec_item-plant           = lrec_stock-werks.
    lrec_item-stge_loc        = lrec_stock-lgort.
    lrec_item-batch           = lrec_stock-charg.
    lrec_item-move_type       = lrec_stock-bwart.
    lrec_item-entry_uom       = lrec_stock-meins.
    lrec_item-entry_qnt       = lrec_stock-menge.
    lrec_item-move_plant      = t_werks-low.
    lrec_item-move_stloc      = t_lgort-low.
    APPEND lrec_item TO gm_item.
    CLEAR  lrec_item.

    IF lrec_stock-sernp      = 'Z001'.
      l_matdoc_itm             = l_matdoc_itm + 1.
      lrec_gm_sernr-matdoc_itm = l_matdoc_itm.
      lrec_gm_sernr-serialno   = lrec_stock-sernr.
      APPEND lrec_gm_sernr TO gm_sernr.
      CLEAR  lrec_gm_sernr.
    ENDIF.

    DESCRIBE TABLE gm_item LINES l_lines.
    IF l_lines = l_maxpo.
      lflg_last_proc = space.
      CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
        EXPORTING
          goodsmvt_header       = gm_head
          goodsmvt_code         = gm_code
        IMPORTING
          goodsmvt_headret      = mat_doc
        TABLES
          goodsmvt_item         = gm_item
          goodsmvt_serialnumber = gm_sernr
          return                = return.

      LOOP AT return INTO lrec_return WHERE type = 'E'
                                         OR type = 'A'
                                         OR type = 'X'.
        PERFORM set_log USING mat_doc
                              lrec_return-type lrec_return-message.
      ENDLOOP.
      IF sy-subrc <> 0.
        COMMIT WORK AND WAIT.
        MESSAGE s060(m7) WITH mat_doc-mat_doc INTO l_msg.
        PERFORM set_log USING mat_doc 'S' l_msg.
      ENDIF.
      CLEAR:   l_matdoc_itm.
      REFRESH: gm_item, gm_sernr, return.
    ELSE.
      lflg_last_proc = 'X'.
    ENDIF.
  ENDLOOP.

  IF lflg_last_proc = 'X'.
    CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
      EXPORTING
        goodsmvt_header       = gm_head
        goodsmvt_code         = gm_code
      IMPORTING
        goodsmvt_headret      = mat_doc
      TABLES
        goodsmvt_item         = gm_item
        goodsmvt_serialnumber = gm_sernr
        return                = return.

    LOOP AT return INTO lrec_return WHERE type = 'E'
                                       OR type = 'A'
                                       OR type = 'X'.
      PERFORM set_log USING mat_doc
                            lrec_return-type lrec_return-message.
    ENDLOOP.
    IF sy-subrc <> 0.
      COMMIT WORK AND WAIT.
      MESSAGE s060(m7) WITH mat_doc-mat_doc INTO l_msg.
      PERFORM set_log USING mat_doc 'S' l_msg.
    ENDIF.
  ENDIF.
  CLEAR:   l_matdoc_itm.
  REFRESH: gm_item, gm_sernr, return.

ENDFORM.                    " GOODSMVT_CREATE
*&---------------------------------------------------------------------*
*&      Form  SET_SERNR
*&---------------------------------------------------------------------*
*       Get Serial Number
*----------------------------------------------------------------------*
FORM set_sernr  TABLES i_stock STRUCTURE rec_stock
                       i_sernr STRUCTURE rec_sernr
                 USING stock   TYPE ty_stock.

  DATA: lrec_stock   TYPE ty_stock,
        lrec_sernr   TYPE ty_sernr.
  DATA: l_lines_proc TYPE i.
  DATA: l_subrc      TYPE sy-subrc,
        l_mat_doc    TYPE bapi2017_gm_head_ret,
        l_msg        TYPE bapi_msg.

  lrec_stock = stock.
  CASE stock-stock_typ.
    WHEN 'MARD'.
      LOOP AT i_sernr[] INTO lrec_sernr
                     WHERE b_werk  = stock-werks
                       AND b_lager = stock-lgort
                       AND matnr   = stock-matnr.
        l_lines_proc = l_lines_proc + 1.
        IF l_lines_proc > stock-menge.
          EXIT.
        ENDIF.
        lrec_stock-menge         = 1.
        lrec_stock-sernr         = lrec_sernr-sernr.
        APPEND lrec_stock TO i_stock.
      ENDLOOP.
      l_subrc = sy-subrc.
    WHEN 'MCHB'.
      LOOP AT i_sernr[] INTO lrec_sernr
                     WHERE b_werk   = stock-werks
                       AND b_lager  = stock-lgort
                       AND matnr    = stock-matnr
                       AND b_charge = stock-charg.
        l_lines_proc = l_lines_proc + 1.
        IF l_lines_proc > stock-menge.
          EXIT.
        ENDIF.
        lrec_stock-menge         = 1.
        lrec_stock-sernr         = lrec_sernr-sernr.
        APPEND lrec_stock TO i_stock.
      ENDLOOP.
      l_subrc = sy-subrc.
  ENDCASE.

  IF l_subrc <> 0.
    MESSAGE s008 INTO l_msg WITH stock-sernp stock-matnr
                                 stock-werks stock-lgort.
    PERFORM set_log USING l_mat_doc 'E' l_msg.
  ENDIF.

ENDFORM.                    " SET_SERNR
*&---------------------------------------------------------------------*
*&      Form  GET_SERNR
*&---------------------------------------------------------------------*
*       Get Serial Number
*----------------------------------------------------------------------*
FORM get_sernr  TABLES i_sernr STRUCTURE rec_sernr.

  SELECT * INTO CORRESPONDING FIELDS OF TABLE i_sernr
    FROM equi INNER JOIN eqbs ON equi~equnr = eqbs~equnr
   WHERE eqbs~b_werk  IN s_werks
     AND eqbs~b_lager IN s_lgort
     AND equi~matnr   IN s_matnr
     AND equi~lvorm   =  space.
  SORT i_sernr BY     b_werk b_lager b_charge matnr sernr.

ENDFORM.                    " GET_SERNR
*&---------------------------------------------------------------------*
*&      Form  GET_MEINS
*&---------------------------------------------------------------------*
*       Get Base Unit
*----------------------------------------------------------------------*
FORM get_meins  USING    matnr TYPE mara-matnr
                CHANGING meins TYPE mara-meins.
  CLEAR meins.
  SELECT SINGLE meins INTO meins FROM mara WHERE matnr = matnr.

ENDFORM.                    " GET_MEINS
*&---------------------------------------------------------------------*
*&      Form  BATCH_MANAGEMENT
*&---------------------------------------------------------------------*
*       Batch Management
*----------------------------------------------------------------------*
FORM batch_management  USING    stock TYPE ty_stock
                       CHANGING subrc.

  DATA: l_charg      TYPE mchb-charg,
        l_mat_doc    TYPE bapi2017_gm_head_ret,
        l_msg        TYPE bapi_msg.
  DATA: li_bdc       TYPE TABLE OF bdcdata,
        li_message   TYPE TABLE OF bdcmsgcoll,
        lrec_message TYPE bdcmsgcoll.
  DATA: l_mode       TYPE ctu_mode,
        l_update     TYPE ctu_update,
        l_msgtx      TYPE bapi_msg.

  DATA: gt_return    TYPE STANDARD TABLE OF bapiret2,
        lrec_return  LIKE LINE OF gt_return,
        gs_batchctrl TYPE bapibatchctrl,
        gv_batch     TYPE bapibatchkey-batch.
  DATA: transaction_id TYPE arfctid,
        commit_work(15) VALUE 'COMMIT_WORK',
        status_of_transaction(15).

  CLEAR: subrc.
  CHECK  stock-xchar = 'X' AND stock-charg IS NOT INITIAL.

  SELECT SINGLE mchb~charg INTO l_charg
    FROM mcha INNER JOIN mchb
      ON ( mcha~matnr = mchb~matnr AND
           mcha~werks = mchb~werks AND
           mcha~charg = mchb~charg )
   WHERE mcha~lvorm = space
     AND mchb~matnr = stock-matnr
     AND mchb~werks = t_werks-low
     AND mchb~lgort = t_lgort-low
     AND mchb~charg = stock-charg
     AND mchb~lvorm = space.

  IF sy-subrc <> 0.
**   Call Batch Input Tcode MSC1N
*    PERFORM set_bdcdata TABLES li_bdc
*                         USING stock.
*    l_mode   = 'N'.
*    l_update = 'S'.
*    CALL TRANSACTION 'MSC1N' USING li_bdc
*                              MODE l_mode
*                            UPDATE l_update
*                     MESSAGES INTO li_message.
*    LOOP AT li_message INTO lrec_message WHERE msgtyp = 'E'
*                                            OR msgtyp = 'A'
*                                            OR msgtyp = 'X'.
*    ENDLOOP.
*    IF sy-subrc = 0.
*      subrc = 4.
*      MESSAGE s015 INTO l_msg WITH stock-charg stock-matnr
*                                   t_werks-low t_lgort-low.
*      PERFORM set_log USING l_mat_doc 'E' l_msg.
*    ENDIF.

*   Call Bapi BAPI_BATCH_CREATE
    CALL FUNCTION 'TRANSACTION_BEGIN'
      IMPORTING
        transaction_id = transaction_id
      EXCEPTIONS
        OTHERS         = 1.

    gs_batchctrl-batchlevel = '0'.
    gs_batchctrl-class_num  = c_class.
    gs_batchctrl-doclassify = 'X'.

    CALL FUNCTION 'BAPI_BATCH_CREATE'
      EXPORTING
        material                   = stock-matnr
        batch                      = stock-charg
        plant                      = t_werks-low
*       BATCHATTRIBUTES            =
        batchcontrolfields         = gs_batchctrl
        batchstoragelocation       = t_lgort-low
*       INTERNALNUMBERCOM          =
*       EXTENSION1                 =
*       MATERIAL_EVG               =
      IMPORTING
        batch                      = gv_batch
*       BATCHATTRIBUTES            =
      TABLES
        return                     = gt_return.

    CALL FUNCTION 'TRANSACTION_STATUS'
      IMPORTING
        status = status_of_transaction
      EXCEPTIONS
        OTHERS = 1.

    IF status_of_transaction = commit_work.
      COMMIT WORK AND WAIT.
    ENDIF.

    CALL FUNCTION 'TRANSACTION_END'
      EXPORTING
        transaction_id = transaction_id
      EXCEPTIONS
        OTHERS         = 1.

    LOOP AT gt_return INTO lrec_return WHERE type = 'E'
                                          OR type = 'A'.
    ENDLOOP.
    IF sy-subrc = 0.
      subrc = 4.
      PERFORM set_log USING l_mat_doc
                            lrec_return-type lrec_return-message.
    ENDIF.
  ENDIF.
ENDFORM.                    " BATCH_MANAGEMENT
*&---------------------------------------------------------------------*
*&      Form  SET_BDCDATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM set_bdcdata TABLES li_bdc STRUCTURE rec_bdc
                  USING stock  TYPE      ty_stock.

  PERFORM set_bdc_dynpro TABLES li_bdc
                          USING 'SAPLCHRG'
                                '1000'.
  PERFORM set_bdc_field  TABLES li_bdc
                          USING 'DFBATCH-MATNR'
                                stock-matnr.
  PERFORM set_bdc_field  TABLES li_bdc
                          USING 'DFBATCH-REF_MATNR'
                                stock-matnr.
  PERFORM set_bdc_field  TABLES li_bdc
                          USING 'DFBATCH-WERKS'
                                t_werks-low.
  PERFORM set_bdc_field  TABLES li_bdc
                          USING 'DFBATCH-REF_WERKS'
                                stock-werks.
  PERFORM set_bdc_field  TABLES li_bdc
                          USING 'DFBATCH-CHARG'
                                stock-charg.
  PERFORM set_bdc_field  TABLES li_bdc
                          USING 'DFBATCH-REF_CHARG'
                                stock-charg.
  PERFORM set_bdc_field  TABLES li_bdc
                          USING 'DFBATCH-LGORT'
                                t_lgort-low.
  PERFORM set_bdc_field  TABLES li_bdc
                          USING 'BDC_OKCODE'
                                '=ENTR'.

  PERFORM set_bdc_dynpro TABLES li_bdc
                          USING 'SAPLCHRG'
                                '1000'.
  PERFORM set_bdc_field  TABLES li_bdc
                          USING 'BDC_OKCODE'
                                '=SAVE'.

ENDFORM.                    " SET_BDCDATA
*&---------------------------------------------------------------------*
*&      Form  SET_BDC_DYNPRO
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM set_bdc_dynpro TABLES li_bdc STRUCTURE rec_bdc
                     USING program dynpro.

  DATA: lrec_bdc TYPE bdcdata.
  lrec_bdc-program  = program.
  lrec_bdc-dynpro   = dynpro.
  lrec_bdc-dynbegin = 'X'.
  APPEND lrec_bdc TO li_bdc.

ENDFORM.                    " SET_BDC_DYNPRO
*&---------------------------------------------------------------------*
*&      Form  SET_BDC_FIELD
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM set_bdc_field TABLES li_bdc STRUCTURE rec_bdc
                    USING fnam fval.

  DATA: lrec_bdc TYPE bdcdata.
  lrec_bdc-fnam  = fnam.
  lrec_bdc-fval  = fval.
  APPEND lrec_bdc TO li_bdc.

ENDFORM.                    " SET_BDC_FIELD

*Text symbol text£º
*B01:Issuing
*B02:Receiving

*B03:Material Doc.
*Selection text£º
*P_MAXPO:        Maximum No. of Items
*P_ZEILE:        Max Matl Doc.Items
*S_BKTXT:        Header Text
*S_BUDAT:        Posting Date
*S_LGORT:        Storage Loc.
*S_MATNR:        Material
*S_WERKS:        Plant
*T_LGORT:        Storage Loc.
*T_WERKS:        Plant
