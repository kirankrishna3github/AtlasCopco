*&---------------------------------------------------------------------*
*& Report  YSE_FICO_RFPERIOD_CLOSE
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& Close Periods                                                       *
*& (Copy of RFPERIOD_CLOSE)                                            *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 23.03.2012
*  Change Request Number : CR2478
*  Transport request Nr. : CD1K970905
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME           | CORRECTION NR. | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-000 |            |                | CD1K......     | CR....      *
*----------------------------------------------------------------------*

************************************************************************


REPORT  yse_fico_rfperiod_close NO STANDARD PAGE HEADING MESSAGE-ID tfc0.

TABLES:  t001, t001b, t009.
DATA:    rectyp LIKE t001b-rrcty VALUE '0'.  " Record type restricted

* -------------------------------------------------------------------- *
* Selection criteria
* -------------------------------------------------------------------- *
SELECTION-SCREEN BEGIN OF BLOCK 001 WITH FRAME TITLE text-001. "#EC *
PARAMETERS:     cocovar  LIKE t001b-bukrs OBLIGATORY,
                fyear    LIKE t001b-frye1 OBLIGATORY,
                prd1_to  LIKE t001b-tope1 OBLIGATORY,
                prd2_to  LIKE t001b-tope2,
                prd3_to  LIKE t001b-tope3           MODIF ID ehp,
                actyp    LIKE t001b-mkoar OBLIGATORY,
                vkont    LIKE t001b-vkont,                  "New
                bkont    LIKE t001b-bkont.                  "New
SELECTION-SCREEN END OF BLOCK 001.


* -------------------------------------------------------------------- *
* Initialization
* -------------------------------------------------------------------- *
INITIALIZATION.


* -------------------------------------------------------------------- *
* Encapsulation of SchedMan routines
* -------------------------------------------------------------------- *
  INCLUDE rf_prd_cl_sched.

AT SELECTION-SCREEN OUTPUT.
  IF cl_fagl_switch_check=>fagl_fin_gl_2_rs( ) IS INITIAL.  " EHP4 NewGL

    LOOP AT SCREEN.
      IF screen-group1 = 'EHP'.
        screen-active = '0'.
      ENDIF.
      MODIFY SCREEN.
    ENDLOOP.
  ENDIF.

* -------------------------------------------------------------------- *
* Start of selection
* MAIN PROGRAM
* -------------------------------------------------------------------- *
START-OF-SELECTION.

* Initialize SCHEDMAN
  PERFORM scm_init.

* Authority check
  PERFORM check_authority.

* Close Period
  PERFORM determine_open_range.

* Close SCHEDMAN
  PERFORM scm_close.

END-OF-SELECTION.


* -------------------------------------------------------------------- *
* At SELECTION-SCREEN
* -------------------------------------------------------------------- *
AT SELECTION-SCREEN ON cocovar.
  PERFORM selection_check_1.

AT SELECTION-SCREEN ON fyear.
  PERFORM selection_check_2.

AT SELECTION-SCREEN ON prd1_to.
  PERFORM selection_check_3_1.

AT SELECTION-SCREEN ON prd2_to.
  PERFORM selection_check_3_2 USING prd2_to.

AT SELECTION-SCREEN ON prd3_to.
  PERFORM selection_check_3_2 USING prd3_to.

AT SELECTION-SCREEN ON actyp.
  PERFORM selection_check_4.


* -------------------------------------------------------------------- *
* FORM  SELECTION_CHECK_1
* -------------------------------------------------------------------- *
FORM selection_check_1.

  SELECT * FROM t001b
           WHERE rrcty = rectyp
             AND bukrs = cocovar.                           "#EC NEEDED
  ENDSELECT.

  IF sy-subrc <> 0.
    PERFORM log_almsg_var  USING 'TFC0' 'E' '710' cocovar '' '' ''.
  ENDIF.

ENDFORM.                    "SELECTION_CHECK_1


* -------------------------------------------------------------------- *
* FORM  SELECTION_CHECK_2
* -------------------------------------------------------------------- *
FORM selection_check_2.

  IF fyear < 1995 OR fyear > 2099.
    PERFORM log_almsg_var  USING 'TFC0' 'E' '711' '' '' '' ''.
  ENDIF.

ENDFORM.                    "SELECTION_CHECK_2


* -------------------------------------------------------------------- *
* FORM  SELECTION_CHECK_3_1
* -------------------------------------------------------------------- *
FORM selection_check_3_1.

  IF prd1_to EQ space.
    PERFORM log_almsg_var  USING 'TFC0' 'E' '712' '' '' '' ''.
  ENDIF.
  IF prd1_to < 1 OR prd1_to > 99.
    PERFORM log_almsg_var  USING 'TFC0' 'E' '713' '' '' '' ''.
  ENDIF.

ENDFORM.                    "SELECTION_CHECK_3_1


* -------------------------------------------------------------------- *
* FORM  SELECTION_CHECK_3_2
* -------------------------------------------------------------------- *
FORM selection_check_3_2 USING prd_to LIKE prd2_to.

  IF prd_to <> space.
    IF prd_to < 1 OR prd_to > 99.
      PERFORM log_almsg_var  USING 'TFC0' 'E' '713' '' '' '' ''.
    ENDIF.
  ENDIF.

ENDFORM.                    "SELECTION_CHECK_3_2


* -------------------------------------------------------------------- *
* FORM  SELECTION_CHECK_4
* -------------------------------------------------------------------- *
FORM selection_check_4.

  DATA: it_dd07l LIKE dd07l OCCURS 1.
  DATA: wa_dd07l LIKE dd07l.
  DATA: save_subrc LIKE sy-subrc.

  SELECT * FROM dd07l INTO TABLE it_dd07l
           WHERE domname = 'MKOAR'.

  IF sy-subrc EQ 0.
    save_subrc = 1.
    LOOP AT it_dd07l INTO wa_dd07l.
      IF wa_dd07l-domvalue_l EQ actyp.
        save_subrc = 0.
      ENDIF.
    ENDLOOP.

    IF save_subrc <> 0.
      PERFORM log_almsg_var  USING 'TFC0' 'E' '714' '' '' '' ''.
    ENDIF.
  ELSE.
    PERFORM log_almsg_var  USING 'TFC0' 'E' '715' actyp '' '' ''.
  ENDIF.

ENDFORM.                    "SELECTION_CHECK_4


* -------------------------------------------------------------------- *
* FORM  CHECK_AUTHORITY
* -------------------------------------------------------------------- *
FORM check_authority.

  CALL FUNCTION 'VIEW_AUTHORITY_CHECK'
    EXPORTING
      view_action                    = 'U'
      view_name                      = 'V_T001B'
    EXCEPTIONS
      table_not_found                = 5
      no_authority                   = 8
      no_clientindependent_authority = 9
      no_linedependent_authority     = 10.

  CASE sy-subrc.
    WHEN 0.
    WHEN 5.
      PERFORM log_almsg_var  USING 'TFC0' 'E' '716' t001b '' '' ''.
    WHEN 9.
      PERFORM log_almsg_var  USING 'TFC0' 'E' '717' '' '' '' ''.
    WHEN 8.
      PERFORM log_almsg_var  USING 'TFC0' 'E' '718' '' '' '' ''.
    WHEN 10.
      PERFORM log_almsg_var  USING 'TFC0' 'E' '719' '' '' '' ''.
  ENDCASE.

ENDFORM.                    "CHECK_AUTHORITY


* -------------------------------------------------------------------- *
* FORM  DETERMINE_OPEN_RANGE
* -------------------------------------------------------------------- *
FORM determine_open_range.

  DATA: it_t001b     LIKE t001b OCCURS 100.
  DATA: wa_t001b_old LIKE t001b.
  DATA: wa_t001b_new LIKE t001b.
  DATA: wa_t001      LIKE t001.
  DATA: it_t001      LIKE t001  OCCURS 100.
  DATA: wa_t009      LIKE t009.
  DATA: p_periv_old  LIKE t001-periv VALUE ' '.
  DATA: p_anzp1      LIKE t009-anzbp.
  DATA: p_anzp2      LIKE t009-anzsp.

* check if some error exists
  CHECK g_e_msg IS INITIAL.

* Get customizing data
  SELECT * FROM t001b INTO TABLE it_t001b
          WHERE rrcty = rectyp
            AND bukrs = cocovar
            AND mkoar = actyp
            AND bkont = bkont                               "New
            AND vkont = vkont.                              "New

  IF sy-subrc EQ 0.

    SELECT * FROM t001 INTO TABLE it_t001
            WHERE opvar = cocovar.

    IF sy-subrc <> 0.
      PERFORM log_almsg_var  USING 'TFC0' 'E' '735' t001 '' '' ''.
      RETURN.
    ENDIF.

* Find appropriate Period Limits from suitable Fiscal Year Variant
    LOOP AT it_t001 INTO wa_t001.
      IF wa_t001-periv <> p_periv_old.

        SELECT SINGLE * FROM t009 INTO wa_t009
            WHERE periv = wa_t001-periv.

* Restriction: If Company Codes with different Fiscal Year Variants
*              were assigned to the same Company Code Variant, choose
*              Fiscal Year Variant with largest number of posting
*              periods
        IF wa_t009-anzbp > p_anzp1.
          p_anzp1 = wa_t009-anzbp.
          p_anzp2 = wa_t009-anzbp + wa_t009-anzsp.
        ENDIF.

        p_periv_old = wa_t001-periv.
      ENDIF.

    ENDLOOP.

    IF p_anzp1 IS INITIAL.
      PERFORM log_almsg_var  USING 'TFC0' 'E' '735' t009 '' '' ''.
      RETURN.
    ELSE.
      PERFORM log_almsg_var  USING 'TFC0' 'S' '736' p_periv_old '' '' ''.
    ENDIF.


    LOOP AT it_t001b INTO wa_t001b_old.

      wa_t001b_new = wa_t001b_old.

* Check Period 1

*  Period_From
      wa_t001b_new-frpe1 = prd1_to + 1.
      wa_t001b_new-frye1 = fyear.

      IF wa_t001b_new-frpe1 > p_anzp1.
        wa_t001b_new-frpe1 = 1.
        ADD 1 TO wa_t001b_new-frye1.
        PERFORM log_almsg_var  USING 'TFC0' 'S' '739'
                wa_t001b_new-frpe1 wa_t001b_new-frye1 '1' ''.
      ENDIF.

*  Period_To
      IF     ( wa_t001b_old-toye1 > wa_t001b_new-frye1 ).
*       previous entry can be used
      ELSEIF ( wa_t001b_old-toye1 = wa_t001b_new-frye1 ).
        IF ( wa_t001b_old-tope1 <  wa_t001b_new-frpe1 ).
          wa_t001b_new-tope1 = wa_t001b_new-frpe1.
          PERFORM log_almsg_var  USING 'TFC0' 'S' '740'
                  wa_t001b_new-tope1 wa_t001b_new-toye1 '1' ''.
        ENDIF.
      ELSEIF ( wa_t001b_old-toye1 < wa_t001b_new-frye1 ).
        wa_t001b_new-tope1 = wa_t001b_new-frpe1.
        wa_t001b_new-toye1 = wa_t001b_new-frye1.
        PERFORM log_almsg_var  USING 'TFC0' 'S' '740'
                wa_t001b_new-tope1 wa_t001b_new-toye1 '1' ''.
      ENDIF.


* Check Period 2

*  Period_From
      IF NOT prd2_to IS INITIAL.
        wa_t001b_new-frpe2 = prd2_to + 1.
        wa_t001b_new-frye2 = fyear.

        IF wa_t001b_new-frpe2 > p_anzp2.
          wa_t001b_new-frpe2 = p_anzp1 + 1.
          ADD 1 TO wa_t001b_new-frye2.
          PERFORM log_almsg_var  USING 'TFC0' 'S' '739'
                  wa_t001b_new-frpe2 wa_t001b_new-frye2 '2' ''.
        ENDIF.

*  Period_To
        IF     ( wa_t001b_old-toye2 > wa_t001b_new-frye2 ).
*       previous entry can be used
        ELSEIF ( wa_t001b_old-toye2 = wa_t001b_new-frye2 ).
          IF ( wa_t001b_old-tope2 <  wa_t001b_new-frpe2 ).
            wa_t001b_new-tope2 = wa_t001b_new-frpe2.
            PERFORM log_almsg_var  USING 'TFC0' 'S' '740'
                    wa_t001b_new-tope2 wa_t001b_new-toye2 '2' ''.
          ENDIF.
        ELSEIF ( wa_t001b_old-toye2 < wa_t001b_new-frye2 ).
          wa_t001b_new-tope2 = wa_t001b_new-frpe2.
          wa_t001b_new-toye2 = wa_t001b_new-frye2.
          PERFORM log_almsg_var  USING 'TFC0' 'S' '740'
                  wa_t001b_new-tope2 wa_t001b_new-toye2 '2' ''.
        ENDIF.
      ENDIF.

* Check Period 3
      IF actyp = '+'.
*  Period_From
        IF NOT prd3_to IS INITIAL.
          wa_t001b_new-frpe3 = prd3_to + 1.
          wa_t001b_new-frye3 = fyear.

          IF wa_t001b_new-frpe3 > p_anzp1.
            wa_t001b_new-frpe3 =  1.
            ADD 1 TO wa_t001b_new-frye3.
            PERFORM log_almsg_var  USING 'TFC0' 'S' '739'
                    wa_t001b_new-frpe3 wa_t001b_new-frye3 '3' ''.
          ENDIF.

*  Period_To
          IF     ( wa_t001b_old-toye3 > wa_t001b_new-frye3 ).
*       previous entry can be used
          ELSEIF ( wa_t001b_old-toye3 = wa_t001b_new-frye3 ).
            IF ( wa_t001b_old-tope3 <  wa_t001b_new-frpe3 ).
              wa_t001b_new-tope3 = wa_t001b_new-frpe3.
              PERFORM log_almsg_var  USING 'TFC0' 'S' '740'
                      wa_t001b_new-tope3 wa_t001b_new-toye3 '3' ''.
            ENDIF.
          ELSEIF ( wa_t001b_old-toye3 < wa_t001b_new-frye3 ).
            wa_t001b_new-tope3 = wa_t001b_new-frpe3.
            wa_t001b_new-toye3 = wa_t001b_new-frye3.
            PERFORM log_almsg_var  USING 'TFC0' 'S' '740'
                    wa_t001b_new-tope3 wa_t001b_new-toye3 '3' ''.
          ENDIF.
        ENDIF.
      ELSE.
        IF 1 = 2. MESSAGE s360(fc). ENDIF.
        PERFORM log_almsg_var  USING 'FC' 'S' '360'
        '' '' '' ''.
      ENDIF.


* Update
      UPDATE t001b FROM wa_t001b_new.
      IF sy-subrc EQ 0.
        PERFORM log_almsg_var  USING 'TFC0' 'S' '720' '' '' '' ''.
      ELSE.
        PERFORM log_almsg_var  USING 'TFC0' 'E' '721' t001b
                wa_t001b_new-rrcty wa_t001b_new-bukrs wa_t001b_new-mkoar.
        RETURN.
      ENDIF.

    ENDLOOP.

  ELSE.

    PERFORM log_almsg_var  USING 'TFC0' 'S' '722' '' '' '' ''.
  ENDIF.

ENDFORM.                    "DETERMINE_OPEN_RANGE

*Text symbol text£º

*001:Close Periods
*Selection text£º
*ACTYP:D       .
*BKONT:D       .
*COCOVAR:D       .
*FYEAR:D       .
*PRD1_TO:D       .
*PRD2_TO:D       .
*PRD3_TO:        To Period (Interval 3)
*VKONT:D       .
