REPORT yse_downl_equipments MESSAGE-ID yam_inf.

************************************************************************
*                   T A B L E S                                        *
************************************************************************
TABLES: v_equi,
        ihpa,
        kna1.

************************************************************************
*                   C O N S T A N T S                                  *
************************************************************************
CONSTANTS : c_equip(5)    TYPE c VALUE 'EQUIP',
            c_99991231(8) TYPE c VALUE '99991231',
            c_underscore  TYPE c value '_'.

************************************************************************
*                   V A R I A B L E S                                  *
************************************************************************
DATA: g_ofile  LIKE      /sapdmc/lsoinp-filename,
      g_directory(25) TYPE c VALUE '/var/load/xxx/UK/convert/'.

************************************************************************
*                  I N T E R N A L   T A B L E S                       *
************************************************************************
DATA : BEGIN OF i_equi OCCURS 0,
          equnr LIKE v_equi-equnr,
          eqktx LIKE v_equi-eqktx,
          eqart LIKE v_equi-eqart,
          inbdt LIKE v_equi-inbdt,
          ansdt LIKE v_equi-ansdt,
          swerk LIKE v_equi-swerk,
          iwerk LIKE v_equi-iwerk,
          ingrp LIKE v_equi-ingrp,
          gewrk LIKE v_equi-gewrk,
          submt LIKE v_equi-submt,
          vkbur LIKE v_equi-vkbur,
          vkgrp LIKE v_equi-vkgrp,
          matnr LIKE v_equi-matnr,
          sernr LIKE v_equi-sernr,
          hequi LIKE v_equi-hequi,
          objnr LIKE v_equi-objnr,
          parvw LIKE ihpa-parvw,
          parnr LIKE ihpa-parnr,
          land1 LIKE kna1-land1,
          name1 LIKE kna1-name1,
          name2 LIKE kna1-name2,
          ort01 LIKE kna1-ort01,
          pstlz LIKE kna1-pstlz,
          ktokd LIKE kna1-ktokd,
          stras LIKE kna1-stras,
          telf1 LIKE kna1-telf1,
       END OF i_equi.

DATA: p_logsys    LIKE tbdlst-logsys,
      g_ersda(12) type c.                      " YYYYMMDDHHMM


************************************************************************
*       S E L E C T - O P T I O N S / P A R A M E T E R S              *
************************************************************************

SELECTION-SCREEN : BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.

SELECT-OPTIONS: s_equnr FOR v_equi-equnr,
                s_swerk FOR v_equi-swerk,
                s_eqart FOR v_equi-eqart,
                s_ingrp FOR v_equi-ingrp.
SELECTION-SCREEN: END OF BLOCK b1.


************************************************************************
INITIALIZATION.

  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = p_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.

  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
* ELSE.
*   REPLACE FIRST OCCURRENCE OF c_clnt IN p_logsys WITH c_wbi  .
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

  PERFORM get_detail.

  IF NOT i_equi[] IS INITIAL.
*   write outputfile to application server
    CONCATENATE sy-datum sy-uzeit(4) INTO g_ersda.
    CONCATENATE c_equip g_ersda
              INTO g_ofile SEPARATED BY c_underscore.
    REPLACE 'xxx' IN g_directory WITH p_logsys(3).
    CONCATENATE g_directory g_ofile INTO g_ofile.

    PERFORM write_outputfile.
  ENDIF.


************************************************************************
*       E N D - O F - S E L E C T I O N    E V E N T                   *
************************************************************************
END-OF-SELECTION .

  WRITE: / 'Job finished'(i02).


*--- S U B R O U T I N E S --------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  Get_Detail
*&---------------------------------------------------------------------*
*       To get the information for all or the selected plant(s)
*----------------------------------------------------------------------*
FORM get_detail.

  CLEAR i_equi.
  REFRESH i_equi.

** Select data based on Selection screen parameters
  SELECT a~equnr a~eqart a~inbdt a~ansdt a~iwerk a~ingrp a~submt a~vkbur a~vkgrp
         a~sernr a~matnr a~eqktx a~swerk a~objnr a~gewrk a~hequi
         b~parvw b~parnr
         c~land1 c~name1 c~name2 c~ort01 c~pstlz c~ktokd c~stras c~telf1
           INTO CORRESPONDING FIELDS OF TABLE i_equi
           FROM v_equi AS a
    INNER JOIN ihpa AS b
    ON b~objnr = a~objnr
    INNER JOIN kna1 AS c
    ON c~kunnr = b~parnr
           WHERE a~equnr    IN s_equnr
             AND a~eqart    IN s_eqart
             AND a~ingrp    IN s_ingrp
             AND a~spras    EQ sy-langu
             AND a~datbi    EQ c_99991231
             AND a~swerk    IN s_swerk
             AND b~kzloesch NE 'X'
             AND b~obtyp    EQ 'IEQ'.

** Process Error - No data found
  IF sy-subrc NE 0 .
    WRITE: text-e01.
    EXIT.
  ENDIF .

ENDFORM.                    " Get_Detail

*&---------------------------------------------------------------------*
*&      Form  Write_outputfile
*&---------------------------------------------------------------------*
*       Write outputfile
*----------------------------------------------------------------------*
FORM write_outputfile.

  OPEN DATASET g_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, g_ofile.
*   message e011(YAM_DC).                      "Open dataset failed
    EXIT.
  ENDIF.

  SORT i_equi BY equnr.

  LOOP AT i_equi.
    TRANSFER i_equi TO g_ofile.
  ENDLOOP.

ENDFORM.                    " Write_outputfile

*Text symbol text£º
*001:Selection Screen Input
*E01:No data available
*E03:Open dataset failed for :

*I02:Job finished
*Selection text£º
*S_EQART:D       .
*S_EQUNR:D       .
*S_INGRP:D       .
*S_SWERK:D       .
