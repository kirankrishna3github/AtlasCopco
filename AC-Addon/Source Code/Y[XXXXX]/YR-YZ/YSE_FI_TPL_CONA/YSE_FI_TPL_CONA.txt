*----------------------------------------------------------------------*
* PROGRAM ID    : YSE_FI_TPL_CONA                                      *
* PROGRAM TITLE : P&L report CONA                                       *
* AUTHOR        : Yang Lei                                             *
* DATE          : 08/06/2017                                           *
* DEVELOPMENT ID:                                                      *
*                                                                      *
* CHANGE REQUEST NUMBER:                                               *
*                                                                      *
* Program Description:                                                 *
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
*  DATE    | NAME     |TRANSPORT  | CR# and short Description          *
*----------------------------------------------------------------------*

REPORT  YSE_FI_TPL_CONA NO STANDARD PAGE HEADING LINE-SIZE 255.

************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES:
  BKPF.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:
  BEGIN OF ty_bkpf,
    bukrs      TYPE bkpf-bukrs, "Company Code
    belnr      TYPE bkpf-belnr, "Accounting Document Number
    gjahr      TYPE bkpf-gjahr, "Fiscal Year
    blart      TYPE bkpf-blart, "Document Type
    budat      TYPE bkpf-budat, "Posting Date in the Document
  END OF ty_bkpf,

  BEGIN OF ty_bseg,
    bukrs      TYPE bseg-bukrs, "Company Code
    belnr      TYPE bseg-belnr, "Accounting Document Number
    gjahr      TYPE bseg-gjahr, "Fiscal Year
    buzei      TYPE bseg-buzei, "Line Number
    hkont      TYPE bseg-hkont, "General Ledger Account
    bschl      TYPE bseg-bschl, "Posting Key
    shkzg      TYPE bseg-shkzg, "Debit/Credit Indicator
    gvtyp      TYPE bseg-gvtyp, "P&L account
    dmbtr      TYPE bseg-dmbtr, "Amount in Local Currency
    wrbtr      TYPE bseg-wrbtr, "Amount in document currency
    fkber_long TYPE bseg-fkber_long, "Functional Area
    segment    TYPE bseg-segment,    "Segment for Segmental Reporting
    xref2      TYPE bseg-xref2, "Business partner reference key
  END OF ty_bseg,

  BEGIN OF ty_map,
    fkber_long TYPE bseg-fkber_long, "Functional Area
    segment    TYPE bseg-segment,    "Segment for Segmental Reporting
    xref2      TYPE bseg-xref2, "Business partner reference key
    dmbtr      TYPE bseg-dmbtr, "Amount in Local Currency
  END OF ty_map.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_alv  TYPE yse_fi_tpl_cona,
  gs_bkpf TYPE ty_bkpf,
  gs_bseg TYPE ty_bseg,
  gs_map  TYPE ty_map,
  gs_layout TYPE slis_layout_alv,
  gs_disvar TYPE disvariant.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_alv  TYPE STANDARD TABLE OF yse_fi_tpl_cona,
  gt_bkpf TYPE STANDARD TABLE OF ty_bkpf,
  gt_bseg TYPE STANDARD TABLE OF ty_bseg,
  gt_map  TYPE STANDARD TABLE OF ty_map,
  gt_fieldcat TYPE slis_t_fieldcat_alv.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_col(10) TYPE n,
  gv_save    TYPE c.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
*CONSTANTS:

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.
  PARAMETERS p_bukrs TYPE bukrs DEFAULT 'CONA'.
  SELECT-OPTIONS s_budat FOR bkpf-budat.
  PARAMETERS p_chk   TYPE c AS CHECKBOX.
SELECTION-SCREEN END OF BLOCK b1.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN OUTPUT                                          *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.
* Set selection screen
  PERFORM set_screen.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* Get data
  PERFORM get_data.
* Edit data
  PERFORM edit_data.
* Update table
  PERFORM update_table.
* FIELDCAT set
  PERFORM alv_fieldcat_set.
* LAYOUT set
  PERFORM alv_layout_set.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
* ALV display
  PERFORM alv_display.

*&---------------------------------------------------------------------*
*&      Form  SET_SCREEN
*&---------------------------------------------------------------------*
*       Set selection screen
*----------------------------------------------------------------------*
FORM SET_SCREEN .

  LOOP AT SCREEN.
    IF screen-name = 'P_BUKRS'.
      screen-input = 0.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " SET_SCREEN
*&---------------------------------------------------------------------*
*&      Form  GET_DATA
*&---------------------------------------------------------------------*
*       Get data
*----------------------------------------------------------------------*
FORM GET_DATA .

  SELECT *
    FROM yse_fi_tpl_cona
    INTO TABLE gt_alv.
  IF sy-subrc <> 0.
    MESSAGE s001(00) WITH text-001 DISPLAY LIKE 'E'.
*   No data found!
    LEAVE LIST-PROCESSING.
  ELSE.
    SORT gt_alv
      BY zzfuncarea ASCENDING.
  ENDIF.

  IF p_chk = 'X'.
    SELECT bukrs
           belnr
           gjahr
           blart
           budat
      FROM bkpf
      INTO TABLE gt_bkpf
     WHERE bukrs =  p_bukrs
       AND budat IN s_budat.
    IF sy-subrc <> 0.
      MESSAGE s001(00) WITH text-001 DISPLAY LIKE 'E'.
*     No data found!
      LEAVE LIST-PROCESSING.
    ENDIF.

    SELECT bukrs
           belnr
           gjahr
           buzei
           hkont
           bschl
           shkzg
           gvtyp
           dmbtr
           wrbtr
           fkber_long
           segment
           xref2
      FROM bseg
      INTO TABLE gt_bseg
       FOR ALL ENTRIES IN gt_bkpf
     WHERE bukrs = gt_bkpf-bukrs
       AND belnr = gt_bkpf-belnr
       AND gjahr = gt_bkpf-gjahr.
  ENDIF.

ENDFORM.                    " GET_DATA
*&---------------------------------------------------------------------*
*&      Form  EDIT_DATA
*&---------------------------------------------------------------------*
*       Edit data
*----------------------------------------------------------------------*
FORM EDIT_DATA .
  DATA:
    lv_dmbtr TYPE bseg-dmbtr,
    lt_map1  TYPE STANDARD TABLE OF ty_map,
    lt_map2  TYPE STANDARD TABLE OF ty_map,
    lt_alv   TYPE STANDARD TABLE OF yse_fi_tpl_cona,
    ls_alv   TYPE yse_fi_tpl_cona.

  CLEAR gt_map.

  IF p_chk = 'X'.
    LOOP AT gt_bseg INTO gs_bseg
                   WHERE gvtyp = 'X'
                     AND ( segment = '0000002710'
                        OR segment = '0000002740' ).

      READ TABLE gt_alv TRANSPORTING NO FIELDS
            WITH KEY zzfuncarea = gs_bseg-fkber_long
            BINARY SEARCH.
      IF sy-subrc <> 0.
        CONTINUE.
      ENDIF.

      MOVE-CORRESPONDING gs_bseg TO gs_map.
      IF gs_bseg-shkzg = 'S'.
        gs_map-dmbtr = gs_map-dmbtr * -1.
      ENDIF.

      APPEND gs_map TO gt_map.
    ENDLOOP.

*   Group by FKBER_LONG + SEGMENT
    SORT gt_map
      BY fkber_long ASCENDING
         segment    ASCENDING.

    LOOP AT gt_map INTO gs_map.
      lv_dmbtr = lv_dmbtr + gs_map-dmbtr.
      AT END OF segment.
        gs_map-dmbtr = lv_dmbtr.
        APPEND gs_map TO lt_map1.
        CLEAR lv_dmbtr.
      ENDAT.
    ENDLOOP.

    SORT lt_map1
      BY fkber_long ASCENDING
         segment    ASCENDING.

*   Group by FKBER_LONG + SEGMENT + XREF2
    SORT gt_map
      BY fkber_long ASCENDING
         segment    ASCENDING
         xref2      ASCENDING.

    LOOP AT gt_map INTO gs_map.
      lv_dmbtr = lv_dmbtr + gs_map-dmbtr.
      AT END OF xref2.
        gs_map-dmbtr = lv_dmbtr.
        APPEND gs_map TO lt_map2.
        CLEAR lv_dmbtr.
      ENDAT.
    ENDLOOP.

    SORT lt_map2
      BY fkber_long ASCENDING
         segment    ASCENDING
         xref2      ASCENDING.

*   Get target data
    lt_alv = gt_alv.
    CLEAR gt_alv.

    LOOP AT lt_alv INTO ls_alv.
*     2740-total
      READ TABLE lt_map1 INTO gs_map
            WITH KEY fkber_long = ls_alv-zzfuncarea
                     segment    = '0000002740'
            BINARY SEARCH.
      IF sy-subrc = 0.
        ls_alv-zzrcetotal = gs_map-dmbtr.
      ELSE.
        ls_alv-zzrcetotal = 0.
      ENDIF.

*     2710-total
      READ TABLE lt_map1 INTO gs_map
            WITH KEY fkber_long = ls_alv-zzfuncarea
                     segment    = '0000002710'
            BINARY SEARCH.
      IF sy-subrc = 0.
        ls_alv-zzrcitotal = gs_map-dmbtr.
      ELSE.
        ls_alv-zzrcitotal = 0.
      ENDIF.

*     2740-CON
      READ TABLE lt_map2 INTO gs_map
            WITH KEY fkber_long = ls_alv-zzfuncarea
                     segment    = '0000002740'
                     xref2      = 'CON'
            BINARY SEARCH.
      IF sy-subrc = 0.
        ls_alv-zzrcecon = gs_map-dmbtr.
      ELSE.
        ls_alv-zzrcecon = 0.
      ENDIF.

*     2710-CON
      READ TABLE lt_map2 INTO gs_map
            WITH KEY fkber_long = ls_alv-zzfuncarea
                     segment    = '0000002710'
                     xref2      = 'CON'
            BINARY SEARCH.
      IF sy-subrc = 0.
        ls_alv-zzrcicon = gs_map-dmbtr.
      ELSE.
        ls_alv-zzrcicon = 0.
      ENDIF.

*     2740-XHA
      READ TABLE lt_map2 INTO gs_map
            WITH KEY fkber_long = ls_alv-zzfuncarea
                     segment    = '0000002740'
                     xref2      = 'XHA'
            BINARY SEARCH.
      IF sy-subrc = 0.
        ls_alv-zzrcexha = gs_map-dmbtr.
      ELSE.
        ls_alv-zzrcexha = 0.
      ENDIF.

*     2710-XHA
      READ TABLE lt_map2 INTO gs_map
            WITH KEY fkber_long = ls_alv-zzfuncarea
                     segment    = '0000002710'
                     xref2      = 'XHA'
            BINARY SEARCH.
      IF sy-subrc = 0.
        ls_alv-zzrcixha = gs_map-dmbtr.
      ELSE.
        ls_alv-zzrcixha = 0.
      ENDIF.

      APPEND ls_alv TO gt_alv.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " EDIT_DATA
*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM ALV_FIELDCAT_SET .

  CLEAR:
    gv_col,
    gt_fieldcat.

*   Field definitions
    PERFORM add_field USING:
       'I_SERIAL'   10 'SER No.'    'SER No.',
       'ZZFAMLINE'  4  'PL Line '   'PL Line ',
       'ZZFUNCAREA' 4  'FAM Line'   'FAM Line',
       'ZZRCETOTAL' 1  '2740-Total' '2740-Total',
       'ZZRCITOTAL' 10 '2710-Total' '2710-Total',
       'ZZRCECON'   6  '2740-CON'   '2740-CON',
       'ZZRCICON'   13 '2710-CON'   '2710-CON',
       'ZZRCEXHA'   13 '2740-XHA'   '2740-XHA',
       'ZZRCIXHA'   11 '2710-XHA'   '2710-XHA'.

ENDFORM.                    " ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr_s
                         p_descr_l.
  DATA:
     ls_fieldcat TYPE slis_fieldcat_alv.

  gv_col = gv_col + 1.

  CLEAR ls_fieldcat.
  ls_fieldcat-col_pos    = gv_col.
  ls_fieldcat-fieldname  = p_field.
  ls_fieldcat-outputlen  = p_len.
  ls_fieldcat-seltext_l  = p_descr_l.
  ls_fieldcat-seltext_m  = p_descr_l.
  ls_fieldcat-seltext_s  = p_descr_s.
  ls_fieldcat-fix_column = 'X'.
  ls_fieldcat-emphasize  = 'X'.

  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ADD_FIELD
*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM ALV_LAYOUT_SET .

  CLEAR: gs_layout.
  gs_layout-zebra = 'X'.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = 'X'.   "Auto optimize column width
  gs_layout-detail_popup = 'X'.        "Show detail screen

ENDFORM.                    " ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*&      Form  UPDATE_TABLE
*&---------------------------------------------------------------------*
*       Update table
*----------------------------------------------------------------------*
FORM UPDATE_TABLE .

  UPDATE yse_fi_tpl_cona FROM TABLE gt_alv.
  IF sy-subrc <> 0.
    ROLLBACK WORK.
    MESSAGE 'Updata Error!' TYPE 'S' DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
  ENDIF.

  COMMIT WORK AND WAIT.

ENDFORM.                    " UPDATE_TABLE
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM ALV_DISPLAY .

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      is_layout                = gs_layout
      it_fieldcat              = gt_fieldcat
      i_save                   = gv_save
      is_variant               = gs_disvar
    TABLES
      t_outtab                 = gt_alv
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE 'S' NUMBER sy-msgno
          WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
          DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*Text symbol text£º
*001:No data found!

*T01:Input
*Selection text£º
*P_BUKRS:        Company code
*P_CHK:        Real time refresh
*S_BUDAT:        Posting Date
