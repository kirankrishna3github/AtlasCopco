*----------------------------------------------------------------------
* PROGRAM ID           : YSE_I011_UPDATE_MWST                          *
* PROGRAM TITLE        : Supersession handling - YSE_I011              *
*                            MWST handling                             *
* AUTHOR               : Marc Jacobs                                   *
* DATE                 : 03/08/2009                                    *
* DEVELOPMENT ID       :                                               *
* CHANGE REQUEST NUMBER: CR0970                                        *
* PROGRAM DESCRIPTION  : Correction MWST (create or update MLAN)       *
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-xxx |          |          |                 |                    *
*----------------------------------------------------------------------*

REPORT  yse_i011_update_mwst      NO STANDARD PAGE HEADING
                                  LINE-SIZE 150
                                  LINE-COUNT 80.

*- Tables -------------------------------------------------------------*
TABLES: mara, mvke.                   "Sales Data for Material
*- Internal tables
*----------------------------------------------------------------------*
* Control report data
DATA: BEGIN OF g_output OCCURS 0,
        matnr LIKE mara-matnr,
        vkorg LIKE mvke-vkorg,
        aland LIKE mlan-aland,
        errflg(1),
        text(100).
DATA: END OF g_output.

DATA: BEGIN OF i_mvke OCCURS 0.
        INCLUDE STRUCTURE mvke.
DATA: END   OF i_mvke.

* General Data for Material
DATA: BEGIN OF gt_mara OCCURS 0.
        INCLUDE STRUCTURE mara_ueb.
DATA: END   OF gt_mara.

* Sales Data for Material
DATA: BEGIN OF gt_mvke OCCURS 0.
        INCLUDE STRUCTURE mvke_ueb.
DATA: END   OF gt_mvke.

* Tax clasification for Material
DATA: BEGIN OF gt_mlan OCCURS 0.
        INCLUDE STRUCTURE steu_ueb.
DATA: END   OF gt_mlan.

* Messages
DATA: BEGIN OF gt_messtab OCCURS 0.
        INCLUDE STRUCTURE merrdat.
DATA: END   OF gt_messtab.


*- Variables ----------------------------------------------------------*
DATA: gv_bukrs    TYPE bukrs,
      gv_land1    TYPE land1,
      wa_mlan     TYPE mlan,
      g_subrc     TYPE subrc,
      g_errors TYPE bierrnum,
      g_msg_no TYPE msgnr,
      g_cnt_tranc  TYPE i VALUE 1,
      g_cnt_d_ind  TYPE i VALUE 1.


*- Constants ----------------------------------------------------------*
CONSTANTS: c_11    LIKE mvke-vkorg VALUE '11',    " Distr. channel
           c_mwst  TYPE tatyp      VALUE 'MWST',  " Tax category
           c_mm02  LIKE sy-tcode   VALUE 'MM02'.  " Transaction code
*- Select-options / Parameters ----------------------------------------*
SELECT-OPTIONS: s_matnr FOR mara-matnr OBLIGATORY,
                s_vkorg FOR mvke-vkorg.
PARAMETERS : p_check AS CHECKBOX.
*- Selection-screen ---------------------------------------------------*
*- Start-of-selection -------------------------------------------------*
START-OF-SELECTION.

  SELECT * FROM mvke
      INTO TABLE i_mvke
      WHERE matnr IN s_matnr
        AND vkorg IN s_vkorg
        AND vtweg = c_11.

  IF i_mvke[] IS INITIAL.
    WRITE: / text-e10.
  ENDIF.

*- End-of-selection ---------------------------------------------------*
END-OF-SELECTION.

  SORT:  i_mvke .

  LOOP AT i_mvke.
    CLEAR: gv_bukrs, gv_land1, wa_mlan.
    SELECT SINGLE bukrs INTO gv_bukrs FROM
      tvko WHERE vkorg EQ i_mvke-vkorg.
    SELECT SINGLE land1 INTO gv_land1 FROM
      t001 WHERE bukrs = gv_bukrs.
    IF NOT gv_land1 IS INITIAL.
      SELECT SINGLE * INTO wa_mlan FROM mlan
        WHERE matnr = i_mvke-matnr
          AND aland = gv_land1.
      IF sy-subrc <> 0.
        IF p_check = 'X'.
          CLEAR g_output.
          MOVE i_mvke-matnr TO g_output-matnr.
          MOVE i_mvke-vkorg TO g_output-vkorg.
          MOVE gv_land1 TO g_output-aland.
          MOVE text-e07 TO g_output-text.
          APPEND g_output.
        ELSE.
          PERFORM update_mlan.
        ENDIF.
      ELSE.
        IF wa_mlan-taxm1 IS INITIAL OR
           ( wa_mlan-taxm1 = 0 AND wa_mlan-aland <> 'HK' ) OR
           ( wa_mlan-taxm1 = 1 AND wa_mlan-aland = 'HK' ).
          IF p_check = 'X'.
            CLEAR g_output.
            MOVE i_mvke-matnr TO g_output-matnr.
            MOVE i_mvke-vkorg TO g_output-vkorg.
            MOVE gv_land1 TO g_output-aland.
            IF wa_mlan-taxm1 IS INITIAL.
              MOVE text-e05 TO g_output-text.
            ELSE.
              MOVE text-e06 TO g_output-text.
            ENDIF.
            APPEND g_output.
          ELSE.
            PERFORM update_mlan.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDLOOP.

  PERFORM write_output.
*&---------------------------------------------------------------------*
*&      Form  update_mlan
*&---------------------------------------------------------------------*
*       Update MLAN-taxm1 if the field is initial
*----------------------------------------------------------------------*
FORM update_mlan.

* General Material Data
  gt_mara-mandt = sy-mandt.
  gt_mara-tcode = c_mm02.
  gt_mara-tranc = g_cnt_tranc.
  gt_mara-d_ind = g_cnt_d_ind.

  gt_mara-matnr = i_mvke-matnr.
  APPEND gt_mara.

* Sales Data for Material
  gt_mvke-mandt = sy-mandt.
  gt_mvke-tranc = g_cnt_tranc.
  gt_mvke-d_ind = g_cnt_d_ind.
  MOVE-CORRESPONDING i_mvke TO gt_mvke.
  APPEND gt_mvke.

* Tax classification for Material
  gt_mlan-mandt = sy-mandt.
  gt_mlan-tranc = g_cnt_tranc.
  gt_mlan-d_ind = g_cnt_d_ind.

  gt_mlan-matnr = i_mvke-matnr.
  gt_mlan-aland = gv_land1.
  gt_mlan-tatyp = c_mwst.
  IF gv_land1 <> 'HK '.
    gt_mlan-taxkm = '1'.
  ELSE.
    gt_mlan-taxkm = '0'.
  ENDIF.
  APPEND gt_mlan.

  PERFORM change_material USING gt_mvke-matnr .

ENDFORM.                    "update_mlan


*&---------------------------------------------------------------------*
*&      Form  PREPARE_MESSAGE_TEXT
*&---------------------------------------------------------------------*
*       Format message with variables
*----------------------------------------------------------------------*
*  -->  P_MESSTAB  Messages table header line
*  <--  R_TEXT     Message text
*----------------------------------------------------------------------*
FORM prepare_message_text USING    p_messtab LIKE merrdat
                          CHANGING r_text.

  CLEAR r_text.
  g_msg_no = p_messtab-msgno.       "Type conflict!
  CALL FUNCTION 'MESSAGE_PREPARE'
    EXPORTING
      language               = sy-langu
      msg_id                 = p_messtab-msgid
      msg_no                 = g_msg_no
      msg_var1               = p_messtab-msgv1
      msg_var2               = p_messtab-msgv2
      msg_var3               = p_messtab-msgv3
      msg_var4               = p_messtab-msgv4
    IMPORTING
      msg_text               = r_text
    EXCEPTIONS
      function_not_completed = 1
      message_not_found      = 2
      OTHERS                 = 3.

  IF sy-subrc NE 0.
    CONCATENATE p_messtab-msgty p_messtab-msgid p_messtab-msgno
                INTO r_text SEPARATED BY space.
  ELSE.
    CONCATENATE p_messtab-msgty p_messtab-msgid p_messtab-msgno r_text
                INTO r_text SEPARATED BY space.
  ENDIF.

ENDFORM.                    " PREPARE_MESSAGE_TEXT

*&---------------------------------------------------------------------*
*&      Form  WRITE_OUTPUT
*&---------------------------------------------------------------------*
*       Output the control report
*----------------------------------------------------------------------*
FORM write_output.

  SORT g_output BY matnr.
* Output
  LOOP AT g_output.
    WRITE:/ g_output-matnr, g_output-vkorg , g_output-aland, g_output-text.
  ENDLOOP.

ENDFORM.                    " WRITE_OUTPUT
*&---------------------------------------------------------------------*
*&      Form  change_material
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->R_MATNR    text
*----------------------------------------------------------------------*
FORM change_material USING    r_matnr .

* Change Material
  CALL FUNCTION 'MATERIAL_MAINTAIN_DARK'
    EXPORTING
      p_kz_no_warn              = 'N'
      kz_prf                    = 'W'
    IMPORTING
      number_errors_transaction = g_errors
    TABLES
      amara_ueb                 = gt_mara
      amvke_ueb                 = gt_mvke
      asteu_ueb                 = gt_mlan
      amerrdat                  = gt_messtab
    EXCEPTIONS
      kstatus_empty             = 1
      tkstatus_empty            = 2
      t130m_error               = 3
      internal_error            = 4
      too_many_errors           = 5
      update_error              = 6
      OTHERS                    = 7.

  IF sy-subrc EQ 0.
    IF g_errors NE 0.
* Add messages to output table
      LOOP AT gt_messtab.
        CLEAR g_output.
        IF gt_messtab-msgty EQ 'E' OR gt_messtab-msgty EQ 'A'.
          MOVE 'X' TO g_output-errflg.
        ENDIF.
        MOVE r_matnr TO g_output-matnr.
        MOVE gt_mvke-vkorg TO g_output-vkorg.
        MOVE gt_mlan-aland TO g_output-aland.

        PERFORM prepare_message_text USING    gt_messtab
                                     CHANGING g_output-text.
        APPEND g_output.
      ENDLOOP.
    ELSE.
      COMMIT WORK AND WAIT.
      CLEAR g_output.
      MOVE r_matnr TO g_output-matnr.
      MOVE gt_mvke-vkorg TO g_output-vkorg.
      MOVE gt_mlan-aland TO g_output-aland.
      MOVE text-i01 TO g_output-text.
      APPEND g_output.
    ENDIF.
  ELSE.
    ROLLBACK WORK.
* Add message to output table
    CLEAR g_output.
    MOVE 'X'      TO g_output-errflg.
    MOVE r_matnr  TO g_output-matnr.
    MOVE gt_mvke-vkorg TO g_output-vkorg.
    MOVE gt_mlan-aland TO g_output-aland.
    MOVE text-e04 TO g_output-text.
    APPEND g_output.
  ENDIF.

* Refresh interface tables
  CLEAR: gt_mara, gt_mvke, gt_mlan, gt_messtab, g_errors.
  REFRESH: gt_mara, gt_mvke, gt_mlan, gt_messtab.

ENDFORM.                    "change_material

*Text symbol text£º
*E01:Enter a valid material number
*E04:Update material failed !!!
*E05:MWST not filled
*E06:Wrong MWST value
*E07:MLAN record missing
*E10:No materials selected

*I01:Corrected
*Selection text£º
*P_CHECK:        Test run
*S_MATNR:D       .
*S_VKORG:D       .
