*&---------------------------------------------------------------------*
*& Report  YSE_ACT_CONTRACT_HDR_STAT_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Contract Header Status extraction                            *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 10.10.2012
*  Change Request Number :
*  Transport request Nr. : CD1K973466
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                 | CD1K9.....    |             *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_act_contract_hdr_stat_extr.

TABLES: vbak,
        veda,
        vbuk,
        jest,
        tj02t,
        tj30t.

* Types
TYPES: BEGIN OF ty_vbak,
         vbeln      TYPE vbeln_va,
         objnr      TYPE objko,
         vkuegru    TYPE vkgru_veda,
         veindat    TYPE vedat_veda,
      END OF ty_vbak.

TYPES: BEGIN OF ty_jest,
         objnr      TYPE j_objnr,
         stat       TYPE j_status,
      END OF ty_jest.

TYPES: BEGIN OF ty_tj02t,
         istat      TYPE j_istat,
         txt04      TYPE j_txt04,
      END OF ty_tj02t.

TYPES: BEGIN OF ty_tj30t,
         estat      TYPE j_estat,
         txt04      TYPE j_txt04,
      END OF ty_tj30t.

* Internal tables
DATA: gt_vbak  TYPE STANDARD TABLE OF ty_vbak
                    WITH HEADER LINE.

DATA: gt_jest  TYPE SORTED TABLE OF ty_jest
                    WITH NON-UNIQUE KEY objnr
                    WITH HEADER LINE.

DATA: gt_tj02t TYPE HASHED TABLE OF ty_tj02t
                    WITH UNIQUE KEY istat
                    WITH HEADER LINE.

DATA: gt_tj30t TYPE HASHED TABLE OF ty_tj30t
                    WITH UNIQUE KEY estat
                    WITH HEADER LINE.

DATA: BEGIN OF gt_out  OCCURS 0,
        vbeln       TYPE vbeln_va,
        tab01(1)    TYPE c,
        stat        TYPE j_status,
        tab02(1)    TYPE c,
        txt04       TYPE j_txt04,
        tab03(1)    TYPE c,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        vbeln       TYPE fieldname,
        tab01(1)    TYPE c,
        stat        TYPE fieldname,
        tab02(1)    TYPE c,
        txt04       TYPE fieldname,
        tab03(1)    TYPE c,
      END OF gt_outh.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_ersda(14)     TYPE c,                      "YYYYMMDDHHMMSS
      gv_error         TYPE xfeld.

*CONSTANTS: gc_stat_dl  TYPE j_status  VALUE 'I0076'.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
*PARAMETERS: p_vkorg  LIKE vbak-vkorg  OBLIGATORY  MEMORY ID vko.
PARAMETERS: p_bukrs  TYPE aufk-bukrs OBLIGATORY MEMORY ID buk,
            p_vkorg  LIKE vbak-vkorg MEMORY ID vko.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_vkbur  FOR vbak-vkbur,
                s_vkgrp  FOR vbak-vkgrp,
                s_vbdat  FOR veda-vbegdat,
                s_vndat  FOR veda-venddat,
                s_auart  FOR vbak-auart,
                s_vbeln  FOR vbak-vbeln,
                s_gbstk  FOR vbuk-gbstk,
                s_abstk  FOR vbuk-abstk,
                s_fksak  FOR vbuk-fksak,
                s_spstg  FOR vbuk-spstg,
                s_istat  FOR tj02t-istat,
                s_estat  FOR tj30t-estat.
SELECTION-SCREEN SKIP.
PARAMETERS: p_canc_r   AS CHECKBOX,
            p_canc_d   AS CHECKBOX.
SELECTION-SCREEN  END OF BLOCK input.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Timestamp
  CONCATENATE sy-datum sy-uzeit INTO gv_ersda.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Status selection
  PERFORM status_selection.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_vbak[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No contracts selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  STATUS_SELECTION
*&---------------------------------------------------------------------*
*       Status selection
*----------------------------------------------------------------------*
FORM status_selection .

* System status
  SELECT istat txt04
         INTO TABLE gt_tj02t
         FROM tj02t
         WHERE spras = 'E'.
* Selection
  IF s_istat[] IS INITIAL.
    LOOP AT gt_tj02t.
      s_istat-low    = gt_tj02t-istat.
      s_istat-sign   = 'I'.
      s_istat-option = 'EQ'.
      APPEND s_istat.
    ENDLOOP.
  ENDIF.

* User status
  SELECT estat txt04
         INTO TABLE gt_tj30t
         FROM tj30t
         WHERE stsma = 'ZAM00002'
           AND spras = 'E'.
* Selection
  IF s_estat[] IS INITIAL.
    LOOP AT gt_tj30t.
      s_estat-low    = gt_tj30t-estat.
      s_estat-sign   = 'I'.
      s_estat-option = 'EQ'.
      APPEND s_estat.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " STATUS_SELECTION

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

  DATA: lv_where TYPE string.
  lv_where = 'tvko~bukrs EQ p_bukrs'.
  IF p_vkorg IS NOT INITIAL.
    CONCATENATE lv_where 'AND h~vkorg = p_vkorg' INTO lv_where SEPARATED BY space.
  ENDIF.
  CONCATENATE lv_where 'AND h~vkbur IN s_vkbur'
                       'AND h~vkgrp IN s_vkgrp'
                       'AND c~vbegdat IN s_vbdat'
                       'AND c~venddat IN s_vndat'
                       'AND h~auart IN s_auart'
                       'AND h~vbeln IN s_vbeln'
                       'AND s~gbstk IN s_gbstk'
                       'AND s~abstk IN s_abstk'
                       'AND s~fksak IN s_fksak'
                       'AND s~spstg IN s_spstg'
                       'AND h~vbtyp = ''G'''
                       'AND c~vposn = 0'
    INTO lv_where SEPARATED BY space.

* Contract headers
*  SELECT h~vbeln h~objnr c~vkuegru c~veindat
*         INTO TABLE gt_vbak
*         FROM vbak AS h
*         INNER JOIN vbuk AS s
*                    ON s~vbeln = h~vbeln
*         INNER JOIN veda AS c
*                    ON c~vbeln = h~vbeln
*         WHERE h~vkorg   =  p_vkorg
*           AND h~vkbur   IN s_vkbur
*           AND h~vkgrp   IN s_vkgrp
*           AND c~vbegdat IN s_vbdat
*           AND c~venddat IN s_vndat
*           AND h~auart   IN s_auart
*           AND h~vbeln   IN s_vbeln
*           AND s~gbstk   IN s_gbstk
*           AND s~abstk   IN s_abstk
*           AND s~fksak   IN s_fksak
*           AND s~spstg   IN s_spstg
*           AND h~vbtyp   =  'G'
*           AND c~vposn   =  0.
  SELECT h~vbeln h~objnr c~vkuegru c~veindat
         INTO TABLE gt_vbak
         FROM vbak AS h
         INNER JOIN vbuk AS s
                    ON s~vbeln = h~vbeln
         INNER JOIN veda AS c
                    ON c~vbeln = h~vbeln
         INNER JOIN tvko AS tvko
           ON tvko~vkorg EQ h~vkorg
         WHERE (lv_where).
  CHECK NOT gt_vbak[] IS INITIAL.

* Check cancellation
  IF NOT p_canc_r IS INITIAL.
    DELETE gt_vbak WHERE NOT vkuegru IS INITIAL.
  ENDIF.
  IF NOT p_canc_d IS INITIAL.
    DELETE gt_vbak WHERE NOT veindat IS INITIAL.
  ENDIF.

  SORT gt_vbak BY vbeln.

* Header status data
  SELECT objnr stat
         INTO TABLE gt_jest
         FROM jest
         FOR ALL ENTRIES IN gt_vbak
         WHERE objnr = gt_vbak-objnr
           AND ( stat IN s_istat  OR
                 stat IN s_estat )
           AND inact = ' '.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_vbak.

*   Status
    LOOP AT gt_jest WHERE objnr = gt_vbak-objnr.

      CLEAR: gt_out.
      MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03.

      gt_out-vbeln = gt_vbak-vbeln.
      gt_out-stat  = gt_jest-stat.

      CASE gt_jest-stat(1).
*       System status
        WHEN 'I'.
          READ TABLE gt_tj02t WITH TABLE KEY istat = gt_jest-stat.
          IF sy-subrc = 0.
            gt_out-txt04 = gt_tj02t-txt04.
          ENDIF.
*       User status
        WHEN 'E'.
          READ TABLE gt_tj30t WITH TABLE KEY estat = gt_jest-stat.
          IF sy-subrc = 0.
            gt_out-txt04 = gt_tj30t-txt04.
          ENDIF.
        WHEN OTHERS.
      ENDCASE.

      APPEND gt_out.

    ENDLOOP.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

* Open file
*  CONCATENATE 'CONTRACT_HDR_STAT' p_vkorg gv_ersda
  CONCATENATE 'CONTRACT_HDR_STAT' p_bukrs
             INTO gv_ofile SEPARATED BY '_'.
  IF p_vkorg IS NOT INITIAL.
    CONCATENATE gv_ofile p_vkorg
               INTO gv_ofile SEPARATED BY '_'.
  ENDIF.
  CONCATENATE gv_ofile gv_ersda
             INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM fill_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

* Fill output
  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  FILL_HEADER
*&---------------------------------------------------------------------*
*       Fill header
*----------------------------------------------------------------------*
FORM fill_header .

  CLEAR: gt_outh.
  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03.

  gt_outh-vbeln       = 'VBELN'.
  gt_outh-stat        = 'STAT'.
  gt_outh-txt04       = 'TXT04'.
  APPEND gt_outh.

  gt_outh-vbeln       = 'Contract'.
  gt_outh-stat        = 'Status Code'.
  gt_outh-txt04       = 'Short Text'.
  APPEND gt_outh.

ENDFORM.                    " FILL_HEADER

*Text symbol text£º
*E01:No contracts selected
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_BUKRS:D       .
*P_CANC_D:        Check cancellation receipt
*P_CANC_R:        Check cancellation reason
*P_VKORG:D       .
*S_ABSTK:D       .
*S_AUART:D       .
*S_ESTAT:D       .
*S_FKSAK:D       .
*S_GBSTK:D       .
*S_ISTAT:D       .
*S_SPSTG:D       .
*S_VBDAT:D       .
*S_VBELN:D       .
*S_VKBUR:D       .
*S_VKGRP:D       .
*S_VNDAT:D       .
