*&---------------------------------------------------------------------*
*& Report  YSE_FI_AM_RR_LT_ST_3111
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& Split Deferred Revenue (LT/ST) / Reclass GPS code 3111 (AM)         *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 22.08.2011
*  Change Request Number : CR2012
*  Transport request Nr. : CD1K967345
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME           | CORRECTION NR. | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | xx.xx.20xx |                | CD1K9......    | CR.....     *
*         |                                                            *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_fi_am_rr_lt_st_3111 NO STANDARD PAGE HEADING
                                LINE-SIZE 170.

TABLES: vbak,
        vbap,
        veda,
        vbfa,
        vbrp,
        bkpf,
        vbkd,
        fplt,
        fpla,
        bsis,
        bsas,
        bseg,
        konv,
        coep,
        cobk,
        tcurr,
        t001.

TYPES: BEGIN OF ty_vbap,
         vbeln     TYPE vbeln,
         posnr     TYPE posnr,
         waerk     TYPE waers,
         netwr     TYPE netwr,
         objnr     TYPE j_objnr,
       END OF ty_vbap.

TYPES: BEGIN OF ty_veda,
         vbeln     TYPE vbeln,
         vposn     TYPE posnr,
         vbegdat   TYPE datum,
         venddat   TYPE datum,
         vkuegru   TYPE kuegru,
       END OF ty_veda.

TYPES: BEGIN OF ty_bsis,
         vbeln     TYPE vbeln,
         posnr     TYPE posnr,
         budat     TYPE budat,
         vbeln_r   TYPE vbeln,
         posnr_r   TYPE posnr,
         belnr     TYPE belnr_d,
         gjahr     TYPE gjahr,
         buzei     TYPE buzei,
         dmbtr     TYPE dmbtr,
         wrbtr     TYPE wrbtr,
         waers     TYPE waers,
         shkzg     TYPE shkzg,
         prctr     TYPE prctr,
       END OF ty_bsis.

TYPES: BEGIN OF ty_bseg,
         belnr     TYPE belnr_d,
         gjahr     TYPE gjahr,
         buzei     TYPE buzei,
         vbel2     TYPE vbeln,
         posn2     TYPE posnr,
       END OF ty_bseg.

TYPES: BEGIN OF ty_fpla,
         vbeln     TYPE vbeln_va,
         posnr     TYPE posnr_va,
         fplnr     TYPE fplnr,
         bedat     TYPE bedat_fp,
         endat     TYPE endat_fp,
       END OF ty_fpla.

TYPES: BEGIN OF ty_curr,
         kurst     TYPE kurst,
         fcurr     TYPE fcurr,
         tcurr     TYPE tcurr_curr,
         gdatu     TYPE gdatu_inv,
         ukurs     TYPE ukurs_curr,
       END OF ty_curr.

TYPES: BEGIN OF ty_coep,
         objnr     TYPE j_objnr,
         budat     TYPE budat,
         twaer     TYPE twaer,
         wtgbtr    TYPE wtgxxx,
         wogbtr    TYPE wogxxx,
         belnr     TYPE co_belnr,
         buzei     TYPE co_buzei,
       END OF ty_coep.

TYPES: BEGIN OF ty_prctr,
         zuonr     TYPE dzuonr,
         hkont     TYPE hkont,
         budat     TYPE budat,
         belnr     TYPE belnr_d,
         gjahr     TYPE gjahr,
         prctr     TYPE prctr,
       END OF ty_prctr.

DATA: gt_vbap  TYPE HASHED TABLE OF ty_vbap
                    WITH UNIQUE KEY vbeln posnr
                    WITH HEADER LINE.

DATA: gt_veda  TYPE TABLE OF ty_veda
                    WITH HEADER LINE.

DATA: gt_bsis  TYPE SORTED TABLE OF ty_bsis
                    WITH NON-UNIQUE KEY vbeln posnr budat
                    WITH HEADER LINE,
      gt_bsisi TYPE TABLE OF ty_bsis
                    WITH HEADER LINE.

DATA: gt_bseg  TYPE HASHED TABLE OF ty_bseg
                    WITH UNIQUE KEY belnr gjahr buzei
                    WITH HEADER LINE.

DATA: gt_fpla  TYPE HASHED TABLE OF ty_fpla
                    WITH UNIQUE KEY vbeln posnr
                    WITH HEADER LINE.

DATA: gt_curr  TYPE HASHED TABLE OF ty_curr
                    WITH UNIQUE KEY kurst fcurr tcurr
                    WITH HEADER LINE,
      gt_curri TYPE TABLE OF ty_curr
                    WITH HEADER LINE.

DATA: gt_coep  TYPE SORTED TABLE OF ty_coep
                    WITH NON-UNIQUE KEY objnr budat
                    WITH HEADER LINE.

DATA: gt_prctr TYPE HASHED TABLE OF ty_prctr
                    WITH UNIQUE KEY zuonr hkont
                    WITH HEADER LINE,
      gt_prctri TYPE TABLE OF ty_prctr
                     WITH HEADER LINE.

* Outout table
DATA: BEGIN OF gt_out  OCCURS 0,
         vbeln       TYPE vbeln_va,
         posnr       TYPE posnr_va,
         waers       TYPE waers,            "Local currency
         bil_val_lc  TYPE wogxxx,
         rev_rec_lc  TYPE wogxxx,
         def_rr_lc   TYPE wogxxx,
         lt_defr_lc  TYPE wogxxx,
         prctr       TYPE prctr,
         bil_cy_lc   TYPE wogxxx,
         prctr_bcy   TYPE prctr,
         revr_cy_lc  TYPE wogxxx,
         prctr_rcy   TYPE prctr,
      END OF gt_out.

DATA: gv_waers_cc  TYPE waers,
      gv_waers_int TYPE waers,
      gv_gdatu     TYPE gdatu_inv,
      gv_kurst     TYPE kurst,
      gv_slt_bp    TYPE datum,
      gv_slt_ep    TYPE datum,
      gv_slt_by    TYPE datum,
      gv_bil_lc    TYPE wogxxx,
      gv_revr_lc   TYPE wogxxx,
      gv_revr_cc   TYPE wogxxx,
      gv_defr_so   TYPE wtgxxx,
      gv_defr_lc   TYPE wogxxx,
      gv_ltdefr_lc TYPE wogxxx,
      gv_diff_lc   TYPE wogxxx,
      gv_bilcy_lc  TYPE wogxxx,
      gv_revrcy_lc TYPE wogxxx,
      gv_days_settl(4)   TYPE p,
      gv_zuonr     TYPE dzuonr.

CONSTANTS: gc_a(1)    TYPE c      VALUE 'A',
           gc_g(1)    TYPE c      VALUE 'G',
           gc_m(1)    TYPE c      VALUE 'M',
           gc_n(1)    TYPE c      VALUE 'N',
           gc_o(1)    TYPE c      VALUE 'O',
           gc_p(1)    TYPE c      VALUE 'P',
           gc_vbrk    TYPE awtyp  VALUE 'VBRK',
           gc_zz      TYPE blart  VALUE 'ZZ'.

RANGES: r_zuonr  FOR  bsis-zuonr.

* Selections
SELECTION-SCREEN  BEGIN OF BLOCK sel  WITH FRAME  TITLE text-s01.
PARAMETERS: p_bukrs   TYPE bukrs  OBLIGATORY
                                  MEMORY ID buk.
SELECT-OPTIONS: so_vbeln  FOR vbap-vbeln.
SELECTION-SCREEN  END OF BLOCK sel.
* Def Rev Short-Long Term
SELECTION-SCREEN  BEGIN OF BLOCK slt  WITH FRAME  TITLE text-s02.
PARAMETERS:     p_slt_p TYPE monat  OBLIGATORY,
                p_slt_y TYPE gjahr  OBLIGATORY.
SELECTION-SCREEN  END OF BLOCK slt.
* Reclass 3111/3115/3116
SELECTION-SCREEN  BEGIN OF BLOCK recl  WITH FRAME  TITLE text-s03.
PARAMETERS:     p_rec_pf TYPE monat  OBLIGATORY,
                p_rec_pt TYPE monat  MODIF ID nup,
                p_rec_y  TYPE gjahr  MODIF ID nup.
SELECTION-SCREEN  END OF BLOCK recl.
* G/L Accounts
SELECTION-SCREEN  BEGIN OF BLOCK acc  WITH FRAME  TITLE text-s04.
PARAMETERS:     p_acc1     TYPE hkont  DEFAULT '0002997001'  OBLIGATORY
                                       MODIF ID nup,
                p_acc2     TYPE hkont  DEFAULT '0001780002'  OBLIGATORY
                                       MODIF ID nup.
SELECTION-SCREEN  END OF BLOCK acc.


*----------------------------------------------------------------------*
* On the selection screen                                              *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-group1 = 'NUP'.
      screen-input = 0.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON p_bukrs.

  PERFORM  check_authorization.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

  p_rec_pt = p_slt_p.
  p_rec_y  = p_slt_y.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*

INITIALIZATION.

  p_rec_pf = 1.
  p_slt_p = p_rec_pt = sy-datum+4(2).
  p_slt_y = p_rec_y  = sy-datum(4).


*----------------------------------------------------------------------*
START-OF-SELECTION.

  PERFORM initial.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_vbap[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No contracts selected'(e01).
    EXIT.
  ENDIF.

* Process data
  PERFORM process_data.

* List data
  PERFORM list_data.


*----------------------------------------------------------------------*
TOP-OF-PAGE.

  WRITE: /01 'Date:', sy-datum,
          40 'Split Deferred Revenue (LT/ST) / Reclass GPS code 3111 (AM)',
         120 'Company:', p_bukrs,
         167(4) sy-pagno.
  ULINE.

  WRITE: /01 'Contract',
          12 'Item',
          20 'Curr.',
          26 '     Billing Val.',
          44 '     Revenue Rec.',
          62 '     Def. RR 12m.',
          80 '     LT Def. Rev.',
          99 'ProfitCtr.',
         111 '    CY Bill. Val.',
         130 'ProfitCtr.',
         142 '     CY Rev. Rec.',
         161 'ProfitCtr.'.

  ULINE.


*----------------------------------------------------------------------*
*       FORMS
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  AUTHORITY-CHECK OBJECT 'F_BKPF_BUK'
           ID 'ACTVT' DUMMY
           ID 'BUKRS' FIELD p_bukrs.

  IF sy-subrc = 4.
*     No authorisation to display data
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH p_bukrs.
    EXIT.
  ELSEIF sy-subrc <> 0.
*     Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
    EXIT.
  ENDIF.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  INITIAL
*&---------------------------------------------------------------------*
*       Initialization
*----------------------------------------------------------------------*
FORM initial .

* Company currency
  SELECT SINGLE waers INTO gv_waers_cc
         FROM t001
         WHERE bukrs = p_bukrs.
  IF sy-subrc NE 0.
  ENDIF.

* Exchange rate type
  CASE p_bukrs.
    WHEN 'MRUA'.
      gv_kurst = 'DRU'.
    WHEN 'POLA'.
      gv_kurst = 'D'.
    WHEN OTHERS.
      gv_kurst = 'EURX'.
  ENDCASE.

* Intermediate currency
  CASE p_bukrs.
    WHEN 'MRUA'.
      gv_waers_int = gv_waers_cc.
    WHEN 'POLA'.
      gv_waers_int = gv_waers_cc.
    WHEN OTHERS.
      gv_waers_int = 'EUR'.
  ENDCASE.

* First day of year
  gv_slt_by(4) = p_slt_y.
  gv_slt_by+4(4) = '0101'.

* First day of period
  gv_slt_bp(4) = p_rec_y.
  gv_slt_bp+4(2) = p_rec_pf.
  gv_slt_bp+6(2) = '01'.

* Get last day of the period
  gv_slt_ep(4) = p_rec_y.
  gv_slt_ep+4(2) = p_rec_pt.
  gv_slt_ep+6(2) = '01'.
  CALL FUNCTION 'DATE_GET_MONTH_LASTDAY'
    EXPORTING
      i_date = gv_slt_ep
    IMPORTING
      e_date = gv_slt_ep.

* Invert current date
  CONVERT DATE gv_slt_ep INTO INVERTED-DATE gv_gdatu.

ENDFORM.                    " INITIAL

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Contracts
  SELECT h~vbeln i~posnr h~waerk i~netwr i~objnr
         INTO TABLE gt_vbap
         FROM vbak AS h
         INNER JOIN vbap AS i
                    ON h~vbeln = i~vbeln
         INNER JOIN veda AS c
                    ON h~vbeln = c~vbeln
         WHERE h~vbeln    IN so_vbeln
           AND h~bukrs_vf =  p_bukrs
           AND h~vbtyp    =  gc_g
           AND c~vposn    = 0
           AND c~vbegdat  LE gv_slt_ep
           AND c~venddat  GE gv_slt_by
           AND c~vkuegru  =  '  '.

  CHECK NOT gt_vbap[] IS INITIAL.

  SORT gt_vbap.

* Check contract data
  SELECT vbeln vposn vbegdat venddat vkuegru
         INTO TABLE gt_veda
         FROM veda
         FOR ALL ENTRIES IN gt_vbap
         WHERE vbeln = gt_vbap-vbeln
           AND vposn = gt_vbap-posnr.
* Canceled
  LOOP AT gt_veda WHERE vkuegru NE space.
    DELETE gt_vbap WHERE vbeln = gt_veda-vbeln
                     AND posnr = gt_veda-vposn.
  ENDLOOP.
* Not yet started
  LOOP AT gt_veda WHERE vbegdat > gv_slt_ep.
    DELETE gt_vbap WHERE vbeln = gt_veda-vbeln
                     AND posnr = gt_veda-vposn.
  ENDLOOP.
* Finished
  LOOP AT gt_veda WHERE venddat < gv_slt_by.
    DELETE gt_vbap WHERE vbeln = gt_veda-vbeln
                     AND posnr = gt_veda-vposn.
  ENDLOOP.
  FREE gt_veda.

* Billing values
  SELECT f~vbelv f~posnv d~budat f~vbeln f~posnn d~belnr d~gjahr d~buzei
         d~dmbtr d~wrbtr d~waers d~shkzg d~prctr
         INTO TABLE gt_bsis
         FROM vbfa AS f
         INNER JOIN bkpf AS h
                    ON h~awkey = f~vbeln
         INNER JOIN bsis AS d
                    ON d~bukrs = h~bukrs AND
                       d~gjahr = h~gjahr AND
                       d~belnr = h~belnr
         FOR ALL ENTRIES IN gt_vbap
         WHERE f~vbelv   =  gt_vbap-vbeln
           AND f~posnv   =  gt_vbap-posnr
           AND f~vbtyp_n IN (gc_m, gc_n, gc_o, gc_p)
           AND h~bukrs   =  p_bukrs
           AND h~awtyp   =  gc_vbrk
           AND h~budat   LE gv_slt_ep.
  SELECT f~vbelv f~posnv d~budat f~vbeln f~posnn d~belnr d~gjahr d~buzei
         d~dmbtr d~wrbtr d~waers d~shkzg d~prctr
         APPENDING TABLE gt_bsis
         FROM vbfa AS f
         INNER JOIN bkpf AS h
                    ON h~awkey = f~vbeln
         INNER JOIN bsas AS d
                    ON d~bukrs = h~bukrs AND
                       d~gjahr = h~gjahr AND
                       d~belnr = h~belnr
         FOR ALL ENTRIES IN gt_vbap
         WHERE f~vbelv   =  gt_vbap-vbeln
           AND f~posnv   =  gt_vbap-posnr
           AND f~vbtyp_n IN (gc_m, gc_n, gc_o, gc_p)
           AND h~bukrs   =  p_bukrs
           AND h~awtyp   =  gc_vbrk
           AND h~budat   LE gv_slt_ep.
* Remove duplicates
  gt_bsisi[] = gt_bsis[].
  SORT gt_bsisi BY vbeln posnr belnr gjahr buzei.
  DELETE ADJACENT DUPLICATES FROM gt_bsisi
         COMPARING vbeln posnr belnr gjahr buzei.
  SORT gt_bsisi.
  gt_bsis[] = gt_bsisi[].
  FREE gt_bsisi.
* Check contract item (via BSEG)
  SELECT belnr gjahr buzei vbel2 posn2 INTO TABLE gt_bseg
         FROM bseg
         FOR ALL ENTRIES IN gt_bsis
         WHERE bukrs = p_bukrs
           AND belnr = gt_bsis-belnr
           AND gjahr = gt_bsis-gjahr
           AND buzei = gt_bsis-buzei.
  LOOP AT gt_bsis.
    CLEAR gt_bseg.
    READ TABLE gt_bseg WITH TABLE KEY belnr = gt_bsis-belnr
                                      gjahr = gt_bsis-gjahr
                                      buzei = gt_bsis-buzei.
    IF gt_bseg-vbel2 NE gt_bsis-vbeln  OR
       gt_bseg-posn2 NE gt_bsis-posnr.
      DELETE gt_bsis.
    ENDIF.
  ENDLOOP.
  FREE gt_bseg.

* Revenue
  SELECT ci~objnr ch~budat ci~twaer ci~wtgbtr ci~wogbtr
         ci~belnr ci~buzei
         INTO TABLE gt_coep
         FROM coep AS ci
         INNER JOIN cobk AS ch
                    ON ci~kokrs = ch~kokrs  AND
                       ci~belnr = ch~belnr
         FOR ALL ENTRIES IN gt_vbap
         WHERE ci~objnr =  gt_vbap-objnr
           AND ci~wrttp =  '04'             "Actual
           AND ci~lednr =  '00'
           AND ci~versn =  '000'
           AND ci~beknz IN ('H', 'S')
           AND ci~beltp = '2'
           AND ch~budat LE gv_slt_ep.

* Billing plan
  SELECT so~vbeln so~posnr so~fplnr
         bp~bedat bp~endat
         INTO TABLE gt_fpla
         FROM vbkd AS so
         INNER JOIN fpla AS bp
                    ON so~fplnr = bp~fplnr
         FOR ALL ENTRIES IN gt_vbap
         WHERE so~vbeln =  gt_vbap-vbeln
           AND so~posnr =  gt_vbap-posnr.

* Build range for assignments (contract nr + item)
  r_zuonr-sign = 'I'.
  r_zuonr-option = 'EQ'.
  LOOP AT gt_vbap.
    CONCATENATE gt_vbap-vbeln gt_vbap-posnr INTO r_zuonr-low.
    APPEND r_zuonr.
  ENDLOOP.

* Profitcenter
  SELECT zuonr hkont budat belnr gjahr prctr
         INTO TABLE gt_prctri
         FROM bsis
         WHERE bukrs =  p_bukrs
           AND hkont IN (p_acc1, p_acc2)
           AND zuonr IN r_zuonr
           AND blart =  gc_zz
           AND budat BETWEEN gv_slt_by AND gv_slt_ep.
  SELECT zuonr hkont budat belnr gjahr prctr
         APPENDING TABLE gt_prctri
         FROM bsas
         WHERE bukrs =  p_bukrs
           AND hkont IN (p_acc1, p_acc2)
           AND zuonr IN r_zuonr
           AND blart =  gc_zz
           AND budat BETWEEN gv_slt_by AND gv_slt_ep.
  DELETE gt_prctri WHERE prctr IS INITIAL.

* Get the most recent documents per contract item
  SORT gt_prctri BY zuonr hkont budat DESCENDING.
  DELETE ADJACENT DUPLICATES FROM gt_prctri
         COMPARING zuonr hkont.
  gt_prctr[] = gt_prctri[].
  FREE gt_prctri.

* Exchange rates
  SELECT kurst fcurr tcurr gdatu ukurs INTO TABLE gt_curri
         FROM tcurr
         WHERE kurst =  gv_kurst
           AND tcurr =  gv_waers_int
           AND gdatu GE gv_gdatu.
  SORT gt_curri BY kurst fcurr tcurr gdatu.
  DELETE ADJACENT DUPLICATES FROM gt_curri COMPARING kurst fcurr tcurr.
  gt_curr[] = gt_curri[].
  FREE gt_curri.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  PROCESS_DATA
*&---------------------------------------------------------------------*
*       Process data
*----------------------------------------------------------------------*
FORM process_data .

  LOOP AT gt_vbap.

    CLEAR gt_out.
    MOVE-CORRESPONDING gt_vbap TO gt_out.
    gt_out-waers      = gv_waers_cc.

    gv_zuonr(10)   = gt_vbap-vbeln.
    gv_zuonr+10(6) = gt_vbap-posnr.

*   Billing Value
    CLEAR: gv_bil_lc,
           gv_bilcy_lc.
    LOOP AT gt_bsis WHERE vbeln = gt_vbap-vbeln
                      AND posnr = gt_vbap-posnr.
      IF gt_bsis-shkzg = 'S'.
        gv_bil_lc = gv_bil_lc - gt_bsis-dmbtr.
      ELSE.
        gv_bil_lc = gv_bil_lc + gt_bsis-dmbtr.
      ENDIF.
      IF gt_bsis-budat BETWEEN gv_slt_bp AND gv_slt_ep.
        IF gt_bsis-shkzg = 'S'.
          gv_bilcy_lc = gv_bilcy_lc - gt_bsis-dmbtr.
        ELSE.
          gv_bilcy_lc = gv_bilcy_lc + gt_bsis-dmbtr.
        ENDIF.
      ENDIF.
    ENDLOOP.
    gt_out-bil_val_lc = gv_bil_lc.

*   Total Revenue Recognized
    CLEAR: gv_revr_lc,
           gv_revrcy_lc.
    LOOP AT gt_coep WHERE objnr = gt_vbap-objnr.
      gv_revr_lc = gv_revr_lc - gt_coep-wogbtr.
      IF gt_coep-budat BETWEEN gv_slt_bp AND gv_slt_ep.
        gv_revrcy_lc = gv_revrcy_lc - gt_coep-wogbtr.
      ENDIF.
    ENDLOOP.
    gt_out-rev_rec_lc = gv_revr_lc.

*   Total days
    CLEAR gv_days_settl.
    READ TABLE gt_fpla WITH TABLE KEY vbeln = gt_vbap-vbeln
                                      posnr = gt_vbap-posnr.
    IF sy-subrc = 0.
      gv_days_settl = gt_fpla-endat - gt_fpla-bedat.
    ENDIF.
    IF gv_days_settl = 0.
      gv_days_settl = 1.
    ENDIF.

*   Def RR 12 months
    CLEAR: gv_defr_so, gv_defr_lc, gv_diff_lc,
           gv_ltdefr_lc.
    gv_defr_so = ( gt_vbap-netwr * 365 ) / gv_days_settl.
    PERFORM convert_amount_int USING gv_defr_so
                                     gt_vbap-waerk
                                     gv_defr_lc
                                     gv_waers_cc.
    gt_out-def_rr_lc  = gv_defr_lc.

    gv_diff_lc = gv_bil_lc - gv_revr_lc.

    IF gv_bil_lc > gv_revr_lc  AND
       gv_diff_lc > gv_defr_lc.
      gv_ltdefr_lc = gv_diff_lc - gv_defr_lc.
    ENDIF.
    gt_out-lt_defr_lc = gv_ltdefr_lc.
    READ TABLE gt_prctr WITH TABLE KEY zuonr = gv_zuonr
                                       hkont = p_acc1.
    IF sy-subrc = 0.
      gt_out-prctr = gt_prctr-prctr.
    ENDIF.

    gt_out-bil_cy_lc = gv_bilcy_lc.

    gt_out-revr_cy_lc = gv_revrcy_lc.
    READ TABLE gt_prctr WITH TABLE KEY zuonr = gv_zuonr
                                       hkont = p_acc1.
    IF sy-subrc = 0.
      gt_out-prctr_rcy = gt_prctr-prctr.
    ELSE.
      READ TABLE gt_prctr WITH TABLE KEY zuonr = gv_zuonr
                                         hkont = p_acc2.
      IF sy-subrc = 0.
        gt_out-prctr_rcy = gt_prctr-prctr.
      ENDIF.
    ENDIF.
    gt_out-prctr_bcy = gt_out-prctr_rcy.

    APPEND gt_out.

  ENDLOOP.

ENDFORM.                    " PROCESS_DATA

*&---------------------------------------------------------------------*
*&      Form  CONVERT_AMOUNT_INT
*&---------------------------------------------------------------------*
*       Convert amount via intermediate currency
*----------------------------------------------------------------------*
*      -->P_KWERT_IN  : Given Value
*      -->P_WAERS_IN  : Given Currency
*      -->P_KWERT_OUT : Converted Value
*      -->P_WAERS_RES : Result Currency
*----------------------------------------------------------------------*
FORM convert_amount_int  USING    p_kwert_in
                                  p_waers_in
                                  p_kwert_out
                                  p_waers_res.

  DATA: lv_value_int   TYPE wtgxxx.

* Input Currency = Result currency -> NO conversion
  IF p_waers_in = p_waers_res.
    p_kwert_out = p_kwert_in.
    RETURN.
  ENDIF.

* Intermediate exchange rate
  IF p_waers_in = gv_waers_int.
    lv_value_int = p_kwert_in.
  ELSE.
    CLEAR gt_curr.
    READ TABLE gt_curr WITH TABLE KEY kurst = gv_kurst
                                      fcurr = p_waers_in
                                      tcurr = gv_waers_int.
    CHECK sy-subrc = 0.
*     Calculate ammount in intermediate currency
    IF gt_curr-ukurs = 0.
      gt_curr-ukurs = 1.
    ENDIF.
    lv_value_int = p_kwert_in * gt_curr-ukurs.
  ENDIF.

* Result exchange rate
  IF p_waers_res = gv_waers_int.
    p_kwert_out = lv_value_int.
  ELSE.
    CLEAR gt_curr.
    READ TABLE gt_curr WITH TABLE KEY kurst = gv_kurst
                                      fcurr = p_waers_res
                                      tcurr = gv_waers_int.
    CHECK sy-subrc = 0.
*     Calculate ammount in result currency
    IF gt_curr-ukurs = 0.
      gt_curr-ukurs = 1.
    ENDIF.
    p_kwert_out = lv_value_int / gt_curr-ukurs.
  ENDIF.

ENDFORM.                    " CONVERT_AMOUNT_INT

*&---------------------------------------------------------------------*
*&      Form  LIST_DATA
*&---------------------------------------------------------------------*
*       List data
*----------------------------------------------------------------------*
FORM list_data .

  IF gt_out[] IS INITIAL.
    WRITE: /01 'No amounts calculated (to be posted)'(i01).
    ULINE.
    RETURN.
  ENDIF.

  LOOP AT gt_out.
    WRITE: /01     gt_out-vbeln,
            12     gt_out-posnr,
            20     gt_out-waers,            "Local curr.
            26(17) gt_out-bil_val_lc  CURRENCY gt_out-waers,
            44(17) gt_out-rev_rec_lc  CURRENCY gt_out-waers,
            62(17) gt_out-def_rr_lc   CURRENCY gt_out-waers,
            80(17) gt_out-lt_defr_lc  CURRENCY gt_out-waers,
            99     gt_out-prctr,
           111(17) gt_out-bil_cy_lc   CURRENCY gt_out-waers,
           130     gt_out-prctr_bcy,
           142(17) gt_out-revr_cy_lc  CURRENCY gt_out-waers,
           161     gt_out-prctr_rcy.
  ENDLOOP.

  ULINE.

ENDFORM.                    " LIST_DATA

*Text symbol text£º
*I01:No amounts calculated (to be posted)
*S01:Selection
*S02:Def. Rev. Short-Long Term
*S03:Reclass 3111/3115/3116

*S04:G/L Accounts
*Selection text£º
*P_ACC1:        G/L Account 1
*P_ACC2:        G/L Account 2
*P_BUKRS:D       .
*P_REC_PF:        Posting period From
*P_REC_PT:        To
*P_REC_Y:        Year
*P_SLT_P:        Posting period
*P_SLT_Y:        Year
*SO_VBELN:        Contract
