*----------------------------------------------------------------------*
* Author  : Anda Wu                                                    *
* Created : 05.19.2015                                                 *
* Dev.nbr: CR3643- synchronize issue of SAP-SRM interface              *
*----------------------------------------------------------------------*
* This report  is  new report for returns  of Russia                   *
************************************************************************
* Mod-xxx | xxxx.xx.xx | xxxx xxxx     | CD1Kxxxxxxxxxx  |   CRXXXX    *
************************************************************************
************************************************************************
REPORT yse_srm_syn NO STANDARD PAGE HEADING LINE-SIZE 255.

************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES:
  aufk.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:

  BEGIN OF ty_alv_output,
    aufnr    TYPE char12,       "SEO Order Number
    aenam    TYPE aufk-aenam,   "Last changed by
    ernam    TYPE aufk-ernam,   "Entered by
    erdat    TYPE aufk-erdat,   "Created on
    aedat    TYPE aufk-aedat,   "Change date for Order Master
    bukrs    TYPE aufk-bukrs,   "Company Code
    werks    TYPE aufk-werks,   "Plant
    msgty    TYPE bdc_mart  ,   "Message Type
    msgtx    TYPE char120,      "Message text
  END OF ty_alv_output.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_aufk     TYPE STANDARD TABLE OF aufk,
  gt_bdcdata  TYPE STANDARD TABLE OF bdcdata,
  gt_alvdata  TYPE STANDARD TABLE OF ty_alv_output,
  gt_fieldcat TYPE slis_t_fieldcat_alv.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_aufk     TYPE aufk,
  gs_alvdata  TYPE ty_alv_output,
  gs_disvar   TYPE disvariant,
  gs_layout   TYPE slis_layout_alv.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_col(10)  TYPE n,
  gv_text     TYPE string,
  gv_save     TYPE c,
  gv_callback TYPE slis_formname.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x                TYPE c VALUE 'X',
  gc_callback_routine TYPE slis_formname VALUE 'USER_COMMAND'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-t02 .
SELECT-OPTIONS:
  s_aedat      FOR aufk-aedat OBLIGATORY, "Change date for Order Master
  s_aenam      FOR aufk-aenam,            "Material ID
  s_aufnr      FOR aufk-aufnr,            "Service order number
  s_ernam      FOR aufk-ernam,
  s_erdat      FOR aufk-erdat,
  s_bukrs      FOR aufk-bukrs,
  s_werks      FOR aufk-werks.
SELECTION-SCREEN END OF BLOCK b2.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initilization
  PERFORM frm_init.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* get data
  PERFORM get_alv_data.
* alv property set
  PERFORM alv_prop_set.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
* ALV display
  PERFORM alv_display.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do initilization
*----------------------------------------------------------------------*
FORM frm_init .

* initialize the internal table,workareas and variables
  REFRESH:
    gt_aufk,
    gt_alvdata,
    gt_fieldcat.

  CLEAR:
    gs_alvdata,
    gs_disvar,
    gs_layout,
    gv_col,
    gv_text,
    gv_save,
    gv_callback.

ENDFORM.                    " FRM_INIT

*&---------------------------------------------------------------------*
*&      Form  GET_ALV_DATA
*&---------------------------------------------------------------------*
*       get data
*---------------------------------------------------------------------c-*
FORM get_alv_data .
** Gain data from table AUFK - Order master data
  PERFORM get_data_aufk.
** Generate ALV data for output
*  PERFORM generate_alv_data.
* Edit all available
*  PERFORM edit_alv_data.
ENDFORM.                    " GET_ALV_DATA

*&---------------------------------------------------------------------*
*&      Form  ALV_PROP_SET
*&---------------------------------------------------------------------*
*       alv property set
*----------------------------------------------------------------------*
FORM alv_prop_set .

* FIELDCAT set
  PERFORM alv_fieldcat_set.

* LAYOUT set
  PERFORM alv_layout_set.

* Set Others
  PERFORM alv_others_set.

ENDFORM.                    " ALV_PROP_SET


*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       Set Others
*----------------------------------------------------------------------*
FORM alv_others_set .

  gv_callback = gc_callback_routine.

ENDFORM.                    " ALV_OTHERS_SET

*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM alv_fieldcat_set .

** Create Fieldcatalogue from internal table

  CLEAR gv_col.

* Field definitions
  PERFORM add_field USING:
     'AUFNR'    12  'SEO'(s01)     'Service Order'(l01) 'X',
     'ERNAM'    12  'Entered by'(s07) 'Entered by'(s07) ' ',
     'ERDAT'    8   'Created on'(s08) 'Created on'(s08) ' ',
     'AENAM'    12  'Changed by'(s02) 'Last changed by'(l02) ' ',
     'AEDAT'    8 'Changed on'(s03) 'Change date for order master'(l03) ' ',
     'MSGTY'    1 'Msg.Typ'(s04)   'Message Type'(l04) ' ',
     'MSGTX'    120 'Msg.Txt'(s05)    'Process Message'(l05)' '.
ENDFORM.                    " ALV_FIELDCAT_SET

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr_s
                         p_descr_l
                         p_hotsp.
  DATA:
     ls_fieldcat TYPE slis_fieldcat_alv.

  gv_col = gv_col + 1.

  CLEAR ls_fieldcat.
  ls_fieldcat-col_pos    = gv_col.
  ls_fieldcat-fieldname  = p_field.
  ls_fieldcat-outputlen  = p_len.
  ls_fieldcat-seltext_l  = p_descr_l.
  ls_fieldcat-seltext_m  = p_descr_l.
  ls_fieldcat-seltext_s  = p_descr_s.
  ls_fieldcat-fix_column = 'X'.
  ls_fieldcat-emphasize  = 'X'.
  ls_fieldcat-hotspot    = p_hotsp.

* Suppress leading zeros for the material number
  IF p_field = 'MATNR'.
    ls_fieldcat-edit_mask = '==MATN1'.
  ENDIF.

  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ADD_FIELD
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_lines(10)  TYPE c.

  CLEAR: gv_text.
  DESCRIBE TABLE gt_alvdata LINES lv_lines.
  CONCATENATE 'Totally'(t62) lv_lines 'entries are process'(t63)
                INTO gv_text SEPARATED BY space.
  MESSAGE i000(yse_sales_log) WITH gv_text.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'FRM_MY_STATUS'
      i_callback_user_command  = gv_callback
      is_layout                = gs_layout
      it_fieldcat              = gt_fieldcat
      i_save                   = gv_save
      is_variant               = gs_disvar
    TABLES
      t_outtab                 = gt_alvdata
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   "Auto optimize column width
  gs_layout-detail_popup = gc_x.        "Show detail screen

ENDFORM.                    " ALV_LAYOUT_SET

*&---------------------------------------------------------------------*
*&      Form  FRM_MY_STATUS
*&---------------------------------------------------------------------*
*       Status Set
*----------------------------------------------------------------------*
*  --> I_EXTAB    STATUS
*----------------------------------------------------------------------*
FORM frm_my_status
  USING i_extab TYPE slis_t_extab.              "#EC CALLED "#EC NEEDED

  SET PF-STATUS '9100'.

ENDFORM.        "FRM_MY_STATUS

*&---------------------------------------------------------------------*
*&      Form  frm_iw32_save
*&---------------------------------------------------------------------*
*       Write entry to datebase table
*----------------------------------------------------------------------*
FORM frm_iw32_save .

  DATA: lv_mess_str   TYPE string,
*        ls_update_log TYPE ty_update_log,
*        ls_qmel       TYPE qmel,
*        ls_vbkd       TYPE ty_vbkd,
*        ls_equz_iloa  TYPE ty_equz_iloa,
*"        lv_objectkey  TYPE bapi1003_key-object,
        ls_option     TYPE ctu_params ,
        ls_bdcmsg     TYPE bdcmsgcoll.
*
  DATA: lt_bdcmsgcoll TYPE STANDARD TABLE OF bdcmsgcoll,
        ls_bdcdata  TYPE   bdcdata .

  ls_option-dismode   = 'N'.
  ls_option-updmode   = 'S'.
  ls_option-racommit  = 'X'.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    'SAPLCOIH'  '0101'  'X'  ''  ''
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO gt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_CURSOR'  'CAUFVD-AUFNR'
          CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO gt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_OKCODE'  '/00'
          CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO gt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'CAUFVD-AUFNR'  gs_aufk-aufnr
          CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO gt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    'SAPLCOIH'  '3000'  'X'  ''  ''
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO gt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_OKCODE'  '=BU'
          CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO gt_bdcdata.

  CALL TRANSACTION 'IW32'
    USING gt_bdcdata
    OPTIONS FROM ls_option
    MESSAGES INTO lt_bdcmsgcoll.

  READ TABLE lt_bdcmsgcoll INTO ls_bdcmsg
      WITH KEY msgtyp = 'E'.
  IF sy-subrc = 0.
    CLEAR: lv_mess_str.
    MESSAGE ID ls_bdcmsg-msgid TYPE ls_bdcmsg-msgtyp
      NUMBER ls_bdcmsg-msgnr
      WITH ls_bdcmsg-msgv1 ls_bdcmsg-msgv2
           ls_bdcmsg-msgv3 ls_bdcmsg-msgv4
      INTO lv_mess_str.
    gs_alvdata-msgty = ls_bdcmsg-msgtyp.
    gs_alvdata-msgtx = lv_mess_str.
  ELSE.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        wait = 'X'.

    gs_alvdata-msgty = 'S'.
    gs_alvdata-msgtx = 'No error'(088).
  ENDIF.

  REFRESH: gt_bdcdata,lt_bdcmsgcoll.

ENDFORM.                    " frm_iw32_save

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       USER_COMMAND
*----------------------------------------------------------------------*
FORM user_command USING u_ucomm     LIKE sy-ucomm           "#EC CALLED
                        p_selfield  TYPE slis_selfield.

  CASE u_ucomm.
    WHEN '&IC1'.        " SAP standard code for double-clicking
      IF p_selfield-fieldname = 'AUFNR'.
        IF p_selfield-value IS NOT INITIAL.
          SET PARAMETER ID 'ANR' FIELD p_selfield-value.
          CALL TRANSACTION 'IW32'.
        ENDIF.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.

ENDFORM.                    " USER_COMMAND
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_AUFK
*&---------------------------------------------------------------------*
*       Gain data from table AUFK - Order master data
*----------------------------------------------------------------------*
FORM get_data_aufk .

  DATA: lt_qmel TYPE STANDARD TABLE OF qmel,
        ls_qmel TYPE qmel,
        lt_qmih TYPE STANDARD TABLE OF qmih,
        ls_qmih TYPE qmih.
  SELECT *
    FROM aufk
    INTO TABLE gt_aufk
    WHERE aufnr IN s_aufnr
      AND ernam IN s_ernam
      AND erdat IN s_erdat
      AND aenam IN s_aenam
      AND aedat IN s_aedat
      AND bukrs IN s_bukrs
      AND werks IN s_werks.
  IF sy-subrc <> 0.
    MESSAGE s001(00) DISPLAY LIKE 'E' WITH text-t01.
    LEAVE LIST-PROCESSING.
  ENDIF.
  CHECK gt_aufk IS NOT INITIAL.
  SELECT *
    FROM qmel
    INTO TABLE lt_qmel
    FOR ALL ENTRIES IN gt_aufk
    WHERE aufnr = gt_aufk-aufnr.
  IF lt_qmel IS NOT INITIAL.
    SELECT *
      FROM qmih
      INTO TABLE lt_qmih
      FOR ALL ENTRIES IN lt_qmel
      WHERE qmnum = lt_qmel-qmnum.
  ENDIF.

  LOOP AT gt_aufk INTO gs_aufk.
    READ TABLE lt_qmel INTO ls_qmel
      WITH KEY aufnr = gs_aufk-aufnr.
    IF sy-subrc = 0.
      IF  ls_qmel-vkorg <> 'CN01'.
        CONTINUE.
      ENDIF.
      READ TABLE lt_qmih INTO ls_qmih
        WITH KEY qmnum = ls_qmel-qmnum.
      IF sy-subrc = 0.
        SELECT COUNT(*)
          FROM yse_srm_sel WHERE iwerk = ls_qmih-iwerk
                            AND  ingpr = ls_qmih-ingrp.
        IF sy-subrc <> 0.
          CONTINUE.
        ENDIF.
        SELECT COUNT(*)
          FROM   yse_srm_excl
           where iwerk = ls_qmih-iwerk
            and  arbpl = gs_aufk-vaplz .
        IF sy-subrc = 0.
          CONTINUE.
        ENDIF.
      ENDIF.
    ENDIF.

    CLEAR: gs_alvdata.
    PERFORM frm_iw32_save.
    MOVE-CORRESPONDING gs_aufk TO gs_alvdata.
    APPEND gs_alvdata TO gt_alvdata.
  ENDLOOP.

ENDFORM.                    " GET_DATA_AUFK

*Text symbol text£º
*088:No error
*L01:Service Order
*L02:Last changed by
*L03:Change date for order master
*L04:Message Type
*L05:Process Message
*S01:SEO
*S02:Changed by
*S03:Changed on
*S04:Msg.Typ
*S05:Msg.Txt
*S07:Entered by
*S08:Created on
*T01:No process data
*T02:Selection Input
*T62:Totally

*T63:entries are process
*Selection text£º
*S_AEDAT:D       .
*S_AENAM:D       .
*S_AUFNR:D       .
*S_BUKRS:D       .
*S_ERDAT:D       .
*S_ERNAM:D       .
*S_WERKS:D       .
