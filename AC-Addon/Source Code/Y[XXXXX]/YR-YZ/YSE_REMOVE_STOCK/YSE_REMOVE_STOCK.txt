************************************************************************
* Program ID        : YSE_REMOVE_STOCK               *
* Program Title     : Program title                                    *
* Author            : Anson Sun                                        *
* Date              : 20/08/2011                               	     *
* Change Request Number:2182                                           *
* Description       : The purpose of this program is to remove stock   *
*======================================================================*
* Copied From       : (Cloned Program)                               *
* Title             : (Program Title)                          	     *
* Other Related obj : (Object names)                               *
*======================================================================*
* Change History Log                                             	     *
*----------------------------------------------------------------------*
* Mod. no.|  Date    | Name           | Correction Number  | Change    *
*----------------------------------------------------------------------*
* MOD-001 |31/10/2016| Anda Wu        | CD1K990026         |  CR4052   *
*                                                                      *
* Description:                                                   	     *
*----------------------------------------------------------------------*

REPORT  yse_remove_stock MESSAGE-ID yse_cs_sd_msg.
*----------------------------------------------------------------------*
*       TABLES
*----------------------------------------------------------------------*
TABLES:
  mara,                      "General Material Data
  marc,                      "Plant Data for Material
  mard,                      "Storage Location Data for Material
  mchb,                      "Batch Stocks
  mska,                      "Sales Order Stock
  mkpf.                      "Header: Material Document
*----------------------------------------------------------------------*
*       TYPE-POOLS
*----------------------------------------------------------------------*
TYPE-POOLS slis.
*----------------------------------------------------------------------*
*       TYPE
*----------------------------------------------------------------------*
TYPES:
  BEGIN OF ty_stock,
    stock_typ TYPE char04,             "Stock Type(Internal Use)
    werks TYPE marc-werks,             "Plant
    vbeln TYPE mska-vbeln,             "Sales Order
    posnr TYPE mska-posnr,             "Item number
    lgort TYPE mard-lgort,             "Storage Location
*   BEGIN OF MOD-001 INSER
    kunnr TYPE msku-kunnr,             "Customer Number
*   END OF MOD-001 INSER
    matnr TYPE marc-matnr,             "Material Number
    prctr TYPE marc-prctr,             "Profit Centre
    charg TYPE mchb-charg,             "Batch Number
    sernp TYPE marc-sernp,             "Serial Number Profile
    sernr TYPE equi-sernr,             "Serial Number
    xchar TYPE marc-xchar,             "Batch management indicator
    labst TYPE mard-labst,             "w/o batch Un-Use Stock
    clabs TYPE mchb-clabs,             "batch Un-Use Stock
    kalab TYPE mska-kalab,             "sales ord Stock
*   BEGIN OF MOD-001 INSER
    kulab TYPE msku-kulab,             "sales consigment stock
    speme TYPE mard-speme,             "block stock without batch
    cspem TYPE mchb-cspem,             "Block stock with batch
*   END OF MOD-001 INSER
    menge TYPE mard-labst,             "Stock
    meins TYPE mara-meins,             "Base Unit
    bwart TYPE bwart,                  "Movement Type
    sobkz TYPE mska-sobkz,             "Special Stock Indicator
    insmk TYPE mb_insmk,               "Stock Type
  END   OF ty_stock,

  BEGIN OF ty_sernr,
    b_werk   TYPE eqbs-b_werk,         "Plant
    b_lager  TYPE eqbs-b_lager,        "Storage Location
    b_charge TYPE eqbs-b_charge,       "Batch Number
    sobkz    TYPE eqbs-sobkz,          "Special Stock Indicator
    kdauf    TYPE eqbs-kdauf,          "Sales Order Number
    kdpos    TYPE eqbs-kdpos,          "Item Number in Sales Order
    matnr    TYPE equi-matnr,          "Material Number
    sernr    TYPE equi-sernr,          "Serial number
  END   OF ty_sernr.
*----------------------------------------------------------------------*
*       DATA
*----------------------------------------------------------------------*
DATA:
  rec_stock     TYPE ty_stock,
  rec_sernr     TYPE ty_sernr,
  i_log         TYPE TABLE OF yse_remove_stock_log,
  i_fieldcat    TYPE slis_t_fieldcat_alv.
*----------------------------------------------------------------------*
*       SELECTION-SCREEN
*----------------------------------------------------------------------*
SELECTION-SCREEN: BEGIN OF BLOCK b_sel1 WITH FRAME TITLE text-b01.
SELECT-OPTIONS:
  s_werks  FOR marc-werks OBLIGATORY,
  s_lgort  FOR mard-lgort,
  s_matnr  FOR marc-matnr,
  s_prctr  FOR marc-prctr OBLIGATORY.
SELECTION-SCREEN: END   OF BLOCK b_sel1.

SELECTION-SCREEN: BEGIN OF BLOCK b_sel2 WITH FRAME TITLE text-b02.
SELECT-OPTIONS:
  s_budat  FOR mkpf-budat NO-EXTENSION NO INTERVALS OBLIGATORY,
  s_bktxt  FOR mkpf-bktxt NO-EXTENSION NO INTERVALS OBLIGATORY.
SELECTION-SCREEN: END   OF BLOCK b_sel2.
PARAMETERS:
*  p_maxpo  TYPE maxpo default '20' NO-DISPLAY.
  p_maxpo  TYPE maxpo OBLIGATORY.
*----------------------------------------------------------------------*
*       INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

* initial process
  PERFORM ini_process.
*----------------------------------------------------------------------*
*       START-OF-SELECTION
*----------------------------------------------------------------------*
START-OF-SELECTION.

* main process
  PERFORM main_process.
*----------------------------------------------------------------------*
*       END-OF-SELECTION
*----------------------------------------------------------------------*
END-OF-SELECTION.

* log process
  PERFORM display_log.
*&---------------------------------------------------------------------*
*&      Form  ini_process
*&---------------------------------------------------------------------*
*       initial process
*----------------------------------------------------------------------*
FORM ini_process .

  CLEAR: rec_stock,
         rec_sernr.
  REFRESH:
    i_log,
    i_fieldcat.
ENDFORM.                    " ini_process
*&---------------------------------------------------------------------*
*&      Form  main_process
*&---------------------------------------------------------------------*
*       main process
*----------------------------------------------------------------------*
FORM main_process .

  DATA: li_stock TYPE TABLE OF ty_stock,
        li_sernr TYPE TABLE OF ty_sernr.

* Get Serial Number
  PERFORM get_sernr TABLES li_sernr.
* Get Stock
  PERFORM get_stock TABLES li_stock.
* Remove Stock
  PERFORM remove_stock TABLES li_stock
                              li_sernr.

ENDFORM.                    " main_process
*&---------------------------------------------------------------------*
*&      Form  display_log
*&---------------------------------------------------------------------*
*       display log
*----------------------------------------------------------------------*
FORM display_log .
  DATA: lrec_layout TYPE slis_layout_alv.

* edit alv fieldcat
  PERFORM edit_alv_fieldcat.

  lrec_layout-colwidth_optimize = 'X'.
  lrec_layout-zebra             = 'X'.
  CALL FUNCTION 'REUSE_ALV_LIST_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'SET_STATUS_10001'
      i_callback_user_command  = 'USER_COMMAND'
      is_layout                = lrec_layout
      it_fieldcat              = i_fieldcat
    TABLES
      t_outtab                 = i_log
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    EXIT.
  ELSE.
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " display_log
*&---------------------------------------------------------------------*
*&      Form  edit_alv_fieldcat
*&---------------------------------------------------------------------*
*       edit alv fieldcat
*----------------------------------------------------------------------*
FORM edit_alv_fieldcat.

  DATA: lrec_fieldcat TYPE slis_fieldcat_alv.
  REFRESH: i_fieldcat.

  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
    EXPORTING
      i_structure_name       = 'YSE_REMOVE_STOCK_LOG'
      i_bypassing_buffer     = 'X'
    CHANGING
      ct_fieldcat            = i_fieldcat
    EXCEPTIONS
      inconsistent_interface = 1
      program_error          = 2
      OTHERS                 = 3.
  IF sy-subrc <> 0.
    EXIT.
  ENDIF.
*  LOOP AT i_fieldcat INTO lrec_fieldcat.
*    IF lrec_fieldcat-fieldname  = 'MBLNR'
*    OR lrec_fieldcat-fieldname  = 'MJAHR'
*    OR lrec_fieldcat-fieldname  = 'TYPE'
*    OR lrec_fieldcat-fieldname  = 'MESSAGE'.
*    ELSE.
*      lrec_fieldcat-no_out = 'X'.
*      MODIFY i_fieldcat FROM lrec_fieldcat.
*    ENDIF.
*    CLEAR  lrec_fieldcat.
*  ENDLOOP.

ENDFORM.                    " edit_alv_fieldcat
*&---------------------------------------------------------------------*
*&      Form  set_status_10001
*&---------------------------------------------------------------------*
*       set screen status
*----------------------------------------------------------------------*
FORM set_status_10001 USING i_extab TYPE slis_t_extab.

  SET PF-STATUS '10001'.
  SET TITLEBAR  '10001'.

ENDFORM.                               " set_status_10001
*&---------------------------------------------------------------------*
*&      Form  user_command
*&---------------------------------------------------------------------*
*       user command
*----------------------------------------------------------------------*
FORM user_command  USING    i_ucomm    LIKE sy-ucomm
                            i_selfield TYPE slis_selfield.

  CASE i_ucomm.
    WHEN '&IC1'.
      PERFORM display_process USING i_selfield.
  ENDCASE.

ENDFORM.                               " user_command
*&---------------------------------------------------------------------*
*&      Form  display_process
*&---------------------------------------------------------------------*
*       display process
*----------------------------------------------------------------------*
FORM display_process  USING    selfield TYPE slis_selfield.

  DATA lrec_log TYPE yse_remove_stock_log.
  READ TABLE i_log INTO lrec_log INDEX selfield-tabindex.
  IF sy-subrc = 0.
    CASE selfield-fieldname.
      WHEN 'MBLNR'.
        SET PARAMETER ID 'MBN' FIELD lrec_log-mblnr.
        SET PARAMETER ID 'MJA' FIELD lrec_log-mjahr.
        CALL TRANSACTION 'MB03' AND SKIP FIRST SCREEN.
*      WHEN 'VBELN'.
*        SET PARAMETER ID 'AUN' FIELD lrec_log-vbeln.
*        CALL TRANSACTION 'VA03' AND SKIP FIRST SCREEN.
    ENDCASE.
  ENDIF.

ENDFORM.                    " display_process
*&---------------------------------------------------------------------*
*&      Form  SET_LOG
*&---------------------------------------------------------------------*
*       Set Log
*----------------------------------------------------------------------*
*FORM set_log  USING stock    TYPE ty_stock
*                    mat_doc  TYPE bapi2017_gm_head_ret
*                    msg_type TYPE bapi_mtype
*                    msg      TYPE bapi_msg.
FORM set_log  USING mat_doc  TYPE bapi2017_gm_head_ret
                    msg_type TYPE bapi_mtype
                    msg      TYPE bapi_msg
********************BEGIN OF MOD-001 INSERT
                    matlist  TYPE yse_remove_stock_log-matlist
********************END OF MOD-001 INSERT
                    .

  DATA: lrec_log   TYPE yse_remove_stock_log.
*  MOVE-CORRESPONDING stock TO lrec_log.
  lrec_log-mblnr    = mat_doc-mat_doc.
  lrec_log-mjahr    = mat_doc-doc_year.
  lrec_log-type     = msg_type.
  lrec_log-message  = msg.
*******Begin of mod-001 insert *************
*  DATA: LS_ITEM TYPE
  IF msg_type = 'E' OR msg_type = 'A' OR msg_type = 'X'.
    lrec_log-matlist = matlist.
  ENDIF.
*******End of mod-001 insert *************
  APPEND lrec_log TO i_log.

ENDFORM.                    " SET_LOG
*&---------------------------------------------------------------------*
*&      Form  GET_STOCK
*&---------------------------------------------------------------------*
*       Get Stock
*----------------------------------------------------------------------*
FORM get_stock TABLES i_stock STRUCTURE rec_stock.

  DATA: lrec_stock         TYPE ty_stock.
  DATA: li_stock_no_batch  TYPE TABLE OF ty_stock,
        li_stock_batch     TYPE TABLE OF ty_stock,
        li_sales_stock     TYPE TABLE OF ty_stock,
*******Begin of mod-001 insert *************
        li_consi_stock     TYPE TABLE OF ty_stock,
        li_stock_block_batch  TYPE TABLE OF ty_stock,
        li_stock_block  TYPE TABLE OF ty_stock.
*******End of mod-001 insert *************
  DATA: l_tabix            TYPE sy-tabix.

* Get w/o batch Un-Use Stock
  SELECT marc~werks                    "Plant
         mard~lgort                    "Storage Location
         marc~matnr                    "Material Number
         marc~prctr                    "Profit Centre
         marc~sernp                    "Serial Number Profile
         marc~xchar                    "Batch management indicator
         mard~labst                    "w/o batch Un-Use Stock
    INTO CORRESPONDING FIELDS OF TABLE li_stock_no_batch
    FROM marc INNER JOIN mard
      ON marc~matnr = mard~matnr
     AND marc~werks = mard~werks
   WHERE marc~matnr IN s_matnr
     AND marc~werks IN s_werks
     AND marc~prctr IN s_prctr
     AND marc~lvorm = space
     AND marc~xchar = space
     AND mard~lgort IN s_lgort
     AND mard~lvorm = space
     AND mard~labst > 0.
  IF sy-subrc = 0.
    LOOP AT li_stock_no_batch INTO lrec_stock.
      l_tabix              = sy-tabix.
      lrec_stock-bwart     = '502'.
*      lrec_stock-insmk     = .
      lrec_stock-menge     = lrec_stock-labst.
      lrec_stock-stock_typ = 'MARD'.
*     Get Base Unit
      PERFORM get_meins USING lrec_stock-matnr
                     CHANGING lrec_stock-meins.
      APPEND lrec_stock TO i_stock.
      MODIFY li_stock_no_batch FROM lrec_stock INDEX l_tabix.
      CLEAR  lrec_stock.
    ENDLOOP.
  ENDIF.

* Get batch Un-Use Stock
  SELECT marc~werks                    "Plant
         mchb~lgort                    "Storage Location
         mchb~charg                    "Batch Number
         marc~matnr                    "Material Number
         marc~prctr                    "Profit Centre
         marc~sernp                    "Serial Number Profile
         marc~xchar                    "Batch management indicator
         mchb~clabs                    "batch Un-Use Stock
    INTO CORRESPONDING FIELDS OF TABLE li_stock_batch
    FROM marc INNER JOIN mchb
      ON marc~matnr = mchb~matnr
     AND marc~werks = mchb~werks
   WHERE marc~matnr IN s_matnr
     AND marc~werks IN s_werks
     AND marc~prctr IN s_prctr
     AND marc~lvorm = space
     AND marc~xchar = 'X'
     AND mchb~lgort IN s_lgort
     AND mchb~lvorm = space
     AND mchb~clabs > 0.
  IF sy-subrc = 0.
    LOOP AT li_stock_batch INTO lrec_stock.
      l_tabix              = sy-tabix.
      lrec_stock-bwart     = '502'.
*      lrec_stock-insmk     = .
      lrec_stock-menge     = lrec_stock-clabs.
      lrec_stock-stock_typ = 'MCHB'.
*     Get Base Unit
      PERFORM get_meins USING lrec_stock-matnr
                     CHANGING lrec_stock-meins.
      APPEND lrec_stock TO i_stock.
      MODIFY li_stock_batch FROM lrec_stock INDEX l_tabix.
      CLEAR  lrec_stock.
    ENDLOOP.
  ENDIF.

* Get Sales Stock
  SELECT marc~werks                    "Plant
         mska~lgort                    "Storage Location
         mska~charg                    "Batch Number
         mska~sobkz                    "Special Stock Indicator
         mska~vbeln                    "Sales Order
         mska~posnr                    "Item number
         marc~matnr                    "Material Number
         marc~prctr                    "Profit Centre
         marc~sernp                    "Serial Number Profile
         marc~xchar                    "Batch management indicator
         mska~kalab                    "sales ord Stock
    INTO CORRESPONDING FIELDS OF TABLE li_sales_stock
    FROM marc INNER JOIN mska
      ON marc~matnr = mska~matnr
     AND marc~werks = mska~werks
   WHERE marc~matnr IN s_matnr
     AND marc~werks IN s_werks
     AND marc~prctr IN s_prctr
     AND marc~lvorm = space
     AND mska~lgort IN s_lgort
     AND mska~kalab > 0.
  IF sy-subrc = 0.
    LOOP AT li_sales_stock INTO lrec_stock.
      l_tabix              = sy-tabix.
      lrec_stock-bwart     = '502'.
*      lrec_stock-insmk     = .
      lrec_stock-menge     = lrec_stock-kalab.
      lrec_stock-stock_typ = 'MSKA'.
*     Get Base Unit
      PERFORM get_meins USING lrec_stock-matnr
                     CHANGING lrec_stock-meins.
      APPEND lrec_stock TO i_stock.
      MODIFY li_sales_stock FROM lrec_stock INDEX l_tabix.
      CLEAR  lrec_stock.
    ENDLOOP.
  ENDIF.

*****BEGIN OF MOD-001 INSERT
* Get customer  consignment stock
  SELECT marc~werks                    "Plant
         msku~kunnr                    "Customer
         marc~matnr                    "Material Number
         marc~prctr                    "Profit Centre
         msku~charg                    "Batch Number
         marc~sernp                    "Serial Number Profile
         marc~xchar                    "Batch management indicator
         msku~kulab                    "Consignment stock
         msku~sobkz                    "Special stock
    INTO CORRESPONDING FIELDS OF TABLE li_consi_stock
    FROM marc INNER JOIN msku
      ON marc~matnr = msku~matnr
     AND marc~werks = msku~werks
   WHERE marc~matnr IN s_matnr
     AND marc~werks IN s_werks
     AND marc~prctr IN s_prctr
     AND marc~lvorm = space
*     AND marc~xchar = space
     AND msku~kulab > 0.
  IF sy-subrc = 0.
    LOOP AT li_consi_stock INTO lrec_stock.
      l_tabix              = sy-tabix.
      lrec_stock-bwart     = '562'.
*      lrec_stock-insmk     = .
      lrec_stock-menge     = lrec_stock-kulab.
      lrec_stock-stock_typ = 'MSKU'.
*     Get Base Unit
      PERFORM get_meins USING lrec_stock-matnr
                     CHANGING lrec_stock-meins.
      APPEND lrec_stock TO i_stock.
      MODIFY li_consi_stock FROM lrec_stock INDEX l_tabix.
      CLEAR  lrec_stock.
    ENDLOOP.
  ENDIF.

* Get blocked Stock(Without batch)
  SELECT marc~werks                    "Plant
         mard~lgort                    "Storage Location
         marc~matnr                    "Material Number
         marc~prctr                    "Profit Centre
         marc~sernp                    "Serial Number Profile
         marc~xchar                    "Batch management indicator
         mard~speme                    "Block stock
    INTO CORRESPONDING FIELDS OF TABLE li_stock_block
    FROM marc INNER JOIN mard
      ON marc~matnr = mard~matnr
     AND marc~werks = mard~werks
   WHERE marc~matnr IN s_matnr
     AND marc~werks IN s_werks
     AND marc~prctr IN s_prctr
     AND marc~lvorm = space
     AND marc~xchar = space
     AND mard~lgort IN s_lgort
     AND mard~lvorm = space
     AND mard~speme > 0.
  IF sy-subrc = 0.
    LOOP AT li_stock_block INTO lrec_stock.
      l_tabix              = sy-tabix.
      lrec_stock-bwart     = '506'.
*      lrec_stock-insmk     = .
      lrec_stock-menge     = lrec_stock-speme.
      lrec_stock-stock_typ = 'MARD'.
*     Get Base Unit
      PERFORM get_meins USING lrec_stock-matnr
                     CHANGING lrec_stock-meins.
      APPEND lrec_stock TO i_stock.
      MODIFY li_stock_block FROM lrec_stock INDEX l_tabix.
      CLEAR  lrec_stock.
    ENDLOOP.
  ENDIF.

* Get blocked Stock(With batch)
  SELECT marc~werks                    "Plant
         mchb~lgort                    "Storage Location
         mchb~charg                    "Batch Number
         marc~matnr                    "Material Number
         marc~prctr                    "Profit Centre
         marc~sernp                    "Serial Number Profile
         marc~xchar                    "Batch management indicator
         mchb~cspem                    "Blocked Stock
    INTO CORRESPONDING FIELDS OF TABLE li_stock_block_batch
    FROM marc INNER JOIN mchb
      ON marc~matnr = mchb~matnr
     AND marc~werks = mchb~werks
   WHERE marc~matnr IN s_matnr
     AND marc~werks IN s_werks
     AND marc~prctr IN s_prctr
     AND marc~lvorm = space
     AND marc~xchar = 'X'
     AND mchb~lgort IN s_lgort
     AND mchb~lvorm = space
     AND mchb~cspem > 0.
  IF sy-subrc = 0.
    LOOP AT li_stock_block_batch INTO lrec_stock.
      l_tabix              = sy-tabix.
      lrec_stock-bwart     = '506'.
*      lrec_stock-insmk     = .
      lrec_stock-menge     = lrec_stock-cspem.
      lrec_stock-stock_typ = 'MARD'.
*     Get Base Unit
      PERFORM get_meins USING lrec_stock-matnr
                     CHANGING lrec_stock-meins.
      APPEND lrec_stock TO i_stock.
      MODIFY li_stock_block_batch FROM lrec_stock INDEX l_tabix.
      CLEAR  lrec_stock.
    ENDLOOP.
  ENDIF.
*****END OF MOD-001 INSERT

* no data process.
  IF i_stock[] IS INITIAL.
    MESSAGE s001.
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " GET_STOCK
*&---------------------------------------------------------------------*
*&      Form  REMOVE_STOCK
*&---------------------------------------------------------------------*
*       Remove Stock
*----------------------------------------------------------------------*
FORM remove_stock TABLES i_stock STRUCTURE rec_stock
                         i_sernr STRUCTURE rec_sernr.

  DATA: lrec_stock        TYPE ty_stock,
        li_stock_bak      TYPE TABLE OF ty_stock.

  SORT i_stock[] BY stock_typ werks
                    kunnr           " MOD-001 ADD
                    vbeln posnr lgort.
  LOOP AT i_stock[] INTO lrec_stock.
*   Serial Number Profile
    IF lrec_stock-sernp   = 'Z001'.
      PERFORM set_sernr TABLES li_stock_bak
                               i_sernr
                         USING lrec_stock.
    ELSE.
      APPEND lrec_stock TO li_stock_bak.
    ENDIF.

********** Begin Goods Movement Create **********
    AT END OF posnr.
      PERFORM goodsmvt_create TABLES li_stock_bak.
      REFRESH li_stock_bak.
    ENDAT.
********** End   Goods Movement Create **********
    CLEAR lrec_stock.
  ENDLOOP.

ENDFORM.                    " REMOVE_STOCK
*&---------------------------------------------------------------------*
*&      Form  GOODSMVT_CREATE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM goodsmvt_create  TABLES i_stock STRUCTURE rec_stock.

  DATA: lrec_stock        TYPE ty_stock,
        gm_head           TYPE bapi2017_gm_head_01,
        gm_code           TYPE bapi2017_gm_code,
        lrec_item         TYPE bapi2017_gm_item_create,
        lrec_gm_sernr     TYPE bapi2017_gm_serialnumber,
        mat_doc           TYPE bapi2017_gm_head_ret,
        lrec_return       TYPE bapiret2,
        lrec_stock_chk    TYPE ty_stock.

  DATA: gm_item           TYPE TABLE OF bapi2017_gm_item_create,
        gm_sernr          TYPE TABLE OF bapi2017_gm_serialnumber,
        return            TYPE TABLE OF bapiret2,
        li_stock_chk      TYPE TABLE OF ty_stock.

  DATA: l_lines           TYPE i,
        l_matdoc_itm      TYPE i,
        lflg_last_proc    TYPE c,
        l_msg             TYPE bapi_msg.

* GMCODE Table T158G - 01 - MB01 - Goods Receipts for Purchase Order
*                      02 - MB31 - Goods Receipts for Prod Order
*                      03 - MB1A - Goods Issue
*                      04 - MB1B - Transfer Posting
*                      05 - MB1C - Enter Other Goods Receipt
*                      06 - MB11 - Enter Goods Movement

  CHECK i_stock[] IS NOT INITIAL.

  gm_head-pstng_date = s_budat-low.
  gm_head-doc_date   = sy-datum.
  gm_head-pr_uname   = sy-uname.
  gm_head-header_txt = s_bktxt-low.
  gm_code-gm_code    = '05'.

  LOOP AT i_stock INTO lrec_stock.
    lrec_item-material        = lrec_stock-matnr.
    lrec_item-plant           = lrec_stock-werks.
    lrec_item-stge_loc        = lrec_stock-lgort.
    lrec_item-batch           = lrec_stock-charg.
    lrec_item-move_type       = lrec_stock-bwart.
*    lrec_item-stck_type       = lrec_stock-insmk.
    lrec_item-spec_stock      = lrec_stock-sobkz.
    lrec_item-customer        = lrec_stock-kunnr. "MOD-001 INSERT
    lrec_item-val_sales_ord   = lrec_stock-vbeln.
    lrec_item-val_s_ord_item  = lrec_stock-posnr.
    lrec_item-entry_uom       = lrec_stock-meins.
    lrec_item-entry_qnt       = lrec_stock-menge.
    APPEND lrec_item TO gm_item.
    CLEAR  lrec_item.

    IF lrec_stock-sernp      = 'Z001'.
      l_matdoc_itm             = l_matdoc_itm + 1.
      lrec_gm_sernr-matdoc_itm = l_matdoc_itm.
      lrec_gm_sernr-serialno   = lrec_stock-sernr.
      APPEND lrec_gm_sernr TO gm_sernr.
      CLEAR  lrec_gm_sernr.
    ENDIF.

    APPEND lrec_stock TO li_stock_chk.
    CLEAR  lrec_stock.

    DESCRIBE TABLE gm_item LINES l_lines.
    IF l_lines = p_maxpo.
      lflg_last_proc = space.
      CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
        EXPORTING
          goodsmvt_header       = gm_head
          goodsmvt_code         = gm_code
        IMPORTING
          goodsmvt_headret      = mat_doc
        TABLES
          goodsmvt_item         = gm_item
          goodsmvt_serialnumber = gm_sernr
          return                = return.

      LOOP AT return INTO lrec_return WHERE type = 'E'
                                         OR type = 'A'
                                         OR type = 'X'.
*        LOOP AT li_stock_chk INTO lrec_stock_chk.
*          PERFORM set_log USING lrec_stock_chk   mat_doc
*                                lrec_return-type lrec_return-message.
*          CLEAR lrec_stock_chk.
*        ENDLOOP.
        READ TABLE gm_item INTO lrec_item INDEX lrec_return-row.
        PERFORM set_log USING mat_doc
                              lrec_return-type lrec_return-message
                              lrec_item-material
                              .
      ENDLOOP.
      IF sy-subrc <> 0.
        COMMIT WORK AND WAIT.
        MESSAGE s060(m7) WITH mat_doc-mat_doc INTO l_msg.
*        PERFORM set_log USING lrec_stock_chk mat_doc 'S' l_msg.
        PERFORM set_log USING mat_doc
                              'S' l_msg ''.
      ENDIF.
      CLEAR:   l_matdoc_itm.
      REFRESH: gm_item, gm_sernr, return, li_stock_chk.
    ELSE.
      lflg_last_proc = 'X'.
    ENDIF.
  ENDLOOP.

  IF lflg_last_proc = 'X'.
    CALL FUNCTION 'BAPI_GOODSMVT_CREATE'
      EXPORTING
        goodsmvt_header       = gm_head
        goodsmvt_code         = gm_code
      IMPORTING
        goodsmvt_headret      = mat_doc
      TABLES
        goodsmvt_item         = gm_item
        goodsmvt_serialnumber = gm_sernr
        return                = return.

    LOOP AT return INTO lrec_return WHERE type = 'E'
                                       OR type = 'A'
                                       OR type = 'X'.
*      LOOP AT li_stock_chk INTO lrec_stock_chk.
*        PERFORM set_log USING lrec_stock_chk   mat_doc
*                              lrec_return-type lrec_return-message.
*        CLEAR lrec_stock_chk.
*      ENDLOOP.
      READ TABLE gm_item INTO lrec_item INDEX lrec_return-row.
      PERFORM set_log USING mat_doc
                            lrec_return-type lrec_return-message
                            lrec_item-material.
    ENDLOOP.
    IF sy-subrc <> 0.
      COMMIT WORK AND WAIT.
      MESSAGE s060(m7) WITH mat_doc-mat_doc INTO l_msg.
*      PERFORM set_log USING lrec_stock_chk mat_doc 'S' l_msg.
      PERFORM set_log USING mat_doc
                            'S' l_msg ''.
    ENDIF.
  ENDIF.
  CLEAR:   l_matdoc_itm.
  REFRESH: gm_item, gm_sernr, return, li_stock_chk.

ENDFORM.                    " GOODSMVT_CREATE
*&---------------------------------------------------------------------*
*&      Form  SET_SERNR
*&---------------------------------------------------------------------*
*       Get Serial Number
*----------------------------------------------------------------------*
FORM set_sernr  TABLES i_stock STRUCTURE rec_stock
                       i_sernr STRUCTURE rec_sernr
                 USING stock   TYPE ty_stock.

  DATA: lrec_stock   TYPE ty_stock,
        lrec_sernr   TYPE ty_sernr.
  DATA: l_lines_proc TYPE i.
  DATA: l_subrc      TYPE sy-subrc,
        l_mat_doc    TYPE bapi2017_gm_head_ret,
        l_msg        TYPE bapi_msg.

  lrec_stock = stock.
  CASE stock-stock_typ.
    WHEN 'MARD'.
      LOOP AT i_sernr[] INTO lrec_sernr
                     WHERE b_werk  = stock-werks
                       AND b_lager = stock-lgort
                       AND matnr   = stock-matnr.
        l_lines_proc = l_lines_proc + 1.
        IF l_lines_proc > stock-menge.
          EXIT.
        ENDIF.
        lrec_stock-menge         = 1.
        lrec_stock-sernr         = lrec_sernr-sernr.
        APPEND lrec_stock TO i_stock.
      ENDLOOP.
      l_subrc = sy-subrc.
    WHEN 'MCHB'.
      LOOP AT i_sernr[] INTO lrec_sernr
                     WHERE b_werk   = stock-werks
                       AND b_lager  = stock-lgort
                       AND matnr    = stock-matnr
                       AND b_charge = stock-charg.
        l_lines_proc = l_lines_proc + 1.
        IF l_lines_proc > stock-menge.
          EXIT.
        ENDIF.
        lrec_stock-menge         = 1.
        lrec_stock-sernr         = lrec_sernr-sernr.
        APPEND lrec_stock TO i_stock.
      ENDLOOP.
      l_subrc = sy-subrc.
    WHEN 'MSKA'.
      LOOP AT i_sernr[] INTO lrec_sernr
                     WHERE b_werk   = stock-werks
                       AND b_lager  = stock-lgort
                       AND matnr    = stock-matnr
                       AND b_charge = stock-charg
                       AND sobkz    = stock-sobkz
                       AND kdauf    = stock-vbeln
                       AND kdpos    = stock-posnr.
        l_lines_proc = l_lines_proc + 1.
        IF l_lines_proc > stock-menge.
          EXIT.
        ENDIF.
        lrec_stock-menge         = 1.
        lrec_stock-sernr         = lrec_sernr-sernr.
        APPEND lrec_stock TO i_stock.
      ENDLOOP.
*******BEGIN OF MOD-001 INSERT
    WHEN 'MSKU'.
      LOOP AT i_sernr[] INTO lrec_sernr
                     WHERE b_werk   = stock-werks
                       AND matnr    = stock-matnr
                       AND b_charge = stock-charg
                       AND sobkz    = stock-sobkz.
        l_lines_proc = l_lines_proc + 1.
        IF l_lines_proc > stock-menge.
          EXIT.
        ENDIF.
        lrec_stock-menge         = 1.
        lrec_stock-sernr         = lrec_sernr-sernr.
        APPEND lrec_stock TO i_stock.
      ENDLOOP.
*******END  OF MOD-001 INSERT
      l_subrc = sy-subrc.
  ENDCASE.

  IF l_subrc <> 0.
    MESSAGE s008 INTO l_msg WITH stock-sernp stock-matnr
                                 stock-werks stock-lgort.
*    PERFORM set_log USING stock l_mat_doc 'E' l_msg.
    PERFORM set_log USING l_mat_doc
                          'E' l_msg stock-matnr.
  ENDIF.

ENDFORM.                    " SET_SERNR
*&---------------------------------------------------------------------*
*&      Form  GET_SERNR
*&---------------------------------------------------------------------*
*       Get Serial Number
*----------------------------------------------------------------------*
FORM get_sernr  TABLES i_sernr STRUCTURE rec_sernr.

  SELECT * INTO CORRESPONDING FIELDS OF TABLE i_sernr
    FROM equi INNER JOIN eqbs ON equi~equnr = eqbs~equnr
   WHERE eqbs~b_werk  IN s_werks
     AND eqbs~b_lager IN s_lgort
     AND equi~matnr   IN s_matnr
     AND equi~lvorm   =  space.
  SORT i_sernr BY     b_werk b_lager b_charge
                      sobkz kdauf kdpos matnr sernr.

ENDFORM.                    " GET_SERNR
*&---------------------------------------------------------------------*
*&      Form  GET_MEINS
*&---------------------------------------------------------------------*
*       Get Base Unit
*----------------------------------------------------------------------*
FORM get_meins  USING    matnr TYPE mara-matnr
                CHANGING meins TYPE mara-meins.
  CLEAR meins.
  SELECT SINGLE meins INTO meins FROM mara WHERE matnr = matnr.

ENDFORM.                    " GET_MEINS

*Text symbol text£º
*B01:Stock Data

*B02:Material Doc.
*Selection text£º
*P_MAXPO:        Maximum No. of Document Items
*S_BKTXT:        Head Text
*S_BUDAT:        Last Day of Previous
*S_LGORT:        Storage Loc.
*S_MATNR:        Material
*S_PRCTR:        Profit Centre
*S_WERKS:        Plant
