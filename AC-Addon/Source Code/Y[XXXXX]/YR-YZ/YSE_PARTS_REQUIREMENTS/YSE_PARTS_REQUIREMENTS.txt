*&---------------------------------------------------------------------*
*& Report  YSE_PARTS_REQUIREMENTS
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*&  Future requirements for parts indicator program                    *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 25.06.2010
*  Change Request Number : CR1049
*  Transport request Nr. : CD1K957636
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.| DATE       | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | xx/xx/xxxx | X               | CD1K9xxxxx    | CRxxxx      *
*         | Text                                                       *
*----------------------------------------------------------------------*

REPORT  yse_parts_requirements.

TYPE-POOLS: slis.

* Tables
TABLES: vbak,
        viqmel,
        caufv,
        afih,
        afvv,
        iloa,
        resb,
        pmsdo,
        crhd,
        plas,
        plpo,
        plmz,
        stpo,
        mpla,
        mpos,
        mhis,
        rmipm,
        yam_ctam_ccodes,
        t024i.

* Global definitions
TYPES: BEGIN OF ty_caufv,
         aufnr     LIKE caufv-aufnr,
         bukrs     LIKE caufv-bukrs,
         objnr     LIKE caufv-objnr,
         equnr     LIKE afih-equnr,
         serialnr  LIKE afih-serialnr,
         plnnr     LIKE caufv-plnnr,
         plnal     LIKE caufv-plnal,
         kdauf     LIKE caufv-kdauf,
         kdpos     LIKE caufv-kdpos,
         bemot     LIKE caufv-bemot,
       END OF ty_caufv.
DATA: gt_caufv  TYPE TABLE OF ty_caufv
                     WITH HEADER LINE,
      gt_caufvh TYPE HASHED TABLE OF ty_caufv
                     WITH UNIQUE KEY aufnr.

DATA: BEGIN OF gt_resb OCCURS 0,
        aufnr     LIKE resb-aufnr,
        rsnum     LIKE resb-rsnum,
        rspos     LIKE resb-rspos,
        rsart     LIKE resb-rsart,
        matnr     LIKE resb-matnr,
        meins     LIKE resb-meins,
        waers     LIKE resb-waers,
        bdmng     LIKE resb-bdmng,
        bdter     LIKE resb-bdter,
        wempf     LIKE resb-wempf,
        aufpl     LIKE resb-aufpl,
        gpreis    LIKE resb-gpreis,
        peinh     LIKE resb-peinh,
        objnr     LIKE resb-objnr,
*        fsavd     LIKE afvv-fsavd,
      END OF gt_resb.

TYPES: BEGIN OF ty_pmsdo,
         objnr     LIKE pmsdo-objnr,
         matnr     LIKE pmsdo-matnr,
       END OF ty_pmsdo.
DATA: gt_pmsdo  TYPE HASHED TABLE OF ty_pmsdo
                     WITH UNIQUE KEY objnr
                     WITH HEADER LINE.

TYPES: BEGIN OF ty_mara,
         matnr     LIKE mara-matnr,
         mtart     LIKE mara-mtart,
       END OF ty_mara.
DATA: gt_marai TYPE TABLE OF ty_mara,
      gt_mara  TYPE HASHED TABLE OF ty_mara
                    WITH UNIQUE KEY matnr
                    WITH HEADER LINE.

DATA: BEGIN OF gt_mplan OCCURS 0,
        warpl     LIKE mpla-warpl,
        abnum     LIKE mhis-abnum,
        zaehl     LIKE mhis-zaehl,
        wapos     LIKE mpos-wapos,
        equnr     LIKE mpos-equnr,
        serialnr  LIKE mpos-serialnr,
        plan_sort LIKE mpla-plan_sort,
        pstxt     LIKE mpos-pstxt,
        plnty     LIKE mpos-plnty,
        plnnr     LIKE mpos-plnnr,
        plnal     LIKE mpos-plnal,
        wpgrp     LIKE mpos-wpgrp,
        gewrk     LIKE mpos-gewrk,
        iwerk     LIKE mpos-iwerk,
        kdauf     LIKE mpos-kdauf,
        kdpos     LIKE mpos-kdpos,
        nplda     LIKE mhis-nplda,
        wppos     LIKE mhio-wppos,
        bukrs     LIKE vbak-bukrs_vf,
      END OF gt_mplan.

DATA: BEGIN OF gt_final OCCURS 0,
        warpl        LIKE mpla-warpl,
        wapos        LIKE mpos-wapos,
        pstxt        LIKE mpos-pstxt,
        abnum        LIKE mhis-abnum,
        plan_sort    LIKE mpla-plan_sort,
        equnr        LIKE mpos-equnr,
        serialnr     LIKE mpos-serialnr,
        eqktx        LIKE eqkt-eqktx,
        plnnr        LIKE mpos-plnnr,
        plnal        LIKE mpos-plnal,
        parnr        LIKE ihpa-parnr,
        parnr_name   LIKE kna1-name1,
        billto       LIKE ihpa-parnr,
        billto_name  LIKE kna1-name1,
        aufnr        LIKE caufv-aufnr,
        kdauf        LIKE mpos-kdauf,
        kdpos        LIKE mpos-kdpos,
        idnrk        LIKE stpo-idnrk,
        meins        LIKE stpo-meins,
        spp_cost(7)  TYPE p DECIMALS 2,
        waers        LIKE stpo-waers,
        menge        LIKE stpo-menge,
        nplda        LIKE mhis-nplda,
        wempf        LIKE resb-wempf,
        sttxt        LIKE bsvx-sttxt,
        selected,
      END OF gt_final.

DATA: BEGIN OF gt_plas OCCURS 0,
        plnty     LIKE plas-plnty,
        plnnr     LIKE plas-plnnr,
        plnal     LIKE plas-plnal,
        plnfl     LIKE plas-plnfl,
        plnkn     LIKE plas-plnkn,
        zaehl     LIKE plas-zaehl,
      END OF gt_plas.

DATA: BEGIN OF gt_plpo OCCURS 0,
        plnty     LIKE plas-plnty,
        plnnr     LIKE plas-plnnr,
        plnkn     LIKE plas-plnkn,
        zaehl     LIKE plas-zaehl,
        steus     LIKE plpo-steus,
        larnt     LIKE plpo-larnt,
        arbei     LIKE plpo-arbei,
        arbeh     LIKE plpo-arbeh,
        werks     LIKE plpo-werks,
      END OF gt_plpo.

DATA: BEGIN OF gt_plmz OCCURS 0,
        plnty     LIKE plas-plnty,
        plnnr     LIKE plas-plnnr,
        plnkn     LIKE plas-plnkn,
        stlty     LIKE plmz-stlty,
        stlnr     LIKE plmz-stlnr,
        stlkn     LIKE plmz-stlkn,
        imeng     LIKE plmz-imeng,
        imein     LIKE plmz-imein,
      END OF gt_plmz.

TYPES: BEGIN OF ty_stpo,
         stlty     LIKE stpo-stlty,
         stlnr     LIKE stpo-stlnr,
         stlkn     LIKE stpo-stlkn,
         idnrk     LIKE stpo-idnrk,
         menge     LIKE stpo-menge,
         meins     LIKE stpo-meins,
       END OF ty_stpo.
DATA: gt_stpoi TYPE TABLE OF ty_stpo,
      gt_stpo  TYPE HASHED TABLE OF ty_stpo
                    WITH UNIQUE KEY stlty stlnr stlkn
                    WITH HEADER LINE.

TYPES: BEGIN OF ty_eqkt,
         equnr     LIKE eqkt-equnr,
         spras     LIKE eqkt-spras,
         eqktx     LIKE eqkt-eqktx,
       END OF ty_eqkt.
DATA: gt_eqkti TYPE TABLE OF ty_eqkt,
      gt_eqkt  TYPE HASHED TABLE OF ty_eqkt
                    WITH UNIQUE KEY equnr spras
                    WITH HEADER LINE.

DATA: gt_basic_data  TYPE cuvtab           OCCURS 0  WITH HEADER LINE,
      gt_value_n     TYPE cuvtab_valn      OCCURS 0  WITH HEADER LINE,
      gt_value_c     TYPE cuvtab_valc      OCCURS 0  WITH HEADER LINE,
      gt_ctam_ccodes TYPE HASHED TABLE OF yam_ctam_ccodes
                          WITH UNIQUE KEY bukrs
                          WITH HEADER LINE,
      gt_t001        TYPE HASHED TABLE OF t001
                          WITH UNIQUE KEY bukrs
                          WITH HEADER LINE.

DATA: gs_caufv         TYPE ty_caufv,
      gs_resb          LIKE gt_resb.

DATA: gv_ctam          TYPE xfeld,
      gv_rel           TYPE xfeld,
      gv_text          TYPE char80,
      gv_objnr_eq      LIKE ihpa-objnr,
      gv_eqktx         LIKE eqkt-eqktx,
      gv_parnr         LIKE ihpa-parnr,
      gv_billto        LIKE ihpa-parnr,
      gv_name1         LIKE kna1-name1,
      gv_namebt        LIKE kna1-name1,
      gv_objnr_warpl   LIKE jest-objnr,
      gv_objnr_equi    LIKE ihpa-objnr,
      gv_spp_cost      TYPE stprs,
      gv_spp_unit      TYPE peinh,
      gv_waers         TYPE waers,
      gv_sttxt         TYPE j_stext,
      gv_index         TYPE sy-tabix,
      gv_repid         LIKE ceddb-program,
      gv_ic1           LIKE sy-ucomm VALUE '&IC1',
      gt_fieldcat      TYPE slis_t_fieldcat_alv,
      g_events_tab     TYPE slis_t_event,
      g_form_user_command TYPE slis_formname VALUE 'USER_COMMAND_L'.

* Global constants
CONSTANTS: gc_x(1)        TYPE c       VALUE 'X',
           gc_zero(8)     TYPE c       VALUE '00000000',
           gc_fp_ctam     TYPE bemot   VALUE 'FP',
           gc_sp_ctam     TYPE bemot   VALUE 'SP',
           gc_fp_seed     TYPE matkl   VALUE '042ZFP',
           gc_sp_seed     TYPE matkl   VALUE '043ZSP',
           gc_ag          TYPE parvw   VALUE 'AG',
           gc_re          TYPE parvw   VALUE 'RE',
           gc_ie(2)       TYPE c       VALUE 'IE',
           gc_z_vc_table  TYPE vtnam   VALUE 'Z_VC_TABLE',
           gc_country(20) TYPE c       VALUE 'CH_EQUIPMENT_COUNTRY',
           gc_gmix        TYPE werks_d VALUE 'GMIX',
           gc_zam001      TYPE lstar   VALUE 'ZAM001',
           gc_zam010      TYPE lstar   VALUE 'ZAM010',
           gc_tl9080      TYPE ktsch   VALUE 'TL9080',
           gc_zco3        TYPE steus   VALUE 'ZCO3'.


*.................. Layout selection screen............................*
SELECTION-SCREEN BEGIN OF BLOCK b1  WITH FRAME  TITLE text-b01.
SELECT-OPTIONS: s_vkorg  FOR  vbak-vkorg   OBLIGATORY  MEMORY ID vko
                                           NO INTERVALS  NO-EXTENSION,
                s_iwerk  FOR  mpos-iwerk   MEMORY ID wrk,
                s_idnrk  FOR  stpo-idnrk,
                s_aufnr  FOR  caufv-aufnr,
                s_nplda  FOR  mhis-nplda   OBLIGATORY,
                s_kdauf  FOR  vbak-vbeln   MATCHCODE OBJECT vmva,
                s_warpl  FOR  mpla-warpl,
                s_equnr  FOR  mpos-equnr,
                s_sernr  FOR  mpos-serialnr.
SELECTION-SCREEN END OF BLOCK b1.

*
*..................  MAIN PROGRAM  ....................................*

START-OF-SELECTION.

  CALL METHOD ycl_statistics=>record_transaction.

  PERFORM check_authorization.

  SELECT * INTO TABLE gt_ctam_ccodes
           FROM yam_ctam_ccodes.

  SELECT * INTO TABLE gt_t001
           FROM t001.

* Select service orders (Selection given)
  IF NOT s_aufnr[] IS INITIAL.
    PERFORM select_servord.
  ENDIF.

* Select maintenance plans (Selection given)
  IF NOT s_warpl[] IS INITIAL.
    PERFORM select_maintplan.
  ENDIF.

* Select service orders & maintenance plans (No selection given)
  IF s_aufnr[] IS INITIAL  AND
     s_warpl[] IS INITIAL.
    PERFORM select_servord.
    PERFORM select_maintplan.
  ENDIF.

* create ALV-GRID
  SORT gt_final.
  PERFORM build_field_catlog CHANGING gt_fieldcat.
  PERFORM fill_events_f14.
  PERFORM alv_display.

*
*..................  FORMS  ...........................................*

*&---------------------------------------------------------------------*
*&      Form  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorization (for sales organization)
*----------------------------------------------------------------------*
FORM check_authorization .

  LOOP AT s_vkorg.
    AUTHORITY-CHECK OBJECT 'V_VBAK_VKO'
                        ID 'VKORG' FIELD s_vkorg-low
                        ID 'VTWEG' DUMMY
                        ID 'SPART' DUMMY
                        ID 'ACTVT' DUMMY.

    IF sy-subrc = 4.
*   No authorisation to display data from Sales Organisation
      MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '006' WITH s_vkorg-low.
    ELSEIF sy-subrc <> 0.
*   Error checking authorization.
      MESSAGE ID 'YSE_RENTAL' TYPE 'E' NUMBER '046'.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  SELECT_SERVORD
*&---------------------------------------------------------------------*
*       Select service orders
*----------------------------------------------------------------------*
FORM select_servord .

* Progress indicator
  gv_text = 'Service Orders are being selected'(i99).
  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      text = gv_text.

  SELECT c~aufnr c~bukrs c~objnr a~equnr a~serialnr
         c~plnnr c~plnal c~kdauf c~kdpos c~bemot
         INTO TABLE gt_caufv
         FROM caufv AS c
         INNER JOIN afih AS a
                    ON c~aufnr = a~aufnr
         INNER JOIN iloa AS i
                    ON a~iloan = i~iloan
         WHERE c~aufnr    IN s_aufnr
           AND c~kdauf    IN s_kdauf
           and c~loekz    =  ' '
           AND i~vkorg    IN s_vkorg
           AND a~iwerk    IN s_iwerk
           AND a~equnr    IN s_equnr
           AND a~serialnr IN s_sernr
           AND a~iphas    IN ('0', '1', '2').

  CHECK NOT gt_caufv[] IS INITIAL.

  SORT gt_caufv BY equnr.

  CLEAR gt_eqkti[].
  SELECT equnr spras eqktx INTO TABLE gt_eqkti
         FROM eqkt
         FOR ALL ENTRIES IN gt_caufv
         WHERE equnr = gt_caufv-equnr
           AND spras = sy-langu.
  SORT gt_eqkti BY equnr spras.
  DELETE ADJACENT DUPLICATES FROM gt_eqkti
         COMPARING equnr spras.
  gt_eqkt[] = gt_eqkti[].
  FREE gt_eqkti[].

  SORT gt_caufv BY objnr.

  SELECT objnr matnr INTO TABLE gt_pmsdo
         FROM pmsdo
         FOR ALL ENTRIES IN gt_caufv
         WHERE objnr = gt_caufv-objnr.

  SORT gt_pmsdo BY matnr.

  SELECT matnr mtart INTO TABLE gt_marai
         FROM mara
         FOR ALL ENTRIES IN gt_pmsdo
         WHERE matnr = gt_pmsdo-matnr.

  SORT gt_marai BY matnr.
  DELETE ADJACENT DUPLICATES FROM gt_marai
         COMPARING matnr.
  gt_mara[] = gt_marai[].
  FREE gt_marai[].

  SORT: gt_caufv,
        gt_pmsdo.

  LOOP AT gt_caufv.

*   Progress indicator
    CONCATENATE 'Service Order'(i08) gt_caufv-aufnr 'is being processed'(i98)
                INTO gv_text  SEPARATED BY space.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        text = gv_text.

*   Check status 'Technically completed'
    CALL FUNCTION 'STATUS_CHECK'
      EXPORTING
        objnr             = gt_caufv-objnr
        status            = 'I0045'
      EXCEPTIONS
        object_not_found  = 1
        status_not_active = 2
        OTHERS            = 3.
    IF sy-subrc EQ 0.
      DELETE gt_caufv.
      CONTINUE.
    ENDIF.
*   Check status 'Closed'
    CALL FUNCTION 'STATUS_CHECK'
      EXPORTING
        objnr             = gt_caufv-objnr
        status            = 'I0046'
      EXCEPTIONS
        object_not_found  = 1
        status_not_active = 2
        OTHERS            = 3.
    IF sy-subrc EQ 0.
      DELETE gt_caufv.
      CONTINUE.
    ENDIF.

    READ TABLE gt_ctam_ccodes WITH TABLE KEY bukrs = gt_caufv-bukrs.
    IF sy-subrc = 0.
*     CTAM
      IF gt_caufv-bemot NE gc_fp_ctam  AND
         gt_caufv-bemot NE gc_sp_ctam.
        DELETE gt_caufv.
        CONTINUE.
      ENDIF.
    ELSE.
*     SEED
      CLEAR gt_mara.
      READ TABLE gt_pmsdo WITH TABLE KEY objnr = gt_caufv-objnr.
      IF sy-subrc = 0.
        READ TABLE gt_mara WITH TABLE KEY matnr = gt_pmsdo-matnr.
      ENDIF.
      IF gt_mara-mtart NE gc_fp_seed  AND
         gt_mara-mtart NE gc_sp_seed.
        DELETE gt_caufv.
        CONTINUE.
      ENDIF.
    ENDIF.
  ENDLOOP.

  CHECK NOT gt_caufv[] IS INITIAL.

  FREE: gt_pmsdo, gt_mara.

  SELECT r~aufnr r~rsnum r~rspos r~rsart r~matnr r~meins
         r~waers r~bdmng r~bdter r~wempf r~aufpl r~gpreis
         r~peinh r~objnr
*        a~fsavd
         INTO TABLE gt_resb
         FROM resb AS r
*         INNER JOIN afvv AS a
*                    ON r~aufpl = a~aufpl
         FOR ALL ENTRIES IN gt_caufv
         WHERE r~aufnr  = gt_caufv-aufnr
           AND r~matnr IN s_idnrk
           AND r~bdter IN s_nplda.
*           AND r~bdter GE sy-datum.

  SORT gt_resb.

  gt_caufvh[] = gt_caufv[].
  FREE gt_caufv.

  LOOP AT gt_resb INTO gs_resb.

*   Progress indicator
    CONCATENATE 'Service Order'(i08) gs_resb-aufnr 'is being processed'(i98)
                INTO gv_text  SEPARATED BY space.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        text = gv_text.

    AT NEW aufnr.
      CLEAR gs_caufv.
      READ TABLE gt_caufvh INTO gs_caufv
                           WITH TABLE KEY aufnr = gs_resb-aufnr.
      READ TABLE gt_ctam_ccodes WITH TABLE KEY bukrs = gs_caufv-bukrs.
      IF sy-subrc = 0.
        gv_ctam = 'X'.
      ELSE.
        CLEAR: gv_ctam,
               gv_rel.
        CALL FUNCTION 'STATUS_CHECK'
          EXPORTING
            objnr             = gs_caufv-objnr
            status            = 'I0002'         "system status 'REL'
          EXCEPTIONS
            object_not_found  = 1
            status_not_active = 2
            OTHERS            = 3.
        IF sy-subrc = 0.
          gv_rel = 'X'.
        ENDIF.
      ENDIF.
      CALL FUNCTION 'STATUS_TEXT_EDIT'
        EXPORTING
          objnr            = gs_caufv-objnr
          only_active      = 'X'
          spras            = sy-langu
        IMPORTING
          line             = gv_sttxt
        EXCEPTIONS
          object_not_found = 1
          OTHERS           = 2.
      IF sy-subrc <> 0.
*        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
      ENDIF.

*     Select customer (sold-to)
      CONCATENATE gc_ie gs_caufv-equnr INTO gv_objnr_eq.
      SELECT SINGLE parnr INTO gv_parnr
          FROM ihpa
          WHERE objnr    = gv_objnr_eq
            AND parvw    = gc_ag
            AND kzloesch = space.
      IF sy-subrc = 0.
        SELECT SINGLE name1 INTO gv_name1
            FROM kna1
            WHERE kunnr = gv_parnr.
      ENDIF.

*     Select bill-to
      SELECT SINGLE parnr INTO gv_billto
          FROM ihpa
          WHERE objnr    = gv_objnr_eq
            AND parvw    = gc_re
            AND kzloesch = space.
      IF sy-subrc = 0.
        SELECT SINGLE name1 INTO gv_namebt
            FROM kna1
            WHERE kunnr = gv_billto.
      ENDIF.

*     Select the equipment description
      CLEAR gt_eqkt.
      READ TABLE gt_eqkt WITH TABLE KEY equnr = gs_caufv-equnr
                                        spras = sy-langu.
      gv_eqktx = gt_eqkt-eqktx.

*     Select currency
      CLEAR gt_t001.
      READ TABLE gt_t001 WITH TABLE KEY bukrs = gs_caufv-bukrs.
      gt_final-waers = gt_t001-waers.

    ENDAT.

*   Check deletion indicator
    CALL FUNCTION 'STATUS_CHECK'
      EXPORTING
        objnr             = gs_resb-objnr
        status            = 'I0013'
      EXCEPTIONS
        object_not_found  = 1
        status_not_active = 2
        OTHERS            = 3.
    IF sy-subrc = 0.
      CONTINUE.
*    ELSE.
*      MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*              WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    ENDIF.

    IF gv_ctam IS INITIAL.
      CHECK NOT gv_rel IS INITIAL.
    ELSE.
      CHECK NOT gs_resb IS INITIAL.
    ENDIF.

    CLEAR gt_final.
    MOVE-CORRESPONDING gs_caufv TO gt_final.
    MOVE-CORRESPONDING gs_resb  TO gt_final.
    WRITE gs_caufv-equnr TO gt_final-equnr NO-ZERO.
    WRITE gs_caufv-serialnr TO gt_final-serialnr NO-ZERO.
    WRITE gs_caufv-kdauf TO gt_final-kdauf NO-ZERO.
    gt_final-idnrk    = gs_resb-matnr.
*    gt_final-nplda    = gs_resb-fsavd.
    gt_final-nplda    = gs_resb-bdter.
    gt_final-spp_cost = gs_resb-gpreis / gs_resb-peinh.
    gt_final-menge    = gs_resb-bdmng.
    gt_final-sttxt    = gv_sttxt.
    WRITE gv_parnr TO gt_final-parnr NO-ZERO.
    gt_final-parnr_name = gv_name1.
    WRITE gv_billto TO gt_final-billto NO-ZERO.
    gt_final-billto_name = gv_namebt.
    gt_final-eqktx = gv_eqktx.

    APPEND gt_final.
    CLEAR gt_final.

  ENDLOOP.

  FREE gt_caufvh.

ENDFORM.                    " SELECT_SERVORD

*&---------------------------------------------------------------------*
*&      Form  SELECT_MAINTPLAN
*&---------------------------------------------------------------------*
*       Select maintenance plans
*----------------------------------------------------------------------*
FORM select_maintplan .

* Progress indicator
  gv_text = 'Maintenance Plans are being selected'(i97).
  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      text = gv_text.

  SELECT a~warpl c~abnum c~zaehl b~wapos b~equnr b~serialnr
         a~plan_sort b~pstxt b~plnty b~plnnr b~plnal
         b~wpgrp b~gewrk b~iwerk b~kdauf b~kdpos c~nplda
         d~wppos v~bukrs_vf
         INTO TABLE gt_mplan
         FROM mpla AS a
         INNER JOIN mpos AS b
               ON a~warpl = b~warpl
         INNER JOIN mhis AS c
               ON c~warpl = a~warpl
         INNER JOIN mhio AS d
               ON d~warpl = c~warpl AND
                  d~abnum = c~abnum AND
                  d~wppos = b~wapos
         INNER JOIN vbak AS v
               ON v~vbeln = b~kdauf
         WHERE a~warpl    IN s_warpl
           AND b~equnr    IN s_equnr
           AND b~kdauf    IN s_kdauf
           AND b~iwerk    IN s_iwerk
           AND b~serialnr IN s_sernr
           AND v~vkorg    IN s_vkorg
           AND c~nplda    IN s_nplda
*           AND c~nplda    GE sy-datum
           AND c~tstat    NE gc_x
           AND ( c~lrmdt EQ gc_zero OR
                 c~lrmdt EQ space ).

  SORT gt_mplan BY equnr.

  CLEAR gt_eqkti[].
  SELECT equnr spras eqktx INTO TABLE gt_eqkti
         FROM eqkt
         FOR ALL ENTRIES IN gt_mplan
         WHERE equnr = gt_mplan-equnr
           AND spras = sy-langu.
  SORT gt_eqkti BY equnr spras.
  DELETE ADJACENT DUPLICATES FROM gt_eqkti
         COMPARING equnr spras.
  gt_eqkt[] = gt_eqkti[].
  FREE gt_eqkti[].

  SORT gt_mplan.

*   Select only the active maintenance plans
*   not deleted / not inactive
  LOOP AT gt_mplan.

    CONCATENATE 'WO' gt_mplan-warpl INTO gv_objnr_warpl.

    CALL FUNCTION 'STATUS_CHECK'
      EXPORTING
        objnr             = gv_objnr_warpl
        status            = 'I0320'
      EXCEPTIONS
        object_not_found  = 1
        status_not_active = 2
        OTHERS            = 3.
    IF sy-subrc EQ 0.
      DELETE gt_mplan.
      CONTINUE.
    ELSE.
      CALL FUNCTION 'STATUS_CHECK'
        EXPORTING
          objnr             = gv_objnr_warpl
          status            = 'I0076'
        EXCEPTIONS
          object_not_found  = 1
          status_not_active = 2
          OTHERS            = 3.
      IF sy-subrc EQ 0.
        DELETE gt_mplan.
        CONTINUE.
      ENDIF.
    ENDIF.
  ENDLOOP.

  IF gt_mplan[] IS INITIAL.
    EXIT.
  ENDIF.

* Preselect variant table for central task lists
  CALL FUNCTION 'CUTX_INIT_STRUC_ENTRY_PLANNING'
    EXPORTING
      var_tab                = gc_z_vc_table
    TABLES
      et_basic_data          = gt_basic_data
*     ET_TABLE_TEXTS         =
*     ET_TABLE_FIELDS        =
*     ET_TABLE_INDICES       =
*     ET_TABLE_LINE          =
      et_table_value_n       = gt_value_n
      et_table_value_c       = gt_value_c
    EXCEPTIONS
      table_not_found        = 1
      OTHERS                 = 2.

  IF sy-subrc <> 0.
*    WRITE: / text-i02.              "No variant table available
    EXIT.
  ENDIF.

  SORT gt_mplan BY warpl abnum wapos.

  REFRESH: gt_plas,
           gt_plpo.

  LOOP AT gt_mplan.

*   Progress indicator
    CONCATENATE 'Maintenance Plan'(i09) gt_mplan-warpl 'is being processed'(i98)
                INTO gv_text  SEPARATED BY space.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        text = gv_text.

    MOVE-CORRESPONDING gt_mplan TO gt_final.
    WRITE gt_mplan-warpl TO gt_final-warpl NO-ZERO.
    WRITE gt_mplan-wapos TO gt_final-wapos NO-ZERO.
    WRITE gt_mplan-equnr TO gt_final-equnr NO-ZERO.
    WRITE gt_mplan-serialnr TO gt_final-serialnr NO-ZERO.
    WRITE gt_mplan-kdauf TO gt_final-kdauf NO-ZERO.

*   Select tasklist info
    SELECT plnty plnnr plnal plnfl plnkn zaehl
           INTO TABLE gt_plas
           FROM plas
           WHERE plnty = gt_mplan-plnty
             AND plnnr = gt_mplan-plnnr
             AND plnal = gt_mplan-plnal
             AND loekz = space.

    IF NOT gt_plas[] IS INITIAL.
      SELECT plnty plnnr plnkn zaehl steus
             larnt arbei arbeh werks
             INTO TABLE gt_plpo
             FROM plpo
             FOR ALL ENTRIES IN gt_plas
             WHERE plnty = gt_plas-plnty
               AND plnnr = gt_plas-plnnr
               AND plnkn = gt_plas-plnkn
               AND loekz = space
               AND ( steus = gc_zco3    OR
                     larnt = gc_zam010  OR
                     ktsch = gc_tl9080 ).         "Oil included
    ENDIF.

    IF NOT gt_plpo[] IS INITIAL.

*     Select spare parts
      REFRESH: gt_plmz,
               gt_stpo.

      SELECT plnty plnnr plnkn stlty stlnr stlkn imeng imein
             INTO TABLE gt_plmz
             FROM plmz
             FOR ALL ENTRIES IN gt_plpo
             WHERE plnnr = gt_plpo-plnnr
               AND plnty = gt_plpo-plnty
               AND plnkn = gt_plpo-plnkn
               AND loekz = space.

      IF NOT gt_plmz[] IS INITIAL.
        SELECT stlty stlnr stlkn idnrk menge meins
               INTO TABLE gt_stpo
               FROM stpo
               FOR ALL ENTRIES IN gt_plmz
               WHERE stlty = gt_plmz-stlty
                 AND stlnr = gt_plmz-stlnr
                 AND stlkn = gt_plmz-stlkn
                 AND lkenz = space.
      ENDIF.

      IF NOT gt_stpo[] IS INITIAL.

*       Get cost of spare parts. Always read with planning plant
        LOOP AT gt_plmz.

          CLEAR gt_stpo.
          READ TABLE gt_stpo WITH TABLE KEY stlty = gt_plmz-stlty
                                            stlnr = gt_plmz-stlnr
                                            stlkn = gt_plmz-stlkn.

          CHECK NOT gt_stpo-idnrk IS INITIAL.
          CHECK gt_stpo-idnrk IN s_idnrk.

          IF sy-subrc = 0.
            SELECT SINGLE stprs peinh
                   INTO (gv_spp_cost, gv_spp_unit)
                   FROM mbew
                   WHERE matnr = gt_stpo-idnrk
                     AND bwkey = gt_mplan-iwerk.

            IF sy-subrc = 0.
              gv_spp_cost =
                    gv_spp_cost * gt_plmz-imeng / gv_spp_unit.
              ADD gv_spp_cost TO gt_final-spp_cost.
              gt_final-idnrk = gt_stpo-idnrk.
              gt_final-menge = gt_plmz-imeng.
              gt_final-meins = gt_plmz-imein.

*             Select customer (sold-to)
              CONCATENATE gc_ie gt_mplan-equnr INTO gv_objnr_equi.
              SELECT SINGLE parnr INTO gv_parnr
                     FROM ihpa
                     WHERE objnr    = gv_objnr_equi
                       AND parvw    = gc_ag
                       AND kzloesch = space.

*             Select customer name (sold-to)
              IF sy-subrc = 0.
                WRITE gv_parnr TO gt_final-parnr NO-ZERO.
                SELECT SINGLE name1 INTO gt_final-parnr_name
                       FROM kna1
                       WHERE kunnr = gv_parnr.
              ENDIF.

*             Select bill-to
              SELECT SINGLE parnr INTO gv_billto
                     FROM ihpa
                     WHERE objnr    = gv_objnr_equi
                       AND parvw    = gc_re
                       AND kzloesch = space.

*             Select customer name (bill-to)
              IF sy-subrc = 0.
                WRITE gv_billto TO gt_final-billto NO-ZERO.
                SELECT SINGLE name1 INTO gt_final-billto_name
                       FROM kna1
                       WHERE kunnr = gv_billto.
              ENDIF.

*             Select the equipment description
              CLEAR gt_eqkt.
              READ TABLE gt_eqkt WITH TABLE KEY equnr = gt_mplan-equnr
                                                spras = sy-langu.
              gt_final-eqktx = gt_eqkt-eqktx.

*             Select currency
              CLEAR gt_t001.
              READ TABLE gt_t001 WITH TABLE KEY bukrs = gt_mplan-bukrs.
              gt_final-waers = gt_t001-waers.

              APPEND gt_final.
              CLEAR gt_final-spp_cost.

            ENDIF.
          ENDIF.

        ENDLOOP.
      ENDIF.

    ENDIF.

  ENDLOOP.

ENDFORM.                    " SELECT_MAINTPLAN

*&---------------------------------------------------------------------*
*&      Form  build_field_catlog
*&---------------------------------------------------------------------*
*       Build ALV field catalog
*----------------------------------------------------------------------*
*      <--P_GT_FIELDCAT  text
*----------------------------------------------------------------------*
FORM build_field_catlog  CHANGING pt_fieldcat TYPE slis_t_fieldcat_alv.

  DATA : ls_fcat TYPE slis_fieldcat_alv.

*--------------------Maintenance plan-------------------------*
  ls_fcat-fieldname = 'WARPL'.
  ls_fcat-rollname = 'WARPL'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------Maintenance item-------------------------*
  ls_fcat-fieldname = 'WAPOS'.
  ls_fcat-rollname = 'WAPOS'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------------Item Short Text---------------------*
  ls_fcat-fieldname = 'PSTXT'.
  ls_fcat-rollname = 'POSTXT'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------Maintenance call-------------------------*
  ls_fcat-fieldname = 'ABNUM'.
  ls_fcat-rollname = 'ABNUM'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*----------------Sort field for maintenance plans----------------*
  ls_fcat-fieldname = 'PLAN_SORT'.
  ls_fcat-rollname = 'PLAN_SORT'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------------Equipment---------------------------*
  ls_fcat-fieldname = 'EQUNR'.
  ls_fcat-rollname = 'EQUNR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------------Serial number-----------------------*
  ls_fcat-fieldname = 'SERIALNR'.
  ls_fcat-rollname = 'GERNR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------------Equipment description---------------*
  ls_fcat-fieldname = 'EQKTX'.
  ls_fcat-outputlen = '40'.
  ls_fcat-seltext_l = 'Equipment description'(i01).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-------------------Key for Task List Group--------------------*
  ls_fcat-fieldname = 'PLNNR'.
  ls_fcat-outputlen = '16'.
  ls_fcat-seltext_l = 'Task List Group'(i02).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Group Counter-----------------*
  ls_fcat-fieldname = 'PLNAL'.
  ls_fcat-rollname = 'PLNAL'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Sold-to partner---------------------*
  ls_fcat-fieldname = 'PARNR'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Sold-to partner'(i03).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Sold-to name------------------------*
  ls_fcat-fieldname = 'PARNR_NAME'.
  ls_fcat-outputlen = '35'.
  ls_fcat-seltext_l = 'Name Sold-to'(i04).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Bill-to partner---------------------*
  ls_fcat-fieldname = 'BILLTO'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Bill-to partner'(i05).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Bill-to name------------------------*
  ls_fcat-fieldname = 'BILLTO_NAME'.
  ls_fcat-outputlen = '35'.
  ls_fcat-seltext_l = 'Name Bill-to'(i06).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Service Order------------------------*
  ls_fcat-fieldname = 'AUFNR'.
  ls_fcat-outputlen = '14'.
  ls_fcat-seltext_l = 'Service Order'(i08).
  ls_fcat-edit_mask = '==ALPHA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Contract---------------------------*
  ls_fcat-fieldname = 'KDAUF'.
*  ls_fcat-rollname = 'VBELN_VA'.
  ls_fcat-outputlen = '12'.
  ls_fcat-seltext_l  = 'Contract Nr.'(i07).
  ls_fcat-edit_mask = '==ALPHA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Sales Document Item----------------*
  ls_fcat-fieldname = 'KDPOS'.
  ls_fcat-rollname = 'POSNR_VA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Part-------------------------------*
  ls_fcat-fieldname = 'IDNRK'.
  ls_fcat-rollname = 'IDNRK'.
  ls_fcat-edit_mask = '==MATN1'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------UOM--------------------------------*
  ls_fcat-fieldname = 'MEINS'.
  ls_fcat-rollname = 'MEINS'.
  ls_fcat-edit_mask = '==CUNIT'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*----------------Spare parts Cost--------------------------*
  ls_fcat-fieldname = 'SPP_COST'.
  ls_fcat-outputlen = '16'.
  ls_fcat-seltext_l = 'Spare parts Cost'(i18).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Currency---------------------------*
  ls_fcat-fieldname = 'WAERS'.
  ls_fcat-rollname = 'WAERS'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Quantity---------------------------*
  ls_fcat-fieldname = 'MENGE'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Quantity'(i19).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------Planned date-----------------------------*
  ls_fcat-fieldname = 'NPLDA'.
  ls_fcat-rollname = 'NPLDA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------Recipient-------------------------------*
  ls_fcat-fieldname = 'WEMPF'.
  ls_fcat-rollname = 'WEMPF'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------System Status--------------------------*
  ls_fcat-fieldname = 'STTXT'.
  ls_fcat-rollname = 'J_STEXT'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.

ENDFORM.                    " build_field_catlog

*&---------------------------------------------------------------------*
*&      Form  fill_events_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM fill_events_f14 .

  DATA h_event       TYPE slis_alv_event.

  CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
    EXPORTING
      i_list_type = 0
    IMPORTING
      et_events   = g_events_tab.

*--- allocate form for user-command ---------------------------------*
  READ TABLE g_events_tab WITH KEY name = slis_ev_user_command
                        INTO h_event.
  IF sy-subrc = 0.
    MOVE g_form_user_command TO h_event-form.
    MODIFY g_events_tab FROM h_event INDEX sy-tabix.
  ENDIF.

ENDFORM.                    " fill_events_f14

*&--------------------------------------------------------------------*
*&      Form  user_command_l
*&--------------------------------------------------------------------*
*       text
*---------------------------------------------------------------------*
*      -->P_UCOMM    text
*      -->P_SELFIELD text
*---------------------------------------------------------------------*
FORM user_command_l USING p_ucomm LIKE sy-ucomm
                          p_selfield TYPE slis_selfield.

  p_selfield-refresh = 'S'.
  PERFORM check_pf2_with_object_f16 USING p_ucomm.
  PERFORM set_p_selfield_general_f16 USING p_selfield.

  CASE p_ucomm.
    WHEN 'ISEL'.
      p_ucomm = 'DISP'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
  ENDCASE.

ENDFORM.                    "user_command_l

*&---------------------------------------------------------------------*
*&      Form  check_pf2_with_object_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*----------------------------------------------------------------------*
FORM check_pf2_with_object_f16  USING    p_ucomm.

  CHECK p_ucomm = gv_ic1.
  p_ucomm = 'ISEL'.

ENDFORM.                    " check_pf2_with_object_f16

*&---------------------------------------------------------------------*
*&      Form  set_p_selfield_general_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM set_p_selfield_general_f16  USING f_selfield TYPE slis_selfield.

  f_selfield-col_stable = gc_x.
  f_selfield-row_stable = gc_x.

ENDFORM.                    " set_p_selfield_general_f16

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*      -->P_ENDCASE  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_f16  USING p_ucomm LIKE sy-ucomm
                                p_selfield TYPE slis_selfield.

  PERFORM check_object_tab_marked_f14 USING p_ucomm p_selfield.

  LOOP AT gt_final WHERE selected = gc_x .
    gv_index = sy-tabix.
    PERFORM fcodes_with_mark_l USING p_ucomm p_selfield.
    gt_final-selected = ' '.
    MODIFY gt_final INDEX gv_index.
  ENDLOOP.

  CLEAR p_ucomm.

ENDFORM.                    " fcodes_with_mark_f16

*&---------------------------------------------------------------------*
*&      Form  check_object_tab_marked_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM check_object_tab_marked_f14  USING    p_ucomm LIKE sy-ucomm
                                        p_selfield TYPE slis_selfield.

  READ TABLE gt_final WITH KEY selected = gc_x.

  IF NOT sy-subrc IS INITIAL.
    IF NOT p_selfield-tabindex IS INITIAL.
      READ TABLE gt_final INDEX p_selfield-tabindex.
      gt_final-selected = gc_x.
      MODIFY gt_final INDEX p_selfield-tabindex.
    ENDIF.
  ELSE.
*--- Checkbox markiert -----------------------------------------------*
    p_selfield-sel_tab_field = 'G_MARK'.
  ENDIF.

ENDFORM.                    " check_object_tab_marked_f14

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_l
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_F_UCOMM  text
*      -->P_F_SELFIELD  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_l  USING    p_ucomm LIKE sy-ucomm
                               p_selfield TYPE slis_selfield.

  DATA: h_ucomm LIKE sy-ucomm.

  CASE p_ucomm.
*   Display MP
    WHEN 'DISP'.
      IF NOT gt_final-warpl IS INITIAL.
        SET PARAMETER ID 'MPL' FIELD  gt_final-warpl.
        CALL TRANSACTION 'IP03' AND SKIP FIRST SCREEN.
      ENDIF.
  ENDCASE.

ENDFORM.                    " fcodes_with_mark_l

*&---------------------------------------------------------------------*
*&      Form  alv_display
*&---------------------------------------------------------------------*
*       Display list
*----------------------------------------------------------------------*
FORM alv_display .

  gv_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
     EXPORTING
       i_callback_program                =  gv_repid
       i_save                            = 'A'
       it_events                         =  g_events_tab[]
*      I_GRID_TITLE                      =
*      I_GRID_SETTINGS                   =
*      is_layout                         =  g_layout
       it_fieldcat                       =  gt_fieldcat[]
     TABLES
        t_outtab                         =  gt_final.

ENDFORM.                    " alv_display

*Text symbol text£º
*B01:Selection
*I01:Equipment description
*I02:Task List Group
*I03:Sold-to partner
*I04:Name Sold-to
*I05:Bill-to partner
*I06:Name Bill-to
*I07:Contract Nr.
*I08:Service Order
*I09:Maintenance Plan
*I97:Maintenance Plans are being selected
*I98:is being processed

*I99:Service orders are being selected
*Selection text£º
*S_AUFNR:        Service Order
*S_EQUNR:D       .
*S_IDNRK:D       .
*S_IWERK:D       .
*S_KDAUF:        Contract
*S_NPLDA:D       .
*S_SERNR:D       .
*S_VKORG:D       .
*S_WARPL:D       .
