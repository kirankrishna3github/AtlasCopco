REPORT  yse_extract_salmon_data_2 LINE-SIZE 500.
************************************************** PROGRAM  INFO *******
* Author      : Luc Mertens                                            *
* Date        : 20.03.2008                                             *
* -------------------------------------------------------------------- *
* Description : The purpose of this program is to fill table           *
*               YSE_SALMON, which is used to send IDOCs for the        *
*               SALMON interface.                                      *
*               Copy from YSE_EXTRACT_SALMON_DATA, but data from CO-PA *
************************************************************************
* Corr/Vers  Date        Pers.     Description                         *
* ---------  ----------  --------- ---------------------------------   *
* MOD-001 | 2009.04.21 | Geert Rutten | CD1K947752  | CR0789
*----------------------------------------------------------------------*
* MOD-002 | 2010.01.06 | Marc Jacobs  | CD1K953305  | CR1161           *
*----------------------------------------------------------------------*
* MOD-003 | 2010.03.10 | Marc Jacobs  | CD1K955207  | CR1281           *
*----------------------------------------------------------------------*
* MOD-004 | 2017.04.12 | Tony Yang    | CD1K991490  | CR4178           *
*----------------------------------------------------------------------*
************************************************************************
*----------------------------------------------------------------------*
* DATA DECLARATION
*----------------------------------------------------------------------*
TABLES: ce11000,
        vbfa.

CONSTANTS: c_sr(2)         VALUE 'SR',
           c_si(2)         VALUE 'SI'.

DATA: BEGIN OF r_salmon_table,
        mandt       LIKE yse_salmon-mandt,
        sdrcid      LIKE yse_salmon-sdrcid,
        sdord       LIKE yse_salmon-sdord,
        sdinv       LIKE yse_salmon-sdinv,
        sdline      LIKE yse_salmon-sdline,
        sdtrdt      LIKE yse_salmon-sdtrdt,
        sdprod      LIKE yse_salmon-sdprod,
        sdpgc       LIKE yse_salmon-sdpgc,
        sdgac       LIKE yse_salmon-sdgac,
        sdcust      LIKE yse_salmon-sdcust,
        sdregn      LIKE yse_salmon-sdregn,
        sdfcc       LIKE yse_salmon-sdfcc,
* begin of change MOD-003
*        SDFAM       like ce11000-bukrs,
        sdfam       LIKE yse_salmon-sdfam,
* end of change MOD-003
        sdrgac      LIKE yse_salmon-sdrgac,
        sddesc      LIKE yse_salmon-sddesc,
        sdname      LIKE yse_salmon-sdname,
        sdcval(8)   TYPE p DECIMALS 2,
        sdlval(8)   TYPE p DECIMALS 2,
        sdsval(8)   TYPE p DECIMALS 2,
        sdqty(8)    TYPE p DECIMALS 3,
        sdsrep      LIKE yse_salmon-sdsrep,
        sdacst(8)   TYPE p DECIMALS 2,
        sdgcus      LIKE yse_salmon-sdgcus,
        sdgind      LIKE yse_salmon-sdgind,
        sdarc1      LIKE yse_salmon-sdarc1,
        sdarc2      LIKE yse_salmon-sdarc2,
        sdarc3      LIKE yse_salmon-sdarc3,
        sdhcpo      LIKE yse_salmon-sdhcpo,
        sdwebn      LIKE yse_salmon-sdwebn,
        sdcurr      LIKE yse_salmon-sdcurr,
        sdfocd      LIKE yse_salmon-sdfocd,
        sdccus      LIKE yse_salmon-sdccus,
        sdssr       LIKE yse_salmon-sdssr,
        sdgrp1      LIKE yse_salmon-sdgrp1,
        sdgrp2      LIKE yse_salmon-sdgrp2,
        sdgrp3      LIKE yse_salmon-sdgrp3,
        sdgrp4      LIKE yse_salmon-sdgrp4,
        sdgrp5      LIKE yse_salmon-sdgrp5,
        sdbrand     LIKE yse_salmon-sdbrand,
        sdsalgr     LIKE yse_salmon-sdsalgr,
        sdmeas      LIKE yse_salmon-sdmeas,
        sdrundte    LIKE yse_salmon-sdrundte,
        sdruntim    LIKE yse_salmon-sdruntim,
        sdbrcd      LIKE yse_salmon-sdbrcd,
        sdmatg      LIKE yse_salmon-sdmatg,
        sdpord      LIKE yse_salmon-sdpord,
        sdplin      LIKE yse_salmon-sdplin,
        sdicf       LIKE yse_salmon-sdicf,
        sdvend      LIKE yse_salmon-sdvend,
* begin of insertion MOD-003
        sdsorg      like yse_salmon-sdsorg,
* end of insertion MOD-003
      END OF r_salmon_table.

DATA: t_salmon_table LIKE r_salmon_table OCCURS 0,
      ls_salmon      LIKE r_salmon_table,
      ls_save_salmon LIKE r_salmon_table.

DATA: BEGIN OF r_sal,
        vrgar   LIKE ce11000-vrgar,
        kaufn   LIKE ce11000-kaufn,
        kdpos   LIKE ce11000-kdpos,
        budat   LIKE ce11000-budat,
        ww006   LIKE ce11000-ww006,
        ww007   LIKE ce11000-ww007,
        ww009   LIKE ce11000-ww009,
        artnr   LIKE ce11000-artnr,
        kndnr   LIKE ce11000-kndnr,
        rbeln   LIKE ce11000-rbeln,
        ktgrd   LIKE ce11000-ktgrd,
        bukrs   LIKE ce11000-bukrs,
        spart   LIKE ce11000-spart,
        matkl   LIKE ce11000-matkl,
        vv100   LIKE ce11000-vv100,
        vv130   LIKE ce11000-vv130,
        absmg   LIKE ce11000-absmg,
        vkorg   LIKE ce11000-vkorg,
        vtweg   LIKE ce11000-vtweg,
        ww008   LIKE ce11000-ww008,
        rec_waers LIKE ce11000-rec_waers,
        vv110   LIKE ce11000-vv110,
        vv650   LIKE ce11000-vv650,
* begin of change MOD-004
        hzdat   LIKE ce11000-hzdat,
* end of change MOD-004
      END OF r_sal.

DATA: t_sal LIKE r_sal OCCURS 0.

DATA: BEGIN OF lt_sal_final OCCURS 0.
        INCLUDE STRUCTURE yse_salmon.
DATA: END OF lt_sal_final.

DATA: h_answer.

TYPES: BEGIN OF ty_old_matnr,
         matnr    TYPE matnr,
         oldmatnr TYPE bismt,
         mtart    TYPE mtart,
       END OF ty_old_matnr.

TYPES: BEGIN OF ty_makt,
         matnr    TYPE matnr,
         spras    TYPE spras,
         maktx    TYPE maktx,
       END OF ty_makt.

TYPES: BEGIN OF ty_t001,
         bukrs    TYPE bukrs,
         spras    TYPE spras,
       END OF ty_t001.

DATA: lt_oldmatnr TYPE STANDARD TABLE OF ty_old_matnr,
      ls_oldmatnr TYPE ty_old_matnr,
      lt_makt     TYPE STANDARD TABLE OF ty_makt,
      ls_makt     TYPE ty_makt,
      lt_t001     TYPE STANDARD TABLE OF ty_t001,
      ls_t001     TYPE ty_t001,
      gv_vkorg    LIKE vbak-vkorg,
      gv_spart    LIKE vbak-spart,
      gv_knumh    LIKE konp-knumh,
      gv_prsdt    LIKE vbkd-prsdt,
      gv_hkunnr   LIKE knvh-hkunnr,
      gv_uepos    LIKE vbap-uepos,
      gv_adrnr    LIKE kna1-adrnr,
      gv_sdcval   LIKE yse_salmon-sdcval,
      gv_sdsval   LIKE yse_salmon-sdsval,
      gv_sdqty    LIKE yse_salmon-sdqty,
      gv_sdprod   TYPE matnr.

RANGES: perio FOR ce11000-perio,
        recid FOR yse_salmon-sdrcid.

FIELD-SYMBOLS: <line>.

*----------------------------------------------------------------------*
* SELECTION SCREEN
*----------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-100.
SELECT-OPTIONS: s_vkorg  FOR ce11000-vkorg,
                s_vtweg  FOR ce11000-vtweg DEFAULT '01',
* Begin of Delete MOD-001
*                S_PERIOD FOR ce11000-budat,
* End of Delete MOD-001
* Begin of Insert MOD-001
                s_period FOR ce11000-hzdat,
* End of Insert MOD-001
                s_vrgar  FOR ce11000-vrgar,
                s_kaufn  FOR ce11000-kaufn.
SELECTION-SCREEN END OF BLOCK b2.
SELECTION-SCREEN BEGIN OF BLOCK b3 WITH FRAME TITLE text-004.
PARAMETERS: p_test AS CHECKBOX DEFAULT 'X'.
PARAMETERS: p_ins RADIOBUTTON GROUP r1 DEFAULT 'X',
            p_del RADIOBUTTON GROUP r1.
SELECTION-SCREEN END OF BLOCK b3.

*----------------------------------------------------------------------*
* START PROGRAM
*----------------------------------------------------------------------*
START-OF-SELECTION.

* If date range is initial, set it yesterday and today
  IF s_period IS INITIAL.
    s_period-sign   = 'I'.
    s_period-option = 'BT'.
    s_period-low    = sy-datum - 1.
    s_period-high   = sy-datum.
    APPEND s_period.
  ENDIF.

* derive year/period to access CO-PA
  LOOP AT s_period.
    MOVE s_period-low+0(4) TO perio-low+0(4).
    MOVE s_period-low+4(2) TO perio-low+4(3).
    IF s_period-high IS INITIAL.
      perio-sign   = 'I'.
      perio-option = 'EQ'.
    ELSE.
      perio-sign   = 'I'.
      perio-option = 'BT'.
      MOVE s_period-high+0(4) TO perio-high+0(4).
      MOVE s_period-high+4(2) TO perio-high+4(3).
    ENDIF.
    APPEND perio.
    CLEAR perio.
  ENDLOOP.

  IF p_ins EQ 'X'. "If radiobutton "Insert table" <--> "Delete table"

    SELECT vrgar kaufn kdpos budat rbeln artnr ww007 ww006 kndnr
           vv130 vv100 ww009 bukrs spart matkl absmg vkorg vtweg
           ww008 rec_waers vv110 vv650
* begin of change MOD-004
           hzdat
* end of change MOD-004
        FROM ce11000
       INTO CORRESPONDING FIELDS OF TABLE t_sal
        WHERE paledger EQ '02'
          AND vrgar    IN s_vrgar
          AND perio    IN perio
* Begin of Delete MOD-001
*          AND budat    in s_period
* End of Delete MOD-001
* Begin of Delete MOD-001
          AND hzdat IN s_period
* End of Delete MOD-001
          AND vkorg    IN s_vkorg
          AND vtweg    IN s_vtweg
          AND kaufn    IN s_kaufn.


*.. Select old materialnr. for all found matnrs
*.. + description (needed in local language key)
    IF sy-subrc EQ 0.
      CLEAR: lt_oldmatnr[],
             lt_makt[].

      SELECT matnr bismt mtart
        FROM mara INTO TABLE lt_oldmatnr
        FOR ALL ENTRIES IN t_sal
        WHERE matnr = t_sal-artnr.

      SORT lt_oldmatnr BY matnr.
      DELETE ADJACENT DUPLICATES FROM lt_oldmatnr COMPARING matnr.

      SELECT matnr spras maktx
        FROM makt INTO TABLE lt_makt
        FOR ALL ENTRIES IN lt_oldmatnr
        WHERE matnr = lt_oldmatnr-matnr.

      SORT lt_makt BY matnr spras.

      SELECT bukrs spras
        FROM t001 INTO TABLE lt_t001.

      SORT lt_t001 BY bukrs.
    ENDIF.

    IF t_sal[] IS INITIAL.
      MESSAGE s001(00) WITH text-i01.
      EXIT.
    ENDIF.

*.. Fill table
    LOOP AT t_sal INTO r_sal.
      IF r_sal-vrgar = 'A'.
        r_salmon_table-sdrcid = c_sr.
      ELSE.
        r_salmon_table-sdrcid = c_si.
        r_salmon_table-sdinv  = r_sal-rbeln.    " SD document number
      ENDIF.

      r_salmon_table-sdord  = '000000000000'.   " Order number
      r_salmon_table-sdord+2(10) = r_sal-kaufn. " Order number
      r_salmon_table-sdline = r_sal-kdpos.      " Sales document item
* begin of change MOD-004
      r_salmon_table-sdtrdt = r_sal-hzdat.      " Transaction date
*      r_salmon_table-sdtrdt = r_sal-budat.      " Transaction date
* end of change MOD-004

      READ TABLE lt_oldmatnr INTO ls_oldmatnr
        WITH KEY matnr = r_sal-artnr
          BINARY SEARCH.

      IF NOT ls_oldmatnr-oldmatnr IS INITIAL.
        MOVE ls_oldmatnr-oldmatnr TO r_salmon_table-sdprod.
      ELSE.
        MOVE ls_oldmatnr-matnr+8(10) TO r_salmon_table-sdprod.
      ENDIF.
*     move ls_oldmatnr-mtart to r_salmon_table-sdssr.

      r_salmon_table-sdgac  = r_sal-ww006.      " GAC
      r_salmon_table-sdrgac = r_sal-ww006.      " Official Atlas GAC
      r_salmon_table-sdpgc  = r_sal-ww007.      " PGC
      r_salmon_table-sdcust = r_sal-kndnr.      " Customer number
      r_salmon_table-sdfcc  = r_sal-ww009.      " Country ship-to
      r_salmon_table-sdicf  = r_sal-ktgrd.      " Account assign.group
      r_salmon_table-sdfam  = r_sal-bukrs.      " Company code
* begin of insertion MOD-003
      r_salmon_table-sdsorg = r_sal-vkorg.
* end of insertion MOD-003
*     Total sales value
      r_salmon_table-sdsval = r_sal-vv100 + r_sal-vv110.
      r_salmon_table-sdcval = r_sal-vv130.      " Total cost  value
      r_salmon_table-sdqty  = r_sal-absmg.      " Quantity
      r_salmon_table-sdbrcd = r_sal-spart.      " Division
      r_salmon_table-sdmatg = r_sal-matkl.      " Material group
      r_salmon_table-sdregn = r_sal-ww008.      " Region
      r_salmon_table-sdssr(4) = r_salmon_table-sdpgc(4).
      r_salmon_table-sdssr+4(10) = '          '.
      r_salmon_table-sdcurr = r_sal-rec_waers.

      SELECT SINGLE kdgrp
             FROM knvv
             INTO r_salmon_table-sdarc3
             WHERE kunnr EQ r_sal-kndnr
               AND vkorg EQ r_sal-vkorg
               AND vtweg EQ r_sal-vtweg
               AND spart EQ r_sal-spart.

      SELECT SINGLE hkunnr
             FROM knvh
             INTO gv_hkunnr
             WHERE hityp EQ 'A'
               AND kunnr EQ r_sal-kndnr
               AND vkorg EQ r_sal-vkorg
               AND vtweg EQ r_sal-vtweg
               AND spart EQ r_sal-spart
               AND datbi EQ '99991231'.

      IF sy-subrc = 0.
        SELECT SINGLE adrnr INTO gv_adrnr
            FROM kna1
            WHERE kunnr = gv_hkunnr.

        IF sy-subrc = 0.
          SELECT SINGLE sort1 INTO r_salmon_table-sdgcus
            FROM adrc WHERE addrnumber = gv_adrnr.
        ENDIF.
      ENDIF.

*.... Collect the line to internal table
      COLLECT r_salmon_table INTO t_salmon_table.

*     WRITE:/ R_SALMON_TABLE.
      CLEAR r_salmon_table.
    ENDLOOP.

*.. Still calculate some values and get additional fields
    LOOP AT t_salmon_table INTO ls_salmon.
      READ TABLE lt_t001 INTO ls_t001
        WITH KEY bukrs = ls_salmon-sdfam
          BINARY SEARCH.

      IF sy-subrc = 0.
*...... Convert to Internal Material Number
        CALL FUNCTION 'CONVERSION_EXIT_MATN1_INPUT'
          EXPORTING
            input  = ls_salmon-sdprod
          IMPORTING
            output = gv_sdprod.

        READ TABLE lt_makt INTO ls_makt
          WITH KEY matnr = gv_sdprod
                   spras = ls_t001-spras
            BINARY SEARCH.

        IF sy-subrc = 0.
          ls_salmon-sddesc = ls_makt-maktx.
        ENDIF.
      ENDIF.

      SELECT SINGLE name1 bran1
             FROM kna1
             INTO (ls_salmon-sdname, ls_salmon-sdgind)
             WHERE kunnr EQ ls_salmon-sdcust.

      SELECT SINGLE vbeln posnn
         INTO (ls_salmon-sdpord, ls_salmon-sdplin)
         FROM vbfa WHERE vbelv   = ls_salmon-sdord+2(10)
                     AND posnv   = ls_salmon-sdline
                     AND vbtyp_n = 'V'.

      CLEAR gv_uepos.
      SELECT SINGLE uepos
         INTO gv_uepos
         FROM vbap WHERE vbeln   = ls_salmon-sdord+2(10)
                     AND posnr   = ls_salmon-sdline.

      IF gv_uepos = '000000'.
        ls_salmon-sdfocd = ls_salmon-sdline+3(3).
      ELSE.
        ls_salmon-sdfocd = gv_uepos+3(3).
      ENDIF.

*.... get sales employee
      SELECT SINGLE pernr
         INTO ls_salmon-sdsrep
         FROM vbpa WHERE vbeln = ls_salmon-sdord+2(10)
                     AND parvw = 'VE'.

      IF sy-subrc <> 0.
        SELECT SINGLE pernr
           INTO ls_salmon-sdsrep
           FROM vbpa WHERE vbeln = ls_salmon-sdord+2(10)
                       AND parvw = 'ZX'.

        IF sy-subrc <> 0.
          SELECT SINGLE pernr
             INTO ls_salmon-sdsrep
             FROM vbpa WHERE vbeln = ls_salmon-sdord+2(10)
                         AND parvw = 'ZY'.
        ENDIF.
      ENDIF.

      SELECT SINGLE lifnr
         INTO ls_salmon-sdvend
         FROM ekko WHERE ebeln = ls_salmon-sdpord.

*.... Actual cost per unit
*      if not ls_salmon-sdqty is initial.
*        ls_salmon-SDACST = ls_salmon-sdcval / ls_salmon-sdqty.
*      endif.

      CLEAR: gv_vkorg,
             gv_spart,
             gv_prsdt.

      SELECT SINGLE vkorg spart
           INTO (gv_vkorg, gv_spart)
           FROM vbak WHERE vbeln = ls_salmon-sdord+2(10).

* begin of change MOD-002
*      select single prsdt into gv_prsdt
      SELECT SINGLE prsdt ihrez INTO (gv_prsdt, ls_salmon-sdwebn)
* end of change MOD-002
           FROM vbkd WHERE vbeln = ls_salmon-sdord+2(10)
                       AND posnr = ls_salmon-sdline.

      SELECT SINGLE knumh INTO gv_knumh
           FROM a954 WHERE kappl =  'V'
                       AND kschl =  'ZPRO'
                       AND vkorg =  gv_vkorg
                       AND spart =  gv_spart
                       AND matnr =  gv_sdprod
                       AND datab <= gv_prsdt
                       AND datbi >= gv_prsdt.

      IF sy-subrc = 0.
        SELECT SINGLE kbetr INTO ls_salmon-sdacst
             FROM konp WHERE knumh    EQ gv_knumh
                         AND loevm_ko NE 'X'.
      ENDIF.

      MODIFY t_salmon_table FROM ls_salmon
                  TRANSPORTING sdacst
                               sdvend
                               sdsrep
                               sdname
                               sdgind
                               sdpord
                               sdplin
                               sdfocd
* begin of insertion MOD-002
                               sdwebn
* end of insertion MOD-002
                               sddesc.
    ENDLOOP.

*    sort t_salmon_table by sdrcid sdord sdinv sdline sdtrdt.
*
**.. loop again because of possible multiple charact. (ex.PGC)
*    clear ls_save_salmon.
*    loop at t_salmon_table into ls_salmon.
*      if ls_salmon-sdrcid = ls_save_salmon-sdrcid and
*         ls_salmon-sdord  = ls_save_salmon-sdord  and
*         ls_salmon-sdline = ls_save_salmon-sdline and
*         ls_salmon-sdtrdt = ls_save_salmon-sdtrdt and
*         ls_salmon-sdinv  = ls_save_salmon-sdinv.
*
**       delete t_salmon_table from ls_salmon.
*        DELETE t_salmon_table WHERE SDRCID EQ ls_salmon-sdrcid
*                                AND SDORD  EQ ls_salmon-sdord
*                                AND SDLINE EQ ls_salmon-sdline
*                                AND SDTRDT EQ ls_salmon-sdtrdt
*                                AND SDINV  EQ ls_salmon-sdinv.
*
**        move ls_salmon-sdcval to gv_sdcval.
**        move ls_salmon-sdsval to gv_sdsval.
**        move ls_salmon-sdqty  to gv_sdqty.
**        move ls_save_salmon to ls_salmon.
**        add gv_sdcval to ls_salmon-sdcval.
**        add gv_sdsval to ls_salmon-sdsval.
**        add gv_sdqty  to ls_salmon-sdqty.
*        add ls_save_salmon-sdcval to ls_salmon-sdcval.
*        add ls_save_salmon-sdsval to ls_salmon-sdsval.
*        add ls_save_salmon-sdqty  to ls_salmon-sdqty.
*
**...... calculate again the actual cost per unit
*        if not ls_salmon-sdqty is initial.
*          ls_salmon-sdacst = ls_salmon-sdcval / ls_salmon-sdqty.
*        endif.
**       modify t_salmon_table from ls_salmon.
*        insert ls_salmon into t_salmon_table.
*      else.
*        move ls_salmon to ls_save_salmon.
*      endif.
*    endloop.

    IF p_test EQ ' '.                         " Delete table entries
      LOOP AT t_salmon_table INTO ls_salmon.
        DELETE FROM yse_salmon WHERE sdrcid EQ ls_salmon-sdrcid
                                 AND sdord  EQ ls_salmon-sdord
                                 AND sdline EQ ls_salmon-sdline
                                 AND sdtrdt EQ ls_salmon-sdtrdt
                                 AND sdinv  EQ ls_salmon-sdinv
                                 AND sdprod EQ ls_salmon-sdprod
                                 AND sdpgc  EQ ls_salmon-sdpgc
                                 AND sdgac  EQ ls_salmon-sdgac
                                 AND sdcust EQ ls_salmon-sdcust
                                 AND sdregn EQ ls_salmon-sdregn
                                 AND sdfcc  EQ ls_salmon-sdfcc
                                 AND sdfam  EQ ls_salmon-sdfam
                                 AND sdrgac EQ ls_salmon-sdrgac.
      ENDLOOP.

      IF NOT t_salmon_table[] IS INITIAL.     " Append table entries
        LOOP AT t_salmon_table INTO ls_salmon.
          MOVE-CORRESPONDING ls_salmon TO lt_sal_final.
          APPEND lt_sal_final.
          CLEAR lt_sal_final.
        ENDLOOP.
        MODIFY yse_salmon FROM TABLE lt_sal_final.
      ENDIF.
    ENDIF.

*.. write newly created entries
    LOOP AT t_salmon_table INTO ls_salmon.
      MOVE-CORRESPONDING ls_salmon TO lt_sal_final.
      WRITE lt_sal_final.
    ENDLOOP.

  ELSE. "If radiobutton "delete table" is chosen <--> "insert table"

*.. Convert record type from input(vrgar) into recid(YSE_SALMON)
    LOOP AT s_vrgar.
      CASE s_vrgar-low.
        WHEN 'A'.
          MOVE c_sr TO recid-low.
        WHEN 'F'.
          MOVE c_si TO recid-low.
      ENDCASE.

      IF s_vrgar-high IS INITIAL.
        recid-sign   = 'I'.
        recid-option = 'EQ'.
      ELSE.
        recid-sign   = 'I'.
        recid-option = 'BT'.
        CASE s_vrgar-high.
          WHEN 'A'.
            MOVE c_sr TO recid-high.
          WHEN 'F'.
            MOVE c_si TO recid-high.
        ENDCASE.
      ENDIF.
      APPEND recid.
      CLEAR recid.
    ENDLOOP.

    IF p_test EQ ' '.
      IF sy-batch = 'X'.
*...... No popup in background
        DELETE FROM yse_salmon WHERE sdrcid IN recid
                                 AND sdtrdt IN s_period.
      ELSE.
        CLEAR h_answer.
        CALL FUNCTION 'POPUP_TO_CONFIRM'
          EXPORTING
            titlebar       = 'PLEASE CONFIRM DELETION'
            text_question  = 'DELETE ALL SELECTED TABLE ENTRIES ?'
            text_button_1  = 'YES'
          IMPORTING
            answer         = h_answer
          EXCEPTIONS
            text_not_found = 1
            OTHERS         = 2.

        IF h_answer EQ '1'.
          DELETE FROM yse_salmon WHERE sdrcid IN recid
                                   AND sdtrdt IN s_period.
        ENDIF.
      ENDIF.
    ENDIF.

  ENDIF.

*Text symbol text£º
*004:Processing Mode:
*100:Selection screen input

*I01:No data from CO-PA selected
*Selection text£º
*P_DEL:        Delete Table entries
*P_INS:        Insert Table entries
*P_TEST:        Test Mode (no update)
*S_KAUFN:D       .
*S_PERIOD:        Period
*S_VKORG:D       .
*S_VRGAR:D       .
*S_VTWEG:D       .
