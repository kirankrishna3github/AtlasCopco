*----------------------------------------------------------------------
* PROGRAM ID           : YSE_ORDERS_INVOICED                           *
* PROGRAM TITLE        : Orders invoiced report                        *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 13/10/2008                                    *
* DEVELOPMENT ID       :                                               *
* CHANGE REQUEST NUMBER: CD1K943984                                    *
* PROGRAM DESCRIPTION  : Create report with orders invoiced info       *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME                |CORRECTION NR| CHANGE REF# *
*----------------------------------------------------------------------*
* MOD-001 |03/02/2009|M.Jacobs             |CD1K946065   | NEWGL       *
* MOD-002 |30/03/2009|M.Jacobs             |CD1K947337   |report-usage *
*----------------------------------------------------------------------*
*         |31/10/2011|Johnny Wu            |CD1K968676   | CR2288      *
*               Add ContractType in the output                         *
*MOD-004  |04/09/2012|Raghavendra D.V.S.   |CD1K973142   | CR2529      *
* Bugfix: To create a new column for the seed countries                *
*----------------------------------------------------------------------*
report yse_orders_invoiced no standard page heading
                                    line-size 400.

type-pools: slis.

tables: vbak,                          "Sales Document: Header Data
        vbap,
        vbkd,
        adrc,
        kna1,
        ce11000,
        rihea.

*- VARIABLES----------------------------------------------------------
data: begin of gt_vbap occurs 0,
        vbeln     like vbak-vbeln,
        posnr     like vbap-posnr,
        kunnr     like vbak-kunnr,
        vkbur     like vbak-vkbur,
        vkgrp     like vbak-vkgrp,
        kdgrp     like vbkd-kdgrp,
        matkl     like vbap-matkl,
        objnr     like vbap-objnr,
        vkorg     like vbak-vkorg,
        bzirk     like vbkd-bzirk,
        vtweg     like vbak-vtweg,
        spart     like vbak-spart,
        werks     like vbap-werks,
        auart     like vbak-auart,
        matnr     like vbap-matnr,
        arktx     like vbap-arktx,
        bran1     like kna1-bran1,
        name1     like kna1-name1,
        region    like adrc-region,
        country   like adrc-country,
        prsdt     like vbkd-prsdt,
      end of gt_vbap.

data: begin of gt_copa occurs 0,
        kaufn     type kdauf,
        kdpos     type kdpos,
        vrgar     type rke_vrgar,
        budat     like ce11000-budat,
*       fadat     like ce11000-fadat,
        vv110     type rke2_vv110,
        vv120     type rke2_vv120,
        kokrs     type kokrs,
        prctr     type prctr,
        equnr     type equnr,
        ww003     type rkeg_ww003,
        rec_waers type rke_rec_waers,
        vv100     type rke2_vv100,
        vv112     type rke2_vv112,
*       vv113     type RKE2_VV113,
        vv114     type rke2_vv114,
        vv130     type rke2_vv130,
        vv140     type rke2_vv140,
        vv150     type rke2_vv150,
        vv200     type rke2_vv200,
        vv300     type rke2_vv300,
        vv400     type rke2_vv400,
        vv500     type rke2_vv500,
        vv600     type rke2_vv600,
        ww002     type rkeg_ww002,
        ww006     type rkeg_ww006,
        ww007     type rkeg_ww007,
        paobjnr   like ce41000-paobjnr,
        pasubnr   like ce41000-pasubnr,
* begin of insertion MOD-001
        bukrs     type bukrs,
* end of insertion MOD-001
      end of gt_copa.

data: begin of gt_final occurs 0,
        vbeln               like vbak-vbeln,
        posnr               like vbap-posnr,
        budat               like ce11000-budat,
*       fadat               like ce11000-fadat,
        kunnr               like vbak-kunnr,
        vkbur               like vbak-vkbur,
        vkgrp               like vbak-vkgrp,
        kdgrp               like vbkd-kdgrp,
        matkl               like vbap-matkl,
        objnr               like vbap-objnr,
        vkorg               like vbak-vkorg,
        bzirk               like vbkd-bzirk,
        vtweg               like vbak-vtweg,
        spart               like vbak-spart,
        werks               like vbap-werks,
        auart               like vbak-auart,
        matnr               like vbap-matnr,
        arktx               like vbap-arktx,
        bran1               like kna1-bran1,
        name1               like kna1-name1,
        region              like adrc-region,
        country             like adrc-country,
        kaufn               type kdauf,
        kdpos               type kdpos,
        equnr               type equnr,
        kokrs               type kokrs,
        prctr               type prctr,
        ww003               type rkeg_ww003,
        rec_waers           type waers,
        tot_vv100           type rke2_vv100,
        tot_vv112           type rke2_vv112,
        tot_vv110           type rke2_vv110,
        tot_vv114           type rke2_vv114,
        tot_vv130           type rke2_vv130,
        tot_vv140           type rke2_vv140,
        tot_vv150           type rke2_vv150,
        tot_vv200           type rke2_vv200,
        tot_vv300           type rke2_vv300,
        tot_vv400           type rke2_vv400,
        tot_vv500           type rke2_vv500,
        tot_vv600           type rke2_vv600,
        ww002               type rkeg_ww002,
        ww006               type rkeg_ww006,
        ww007               type rkeg_ww007,
        net_inv_sls(15)     type p decimals 2,
        unadj_cogs(15)      type p decimals 2,
        unadj_gp(15)        type p decimals 2,
        %ugp(3)             type p decimals 2,
        sttxt               type j_stext,
        prctr_text          type ktext,
        vkorg_text          type vtxtk,
        bzirk_text          type bztxt,
        vkbur_text          type bezei20,
        ww002_text          type bezap,
        ww006_text          type bezap,
        ww007_text          type bezap,
        werks_text          type name1,
        equnr_text          type ktx01,
        region_text         type bezei20,
        vkgrp_text          type bezei20,
        kdgrp_text          type vtxtk,
        bran1_text          type vtext,
        prsdt               like vbkd-prsdt,
        cont_type(32)       type c,                             "CD1K968676
        unadj_cogs_seed(15) type p decimals 2,                  " Mod-004
        selected,
      end of gt_final.

data: begin of gt_stai1 occurs 0,
        low(4) type c,
      end of gt_stai1.

data: begin of gt_stae1 occurs 0,
        low(4) type c,
      end of gt_stae1.

data: begin of h_status_tab occurs 20.
        include structure jstat.
data: end of h_status_tab.

data: begin of h_status_text_tab occurs 20,
        txt04 like tj02t-txt04.
data: end of h_status_text_tab.
data: h_stat_flag.
data : gv_bukrs2 type zvalue_field.

data: gv_adrnr             like kna1-adrnr,
      gv_bukrs             type bukrs,
      gv_waers             type waers_cp,
      gv_land1             like t001-land1,
      gv_ww002             like ce11000-ww002,
      gv_repid             type sy-repid,
      gv_index             type sy-tabix,
      gv_ic1               like sy-ucomm value '&IC1',
      gt_fieldcat          type slis_t_fieldcat_alv,
      gt_events_tab        type slis_t_event,
      gv_newgl_active(1)   type c,
      gv_form_user_command type slis_formname value 'USER_COMMAND_L'.

data: g_stai1_lines like sy-tabix.
data: g_stae1_lines like sy-tabix.

data : g_incl(1) type c,
       g_excl(1) type c.

*- Constants------------------------------------------------------------
constants: gc_x(1)        type c       value 'X',
           c_c(1)         type c       value 'C',        "Sales Orders
           c_g(1)         type c       value 'G',        "Contracts
           c_l(1)         type c       value 'L',
           c_k(1)         type c       value 'K',
           c_h(1)         type c       value 'H',
           c_zo01         type auart   value 'ZO01',     "CC Service Sales
           c_zc01         type auart   value 'ZC01',     "Contract
           c_zor          type auart   value 'ZOR ',     "Standard order
           c_zor1         type auart   value 'ZOR1',
           c_zor2         type auart   value 'ZOR2',
           c_zor3         type auart   value 'ZOR3',
           c_z1cr         type auart   value 'Z1CR',
           c_z1dr         type auart   value 'Z1DR',
           c_zre1         type auart   value 'ZRE1',
           c_zkr          type auart   value 'ZKR',
           c_zrk          type auart   value 'ZRK',
           c_99999(3)     type p decimals 2 value '999.99'.

*- SELECTION SCREEN---------------------------------------------------
selection-screen: begin of block b1 with frame title text-001.
parameters:     p_vkorg  type vbak-vkorg obligatory memory id vko.
select-options: s_vbeln  for  vbak-vbeln matchcode object vmva,
                s_posnr  for  vbap-posnr,
                s_perio  for  ce11000-perio obligatory,
                s_bzirk  for  vbkd-bzirk,
                s_vkbur  for  vbak-vkbur,
                s_vkgrp  for  vbak-vkgrp,
                s_kunnr  for  vbak-kunnr,
                s_region for  adrc-region,
                s_kdgrp  for  vbkd-kdgrp,
                s_bran1  for  kna1-bran1,
                s_prctr  for  ce11000-prctr,
                s_plc    for  ce11000-ww002 obligatory,
                s_gac    for  ce11000-ww006,
                s_pgc    for  ce11000-ww007,
                s_equnr  for  ce11000-equnr,
                s_matkl  for  vbap-matkl,
                s_matnr  for  vbap-matnr.
select-options:
  s_stai1 for rihea-i_estatin matchcode object i_status no intervals,
  s_stae1 for rihea-i_estatex matchcode object i_status no intervals.
selection-screen: end of block b1.


*.................. Selection screen validations...................... *
at selection-screen on p_vkorg.

  authority-check object 'V_VBRK_VKO'
           id 'VKORG' field p_vkorg
           id 'ACTVT' field '03'.

  if sy-subrc ne 0.
*.. No authorization for sales organisation: &1
    message e001(00) with text-e03 p_vkorg.
  endif.

* begin of insertion MOD-002
*...................Transaction counter...............................*
  call method ycl_statistics=>record_transaction .
* end of insertion MOD-002
at selection-screen on s_perio.

  if s_vbeln-low  is initial and
     s_vbeln-high is initial.
    if not s_perio-high is initial.
*.... Fill in only 1 period !
      message e001(00) with text-e04.
    endif.
  endif.

*-INITIALIZATION--------------------------------------------------------
initialization.
* to become SDI statusses at F4
  export p_obtyp = 'VBP' to memory id 'PM_OBTYP'.


*-START OF SELECTION----------------------------------------------------
start-of-selection.

* Fill status tables
  refresh: gt_stai1,
           gt_stae1.

  loop at s_stai1.
    clear gt_stai1.
    move s_stai1-low to gt_stai1-low.
    append gt_stai1.
  endloop.

  loop at s_stae1.
    clear gt_stae1.
    move s_stae1-low to gt_stae1-low.
    append gt_stae1.
  endloop.

* Number of records in the status-tables
  describe table gt_stai1 lines g_stai1_lines.
  describe table gt_stae1 lines g_stae1_lines.

* Select order items
  select a~vbeln a~kunnr a~vkgrp b~posnr
         a~auart a~vkorg a~vtweg a~spart a~vkbur
         b~matnr b~arktx b~werks b~objnr b~matkl
        into corresponding fields of table gt_vbap
        from vbak as a
        inner join vbap as b
         on a~vbeln eq b~vbeln
*        inner join vbkd as c
*         on  a~vbeln eq c~vbeln
*         and b~posnr eq c~posnr
        where a~vbeln in s_vbeln
          and b~posnr in s_posnr
          and a~vkorg eq p_vkorg
          and a~kunnr in s_kunnr
          and a~vbtyp in (c_c, c_g, c_l, c_h, c_k)
*          and a~auart in (c_zo01, c_zc01, c_zor, c_zor1, c_zor2, c_zor3,
*                          c_z1cr, c_z1dr, c_zre1, c_zkr, c_zrk)
          and a~vkgrp in s_vkgrp
*          AND c~bzirk in s_bzirk
          and a~vkbur in s_vkbur
*          AND c~kdgrp in s_kdgrp
          and b~matnr in s_matnr
          and b~matkl in s_matkl.

  if gt_vbap[] is initial.
    write: / text-i01.              "No orders selected
    exit.
  endif.

  loop at gt_vbap.
* check on vbkd (first with item , if not found without item)
    select single kdgrp bzirk prsdt
       into (gt_vbap-kdgrp, gt_vbap-bzirk, gt_vbap-prsdt)
       from vbkd
      where vbeln = gt_vbap-vbeln
        and posnr = gt_vbap-posnr.
    if not sy-subrc = 0.
      select single kdgrp bzirk prsdt
        into  (gt_vbap-kdgrp, gt_vbap-bzirk, gt_vbap-prsdt)
        from vbkd
       where vbeln = gt_vbap-vbeln.
    endif.
    if not gt_vbap-bzirk in s_bzirk.
      delete gt_vbap.
      continue.
    endif.
    if not gt_vbap-kdgrp in s_kdgrp.
      delete gt_vbap.
      continue.
    endif.

*.. check status inclusive / exlusive
    if not ( g_stai1_lines is initial
        and g_stae1_lines is initial ) .
      perform check_statusses using gt_vbap-objnr.
      if not g_stai1_lines is initial and g_incl = ' '.
        delete gt_vbap.
        continue.
      elseif not g_stae1_lines is initial and g_excl = 'X'.
        delete gt_vbap.
        continue.
      endif.
    endif.

*.. check region and customer industrial code
    select single adrnr bran1 name1
       into (gv_adrnr, gt_vbap-bran1, gt_vbap-name1)
       from kna1 where kunnr eq gt_vbap-kunnr
                   and bran1 in s_bran1.

    if sy-subrc = 0.
*      modify gt_vbap transporting bran1 name1.
    else.
      delete gt_vbap.
      continue.
    endif.

    select single region country
       into (gt_vbap-region, gt_vbap-country)
       from adrc where addrnumber eq gv_adrnr
                   and date_from  eq '00010101'
                   and region     in s_region.

    if sy-subrc = 0.
*      modify gt_vbap transporting region country.
    else.
      delete gt_vbap.
      continue.
    endif.

    modify gt_vbap.

  endloop.

* Select CO-PA info
  sort gt_vbap by vbeln posnr.
  refresh: gt_copa.

  loop at gt_vbap.
    select kaufn kdpos vv100 vv112 vv113 vv114 vv130 vv120 vv110
           vv200 vv300 vv400 vv500 vv600 ww002 ww006 ww007 equnr
           kokrs prctr ww003 rec_waers vrgar budat
           paobjnr pasubnr vv140 vv150
* begin of insertion MOD-001
           bukrs
* end of insertion MOD-001
         from ce11000
         appending corresponding fields of table gt_copa
         where paledger eq '02'
           and vrgar    in ('C', 'B', 'F')
           and perio    in s_perio
           and kaufn    eq gt_vbap-vbeln
           and kdpos    eq gt_vbap-posnr
           and equnr    in s_equnr
           and prctr    in s_prctr
           and ww002    in s_plc
           and ww006    in s_gac
           and ww007    in s_pgc.
  endloop.

  if not gt_copa[] is initial.
    loop at gt_copa.
* begin of insertion MOD-001
      call function 'YSE_CONVERT_PRCTR_BL'
        exporting
          prctr_in    = gt_copa-prctr
          bukrs       = gt_copa-bukrs
        importing
          segment_out = gt_copa-prctr.
* end of insertion MOD-001
      select single ww002 into gv_ww002
          from ce41000
          where aktbo = gc_x
            and paobjnr = gt_copa-paobjnr
            and pasubnr = gt_copa-pasubnr.
      if sy-subrc = 0.
        gt_copa-ww002 = gv_ww002.
        modify gt_copa.
      endif.
    endloop.
  endif.

*-----------------------------------------------------------------------
end-of-selection.

* Cumulate detailrecords
  sort gt_copa by kaufn kdpos.
  refresh: gt_final.

  loop at gt_copa.
    move-corresponding gt_copa to gt_final.

    read table gt_vbap with key vbeln = gt_copa-kaufn
                                posnr = gt_copa-kdpos
                 binary search.
    move-corresponding gt_vbap to gt_final.

    "CD1K968676 begin
    clear gt_final-cont_type.
    case gt_vbap-matkl+4(2).
      when 'TR'.
        gt_final-cont_type = '* Total_Responsibity'.
      when 'PM'.
        gt_final-cont_type = '* Preventive_Maintenance'.
      when 'IP'.
        gt_final-cont_type = '* Inspection'.
      when 'CC'.
        gt_final-cont_type = '* Cover_Care'.
      when 'PP'.
        gt_final-cont_type = '* Parts_Only'.
      when 'XT'.
        gt_final-cont_type = '* AirExtend'.
      when '044Z'.
        gt_final-cont_type = '** Other_Contract_Type'.
      when others.
        gt_final-cont_type = ''.
    endcase.
    "CD1K968676 end

    gt_final-tot_vv200 = gt_copa-vv200.
    gt_final-tot_vv300 = gt_copa-vv300.
    gt_final-tot_vv400 = gt_copa-vv400.
    gt_final-tot_vv500 = gt_copa-vv500.
    gt_final-tot_vv600 = gt_copa-vv600.
    gt_final-tot_vv100 = gt_copa-vv100.
    gt_final-tot_vv112 = gt_copa-vv112.
    gt_final-tot_vv110 = gt_copa-vv110.
    gt_final-tot_vv114 = gt_copa-vv114.
    gt_final-tot_vv130 = gt_copa-vv130.
    gt_final-tot_vv140 = gt_copa-vv140.
    gt_final-tot_vv150 = gt_copa-vv150.

    collect gt_final.
    clear gt_final.
  endloop.

* Calculate remaining fields and get missing info
  loop at gt_final.
*.. Net invoiced sales
    gt_final-net_inv_sls = gt_final-tot_vv100 + gt_final-tot_vv110.

*.. Unadjusted COGS Actual
    gt_final-unadj_cogs = gt_final-tot_vv200 + gt_final-tot_vv300 +
           gt_final-tot_vv400 + gt_final-tot_vv500 + gt_final-tot_vv600 +
           gt_final-tot_vv112 + gt_final-tot_vv114 + gt_final-tot_vv130 +
           gt_final-tot_vv140 + gt_final-tot_vv150.

*BEGIN OF change MOD-004
  clear gt_final-unadj_cogs_seed.
  gt_final-unadj_cogs_seed = gt_final-unadj_cogs - gt_final-tot_vv112.
*END OF change MOD-004

*.. Unadjusted Gross Profit Actual
    gt_final-unadj_gp = gt_final-net_inv_sls - gt_final-unadj_cogs.

*.. Unadjusted Gross Profit Actual
    catch system-exceptions arithmetic_errors = 1.
      if not gt_final-net_inv_sls is initial.
        gt_final-%ugp = 100 * gt_final-unadj_gp / gt_final-net_inv_sls.
      endif.
    endcatch.
    if sy-subrc = 1.
      gt_final-%ugp = c_99999.
    endif.

*.. Get SDI status
    call function 'STATUS_TEXT_EDIT'
      exporting
        flg_user_stat    = 'X'
        objnr            = gt_final-objnr
        only_active      = 'X'
        spras            = sy-langu
      importing
        line             = gt_final-sttxt
      exceptions
        object_not_found = 1
        others           = 2.

*.. Get descriptions
*.. Profit Center
    select single ktext into gt_final-prctr_text
       from cepct where spras = sy-langu
                    and prctr = gt_final-prctr
                    and datbi = '99991231'
                    and kokrs = gt_final-kokrs.

*.. Sales organization
    select single vtext into gt_final-vkorg_text
       from tvkot where spras = sy-langu
                    and vkorg = gt_final-vkorg.

*.. Sales district
    select single bztxt into gt_final-bzirk_text
       from t171t where spras = sy-langu
                    and bzirk = gt_final-bzirk.

*.. Sales office
    select single bezei into gt_final-vkbur_text
       from tvkbt where spras = sy-langu
                    and vkbur = gt_final-vkbur.

*.. PLC
    select single bezek into gt_final-ww002_text
       from t25a1 where spras = sy-langu
                    and ww002 = gt_final-ww002.

*.. GAC
    select single bezek into gt_final-ww006_text
       from t25a3 where spras = sy-langu
                    and ww006 = gt_final-ww006.

*.. PGC
    if not gt_final-ww007 is initial.
      select single bezek into gt_final-ww007_text
         from t25a4 where spras = sy-langu
                      and ww007 = gt_final-ww007.
    endif.

*.. Plant
    select single name1 into gt_final-werks_text
       from t001w where werks = gt_final-werks.

*.. Equipment
    if not gt_final-equnr is initial.
      select single eqktx into gt_final-equnr_text
         from eqkt  where equnr = gt_final-equnr
                      and spras = sy-langu.
    endif.

*.. Region
    if not gt_final-region is initial.
      select single bezei into gt_final-region_text
         from t005u where spras = sy-langu
                      and land1 = gt_final-country
                      and bland = gt_final-region.
    endif.

*.. Sales group
    select single bezei into gt_final-vkgrp_text
       from tvgrt where spras = sy-langu
                    and vkgrp = gt_final-vkgrp.

*.. Customer group
    select single ktext into gt_final-kdgrp_text
       from t151t where spras = sy-langu
                    and kdgrp = gt_final-kdgrp.

*.. Customer industrial code
    select single vtext into gt_final-bran1_text
       from tbrct where spras = sy-langu
                    and braco = gt_final-bran1.

*.. Add new fields to internal table
    modify gt_final transporting net_inv_sls
                                 unadj_cogs
                                 unadj_gp
                                 %ugp
                                 sttxt
                                 prctr_text
                                 vkorg_text
                                 bzirk_text
                                 vkbur_text
                                 ww002_text
                                 ww006_text
                                 ww007_text
                                 werks_text
                                 equnr_text
                                 region_text
                                 vkgrp_text
                                 kdgrp_text
                                 bran1_text
                                 unadj_cogs_seed.       " MOD-004
  endloop.

* create ALV-GRID
  perform build_field_catlog changing gt_fieldcat.
  perform fill_events_f14.
  perform alv_display.


*- SUBROUTINES---------------------------------------------------------
*&---------------------------------------------------------------------*
*&      Form  build_field_catlog
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      <--P_GT_FIELDCAT  text
*----------------------------------------------------------------------*
form build_field_catlog  changing pt_fieldcat type slis_t_fieldcat_alv.

  data : ls_fcat type slis_fieldcat_alv.

*-----------------------Profit Center---------------------*
  ls_fcat-fieldname = 'PRCTR'.
*  ls_fcat-rollname = 'PRCTR'.
  ls_fcat-outputlen = '15'.
  select single bukrs into gv_bukrs2 from tvko
    where vkorg = p_vkorg.
  call function 'YSE_CHECK_DEV_MATRIX'
    exporting
      object      = 'YSE_NEWGL_ACTIVE'
      checkfield  = 'BUKRS'
      value_field = gv_bukrs2
      counter     = 1
    exceptions
      active      = 1
      passive     = 2
      not_found   = 3
      others      = 4.
  if sy-subrc = 1.
    gv_newgl_active = 'Y'.
  else.
    gv_newgl_active = 'N'.
  endif.
  if gv_newgl_active = 'Y'.
    ls_fcat-seltext_l = 'Segment'(i50).
  else.
    ls_fcat-seltext_l = 'Profit Center'(i49).
  endif.
  ls_fcat-no_convext = 'X'.
  ls_fcat-no_zero    = 'X'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Profit Center text----------------*
  ls_fcat-fieldname = 'PRCTR_TEXT'.
  ls_fcat-outputlen = '15'.
  if gv_newgl_active = 'Y'.
    ls_fcat-seltext_l = 'Segment (Text)'(i52).
  else.
    ls_fcat-seltext_l = 'Profit Center (Text)'(i11).
  endif.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Sales organization----------------*
  ls_fcat-fieldname = 'VKORG'.
  ls_fcat-rollname = 'VKORG'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Sales organization text-----------*
  ls_fcat-fieldname = 'VKORG_TEXT'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Sales Org.(Text)'(i12).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Region----------------------------*
  ls_fcat-fieldname = 'REGION'.
  ls_fcat-rollname = 'REGIO'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Region Text----------------------*
  ls_fcat-fieldname = 'REGION_TEXT'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Region (Text)'(i13).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Sales district--------------------*
  ls_fcat-fieldname = 'BZIRK'.
  ls_fcat-rollname = 'BZIRK'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Sales district text--------------*
  ls_fcat-fieldname = 'BZIRK_TEXT'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Sales district(Text)'(i14).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Sales office----------------------*
  ls_fcat-fieldname = 'VKBUR'.
  ls_fcat-rollname = 'VKBUR'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Sales office text--------------*
  ls_fcat-fieldname = 'VKBUR_TEXT'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Sales office(Text)'(i15).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Sales group---------------------*
  ls_fcat-fieldname = 'VKGRP'.
  ls_fcat-rollname = 'VKGRP'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Sales group text--------------*
  ls_fcat-fieldname = 'VKGRP_TEXT'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Sales group(Text)'(i55).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Acc.indicator-------------------*
  ls_fcat-fieldname = 'WW003'.
  ls_fcat-rollname = 'RKEG_WW003'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Customer industrial code--------*
  ls_fcat-fieldname = 'BRAN1'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Customer Industrial Code'(i16).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Customer ind.code text------*
  ls_fcat-fieldname = 'BRAN1_TEXT'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Customer Ind.Code(Text)'(i17).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Customer group------------------*
  ls_fcat-fieldname = 'KDGRP'.
  ls_fcat-rollname = 'KDGRP'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Customer group text-----------*
  ls_fcat-fieldname = 'KDGRP_TEXT'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Customer group(Text)'(i18).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Customer------------------------*
  ls_fcat-fieldname = 'KUNNR'.
  ls_fcat-rollname = 'KUNNR'.
  ls_fcat-no_convext = 'X'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Customer name-----------------*
  ls_fcat-fieldname = 'NAME1'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Customer Name'(i19).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Distr.Channel--------------------*
  ls_fcat-fieldname = 'VTWEG'.
  ls_fcat-rollname = 'VTWEG'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Division-------------------------*
  ls_fcat-fieldname = 'SPART'.
  ls_fcat-rollname = 'SPART'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------PLC-------------------------------*
  ls_fcat-fieldname = 'WW002'.
  ls_fcat-rollname = 'RKEG_WW002'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------PLC text----------------------*
  ls_fcat-fieldname = 'WW002_TEXT'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'PLC(Text)'(i20).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------GAC-------------------------------*
  ls_fcat-fieldname = 'WW006'.
  ls_fcat-rollname = 'RKEG_WW006'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------GAC text----------------------*
  ls_fcat-fieldname = 'WW006_TEXT'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'GAC(Text)'(i21).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------PGC-------------------------------*
  ls_fcat-fieldname = 'WW007'.
  ls_fcat-rollname = 'RKEG_WW007'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------PGC text----------------------*
  ls_fcat-fieldname = 'WW007_TEXT'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'PGC(Text)'(i22).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Material group--------------------*
  ls_fcat-fieldname = 'MATKL'.
  ls_fcat-rollname = 'MATKL'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Plant-----------------------------*
  ls_fcat-fieldname = 'WERKS'.
  ls_fcat-rollname = 'WERKS_D'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Plant text--------------------*
  ls_fcat-fieldname = 'WERKS_TEXT'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Plant(Text)'(i23).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Equipment-------------------------*
  ls_fcat-fieldname = 'EQUNR'.
  ls_fcat-rollname = 'EQUNR'.
  ls_fcat-no_convext = 'X'.
  ls_fcat-no_zero = 'X'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Equipment description---------*
  ls_fcat-fieldname = 'EQUNR_TEXT'.
  ls_fcat-outputlen = '40'.
  ls_fcat-seltext_l = 'Equipment description'(i24).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Sales document--------------------*
  ls_fcat-fieldname = 'VBELN'.
  ls_fcat-rollname = 'VBELN_VA'.
  ls_fcat-no_convext = 'X'.
  ls_fcat-no_zero    = 'X'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Sales document type---------------*
  ls_fcat-fieldname = 'AUART'.
  ls_fcat-rollname = 'AUART'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Sales Document Item----------------*
  ls_fcat-fieldname = 'POSNR'.
  ls_fcat-rollname = 'POSNR_VA'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
**-----------------------Billing Date-----------------------*
*  ls_fcat-fieldname = 'FADAT'.
*  ls_fcat-rollname = 'FADAT'.
*  APPEND ls_fcat TO pt_fieldcat.
*  CLEAR ls_fcat.
*-----------------------Posting Date-----------------------*
  ls_fcat-fieldname = 'BUDAT'.
  ls_fcat-rollname = 'DAERF'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------SDI status-------------------------*
  ls_fcat-fieldname = 'STTXT'.
  ls_fcat-rollname = 'J_STEXT'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Product---------------------------*
  ls_fcat-fieldname = 'MATNR'.
  ls_fcat-outputlen = '10'.
  ls_fcat-seltext_l = 'Product'(i29).
  ls_fcat-no_zero   = 'X'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Product description---------------*
  ls_fcat-fieldname = 'ARKTX'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Product description'(i30).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Revenues Actual---------------------*
  ls_fcat-fieldname = 'TOT_VV100'.
  ls_fcat-outputlen = '10'.
  ls_fcat-seltext_l = 'Revenues Actual'(i31).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Discount ----------------------*
  ls_fcat-fieldname = 'TOT_VV110'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Discount'(i32).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Net invoiced sales Actual----------*
  ls_fcat-fieldname = 'NET_INV_SLS'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Net invoiced sales Actual'(i33).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Unadjusted COS Actual----------*
  ls_fcat-fieldname = 'TOT_VV130'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Unadjusted COS Actual'(i34).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Parts Actual-------------------*
  ls_fcat-fieldname = 'TOT_VV200'.
  ls_fcat-outputlen = '10'.
  ls_fcat-seltext_l = 'Parts Actual'(i35).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Labour Actual-------------------*
  ls_fcat-fieldname = 'TOT_VV300'.
  ls_fcat-outputlen = '10'.
  ls_fcat-seltext_l = 'Labour Actual'(i36).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Mileage Actual-------------------*
  ls_fcat-fieldname = 'TOT_VV400'.
  ls_fcat-outputlen = '10'.
  ls_fcat-seltext_l = 'Mileage Actual'(i37).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Ad Hoc Expenses Actual-------------------*
  ls_fcat-fieldname = 'TOT_VV600'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Ad Hoc Expenses Actual'(i38).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Subcontracting Actual-------------------*
  ls_fcat-fieldname = 'TOT_VV500'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Subcontracting Actual'(i39).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------COGS-Contract provis Actual-------------*
  ls_fcat-fieldname = 'TOT_VV112'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'COGS-Contract provis Actual'(i40).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Fix price accruals Actual---------------*
  ls_fcat-fieldname = 'TOT_VV114'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Fix price accruals Actual'(i41).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Unadjusted COGS Actual------------------*
  ls_fcat-fieldname = 'UNADJ_COGS'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Unadjusted COGS Actual'(i42).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Unadjusted Gross Profit Actual----------*
  ls_fcat-fieldname = 'UNADJ_GP'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Unadjusted Gross Profit Actual'(i43).
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------% UGP Actual----------------------------*
  ls_fcat-fieldname = '%UGP'.
  ls_fcat-outputlen = '10'.
  ls_fcat-seltext_l = '% UGP Actual'(i44).
  ls_fcat-no_sum    = 'X'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*-----------------------Currency------------------------*
  ls_fcat-fieldname = 'REC_WAERS'.
  ls_fcat-rollname = 'WAERS'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.

  "CD1K968676 begin
  ls_fcat-fieldname = 'CONT_TYPE'.
  ls_fcat-outputlen = '32'.
  ls_fcat-seltext_l = 'Contract Type'(i56).
  ls_fcat-no_sum    = 'X'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.

  "CD1K968676 end

*  BEGIN OF CHANGE MOD-004
*-----------------------Unadjusted COGS Actual-SEED------------------*
  ls_fcat-fieldname = 'UNADJ_COGS_SEED'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Unadjusted COGS Actual-SEED'.
  append ls_fcat to pt_fieldcat.
  clear ls_fcat.
*  END OF CHANGE MOD-004

endform.                    " build_field_catlog

*&---------------------------------------------------------------------*
*&      Form  fill_events_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form fill_events_f14 .

  data lv_event       type slis_alv_event.

  call function 'REUSE_ALV_EVENTS_GET'
    exporting
      i_list_type = 0
    importing
      et_events   = gt_events_tab.

*--- allocate form for user-command ---------------------------------*
  read table gt_events_tab with key name = slis_ev_user_command
                        into lv_event.
  if sy-subrc = 0.
    move gv_form_user_command to lv_event-form.
    modify gt_events_tab from lv_event index sy-tabix.
  endif.

endform.                    " fill_events_f14

*&--------------------------------------------------------------------*
*&      Form  user_command_l
*&--------------------------------------------------------------------*
*       text
*---------------------------------------------------------------------*
*      -->P_UCOMM    text
*      -->P_SELFIELD text
*---------------------------------------------------------------------*
form user_command_l using p_ucomm like sy-ucomm
                          p_selfield type slis_selfield.

  p_selfield-refresh = 'S'.
  perform check_pf2_with_object_f16 using p_ucomm.
  perform set_p_selfield_general_f16 using p_selfield.

  case p_ucomm.
    when 'ISEL'.
      p_ucomm = 'DISP'.
      perform fcodes_with_mark_f16 using p_ucomm p_selfield.
  endcase.

endform.                    "user_command_l

*&---------------------------------------------------------------------*
*&      Form  check_pf2_with_object_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*----------------------------------------------------------------------*
form check_pf2_with_object_f16  using    p_ucomm.

  check p_ucomm = gv_ic1.
  p_ucomm = 'ISEL'.

endform.                    " check_pf2_with_object_f16

*&---------------------------------------------------------------------*
*&      Form  set_p_selfield_general_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
form set_p_selfield_general_f16  using f_selfield type slis_selfield.

  f_selfield-col_stable = gc_x.
  f_selfield-row_stable = gc_x.

endform.                    " set_p_selfield_general_f16

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*      -->P_ENDCASE  text
*----------------------------------------------------------------------*
form fcodes_with_mark_f16  using p_ucomm like sy-ucomm
                                p_selfield type slis_selfield.

  perform check_object_tab_marked_f14 using p_ucomm p_selfield.

  loop at gt_final where selected = gc_x .
    gv_index = sy-tabix.
    perform fcodes_with_mark_l using p_ucomm p_selfield.
    gt_final-selected = ' '.
    modify gt_final index gv_index.
  endloop.

  clear p_ucomm.

endform.                    " fcodes_with_mark_f16

*&---------------------------------------------------------------------*
*&      Form  check_object_tab_marked_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
form check_object_tab_marked_f14  using    p_ucomm like sy-ucomm
                                        p_selfield type slis_selfield.

  read table gt_final with key selected = gc_x.

  if not sy-subrc is initial.
    if not p_selfield-tabindex is initial.
      read table gt_final index p_selfield-tabindex.
      gt_final-selected = gc_x.
      modify gt_final index p_selfield-tabindex.
    endif.
  else.
*--- Checkbox markiert -----------------------------------------------*
    p_selfield-sel_tab_field = 'G_MARK'.
  endif.

endform.                    " check_object_tab_marked_f14

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_l
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_F_UCOMM  text
*      -->P_F_SELFIELD  text
*----------------------------------------------------------------------*
form fcodes_with_mark_l  using   p_ucomm like sy-ucomm
                              p_selfield type slis_selfield.

  data: h_ucomm like sy-ucomm.

  case p_ucomm.
*   Display Contract
    when 'DISP'.
      set parameter id 'KTN' field gt_final-vbeln.
      call transaction 'VA43' and skip first screen.
  endcase.

endform.                    " fcodes_with_mark_l

*&---------------------------------------------------------------------*
*&      Form  alv_display
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form alv_display .

  gv_repid = sy-repid.

  call function 'REUSE_ALV_GRID_DISPLAY'
     exporting
       i_callback_program                =  gv_repid
       i_save                            = 'A'
       it_events                         =  gt_events_tab[]
*      I_GRID_TITLE                      =
*      I_GRID_SETTINGS                   =
*      is_layout                         =  g_layout
       it_fieldcat                       =  gt_fieldcat[]
     tables
        t_outtab                         =  gt_final.

endform.                    " alv_display

*&---------------------------------------------------------------------*
*&      Form  check_statusses
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form check_statusses using r_objnr.

  clear: g_excl,
         g_incl.

  refresh: h_status_tab,
           h_status_text_tab.

* get active statusses
  call function 'STATUS_READ'
    exporting
      objnr       = r_objnr
      only_active = 'X'
    tables
      status      = h_status_tab
    exceptions
      others      = 01.

  check sy-subrc = 0.

* get status descriptions
  loop at h_status_tab.
    call function 'STATUS_NUMBER_CONVERSION'
      exporting
        language      = sy-langu
        objnr         = r_objnr
        status_number = h_status_tab-stat
      importing
        txt04         = h_status_text_tab-txt04
      exceptions
        others        = 01.
    if sy-subrc = 0.
      append h_status_text_tab.
    endif.
  endloop.

* check status inclusive
  if not g_stai1_lines is initial.
    loop at h_status_text_tab.
      loop at gt_stai1.
        if gt_stai1-low = h_status_text_tab-txt04.
          g_incl = 'X'.
          exit.
        endif.
      endloop.
      if g_incl = 'X'.
        exit.
      endif.
    endloop.
  endif.

* check status exclusive
  if not g_stae1_lines is initial.
    loop at h_status_text_tab.
      loop at gt_stae1.
        if gt_stae1-low = h_status_text_tab-txt04.
          g_excl = 'X'.
          exit.
        endif.
      endloop.
      if g_excl = 'X'.
        exit.
      endif.
    endloop.
  endif.

endform.                    "check_statusse

*Text symbol text£º
*001:Selection screen input
*E03:No authorization for sales organisation: &1
*E04:Fill in only 1 period !
*E05:Pls Fill in SalesOrder Creation or TECO Date
*I01:No contracts selected !
*I10:Act_rev
*I11:Profit Center (Text)
*I12:Sales Org.(Text)
*I13:Region (Text)
*I14:Sales district(Text)
*I15:Sales office(Text)
*I16:Customer Industrial Code
*I17:Customer Ind.Code(Text)
*I18:Customer group(Text)
*I19:Customer Name
*I20:PLC(Text)
*I21:GAC(Text)
*I22:PGC(Text)
*I23:Plant(Text)
*I24:Equipment description
*I25:Contract
*I26:Contract item invoiced
*I27:Contract item invoice outstanding
*I28:Contract item value total
*I29:Product
*I30:Product description
*I31:Revenues Actual
*I32:Discount
*I33:Net invoiced sales Actual
*I34:Unadjusted COS Actual
*I35:Parts Actual
*I36:Labour Actual
*I37:Mileage Actual
*I38:Ad Hoc Expenses Actual
*I39:Subcontracting Actual
*I40:COGS-Contract provis Actual
*I41:Fix price accruals Actual
*I42:Unadjusted COGS Actual
*I43:Unadjusted Gross Profit Actual
*I44:% UGP Actual
*I45:Tot.Revenues Planned
*I46:Tot.Costs Planned
*I47:Tot.Planned GP value
*I48:Tot.Planned GP value %
*I49:Profit Center
*I50:Segment
*I52:Segment (Text)
*I55:Sales group(Text)

*I56:Contract Type
*Selection text£º
*P_VKORG:D       .
*S_BRAN1:        Customer industrial code
*S_BZIRK:D       .
*S_EQUNR:D       .
*S_GAC:D       .
*S_KDGRP:D       .
*S_KUNNR:        Customer
*S_MATKL:D       .
*S_MATNR:        Product
*S_PERIO:D       .
*S_PGC:D       .
*S_PLC:D       .
*S_POSNR:D       .
*S_PRCTR:D       .
*S_REGION:D       .
*S_STAE1:        SDI Status excluded
*S_STAI1:        SDI Status included
*S_VBELN:D       .
*S_VKBUR:D       .
*S_VKGRP:D       .
