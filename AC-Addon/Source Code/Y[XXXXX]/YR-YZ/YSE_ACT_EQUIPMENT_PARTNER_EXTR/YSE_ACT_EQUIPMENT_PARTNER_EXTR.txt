*&---------------------------------------------------------------------*
*& Report  YSE_ACT_EQUIPMENT_PARTNER_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Equipment Partners extraction                                *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 05.09.2012
*  Change Request Number :
*  Transport request Nr. : CD1K973154
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 19.03.2014 | Jules Smets     | CD1K980489    | CR1794 (Act)*
*----------------------------------------------------------------------*
* MOD-002 | 25.01.2017 | Geert Rutten    | CD1K990791    | PBCS1051    *
*----------------------------------------------------------------------*
************************************************************************

REPORT  yse_act_equipment_partner_extr.

TABLES: equi,
        equz,
        iloa,
        iflot,
        jest,
        ihpa,
        kna1,
        adrc.

TYPES: BEGIN OF ty_equi,
         equnr      TYPE equnr,
         objnr      TYPE j_objnr,
         tplnr      TYPE tplnr,
         tplma      TYPE tplnr,
      END OF ty_equi.

TYPES: BEGIN OF ty_dlfl,
         objnr      TYPE j_objnr,
      END OF ty_dlfl.

TYPES: BEGIN OF ty_iflo,
         tplnr      TYPE tplnr,
         objnr      TYPE j_objnr,
      END OF ty_iflo.

TYPES: BEGIN OF ty_ihpa,
         objnr      TYPE j_objnr,
         parvw      TYPE parvw,
         parnr      TYPE kunnr,
       END OF ty_ihpa.

TYPES: BEGIN OF ty_adrc,
         kunnr      TYPE kunnr,
         sort1      TYPE ad_sort1,
       END OF ty_adrc.

DATA: gt_equi  TYPE TABLE OF ty_equi
                    WITH HEADER LINE.

DATA: gt_dlfl  TYPE HASHED TABLE OF ty_dlfl
                    WITH UNIQUE KEY objnr
                    WITH HEADER LINE,
      gt_dlfli TYPE TABLE OF ty_dlfl
                    WITH HEADER LINE.

DATA: gt_iflo  TYPE HASHED TABLE OF ty_iflo
                    WITH UNIQUE KEY tplnr
                    WITH HEADER LINE,
      gt_ifloi TYPE TABLE OF ty_iflo
                    WITH HEADER LINE.

DATA: gt_ihpa  TYPE HASHED TABLE OF ty_ihpa
                    WITH UNIQUE KEY objnr parvw
                    WITH HEADER LINE,
* Begin of insert MOD-002
      gs_ihpa TYPE ty_ihpa,
* End of insert MOD-002
      gt_ihpai TYPE TABLE OF ty_ihpa
                    WITH HEADER LINE.

DATA: gt_adrc  TYPE HASHED TABLE OF ty_adrc
                    WITH UNIQUE KEY kunnr
                    WITH HEADER LINE,
      gt_adrci TYPE TABLE OF ty_adrc
                    WITH HEADER LINE.

DATA: BEGIN OF gt_out  OCCURS 0,
        equnr       TYPE equnr,
        tab01(1)    TYPE c,
        parnr_we    TYPE kunnr,
        tab02(1)    TYPE c,
        sort1_we    TYPE ad_sort1,
        tab03(1)    TYPE c,
        parnr_ag    TYPE kunnr,
        tab04(1)    TYPE c,
        sort1_ag    TYPE ad_sort1,
        tab05(1)    TYPE c,
        parnr_re    TYPE kunnr,
        tab06(1)    TYPE c,
        sort1_re    TYPE ad_sort1,
        tab07(1)    TYPE c,
        parnr_rg    TYPE kunnr,
        tab08(1)    TYPE c,
        sort1_rg    TYPE ad_sort1,
        tab09(1)    TYPE c,
        install(10) TYPE c,
        tab10(1)    TYPE c,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        equnr       TYPE fieldname,
        tab01(1)    TYPE c,
        parnr_we    TYPE fieldname,
        tab02(1)    TYPE c,
        sort1_we    TYPE fieldname,
        tab03(1)    TYPE c,
        parnr_ag    TYPE fieldname,
        tab04(1)    TYPE c,
        sort1_ag    TYPE fieldname,
        tab05(1)    TYPE c,
        parnr_re    TYPE fieldname,
        tab06(1)    TYPE c,
        sort1_re    TYPE fieldname,
        tab07(1)    TYPE c,
        parnr_rg    TYPE fieldname,
        tab08(1)    TYPE c,
        sort1_rg    TYPE fieldname,
        tab09(1)    TYPE c,
        install     TYPE fieldname,
        tab10(1)    TYPE c,
      END OF gt_outh.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_error         TYPE xfeld,
      gv_parnr         TYPE kunnr,
      gv_sort1         TYPE ad_sort1.

CONSTANTS: gc_stat_dl  TYPE j_status  VALUE 'I0076',
           gc_stat_in  TYPE j_status  VALUE 'I0320',
           gc_stat_scr TYPE j_status  VALUE 'E0005'.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
PARAMETERS: p_bukrs  LIKE iloa-bukrs  OBLIGATORY  MEMORY ID buk.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_equnr  FOR equi-equnr.
SELECTION-SCREEN  END OF BLOCK input.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_equi[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No equipments selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Equipment time segment
  SELECT e~equnr e~objnr l~tplnr f~tplma
         INTO TABLE gt_equi
         FROM equi AS e
         INNER JOIN equz AS z
                    ON z~equnr = e~equnr
         INNER JOIN iloa AS l
                    ON l~iloan = z~iloan
*         INNER JOIN iflot AS f
         LEFT OUTER JOIN iflot AS f
                    ON f~tplnr = l~tplnr
         WHERE e~equnr IN s_equnr
           AND z~datbi =  '99991231'
           AND l~bukrs =  p_bukrs.
  CHECK NOT gt_equi[] IS INITIAL.

*** MOD-001 * begin ***
** Check for deleted/scrapped equipments
*  SELECT objnr
*         INTO TABLE gt_dlfli
*         FROM jest
*         FOR ALL ENTRIES IN gt_equi
*         WHERE objnr = gt_equi-objnr
*           AND ( stat = gc_stat_dl   OR
*                 stat = gc_stat_in   OR
*                 stat = gc_stat_scr )
*           AND inact = ' '.
*  SORT gt_dlfli BY objnr.
*  DELETE ADJACENT DUPLICATES FROM gt_dlfli
*                                  COMPARING objnr.
*  gt_dlfl[] = gt_dlfli[].
*  FREE gt_dlfli.
*
*  LOOP AT gt_equi.
*    READ TABLE gt_dlfl WITH TABLE KEY objnr = gt_equi-objnr.
*    IF sy-subrc = 0.
*      DELETE gt_equi.
*    ENDIF.
*  ENDLOOP.
*  FREE gt_dlfl.
*  CHECK NOT gt_equi[] IS INITIAL.
*** MOD-001 * end ***

* Function location data
  SELECT tplnr objnr
         INTO TABLE gt_ifloi
         FROM iflot
         FOR ALL ENTRIES IN gt_equi
         WHERE tplnr = gt_equi-tplnr
            OR tplnr = gt_equi-tplma.
  SORT gt_ifloi BY tplnr.
  DELETE ADJACENT DUPLICATES FROM gt_ifloi
                                  COMPARING tplnr.
  gt_iflo[] = gt_ifloi[].
  FREE gt_ifloi.

* Partner data
* Equipments
  SELECT objnr parvw parnr
         INTO TABLE gt_ihpai
         FROM ihpa
         FOR ALL ENTRIES IN gt_equi
         WHERE objnr    = gt_equi-objnr
           AND parvw   IN ('AG', 'RE', 'RG', 'WE')
           AND kzloesch = ' '.
* Functional locations
  SELECT objnr parvw parnr
         APPENDING TABLE gt_ihpai
         FROM ihpa
         FOR ALL ENTRIES IN gt_iflo
         WHERE objnr    = gt_iflo-objnr
           AND parvw   IN ('AG', 'RE', 'RG', 'WE')
           AND kzloesch = ' '.

  SORT gt_ihpai BY objnr parvw.
  DELETE ADJACENT DUPLICATES FROM gt_ihpai
                                  COMPARING objnr parvw.
  gt_ihpa[] = gt_ihpai[].
  FREE gt_ihpai.

* Begin of insert MOD-002

  LOOP AT gt_ihpa INTO gs_ihpa.
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        INPUT         = gs_ihpa-parnr
      IMPORTING
       OUTPUT        = gs_ihpa-parnr.

    MODIFY TABLE gt_ihpa FROM gs_ihpa.
  ENDLOOP.

* End of insert MOD-002

* Search terms
  SELECT k~kunnr a~sort1
         INTO TABLE gt_adrci
         FROM kna1 AS k
         INNER JOIN adrc AS a
                    ON a~addrnumber = k~adrnr
         FOR ALL ENTRIES IN gt_ihpa
         WHERE k~kunnr = gt_ihpa-parnr
           AND a~sort1 NE ' '.
  SORT gt_adrci BY kunnr.
  DELETE ADJACENT DUPLICATES FROM gt_adrci
                                  COMPARING kunnr.
  gt_adrc[] = gt_adrci[].
  FREE gt_adrci.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_equi.

    CLEAR: gt_out.
    MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03,
                 gt_out-tab04, gt_out-tab05, gt_out-tab06,
                 gt_out-tab07, gt_out-tab08, gt_out-tab09,
                 gt_out-tab10.

    gt_out-equnr = gt_equi-equnr.

*   Ship-to Party (SH)
    PERFORM get_partner USING 'WE'.
    gt_out-parnr_we = gv_parnr.
    PERFORM get_sort1.
    gt_out-sort1_we = gv_sort1.

*   Sold-to Party (SP)
    PERFORM get_partner USING 'AG'.
    gt_out-parnr_ag = gv_parnr.
    PERFORM get_sort1.
    gt_out-sort1_ag = gv_sort1.

*   Bill-to Party (BP)
    PERFORM get_partner USING 'RE'.
    gt_out-parnr_re = gv_parnr.
    PERFORM get_sort1.
    gt_out-sort1_re = gv_sort1.

*   Payer Party (PY)
    PERFORM get_partner USING 'RG'.
    gt_out-parnr_rg = gv_parnr.
    PERFORM get_sort1.
    gt_out-sort1_rg = gv_sort1.

    APPEND gt_out.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  GET_PARTNER
*&---------------------------------------------------------------------*
*       Get partner from equipment or functional mocation
*----------------------------------------------------------------------*
*      -->P_PARVW : Partner function
*----------------------------------------------------------------------*
FORM get_partner  USING    p_parvw.

  CLEAR gv_parnr.

* Equipment
  READ TABLE gt_ihpa
             WITH TABLE KEY objnr = gt_equi-objnr
                            parvw = p_parvw.
  IF sy-subrc = 0.
    gv_parnr = gt_ihpa-parnr.
    EXIT.
  ENDIF.

* Functional location
  READ TABLE gt_iflo
             WITH TABLE KEY tplnr = gt_equi-tplnr.
  IF sy-subrc = 0.
    READ TABLE gt_ihpa
               WITH TABLE KEY objnr = gt_iflo-objnr
                              parvw = p_parvw.
    IF sy-subrc = 0.
      gv_parnr = gt_ihpa-parnr.
      EXIT.
    ENDIF.
  ENDIF.

* Superior functional location
  READ TABLE gt_iflo
             WITH TABLE KEY tplnr = gt_equi-tplma.
  IF sy-subrc = 0.
    READ TABLE gt_ihpa
               WITH TABLE KEY objnr = gt_iflo-objnr
                              parvw = p_parvw.
    IF sy-subrc = 0.
      gv_parnr = gt_ihpa-parnr.
    ENDIF.
  ENDIF.

ENDFORM.                    " GET_PARTNER

*&---------------------------------------------------------------------*
*&      Form  GET_SORT1
*&---------------------------------------------------------------------*
*       Get search term for partner
*----------------------------------------------------------------------*
FORM get_sort1 .

  CLEAR gv_sort1.

  CHECK NOT gv_parnr IS INITIAL.

  READ TABLE gt_adrc WITH TABLE KEY kunnr = gv_parnr.
  IF sy-subrc = 0.
    gv_sort1 = gt_adrc-sort1.
  ENDIF.

ENDFORM.                    " GET_SORT1

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

  CONCATENATE 'EQUIPMENT' 'PARTNERS' p_bukrs
             INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM fill_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

* Fill output
  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  FILL_HEADER
*&---------------------------------------------------------------------*
*       Fill header
*----------------------------------------------------------------------*
FORM fill_header .

  CLEAR: gt_outh.
  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03,
               gt_outh-tab04, gt_outh-tab05, gt_outh-tab06,
               gt_outh-tab07, gt_outh-tab08, gt_outh-tab09,
               gt_outh-tab10.

  gt_outh-equnr     = 'EQUNR'.
  gt_outh-parnr_we  = 'PARNR_we'.
  gt_outh-sort1_we  = 'SORT1_we'.
  gt_outh-parnr_ag  = 'PARNR_ag'.
  gt_outh-sort1_ag  = 'SORT1_ag'.
  gt_outh-parnr_re  = 'PARNR_re'.
  gt_outh-sort1_re  = 'SORT1_re'.
  gt_outh-parnr_rg  = 'PARNR_rg'.
  gt_outh-sort1_rg  = 'SORT1_rg'.
  gt_outh-install   = '(Dummy)'.
  APPEND gt_outh.

  gt_outh-equnr     = 'Equipment'.
  gt_outh-parnr_we  = 'Ship-to'.
  gt_outh-sort1_we  = 'Search Term (Ship-to)'.
  gt_outh-parnr_ag  = 'Sold-to'.
  gt_outh-sort1_ag  = 'Search Term (Sold-to)'.
  gt_outh-parnr_re  = 'Bill-to'.
  gt_outh-sort1_re  = 'Search Term (Bill-to)'.
  gt_outh-parnr_rg  = 'Payer'.
  gt_outh-sort1_rg  = 'Search Term (Payer)'.
  gt_outh-install   = 'Installed at'.
  APPEND gt_outh.

ENDFORM.                    " FILL_HEADER

*Text symbol text£º
*E01:No equipments selected
*E02:Could not open output file
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_BUKRS:D       .
*S_EQUNR:D       .
