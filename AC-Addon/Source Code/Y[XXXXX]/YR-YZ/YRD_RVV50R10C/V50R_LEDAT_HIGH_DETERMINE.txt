*----------------------------------------------------------------------*
***INCLUDE V50R_LEDAT_HIGH_DETERMINE .
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  LEDAT_HIGH_DETERMINE
*&---------------------------------------------------------------------*
FORM ledat_high_determine
*                                                  "b change HP 312104
     USING    if_ledat_high_max TYPE shp_vl10_ledat_high_max
     CHANGING ct_ledat          TYPE shp_ledat_range_t
              cf_ledat_high     TYPE shp_vl10_ledat_high.
*                                                  "e change HP 312104

DATA:
  lf_ledat       TYPE datum
, ls_ledat       TYPE shp_ledat_range
.
  CLEAR cf_ledat_high.

  DESCRIBE TABLE ct_ledat LINES sy-tfill.                  "v_SP_364965
  IF sy-tfill > 1.
*   sicher ist sicher. Es gab Selektionsvafrianten mit mehr
*   als einer Zeile. Das dar fnicht sein.
    DELETE st_ledat FROM 2 TO sy-tfill.
  ENDIF.                                                   "^_SP_364965

  READ TABLE ct_ledat INTO ls_ledat INDEX 1.                 "SP_324680
  IF sy-subrc = 0.

    IF       ls_ledat-sign   = 'I'.               "Including

      IF     ls_ledat-option = 'BT'. cf_ledat_high = ls_ledat-high.
      ELSEIF ls_ledat-option = 'EQ'. cf_ledat_high = ls_ledat-low.
      ELSEIF ls_ledat-option = 'GE'. cf_ledat_high = gf_dat_max.
      ELSEIF ls_ledat-option = 'GT'. cf_ledat_high = gf_dat_max.
      ELSEIF ls_ledat-option = 'LE'. cf_ledat_high = ls_ledat-low.
      ELSEIF ls_ledat-option = 'LT'. cf_ledat_high = ls_ledat-low.
      ELSEIF ls_ledat-option = 'NB'.
        IF     ls_ledat-high < gf_dat_max.
               cf_ledat_high = gf_dat_max.
        ELSEIF ls_ledat-low > '00010101'.
               cf_ledat_high = ls_ledat-low - 1.
        ELSE.  cf_ledat_high = '00010101'.
        ENDIF.
      ELSEIF ls_ledat-option = 'NE'.
        IF     ls_ledat-low  = gf_dat_max.
               cf_ledat_high = gf_dat_max - 1.
        ELSE.
               cf_ledat_high = gf_dat_max.
        ENDIF.
      ENDIF.

    ELSEIF   ls_ledat-sign   = 'E'.               "Excluding

      IF       ls_ledat-option = 'BT'.
        IF     ls_ledat-high < gf_dat_max.
               cf_ledat_high = gf_dat_max.
        ELSEIF ls_ledat-low > '00010101'.
               cf_ledat_high = ls_ledat-low - 1.
        ELSE.  cf_ledat_high = '00010101'.
        ENDIF.
      ELSEIF ls_ledat-option = 'EQ'.
        IF   ls_ledat-low  = gf_dat_max.
             cf_ledat_high = gf_dat_max - 1.
        ELSE.
             cf_ledat_high = gf_dat_max.
        ENDIF.
      ELSEIF ls_ledat-option = 'GE'. cf_ledat_high = ls_ledat-low - 1.
      ELSEIF ls_ledat-option = 'GT'. cf_ledat_high = ls_ledat-low.
      ELSEIF ls_ledat-option = 'LE'. cf_ledat_high = gf_dat_max.
      ELSEIF ls_ledat-option = 'LT'. cf_ledat_high = gf_dat_max.
      ELSEIF ls_ledat-option = 'NB'. cf_ledat_high = ls_ledat-high.
      ELSEIF ls_ledat-option = 'NE'. cf_ledat_high = ls_ledat-low.
      ENDIF.
    ENDIF.
  ELSE.
    ls_ledat-sign   = 'I'.
    ls_ledat-option = 'BT'.
    CLEAR ls_ledat-low.
    ls_ledat-high   = gf_dat_max.
    IF if_ledat_high_max > 0.
      ls_ledat-high = sy-datlo + if_ledat_high_max.
    ENDIF.
    APPEND ls_ledat TO ct_ledat.
    cf_ledat_high   = ls_ledat-high.
  ENDIF.

ENDFORM.                    " LEDAT_HIGH_DETERMINE
