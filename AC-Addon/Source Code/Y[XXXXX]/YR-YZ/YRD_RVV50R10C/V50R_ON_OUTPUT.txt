*----------------------------------------------------------------------*
*   INCLUDE V50R_ON_OUTPUT                                             *
*----------------------------------------------------------------------*
*---------------
* Each enter
*---------------
*Feld            L#nge Typ Bedeutung
*SCREEN-NAME       30   C  Feldname
*SCREEN-GROUP1      3   C  Bewertung der Modif-Gruppe 1
*SCREEN-GROUP2      3   C  Bewertung der Modif-Gruppe 2
*SCREEN-GROUP3      3   C  Bewertung der Modif-Gruppe 3
*SCREEN-GROUP4      3   C  Bewertung der Modif-Gruppe 4
*SCREEN-REQUIRED    1   C  Feld Mu#eingabe
*SCREEN-INPUT       1   C  Feld eingabebereit
*SCREEN-OUTPUT      1   C  Feld wird angezeigt
*SCREEN-INTENSIFIED 1   C  Feld helleuchtend
*SCREEN-INVISIBLE   1   C  Feld unsichtbar
*SCREEN-LENGTH      1   X  L#nge des Feldes
*SCREEN-ACTIVE      1   C  Feld aktiv

* MODIF ID = '001':    input   if Listprofil-attribute can be overwritt.
* MODIF ID = '002': no display if no SD-Documents are to be selected
* MODIF ID = '003': no display if no MM-Documents are to be selected
* MODIF ID = '004': no display if crit. for Route schedule  not allowed
* MODIF ID = '005': no display if crit. for Goodsissue time not allowed
* MODIF ID = '006': no display if no            deliveries are selected
* MODIF ID = '007': no display if no additional deliveries are selected
* MODIF ID = '008': no display if no Due-List is run (no LEDAT)
* MODIF ID = '009': no display if unchecked deliveries are not requested
* MODIF ID = 'SPE': no display if SPM is not actice


AT SELECTION-SCREEN OUTPUT.

  DATA: ls_submit_info LIKE rssubinfo.

* Init in first call after initialization
  PERFORM init_after__parameters_import.

* Variant maintenance active?
  CALL FUNCTION 'RS_SUBMIT_INFO'
       IMPORTING
            p_submit_info = ls_submit_info.


  LOOP AT SCREEN.

    CASE screen-name.
      WHEN 'P_SZENA'.   screen-input = 0.
      WHEN 'P_MM'.
*     Clear MM-selection-criteria if no mm-documents are to be selected
*     Store criteria to get them back on re-activation of mm-selection
        IF p_mm = space.
          CLEAR st_ebeln[].
          CLEAR st_reswk[].
        ENDIF.
      WHEN 'P_SD'.
*     Clear SD-selection-criteria if no sd-documents are to be selected
*     Store criteria to get them back on re-activation of sd-selection
        IF p_sd = space.
          CLEAR st_vbeln[].
          CLEAR st_kunnr[].
          CLEAR st_spdnr[].
          CLEAR st_vkbur[].
          CLEAR st_vkgrp[].
          CLEAR st_abtnr[].
          CLEAR st_lifsp[].
          CLEAR st_autlf[].
        ENDIF.
      WHEN 'P_LF'.
*     Clear Delivery-criteria if no deliveries are to be selected
        IF p_lf = space.
          CLEAR st_kodat[].
          CLEAR st_lddat[].
          CLEAR st_lifex[].
          CLEAR st_wbstk[].
          CLEAR st_lvstk[].
          CLEAR st_tddat[].
          CLEAR st_kkber[].
          CLEAR st_cmgst[].
          CLEAR st_koquk[].
          CLEAR st_pkstk[].
          CLEAR st_trsta[].

        ENDIF.

*      WHEN 'P_PROF1'

      WHEN 'P001'.
        IF ( gx_list_profil-change_allow    IS INITIAL
          OR gx_list_profil-allow_user_role IS INITIAL )
          AND ls_submit_info-mode_vari EQ space.
          screen-input  = 0.
        ENDIF.

      WHEN  'P_PRNAM'.
        IF gx_list_profil-allow_user_role IS INITIAL
        AND ls_submit_info-mode_vari EQ space.
          screen-input  = 0.
        ENDIF.


      WHEN  'PUSH16'.
        IF gx_list_profil-selektionen NA gc_sel_with_ledat.
          screen-input     = 0.
          screen-output    = 0.
          screen-invisible = 1.

        ENDIF.

      WHEN  'P_LF_1'.
        IF gx_list_profil-selektionen NA gc_sel_predecessor.
          screen-input     = 0.
          screen-output    = 0.
          screen-invisible = 1.
        ENDIF.

      WHEN  'P_LF_2'.
        IF p_lf_2 = space.                                  "v_SP_419962
         CLEAR: st_aufnr  , st_charg  , st_cmfre  , st_cmgst  , st_cmngv
              , st_ean11  , st_ernam  , st_erdat  , st_fkstk  , st_gbstk
              , st_kkber  , st_kodat  , st_koquk  , st_lddat  , st_lfart
              , st_lgnum  , st_lgtor  , st_lifex  , st_lstel  , st_lvstk
              , st_pkstk  , st_prvbe  , st_tddat  , st_traid  , st_trsta
              , st_wadat  , st_xblnr  , st_vkor2  , st_vtwe2
              , st_spar2  , st_vkbu2  , st_vkgr2  , st_abtn2  , st_prvbe
              , st_wada2  , st_wauh2  , st_waist  , st_lfdat  , st_werk2
              , st_lgor2  , st_lgtor  , st_vbel2  , st_sammg  , st_lifsk
              , st_matn2  , st_matk2  , st_ean11  , st_kdma2  , st_wakt2
              , st_kunn3  , st_kunw2  , st_spdn2  , st_rout2  , st_aulw2
              , st_tknum  , st_vsbe2  , st_lpri2  , st_kostk  , st_ctlpc
              , st_knkli  , st_grupp  , st_sbgrp.
       REFRESH: st_aufnr  , st_charg  , st_cmfre  , st_cmgst  , st_cmngv
              , st_ean11  , st_ernam  , st_erdat  , st_fkstk  , st_gbstk
              , st_kkber  , st_kodat  , st_koquk  , st_lddat  , st_lfart
              , st_lgnum  , st_lgtor  , st_lifex  , st_lstel  , st_lvstk
              , st_pkstk  , st_prvbe  , st_tddat  , st_traid  , st_trsta
              , st_wadat  , st_xblnr  , st_vkor2  , st_vtwe2
              , st_spar2  , st_vkbu2  , st_vkgr2  , st_abtn2  , st_prvbe
              , st_wada2  , st_wauh2  , st_waist  , st_lfdat  , st_werk2
              , st_lgor2  , st_lgtor  , st_vbel2  , st_sammg  , st_lifsk
              , st_matn2  , st_matk2  , st_ean11  , st_kdma2  , st_wakt2
              , st_kunn3  , st_kunw2  , st_spdn2  , st_rout2  , st_aulw2
              , st_tknum  , st_vsbe2  , st_lpri2  , st_kostk  , st_ctlpc
              , st_knkli  , st_grupp  , st_sbgrp.
        ENDIF.                                              "^_SP_419962

      WHEN  'P_LFOP'.
        IF  p_lf_2 = space
        AND gx_list_profil-selektionen NA gc_sel_predecessor.
          screen-input     = 0.
          screen-output    = 0.
          screen-invisible = 1.
        ENDIF.

*
*     Tabstrips deactivation
*
      WHEN 'S0S_TAB1'.                                    "general data
ENHANCEMENT-SECTION     v50r_on_output_04 SPOTS es_v50r_on_output INCLUDE BOUND.
        IF  gx_list_profil-sel_screen  CA gc_sel_screen_6_tabstrip
        AND gx_list_profil-selektionen NA gc_sel_predecessor.
          screen-invisible = 1.
        ENDIF.
END-ENHANCEMENT-SECTION.

      WHEN 'S0S_TAB2'.                                      "sales order
ENHANCEMENT-SECTION     v50r_on_output_20 SPOTS es_v50r_on_output INCLUDE BOUND .
        IF  gx_list_profil-sel_screen  CA gc_sel_screen_6_tabstrip
        AND gx_list_profil-selektionen NA gc_sel_sd.
          screen-invisible = 1.
        ENDIF.
END-ENHANCEMENT-SECTION.

      WHEN 'S0S_TAB3'.                                      "material
        IF  gx_list_profil-sel_screen  CA gc_sel_screen_6_tabstrip
        AND gx_list_profil-selektionen NA gc_sel_material_tabstrip.
          screen-invisible = 1.
        ENDIF.

      WHEN 'S0S_TAB4'.                                          "partner
        IF  gx_list_profil-sel_screen  CA gc_sel_screen_6_tabstrip
        AND (      gx_list_profil-selektionen NA gc_sel_partner_tabstrip
            OR (   gx_list_profil-selektionen NA gc_sel_predecessor
               AND p_lf_2 = space                            "SP_361757
            )  ).
          screen-invisible = 1.
        ENDIF.

      WHEN 'S0S_TAB5'.                                  "purchase orders
ENHANCEMENT-SECTION     v50r_on_output_05 SPOTS es_v50r_on_output INCLUDE BOUND.
        IF  gx_list_profil-sel_screen  CA gc_sel_screen_6_tabstrip
        AND gx_list_profil-selektionen NA gc_sel_mm.
          screen-invisible = 1.
        ENDIF.
END-ENHANCEMENT-SECTION.

      WHEN 'S0S_TAB7'.                                       "deliveries
ENHANCEMENT-SECTION     v50r_on_output_06 SPOTS es_v50r_on_output INCLUDE BOUND.
        IF  gx_list_profil-sel_screen  CA gc_sel_screen_6_tabstrip
        AND gx_list_profil-selektionen NA gc_sel_del.
          screen-invisible = 1.
*         default customizing because tab is deactivated     "SP_402549
          gx_list_profil-del_for_predec  = ' '.
          gx_list_profil-add_deliveries  = ' '.
          gx_list_profil-open_deliveries = 'X'.

                                                           "v_SP_419962
        ELSEIF gx_list_profil-sel_screen  NA gc_sel_screen_no_tabstrip
        AND gx_list_profil-selektionen EQ gc_sel_del
        AND order_criteria-dynnr = '1010'.
            order_criteria-dynnr = '1070'.
        ENDIF.                                             "^_SP_419962
END-ENHANCEMENT-SECTION.

      WHEN 'S0S_TAB9'
        OR 'S0S_TABA'.
        IF  gx_list_profil-sel_screen  CA gc_sel_screen_6_tabstrip.
          IF gx_list_profil-selektionen NA gc_sel_del
          OR p_lf_2 = space.
            screen-invisible = 1.
          ENDIF.
        ENDIF.

      WHEN 'S0S_TAB8'.                                 "production order
ENHANCEMENT-SECTION     v50r_on_output_07 SPOTS es_v50r_on_output INCLUDE BOUND.
          screen-invisible = 1.

END-ENHANCEMENT-SECTION.
      WHEN 'FC02'.

      WHEN          'T_PROF1'.
        t_prof1  = %_p_prof1_%_app_%-text.

      WHEN          'T_PROF2'.
        t_prof2  = %_p_prof2_%_app_%-text.

      WHEN          'T_PROF3'.
        t_prof3  = %_p_prof3_%_app_%-text.

      WHEN         'T_P_SD'.
        t_p_sd   =  %_p_sd_%_app_%-text.

      WHEN         'T_P_WE'.
        t_p_we   =  %_p_we_%_app_%-text.

      WHEN         'T_P_MM'.
        t_p_mm   =  %_p_mm_%_app_%-text.

      WHEN         'T_P_ST'.
        t_p_st   =  %_p_stock_%_app_%-text.

      WHEN         'T_P_LF'.
        t_p_lf   =  %_p_lf_%_app_%-text.

*     Introducing TM Integration part 1                                                         "v_n_1529684
*     Attention All future coding regarding TM integration must be placed below this comment
      WHEN         'T_P_TMS'.                                                                   "^_n_1529684
        t_p_tms  =  %_p_tms_%_app_%-text.
        IF cl_ops_switch_check=>ops_sfws_ui_erptms( ) IS INITIAL.
          screen-invisible = 1.
        ENDIF.
      WHEN         'P_TMS'.
        IF cl_ops_switch_check=>ops_sfws_ui_erptms( ) IS INITIAL.
          screen-invisible = 1.
        ENDIF.                                                                                  "v_n_1529684
*      Introducing TM Integration part 1
*      Coding NOT concerning TM integration must be placed after this comment                   "^_n_1529684

      WHEN         'T_P001'.
        t_p001   =  %_p001_%_app_%-text.

      WHEN          'T_LERUL'.
        t_lerul  = %_p_lerul_%_app_%-text.
ENHANCEMENT-SECTION     v50r_on_output_08 SPOTS es_v50r_on_output INCLUDE BOUND.
        IF gx_list_profil-selektionen NA gc_sel_with_ledat.
          screen-input     = 0.
          screen-output    = 0.
          screen-invisible = 1.
        ENDIF.
END-ENHANCEMENT-SECTION.

      WHEN         'T_LE_MAX'.
        IF NOT p001 IS INITIAL
        OR gx_list_profil-selektionen NA gc_sel_with_ledat.
          screen-invisible = 1.
        ELSE.
          t_le_max  = %_p_le_max_%_app_%-text.
        ENDIF.

*      WHEN         'T_P_LF_2'.
*        t_lerul  =  %_p_lf_2_%_app_%-text.

      WHEN          '%_ST_FCOD1_%_APP_%-OPTI_PUSH'.
        screen-input = 1.

      WHEN OTHERS.

ENHANCEMENT-POINT v50r_on_output_01 SPOTS es_v50r_on_output INCLUDE BOUND.


    ENDCASE.

ENHANCEMENT-SECTION     v50r_on_output_09 SPOTS es_v50r_on_output INCLUDE BOUND.
    IF screen-group1    = '001'.
      IF p001 = 'X' AND ls_submit_info-mode_vari EQ space.
        screen-input  = 0.
      ENDIF.
    ELSEIF screen-group1 = '002'.
      IF gx_list_profil-selektionen NA gc_sel_partner_tabstrip.
        screen-input  = 0.
        screen-output = 0.
        screen-invisible = 1.
      ENDIF.
    ELSEIF screen-group1 = '003'.
      IF gx_list_profil-selektionen NA gc_sel_mm.
        screen-input  = 0.
        screen-output = 0.
        screen-invisible = 1.
      ENDIF.
*     Don't show fields of EA-DFP in order screen,
      IF screen-group4 = '004' OR
         screen-group4 = '005'.
        IF g_active_eadfp IS INITIAL.
          screen-invisible = 1.
          screen-output    = 0.
          screen-input     = 0.
        ENDIF.
      ENDIF. "EA-DFP fields
    ELSEIF screen-group1 = '004'.
      IF gx_list_profil-show_sched_line NA gc_show_sched_lines_aul.
        screen-input  = 0.
        screen-output = 0.
        screen-invisible = 1.
      ENDIF.
    ELSEIF screen-group1 = '005'.
      IF gx_list_profil-show_sched_line NA gc_show_sched_lines_wau.
        screen-input  = 0.
        screen-output = 0.
        screen-invisible = 1.
      ENDIF.
    ELSEIF screen-group1 = '006'.
      IF gx_list_profil-selektionen NA gc_sel_del
      OR p_lf_1 IS INITIAL.
        screen-input  = 0.
        screen-output = 0.
        screen-invisible = 1.
      ENDIF.
    ELSEIF screen-group1 = '007'.
      IF gx_list_profil-selektionen NA gc_sel_del
      OR p_lf_2 IS INITIAL.
        screen-input  = 0.
        screen-output = 0.
        screen-invisible = 1.
      ENDIF.
    ELSEIF screen-group1 = '008'.
      IF gx_list_profil-selektionen NA gc_sel_with_ledat.
        screen-input     = 0.
        screen-output    = 0.
        screen-invisible = 1.
      ENDIF.
*   SPE OUTB (de)activate block for unchecked deliveries
    ELSEIF screen-group1 = 'SP1' AND p001 = charx.            "n_990782
        screen-input  = 0.                                    "n_990782
    ENDIF.
END-ENHANCEMENT-SECTION.

*   After the group-information is used to set screen      "v_SP_364965
*   maybe overwrite for an individual field
    CASE screen-name.

      WHEN 'P_LE_MAX'.
*       maximum number of days to allow delivery creation
*       don't show the field, if user role is not over-writable
*       don't show the field, if no sd/mm-orders are selected
        IF NOT p001 IS INITIAL
        OR gx_list_profil-selektionen NA gc_sel_with_ledat.
          screen-input  = 0.
          screen-output = 0.
          screen-invisible = 1.
        ENDIF.

      WHEN          '%_ST_FCOD1_%_APP_%-VALU_PUSH'
      OR            '%_ST_FCOD2_%_APP_%-VALU_PUSH'
      .
*       push-button popup show table of selection-parameters also
*       in display-mode
        screen-input = 1.

      WHEN OTHERS.
ENHANCEMENT-POINT v50r_on_output_10 SPOTS es_v50r_on_output INCLUDE BOUND.


    ENDCASE.


    MODIFY SCREEN.
  ENDLOOP.
