*----------------------------------------------------------------------*
*   INCLUDE V50R_DETERMINE_LEDAT                                       *
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  DETERMINE_LEDAT
*&---------------------------------------------------------------------*
form determine_ledat changing cx_list_profil type shp_vl10_list_profil
                              ct_ledat       type shp_ledat_range_t.

data:
  lf_week       like scal-week
, lf_date       like scal-date
, ls_ledat      type shp_ledat_range
.

  check st_ledat[] is initial.

  case cx_list_profil-ledat_rule.
    when '1'.
*   1. Example for selection of items due today.
      ls_ledat-sign   = 'I'.          "all items included...
      ls_ledat-option = 'BT'.         "between...            "SP_324680
      ls_ledat-high   = sy-datlo.     "       ...today and...
      ls_ledat-low    = sy-datlo.     "                   ...today.
      append ls_ledat to ct_ledat.

    when '2'.
*   2. Example for selection of items due today and tomorrow.
      ls_ledat-sign   = 'I'.          "all items included
      ls_ledat-option = 'BT'.         "between...
      ls_ledat-low    = sy-datlo.     "       ...today and...
      ls_ledat-high   = sy-datlo + 1. "                   ...tomorrow
      append ls_ledat to ct_ledat.

    when '3'.
*   3. Example for selection of all items due until end of next week
*     this week...
      call function 'DATE_GET_WEEK'
           exporting
                date         = sy-datlo                  "#EC DOM_EQUAL
           importing
                week         = lf_week
           exceptions
                date_invalid = 1.

      if sy-subrc <> 0.
        refresh ct_ledat.
        exit.
      endif.

*     ...beginning of this week....
      call function 'WEEK_GET_FIRST_DAY'
           exporting
                week         = lf_week
           importing
                date         = lf_date
           exceptions
                week_invalid = 1.

      if sy-subrc <> 0.
        refresh ct_ledat.
        exit.
      endif.

*     everything untill end of next weeknext week.
      lf_date = lf_date + 11.

      ls_ledat-sign   = 'I'.
      ls_ledat-option = 'BT'.
      clear ls_ledat-low.
      ls_ledat-high   = lf_date.
      append ls_ledat to ct_ledat.

    when '4'.
*   4. Example for selection of items due up to today
      ls_ledat-sign   = 'I'.          "all items included
      ls_ledat-option = 'BT'.         "between...
      clear ls_ledat-low.             "       ...ever  and...
      ls_ledat-high   = sy-datlo + 1. "                   ...tomorrow
      append ls_ledat to ct_ledat.

    when '9'.                                          %x4."#EC NOBREAK
      perform userexit_prepare_ledat using    cx_list_profil
                                     changing ct_ledat.

  endcase.

*                                                  "b change HP 312104
  perform ledat_high_determine using    cx_list_profil-ledat_high_max
                               changing ct_ledat[]           "cha 50 ULa
                                        cx_list_profil-ledat_high.
*                                                  "e change HP 312104

endform.                    " DETERMINE_LEDAT
