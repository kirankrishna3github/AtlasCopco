
************************************************************************
* Program ID           : YSE_SD_PREPAYSO                               *
* Program Title        : Sales order that need Pre-payment             *
* Author               : Inge Bruyndonckx                              *
* Date                 : 31.01.2006                                    *
* Change Request Number: 31012007-SE-REP-D071-Sales order
*                        that need Pre-payment (V1)
* Description          :                                               *
*----------------------------------------------------------------------*
REPORT  yse_sd_prepayso.

INCLUDE yse_sd_rep_include.
*----------------------------------------------------------------------*
* Data declarations                                                    *
*----------------------------------------------------------------------*

*--- Tables
TABLES: vbak, vbap, vbuk, tvbst.

*--- Type pools
TYPE-POOLS slis.

*--- Structures
DATA: gs_layout         TYPE slis_layout_alv.
DATA: gs_sd_alv-variant LIKE disvariant.
DATA: g_user_command          TYPE slis_formname VALUE 'USER_COMMAND'.

*--- Internal tables
DATA: it_fieldcat       TYPE slis_t_fieldcat_alv.

DATA: BEGIN OF i_vbak OCCURS 0,
      vbeln LIKE vbak-vbeln,
      auart LIKE vbak-auart,
      audat LIKE vbak-audat,
      vkorg LIKE vbak-vkorg,
      vtweg LIKE vbak-vtweg,
      kunnr LIKE vbak-kunnr,
      netwr LIKE vbak-netwr,
      waerk LIKE vbak-waerk,
      END OF i_vbak.

DATA: BEGIN OF i_vbap OCCURS 0,
      vbeln LIKE vbap-vbeln,
      posnr LIKE vbap-posnr,
      matnr LIKE vbap-matnr,
      kwmeng LIKE vbap-kwmeng,
      vrkme LIKE vbap-vrkme,
      END OF i_vbap.

DATA: BEGIN OF i_vbuk OCCURS 0,
      vbeln LIKE vbuk-vbeln,
      fksak LIKE vbuk-fksak,
      END OF i_vbuk.

DATA: BEGIN OF i_output OCCURS 0,
      auart LIKE vbak-auart,
      audat LIKE vbak-audat,
      vbeln LIKE vbak-vbeln,
      posnr LIKE vbap-posnr,
      vkorg LIKE vbak-vkorg,
      vtweg LIKE vbak-vtweg,
      kunnr LIKE vbak-kunnr,
      matnr LIKE vbap-matnr,
      kwmeng LIKE vbap-kwmeng,
      vrkme LIKE vbap-vrkme,
      netwr LIKE vbak-netwr,
      waerk LIKE vbak-waerk,
      fksak LIKE vbuk-fksak,
      statu LIKE vbmtv-statu.
DATA: END OF i_output.


*--- Variables
DATA: enddatum LIKE sy-datum.
DATA: g_variant_flag    TYPE c.

*----------------------------------------------------------------------*
* Selection screen                                                     *
*----------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-s01.
SELECT-OPTIONS: s_auart FOR vbak-auart DEFAULT 'ZOR1'.
PARAMETERS:     p_vkorg LIKE vbak-vkorg.
SELECT-OPTIONS: s_vtweg FOR vbak-vtweg.
SELECT-OPTIONS: s_spart FOR vbak-spart.
SELECT-OPTIONS: s_matnr FOR vbap-matnr.
SELECT-OPTIONS: s_kunnr FOR vbak-kunnr.
SELECT-OPTIONS: s_audat FOR vbak-audat.
SELECTION-SCREEN: SKIP.
SELECT-OPTIONS: s_fksak FOR vbuk-fksak.
SELECTION-SCREEN END OF BLOCK b1.
SELECTION-SCREEN: SKIP.
SELECTION-SCREEN BEGIN OF BLOCK b02 WITH FRAME TITLE text-s02.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (25) text-s07.
SELECTION-SCREEN POSITION POS_LOW.
PARAMETERS: p_var   LIKE disvariant-variant.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK b02.

INITIALIZATION.
  REFRESH: s_audat.
  s_audat-low = sy-datum.
  s_audat-low+6(2) = 01.

  CLEAR enddatum.
  CALL FUNCTION 'RP_LAST_DAY_OF_MONTHS'
    EXPORTING
      day_in                  = sy-datum
   IMPORTING
     last_day_of_month       = enddatum
* EXCEPTIONS
*   DAY_IN_NO_DATE          = 1
*   OTHERS                  = 2
            .
  s_audat-high = enddatum.
  APPEND s_audat.


AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_var.
  PERFORM variant_inputhelp USING p_var.

AT SELECTION-SCREEN.

  PERFORM check_authorization.
  PERFORM existence_variant USING p_var.

AT SELECTION-SCREEN OUTPUT.

  PERFORM variant_init.
  IF p_var IS INITIAL AND g_variant_flag IS INITIAL.
    PERFORM get_default_variant USING p_var.
    g_variant_flag = 'X'.
  ENDIF.

************************************************************************
* START MAIN PROGRAM                                                   *
************************************************************************
START-OF-SELECTION.

*  PERFORM initialize_data.
*
  PERFORM select_data.
  PERFORM process_data.
  PERFORM display_data.

*&---------------------------------------------------------------------*
*&      Form  select_data
*&---------------------------------------------------------------------*
FORM select_data .


* Select all relevant Sales Orders
  SELECT vbeln auart audat vkorg vtweg kunnr netwr waerk
  FROM vbak
  INTO CORRESPONDING FIELDS OF TABLE i_vbak
  WHERE audat IN s_audat
  AND vkorg = p_vkorg
  AND vtweg IN s_vtweg
  AND kunnr IN s_kunnr
  AND auart IN s_auart
  AND spart IN s_spart.

  DESCRIBE TABLE i_vbak LINES sy-tfill.
  IF sy-tfill NE 0.
    SORT i_vbak.

    SELECT vbeln posnr matnr kwmeng vrkme
    FROM vbap
    INTO CORRESPONDING FIELDS OF TABLE i_vbap
    FOR ALL ENTRIES IN i_vbak
    WHERE vbeln = i_vbak-vbeln
    AND matnr IN s_matnr.

    DESCRIBE TABLE i_vbap LINES sy-tfill.
    IF sy-tfill NE 0.

      SORT i_vbap.

      SELECT vbeln fksak
      FROM vbuk
      INTO CORRESPONDING FIELDS OF TABLE i_vbuk
      FOR ALL ENTRIES IN i_vbap
      WHERE vbeln = i_vbap-vbeln
      AND fksak IN s_fksak.
      SORT i_vbuk.
    ENDIF.
  ENDIF.

ENDFORM.                    " select_data

*&---------------------------------------------------------------------*
*&      Form  process_data
*&---------------------------------------------------------------------*

FORM process_data .

  LOOP AT i_vbap.
    CLEAR i_output.
    MOVE-CORRESPONDING i_vbap TO i_output.
    CLEAR i_vbak.
    READ TABLE i_vbak WITH KEY vbeln = i_vbap-vbeln
                               BINARY SEARCH.
    IF sy-subrc EQ 0.
      MOVE-CORRESPONDING i_vbak TO i_output.
    ENDIF.
    CLEAR i_vbuk.
    READ TABLE i_vbuk WITH KEY vbeln = i_vbap-vbeln
    BINARY SEARCH.
    IF sy-subrc EQ 0.
      MOVE-CORRESPONDING i_vbuk TO i_output.
* get status text
      tvbst-spras = sy-langu.
      tvbst-tbnam = 'VBUK'.
      tvbst-fdnam = 'FKSAK'.
      tvbst-statu = i_output-fksak.

      CALL FUNCTION 'RV_DOCUMENT_STATUS_TEXTS'
           EXPORTING
                tvbst_wa = tvbst
           IMPORTING
*         RETURNCODE = I01
                text = i_output-statu.




      APPEND i_output.
    ENDIF.
  ENDLOOP.
ENDFORM.                    " process_data

*&---------------------------------------------------------------------*
*&      Form  display_data
*&---------------------------------------------------------------------*

FORM display_data .

  PERFORM fill_field_catalog.
  PERFORM alv_output.

ENDFORM.                    " display_data


*----------------------------------------------------------------------*
*       Form  FILL_FIELD_CATALOG                                       *
*----------------------------------------------------------------------*
*       text                                                           *
*----------------------------------------------------------------------*
FORM fill_field_catalog.

  x_repid = sy-repid.
  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
       EXPORTING
            i_program_name         = x_repid
            i_internal_tabname     = 'I_OUTPUT'
*           i_structure_name       =
*           i_client_never_display = 'X'
            i_inclname             = x_repid
*           i_bypassing_buffer     =
*           i_buffer_active        =
       CHANGING
            ct_fieldcat            = it_fieldcat
       EXCEPTIONS
            inconsistent_interface = 1
            program_error          = 2
            OTHERS                 = 3.
  IF sy-subrc <> 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  gs_layout-colwidth_optimize   = 'X'.
  gs_layout-confirmation_prompt = 'X'.
  gs_layout-key_hotspot         = 'X'.
  gs_layout-detail_titlebar
  = 'SALES REP REPORTING FOR COMMISSIONS'.

ENDFORM.                    "fill_field_catalog


*----------------------------------------------------------------------*
*       Form  ALV_OUTPUT                                               *
*----------------------------------------------------------------------*
*       text                                                           *
*----------------------------------------------------------------------*
FORM alv_output.

*  variant-report    = 'YSE_SD_PREPAYSO'.
*  variant-variant   = variant.
*  gs_sd_alv-variant = gx_variant.

*  LEAVE TO LIST-PROCESSING.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
       EXPORTING
*           i_interface_check           = ' '
*           i_bypassing_buffer          =
*           i_buffer_active             = ' '
            i_callback_program          = 'YSE_SD_PREPAYSO'
*           i_callback_pf_status_set    = ' '
            i_callback_user_command     = g_user_command
*           i_callback_top_of_page      = ' '
*           i_callback_html_top_of_page = ' '
*           i_callback_html_end_of_list = ' '
*           i_structure_name            =
*           i_background_id             = ' '
            i_grid_title                = text-001
*           i_grid_settings             =
            is_layout                   = gs_layout
            it_fieldcat                 = it_fieldcat
*           it_excluding                =
*           it_special_groups           =
*            it_sort                     = it_sort
*           it_filter                   =
*           is_sel_hide                 =
            i_default                   = 'X'
            i_save                      = 'A'
            is_variant                  = gs_sd_alv-variant
*           it_events                   =
*           it_event_exit               =
*           is_print                    =
*           is_reprep_id                =
            i_screen_start_column       = 0
            i_screen_start_line         = 0
            i_screen_end_column         = 0
            i_screen_end_line           = 0
*           it_alv_graphics             =
*           it_add_fieldcat             =
*           it_hyperlink                =
*      importing
*           e_exit_caused_by_caller     =
*           es_exit_caused_by_user      =
       TABLES
            t_outtab                    = i_output
       EXCEPTIONS
            program_error               = 1
            OTHERS                      = 2.
  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_OUTPUT

* ----------------------------------------------------------------------
*   FORM
*   USER COMMAND
*
*   User commands management
*
* ----------------------------------------------------------------------
FORM user_command USING pi_ucomm       LIKE sy-ucomm
                        pi_rs_selfield TYPE slis_selfield.  "#EC CALLED

  CASE pi_ucomm.

    WHEN '&IC1'.
      CASE  pi_rs_selfield-fieldname.
        WHEN 'VBELN'.
          SET PARAMETER ID 'AUN' FIELD pi_rs_selfield-value.
          CALL TRANSACTION 'VA03' AND SKIP FIRST SCREEN.
      ENDCASE.

  ENDCASE.

ENDFORM.                                                   "user_comman
*&---------------------------------------------------------------------*
*&      Form  check_authorization
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM check_authorization .

  authority-check object 'V_VBAK_VKO'
           id 'VKORG' field p_vkorg
           id 'VTWEG' dummy
           id 'SPART' dummy
           id 'ACTVT' dummy.

  IF sy-subrc = 4.
*   No authorisation to display data from Sales Organisation p_vkorg
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '006' WITH p_vkorg.
  ELSEIF sy-subrc <> 0.
*   Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
  ENDIF.

ENDFORM.                    " check_authorization

*Text symbol text£º
*001:Sales order that need Pre-payment
*S01:Sales Order Selections
*S02:Output Selections

*S07:ALV Output Variant
*Selection text£º
*P_VAR:D       Layout
*P_VKORG:D       Sales Organization
*S_AUART:D       Sales document type
*S_AUDAT:D       Document date
*S_FKSAK:D       Bill.stat.order-rel.
*S_KUNNR:D       Sold-to party
*S_MATNR:D       Material
*S_SPART:D       Division
*S_VTWEG:D       Distribution Channel
