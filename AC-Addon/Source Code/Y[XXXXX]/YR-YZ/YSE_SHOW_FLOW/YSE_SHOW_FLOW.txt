*----------------------------------------------------------------------*
* PROGRAM ID    : YSE_SOHIST_UPLOAD                                    *
* PROGRAM TITLE : Show the transaction flow of maintenance order       *
* AUTHOR        : Anda Wu                                              *
* DATE          : 18/02/2013                                           *
* DEVELOPMENT ID:                                                      *
*                                                                      *
* CHANGE REQUEST NUMBER:                                               *
*                                                                      *
* Program Description: Check if Service Order is correctly set closed  *
* Only check the close SEO with account indicator '1E'                 *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
*  DATE    | NAME     |TRANSPORT  | CR# and short Description          *
*----------------------------------------------------------------------*
*18/02/2013|Anda Wu   |CD1K975109 |CR2786: Initial version             *
*----------------------------------------------------------------------*
REPORT  yse_show_flow.
TYPE-POOLS slis.
TABLES:
  aufk.

TYPES:
  BEGIN OF ty_aufk,
    aufnr TYPE aufk-aufnr,
    auart TYPE aufk-auart,
    aenam TYPE aufk-aenam,
    werks TYPE aufk-werks,
    idat3 TYPE aufk-idat3,
    kdauf TYPE aufk-kdauf,
    kdpos TYPE aufk-kdpos,
    phas3 TYPE aufk-phas3,
  END OF ty_aufk,
  BEGIN OF ty_vbak,
    vbeln TYPE vbak-vbeln,
  END OF ty_vbak,
  BEGIN OF ty_vbfa,
    vbelv TYPE vbfa-vbelv,
    vbeln TYPE vbfa-vbeln,
    vbtyp_n TYPE vbfa-vbtyp_n,
  END OF ty_vbfa,
  BEGIN OF ty_vbuk,
    vbeln TYPE vbuk-vbeln,
    gbstk TYPE vbuk-gbstk,
  END OF ty_vbuk,
  BEGIN OF ty_alv_data,
    aufnr TYPE aufk-aufnr,
    werks TYPE aufk-werks,
    idat3 TYPE aufk-idat3,
    aenam TYPE aufk-aenam,
    vbeln TYPE vbak-vbeln,
    vbeln2 TYPE vbuk-vbeln,
    gbstk TYPE vbuk-gbstk,
    comment(100) TYPE c,
    color(4) TYPE c,
  END OF ty_alv_data.

DATA:
  gt_aufk TYPE STANDARD TABLE OF ty_aufk,
  gt_vbak TYPE STANDARD TABLE OF ty_vbak,
  gt_vbfa TYPE STANDARD TABLE OF ty_vbfa,
  gt_vbuk TYPE STANDARD TABLE OF ty_vbuk,
  gt_alvdata TYPE STANDARD TABLE OF ty_alv_data,
  gs_layout TYPE slis_layout_alv,
  gt_field TYPE slis_t_fieldcat_alv.

CONSTANTS:
  gc_warn_color(4) TYPE c VALUE 'C600',
  gc_invoice_type  TYPE c VALUE 'M',
  gc_acc_indicator(2) TYPE c VALUE '1E',
  gc_x   TYPE c VALUE 'X',
  gc_status_c TYPE c VALUE 'C'.

SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.
PARAMETERS:
  p_kokrs  TYPE tka01-kokrs OBLIGATORY DEFAULT '1000'.

SELECT-OPTIONS:
  s_werks FOR aufk-werks,
  s_aufnr FOR aufk-aufnr,
  s_auart FOR aufk-auart,
  s_aenam FOR aufk-aenam DEFAULT 'BATCHUSER',
  s_idat3 FOR aufk-idat3 OBLIGATORY DEFAULT sy-datum.
SELECTION-SCREEN END OF BLOCK b1.

INITIALIZATION.
  PERFORM frm_init.

START-OF-SELECTION.
* Gain all source data from db tables
  PERFORM frm_gain_data.
* Generate ALV data and show
  PERFORM frm_proc_alv.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do Initilization
*----------------------------------------------------------------------*
FORM frm_init .
  CLEAR:
    gs_layout .
  REFRESH:
    gt_aufk ,
    gt_vbak ,
    gt_vbfa ,
    gt_vbuk ,
    gt_alvdata ,
    gt_field .
ENDFORM.                    " FRM_INIT
*&---------------------------------------------------------------------*
*&      Form  FRM_GAIN_DATA
*&---------------------------------------------------------------------*
*       Gain all source data from db tables
*----------------------------------------------------------------------*
FORM frm_gain_data .
* Gain Order master data
  PERFORM frm_gain_mastdata.
* Gain Sales Document: Header Data
  PERFORM frm_gain_headdata.
* Gain Sales Document Flow
  PERFORM frm_gain_docuflow.
* Gain Sales Document: Header Status and Administrative Data
  PERFORM frm_gain_status.
ENDFORM.                    " FRM_GAIN_DATA
*&---------------------------------------------------------------------*
*&      Form  FRM_GAIN_MASTDATA
*&---------------------------------------------------------------------*
*       Gain Order master data
*----------------------------------------------------------------------*
FORM frm_gain_mastdata .
* Gain Order master data
  SELECT aufk~aufnr
         aufk~auart
         aufk~aenam
         aufk~werks
         aufk~idat3
         aufk~kdauf
         aufk~kdpos
         aufk~phas3
    FROM aufk
    INTO TABLE gt_aufk
    WHERE  aufk~aufnr IN s_aufnr
      AND  aufk~auart IN s_auart
      AND  aufk~werks IN s_werks
      AND  aufk~kokrs = p_kokrs
      AND ( aufk~loekz = space OR aufk~loekz IS NULL ).


  DELETE gt_aufk WHERE phas3 IS INITIAL.
  DELETE gt_aufk WHERE NOT idat3 IN s_idat3.
  IF NOT s_aenam IS  INITIAL.
    DELETE gt_aufk WHERE NOT aenam IN s_aenam.
  ENDIF.

ENDFORM.                    " FRM_GAIN_MASTDATA
*&---------------------------------------------------------------------*
*&      Form  FRM_GAIN_HEADDATA
*&---------------------------------------------------------------------*
*       Gain Sales Document: Header Data
*----------------------------------------------------------------------*
FORM frm_gain_headdata .
  IF gt_aufk IS NOT INITIAL.
*   Gain Sales Document: Header Data
    SELECT vbeln
      FROM vbak
      INTO TABLE gt_vbak
      FOR ALL ENTRIES IN gt_aufk
      WHERE vbeln = gt_aufk-kdauf.
  ENDIF.
ENDFORM.                    " FRM_GAIN_HEADDATA
*&---------------------------------------------------------------------*
*&      Form  FRM_GAIN_DOCUFLOW
*&---------------------------------------------------------------------*
*       Gain Sales Document Flow
*----------------------------------------------------------------------*
FORM frm_gain_docuflow .
  IF gt_vbak IS NOT INITIAL.
*   Gain Sales Document Flow
    SELECT  vbelv
            vbeln
            vbtyp_n
      FROM vbfa
      INTO TABLE gt_vbfa
      FOR ALL ENTRIES IN gt_vbak
      WHERE vbelv = gt_vbak-vbeln
        AND vbtyp_n = gc_invoice_type.
  ENDIF.
ENDFORM.                    " FRM_GAIN_DOCUFLOW
*&---------------------------------------------------------------------*
*&      Form  FRM_GAIN_STATUS
*&---------------------------------------------------------------------*
*       Gain Sales Document: Header Status and Administrative Data
*----------------------------------------------------------------------*
FORM frm_gain_status .
  IF gt_vbfa IS NOT INITIAL.
*   Gain Sales Document: Header Status and Administrative Data
    SELECT  vbeln
            gbstk
      FROM vbuk
      INTO TABLE gt_vbuk
      FOR ALL ENTRIES IN gt_vbfa
      WHERE vbeln = gt_vbfa-vbeln.
  ENDIF.
ENDFORM.                    " FRM_GAIN_STATUS
*&---------------------------------------------------------------------*
*&      Form  frm_proc_alv
*&---------------------------------------------------------------------*
*       Generate ALV data and show
*----------------------------------------------------------------------*
FORM frm_proc_alv .
* Generate alv output data
  PERFORM frm_gen_alvdata.
* Edit alv layout
  PERFORM frm_alv_layout.
* Edit alv fieldcat
  PERFORM frm_gen_fieldcat.
* ALV Grid display
  PERFORM frm_show_alv.
ENDFORM.                    " frm_proc_alv
*&---------------------------------------------------------------------*
*&      Form  FRM_GEN_ALVDATA
*&---------------------------------------------------------------------*
*       Generate alv output data
*----------------------------------------------------------------------*
FORM frm_gen_alvdata .
  DATA: ls_aufk TYPE ty_aufk,
        ls_vbak TYPE ty_vbak,
        ls_vbfa TYPE ty_vbfa,
        ls_vbuk TYPE ty_vbuk,
        ls_alvdata TYPE ty_alv_data,
        lv_bemot TYPE vbkd-bemot.

* Generate alv output data
  LOOP AT gt_aufk INTO ls_aufk.
    CLEAR: ls_alvdata,
           ls_vbak ,
           ls_vbfa ,
           ls_vbuk,
           lv_bemot.
*   Only check the SEO with accounting indicator = '1E'
    SELECT SINGLE bemot
      FROM vbkd
      INTO lv_bemot
      WHERE vbeln = ls_aufk-kdauf
        AND posnr = ls_aufk-kdpos.
    IF   lv_bemot <> gc_acc_indicator
      OR sy-subrc <> 0.
      CONTINUE.
    ENDIF.
    ls_alvdata-aufnr = ls_aufk-aufnr.
    ls_alvdata-werks = ls_aufk-werks.
    ls_alvdata-idat3 = ls_aufk-idat3.
    ls_alvdata-aenam = ls_aufk-aenam.

    READ TABLE gt_vbak
      WITH KEY vbeln = ls_aufk-kdauf
      INTO ls_vbak.
    IF sy-subrc <> 0.
      ls_alvdata-comment = 'No sales order exist!'(001).
      ls_alvdata-color = gc_warn_color.
      APPEND ls_alvdata TO gt_alvdata.
      CLEAR  ls_aufk.
      CONTINUE.
    ELSE.
      ls_alvdata-vbeln = ls_vbak-vbeln.
    ENDIF.

    READ TABLE gt_vbfa
      WITH KEY vbelv = ls_vbak-vbeln
      INTO ls_vbfa.
    IF sy-subrc <> 0.
      ls_alvdata-comment = 'No Billing exist!'(002).
      ls_alvdata-color = gc_warn_color.
      APPEND ls_alvdata TO gt_alvdata.
      CLEAR  ls_aufk.
      CONTINUE.
    ENDIF.

    READ TABLE gt_vbuk WITH KEY vbeln = ls_vbfa-vbeln
      INTO ls_vbuk.
    IF sy-subrc <> 0 .
      ls_alvdata-comment = 'No Billing exist!'(002).
      ls_alvdata-color = gc_warn_color.
      APPEND ls_alvdata TO gt_alvdata.
      CLEAR  ls_aufk.
      CONTINUE.
    ELSE.
      ls_alvdata-vbeln2 = ls_vbuk-vbeln.
      ls_alvdata-gbstk = ls_vbuk-gbstk .
      IF ls_vbuk-gbstk <> gc_status_c.
        ls_alvdata-comment = 'Billing is not completed'(003).
        ls_alvdata-color = gc_warn_color.
        APPEND ls_alvdata TO gt_alvdata.
        CLEAR  ls_aufk.
        CONTINUE.
      ENDIF.
    ENDIF.

    ls_alvdata-comment = 'Normal'(004).
    APPEND ls_alvdata TO gt_alvdata.
    CLEAR  ls_aufk.
  ENDLOOP.
ENDFORM.                    " FRM_GEN_ALVDATA
*&---------------------------------------------------------------------*
*&      Form  FRM_ALV_LAYOUT
*&---------------------------------------------------------------------*
*       Edit alv layout
*----------------------------------------------------------------------*
FORM frm_alv_layout .
* Edit alv layout
  gs_layout-colwidth_optimize  = gc_x .
  gs_layout-window_titlebar = 'Show Flow'(005).
  gs_layout-info_fieldname = 'COLOR'.
ENDFORM.                    " FRM_ALV_LAYOUT
*&---------------------------------------------------------------------*
*&      Form  FRM_GEN_FIELDCAT
*&---------------------------------------------------------------------*
*       Edit alv fieldcat
*----------------------------------------------------------------------*
FORM frm_gen_fieldcat .
  DATA: ls_field TYPE slis_fieldcat_alv,
        lv_index TYPE sy-tabix.

* Edit alv fieldcat
  CLEAR ls_field.
  lv_index = lv_index + 1.
  ls_field-col_pos = lv_index.
  ls_field-fieldname = 'AUFNR'.
  ls_field-key = 'X'.
  ls_field-seltext_s = 'Service Order'(006).
  ls_field-seltext_l = 'Service Order'(006).
  APPEND ls_field TO gt_field.

  CLEAR ls_field.
  lv_index = lv_index + 1.
  ls_field-col_pos = lv_index.
  ls_field-fieldname = 'WERKS'.
  ls_field-key = 'X'.
  ls_field-seltext_s = 'Plant'(011).
  ls_field-seltext_l = 'Plant'(011).
  APPEND ls_field TO gt_field.

  CLEAR ls_field.
  lv_index = lv_index + 1.
  ls_field-col_pos = lv_index.
  ls_field-fieldname = 'IDAT3'.
  ls_field-key = 'X'.
  ls_field-seltext_s = 'Close Date'(012).
  ls_field-seltext_l = 'Close Date'(012).
  APPEND ls_field TO gt_field.

  CLEAR ls_field.
  lv_index = lv_index + 1.
  ls_field-col_pos = lv_index.
  ls_field-fieldname = 'AENAM'.
  ls_field-key = 'X'.
  ls_field-seltext_s = 'Last Changed By'(013).
  ls_field-seltext_l = 'Last Changed By'(013).
  APPEND ls_field TO gt_field.

  CLEAR ls_field.
  lv_index = lv_index + 1.
  ls_field-col_pos = lv_index.
  ls_field-fieldname = 'VBELN'.
  ls_field-seltext_s = 'Sales Order'(007).
  ls_field-seltext_l = 'Sales Order'(007).
  APPEND ls_field TO gt_field.

  CLEAR ls_field.
  lv_index = lv_index + 1.
  ls_field-col_pos = lv_index.
  ls_field-fieldname = 'VBELN2'.
  ls_field-seltext_s = 'Invoice'(008).
  ls_field-seltext_l = 'Invoice'(008).
  APPEND ls_field TO gt_field.

  CLEAR ls_field.
  lv_index = lv_index + 1.
  ls_field-col_pos = lv_index.
  ls_field-fieldname = 'GBSTK'.
  ls_field-seltext_s = 'Invoice Status'(009).
  ls_field-seltext_l = 'Invoice Status'(009).
  APPEND ls_field TO gt_field.

  CLEAR ls_field.
  lv_index = lv_index + 1.
  ls_field-col_pos = lv_index.
  ls_field-fieldname = 'COMMENT'.
  ls_field-key = 'X'.
  ls_field-seltext_s = 'Process Comment'(010).
  ls_field-seltext_l = 'Process Comment'(010).
  APPEND ls_field TO gt_field.
ENDFORM.                    " FRM_GEN_FIELDCAT
*&---------------------------------------------------------------------*
*&      Form  FRM_SHOW_ALVS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM frm_show_alv .
* Show alv
  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program = sy-cprog
      is_layout          = gs_layout
      it_fieldcat        = gt_field
    TABLES
      t_outtab           = gt_alvdata
    EXCEPTIONS
      program_error      = 1
      OTHERS             = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
ENDFORM.                    " FRM_SHOW_ALVS

*Text symbol text£º
*001:No sales order exist!
*002:No Billing exist!
*003:Billing is not completed
*004:Normal
*005:Show Flow
*006:Service Order
*007:Sales Order
*008:Invoice
*009:Invoice Status
*010:Process Comment
*011:Plant
*012:Close Date
*013:Last Changed By

*T01:Select Criteria
*Selection text£º
*P_KOKRS:D       .
*S_AENAM:D       .
*S_AUART:D       .
*S_AUFNR:D       .
*S_IDAT3:        Close Date
*S_WERKS:D       .
