*&---------------------------------------------------------------------*
*& Report  YSE_SELECT_AC_CONNECT_STCK
*&
*&---------------------------------------------------------------------*
*&
*& AC Connect : Select Materials for Stock Availability Idocs
*&
*&              (Call program YSE_SEND_IDOC_AC_CONNECT_STCK
*&               for the selected materials)
*&
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 15.07.2009
*  Change Request Number : CR0101
*  Transport request Nr. : CD1K949352
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME             | CORRECT. NR. | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 07.06.2011 | Jules Smets      | CD1K965492   | CR1789      *
*         | Add selection of open sales orders                         *
*         |                  open stock transport requisitions         *
*         |                  open transport orders                     *
*----------------------------------------------------------------------*
* MOD-002 |            |                  | CD1K9        | CR          *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_select_ac_connect_stck.

TABLES : yse_sd_acc_stock,
         yse_sd_acc_fam,
         mkpf,
         mseg,
         ekko,
         ekes,
         ekpo.

DATA: it_werks TYPE TABLE OF yse_sd_acc_stock
                    WITH HEADER LINE.

DATA: BEGIN OF it_vkorg  OCCURS 0,
        vkorg   TYPE vkorg,
      END OF it_vkorg.

DATA: BEGIN OF it_vtweg  OCCURS 0,
        vtweg   TYPE vtweg,
      END OF it_vtweg.

DATA: BEGIN OF it_matnr  OCCURS 0,
        matnr   TYPE matnr,
      END OF it_matnr.

DATA: BEGIN OF it_mkpf  OCCURS 0,
        mblnr   TYPE mblnr,
      END OF it_mkpf.

DATA: BEGIN OF it_ekko  OCCURS 0,
        ebeln   TYPE ebeln,
      END OF it_ekko.

DATA: BEGIN OF it_ekes  OCCURS 0,
        ebeln   TYPE ebeln,
        ebelp   TYPE ebelp,
      END OF it_ekes.

*DATA: BEGIN OF it_mvke  OCCURS 0,                          "MOD-001
TYPES: BEGIN OF t_mvke,                                     "MOD-001
         matnr    TYPE matnr,
         prat2    TYPE prat2,
*       END OF it_mvke.                                     "MOD-001
       END OF t_mvke.                                       "MOD-001
*** MOD-001 * begin ***
DATA: it_mvkei TYPE TABLE OF t_mvke.
DATA: it_mvke TYPE HASHED TABLE OF t_mvke
                   WITH UNIQUE KEY matnr
                   WITH HEADER LINE.
*** MOD-001 * end ***

DATA: gv_werks     TYPE werks_d,
      gv_alloc_per TYPE y_alloc_per,                        "MOD-001
      gv_allocdate TYPE dats.                               "MOD-001

CONSTANTS: c_mestyp_stck   LIKE tbdme-mestyp VALUE 'YSE_ACC_STOCK',
*** MOD-001 * begin ***
           c_znb1(4)       TYPE c     VALUE 'ZNB1',
           c_zub1(4)       TYPE c     VALUE 'ZUB1',
           c_lf(4)         TYPE c     VALUE 'LF  ',
           c_zlf(4)        TYPE c     VALUE 'ZLF ',
           c_zl03(4)       TYPE c     VALUE 'ZL03',
           c_zlr1(4)       TYPE c     VALUE 'ZLR1',
           c_zkt(4)        TYPE c     VALUE 'ZKT ',
           c_7(1)          TYPE c     VALUE '7',
           c_j(1)          TYPE c     VALUE 'J',
           c_c(1)          TYPE c     VALUE 'C',
           c_n(1)          TYPE c     VALUE 'N',
           c_1000(4)       TYPE c     VALUE '1000'.
*** MOD-001 * end ***

RANGES: r_werks   FOR mseg-werks,
        r_werksm  FOR ekpo-werks,
        r_ekorg   FOR ekko-ekorg,
        r_vtweg   FOR vbak-vtweg,
        r_matnr   FOR mara-matnr.

* Selection
SELECTION-SCREEN: BEGIN OF BLOCK block1 WITH FRAME TITLE text-001.
PARAMETERS:     p_date   TYPE sydatum  DEFAULT sy-datum  OBLIGATORY.
*** MOD-001 * begin ***
*SELECT-OPTIONS: s_famb   FOR yse_sd_acc_stock-famback.
PARAMETERS:     p_famb TYPE zfamback  OBLIGATORY.
*** MOD-001 * end ***
SELECT-OPTIONS: s_lgort  FOR mseg-lgort  DEFAULT '1000',
                s_bsart  FOR ekko-bsart  DEFAULT 'ZNB1'  OBLIGATORY,
                s_aedat  FOR ekko-aedat,
                s_ebtyp  FOR ekes-ebtyp  DEFAULT 'AB'  OBLIGATORY.
SELECTION-SCREEN SKIP.
PARAMETERS:     p_prat2  TYPE prat2  AS CHECKBOX.
SELECTION-SCREEN: END OF BLOCK block1.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

  CLEAR: r_matnr[],
         r_werks[],
         r_werksm[],
         r_ekorg[].

* Build ranges for selection
  PERFORM build_ranges.

*** MOD-001 * begin ***
* Allocated period
  SELECT SINGLE y_alloc_per INTO gv_alloc_per
         FROM yse_sd_acc_stock
         WHERE famback = p_famb.
  gv_allocdate = p_date + gv_alloc_per.

  CLEAR it_matnr[].

* Get open sales orders
  PERFORM get_open_sales_orders.

* Get open transport requisitions
  PERFORM get_open_transport_req.

* Get open transport orders
  PERFORM get_open_transport_orders.
*** MOD-001 * end ***

* Get materials where a goods movement took place
  PERFORM get_materials_gm.

* Get materials with an update in the PO confirmation
  PERFORM get_materials_poc.

  IF NOT it_matnr[] IS INITIAL.
*   Build range with materials
    PERFORM build_range_mat.
*   Send Idocs
    PERFORM send_idocs.
  ENDIF.


************************************************************************
*       E N D - O F - S E L E C T I O N    E V E N T                   *
************************************************************************
END-OF-SELECTION .


************************************************************************
*--- S U B R O U T I N E S --------------------------------------------*
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  BUILD_RANGES
*&---------------------------------------------------------------------*
*       Build ranges for selection
*----------------------------------------------------------------------*
FORM build_ranges .

* Ranges for plants (all + main)
  SELECT * INTO TABLE it_werks
           FROM yse_sd_acc_stock
*           WHERE famback IN s_famb.                        "MOD-001
           WHERE famback = p_famb.                          "MOD-001

  IF sy-subrc NE 0.
    WRITE: text-e01.
  ELSE.
    LOOP AT it_werks.
*     Main plant
      r_werksm-sign = 'I'.
      r_werksm-option = 'EQ'.
      r_werksm-low = it_werks-werks01.
      APPEND r_werksm.
*     All plants
      r_werks-sign = 'I'.
      r_werks-option = 'EQ'.
      DO 15 TIMES VARYING gv_werks FROM it_werks-werks01
                                   NEXT it_werks-werks02.
        IF gv_werks IS INITIAL.
          EXIT.
        ENDIF.
        r_werks-low = gv_werks.
        APPEND r_werks.
      ENDDO.
    ENDLOOP.
*   Delete duplicates
    SORT r_werks.
    DELETE ADJACENT DUPLICATES FROM r_werks.
    SORT r_werksm.
    DELETE ADJACENT DUPLICATES FROM r_werksm.
  ENDIF.

* Range for Sales organization
  SELECT vkorg INTO TABLE it_vkorg
               FROM yse_sd_acc_fam
*               WHERE famback IN s_famb.                    "MOD-001
               WHERE famback = p_famb.                      "MOD-001

  IF sy-subrc NE 0.
    WRITE: text-e02.
  ELSE.
    LOOP AT it_vkorg.
      r_ekorg-sign = 'I'.
      r_ekorg-option = 'EQ'.
      r_ekorg-low = it_vkorg-vkorg.
      APPEND r_ekorg.
    ENDLOOP.
*   Delete duplicates
    SORT r_ekorg.
    DELETE ADJACENT DUPLICATES FROM r_ekorg.
  ENDIF.

ENDFORM.                    " BUILD_RANGES

*&---------------------------------------------------------------------*
*&      Form  GET_OPEN_SALES_ORDERS                         "MOD-001
*&---------------------------------------------------------------------*
*       Get open sales orders
*----------------------------------------------------------------------*
FORM get_open_sales_orders .

  SELECT matnr APPENDING TABLE it_matnr
         FROM vbbe
         WHERE werks IN r_werks
           AND ( lgort IN s_lgort  OR
                 lgort = ' ' )
           AND ( vbtyp = c_c  OR
               ( vbtyp = c_j
                 AND ( auart = c_lf    OR
                       auart = c_zlf   OR
                       auart = c_zl03  OR
                       auart = c_zlr1  OR
                       auart = c_zkt ) ) )
           AND mbdat LE gv_allocdate
           AND vmeng NE 0.

* Delete duplicates
  SORT it_matnr.
  DELETE ADJACENT DUPLICATES FROM it_matnr.

ENDFORM.                    " GET_OPEN_SALES_ORDERS

*&---------------------------------------------------------------------*
*&      Form  GET_OPEN_TRANSPORT_REQ
*&---------------------------------------------------------------------*
*       Get open transport requisitions                     "MOD-001
*----------------------------------------------------------------------*
FORM get_open_transport_req .

* Only consider open transport req. if storage location = 1000
  IF c_1000 IN s_lgort.
    SELECT matnr APPENDING TABLE it_matnr
             FROM eban
             WHERE reswk IN r_werks
               AND ( lgort = c_1000  OR
                     lgort = ' ' )
               AND frgdt LE gv_allocdate
               AND bsart = c_znb1
               AND pstyp = c_7            "Stock transfer
               AND statu = c_n
               AND ebakz = space
               AND loekz = space.

*   Delete duplicates
    SORT it_matnr.
    DELETE ADJACENT DUPLICATES FROM it_matnr.
  ENDIF.

ENDFORM.                    " GET_OPEN_TRANSPORT_REQ

*&---------------------------------------------------------------------*
*&      Form  GET_OPEN_TRANSPORT_ORDERS
*&---------------------------------------------------------------------*
*       Get open transport orders                           "MOD-001
*----------------------------------------------------------------------*
form GET_OPEN_TRANSPORT_ORDERS .

* Only consider open transport orders if storage location = 1000
  IF c_1000 IN s_lgort.
*   Select from ekpo/ekko
    SELECT b~matnr APPENDING TABLE it_matnr
           FROM ekko AS a
           INNER JOIN ekpo AS b
                   ON a~ebeln = b~ebeln
           WHERE a~reswk in r_werks
             AND ( b~lgort = c_1000  OR
                   b~lgort = ' ' )
             AND b~zzeeind LE gv_allocdate
             AND a~bsart = c_zub1
             AND b~loekz = space.

*   Delete duplicates
    SORT it_matnr.
    DELETE ADJACENT DUPLICATES FROM it_matnr.
  ENDIF.

endform.                    " GET_OPEN_TRANSPORT_ORDERS

*&---------------------------------------------------------------------*
*&      Form  GET_MATERIALS_GM
*&---------------------------------------------------------------------*
*       Get materials where a goods movement took place
*----------------------------------------------------------------------*
FORM get_materials_gm .

* Material documents (header)
  SELECT mblnr INTO TABLE it_mkpf
               FROM mkpf
               WHERE budat = p_date.

  IF NOT it_mkpf[] IS INITIAL.
*   Material documents (segments)
*    SELECT matnr INTO TABLE it_matnr                       "MOD-001
    SELECT matnr APPENDING TABLE it_matnr                   "MOD-001
                 FROM mseg
                 FOR ALL ENTRIES IN it_mkpf
                 WHERE mblnr = it_mkpf-mblnr
                   AND werks IN r_werks
                   AND lgort IN s_lgort.

*   Delete duplicates
    SORT it_matnr.
    DELETE ADJACENT DUPLICATES FROM it_matnr.
  ENDIF.

ENDFORM.                    " GET_MATERIALS_GM

*&---------------------------------------------------------------------*
*&      Form  GET_MATERIALS_POC
*&---------------------------------------------------------------------*
*       Get materials with an update in the PO confirmation
*----------------------------------------------------------------------*
FORM get_materials_poc .

* Open replenishment orders
  SELECT ebeln INTO TABLE it_ekko
               FROM ekko
               WHERE bsart IN s_bsart
                 AND aedat IN s_aedat
                 AND ekorg IN r_ekorg.

  IF NOT it_ekko[] IS INITIAL.
*   (Vendor) Confirmations
    SELECT ebeln ebelp INTO TABLE it_ekes
                       FROM ekes
                       FOR ALL ENTRIES IN it_ekko
                       WHERE ebeln =  it_ekko-ebeln
                         AND ebtyp IN s_ebtyp
                         AND eindt GE p_date.

    IF NOT it_ekes[] IS INITIAL.
*     PO Items
      SELECT matnr APPENDING TABLE it_matnr
                   FROM ekpo
                   FOR ALL ENTRIES IN it_ekes
                   WHERE ebeln = it_ekes-ebeln
                     AND ebelp = it_ekes-ebelp
                     AND werks IN r_werksm
                     AND lgort IN s_lgort.

*     Delete duplicates
      SORT it_matnr.
      DELETE ADJACENT DUPLICATES FROM it_matnr.
    ENDIF.
  ENDIF.

ENDFORM.                    " GET_MATERIALS_POC

*&---------------------------------------------------------------------*
*&      Form  BUILD_RANGE_MAT
*&---------------------------------------------------------------------*
*       Build range with materials
*----------------------------------------------------------------------*
FORM build_range_mat .

* Select materials depending on attribute 2 (when needed)
  IF NOT p_prat2 IS INITIAL.
    PERFORM material_selection.
  ENDIF.

* Build range
  LOOP AT it_matnr.
    r_matnr-sign = 'I'.
    r_matnr-option = 'EQ'.
    r_matnr-low = it_matnr-matnr.
    APPEND r_matnr.
  ENDLOOP.

ENDFORM.                    " BUILD_RANGE_MAT

*&---------------------------------------------------------------------*
*&      Form  MATERIAL_SELECTION
*&---------------------------------------------------------------------*
*       Select materials depending on attribute 2
*----------------------------------------------------------------------*
FORM material_selection .

* Range for Distribution channel
  SELECT vtweg INTO TABLE it_vtweg
               FROM yse_sd_acc_fam
*               WHERE famback IN s_famb.                    "MOD-001
               WHERE famback = p_famb.                      "MOD-001

  IF sy-subrc = 0.
    LOOP AT it_vtweg.
      r_vtweg-sign = 'I'.
      r_vtweg-option = 'EQ'.
      r_vtweg-low = it_vtweg-vtweg.
      APPEND r_vtweg.
    ENDLOOP.
*   Delete duplicates
    SORT r_vtweg.
    DELETE ADJACENT DUPLICATES FROM r_vtweg.
  ENDIF.

* Select material attribute 2 (Pricing)
  SELECT matnr prat2
*         INTO CORRESPONDING FIELDS OF TABLE it_mvke        "MOD-001
         INTO CORRESPONDING FIELDS OF TABLE it_mvkei        "MOD-001
         FROM mvke
         FOR ALL ENTRIES IN it_matnr
         WHERE matnr = it_matnr-matnr
           AND vkorg IN r_ekorg
           AND vtweg IN r_vtweg.
*** MOD-001 * begin ***
*  SORT it_mvke.
*  DELETE ADJACENT DUPLICATES FROM it_mvke.
  SORT it_mvkei BY matnr.
  DELETE ADJACENT DUPLICATES FROM it_mvkei
         COMPARING matnr.
  it_mvke[] = it_mvkei[].
  FREE it_mvkei[].
*** MOD-001 * end ***

* Delete materials (according to attribute 2)
  LOOP AT it_matnr.
    CLEAR it_mvke.
    READ TABLE it_mvke
*               WITH KEY matnr = it_matnr-matnr             "MOD-001
               WITH TABLE KEY matnr = it_matnr-matnr.       "MOD-001
*               BINARY SEARCH.                              "MOD-001
    IF it_mvke-prat2 IS INITIAL.
      DELETE it_matnr.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " MATERIAL_SELECTION

*&---------------------------------------------------------------------*
*&      Form  SEND_IDOCS
*&---------------------------------------------------------------------*
*       Send Idocs
*----------------------------------------------------------------------*
FORM send_idocs .

* Call program to send Idocs
  SUBMIT yse_send_idoc_ac_connect_stck
          WITH p_mess   = c_mestyp_stck
          WITH p_famb   = p_famb
          WITH s_lgort IN s_lgort
          WITH s_matnr IN r_matnr
          WITH s_bsart IN s_bsart
          WITH p_prat2  = p_prat2.
*          with s_mtart ...

ENDFORM.                    " SEND_IDOCS

*Text symbol text£º
*001:Selection parameters
*E01:ERROR : No plants for selected Back-end FAM's

*E02:ERROR : No sales organizations for selected Back-end FAM's
*Selection text£º
*P_DATE:        Selection date
*P_FAMB:        Back-end FAM
*P_PRAT2:        Product attribute 2
*S_AEDAT:        PO created on
*S_BSART:D       .
*S_EBTYP:D       .
*S_FAMB:        Back-end FAM
*S_LGORT:D       .
