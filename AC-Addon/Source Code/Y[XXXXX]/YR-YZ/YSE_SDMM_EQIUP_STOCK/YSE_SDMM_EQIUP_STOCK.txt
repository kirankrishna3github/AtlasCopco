*&---------------------------------------------------------------------*
*----------------------------------------------------------------------*
* Author  : Anda Wu                                                    *
* Created : 11.07.2016                                                 *
* Dev.nbr: CR3913:Equipment Stock Report                               *
*----------------------------------------------------------------------*
* User administration full access report                               *
************************************************************************
* Mod-xxx | xxxx.xx.xx | xxxx xxxx     | CD1Kxxxxxxxxxx  |   CRXXXX    *
************************************************************************
* Mod-xxx | 2016.07.11 | Anda Wu       | CD1K989228      |   CR3913    *
*         Equipment Stock Report
************************************************************************
REPORT yse_sdmm_eqiup_stock NO STANDARD PAGE HEADING LINE-SIZE 255.

************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES: mard,mseg,equi.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:

BEGIN OF ty_alv_output,
  gac         TYPE  char4,              "GAC
  pgc         TYPE  char4,              "PGC
  plc         TYPE  rkeg_ww002,         "PLC
  matnr       TYPE  char18,             "Material
  maktx_e     TYPE  makt-maktx,         "Material Descript(English)
  maktx_z     TYPE  makt-maktx,         "Material Descript(Chinese)
  werks       TYPE  eqbs-b_werk,        "Plant
  lgort       TYPE  mard-lgort,         "Storage Location
  prctr       TYPE  marc-prctr,         "Profit Center
  lbkum       TYPE  mbew-lbkum,         "Total Stock
  stprs       TYPE  mbew-stprs,         "Unit Standard Cost
  ttval       TYPE  mbew-vmsav,         "Total Value
  sobkz       TYPE  eqbs-sobkz,         "Spec Stock
  sernr       TYPE  equi-sernr,         "Serial number
  equnr       TYPE  equi-equnr,         "Equipment number
  eqktx       TYPE  eqkt-eqktx,         "Equipment Description
  sttxt       TYPE  itobattr-sttxt,     "System Status
  gidat       TYPE  erdat,              "First GR Date
  absmg       TYPE  ce11000-absmg,      "12 Month Delivered QTY
  absmg_me    TYPE  ce11000-absmg_me,   "Referrence Uom

END OF ty_alv_output,

BEGIN OF ty_marc,
  matnr       TYPE marc-matnr,          "Material
  werks       TYPE marc-werks,
  lvorm       TYPE marc-lvorm,
  prctr       TYPE marc-prctr,
END OF ty_marc,

BEGIN OF ty_mvke,
  matnr       TYPE mvke-matnr,
  vkorg       TYPE mvke-vkorg,
  vtweg       TYPE mvke-vtweg,
  prodh       TYPE mvke-prodh,
  gac         TYPE char4,              "GAC
  pgc         TYPE char4,              "pgc
  plc         TYPE rkeg_ww002,         "PLC
END OF ty_mvke,

BEGIN OF ty_eqbs,
  equnr       TYPE eqbs-equnr,
  b_werk      TYPE eqbs-b_werk,
  b_lager     TYPE eqbs-b_lager,
  sobkz       TYPE eqbs-sobkz,
END OF ty_eqbs,

BEGIN OF ty_itob,
  equnr       TYPE itob-equnr,
  vkorg       TYPE itob-vkorg,
  vtweg       TYPE itob-vtweg,
  spart       TYPE itob-spart,
END OF ty_itob,

BEGIN OF ty_equipment,
  equnr       TYPE equi-equnr,
END OF ty_equipment,

BEGIN OF ty_mbew,
  matnr       TYPE mbew-matnr,
  bwkey       TYPE mbew-bwkey,
  bwtar       TYPE mbew-bwtar,
  stprs       TYPE mbew-stprs,
  peinh       TYPE mbew-peinh,
END OF ty_mbew,

BEGIN OF ty_ce11000,
  paledger    TYPE ce11000-paledger,
  vrgar       TYPE ce11000-vrgar,
  versi       TYPE ce11000-versi,
  perio       TYPE ce11000-perio,
  paobjnr     TYPE ce11000-paobjnr,
  pasubnr     TYPE ce11000-pasubnr,
  belnr       TYPE ce11000-belnr,
  posnr       TYPE ce11000-posnr,
  fadat       TYPE ce11000-fadat,
  artnr       TYPE ce11000-artnr,
  vkorg       TYPE ce11000-vkorg,
  ww005       TYPE ce11000-ww005,
  absmg_me    TYPE ce11000-absmg_me,
  absmg       TYPE ce11000-absmg,
END OF ty_ce11000,

BEGIN OF ty_equi,
  equnr       TYPE equi-equnr,
  erdat       TYPE equi-erdat,
  matnr       TYPE equi-matnr,
  sernr       TYPE equi-sernr,
  iloan       TYPE equz-iloan,
  iwerk       TYPE equz-iwerk,
  vkorg       TYPE iloa-vkorg,
  vtweg       TYPE iloa-vtweg,
  spart       TYPE iloa-spart,
END OF ty_equi,

BEGIN OF ty_ser03,
  equnr        TYPE equi-equnr,
  obknr       TYPE objk-obknr,
  sernr       TYPE objk-sernr,
  bwart       TYPE ser03-bwart,
  datum       TYPE ser03-datum,
  werk        TYPE ser03-werk,
  lagerort    TYPE ser03-lagerort,
END OF ty_ser03.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_marc       TYPE STANDARD TABLE OF ty_marc,
  gt_ser03      TYPE STANDARD TABLE OF ty_ser03,
  gt_mvke       TYPE STANDARD TABLE OF ty_mvke,
  gt_equi       TYPE STANDARD TABLE OF ty_equi,
  gt_tvkwz      TYPE STANDARD TABLE OF tvkwz,
  gt_makt_en    TYPE STANDARD TABLE OF makt,
  gt_makt_zh    TYPE STANDARD TABLE OF makt,
  gt_eqkt       TYPE STANDARD TABLE OF eqkt,
  gt_mseg       TYPE STANDARD TABLE OF mseg,
  gt_ce11000    TYPE STANDARD TABLE OF ty_ce11000,
  gt_mbew       TYPE STANDARD TABLE OF ty_mbew,
  gt_eqbs       TYPE STANDARD TABLE OF ty_eqbs,
  gt_equipment  TYPE STANDARD TABLE OF ty_equipment,
  gt_alvdata    TYPE STANDARD TABLE OF ty_alv_output,
  gt_fieldcat   TYPE slis_t_fieldcat_alv.


************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_disvar   TYPE disvariant,
  gs_layout   TYPE slis_layout_alv.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_gac(4)     TYPE c,
  gv_pgc(4)     TYPE c,
  gv_plc        TYPE rkeg_ww002,
  gv_col(10)    TYPE n,
  gv_text       TYPE string,
  gv_save       TYPE c,
  gv_callback   TYPE slis_formname.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x                TYPE c VALUE 'X',
  gc_type_e           TYPE c VALUE 'E',
  gc_callback_routine TYPE slis_formname VALUE 'USER_COMMAND'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-t02 .
SELECT-OPTIONS:
  s_werks     FOR   mard-werks OBLIGATORY,  "Plant
  s_lgort     FOR   mard-lgort,             "Storage Location
  s_matnr     FOR   mard-matnr,             "Material
  s_pgc       FOR   gv_pgc,                 "PGC
  s_gac       FOR   gv_gac,                 "GAC
  s_plc       FOR   gv_plc,                 "PLC
  s_bwart     FOR   mseg-bwart NO-DISPLAY.  "Movement types
SELECTION-SCREEN END OF BLOCK b2.

SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.
*PARAMETERS: p_vkorg   TYPE iloa-vkorg OBLIGATORY,   "Sales Org
*            p_vtweg   TYPE iloa-vtweg OBLIGATORY,   "Dist. Channel
*            p_spart   TYPE iloa-spart OBLIGATORY.   "Division

SELECT-OPTIONS:
            s_sernr   FOR  equi-sernr,
            s_equnr   FOR  equi-equnr.
SELECTION-SCREEN END OF BLOCK b1.

* Alv variants
SELECTION-SCREEN BEGIN OF BLOCK b3 WITH FRAME TITLE text-t03.

PARAMETERS: p_vari TYPE disvariant-variant.       "Dispaly Variant

SELECTION-SCREEN END OF BLOCK b3.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initilization
  PERFORM frm_init.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN                                                 *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.
* selection screen check
  PERFORM check_parameter.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN  OUTPUT                                         *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.
* selection screen check
  PERFORM check_parameter_set.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN ON VALUE-REQUEST                                *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
  PERFORM f4_for_variant.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* progress indicator
  PERFORM process_info.
* get data
  PERFORM get_alv_data.
* alv property set
  PERFORM alv_prop_set.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
* ALV display
  PERFORM alv_display.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do initilization
*----------------------------------------------------------------------*
FORM frm_init .

*  DATA: lr_bwart LIKE LINE OF s_bwart.
* initialize the internal table,workareas and variables
  REFRESH:
    gt_alvdata,
    gt_fieldcat.

  CLEAR:
    gs_disvar,
    gs_layout,
    gv_col,
    gv_text,
    gv_save,
    gv_callback.

  gv_save = 'A'.
* Set default alv layout
  gs_disvar-report = sy-repid.
  CALL FUNCTION 'REUSE_ALV_VARIANT_DEFAULT_GET'
    EXPORTING
      i_save        = gv_save
    CHANGING
      cs_variant    = gs_disvar
    EXCEPTIONS
      wrong_input   = 1
      not_found     = 2
      program_error = 3
      OTHERS        = 4.
  IF sy-subrc = 0.
    p_vari = gs_disvar-variant.
  ENDIF.

  s_bwart-sign   = 'I'.
  s_bwart-option = 'EQ'.
  s_bwart-low    = '101'.
  APPEND s_bwart.

  s_bwart-sign   = 'I'.
  s_bwart-option = 'EQ'.
  s_bwart-low    = '651'.
  APPEND s_bwart.

  s_bwart-sign   = 'I'.
  s_bwart-option = 'EQ'.
  s_bwart-low    = '653'.
  APPEND s_bwart.

  s_bwart-sign   = 'I'.
  s_bwart-option = 'EQ'.
  s_bwart-low    = '634'.
  APPEND s_bwart.

ENDFORM.                    " FRM_INIT

*&---------------------------------------------------------------------*
*&      Form  CHECK_PARAMETER
*&---------------------------------------------------------------------*
*       selection screen check
*----------------------------------------------------------------------*
FORM check_parameter .

  DATA: ls_disvar TYPE disvariant.

  IF p_vari IS NOT INITIAL.
    ls_disvar = gs_disvar.
    ls_disvar-variant = p_vari.
    CALL FUNCTION 'REUSE_ALV_VARIANT_EXISTENCE'
      EXPORTING
        i_save        = gv_save
      CHANGING
        cs_variant    = ls_disvar
      EXCEPTIONS
        wrong_input   = 1
        not_found     = 2
        program_error = 3
        OTHERS        = 4.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ELSE.
      gs_disvar = ls_disvar.
    ENDIF.
  ENDIF.

ENDFORM.                    " CHECK_PARAMETER
*&---------------------------------------------------------------------*
*&      Form  PROCESS_INFO
*&---------------------------------------------------------------------*
*       progress indicator
*----------------------------------------------------------------------*
FORM process_info .

  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    CONCATENATE 'Start report:'(t52) sy-repid
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t52.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 1
        text       = gv_text.
  ENDIF.

ENDFORM.                    " PROCESS_INFO
*&---------------------------------------------------------------------*
*&      Form  GET_ALV_DATA
*&---------------------------------------------------------------------*
*       get data
*----------------------------------------------------------------------*
FORM get_alv_data .
* Gain data fro TVKWZ
  PERFORM get_data_tvkwz.
* Gain data from eqbs
  PERFORM get_data_eqbs.
* Gain equipment data from eqbs and itob
  PERFORM get_data_equipment.
* Gain data from table MVKE - Sales Data for Material
  PERFORM get_data_mvke.
* Gain data from table MAKT  - Material Descriptions
  PERFORM get_data_makt.
* Gain Material Valuation data from MBEW
  PERFORM get_data_mbew.
* Gain material of plant view
  PERFORM get_data_marc.
* Gain operatin data from table CE11000
  PERFORM get_data_ce11000.
* Gain material document
  PERFORM get_data_mseg.
* Generate ALV data for output
  PERFORM generate_alv_data.
* Edit all available
  PERFORM edit_alv_data.
ENDFORM.                    " GET_ALV_DATA
*&---------------------------------------------------------------------*
*&      Form  ALV_PROP_SET
*&---------------------------------------------------------------------*
*       alv property set
*----------------------------------------------------------------------*
FORM alv_prop_set .

* Display ALV process indicator
  PERFORM alv_progress_indicator.

* FIELDCAT set
  PERFORM alv_fieldcat_set.

* LAYOUT set
  PERFORM alv_layout_set.

* Set Others
  PERFORM alv_others_set.

ENDFORM.                    " ALV_PROP_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_lines(10)  TYPE c.

  IF gt_alvdata IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-t61.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  SORT gt_alvdata.
  DELETE ADJACENT DUPLICATES FROM gt_alvdata.
  SORT gt_alvdata BY  gac ASCENDING
                      pgc ASCENDING
                      matnr ASCENDING.

  IF sy-batch IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-t63.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 80
        text       = gv_text.
  ELSE.
    CLEAR: gv_text.
    DESCRIBE TABLE gt_alvdata LINES lv_lines.
    CONCATENATE 'Totally'(t62) lv_lines 'entries are generated'(t63)
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ENDIF.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'FRM_MY_STATUS'
      i_callback_user_command  = gv_callback
      is_layout                = gs_layout
      it_fieldcat              = gt_fieldcat
      i_save                   = gv_save
      is_variant               = gs_disvar
    TABLES
      t_outtab                 = gt_alvdata
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  FRM_MY_STATUS
*&---------------------------------------------------------------------*
*       Status Set
*----------------------------------------------------------------------*
*  --> I_EXTAB    STATUS
*----------------------------------------------------------------------*
FORM frm_my_status USING i_extab TYPE slis_t_extab.

  SET PF-STATUS '9100'.

ENDFORM.        "FRM_MY_STATUS

*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM alv_fieldcat_set .

** Create Fieldcatalogue from internal table

  CLEAR gv_col.
* Field definitions
  PERFORM add_field USING:
    'GAC'       4   'GAC'(f01)                        'X' ' ',
    'PGC'       4   'PGC'(f02)                        'X' ' ',
    'PLC'       4   'PLC'(f20)                        'X' ' ',
    'MATNR'     18  'Material Number'(f03)            'X' 'X',
    'MAKTX_E'   40  'Material Description (EN)'(f04)  ' ' ' ',
    'MAKTX_Z'   40  'Material Description (ZH)'(f05)  ' ' ' ',
    'WERKS'     6   'Plant'(f06)                ' ' ' ',
    'LGORT'     6   'Storage Location'(f07)     ' ' ' ',
    'PRCTR'     10  'Profit Center'(f08)        ' ' ' ',
    'LBKUM'     13  'Total Stock'(f09)          ' ' ' ',
    'STPRS'     11  'Unit Standard Cost'(f10)   ' ' ' ',
    'TTVAL'     13  'Total Value'(f11)          ' ' ' ',
    'SOBKZ'     1   'Spec Stock'(f12)           ' ' ' ',
    'SERNR'     8   'Serial number'(f13)             ' ' ' ',
    'EQUNR'     8   'Equipment number'(f14)             ' ' ' ',
    'EQKTX'     8   'Equipment Description'(f15)        ' ' ' ',
    'STTXT'     8   'System Status'(f16)                ' ' ' ',
    'GIDAT'     8   'First GR Date'(f17)                 ' ' ' ',
    'ABSMG'     15  '12 Month Delivered QTY'(f18)         ' ' ' '.
*    'ABSMG_ME'  3   'Referrence Uom'(f19)               ' '.

ENDFORM.                    " ALV_FIELDCAT_SET

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr
                         p_fix_column
                         p_hotsp.
  DATA:
     ls_fieldcat TYPE slis_fieldcat_alv.

  gv_col = gv_col + 1.

  CLEAR ls_fieldcat.
  ls_fieldcat-col_pos    = gv_col.
  ls_fieldcat-fieldname  = p_field.
  ls_fieldcat-outputlen  = p_len.
  ls_fieldcat-seltext_l  = p_descr.
  ls_fieldcat-seltext_m  = p_descr.
  ls_fieldcat-seltext_s  = p_descr.
  ls_fieldcat-fix_column = p_fix_column.
*  ls_fieldcat-emphasize  = 'X'.
  ls_fieldcat-hotspot    = p_hotsp.

* Suppress leading zeros for the material number
  IF p_field = 'MATNR'.
    ls_fieldcat-edit_mask = '==MATN1'.
  ENDIF.

  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   "Auto optimize column width
  gs_layout-detail_popup = gc_x.        "Show detail screen

ENDFORM.                    " ALV_LAYOUT_SET

*&---------------------------------------------------------------------*
*&      Form  GET_DATA_MAKT
*&---------------------------------------------------------------------*
*       Get material description from table make
*----------------------------------------------------------------------*
FORM get_data_makt .

  CHECK gt_equi IS NOT INITIAL.
*Get the material description of English version
  SELECT *
    FROM makt
    INTO TABLE gt_makt_en
    FOR ALL ENTRIES IN gt_equi
    WHERE matnr = gt_equi-matnr
      AND spras = 'E'.
*Get the material description of Chinese version
  SELECT *
    FROM makt
    INTO TABLE gt_makt_zh
    FOR ALL ENTRIES IN gt_equi
    WHERE matnr = gt_equi-matnr
      AND spras = '1'.

ENDFORM.                    " GET_DATA_MAKT

*&---------------------------------------------------------------------*
*&      Form  GENERATE_ALV_DATA
*&---------------------------------------------------------------------*
*       Generate ALV data for output
*----------------------------------------------------------------------*
FORM generate_alv_data .

  DATA: ls_marc       TYPE ty_marc,
        ls_mvke       TYPE ty_mvke,
        ls_makt       TYPE makt,
        ls_mbew       TYPE ty_mbew,
        ls_eqbs       TYPE ty_eqbs,
        ls_equi       TYPE ty_equi,
        ls_eqkt       TYPE eqkt,
        ls_ser03      TYPE ty_ser03,
        ls_temp_equi  TYPE ty_equi,
        lt_status_sys TYPE STANDARD TABLE OF bapi_itob_status,
        lt_status_usr TYPE STANDARD TABLE OF bapi_itob_status,
        ls_status     TYPE bapi_itob_status,
        ls_ce11000    TYPE ty_ce11000,
        ls_alvdata    TYPE  ty_alv_output.

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to generate ALV data'(t64).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t64.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 40
        text       = gv_text.
  ENDIF.

  SORT gt_equi BY equnr ASCENDING
                  erdat ASCENDING.

  LOOP AT gt_equi INTO ls_equi.
    CLEAR:  ls_mvke,
            ls_makt,
            ls_mbew,
            ls_eqbs,
            ls_marc,
            ls_temp_equi,
            ls_ce11000,
            ls_alvdata,
            ls_ser03,
            ls_eqkt.
    IF    ls_equi-vkorg IS NOT INITIAL
      AND ls_equi-vtweg IS NOT INITIAL.
      READ TABLE gt_tvkwz TRANSPORTING NO FIELDS
        WITH KEY  vkorg = ls_equi-vkorg
                  vtweg = ls_equi-vtweg.
      IF sy-subrc <> 0.
        CONTINUE.
      ENDIF.
    ENDIF.

    REFRESH:  lt_status_sys,
              lt_status_usr.

    READ TABLE gt_eqbs INTO ls_eqbs
      WITH KEY equnr = ls_equi-equnr.

    READ TABLE gt_mvke INTO ls_mvke
      WITH KEY matnr = ls_equi-matnr.
    IF    ( s_pgc IS NOT INITIAL AND ls_mvke-pgc NOT IN s_pgc )
      OR  ( s_gac IS NOT INITIAL AND ls_mvke-gac NOT IN s_gac )
      OR  ( s_plc IS NOT INITIAL AND ls_mvke-plc NOT IN s_plc ).
      CONTINUE.
    ENDIF.
    ls_alvdata-gac    = ls_mvke-gac.
    ls_alvdata-pgc    = ls_mvke-pgc.
    ls_alvdata-plc    = ls_mvke-plc.
    ls_alvdata-matnr  = ls_equi-matnr.
    CLEAR: ls_makt.
    READ TABLE gt_makt_en INTO ls_makt
      WITH KEY matnr = ls_equi-matnr.
    ls_alvdata-maktx_e = ls_makt-maktx.
    CLEAR: ls_makt.
    READ TABLE gt_marc INTO ls_marc
      WITH KEY matnr = ls_equi-matnr
               werks = ls_eqbs-b_werk.
    READ TABLE gt_makt_zh INTO ls_makt
      WITH KEY matnr = ls_equi-matnr.
    ls_alvdata-maktx_z = ls_makt-maktx.
    ls_alvdata-werks   = ls_eqbs-b_werk.
    ls_alvdata-lgort  = ls_eqbs-b_lager.
    ls_alvdata-prctr  = ls_marc-prctr.
    ls_alvdata-lbkum  = 1.
    READ TABLE gt_mbew INTO ls_mbew
     WITH KEY matnr = ls_marc-matnr
              bwkey = ls_marc-werks.
    IF sy-subrc = 0.
      IF ls_mbew-peinh <> 0.
        ls_alvdata-stprs  = ls_mbew-stprs / ls_mbew-peinh.
      ENDIF.
    ENDIF.
    ls_alvdata-ttval  = ls_alvdata-lbkum * ls_alvdata-stprs.
    READ TABLE gt_eqbs INTO ls_eqbs
      WITH KEY equnr = ls_equi-equnr.
    IF sy-subrc = 0.
      ls_alvdata-sobkz = ls_eqbs-sobkz.
    ENDIF.
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
      EXPORTING
        input  = ls_equi-equnr
      IMPORTING
        output = ls_alvdata-equnr.
    CALL FUNCTION 'CONVERSION_EXIT_GERNR_OUTPUT'
      EXPORTING
        input  = ls_equi-sernr
      IMPORTING
        output = ls_alvdata-sernr.

    READ TABLE gt_eqkt  INTO ls_eqkt
      WITH KEY equnr = ls_equi-equnr.
    IF sy-subrc = 0.
      ls_alvdata-eqktx = ls_eqkt-eqktx.
    ENDIF.
    CALL FUNCTION 'BAPI_EQUI_GETSTATUS'
      EXPORTING
        equipment     = ls_equi-equnr
      TABLES
        system_status = lt_status_sys
        user_status   = lt_status_usr.
    LOOP AT lt_status_sys INTO ls_status.
      CONCATENATE   ls_alvdata-sttxt
                    ls_status-text
             INTO   ls_alvdata-sttxt
             SEPARATED BY space.
    ENDLOOP.
    READ TABLE gt_equi INTO ls_temp_equi
      WITH KEY sernr = ls_equi-sernr.
    ls_alvdata-gidat = ls_temp_equi-erdat.
    READ TABLE gt_ser03 INTO ls_ser03
      WITH KEY equnr = ls_equi-equnr
               werk = ls_marc-werks.
    IF sy-subrc = 0.
      ls_alvdata-gidat = ls_ser03-datum.
    ENDIF.
    LOOP AT gt_ce11000 INTO ls_ce11000
        WHERE artnr = ls_marc-matnr
          AND vkorg = ls_equi-vkorg.
      ls_alvdata-absmg      = ls_alvdata-absmg + ls_ce11000-absmg.
      ls_alvdata-absmg      = ls_ce11000-absmg.
    ENDLOOP.
    APPEND ls_alvdata TO gt_alvdata.
    CLEAR: ls_alvdata.
  ENDLOOP.

ENDFORM.                    " GENERATE_ALV_DATA

*&---------------------------------------------------------------------*
*&      Form  ALV_PROGRESS_INDICATOR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM alv_progress_indicator .

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to set ALV property'(t60).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t60.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 75
        text       = gv_text.
  ENDIF.

ENDFORM.                    " ALV_PROGRESS_INDICATOR

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       Set Others
*----------------------------------------------------------------------*
FORM alv_others_set .

  gv_callback = gc_callback_routine.

ENDFORM.                    " ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*&      Form  F4_FOR_VARIANT
*&---------------------------------------------------------------------*
*       F4 help for display variant
*----------------------------------------------------------------------*
FORM f4_for_variant .

  DATA: ls_disvar TYPE disvariant,
        lv_exit   TYPE c.

  ls_disvar-report = sy-repid.

  CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
    EXPORTING
      is_variant    = ls_disvar
      i_save        = gv_save
    IMPORTING
      e_exit        = lv_exit
      es_variant    = gs_disvar
    EXCEPTIONS
      not_found     = 1
      program_error = 2
      OTHERS        = 3.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    IF lv_exit = space.
      p_vari = gs_disvar-variant.
    ENDIF.
  ENDIF.

ENDFORM.                    " F4_FOR_VARIANT

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       USER_COMMAND
*----------------------------------------------------------------------*
FORM user_command USING u_ucomm     LIKE sy-ucomm           "#EC CALLED
                        p_selfield  TYPE slis_selfield.

  CASE u_ucomm.
    WHEN '&IC1'.        " SAP standard code for double-clicking
      IF p_selfield-fieldname = 'MATNR'.
        IF p_selfield-value IS NOT INITIAL.
          SET PARAMETER ID 'MAT' FIELD p_selfield-value.
          CALL TRANSACTION 'MM03' AND SKIP FIRST SCREEN.
        ENDIF.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.

ENDFORM.                    " USER_COMMAND
*&---------------------------------------------------------------------*
*&      Form  EDIT_ALV_DATA
*&---------------------------------------------------------------------*
*       Edit all available
*----------------------------------------------------------------------*
FORM edit_alv_data .

ENDFORM.                    " EDIT_ALV_DATA
*&---------------------------------------------------------------------*
*&      Form  CHECK_PARAMETER_SET
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM check_parameter_set .
  LOOP AT SCREEN.
    IF screen-group1 = 'M1'.
      screen-invisible = '1'.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.
ENDFORM.                    " CHECK_PARAMETER_SET
*&---------------------------------------------------------------------*
*&      Form  GET_AGR_TEXTS
*&---------------------------------------------------------------------*
*       Extract sales data from table mvke
*----------------------------------------------------------------------*
FORM get_data_mvke.

  DATA: lv_pgc    TYPE char4,
        lv_prctr  TYPE z_prctr,
        lv_segmnt TYPE fb_segment.

  FIELD-SYMBOLS: <lfs_mvke> TYPE ty_mvke.
  CHECK gt_equi IS NOT INITIAL.
  SELECT    matnr
            vkorg
            vtweg
            prodh
    FROM    mvke
    INTO CORRESPONDING FIELDS OF TABLE gt_mvke
    FOR ALL ENTRIES IN gt_equi
    WHERE  matnr = gt_equi-matnr.

  LOOP AT gt_mvke ASSIGNING <lfs_mvke>.
    CLEAR: lv_pgc,lv_prctr,lv_segmnt.

    <lfs_mvke>-gac = <lfs_mvke>-prodh+0(4).
    <lfs_mvke>-pgc = <lfs_mvke>-prodh+4(4).
    CONDENSE: <lfs_mvke>-gac,<lfs_mvke>-pgc.
    lv_pgc = <lfs_mvke>-pgc.
    SELECT SINGLE prctr
      INTO lv_prctr
      FROM yse_prctr_deriv
      WHERE vtweg = '01'
        AND pgc   = lv_pgc.
    IF sy-subrc = 0.
      SELECT SINGLE segment
        INTO lv_segmnt
        FROM yse_prctr_bl
        WHERE prctr =  lv_prctr.
      IF sy-subrc = 0.
        SELECT SINGLE target1
          FROM k9rcd11000010
          INTO <lfs_mvke>-plc
          WHERE sour1_from = lv_segmnt.
      ENDIF.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " GET_DATA_MVKE
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_EQUIPMENT
*&---------------------------------------------------------------------*
*       Get equipemnts from eqbs and itob
*----------------------------------------------------------------------*
FORM get_data_equipment.

  CHECK gt_equipment IS NOT INITIAL.
  SELECT    equi~equnr
            equi~erdat
            equi~matnr
            equi~sernr
            equz~iloan
            equz~iwerk
            iloa~vkorg
            iloa~vtweg
            iloa~spart
    FROM equi
      INNER JOIN equz
      ON equi~equnr = equz~equnr
      INNER JOIN iloa
      ON equz~iloan = iloa~iloan
    INTO TABLE gt_equi
    FOR ALL ENTRIES IN gt_equipment
    WHERE equi~equnr  = gt_equipment-equnr
      AND equi~matnr  IN s_matnr
      AND equi~sernr  IN s_sernr.
*      AND iloa~vkorg = p_vkorg
*      AND iloa~vtweg = p_vtweg
*      AND iloa~spart = p_spart.

  SORT gt_equi.
  DELETE ADJACENT DUPLICATES FROM gt_equi
    COMPARING equnr erdat matnr sernr.

  SELECT *
    FROM eqkt
    INTO TABLE gt_eqkt
    FOR ALL ENTRIES IN gt_equipment
    WHERE equnr = gt_equipment-equnr
      AND spras = sy-langu.

ENDFORM.                    " GET_DATA_EQUIPMENT

*&---------------------------------------------------------------------*
*&      Form  GET_DATA_MBEW
*&---------------------------------------------------------------------*
*       Gain Material Valuation data from MBEW
*----------------------------------------------------------------------*
FORM get_data_mbew .
  CHECK gt_equi IS NOT INITIAL.
  SELECT    matnr
            bwkey
            bwtar
            stprs
            peinh
    FROM mbew
    INTO TABLE gt_mbew
    FOR ALL ENTRIES IN  gt_equi
    WHERE matnr = gt_equi-matnr
      AND bwkey IN s_werks.
ENDFORM.                    " GET_DATA_MBEW

*&---------------------------------------------------------------------*
*&      Form  GET_DATA_CE11000
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_data_ce11000 .

  DATA: lv_date_from  TYPE sy-datum,
        lv_date_to    TYPE sy-datum,
        lv_per_from(3)   TYPE n,
        lv_yer_from(4)   TYPE n.
*        lv_per_to     TYPE jahrper.
  RANGES: r_per FOR ce11000-perio.
  DATA: lr_per  LIKE LINE OF r_per.

  CHECK gt_marc IS NOT INITIAL.

  lv_date_to        =  sy-datum.
  lv_date_from      =  sy-datum.
  lv_date_from(4)   = lv_date_from(4) - 1.

  lv_per_from = lv_date_from+4(2) .
  lv_yer_from = lv_date_from(4).

  DO 13 TIMES.
    lr_per-sign     = 'I'.
    lr_per-option   = 'EQ'.
    lr_per-low(4)   = lv_yer_from.
    lr_per-low+4(3) = lv_per_from.
    IF lv_per_from = '012'.
      lv_yer_from = lv_yer_from + 1.
      lv_per_from = '001'.
    ELSE.
      lv_per_from = lv_per_from + 1.
    ENDIF.
    APPEND lr_per TO r_per.
  ENDDO.

  SELECT   paledger
           vrgar
           versi
           perio
           paobjnr
           pasubnr
           belnr
           posnr
           fadat
           artnr
           vkorg
           ww005
           absmg_me
           absmg
    INTO TABLE gt_ce11000
    FROM ce11000
    FOR ALL ENTRIES IN gt_marc
    WHERE paledger = '02'
      AND vrgar    = 'F'
      AND perio    IN r_per
      AND artnr    = gt_marc-matnr
*      AND vkorg    = p_vkorg
      AND ww005    = 'ZOR'.

  DELETE gt_ce11000 WHERE fadat < lv_date_from.
  DELETE gt_ce11000 WHERE fadat > lv_date_to.

ENDFORM.                    " GET_DATA_CE11000
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_EQBS
*&---------------------------------------------------------------------*
*       Gain data from eqbs
*----------------------------------------------------------------------*
FORM get_data_eqbs .

  DATA: ls_eqbs       TYPE ty_eqbs,
        ls_equipment   TYPE ty_equipment.

  SELECT  equnr
          b_werk
          b_lager
          sobkz
    FROM  eqbs
    INTO TABLE gt_eqbs
    WHERE equnr IN s_equnr
      AND b_werk IN  s_werks
      AND b_lager IN s_lgort.
  LOOP AT gt_eqbs INTO ls_eqbs.
    ls_equipment-equnr = ls_eqbs-equnr.
    APPEND ls_equipment  TO gt_equipment.
  ENDLOOP.

  SORT gt_equipment.
  DELETE ADJACENT DUPLICATES FROM gt_equipment.

ENDFORM.                    " GET_DATA_EQBS
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_MARC
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_data_marc .
  CHECK gt_equi IS NOT INITIAL.
  SELECT  matnr
          werks
          lvorm
          prctr
    INTO TABLE gt_marc
    FROM marc
    FOR ALL ENTRIES IN gt_equi
    WHERE matnr = gt_equi-matnr
      AND werks IN s_werks
      AND lvorm = ' '.
ENDFORM.                    " GET_DATA_MARC
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_MSEG
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_data_mseg .
*  CHECK gt_marc IS NOT  INITIAL.
*  SELECT *
*    FROM mseg
*    INTO TABLE gt_mseg
*    FOR ALL ENTRIES IN gt_marc
*    WHERE BWART in s_bwart
*      AND MATNR = gt_marc-matnr
*      AND WERKS = gt_marc-werks.
  CHECK gt_equi IS NOT INITIAL.
  SELECT  objk~equnr
          objk~obknr
          objk~sernr
          ser03~bwart
          ser03~datum
          ser03~werk
          ser03~lagerort
    FROM objk
      INNER JOIN ser03
      ON objk~obknr = ser03~obknr
    INTO TABLE gt_ser03
    FOR ALL ENTRIES IN gt_equi
    WHERE objk~equnr = gt_equi-equnr
      AND objk~sernr = gt_equi-sernr
      AND ser03~bwart IN s_bwart
      AND ser03~werk  IN s_werks
      AND ser03~lagerort IN s_lgort.

ENDFORM.                    " GET_DATA_MSEG
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_TVKWZ
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_data_tvkwz .
  SELECT *
    FROM tvkwz
    INTO TABLE gt_tvkwz
    WHERE werks IN s_werks.
ENDFORM.                    " GET_DATA_TVKWZ

*Text symbol text£º
*B01:Wrong WHERE condition!
*F01:GAC
*F02:PGC
*F03:Material Number
*F04:Material Description (EN)
*F05:Material Description (ZH)
*F06:Plant
*F07:Storage Location
*F08:Profit Center
*F09:Total Stock
*F10:Unit Standard Cost
*F11:Total Value
*F12:Spec Stock
*F13:Serial number
*F14:Equipment number
*F15:Equipment Description
*F16:System Status
*F17:First GR Date
*F18:12 Month Delivered QTY
*F20:PLC
*M01:No data fullfill the selection screen you enterred
*T01:Equipment
*T02:General Input
*T03:ALV Layout
*T52:Start report:
*T60:Begin to set ALV property
*T61:No data exist per the selection screen
*T62:Totally
*T63:entries are generated

*T64:Begin to generate ALV data
*Selection text£º
*P_SPART:D       .
*P_VARI:D       .
*P_VKORG:D       .
*P_VTWEG:D       .
*S_BWART:D       .
*S_EQUNR:D       .
*S_GAC:        GAC
*S_LGORT:D       .
*S_MATNR:D       .
*S_PGC:        PGC
*S_PLC:        PLC
*S_SERNR:D       .
*S_WERKS:D       .
