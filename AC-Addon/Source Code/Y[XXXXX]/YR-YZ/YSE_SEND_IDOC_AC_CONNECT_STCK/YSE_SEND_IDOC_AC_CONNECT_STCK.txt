*&---------------------------------------------------------------------*
*& Report  YSE_SEND_IDOC_AC_CONNECT_STCK                               *
*&                                                                     *
*&---------------------------------------------------------------------*
*&                                                                     *
*& AC Connect : AC Connect : Send Stock Availability Idocs             *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 15.05.2009
*  Change Request Number : CR0101
*  Transport request Nr. : CD1K948292
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME             | CORRECT. NR. | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 16.02.2011 | Jules Smets      | CD1K963029   | CR1789      *
*         | Available stock = Unrestricted stock - Allocated stock     *
*----------------------------------------------------------------------*
* MOD-002 | 13.02.2012 | Jules Smets      | CD1K970349   | CR2290      *
*         | Bugfix: AC Connect Next available qty                      *
*----------------------------------------------------------------------*
* MOD-003 | 29.10.2014 | Zhang            | CD1K983634   | CR3363      *
*         | The change of stock                                        *
*----------------------------------------------------------------------*
************************************************************************

REPORT  yse_send_idoc_ac_connect_stck  MESSAGE-ID yam_inf    .

TABLES : yse_sd_acc_stock,
         mara,
         mard,
         mdbs,
         mdla,
         ekko.

DATA: i_edidc_control_comm LIKE edidc  OCCURS 1
                                       WITH HEADER LINE,
      i_edidd_data LIKE edidd  OCCURS 0
                               WITH HEADER LINE .
DATA: wa_edidc    LIKE edidc.
DATA: created_idocs LIKE sy-tabix.

DATA: it_stpl TYPE TABLE OF yse_sd_acc_stock
                   WITH HEADER LINE.

DATA: lv_saveind TYPE eindt.

DATA: BEGIN OF it_urst OCCURS 0,
        werks     TYPE werks_d,
        matnr     TYPE matnr,
        lgort     TYPE lgort_d,
        labst     TYPE labst,
        meins     TYPE meins,
      END OF it_urst.

DATA: BEGIN OF it_neid OCCURS 0,
        werks     TYPE werks_d,
        matnr     TYPE matnr,
*        lgort     TYPE lgort_d,
        ebeln     TYPE ebeln,
        eindt     TYPE eindt,
        menge     TYPE bstmg,
        meins     TYPE meins,
        webaz     TYPE fkday,
      END OF it_neid.

DATA: BEGIN OF it_vkorg  OCCURS 0,
        vkorg   TYPE vkorg,
      END OF it_vkorg.

DATA: BEGIN OF it_vtweg  OCCURS 0,
        vtweg   TYPE vtweg,
      END OF it_vtweg.

*DATA: BEGIN OF it_mvke  OCCURS 0,
TYPES: BEGIN OF t_mvke,
         matnr    TYPE matnr,
         prat2    TYPE prat2,
*       END OF it_mvke.
       END OF t_mvke.
*** MOD-001 * begin ***
DATA: it_mvkei TYPE TABLE OF t_mvke.
DATA: it_mvke TYPE HASHED TABLE OF t_mvke
                   WITH UNIQUE KEY matnr
                   WITH HEADER LINE.

DATA: BEGIN OF it_vbbe OCCURS 0,
       vbeln LIKE vbbe-vbeln,
       posnr LIKE vbbe-posnr,
       matnr LIKE vbbe-matnr,
       werks LIKE vbbe-werks,
       lgort LIKE vbbe-lgort,
       vbtyp LIKE vbbe-vbtyp,
       vmeng LIKE vbbe-vmeng,
      END OF it_vbbe.
DATA: wa_vbbe LIKE it_vbbe.

DATA: BEGIN OF it_eban  OCCURS 0,
        reswk  TYPE reswk,
        matnr  TYPE matnr,
        menge  TYPE bamng,
      END OF it_eban.

DATA: BEGIN OF it_ekpo OCCURS 0 ,
        ebeln  TYPE ebeln,
        ebelp  TYPE ebelp,
        reswk  TYPE reswk,
        matnr  TYPE matnr,
        lgort  TYPE lgort_d,
        menge  TYPE bstmg,
*        elikz  TYPE elikz,                                  "MOD-002
*        erekz  TYPE erekz,                                  "MOD-002
  END OF it_ekpo.
* DATA: wa_ekpo   LIKE LINE OF it_ekpo.                       "MOD-002


* Begin of insert MOD-002

DATA: lv_index TYPE sy-tabix,
      lv_vbeln TYPE vbeln,
      lv_eindt TYPE eindt,
      lv_menge TYPE bstmg,
      lv_erdat TYPE erdat.

DATA: BEGIN OF it_ekpo_n OCCURS 0 ,
        ebeln  TYPE ebeln,
        ebelp  TYPE ebelp,
        werks  TYPE werks,
        matnr  TYPE matnr,
        lgort  TYPE lgort_d,
        menge  TYPE bstmg,
        meins  TYPE bstme,
        menge_ro TYPE bstmg, "open reveived
        menge_io TYPE bstmg, " open invoiced
        eindt    TYPE eindt, " expected delivery of open qty
        lyntp(2)  TYPE c,
*        elikz  TYPE elikz,
*        erekz  TYPE erekz,
  END OF it_ekpo_n.
* DATA: wa_ekpo   LIKE LINE OF it_ekpo.


* End of insert MOD-002

DATA: BEGIN OF it_ekbe OCCURS 0,
        ebeln  TYPE ebeln,
        ebelp  TYPE ebelp,
        werks  TYPE werks_d,
        matnr  TYPE matnr,
        menge  TYPE menge_d,
        belnr  TYPE mblnr,
        BUZEI  type ekbe-BUZEI,
        bwart  TYPE bwart,
  END OF it_ekbe.

TYPES: BEGIN OF t_alloc,
         werks  TYPE werks_d,
         matnr   TYPE matnr,
         lgort  TYPE lgort_d,
         quant  TYPE kwmeng,
       END OF t_alloc.
DATA: it_alloc TYPE HASHED TABLE OF t_alloc
                    WITH UNIQUE KEY werks matnr lgort
                    WITH HEADER LINE.
*** MOD-001 * end ***

*** MOD-002 * begin ***

DATA : lv_line TYPE i.
DATA: lv_typ(2)  TYPE c.

TYPES: BEGIN OF  t_eket,
         ebeln      TYPE eket-ebeln,
         ebelp      TYPE eket-ebelp,
         wemng      TYPE eket-wemng,
       END OF t_eket.
DATA:  it_eket      TYPE HASHED TABLE OF t_eket
                         WITH UNIQUE KEY ebeln ebelp.
DATA:  wa_eket      TYPE t_eket.

TYPES: BEGIN OF  t_eket2,
         ebeln      TYPE eket-ebeln,
         ebelp      TYPE eket-ebelp,
         wemng      TYPE eket-wemng,
       END OF t_eket2.
DATA: wa_eket2   TYPE t_eket2.
DATA: it_eket2   TYPE TABLE OF  t_eket2.

TYPES: BEGIN OF  t_ekbe_invqty,
         ebeln      TYPE ekbe-ebeln,
         ebelp      TYPE ekbe-ebelp,
         zekkn      LIKE ekbe-zekkn,
         vgabe      LIKE ekbe-vgabe,
         gjahr      LIKE ekbe-gjahr,
         belnr      LIKE ekbe-belnr,
         buzei      LIKE ekbe-buzei,
         menge      TYPE ekbe-menge,
         shkzg      TYPE ekbe-shkzg,
       END OF t_ekbe_invqty.
DATA: wa_ekbe_invqty   TYPE t_ekbe_invqty.
DATA: it_ekbe_invqty   TYPE TABLE OF  t_ekbe_invqty.

TYPES: BEGIN OF  t_ekbe_invqty_aggr,
         ebeln      TYPE ekbe-ebeln,
         ebelp      TYPE ekbe-ebelp,
         menge      TYPE ekbe-menge,
       END OF t_ekbe_invqty_aggr.
DATA:  wa_ekbe_invqty_aggr TYPE  t_ekbe_invqty_aggr,
       it_ekbe_invqty_aggr TYPE  HASHED TABLE OF t_ekbe_invqty_aggr
                                 WITH UNIQUE KEY ebeln ebelp.
*** MOD-002 * end ***

TYPES: BEGIN OF t_stock.
        INCLUDE STRUCTURE YSE_E1_ACC_STOCK_av.
TYPES: END OF t_stock.
DATA: it_stock TYPE TABLE OF t_stock WITH HEADER LINE.
DATA: wa_stock TYPE t_stock.

TYPES: BEGIN OF t_mara,
         matnr    TYPE matnr,
         bismt    TYPE bismt,
      END OF t_mara.
*** MOD-001 * begin ***
*DATA: it_mara  TYPE TABLE OF t_mara WITH HEADER LINE.
DATA: it_marai  TYPE TABLE OF t_mara WITH HEADER LINE.
DATA: it_mara  TYPE HASHED TABLE OF t_mara
                    WITH UNIQUE KEY matnr
                    WITH HEADER LINE.
*** MOD-001 * end ***
DATA: wa_mara  TYPE t_mara.

DATA: gv_werks     TYPE werks_d,
      gv_index     TYPE syindex,
      gv_fabkl     TYPE fabkl,
      gv_ebeln     TYPE ebeln,
      gv_allocdate TYPE dats,                               "MOD-001
      gv_avstock   TYPE labst.                              "MOD-001

CONSTANTS : c_input            TYPE c     VALUE '0',
            c_ls(2)            TYPE c     VALUE 'LS',
            c_mestyp_stck      LIKE edidc-mestyp VALUE 'YSE_ACC_STOCK',
            c_idoc_type_stck   LIKE edidc-idoctp VALUE 'YSE_ACC_STOCK',
            c_segment_stck(20) TYPE c     VALUE 'YSE_E1_ACC_STOCK_AV',
            c_x(1)             TYPE c     VALUE 'X',
            c_v(2)             TYPE c     VALUE 'V ',
            c_004(3)           TYPE c     VALUE '004',
            c_0000(4)          TYPE c     VALUE '0000',
            c_underscore(1)    TYPE c     VALUE '_',    " Underscore
            c_sc1(3)           TYPE c     VALUE 'SC1',
*** MOD-001 * begin ***
            c_znb1(4)          TYPE c     VALUE 'ZNB1',
            c_zub1(4)          TYPE c     VALUE 'ZUB1',
            c_lf(4)            TYPE c     VALUE 'LF  ',
            c_zlf(4)           TYPE c     VALUE 'ZLF ',
            c_zl03(4)          TYPE c     VALUE 'ZL03',
            c_zlr1(4)          TYPE c     VALUE 'ZLR1',
            c_zkt(4)           TYPE c     VALUE 'ZKT ',
            c_6(1)             TYPE c     VALUE '6',
            c_7(1)             TYPE c     VALUE '7',
            c_j(1)             TYPE c     VALUE 'J',
            c_c(1)             TYPE c     VALUE 'C',
            c_n(1)             TYPE c     VALUE 'N',
            c_1000(4)          TYPE c     VALUE '1000',
            c_641(3)           TYPE c     VALUE '641',
            c_642(3)           TYPE c     VALUE '642'.
*** MOD-001 * end ***

RANGES: r_werks   FOR mard-werks,
        r_vkorg   FOR vbak-vkorg,
        r_vtweg   FOR vbak-vtweg.

* Selection
PARAMETERS: p_mess  LIKE tbdme-mestyp OBLIGATORY DEFAULT 'YSE_ACC_STOCK'
                    MODIF ID sc1.
SELECTION-SCREEN: BEGIN OF BLOCK block1 WITH FRAME TITLE text-001.
*** MOD-001 * begin ***
*SELECT-OPTIONS: s_famb   FOR yse_sd_acc_stock-famback.
PARAMETERS: p_famb TYPE zfamback  OBLIGATORY.
*** MOD-001 * end ***
SELECT-OPTIONS: s_matnr  FOR mard-matnr,
                s_lgort  FOR mard-lgort  DEFAULT '1000',
                s_mtart  FOR mara-mtart  DEFAULT 'ZMAT',
                s_bsart  FOR ekko-bsart  DEFAULT 'ZNB1'.
SELECTION-SCREEN SKIP.
PARAMETERS:     p_prat2  TYPE prat2  AS CHECKBOX.
SELECTION-SCREEN: END OF BLOCK block1.

PARAMETERS: p_no_out  TYPE xfeld  NO-DISPLAY.


************************************************************************
*    Disable Message Type Screen Field                                 *
************************************************************************
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-group1 = c_sc1.
      screen-input   = c_input.
      MODIFY SCREEN.
      CONTINUE.
    ENDIF.
  ENDLOOP.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Get the information
  PERFORM get_detail.


************************************************************************
*       E N D - O F - S E L E C T I O N    E V E N T                   *
************************************************************************
END-OF-SELECTION .

  IF NOT p_no_out IS INITIAL.
*   NO output
    IF NOT it_stock[] IS INITIAL .
*     Generate Idoc's
      PERFORM create_idocs_stock USING p_mess.
    ENDIF.
  ELSE.
    IF it_stock[] IS INITIAL .
      WRITE: / 'No Idocs created'.
    ELSE.
*   Generate Idoc's
      PERFORM create_idocs_stock USING p_mess.
      WRITE : /   created_idocs , ' Idocs created'.
    ENDIF.
    WRITE: / 'Job finished'.
  ENDIF.


************************************************************************
*--- S U B R O U T I N E S --------------------------------------------*
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  Validate_ALE_Configuration
*&---------------------------------------------------------------------*
FORM validate_ale_configuration .

  DATA: l_create_idoc     TYPE     c .

* CHECK IF IDOC CONFIGURATION IS READY AND IDOC CAN BE PROCESSED.
  CALL FUNCTION 'ALE_MODEL_DETERMINE_IF_TO_SEND'
    EXPORTING
      message_type           = p_mess
    IMPORTING
      idoc_must_be_sent      = l_create_idoc
    EXCEPTIONS
      own_system_not_defined = 1
      OTHERS                 = 2.

  IF sy-subrc <> 0.
    MESSAGE e029 WITH p_mess.
    EXIT.
  ENDIF.

ENDFORM.                    " Validate_ALE_Configuration

*&---------------------------------------------------------------------*
*&      Form  Get_Detail
*&---------------------------------------------------------------------*
*       To get the information for selected Back-end FAM's
*----------------------------------------------------------------------*
FORM get_detail.

  CLEAR: it_stpl[], it_urst[], it_neid[], it_stock[].

* Select conditions data based on Selection screen parameters
  SELECT * INTO TABLE it_stpl
           FROM yse_sd_acc_stock
*           WHERE famback IN s_famb.                        "MOD-001
           WHERE famback = p_famb.                          "MOD-001
* Process Error - No data found for the Selected Back-end FAM's
  IF sy-subrc NE 0 .
    IF NOT p_no_out IS INITIAL.
      WRITE: text-e01.
    ENDIF.
    EXIT.
  ENDIF .

* Allocation period
  READ TABLE it_stpl INDEX 1.
  gv_allocdate = sy-datum + it_stpl-y_alloc_per.

  SORT it_stpl.

* Get stock
  LOOP AT it_stpl.
*   Select plants
    CLEAR r_werks[].
    r_werks-sign = 'I'.
    r_werks-option = 'EQ'.
    DO 15 TIMES VARYING gv_werks FROM it_stpl-werks01 NEXT it_stpl-werks02.
      IF gv_werks IS INITIAL.
        EXIT.
      ENDIF.
      r_werks-low = gv_werks.
      APPEND r_werks.
    ENDDO.

*   Get "unrestricted stock"
    SELECT d~werks d~matnr d~lgort d~labst a~meins
           APPENDING TABLE it_urst
           FROM mara AS a
                INNER JOIN mard AS d
                           ON a~matnr = d~matnr
           WHERE a~matnr IN s_matnr
             AND a~mtart IN s_mtart
             AND d~werks IN r_werks
             AND d~lgort IN s_lgort.
    SORT it_urst.
    DELETE ADJACENT DUPLICATES FROM it_urst
           COMPARING werks matnr.

*   Select materials depending on attribute 2 (when needed)
    IF NOT p_prat2 IS INITIAL.
      PERFORM material_selection.
    ENDIF.

    IF it_urst[] IS INITIAL.
      CONTINUE.
    ENDIF.

* Begin of delete MOD-002
*   Next expected incomming delivery
*   PO item
*    SELECT werks matnr ebeln eindt menge meins webaz
*           APPENDING TABLE it_neid
*           FROM mdbs
*           FOR ALL ENTRIES IN it_urst
*           WHERE matnr =  it_urst-matnr
*             AND werks =  it_urst-werks
*             AND loekz =  space.
**   Shipping Notification
*    SELECT werks matnr ebeln eindt menge meins webaz
*           APPENDING TABLE it_neid
*           FROM mdla
*           FOR ALL ENTRIES IN it_urst
*           WHERE matnr =  it_urst-matnr
*             AND werks =  it_urst-werks
*             AND loekz =  space.
*    SORT it_neid.
*    DELETE ADJACENT DUPLICATES FROM it_neid
*           COMPARING werks matnr.
*   Select PO's which are taken into account
*    SORT it_neid BY ebeln.
*    PERFORM select_po.
*   Calculate date according to factory calendar
*    SORT it_neid.
*    PERFORM calc_date.
* End of delete MOD-002
* Begin of insert MOD-002
*    PERFORM next_avail.
* End of insert MOD-002
  ENDLOOP.

  CHECK NOT it_urst[] IS INITIAL.

*** MOD-001 * begin ***
* Get allocated stock
  PERFORM get_allocated_stock.
*** MOD-001 * end ***

* Materials
  SELECT matnr bismt
         FROM mara
*         INTO CORRESPONDING FIELDS OF TABLE it_mara        "MOD-001
         INTO CORRESPONDING FIELDS OF TABLE it_marai        "MOD-001
         FOR ALL ENTRIES IN it_urst
         WHERE matnr = it_urst-matnr.
*** MOD-001 * begin ***
*  SORT it_mara.
*  DELETE ADJACENT DUPLICATES FROM it_mara.
  SORT it_marai.
  DELETE ADJACENT DUPLICATES FROM it_marai
         COMPARING matnr.
  it_mara[] = it_marai[].
  FREE it_marai[].
*** MOD-001 * end ***

* Build table for IDoc's
  LOOP AT it_stpl.
    DO 15 TIMES VARYING gv_werks FROM it_stpl-werks01 NEXT it_stpl-werks02.
      IF gv_werks IS INITIAL.
        EXIT.
      ENDIF.
      gv_index = sy-index.
*     Get "unrestricted stock"
      LOOP AT it_urst WHERE werks = gv_werks.
        CLEAR it_stock.
*       Sales organisation (back-end FAM)
        it_stock-orgid = it_stpl-famback.
*       Old material number
        CLEAR it_mara.
        READ TABLE it_mara
*                   WITH KEY matnr = it_urst-matnr          "MOD-001
                   WITH KEY matnr = it_urst-matnr.          "MOD-001
*                   BINARY SEARCH.                          "MOD-001
        IF sy-subrc = 0.
          it_stock-bismt = it_mara-bismt.
        ENDIF.
*       Plant
        it_stock-werks = it_urst-werks.
*       Available quantity
*** MOD-001 * begin ***
*        it_stock-aqty = it_urst-labst.
*       Calculate available stock
        PERFORM calc_avail_stock.
        it_stock-aqty = gv_avstock.
*** MOD-001 * end ***
        it_stock-auom = it_urst-meins.
*       Main plant
        IF gv_index = 1.

* LA lines have priority...take most recent date

          CLEAR lv_saveind.
          LOOP AT it_ekpo_n
               WHERE werks = it_urst-werks
                 AND matnr = it_urst-matnr
                 AND eindt GE sy-datum
                 AND lyntp = 'LA'.
            IF lv_saveind > it_ekpo_n-eindt OR lv_saveind IS INITIAL.
              it_stock-nqty  = it_ekpo_n-menge_ro.
              it_stock-nuom  = it_ekpo_n-meins.
              it_stock-ndate = it_ekpo_n-eindt.
              lv_saveind = it_ekpo_n-eindt.
            ENDIF.
          ENDLOOP.

* If not LA lines found-> go and look to AB lines or EKET
* ...take most recent date
          IF it_stock-nqty IS INITIAL OR it_stock-ndate IS INITIAL.
            CLEAR lv_saveind.
            LOOP AT it_ekpo_n
                 WHERE werks = it_urst-werks
                   AND matnr = it_urst-matnr
                   AND eindt GE sy-datum
                   AND lyntp <> 'LA'.
              IF lv_saveind > it_ekpo_n-eindt OR lv_saveind IS INITIAL.
                it_stock-nqty  = it_ekpo_n-menge_ro.
                it_stock-nuom  = it_ekpo_n-meins.
                it_stock-ndate = it_ekpo_n-eindt.
                lv_saveind = it_ekpo_n-eindt.
              ENDIF.
            ENDLOOP.
          ENDIF.

        ENDIF.

        APPEND it_stock.
      ENDLOOP.
    ENDDO.
  ENDLOOP.

  SORT it_stock.
ENDFORM.                    " Get_Detail

*&---------------------------------------------------------------------*
*&      Form  MATERIAL_SELECTION
*&---------------------------------------------------------------------*
*       Select materials depending on attribute 2
*----------------------------------------------------------------------*
FORM material_selection .

* Range for Sales organization
  SELECT vkorg INTO TABLE it_vkorg
               FROM yse_sd_acc_fam
*               WHERE famback IN s_famb.                    "MOD-001
               WHERE famback = p_famb.                      "MOD-001

  IF sy-subrc = 0.
    LOOP AT it_vkorg.
      r_vkorg-sign = 'I'.
      r_vkorg-option = 'EQ'.
      r_vkorg-low = it_vkorg-vkorg.
      APPEND r_vkorg.
    ENDLOOP.
*   Delete duplicates
    SORT r_vkorg.
    DELETE ADJACENT DUPLICATES FROM r_vkorg.
  ENDIF.

* Range for Distribution channel
  SELECT vtweg INTO TABLE it_vtweg
               FROM yse_sd_acc_fam
*               WHERE famback IN s_famb.                    "MOD-001
               WHERE famback = p_famb.                      "MOD-001

  IF sy-subrc = 0.
    LOOP AT it_vtweg.
      r_vtweg-sign = 'I'.
      r_vtweg-option = 'EQ'.
      r_vtweg-low = it_vtweg-vtweg.
      APPEND r_vtweg.
    ENDLOOP.
*   Delete duplicates
    SORT r_vtweg.
    DELETE ADJACENT DUPLICATES FROM r_vtweg.
  ENDIF.

* Select material attribute 2 (Pricing)
  SELECT matnr prat2
         FROM mvke
*         INTO CORRESPONDING FIELDS OF TABLE it_mvke        "MOD-001
         INTO CORRESPONDING FIELDS OF TABLE it_mvkei        "MOD-001
         FOR ALL ENTRIES IN it_urst
         WHERE matnr = it_urst-matnr
           AND vkorg IN r_vkorg
           AND vtweg IN r_vtweg.
*** MOD-001 * begin ***
*  SORT it_mvke.
*  DELETE ADJACENT DUPLICATES FROM it_mvke.
  SORT it_mvkei BY matnr.
  DELETE ADJACENT DUPLICATES FROM it_mvkei
         COMPARING matnr.
  it_mvke[] = it_mvkei[].
  FREE it_mvkei[].
*** MOD-001 * end ***

* Delete materials (according to attribute 2)
  SORT it_urst BY matnr.
  LOOP AT it_urst.
    CLEAR it_mvke.
    READ TABLE it_mvke
*               WITH KEY matnr = it_urst-matnr              "MOD-001
               WITH TABLE KEY matnr = it_urst-matnr.        "MOD-001
*               BINARY SEARCH.                              "MOD-001
    IF it_mvke-prat2 IS INITIAL.
      DELETE it_urst.
    ENDIF.
  ENDLOOP.

  SORT it_urst.

ENDFORM.                    " MATERIAL_SELECTION

*&---------------------------------------------------------------------*
*&      Form  SELECT_PO
*&---------------------------------------------------------------------*
*       Select PO's which are taken into account
*----------------------------------------------------------------------*
FORM select_po .

  LOOP AT it_neid.
*   Check PO document type
    SELECT SINGLE ebeln INTO gv_ebeln
           FROM ekko
           WHERE ebeln =  it_neid-ebeln
             AND bsart IN s_bsart.
    IF sy-subrc NE 0.
      DELETE it_neid.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " SELECT_PO

*&---------------------------------------------------------------------*
*&      Form  CALC_DATE
*&---------------------------------------------------------------------*
*       Calculate date according to factory calendar
*----------------------------------------------------------------------*
FORM calc_date .

  LOOP AT it_neid.

    AT NEW werks.
      SELECT SINGLE fabkl INTO gv_fabkl
             FROM t001w
             WHERE werks = it_neid-werks.
    ENDAT.

    IF it_neid-webaz NE 0.
      CALL FUNCTION 'WDKAL_DATE_ADD_FKDAYS'
        EXPORTING
          i_date        = it_neid-eindt
          i_fkday       = it_neid-webaz
          i_fabkl       = gv_fabkl
       IMPORTING
         e_date        = it_neid-eindt
*         E_FKDAY       =
       EXCEPTIONS
         error         = 1
         OTHERS        = 2
                .
      IF sy-subrc = 0.
        MODIFY it_neid.
      ELSE.
*        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
      ENDIF.
    ENDIF.

  ENDLOOP.

  SORT it_neid.

ENDFORM.                    " CALC_DATE

*&---------------------------------------------------------------------*
*&      Form  GET_ALLOCATED_STOCK                 "MOD-001
*&---------------------------------------------------------------------*
*       Get allocated stock
*----------------------------------------------------------------------*
FORM get_allocated_stock .

  CLEAR it_alloc[].

* Get open sales orders
  PERFORM get_open_sales_orders.

* Get open transport requisitions
  PERFORM get_open_transport_req.

* Get open transport orders
* Cf. Function get_allocated stock > perform find_open_transport_orders
  PERFORM get_open_transport_orders.

ENDFORM.                    " GET_ALLOCATED_STOCK

*&---------------------------------------------------------------------*
*&      Form  GET_OPEN_SALES_ORDERS               "MOD-001
*&---------------------------------------------------------------------*
*       Get open sales orders
*----------------------------------------------------------------------*
FORM get_open_sales_orders .

* Get open order item quantities
  SELECT vbeln posnr matnr werks lgort vbtyp vmeng
         INTO TABLE it_vbbe
         FROM vbbe
         FOR ALL ENTRIES IN it_urst
         WHERE matnr = it_urst-matnr
           AND werks = it_urst-werks
           AND ( lgort = it_urst-lgort  OR
                 lgort = ' ' )
           AND ( vbtyp = c_c  OR
               ( vbtyp = c_j
                 AND ( auart = c_lf    OR
                       auart = c_zlf   OR
                       auart = c_zl03  OR
                       auart = c_zlr1  OR
                       auart = c_zkt ) ) )
           AND mbdat LE gv_allocdate
           AND vmeng NE 0.

  CHECK NOT it_vbbe[] IS INITIAL.

  SORT it_vbbe BY matnr werks.

* Add order quantities to allocations table
  LOOP AT it_vbbe INTO wa_vbbe.

*   When the sales item's StLoc is initial, treat it is if it were
*   StLoc 1000
    IF wa_vbbe-lgort IS INITIAL.
      wa_vbbe-lgort = c_1000.
    ENDIF.
    it_alloc-werks = wa_vbbe-werks.
    it_alloc-matnr = wa_vbbe-matnr.
    it_alloc-lgort = wa_vbbe-lgort.
    it_alloc-quant = wa_vbbe-vmeng.
    COLLECT it_alloc.
  ENDLOOP.

  FREE: it_vbbe.

ENDFORM.                    " GET_OPEN_SALES_ORDERS

*&---------------------------------------------------------------------*
*&      Form  GET_OPEN_TRANSPORT_REQ              "MOD-001
*&---------------------------------------------------------------------*
*       Get open transport requisitions
*----------------------------------------------------------------------*
FORM get_open_transport_req .

* Only consider open transport req if storage location = 1000
  IF c_1000 IN s_lgort.
    SELECT reswk matnr menge
           INTO TABLE it_eban
           FROM eban
           FOR ALL ENTRIES IN it_urst
           WHERE reswk = it_urst-werks
             AND matnr = it_urst-matnr
             AND ( lgort = c_1000  OR
                   lgort = ' ' )
             AND frgdt LE gv_allocdate
             AND bsart = c_znb1
             AND pstyp = c_7            "Stock transfer
             AND statu = c_n
             AND ebakz = space
             AND loekz = space.

    SORT it_eban BY reswk matnr.

*   Add open transport req. quantities to allocations table
    LOOP AT it_eban.
      it_alloc-werks = it_eban-reswk.
      it_alloc-matnr = it_eban-matnr.
      it_alloc-lgort = c_1000.
      it_alloc-quant = it_eban-menge.
      COLLECT it_alloc.
    ENDLOOP.
  ENDIF.

  FREE it_eban.

ENDFORM.                    " GET_OPEN_TRANSPORT_REQ

*&---------------------------------------------------------------------*
*&      Form  GET_OPEN_TRANSPORT_ORDERS           "MOD-001
*&---------------------------------------------------------------------*
*       Get open transport orders
*----------------------------------------------------------------------*
FORM get_open_transport_orders .

* Only consider open transport orders if storage location = 1000
  IF c_1000 IN s_lgort.
*   Select from ekpo/ekko
    SELECT b~ebeln b~ebelp a~reswk b~matnr b~lgort b~menge
*           b~elikz b~erekz                                  "MOD-002
           INTO TABLE it_ekpo
           FROM ekko AS a
           INNER JOIN ekpo AS b
                   ON a~ebeln = b~ebeln
           FOR ALL ENTRIES IN it_urst
           WHERE a~reswk = it_urst-werks
             AND b~matnr = it_urst-matnr
             AND ( b~lgort = c_1000  OR
                   b~lgort = ' ' )
             AND b~zzeeind LE gv_allocdate
             AND a~bsart = c_zub1
             AND b~loekz = space.
*             AND ( b~elikz  NE 'X'   OR                     "MOD-002
*                   b~erekz  NE 'X' ).                       "MOD-002

*   Don't continue open transport orders if no rows returned
    CHECK sy-subrc EQ 0.

*   Select receipted quantities for the open transport order items
    SELECT  ebeln ebelp werks matnr menge belnr BUZEI bwart
           INTO TABLE it_ekbe
           FROM ekbe
           FOR ALL ENTRIES IN it_ekpo
           WHERE ebeln = it_ekpo-ebeln
             AND ebelp = it_ekpo-ebelp
             AND vgabe = c_6.

*   Add transport orders to allocation table
    LOOP AT it_ekpo.
      it_alloc-werks = it_ekpo-reswk.
      it_alloc-matnr = it_ekpo-matnr.
      it_alloc-lgort = c_1000.
      it_alloc-quant = it_ekpo-menge.
      COLLECT it_alloc.
    ENDLOOP.

*   Subtract receipted qty's from allocation table
    LOOP AT it_ekbe.
      it_alloc-werks = it_ekbe-werks.
      it_alloc-matnr = it_ekbe-matnr.
      it_alloc-lgort = c_1000.
      CASE it_ekbe-bwart.
        WHEN c_641.
          it_alloc-quant = it_ekbe-menge * ( -1 ).
        WHEN c_642.
          it_alloc-quant = it_ekbe-menge.
      ENDCASE.
      COLLECT it_alloc.
    ENDLOOP.
  ENDIF.

  FREE: it_ekpo, it_ekbe.

ENDFORM.                    " GET_OPEN_TRANSPORT_ORDERS

*&---------------------------------------------------------------------*
*&      Form  CALC_AVAIL_STOCK                    "MOD-001
*&---------------------------------------------------------------------*
*       Calculate available stock
*----------------------------------------------------------------------*
FORM calc_avail_stock .

* Unrestricted stock
  gv_avstock = it_urst-labst.

* Allocated stock
  READ TABLE it_alloc
             WITH TABLE KEY werks = it_urst-werks
                            matnr = it_urst-matnr
                            lgort = it_urst-lgort.
  IF sy-subrc EQ 0.
*   Available stock
    gv_avstock = gv_avstock - it_alloc-quant.
  ENDIF.

* No negative stock
  IF gv_avstock < 0.
    gv_avstock = 0.
  ENDIF.

ENDFORM.                    " CALC_AVAIL_STOCK

*&---------------------------------------------------------------------*
*&      Form  create_idocs_stock
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->MESSAGE_TYPE  text
*----------------------------------------------------------------------*
FORM create_idocs_stock  USING    message_type.
* Begin of insert MOD-003
  DATA:
    WA_PTOCK TYPE YSE_ACC_STCK_HIS,
    LT_PTOCK TYPE STANDARD TABLE OF YSE_ACC_STCK_HIS,
    LT_STOCK TYPE STANDARD TABLE OF t_stock.

  LT_STOCK[] = it_stock[].
  SORT LT_STOCK BY ORGID ASCENDING
                   WERKS ASCENDING
                   BISMT ASCENDING.
  DELETE ADJACENT DUPLICATES FROM LT_STOCK COMPARING ORGID
                                                     WERKS
                                                     BISMT.
  IF LT_STOCK IS NOT INITIAL.
    SELECT *
       FROM YSE_ACC_STCK_HIS
       INTO TABLE LT_PTOCK
        FOR ALL ENTRIES IN LT_STOCK
      WHERE ORGID = LT_STOCK-ORGID
        AND WERKS = LT_STOCK-WERKS
        AND BISMT = LT_STOCK-BISMT.
  ENDIF.

  IF LT_PTOCK is NOT INITIAL.
    SORT LT_PTOCK by ORGID ASCENDING
                     WERKS ASCENDING
                     BISMT ASCENDING.
  ENDIF.
* End of insert MOD-003

  CLEAR: created_idocs.
* FIND RECEIVING PARTNER
  SELECT SINGLE rcvprn INTO wa_edidc-rcvprn
         FROM edp13
         WHERE mestyp = c_mestyp_stck.
* Control Record
  wa_edidc-mestyp =  c_mestyp_stck.
  wa_edidc-idoctp =  c_idoc_type_stck.
  wa_edidc-rcvprt =  c_ls.

  LOOP AT it_stock INTO wa_stock.

* Begin of insert MOD-003
    CLEAR WA_PTOCK.
    READ TABLE LT_PTOCK INTO WA_PTOCK WITH KEY ORGID = wa_stock-ORGID
                                               WERKS = wa_stock-WERKS
                                               BISMT = wa_stock-BISMT.
    IF SY-SUBRC = 0.
      IF WA_PTOCK-AQTY = wa_stock-aqty.
        DELETE TABLE LT_PTOCK FROM WA_PTOCK.
        CONTINUE.
      ENDIF.
    ELSE.
      MOVE-CORRESPONDING wa_stock TO WA_PTOCK.
      WA_PTOCK-DATUM = SY-DATUM.
      WA_PTOCK-TIME  = SY-UZEIT.
      WA_PTOCK-NAME  = SY-UNAME.
      APPEND WA_PTOCK TO LT_PTOCK.
    ENDIF.
* End of insert MOD-003

    CLEAR i_edidd_data[].
    i_edidd_data-segnam  = c_segment_stck.
    i_edidd_data-sdata   = wa_stock.
    APPEND i_edidd_data.
    CLEAR i_edidd_data.

    IF NOT i_edidd_data[] IS INITIAL.
      CALL FUNCTION 'MASTER_IDOC_DISTRIBUTE'
        EXPORTING
          master_idoc_control            = wa_edidc
        TABLES
          communication_idoc_control     = i_edidc_control_comm
          master_idoc_data               = i_edidd_data
        EXCEPTIONS
          error_in_idoc_control          = 1
          error_writing_idoc_status      = 2
          error_in_idoc_data             = 3
          sending_logical_system_unknown = 4
          OTHERS                         = 5.

      IF sy-subrc <> 0.

      ELSE.
        created_idocs = created_idocs + 1.
      ENDIF.

      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.

      CALL FUNCTION 'EDI_DOCUMENT_DEQUEUE_LATER'
        EXPORTING
          docnum                 = i_edidc_control_comm-docnum
        EXCEPTIONS
          idoc_is_not_to_dequeue = 1
          OTHERS                 = 2.
    ENDIF.

* Begin of insert MOD-003
    IF WA_PTOCK-AQTY = wa_stock-AQTY.
      WA_PTOCK-DOCNUM = i_edidc_control_comm-docnum.
      MODIFY LT_PTOCK FROM WA_PTOCK TRANSPORTING DOCNUM
                                   WHERE  ORGID = wa_stock-ORGID
                                     AND  WERKS = wa_stock-WERKS
                                     AND  BISMT = wa_stock-BISMT.
    ELSE.
      WA_PTOCK-DOCNUM = i_edidc_control_comm-docnum.
      WA_PTOCK-AQTY   = wa_stock-AQTY.
      WA_PTOCK-DATUM  = SY-DATUM.
      WA_PTOCK-TIME   = SY-UZEIT.
      WA_PTOCK-NAME   = SY-UNAME.
      MODIFY LT_PTOCK FROM WA_PTOCK TRANSPORTING AQTY
                                                 DATUM
                                                 TIME
                                                 NAME
                                                 DOCNUM
                                    WHERE  ORGID = wa_stock-ORGID
                                      AND  WERKS = wa_stock-WERKS
                                      AND  BISMT = wa_stock-BISMT.
    ENDIF.
* End of insert MOD-003

  ENDLOOP.

* Begin of insert MOD-003
  IF LT_PTOCK IS NOT INITIAL.
    MODIFY YSE_ACC_STCK_HIS FROM TABLE LT_PTOCK.
  ENDIF.
* End of insert MOD-003
ENDFORM.                    " CREATE_IDOCS_STOCK

* Begin of insert MOD-002
FORM next_avail.

  SELECT b~ebeln b~ebelp b~werks b~matnr b~lgort b~menge b~meins
*           b~elikz b~erekz
           INTO TABLE it_ekpo_n
           FROM ekko AS a
           INNER JOIN ekpo AS b
                   ON a~ebeln = b~ebeln
           FOR ALL ENTRIES IN it_urst
           WHERE b~werks = it_urst-werks
             AND b~matnr = it_urst-matnr
             AND ( b~lgort = c_1000  OR
                   b~lgort = ' ' )
             AND a~bsart = c_znb1
             AND b~loekz = space
             AND ( b~elikz  NE 'X'   OR
                   b~erekz  NE 'X' ).


*  determine  received quantity
  CHECK NOT it_ekpo_n[] IS INITIAL.

  CLEAR it_eket[].
  SELECT ebeln ebelp wemng
  FROM eket
  INTO TABLE it_eket
  FOR ALL ENTRIES IN it_ekpo_n
  WHERE ebeln = it_ekpo_n-ebeln
  AND  ebelp  = it_ekpo_n-ebelp.

  SORT it_eket BY ebeln  ebelp.
*
  LOOP AT it_eket2 INTO wa_eket2.
    MOVE-CORRESPONDING wa_eket2 TO wa_eket.
    COLLECT wa_eket INTO it_eket.
  ENDLOOP.
*  FREE it_eket.

* determine invoiced quantity
  CLEAR it_ekbe_invqty[].
  SELECT ebeln ebelp zekkn vgabe gjahr belnr buzei menge shkzg
  INTO TABLE it_ekbe_invqty
   FROM ekbe
  FOR ALL ENTRIES IN  it_ekpo_n
  WHERE
           ebeln EQ  it_ekpo_n-ebeln
       AND ebelp EQ  it_ekpo_n-ebelp
  AND vgabe = '2'.                  " invoice receipt

  SORT  it_ekbe_invqty  BY  ebeln  ebelp.
*
  LOOP AT  it_ekbe_invqty   INTO  wa_ekbe_invqty.
    IF wa_ekbe_invqty-shkzg EQ 'H'.
      wa_ekbe_invqty-menge =  wa_ekbe_invqty-menge * -1.
    ENDIF.
    MOVE-CORRESPONDING wa_ekbe_invqty TO wa_ekbe_invqty_aggr.
    COLLECT wa_ekbe_invqty_aggr  INTO  it_ekbe_invqty_aggr.
  ENDLOOP.
*  FREE it_ekbe_invqty.


  LOOP AT it_ekpo_n.

    lv_index = sy-tabix.

*   get received quantity.
    CLEAR wa_eket.
    READ TABLE it_eket      WITH TABLE KEY  ebeln = it_ekpo_n-ebeln
                                           ebelp = it_ekpo_n-ebelp
                           INTO       wa_eket.

*   get invoiced  quantity.
    CLEAR wa_ekbe_invqty_aggr.
    READ TABLE it_ekbe_invqty_aggr WITH TABLE KEY  ebeln = it_ekpo_n-ebeln
                                                   ebelp = it_ekpo_n-ebelp
                                   INTO wa_ekbe_invqty_aggr.

* Closed for GR point of view
    IF ( wa_eket-wemng GE it_ekpo_n-menge )
    OR ( wa_ekbe_invqty_aggr-menge GE it_ekpo_n-menge ).
* closed
      DELETE it_ekpo_n INDEX lv_index.
      CONTINUE.
    ELSE.
* open qty
      it_ekpo_n-menge_io = it_ekpo_n-menge - wa_ekbe_invqty_aggr-menge.
      it_ekpo_n-menge_ro = it_ekpo_n-menge - wa_eket-wemng.
      MODIFY it_ekpo_n  INDEX lv_index TRANSPORTING menge_io menge_ro.
    ENDIF.

  ENDLOOP.


  LOOP AT it_ekpo_n.

    lv_index = sy-tabix.

    CLEAR: lv_vbeln, lv_eindt, lv_menge.
    CLEAR lv_typ.
* retrieve per order the delivery date
    SELECT  erdat vbeln eindt menge INTO (lv_erdat, lv_vbeln, lv_eindt, lv_menge)
       FROM ekes
       WHERE ebeln = it_ekpo_n-ebeln
        AND  ebelp = it_ekpo_n-ebelp
        AND  ebtyp = 'LA'
        ORDER BY ERDAT DESCENDING.

    ENDSELECT.
    IF lv_vbeln is not initial.
      lv_typ = 'LA'.
      SELECT SINGLE xblnr INTO lv_vbeln
       FROM ekbe
       WHERE ebeln = it_ekpo_n-ebeln
        AND  ebelp = it_ekpo_n-ebelp
        AND  xblnr = lv_vbeln.
      IF sy-subrc = 0.
        CLEAR lv_eindt.
        CLEAR lv_typ.
        CLEAR lv_menge.
      ENDIF.
    ENDIF.

    IF lv_eindt IS INITIAL.
      SELECT erdat eindt INTO (lv_erdat, lv_eindt)
        FROM ekes
        WHERE ebeln = it_ekpo_n-ebeln
         AND  ebelp = it_ekpo_n-ebelp
         AND  ebtyp = 'AB'
        ORDER BY ERDAT ASCENDING.
      ENDSELECT.
      IF lv_eindt IS NOT INITIAL.
        lv_typ = 'AB'.
      ENDIF.
    ENDIF.

    IF lv_eindt IS INITIAL.
      SELECT SINGLE eindt INTO lv_eindt
        FROM eket
        WHERE ebeln = it_ekpo_n-ebeln
         AND  ebelp = it_ekpo_n-ebelp.
    ENDIF.


    it_ekpo_n-lyntp = lv_typ.
    it_ekpo_n-eindt = lv_eindt.
    IF lv_menge IS NOT INITIAL.
      it_ekpo_n-menge = lv_menge.
      MODIFY it_ekpo_n INDEX lv_index TRANSPORTING eindt lyntp menge.
    ELSE.
      MODIFY it_ekpo_n INDEX lv_index TRANSPORTING eindt lyntp .
    ENDIF.
*    MOVE-CORRESPONDING it_ekpo_n TO it_neid.
*    it_neid-werks = it_ekpo-reswk.


*   APPEND it_neid.

  ENDLOOP.


ENDFORM.                    "next_avail

* End of insert MOD-002
*Text symbol text£º
*001:Selection parameters

*E01:No data found for the selected Back-end FAM's
*Selection text£º
*P_FAMB:        Back-end FAM
*P_MESS:        Message type
*P_PRAT2:        Product attribute 2
*S_BSART:D       .
*S_LGORT:D       .
*S_MATNR:D       .
*S_MTART:D       .
