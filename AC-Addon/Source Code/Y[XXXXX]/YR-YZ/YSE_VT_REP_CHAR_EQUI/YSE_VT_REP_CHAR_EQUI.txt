*&---------------------------------------------------------------------*
*& Report  YSE_VT_REP_CHAR_EQUI
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& Report with Characteristics for Equipments                          *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 09.03.2012
*  Change Request Number : CR2459
*  Transport request Nr. : CD1K970679
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME           | CORRECTION NR. | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-000 |            |                | CD1K......     | CR....      *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_vt_rep_char_equi.

TABLES: v_equi,
        crhd,
        ausp,
        cabnt.

* ALV grid
TYPE-POOLS: slis.

* Internal table with plants
DATA: BEGIN OF it_wrk  OCCURS 0,
        iwerk     TYPE iwerk,
      END OF it_wrk.

* Internal table with equipments
DATA: BEGIN OF it_equi  OCCURS 0,
        equnr     TYPE equnr,
        iwerk     TYPE iwerk,
        ingrp     TYPE ingrp,
        arbpl     TYPE arbpl,
        gewrk     TYPE gewrk,
        eqktx     TYPE ktx01,
      END OF it_equi.
DATA: wa_equi  LIKE LINE OF it_equi.

* Internal table with output lines
DATA: BEGIN OF it_out  OCCURS 0,
        equnr     TYPE equnr,
        eqktx     TYPE ktx01,
        iwerk     TYPE iwerk,
        ingrp     TYPE ingrp,
        arbpl     TYPE arbpl,
        gewrk     TYPE gewrk,
        chars     TYPE atbez,
        value     TYPE atwrt,
      END OF it_out.
DATA: wa_out  LIKE LINE OF it_out.

* Internal table with classificaton data
DATA: BEGIN OF it_clobjdat OCCURS 0.
        INCLUDE STRUCTURE clobjdat.
DATA: END OF it_clobjdat.

* Internal table with classes
DATA: BEGIN OF it_class OCCURS 0.
        INCLUDE STRUCTURE sclass.
DATA: END OF it_class.

DATA: xv_variant         LIKE disvariant,
      xv_variant_flag    TYPE c,
      xv_sd_alv_variant  LIKE disvariant,
      xt_fcat            TYPE slis_t_fieldcat_alv,
      ls_fcat            LIKE LINE OF xt_fcat,
      xt_alv_sort        TYPE slis_t_sortinfo_alv,
      ls_alv_sort        LIKE LINE OF xt_alv_sort,
      xv_user_command    TYPE slis_formname  VALUE 'USER_COMMAND',
      xv_variant_save    TYPE c              VALUE 'U',
      xv_layout          TYPE slis_layout_alv.

DATA: gv_object       TYPE objnum,
      gv_atinn        TYPE atinn,
      gv_col(10)      TYPE n,
      gv_repid        LIKE sy-repid.

CONSTANTS: c_atnam_level    TYPE atnam       VALUE 'ZAM_LEVELS',
           c_atnam_skils    TYPE atnam       VALUE 'ZAM_SKILL_SET',
           c_atnam_permi    TYPE atnam       VALUE 'ZAM_PERMITS',
           c_cltype_002     TYPE klassenart  VALUE '002',
           c_objtab_equi    TYPE tabelle     VALUE 'EQUI',
           c_99991231(8)    TYPE c           VALUE '99991231',
           c_e              TYPE xfeld       VALUE 'E',
           c_x              TYPE xfeld       VALUE 'X',
           c_z              TYPE xfeld       VALUE 'Z'.

* Selections
SELECTION-SCREEN  BEGIN OF BLOCK sel  WITH FRAME  TITLE text-s01.
SELECT-OPTIONS: so_iwerk  FOR v_equi-iwerk  OBLIGATORY
                              MEMORY ID iwk,
                so_ingrp  FOR v_equi-ingrp,
                so_equnr  FOR v_equi-equnr.
SELECTION-SCREEN SKIP.
PARAMETERS: p_level  AS CHECKBOX  DEFAULT 'X',
            p_skils  AS CHECKBOX  DEFAULT 'X',
            p_permi  AS CHECKBOX.
SELECTION-SCREEN SKIP.
PARAMETERS: p_fill   AS CHECKBOX.
SELECTION-SCREEN  END OF BLOCK sel.


*----------------------------------------------------------------------*
* On the selection screen                                              *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

  PERFORM  check_authorization.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

* Statistics for transaction / program
  CALL METHOD ycl_statistics=>record_transaction.

* Build & display list
  PERFORM select_data.
  PERFORM alv_fcat.
  PERFORM alv_layout.
  PERFORM alv_sort.
  PERFORM alv_display.


*&---------------------------------------------------------------------*
*&  Forms                                                              *
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  SELECT werks FROM t001w
               INTO TABLE it_wrk
               WHERE werks IN so_iwerk.

  LOOP AT it_wrk.
    AUTHORITY-CHECK OBJECT 'I_IWERK'
             ID 'TCD'   FIELD sy-tcode
             ID 'IWERK' FIELD it_wrk-iwerk.
    IF sy-subrc = 4.
*     No authorisation to display data
      MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '010' WITH it_wrk-iwerk.
      EXIT.
    ELSEIF sy-subrc <> 0.
*     Error checking authorization.
      MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
      EXIT.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

  REFRESH: it_equi,
           it_out.

* Select Equipments
  SELECT e~equnr e~iwerk e~ingrp w~arbpl e~gewrk e~eqktx
         INTO TABLE it_equi
         FROM v_equi AS e
         INNER JOIN crhd AS w
                    ON e~cr_objty = w~objty  AND
                       e~ppsid    = w~objid
         WHERE e~equnr IN so_equnr
           AND e~iwerk IN so_iwerk
           AND e~ingrp IN so_ingrp
           AND e~eqtyp =  c_z
           AND e~datbi =  c_99991231
           AND e~spras =  sy-langu.
  SORT it_equi.

  CHECK NOT it_equi[] IS INITIAL.

* Fill output table
  LOOP AT it_equi.

*   Get characteristics
    PERFORM get_characteristics.
    CHECK NOT it_clobjdat[] IS INITIAL.

*   Fill output line
    PERFORM fill_line.

  ENDLOOP.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  GET_CHARACTERISTICS
*&---------------------------------------------------------------------*
*       Get characteristics
*----------------------------------------------------------------------*
FORM get_characteristics .

  CLEAR it_clobjdat[].

  MOVE it_equi-equnr TO gv_object.

* Get equipment configuration
  CALL FUNCTION 'CLAF_CLASSIFICATION_OF_OBJECTS'
    EXPORTING
      classtype          = c_cltype_002
      object             = gv_object
      objecttable        = c_objtab_equi
    TABLES
      t_class            = it_class
      t_objectdata       = it_clobjdat
    EXCEPTIONS
      no_classification  = 1
      no_classtypes      = 2
      invalid_class_type = 3
      OTHERS             = 4.

  IF sy-subrc <> 0.
*    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

ENDFORM.                    " GET_CHARACTERISTICS

*&---------------------------------------------------------------------*
*&      Form  FILL_LINE
*&---------------------------------------------------------------------*
*       Fill output line
*----------------------------------------------------------------------*
FORM fill_line .

  CLEAR it_out.

* Equipment data
  MOVE-CORRESPONDING it_equi TO it_out.

* Characteristics
  LOOP AT it_clobjdat.
    CASE it_clobjdat-atnam.
      WHEN c_atnam_level.
        IF NOT p_level IS INITIAL.
          PERFORM fill_chars.
        ENDIF.
      WHEN c_atnam_skils.
        IF NOT p_skils IS INITIAL.
          PERFORM fill_chars.
        ENDIF.
      WHEN c_atnam_permi.
        IF NOT p_permi IS INITIAL.
          PERFORM fill_chars.
        ENDIF.
    ENDCASE.
  ENDLOOP.

ENDFORM.                    " FILL_LINE

*&---------------------------------------------------------------------*
*&      Form  FILL_CHARS
*&---------------------------------------------------------------------*
*       Fill line with Charactristic
*----------------------------------------------------------------------*
FORM fill_chars .

  IF NOT p_fill IS INITIAL.
    CHECK it_clobjdat-ausp1 CN ' ?'.
  ENDIF.
  it_out-chars = it_clobjdat-smbez.
  it_out-value = it_clobjdat-ausp1.
  APPEND it_out.

ENDFORM.                    " FILL_CHARS

*&---------------------------------------------------------------------*
*&      Form  ALV_FCAT
*&---------------------------------------------------------------------*
*       ALV field catalog
*----------------------------------------------------------------------*
FORM alv_fcat .
*
** Create Fieldcatalogue from internal table
** CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE' ...

  CLEAR gv_col.

* Field definitions
  PERFORM add_field USING 'EQUNR'  18  'Equipment'(t01)       'X'.
  PERFORM add_field USING 'EQKTX'  40  'Description'(t02)     ' '.
  PERFORM add_field USING 'IWERK'   6  'Plant'(t03)           ' '.
  PERFORM add_field USING 'INGRP'   6  'PlGrp'(t04)           ' '.
  PERFORM add_field USING 'ARBPL'  12  'Fixed Workctr'(t05)   ' '.
  PERFORM add_field USING 'GEWRK'  12  'Pref. Workctr'(t06)   ' '.
  PERFORM add_field USING 'CHARS'  30  'Characteristic'(t07)  ' '.
  PERFORM add_field USING 'VALUE'  30  'Value'(t08)           ' '.

ENDFORM.                    " ALV_FCAT

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr
                         p_hotsp.

  gv_col = gv_col + 1.

  CLEAR ls_fcat.
  ls_fcat-col_pos    = gv_col.
  ls_fcat-fieldname  = p_field.
  ls_fcat-outputlen  = p_len.
  ls_fcat-seltext_l  = p_descr.
  ls_fcat-seltext_m  = p_descr.
  ls_fcat-seltext_s  = p_descr.
  ls_fcat-fix_column = 'X'.
  ls_fcat-emphasize  = 'X'.
  ls_fcat-hotspot    = p_hotsp.

* Suppress leading zeros for the equipment number
  IF p_field = 'EQUNR'.
    ls_fcat-edit_mask = '==ALPHA'.
  ENDIF.

  APPEND ls_fcat TO xt_fcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT
*&---------------------------------------------------------------------*
*       Modify ALV layout
*----------------------------------------------------------------------*
FORM alv_layout .

  xv_layout-zebra = c_x.
*  xv_layout-colwidth_optimize = c_x.
*  xv_layout-smalltitle = 'X'.
*  xv_layout-grid_title = 'title'.

ENDFORM.                    " ALV_LAYOUT

*----------------------------------------------------------------------*
*       Form  ALV_SORT
*----------------------------------------------------------------------*
*       Sort ALV layout
*----------------------------------------------------------------------*
FORM alv_sort.

* Equipment
  ls_alv_sort-spos      = '1'.
  ls_alv_sort-fieldname = 'EQUNR'.
  ls_alv_sort-up        = 'X'.
  APPEND ls_alv_sort TO xt_alv_sort.
* Description
  ls_alv_sort-spos      = '1'.
  ls_alv_sort-fieldname = 'EQKTX'.
  ls_alv_sort-up        = 'X'.
  APPEND ls_alv_sort TO xt_alv_sort.

ENDFORM.                    " ALV_SORT

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       Display ALV grid
*----------------------------------------------------------------------*
FORM alv_display .

*  xv_sd_alv_variant = xv_variant.
  gv_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = gv_repid
      i_callback_user_command = xv_user_command
*      i_callback_top_of_page  = 'ALV_TOP'
      i_grid_title            = 'Equipment Characteristics'(h01)
      is_layout               = xv_layout
      it_fieldcat             = xt_fcat
      it_sort                 = xt_alv_sort
      i_default               = 'X'
      i_save                  = 'A'
*      is_variant              = xv_sd_alv_variant
      i_screen_start_column   = 0
      i_screen_start_line     = 0
      i_screen_end_column     = 0
      i_screen_end_line       = 0
    TABLES
      t_outtab                = it_out
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.

  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&   Form  USER_COMMAND
*&---------------------------------------------------------------------*
*    Process user command
*----------------------------------------------------------------------*
FORM user_command USING ucomm    LIKE sy-ucomm
                        selfield TYPE slis_selfield.

*-- Check function code
  CASE ucomm.
    WHEN '&IC1'.
*     Select hotspot
      IF NOT selfield-value IS INITIAL.
        CASE selfield-fieldname.
          WHEN 'EQUNR'.
*           Show Equipment
            CLEAR wa_out.
            READ TABLE it_out INDEX selfield-tabindex INTO wa_out.
            SET PARAMETER ID 'EQN' FIELD wa_out-equnr.
            CALL TRANSACTION 'IE03' AND SKIP FIRST SCREEN.
        ENDCASE.
      ENDIF.
  ENDCASE.

ENDFORM.                    " USER_COMMAND

*Text symbol text£º
*H01:Equipment Characteristics
*S01:Selections
*T01:Equipment
*T02:Description
*T03:Plant
*T04:PlGrp
*T05:Fixed Workctr
*T06:Pref. Workctr
*T07:Characteristic

*T08:Value
*Selection text£º
*P_FILL:        Only filled characteristics
*P_LEVEL:        Competence level
*P_PERMI:        Permits
*P_SKILS:        Skill Set
*SO_EQUNR:D       .
*SO_INGRP:D       .
*SO_IWERK:D       .
