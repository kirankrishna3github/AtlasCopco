report ytest_j_3rf_buy_book_03 no standard page heading.

* Global data definition
include j_3rf_bbk_inc_data.
* Field catalogue definition
include j_3rf_bbk_inc_fc1.
include j_3rf_bbk_inc_cl.
include j_3rf_bbk_inc_extract.
include j_3rf_bbk_xml_top.   " 1913657
include j_3rf_bbk_decl_top.  " VAT Declaration top include

* Selection screen definition
selection-screen function key 1.

selection-screen begin of block 0.
select-options:
  br_bukrs for bkpf-bukrs obligatory.
parameters:
  mainbuk like bkpf-bukrs.
select-options:
  br_belnr for bkpf-belnr,
  br_gjahr for bkpf-gjahr no-extension no intervals, " default sy-datum(4),
  br_budat for bkpf-budat,
  br_xblnr for bkpf-xblnr  no-display,
  sel_mona for bkpf-monat.
selection-screen end of block 0.

include j_3rf_bbk_decl_sel.   " 2120495 VAT Declaration options

selection-screen begin of block b3 with frame title text-901.
select-options:
  sel_ukrs for t007f-umkrs no-display,
  sel_bldt for bkpf-bldat,             "TAX RELEVANT DOC DATE
  sel_vtdt for bkpf-vatdate,           "Tax Reporting Date
  sel_mwkz for bset-mwskz,             "TAX CODE
  s_lifnr  for bseg-lifnr,             "VENDOR
  sel_blrt for bkpf-blart.             "DOC TYPES
selection-screen end of block b3.

selection-screen begin of block b1 with frame title text-023.
** 1695097 - Version 2012 (Decree 1137)
*PARAMETERS: ver2012 TYPE J_3RF_BK_2012 AS CHECKBOX DEFAULT 'X' user-command ucomm_ver.
** 2074991 - Statement N735 "On amendments being made to Regulation N1137"
*selection-screen begin of line.
*parameters: ver2014 TYPE J_3RF_BK_2014 AS CHECKBOX DEFAULT 'X' user-command ucomm_ver MODIF ID xbl.
*selection-screen comment 3(70) text-041 for field ver2014 MODIF ID xbl.
*selection-screen end of line.

* Use quarter instead of monthes
PARAMETERS: p_quart   type J_3RQUARTER."1178041
DATA: chk_ref   type j_3rf_bue_chk_ref,  " Check reference field - never used
      chk_gtd2  type c,                  " Look for CD in FI positions - not used
      par_sela  type c value 'X',        " ALV grid - used by default
      sel_opay  type j_3rf_bue_sel_opay, " Find real payment - used by default
      item_text type j_3rf_sgtxt.
* ALV Variant
parameters p_vari like disvariant-variant.
parameters s_adr type j_3rf_bue_s_adr as checkbox.

selection-screen begin of line.
* Exclude unposted taxes
parameters all_incl type j_3rf_bue_all_incl as checkbox.
selection-screen comment 3(54) for field all_incl.
* Output language RU
parameters par_cpag as checkbox.
selection-screen comment 60(18) for field par_cpag.
selection-screen end of line.
* Summarize tax transfers
parameters: par_col type j_3rf_bue_par_col as checkbox.
* CD and Country Code pattern
select-options par_fi for item_text no intervals.
* Transparent numeration
parameters:
  num_auto type j_3rf_bue_num_auto as checkbox default 'X'
                                   user-command ucomm_num,
  num_strt type j_3rf_bue_num_start,
  num_cret type j_3rf_bue_num_cret as checkbox,
  num_del  type j_3rf_bue_num_del  as checkbox,
  laufd    like j_3rbue_book_num-laufd,  " Run date
  laufi    like j_3rbue_book_num-laufi.  " Poisk znatschienii

parameters: par_ext type J_3RF_SGTXT_EXT_CHK as checkbox user-command ucomm_ext.
selection-screen begin of line.
  selection-screen comment 2(30) for field par_extn modif id ex1.
parameters:
* Pattern for External Number in FI documents
  par_extn type j_3rf_sgtxt_ext_num modif id ex1.
  selection-screen comment 38(35) t_extn modif id ex1.
selection-screen end of line.
selection-screen begin of line.
  selection-screen comment 2(30) for field par_extd modif id ex1.
parameters:
* Pattern for External Date in FI documents
  par_extd type j_3rf_sgtxt_ext_dat modif id ex1.
  selection-screen comment 38(35) t_extd modif id ex1.
selection-screen end of line.
* Print External Document Number from Text ID
selection-screen begin of line.
  selection-screen comment 2(30) for field par_extp modif id ex1.
  parameters: par_extp type j_3rf_print_ext_num modif id ex1.
selection-screen end of line.

* External Payment confirmation number and date
selection-screen begin of line.
  selection-screen comment 1(31) text-272 for field par_payn modif id ex2.
parameters:
  par_payn type j_3rf_bk_pay_num modif id ex2.
  selection-screen comment 38(35) t_payn modif id ex2.
selection-screen end of line.
selection-screen begin of line.
  selection-screen comment 1(31) text-274 for field par_payd modif id ex2.
parameters:
  par_payd type j_3rf_bk_pay_date modif id ex2.
  selection-screen comment 38(35) t_payd modif id ex2.
selection-screen end of line.

selection-screen end of block b1.

* Additional sheets selection
selection-screen begin of block add with frame title text-440.
* Additional sheets mode:
* - 0 - totals can be updated
* - 1 - totals cannot be updated
data p_mode.
* Create additional sheets
parameters p_add type j_3rf_bk_p_add as checkbox default 'X' user-command ucomm_add modif id ad2.
* Calculate totals
parameters p_sum type j_3rf_bk_p_sum as checkbox user-command ucomm_sum modif id ad3.
* Update totals
parameters p_update type j_3rf_bk_p_update as checkbox modif id ad1.
* Documents range
select-options ad_belnr for bkpf-belnr modif id ad.
* Fiscal year
select-options ad_gjahr for bkpf-gjahr modif id ad. " OBLIGATORY.
* Posting date
select-options ad_budat for bkpf-budat modif id ad.
* Tax reporting date
select-options ad_vtdat for bkpf-vatdate modif id ad.
* Closed fiscal period
select-options ad_monat for bkpf-monat modif id ad.
* Types of documents
select-options ad_blart for bkpf-blart modif id ad. " DEFAULT 'SA'.
* Additional sheet number
parameters ad_sheet type j_3rf_bk_ad_sheet default 1 modif id ad. " OBLIGATORY.
* Starting document number on additional sheet
parameters ad_doc_n type j_3rf_bk_ad_sheet_num default 1 modif id ad.
* Output date
parameters ad_date type sydatum default sy-datum modif id ad.
selection-screen end of block add.

************************************************************************
*                          EXTRACT
************************************************************************
selection-screen begin of block extract with frame title text-ext.
*
selection-screen begin of line.
parameters: p_noex type j_3rf_bue_p_noex radiobutton group extr.
selection-screen comment 3(50) for field p_noex.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment 3(29) for field p_ex1c.
parameters: p_ex1c like ltex-exname.
selection-screen comment 47(40) p_ext1c for field p_ex1c.
selection-screen end of line.

selection-screen begin of line.
parameters: p_save type j_3rf_bue_p_save radiobutton group extr.
selection-screen comment 3(50) for field p_save.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment 3(29) for field p_ex1.
parameters: p_ex1 like ltex-exname.
selection-screen end of line.
selection-screen begin of line.
selection-screen comment 3(29) for field p_ext1.
parameters: p_ext1 like ltex-text.
selection-screen end of line.

selection-screen begin of line.
parameters: p_load type j_3rf_bue_p_load radiobutton group extr.
selection-screen comment 3(50) for field p_load.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment 3(29) for field p_ex2.
parameters: p_ex2 like ltex-exname.
selection-screen comment 47(40) p_ext2 for field p_ex2.
selection-screen end of line.

selection-screen begin of line.
parameters: p_del type j_3rf_bue_p_del radiobutton group extr.
selection-screen comment 3(50) for field p_del.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment 3(29) for field p_ex3.
parameters: p_ex3 like ltex-exname.
selection-screen comment 47(40) p_ext3 for field p_ex3.
selection-screen end of line.

selection-screen begin of line.  " 2131494
parameters: p_merge type j_3rf_bk_merge_extract radiobutton group extr.
selection-screen comment 3(50) for field p_merge.
selection-screen end of line.

selection-screen begin of line.  " 2131494
selection-screen comment 3(26) for field p_ex_src.
select-options: p_ex_src for l_merge_extract no intervals.
selection-screen comment 70(20) for field p_ex_dst.
parameters: p_ex_dst like ltex-exname.
selection-screen end of line.

parameters: p_alv type j_3rf_bue_pa_alv radiobutton group rnt user-command ucomm_ext.
parameters: p_adobe type j_3rf_bue_p_adobe radiobutton group rnt.

selection-screen begin of line.
selection-screen comment 3(29) for field pa_adobe.
parameters: pa_adobe type fpwbformname default '*_PB*'.
selection-screen end of line.

include j_3rf_bbk_xml_sel.   " 1913657  XML options

selection-screen begin of block outputopt with frame title _outopt.

selection-screen begin of line.
parameters: p_xblnr  type J_3RF_BUE_REFNUM as checkbox.
selection-screen comment (29) for field p_xblnr.
selection-screen end of line.

selection-screen begin of line.
*selection-screen comment 62(1) co_dummy modif id xbl.
* external number from opposite book
parameters: p_xblnr1 type J_3RF_BUE_REFNUM as checkbox modif id xbl.
selection-screen comment (45) text-exl for field p_xblnr1 modif id xbl.
selection-screen end of line.

selection-screen begin of line.
*selection-screen comment 62(1) codummy2 modif id xbl.
* Internal Number for Outgoing Down Payments
parameters: p_xblnr2 type J_3RF_BUE_REFNUM_DP_OUT as checkbox modif id xbl.
selection-screen comment (45) text-exd for field p_xblnr2 modif id xbl.
selection-screen end of line.

parameters: p_tagged type abap_bool as checkbox.
parameters: p_lnggtd type J_3RF_BUY_LONG_GTD as checkbox.
parameters: p_actpay type J_3RF_BUY_ORIGPAY  as checkbox.

parameters: p_dp_bas type J_3RF_BK_DP_BAS as checkbox.
parameters: p_dp_add type J_3RF_BK_DP_ADD as checkbox.

parameters: p_extnum type J_3RF_BUY_LONG_NUM as checkbox. "N1362831

selection-screen end of block outputopt.

selection-screen end of block extract.

include j_3rf_bbk_xml_f01.   " 1913657
include j_3rf_bbk_xml_f02.   " 1913657
include j_3rf_bbk_xml_eve.   " 1913657
include j_3rf_bbk_decl_f01.  " VAT Declaration common routines
include j_3rf_bbk_decl_f03.  " VAT Declaration Purchase Ledger routines
include j_3rf_bbk_decl_eve.  " VAT Declaration events routines

************************************************************************
*     At selection-screen output
************************************************************************
at selection-screen output.

  perform check_before_output. " check fields before output
  perform check_decl_before_output.

  clear gv_xml_visible.
  loop at screen.
    if ( screen-group1 = 'AD' )." OR ( SCREEN-GROUP1 = 'AD1' ).
      if p_add = 'X'.
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    endif.
    if screen-group1 = 'AD1'.
      if p_sum = 'X' AND g_vat_decl_sel IS INITIAL.
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    endif.

    if screen-group1 = 'EX1'.
      if par_ext = 'X'.
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    endif.

*   1695097 - Decree 1137
    if screen-group1 = 'XBL'.
      if ver2012 = 'X'. " hide for decree 1137
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    elseif screen-group1 = 'EX2'.  "  2074991 - 735 statement
      if ver2012 = 'X' AND ver2014 = 'X'.
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    endif.
    if screen-name CS 'P_DP_BAS' OR
       screen-name CS 'P_DP_ADD'.
      if ver2012 = 'X'. " hide for decree 1137
        screen-active = '0'.
      else.
        screen-active = '1'.
      endif.
      modify screen.
    elseif screen-name CS 'NUM_STRT'.
      if num_auto = 'X'.
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    elseif screen-name CS 'NUM_CRET' OR
           screen-name CS 'NUM_DEL'  OR
           screen-name CS 'LAUFD'    OR
           screen-name CS 'LAUFI'.
      if num_auto = 'X'.
        screen-active = '0'.
      else.
        screen-active = '1'.
      endif.
      modify screen.
    endif.

*   XML functionality
    if screen-name CS 'P_XML'.
      if ver2012 = 'X' AND gv_xml_enabled CA 'XB'.
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    endif.
    if screen-group1 = 'XML'.
      if ver2012 = 'X' AND gv_xml_enabled CA 'XB' AND p_xml = 'X'.
        screen-active = '1'.
        gv_xml_visible = 'X'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    elseif screen-group1 = 'XMH'.
      if ver2012 = 'X' AND gv_xml_enabled = 'X' AND p_xml = 'X' AND
         ( g_vat_decl_cust = ' ' OR bk_vers < j3rdl_ver_decl_15 ). " hide for VAT Return
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    elseif screen-group1 = 'XMD'.
      if ver2012 = 'X' AND gv_xml_enabled CA 'XB' AND p_xml = 'X' AND
         ( p_s1_typ = gc_resp_type_auth OR
           p_s1_typ = gc_resp_type_account ).
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    elseif screen-group1 = 'VR1'.  " 2120495
      if gv_xml_enabled CA 'XB' AND g_vat_decl_flag = 'X'  AND
         bk_vers >= j3rdl_ver_decl_15.
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    elseif screen-group1 = 'VR2'.  " 2120495
      if gv_xml_enabled CA 'XB' AND g_vat_decl_flag = 'X'  AND
         g_vat_decl_sel = j3rdl_decl_type_corr.
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    elseif screen-group1 = 'VR3'.  " 2120495
      if gv_xml_enabled CA 'XB' AND g_vat_decl_flag = 'X'  AND
         p_xml = 'X' AND NOT g_vat_decl_sel IS INITIAL.
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    elseif screen-group1 = 'AD2'.  " 2120495
      if g_vat_decl_sel IS INITIAL.
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    elseif screen-group1 = 'AD3'.  " 2120495
      if p_add = 'X' AND g_vat_decl_sel <> gc_vat_decl_sel_main.
        screen-active = '1'.
      else.
        screen-active = '0'.
      endif.
      modify screen.
    endif.
  endloop.

  if sy-langu = 'R' or code_page <> '1500'.
    loop at screen.
      if screen-name = 'PAR_CPAG'.
        screen-active = '0'.
        modify screen.
      endif.
    endloop.
  endif.
************************************************************************
*      initialization.
************************************************************************

initialization.
* 1902051 - get global customizing for Sale/Purchase Ledgers
  CALL FUNCTION 'J_3RF_CUST_DOCLIST_DTI'
    IMPORTING
      e_value    = gc_customizing.

  call function 'BUILD_DEFAULT_YEAR'
    tables
      xgjahr = br_gjahr[].

* BAdI is defined for multiple use -> not exceptions to catch
* get badi ref.                           " 2070322
  CALL FUNCTION 'J_3RF_BADI_DOCLIST_DTI'  " 2070322 - get global BADI instance
    IMPORTING
      e_ref = ref.

* Info ABOUT
  sscrfields-functxt_01 = text-464.

  call function 'REUSE_ALV_EXTRACT_AT_INIT'
    changing
      cs_extract1 = gs_extract1
      cs_extract2 = gs_extract2.

  call function 'REUSE_ALV_EXTRACT_AT_INIT'
    changing
      cs_extract1 = gs_extract3
      cs_extract2 = gs_extract1c.

************************************************************************
*          Report's version
************************************************************************

  version_ = '00500'. date_ = '01.01.2005'. request_ = '0000000001'."

* Read info about Code Page set
  call function 'SCP_GET_CODEPAGE_NUMBER'
    exporting
      database_also  = 'X'
    importing
      appl_codepage  = code_page
    exceptions
      internal_error = 1
      others         = 2.

  perform variant_init.
  perform print_init.
  perform vat_decl_init using j3rdl_decl_section_pl
                              j3rdl_decl_section_pla.
  perform xml_init.

at selection-screen on value-request for p_vari.
  perform f4_for_variant.

************************************************************************
*             Selection check
************************************************************************
at selection-screen.

If not p_quart is initial." start 1178041
  if sel_mona-low > 4 or sel_mona-high > 4 or
     ad_monat-low > 4 or ad_monat-high > 4.
    message e070(9p).
  endif.
endif. "End 1178041

* LC283 -------------------------- *
  case sscrfields-ucomm.
    when 'FC01'.
      concatenate text-133 version_ text-134 date_ text-019 request_
      into ver1 separated by space.
      message: i899(f4) with ver1 text-132 'SAP'.
    when 'UCOMM_ADD'.
      if p_add eq space.
        p_sum = space.
        p_update = space.
      endif.
    when 'UCOMM_SUM'.
      if p_sum eq space.
        p_update = space.
      endif.
    when 'UCOMM_EXT'.
      " do nothing
    when others.
      perform check_selection_screen.
  endcase.

************************************************************************
*               Process on value request
************************************************************************

at selection-screen on value-request for mainbuk.
  describe table br_bukrs lines blines.

  refresh dynp_value_tab.

  dynp_value_tab-fieldname = 'BR_BUKRS-HIGH'.
  append dynp_value_tab.

  dynp_value_tab-fieldname = 'BR_BUKRS-LOW'.
  append dynp_value_tab.

  call function 'DYNP_VALUES_READ'
    exporting
      dyname               = syst-cprog
      dynumb               = syst-dynnr
    tables
      dynpfields           = dynp_value_tab
    exceptions
      invalid_abapworkarea = 04
      invalid_dynprofield  = 08
      invalid_dynproname   = 12
      invalid_dynpronummer = 16
      invalid_request      = 20
      no_fielddescription  = 24
      undefind_error       = 28.
  check sy-subrc = 0.

  loop at dynp_value_tab.
    case dynp_value_tab-fieldname.
      when 'BR_BUKRS-LOW'.
        br_bukrs-low = dynp_value_tab-fieldvalue.
      when 'BR_BUKRS-HIGH'.
        br_bukrs-high = dynp_value_tab-fieldvalue.
    endcase.
  endloop.
  translate br_bukrs-low to upper case.                  "#EC TRANSLANG
  translate br_bukrs-high to upper case.                 "#EC TRANSLANG
  if blines > 0 and  not br_bukrs-low is initial.
    if br_bukrs-high is initial and not br_bukrs-low is initial and
      br_bukrs-option = 'BT'.
      br_bukrs-option = 'EQ'.
    elseif not br_bukrs-high is initial and not br_bukrs-low is initial and
      br_bukrs-option = 'EQ'.
      br_bukrs-option = 'BT'.
    endif.
    modify br_bukrs index 1.
  elseif br_bukrs-low is initial.
    exit.
  else.
    if br_bukrs-high is initial and not br_bukrs-low is initial.
      br_bukrs-sign = 'I'.
      br_bukrs-option = 'EQ'.
    elseif not br_bukrs-high is initial and not br_bukrs-low is initial.
      br_bukrs-sign = 'I'.
      br_bukrs-option = 'BT'.
    endif.
    append br_bukrs.
  endif.
*
  perform f4_mainbuk.

at selection-screen on value-request for p_ex1.

  call function 'REUSE_ALV_EXTRACT_AT_F4_P_EX1'
    changing
      c_p_ex1     = p_ex1
      c_p_ext1    = p_ext1
      cs_extract1 = gs_extract1.

at selection-screen on value-request for p_ex2.

  call function 'REUSE_ALV_EXTRACT_AT_F4_P_EX2'
    changing
      c_p_ex2     = p_ex2
      c_p_ext2    = p_ext2
      cs_extract2 = gs_extract2.

at selection-screen on value-request for p_ex3.

  data: ls_extract_sav like disextract.
  data: l_exit type c.
  data: cs_extract3 like disextract.
  cs_extract3-exname = p_ex3.

  call function 'REUSE_ALV_EXTRACT_F4'
    exporting
      is_extract = gs_extract3
    importing
      es_extract = ls_extract_sav
      e_exit     = l_exit
    exceptions
      not_found  = 2
      others     = 2.

  if sy-subrc = 0 and l_exit is initial.
    p_ext3 = ls_extract_sav-exname.
    gs_extract3 = ls_extract_sav.
    perform text_update using 'P_EXT3'
                            gs_extract3-text.
  elseif sy-subrc ne 0.
    message id sy-msgid type sy-msgty number sy-msgno
            with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  endif.
  p_ex3 = gs_extract3-exname.


************************************************************************
*                     Main
************************************************************************
start-of-selection.
*
  PERFORM check_decl_flags.
  PERFORM pl_check_decl_start USING space.

if not p_quart is initial.
DATA main_quart type bkpf-monat."quarter number
DATA main_quart_hi type bkpf-monat."quarter number
DATA add_quart type bkpf-monat."quarter number
DATA add_quart_r like ad_monat occurs 0.
add_quart_r[] = ad_monat[]. "read from totals table
main_quart = sel_mona-low.
add_quart  = ad_monat-low.
if sel_mona-option = 'BT'. "range
  main_quart_hi = sel_mona-high.
endif.
* convert quarter to monthes only for fiscal year
* equal to calender year !!!
if sel_mona-high is initial.
 if sel_mona-low = '01'.
   sel_mona-high = '03'.
 else.
   sel_mona-high = sel_mona-low * 3.
   sel_mona-low  = ( sel_mona-low - 1 ) * 3 + 1.
 endif.
else. "sel_mona-high <> initial
 if not sel_mona-low = '01'.
   sel_mona-low  = ( sel_mona-low - 1 ) * 3 + 1.
 endif.
   sel_mona-high = sel_mona-high * 3.
 endif.
sel_mona-option = 'BT'. "range
modify sel_mona from sel_mona index 1.
* The same convertion for additiona sheet
if not p_add is initial.
if ad_monat-high is initial.
 if ad_monat-low = '01'.
   ad_monat-high = '03'.
 else.
   ad_monat-high = ad_monat-low  * 3.
   ad_monat-low  = ( ad_monat-low - 1 ) * 3 + 1.
 endif.
else. "ad_monat-high <> initial
 if not ad_monat-low = '01'.
   ad_monat-low  = ( ad_monat-low - 1 ) * 3 + 1.
 endif.
   ad_monat-high = ad_monat-high * 3.
 endif.
ad_monat-option = 'BT'.
modify ad_monat  from ad_monat index 1.
endif. "p_add
endif.
* Fill in numbering buffer
*  SELECT * FROM j_3rbue_book_num INTO TABLE itab_book_num
*    WHERE bukrs in br_bukrs AND gjahr = br_gjahr-low.
*
  data save_br_belnr like standard table of br_belnr with header line.
  save_br_belnr[] = br_belnr[].

* 1695097 - Version 2012 (Decree 1137)
  if ver2012 = 'X'.
    p_dp_bas = 'X'.
    p_dp_add = 'X'.
  else.
    clear ver2014.  " 2074991
  endif.

* 1743956 - get global customizing for Sale/Purchase Ledgers
  CALL FUNCTION 'J_3RF_CUST_DOCLIST_DTI'
    IMPORTING
      e_value    = gc_customizing.

* 2074991 - fill global params
  CLEAR gc_params.
  IF par_ext = 'X'.
    gc_params-print_ext = par_ext.
    gc_params-ext_numid = par_extn.
    gc_params-ext_datid = par_extd.
  ENDIF.
  gc_params-pay_numid = par_payn.  " 2085792
  gc_params-pay_datid = par_payd.
  gc_params-ru_addr = s_adr.

* 1804359 init class
  lcl_buy_book=>g_par_col = par_col.
  IF p_adobe = 'X' or p_xml = 'X'.
    lcl_buy_book=>g_adobe_extnum = p_extnum.
  ENDIF.

* 1909868 - check Text ID options
  IF par_ext  IS INITIAL OR
     par_extn IS INITIAL.
    CLEAR par_extp.
  ENDIF.
  lcl_buy_book=>g_ext_prn = par_extp.

* If load extract
  if p_load = 'X'.
* Data should be selected from extract
    perform view_extract using gs_extract2 changing p_book[] gs_totals.
  elseif p_merge = 'X'.  " 2131494
    perform pl_merge_extracts.
  elseif p_noex = 'X' or p_save = 'X'.
*----------------------------------------------------------------------*
* Generate data based on selection options
*----------------------------------------------------------------------*
    perform main_processing
      changing
        gt_bkpf
        gt_bseg
        gt_doclist
        gt_totals
        p_book[].
*----------------------------------------------------------------------*
*  Post additional values
*----------------------------------------------------------------------*
* Get CD (GTD) and Country Code from Purchase Order
    data l_long_gtd type string.
    perform mm_belnr_old using gt_bkpf gt_bseg changing p_book[] l_long_gtd.
    if not par_fi is initial.
* CD Number in position of FI document
      perform add_fi_gtd using gt_bseg changing p_book[].
    endif.

* Replace external number and date with pattern from FI document
    if par_ext EQ 'X' and
       ( not par_extn is initial or
         not par_extd is initial ).
      perform add_fi_xblnr
        using    par_extn
                 par_extd
        changing p_book[].
    endif.

    free: gt_bkpf, gt_bseg, gt_doclist.

*   2120495 - filter data for VAT Return
    IF NOT g_vat_decl_sel IS INITIAL.
      PERFORM pl_process_data CHANGING p_book[].
    ENDIF.

    if flg_ok_records = 'X' AND
       num_auto is initial.  " 1695097
      perform sort_fields_by_variant using p_book[].
      perform add_transparent_number.
    endif.

    if num_auto is initial. " 1695097
* table J_3RBUE_BOOK_NUM
    loop at p_book where num_line = space.
      if p_book-belnr_add is initial and
         p_book-belnr_trn is initial. "N1326572: 0% tax
        select belnr_transp into p_book-num_line from j_3rbue_book_num
          where bukrs = p_book-bukrs and
                belnr = p_book-belnr_inv and
                gjahr = p_book-gjahr_inv and
                buzei = p_book-buzei_test.
        endselect.
      elseif p_book-belnr_add is initial.
        select belnr_transp into p_book-num_line from j_3rbue_book_num
          where bukrs = p_book-bukrs and
                belnr = p_book-belnr_trn and
                gjahr = p_book-gjahr_trn and
                buzei = p_book-buzei_test.
        endselect.
      else.
*        N1334957 - sequential numbering
*        select belnr_transp into p_book-num_line from j_3rbue_book_num
*          where bukrs = p_book-bukrs and
*                belnr = p_book-belnr_add and
*                gjahr = p_book-gjahr_add and
*                buzei = p_book-buzei_add.
*        endselect.
        sy-subrc = 4.
      endif.
      if sy-subrc = 0.
        modify p_book transporting num_line.
      endif.
    endloop.
    endif.

*   N1334957 - sequential numbering for additional sheets
    data: num_line_add(10) type n.
    field-symbols: <fs_book> type j_3rbuy.

*   1695097 - auto increment basic sheet
    if num_auto = 'X'.
      num_line_add = num_strt.
      if num_line_add is initial.
        num_line_add = 1.
      endif.
      perform sort_fields_by_variant using p_book[].
      loop at p_book assigning <fs_book>
                     where belnr_add is initial.
        <fs_book>-num_line = num_line_add.
        num_line_add = num_line_add + 1.
      endloop.
    endif.

    num_line_add = ad_doc_n.  "N1472064
    if num_line_add is initial.
      num_line_add = 1.
    endif.
    loop at p_book assigning <fs_book>
        where not belnr_add is initial.
      <fs_book>-num_line = num_line_add.
      num_line_add       = num_line_add + 1.
    endloop.

*----------------------------------------------------------------------*
    if flg_ok_records <> 'X' AND
       num_auto is initial.  " 1695097
      perform sort_fields_by_variant using p_book[].
    endif.

    if p_add = 'X' and p_sum = 'X'. " additional sheets analysis
      perform get_incoming_totals changing gt_totals gs_totals.
    endif. " p_add = 'X' AND p_sum = 'X'.

*   2120495 - additional sheets for VAT Return
    if g_vat_decl_sel = gc_vat_decl_sel_corr AND p_sum = 'X'.
      perform pl_decl_incoming_totals changing gt_totals gs_totals.
    endif. " 2120495

  endif. " p_load <> 'X'
*----------------------------------------------------------------------*
* Create_EXTRACT
*----------------------------------------------------------------------*
end-of-selection.
  if p_save = 'X'.
* Save data to extract
    perform create_extract using gs_extract1 p_book[] gs_totals.
  elseif p_del = 'X'.
* Delete extract
    perform delete_extract using gs_extract3.
  elseif p_merge = 'X'.  " 2131494
    exit. " message already shown
  else.
* START ALV OR ADOBE OUTPUT
    perform compress_data.
* Call BAdI
    if not ref is initial.
      perform change_values changing p_book_alv1[].
    endif.
    if p_alv = 'X'.
*     extracts comparison
      if p_noex = 'X' and not p_ex1c is initial.
        perform pl_compare_extract.
      endif.
      perform init_alv_no_cycle.
*     for Correction VAT Declaration select additional sheet by default
      if g_vat_decl_sel = gc_vat_decl_sel_corr.
        g_add_mode = 'X'.
      endif.
      perform call_alv_no_cycle.
    else.
      perform print_legal_form.
    endif.
  endif.                                              "extr was building

*&--------------------------------------------------------------------*
*&      Form  compress_gtd
*&--------------------------------------------------------------------*
*       text
*---------------------------------------------------------------------*
form compress_gtd.
  data: full_lines type i,
        comp_lines type i.

  data: tab like sy-tabix.

  data: wa_book type j_3rbuy.

  describe table p_book lines full_lines.
  describe table p_book_alv1 lines comp_lines.
  if full_lines > comp_lines.
    loop at p_book_alv1.
      tab = sy-tabix.
      loop at p_book into wa_book where
        bukrs = p_book_alv1-bukrs and
        belnr_inv = p_book_alv1-belnr_inv and
        xblnr_inv = p_book_alv1-xblnr_inv and
        bldat_inv = p_book_alv1-bldat_inv  and
        budat_inv = p_book_alv1-budat_inv and
        dmbtr_inv = p_book_alv1-dmbtr_inv and
        dmbtr_wrs_inv = p_book_alv1-dmbtr_wrs_inv and
        wrbtr_inv = p_book_alv1-wrbtr_inv and
        wrbtr_wrs_inv = p_book_alv1-wrbtr_wrs_inv  and
        bktxt_inv = p_book_alv1-bktxt_inv  and
        belnr_pay = p_book_alv1-belnr_pay  and
        xblnr_pay = p_book_alv1-xblnr_pay and
        bldat_pay = p_book_alv1-bldat_pay and
        budat_pay = p_book_alv1-budat_pay  and
        dmbtr_wrs_pay = p_book_alv1-dmbtr_wrs_pay  and
        wrbtr_wrs_pay = p_book_alv1-wrbtr_wrs_pay  and
        bktxt_pay = p_book_alv1-bktxt_pay  and
        belnr_origpay = p_book_alv1-belnr_origpay and
        gjahr_origpay = p_book_alv1-gjahr_origpay and
        bldat_origpay = p_book_alv1-bldat_origpay and
        budat_origpay = p_book_alv1-budat_origpay and
        xblnr_origpay = p_book_alv1-xblnr_origpay and
        bktxt_origpay = p_book_alv1-bktxt_origpay and
        bldat_trn = p_book_alv1-bldat_trn and
        budat_trn = p_book_alv1-budat_trn and
        monat_trn = p_book_alv1-monat_trn and
        dmbtr_wrs_trn = p_book_alv1-dmbtr_wrs_trn and
        wrbtr_wrs_trn = p_book_alv1-wrbtr_wrs_trn and
        hwbas_wrs_trn = p_book_alv1-hwbas_wrs_trn and
        fwbas_wrs_trn = p_book_alv1-fwbas_wrs_trn and
        hwbas2_wrs_trn = p_book_alv1-hwbas2_wrs_trn and
        fwbas2_wrs_trn = p_book_alv1-fwbas2_wrs_trn and
        hwbas3_wrs_trn = p_book_alv1-hwbas3_wrs_trn and
        fwbas3_wrs_trn = p_book_alv1-fwbas3_wrs_trn and
        hwbas4_wrs_trn = p_book_alv1-hwbas4_wrs_trn and
        fwbas4_wrs_trn = p_book_alv1-fwbas4_wrs_trn and
        hwbas5_wrs_trn = p_book_alv1-hwbas5_wrs_trn and
        fwbas5_wrs_trn = p_book_alv1-fwbas5_wrs_trn and
        hwste_wrs_trn = p_book_alv1-hwste_wrs_trn and
        fwste_wrs_trn = p_book_alv1-fwste_wrs_trn and
        hwste2_wrs_trn = p_book_alv1-hwste2_wrs_trn and
        fwste2_wrs_trn = p_book_alv1-fwste2_wrs_trn and
        hwste3_wrs_trn = p_book_alv1-hwste3_wrs_trn and
        fwste3_wrs_trn = p_book_alv1-fwste3_wrs_trn and
        hwste5_wrs_trn = p_book_alv1-hwste5_wrs_trn and
        fwste5_wrs_trn = p_book_alv1-fwste5_wrs_trn and
        gjahr_test = p_book_alv1-gjahr_test and
*        gjahr_test = p_book_alv1-gjahr_test AND
        name1_cred = p_book_alv1-name1_cred and
        name2_cred = p_book_alv1-name2_cred and
        stcd1_cred = p_book_alv1-stcd1_cred and
        stcd3_cred = p_book_alv1-stcd3_cred and
        vatdate_inv = p_book_alv1-vatdate_inv and
        vatdate_trn = p_book_alv1-vatdate_trn and
        part_payment = p_book_alv1-part_payment.

        search p_book_alv1-mm_inv_vornu for wa_book-mm_inv_vornu.
        if sy-subrc ne 0.
          concatenate p_book_alv1-mm_inv_vornu wa_book-mm_inv_vornu
          into p_book_alv1-mm_inv_vornu.
*          p_book_alv1-reserv = STRLEN( p_book_alv1-mm_inv_vornu ).
        endif.
        if wa_book-mm_inv_voiso ne space.
          p_book_alv1-mm_inv_voiso = wa_book-mm_inv_voiso.
        endif.
        modify p_book_alv1 index tab.
      endloop.
    endloop.
  endif.
endform.                    "compress_gtd

*----------------------------------------------------------------------*
*       FORM PRINT_FC (print of actual catalogue of fields)
*----------------------------------------------------------------------*
form print_fc.

  data: fieldcat  type slis_t_fieldcat_alv,
        wa_fieldcat type slis_fieldcat_alv.

  data: begin of itab_fieldcat occurs 0.
          include structure wa_fieldcat.
  data: end of itab_fieldcat.
* FIELDNAME
  wa_fieldcat-fieldname    = 'FIELDNAME'.
  wa_fieldcat-seltext_l    = text-800.
  wa_fieldcat-seltext_m    =  wa_fieldcat-seltext_l.
  wa_fieldcat-seltext_s    =  wa_fieldcat-seltext_l.
  wa_fieldcat-reptext_ddic =  wa_fieldcat-seltext_l.
  wa_fieldcat-outputlen    =  '15'.
  append wa_fieldcat to fieldcat.
* SELTEXT_L
  wa_fieldcat-fieldname    = 'SELTEXT_L'.
  wa_fieldcat-seltext_l    = text-801.
  wa_fieldcat-seltext_m    =  wa_fieldcat-seltext_l.
  wa_fieldcat-seltext_s    =  wa_fieldcat-seltext_l.
  wa_fieldcat-reptext_ddic =  wa_fieldcat-seltext_l.
  append wa_fieldcat to fieldcat.
* REPTEXT_DDIC
  wa_fieldcat-fieldname    = 'REPTEXT_DDIC'.
  wa_fieldcat-seltext_l    = text-802.
  wa_fieldcat-seltext_m    =  wa_fieldcat-seltext_l.
  wa_fieldcat-seltext_s    =  wa_fieldcat-seltext_l.
  wa_fieldcat-reptext_ddic =  wa_fieldcat-seltext_l.
  wa_fieldcat-ddictxt      = 'L'.
  wa_fieldcat-outputlen    =  '45'.
  append wa_fieldcat to fieldcat.

*
  data: fnc_excluding type slis_t_extab with header line.
  fnc_excluding-fcode = '&NT1'.
  append fnc_excluding.
*
  itab_fieldcat[] = gt_fieldcat[].
  delete itab_fieldcat where tech = 'X'.

  call function 'REUSE_ALV_POPUP_TO_SELECT'
    exporting
      i_title          = text-803
      i_tabname        = '1'
      i_structure_name = 'ITAB_FIELDCAT'
      it_fieldcat      = fieldcat[]
      it_excluding     = fnc_excluding[]
    tables
      t_outtab         = itab_fieldcat
    exceptions
      program_error    = 1
      others           = 2.
  if sy-subrc <> 0.
    message id sy-msgid type sy-msgty number sy-msgno
          with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  endif.
endform.                    "print_fc
*----------------------------------------------------------------------*
*       FORM VARIANT_INIT
*----------------------------------------------------------------------*
form variant_init.
  clear g_variant.
  g_save               = 'A'.
  g_repid              = sy-repid.
  g_variant-report     = g_repid.
  gx_variant           = g_variant.
  call function 'REUSE_ALV_VARIANT_DEFAULT_GET'
    exporting
      i_save     = g_save
    changing
      cs_variant = gx_variant
    exceptions
      not_found  = 2.
  if sy-subrc = 0.
    p_vari = gx_variant-variant.
  endif.
endform.                                                  " VARIANT_INIT
*&---------------------------------------------------------------------*
*&      Form  end_of_page
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form end_of_page.

  write: / text-018, sy-pagno.

endform.                    "end_of_page

*----------------------------------------------------------------------*
*       FORM E05_LAYOUT_BUILD
*----------------------------------------------------------------------*
form e05_layout_build using e05_ls_layout type slis_layout_alv.
  e05_ls_layout-f2code            = '&ETA'.
  e05_ls_layout-lights_fieldname  = 'LIGHTS'.
  e05_ls_layout-info_fieldname    = 'COL22'.
  e05_ls_layout-coltab_fieldname  = space.
  e05_ls_layout-lights_condense   = 'X'.
  e05_ls_layout-detail_popup      = 'X'.
  e05_ls_layout-reprep            = 'X'.
  e05_ls_layout-group_change_edit = 'X'.
  e05_ls_layout-get_selinfos      = 'X'.
endform.                                               "e05_layout_build
*----------------------------------------------------------------------*
*       FORM F4_FOR_VARIANT
*----------------------------------------------------------------------*
form f4_for_variant.
  call function 'REUSE_ALV_VARIANT_F4'
    exporting
      is_variant = g_variant
      i_save     = g_save
    importing
      e_exit     = g_exit
      es_variant = gx_variant
    exceptions
      not_found  = 2.
  if sy-subrc = 2.
    message id sy-msgid type 'S'
    number sy-msgno with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  else.
    if g_exit = space.
      p_vari = gx_variant-variant.
    endif.
  endif.
endform.                                                 "f4_for_variant
*----------------------------------------------------------------------*
*       FORM PAI_OF_SELECTION_SCREEN
*----------------------------------------------------------------------*
form pai_of_selection_screen.
*
  if not p_vari is initial.
    move g_variant to gx_variant.
    move p_vari to gx_variant-variant.
    call function 'REUSE_ALV_VARIANT_EXISTENCE'
      exporting
        i_save      = g_save
      changing
        cs_variant  = gx_variant
      exceptions
        wrong_input = 1
        not_found   = 2
        others      = 3.
    if sy-subrc = 0.
      g_variant = gx_variant.
    else.
      message id sy-msgid type 'W' number sy-msgno
              with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      clear p_vari.
    endif.
  endif.
endform.                                       " PAI_OF_SELECTION_SCREEN
*----------------------------------------------------------------------*
*       FORM EVENTS_INIT (TOP(END)_OF_PAGE, TOP(END)_OF_LIST)
*----------------------------------------------------------------------*
form events_init using rt_events type slis_t_event.
  data: ls_events type slis_alv_event.

  clear ls_events.
  ls_events-name = 'END_OF_PAGE'.
  ls_events-form = 'END_OF_PAGE'.
  append ls_events to rt_events.

  clear ls_events.
  ls_events-name = 'TOP_OF_LIST'.
  ls_events-form = 'TOP_OF_LIST'.
  append ls_events to rt_events.

  clear ls_events.
  ls_events-name = 'END_OF_LIST'.
  ls_events-form = 'END_OF_LIST'.
  append ls_events to rt_events.

endform.                    "events_init
*---------------------------------------------------------------------*
*       FORM HTML_TOP_OF_PAGE                                         *
*---------------------------------------------------------------------*
*       Writes selection parameters                                   *
*---------------------------------------------------------------------*
form html_top_of_page using cl_dd type ref to cl_dd_document."#EC CALLED

  data: lt_texts type sdydo_text_table,
        ls_texts type sdydo_text_element,
          paval_tmp like t001z-paval,
          stcd3_tmp like t001z-paval,
        name1_sel like addr1_val-name1,
        name2_sel like addr1_val-name2,
        name3_sel like addr1_val-name3,
        name4_sel like addr1_val-name4,
        name1_sel_r like addr1_val-name1,
        name2_sel_r like addr1_val-name2,
        name3_sel_r like addr1_val-name3,
        name4_sel_r like addr1_val-name4,
        date_from(10)   type c,
        date_to(10)     type c,
        fiscal_year(4)  type c,
        date_output(10) type c,
        monat_low       type bkpf-monat,
        monat_high      type bkpf-monat,
        l_monat_low     type bkpf-monat,           " 2056387
        l_monat_high    type bkpf-monat,           " 2056387
        ls_add_quart_r  like line of ad_monat[],   " 2056387
        l_gjahr         type bkpf-gjahr,
        l_budat_begin   type bkpf-budat,
        l_budat_end     type bkpf-budat.

  data: table_element type ref to cl_dd_table_element,
        column_1      type ref to cl_dd_area.

  refresh: lt_header.

  call method cl_dd->add_table
    exporting
      no_of_columns = 1
      border        = '0'
      width         = '100%'
    importing
      table         = table_element.

  call method table_element->add_column
    importing
      column = column_1.

  call method table_element->set_column_style
    exporting
      col_no       = 1
      sap_align    = 'RIGHT'
      sap_fontsize = cl_dd_document=>small.

  if ver2012 is initial.
  if g_add_mode eq space.
    append text-002 to lt_texts.
  else.
    append text-009 to lt_texts.
  endif.
  append text-003 to lt_texts.
  append text-004 to lt_texts.
  append text-005 to lt_texts.
  else.
    if g_add_mode eq space.
      append text-030 to lt_texts.
      append text-031 to lt_texts.
      append text-032 to lt_texts.
      append text-033 to lt_texts.
      if ver2014 = 'X'. " 2074991
        append text-042 to lt_texts.
      endif. " 2074991
    else.
      append text-034 to lt_texts.
      append text-035 to lt_texts.
      if ver2014 = space. " 2074991
        append text-039 to lt_texts. " 2005330
      else.
        append text-042 to lt_texts.
      endif. " 2074991
    endif.
  endif.

  call method column_1->add_text
    exporting
      text       = space
      text_table = lt_texts
      fix_lines  = 'X'.

  perform select_addr using name1_sel
                            name2_sel
                            name3_sel
                            name4_sel
                            name1_sel_r
                            name2_sel_r
                            name3_sel_r
                            name4_sel_r
                              mainbuk.

* INN
  select single paval into paval_tmp from t001z
  where bukrs = mainbuk and party = 'SAPR01'.
* KPP
  select single paval into stcd3_tmp from t001z
  where bukrs = mainbuk and party = 'SAPR10'.

  call method cl_dd->add_table
    exporting
      no_of_columns = 1
      border        = '0'
      width         = '100%'
    importing
      table         = table_element.

  call method table_element->add_column
    importing
      column = column_1.

  call method table_element->set_column_style
    exporting
      col_no    = 1
      sap_align = 'CENTER'
      sap_style = cl_dd_document=>heading.

  if g_add_mode eq space.
    move text-001 to ls_texts.
  else.
    concatenate text-006 ad_sheet into ls_texts separated by space.
  endif.
  call method column_1->add_text
    exporting
      text = ls_texts.
  append ls_texts to lt_header.

  refresh lt_texts.

  if g_add_mode eq space.
    clear: l_budat_begin, l_budat_end.
    if not sel_mona is initial.
      PERFORM get_calendar_period USING    sel_mona-low
                                  CHANGING monat_low.
      PERFORM get_calendar_period USING    sel_mona-high
                                  CHANGING monat_high.
      date_from = monat_low.
      if not monat_high is initial.
        date_to   = monat_high.
      else.
        date_to   = monat_low.
      endif.
      if not br_gjahr-low is initial.
        fiscal_year = br_gjahr-low.
      endif.
    elseif not br_budat is initial.
      l_budat_begin = br_budat-low.
      l_budat_end   = l_budat_begin.
      if not br_budat-high is initial.
        l_budat_end = br_budat-high.
      endif.
    elseif not sel_bldt is initial.
      l_budat_begin = sel_bldt-low.
      l_budat_end   = l_budat_begin.
      if not sel_bldt-high is initial.
        l_budat_end = sel_bldt-high.
      endif.
    elseif not sel_vtdt is initial.
      l_budat_begin = sel_vtdt-low.
      l_budat_end   = l_budat_begin.
      if not sel_vtdt-high is initial.
        l_budat_end = sel_vtdt-high.
      endif.
    endif.
    if not l_budat_begin is initial.
      write l_budat_begin to date_from dd/mm/yyyy.
      write l_budat_end   to date_to   dd/mm/yyyy.
    endif.
    if ver2012 = 'X'.
      l_gjahr       = br_gjahr-low.
      perform adobe_period
         changing l_gjahr
                  monat_low
                  monat_high
                  l_budat_begin
                  l_budat_end.
      write l_budat_begin to date_from dd/mm/yyyy.
      write l_budat_end   to date_to   dd/mm/yyyy.
      clear fiscal_year.
    endif.
  else.
    if not ad_monat is initial.
      l_monat_low  = ad_monat-low.
      l_monat_high = ad_monat-high.
*     2056387 - tax period from quarter
      if not p_quart is initial AND
         not add_quart_r[] is initial.
        loop at add_quart_r into ls_add_quart_r.
          l_monat_low  = ls_add_quart_r-low.
          l_monat_high = ls_add_quart_r-high.
          exit.
        endloop.
      endif.
      PERFORM get_calendar_period USING    l_monat_low  " 2056387
                                  CHANGING monat_low.
      PERFORM get_calendar_period USING    l_monat_high " 2056387
                                  CHANGING monat_high.
      date_from = monat_low.
      if not monat_high is initial.
        date_to = monat_high.
      else.
        date_to = monat_low.
      endif.
      if not ad_gjahr-low is initial.
        fiscal_year = ad_gjahr-low.
      endif.
    elseif not ad_budat is initial.
      write ad_budat-low to date_from dd/mm/yyyy..
      if not ad_budat-high is initial.
        write ad_budat-high to date_to dd/mm/yyyy.
      else.
        write ad_budat-low  to date_to dd/mm/yyyy.
      endif.
    elseif not ad_vtdat is initial.
      write ad_vtdat-low to date_from dd/mm/yyyy..
      if not ad_vtdat-high is initial.
        write ad_vtdat-high to date_to dd/mm/yyyy.
      else.
        write ad_vtdat-low  to date_to dd/mm/yyyy.
      endif.
    endif.
  endif.
  if s_adr is initial.
    concatenate text-807
                name1_sel
                name2_sel
                name3_sel
                name4_sel
    into ls_texts separated by space.
  else.
    concatenate text-807
                name1_sel_r
                name2_sel_r
                name3_sel_r
                name4_sel_r
    into ls_texts separated by space.
  endif.
  append ls_texts to lt_texts.

  if s_adr is initial.
    concatenate text-807 name1_sel into ls_texts separated by space.
  else.
    concatenate text-807 name1_sel_r into ls_texts separated by space.
  endif.
  append ls_texts to lt_header.

  concatenate text-012
              paval_tmp
              stcd3_tmp into ls_texts separated by space.
  append ls_texts to lt_texts.

  concatenate paval_tmp stcd3_tmp into ls_texts separated by '/'.
  append ls_texts to lt_header.

  concatenate text-172
              text-810
              date_from
              text-811
              date_to
              fiscal_year into ls_texts separated by space.
  append ls_texts to lt_header.

  if g_add_mode eq space.
    concatenate text-809
                text-810
                date_from
                text-811
                date_to
                fiscal_year into ls_texts separated by space.
    append ls_texts to lt_texts.
  else.
    if ver2012 is initial.
    concatenate text-007
                text-810
                date_from
                text-811
                date_to
                fiscal_year into ls_texts separated by space.
    else.
*    concatenate text-036
    concatenate text-040   " 2005330
                text-810
                date_from
                text-811
                date_to
                fiscal_year into ls_texts separated by space.
    endif.
    append ls_texts to lt_texts.
    write ad_date to date_output dd/mm/yyyy.
    concatenate text-008
                date_output into ls_texts separated by space.
    append ls_texts to lt_texts.
    append ls_texts to lt_header.
  endif.

  call method cl_dd->add_text
    exporting
      text         = space
      text_table   = lt_texts
      fix_lines    = 'X'
      sap_fontsize = cl_dd_document=>medium.
endform.                                               "HTML_TOP_OF_PAGE
*----------------------------------------------------------------------*
*       FORM SET_STATUS Sets PF-STAUS
*----------------------------------------------------------------------*
form set_status using  extab type slis_t_extab.
  data: rt_extab type slis_t_extab with header line.

  if p_add eq space.
    rt_extab-fcode = 'ADD'.
    append rt_extab to extab.
    rt_extab-fcode = 'BAS'.
    append rt_extab to extab.
    rt_extab-fcode = 'ORIG'.
    append rt_extab to extab.
  else.
    if g_add_mode eq space.
      rt_extab-fcode = 'ORIG'.
      append rt_extab to extab.
      rt_extab-fcode = 'BAS'.
      append rt_extab to extab.
    else.
      rt_extab-fcode = 'ADD'.
      append rt_extab to extab.
    endif.
  endif.

  set pf-status 'STANDARD' excluding extab.
endform.                                                     "set_status
*----------------------------------------------------------------------*
*       FORM USER_COMMAND
*----------------------------------------------------------------------*
form user_command using r_ucomm like sy-ucomm
                    rs_selfield type slis_selfield.
  data fcc(3) type c.
  case r_ucomm.
* Show Reversed/Corrected document (FB03)
    when 'ORIG'.
      read table p_book_alv index rs_selfield-tabindex.
      if p_book_alv-bukrs     <> space and
         p_book_alv-belnr_add <> space and
         p_book_alv-gjahr_add <> space.
        set parameter id 'BUK' field p_book_alv-bukrs.
        set parameter id 'BLN' field p_book_alv-belnr_add.
        set parameter id 'GJR' field p_book_alv-gjahr_add.
        call transaction 'FB03' and skip first screen.    "#EC CI_CALLTA
      endif.
      clear r_ucomm.
    when 'INV'.
      read table p_book_alv index rs_selfield-tabindex.
      set parameter id 'BUK' field  p_book_alv-bukrs.
      set parameter id 'BLN' field  p_book_alv-belnr_inv.
      set parameter id 'GJR' field  p_book_alv-gjahr_inv.
*      SET PARAMETER ID 'GJR' FIELD  p_book-bldat_inv(4).
      call transaction 'FB03' and skip first screen.    "#EC CI_CALLTA
      clear r_ucomm.
    when 'PAY'.
      read table p_book_alv index rs_selfield-tabindex.   "CursorPosit.
      if p_book_alv-bukrs     <> space and
         p_book_alv-belnr_pay <> space and
         p_book_alv-gjahr_pay <> space.
        set parameter id 'BUK' field  p_book_alv-bukrs.
        set parameter id 'BLN' field  p_book_alv-belnr_pay.
*        SET PARAMETER ID 'GJR' FIELD  p_book-bldat_pay(4).
        set parameter id 'GJR' field  p_book_alv-gjahr_pay.
        call transaction 'FB03' and skip first screen.    "#EC CI_CALLTA
        clear r_ucomm.
      endif.
    when 'TRN'.
      data: begin of out_type,
            bukrs like p_book-bukrs,
            belnr_trn like p_book-belnr_trn,
            budat_trn like p_book-budat_trn,
            gjahr_trn like p_book-gjahr_trn,
            end of out_type.
      data: outtab_trn like table of out_type with header line
        with non-unique key
          bukrs
          belnr_trn
          budat_trn
          gjahr_trn.
      read table p_book_alv index rs_selfield-tabindex.   "CursorPosit.

      if not par_col is initial.
        if p_book_alv-belnr_trn is initial.
          loop at p_book where bukrs = p_book_alv-bukrs and
*           num_line = p_book_alv-num_line AND
*           dmbtr_inv = p_book_alv-dmbtr_inv AND
*           wrbtr_inv = p_book_alv-wrbtr_inv AND
            belnr_inv = p_book_alv-belnr_inv and
            xblnr_inv = p_book_alv-xblnr_inv and
            bldat_inv = p_book_alv-bldat_inv  and
            budat_inv = p_book_alv-budat_inv and
            dmbtr_wrs_inv = p_book_alv-dmbtr_wrs_inv and
            wrbtr_wrs_inv = p_book_alv-wrbtr_wrs_inv  and
            bktxt_inv = p_book_alv-bktxt_inv  and
            belnr_pay = p_book_alv-belnr_pay  and
            xblnr_pay = p_book_alv-xblnr_pay and
            bldat_pay = p_book_alv-bldat_pay and
            budat_pay = p_book_alv-budat_pay  and
            dmbtr_wrs_pay = p_book_alv-dmbtr_wrs_pay  and
            wrbtr_wrs_pay = p_book_alv-wrbtr_wrs_pay  and
            bktxt_pay = p_book_alv-bktxt_pay  and
            belnr_origpay = p_book_alv-belnr_origpay and
            gjahr_origpay = p_book_alv-gjahr_origpay and
            bldat_origpay = p_book_alv-bldat_origpay and
            budat_origpay = p_book_alv-budat_origpay and
            xblnr_origpay = p_book_alv-xblnr_origpay and
            bktxt_origpay = p_book_alv-bktxt_origpay and
            bldat_trn = p_book_alv-bldat_trn and
            budat_trn = p_book_alv-budat_trn and
            monat_trn = p_book_alv-monat_trn and
            dmbtr_wrs_trn = p_book_alv-dmbtr_wrs_trn and
            wrbtr_wrs_trn = p_book_alv-wrbtr_wrs_trn and
            hwbas_wrs_trn = p_book_alv-hwbas_wrs_trn and
            fwbas_wrs_trn = p_book_alv-fwbas_wrs_trn and
            hwbas2_wrs_trn = p_book_alv-hwbas2_wrs_trn and
            fwbas2_wrs_trn = p_book_alv-fwbas2_wrs_trn and
            hwbas3_wrs_trn = p_book_alv-hwbas3_wrs_trn and
            fwbas3_wrs_trn = p_book_alv-fwbas3_wrs_trn and
            hwbas4_wrs_trn = p_book_alv-hwbas4_wrs_trn and
            fwbas4_wrs_trn = p_book_alv-fwbas4_wrs_trn and
            hwbas5_wrs_trn = p_book_alv-hwbas5_wrs_trn and
            fwbas5_wrs_trn = p_book_alv-fwbas5_wrs_trn and
            hwste_wrs_trn = p_book_alv-hwste_wrs_trn and
            fwste_wrs_trn = p_book_alv-fwste_wrs_trn and
            hwste2_wrs_trn = p_book_alv-hwste2_wrs_trn and
            fwste2_wrs_trn = p_book_alv-fwste2_wrs_trn and
            hwste3_wrs_trn = p_book_alv-hwste3_wrs_trn and
            fwste3_wrs_trn = p_book_alv-fwste3_wrs_trn and
            hwste5_wrs_trn = p_book_alv-hwste5_wrs_trn and
            fwste5_wrs_trn = p_book_alv-fwste5_wrs_trn and
            gjahr_test = p_book_alv-gjahr_test and
            name1_cred = p_book_alv-name1_cred and
            name2_cred = p_book_alv-name2_cred and
            stcd1_cred = p_book_alv-stcd1_cred and
            stcd3_cred = p_book_alv-stcd3_cred and
            vatdate_inv = p_book_alv1-vatdate_inv and
            vatdate_trn = p_book_alv1-vatdate_trn and
            part_payment = p_book_alv-part_payment.

            move-corresponding p_book to outtab_trn.
            collect outtab_trn.
          endloop.
        endif.
      else.
        if not p_book_alv-belnr_trn is initial.
          move-corresponding p_book_alv to outtab_trn.
          collect outtab_trn.
* 0% deferred tax transfer document
        else.
          message i201(f4).
          exit.
        endif.
      endif.

      data fc type slis_t_fieldcat_alv.
      data ls type slis_fieldcat_alv.
      refresh fc[] .

      clear ls.
      ls-fieldname     = 'BELNR_TRN'.
      ls-ref_tabname   = gc_j_3rbuy_alv.
      ls-ref_fieldname = 'BELNR_TRN'.
      append ls to fc.

      clear ls.
      ls-fieldname     = 'GJAHR_TRN'.
      ls-ref_tabname   = gc_j_3rbuy_alv.
      ls-ref_fieldname = 'GJAHR_TRN'.
      append ls to fc.

      clear ls.
      ls-fieldname     = 'BUDAT_TRN'.
      ls-ref_tabname   = gc_j_3rbuy_alv.
      ls-ref_fieldname = 'BUDAT_TRN'.
      append ls to fc.

      data:      gs_selfield type slis_selfield,
            g_exit(1) type c.
      data: out_lines type i.
*      COLLECT outtab_trn.
      describe table outtab_trn lines out_lines.

      if out_lines > 1.
        call function 'REUSE_ALV_POPUP_TO_SELECT'
          exporting
            i_tabname     = '1'
            it_fieldcat   = fc
          importing
            es_selfield   = gs_selfield
            e_exit        = g_exit
          tables
            t_outtab      = outtab_trn[]
          exceptions
            program_error = 1
            others        = 2.
        if sy-subrc <> 0.
          message id sy-msgid type sy-msgty number sy-msgno
                  with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
        endif.

        if g_exit ne 'X'.
          read table outtab_trn index gs_selfield-tabindex.
          set parameter id 'BUK' field  outtab_trn-bukrs.
          set parameter id 'BLN' field  outtab_trn-belnr_trn.
          set parameter id 'GJR' field  outtab_trn-gjahr_trn.
          call transaction 'FB03' and skip first screen.    "#EC CI_CALLTA
          clear r_ucomm.
        endif.
      else.
        read table outtab_trn.
        set parameter id 'BUK' field  outtab_trn-bukrs.
        set parameter id 'BLN' field  outtab_trn-belnr_trn.
        set parameter id 'GJR' field  outtab_trn-gjahr_trn.
        call transaction 'FB03' and skip first screen.    "#EC CI_CALLTA
        clear r_ucomm.
      endif.
    when 'CLR'.
      read table p_book_alv index rs_selfield-tabindex.   "CursorPosit.
      perform call_cleared_items using p_book_alv-bukrs
                                       p_book_alv-belnr_pay
                                       p_book_alv-gjahr_pay.
      clear r_ucomm.
*   Call Transaction "Purchase Order"
    when 'POR'.
      read table p_book_alv index rs_selfield-tabindex.
      if p_book_alv-mm_inv_ebln <> space.
        set parameter id 'BES' field  p_book_alv-mm_inv_ebln.
        call transaction 'ME23' and skip first screen.    "#EC CI_CALLTA
        clear r_ucomm.
      endif.
* LC283 ----------------------------------- *
    when 'ADD'.
      rs_selfield-exit = 'X'.
      g_add_mode = 'X'.
      perform call_alv_no_cycle.
      clear r_ucomm.
    when 'BAS'.
      rs_selfield-exit = 'X'.
      clear g_add_mode.
      perform call_alv_no_cycle.
      clear r_ucomm.
* LC283 ----------------------------------- *
  endcase.
endform.                    "user_command
*----------------------------------------------------------------------*
*       FORM PRINT_INIT
*----------------------------------------------------------------------*
form print_init.
  clear gs_print.
  gs_print-reserve_lines      = '2'.
  gs_print-no_print_listinfos = 'X'. "No print info
  gs_print-no_print_selinfos  = 'X'. "No sel info
endform.                                                     "print_init
*---------------------------------------------------------------------*
*       FORM ADD_FI_GTD                                               *
*---------------------------------------------------------------------*
form add_fi_gtd
  using
    it_bseg type j3rdl_t_bseg_cache
  changing
    ct_book type j_3r_t_buy.

  data: wa_bseg type j3rdl_bseg_cache,
        wa_book type j_3rbuy,
        flag_modified(1) type c,
        position         type i,
        flag_asterix     type sy-subrc, " 1776529
        l_index          type sy-tabix, " 1924130
        l_long_gtd       type string.

  loop at ct_book into wa_book.
    l_index = sy-tabix.
    clear flag_modified.
    l_long_gtd = wa_book-mm_inv_vornu.
    loop at it_bseg into wa_bseg
      where bukrs = wa_book-bukrs      and
            belnr = wa_book-belnr_inv  and
            gjahr = wa_book-gjahr_inv.
      check wa_bseg-mwart is initial and
            not wa_bseg-sgtxt is initial and
            wa_bseg-sgtxt in par_fi.
      loop at par_fi.
        replace all occurrences of '*' in par_fi-low with space.
        flag_asterix = sy-subrc.  " 1776529
        if par_fi-low is initial. " 1285508
          continue.               " 1285508
        endif.                    " 1285508
        find first occurrence of par_fi-low in wa_bseg-sgtxt match offset position.
        if sy-subrc = 0.
          replace all occurrences of par_fi-low in wa_bseg-sgtxt with space.
          wa_bseg-sgtxt = wa_bseg-sgtxt+position.
          condense wa_bseg-sgtxt.
          if flag_asterix = 0      and " 1776529 - remove '*'
             wa_bseg-sgtxt is not initial.
            if wa_bseg-sgtxt(1) = '*'.
              wa_bseg-sgtxt = wa_bseg-sgtxt+1.
              condense wa_bseg-sgtxt.
            endif.
          endif.
          exit.
        endif.
      endloop.
      concatenate l_long_gtd
                  wa_bseg-sgtxt ';'
             into l_long_gtd. "separated by space.
      condense l_long_gtd.
      flag_modified = 'X'.
    endloop.
    if not flag_modified is initial.
      PERFORM remove_gtd_duplicates  CHANGING l_long_gtd. " 1924130
      wa_book-mm_inv_vornu = l_long_gtd.
      modify ct_book from wa_book index l_index transporting mm_inv_vornu.
    endif.
  endloop.
endform.                                                     "add_fi_gtd
*----------------------------------------------------------------------*
*       FORM CREATE_NUM_DATA
*----------------------------------------------------------------------*
form create_num_data.                  "For numbering lines

* Check Flag Numbering 'Active'
  if laufd = 0.
    set cursor field 'laufd'.
    message id 'F7' type 'E' number '208'.
    flg_ok_records = space.
  endif.
  if laufi = space.
    set cursor field 'laufi'.
    message id 'F7' type 'E' number '209'.
    flg_ok_records = space.
  endif.
  if laufd <> space and laufi <> space.
    select single * from j_3rbue_book_ind "key!
      where
            laufd = laufd and
            laufi = laufi.                 "#EC CI_NOFIRST
    if sy-subrc = 0.
      set cursor field 'laufi'.
      message id 'F7' type 'E' number '210'.
      flg_ok_records = space.
    else.
      flg_ok_records = 'X'.
    endif.
  endif.
endform.                                               "CREATE_NUM_DATA'
*----------------------------------------------------------------------*
*       FORM ADD_TRANSP_NUMER
*----------------------------------------------------------------------*
form add_transparent_number.
  data: curr_index     like sy-tabix,
        cnt_max_number like bset-belnr,
        nlin(10)       type n.

  data: begin of itab_insp occurs 0.
          include structure j_3rbue_book_num.
  data: end   of itab_insp.

  data itab_insh like j_3rbue_book_ind occurs 0 with header line.

  data: belnr_pay_old like p_book-belnr_pay,
        gjahr_pay_old like p_book-gjahr_pay,
        belnr_inv_old like p_book-belnr_inv,
        gjahr_inv_old like p_book-gjahr_inv,
        belnr_trn_old like p_book-belnr_trn,
        gjahr_trn_old like p_book-gjahr_trn,
        buzei_test_old like p_book-buzei_test,
        bldat_trn_old like p_book-bldat_trn,
        budat_trn_old like p_book-budat_trn.

  clear itab_insh. refresh itab_insh.
  clear belnr_pay_old.
  clear gjahr_pay_old.
  clear belnr_inv_old.
  clear gjahr_inv_old.
  clear belnr_trn_old.
  clear gjahr_trn_old.
  clear buzei_test_old.
  clear bldat_trn_old.
  clear budat_trn_old.

  if not p_book[] is initial.

    j_3rbue_book_ind-mandt       = sy-mandt.
    j_3rbue_book_ind-bukrs       = br_bukrs-low.      "it_bkpf-bukrs.
    j_3rbue_book_ind-gjahr       = br_gjahr-low.      "it_bkpf-gjahr.
* From Selection Screen
    j_3rbue_book_ind-laufd       = laufd.
    j_3rbue_book_ind-laufi       = laufi.
    j_3rbue_book_ind-budat_start = br_budat-low.
    j_3rbue_book_ind-budat_end   = br_budat-high.
    j_3rbue_book_ind-bldt_start  = sel_bldt-low.
    j_3rbue_book_ind-bldt_end    = sel_bldt-high.
    IF  sel_bldt-low  IS INITIAL AND
        sel_bldt-high IS INITIAL AND
        NOT sel_vtdt-low  IS INITIAL AND
        NOT sel_vtdt-high IS INITIAL.
      j_3rbue_book_ind-bldt_start  = sel_vtdt-low.
      j_3rbue_book_ind-bldt_end    = sel_vtdt-high.
    ENDIF.

    j_3rbue_book_ind-monat_begin = sel_mona-low.
    j_3rbue_book_ind-monat_end   = sel_mona-high.
    insert j_3rbue_book_ind.
    select max( belnr_transp )
           from j_3rbue_book_num
           into (cnt_max_number)
           where bukrs in  br_bukrs and gjahr = br_gjahr-low.
    IF NOT sy-subrc IS INITIAL.
      CLEAR cnt_max_number.
    ENDIF.
    nlin = cnt_max_number.
    loop at p_book where belnr_add is initial.
      IF p_book-belnr_trn IS INITIAL. "N1326572: 0% tax
        select single * from  j_3rbue_book_num
                        where
                        bukrs =  p_book-bukrs      and
                        belnr =  p_book-belnr_inv  and
                        buzei =  p_book-buzei_test and
                        gjahr =  p_book-gjahr_inv.
      ELSE.
        select single * from  j_3rbue_book_num
                        where
                        bukrs =  p_book-bukrs      and
                      belnr =  p_book-belnr_trn  and
                      buzei =  p_book-buzei_test and
                      gjahr =  p_book-gjahr_trn.
      ENDIF.
* sy-tabix could be spoiled. Let us save it's value.
      curr_index = sy-tabix.
      if sy-subrc <> 0.                  "NO records found
* Fill itab ITAB_INSP(Position).
        itab_insp-mandt        = sy-mandt.
        itab_insp-bukrs        = p_book-bukrs.
        itab_insp-belnr        = p_book-belnr_trn.          "#DOC0
        itab_insp-gjahr        = p_book-gjahr_trn.  "fiscal year
        itab_insp-buzei        = p_book-buzei_test. "Position
        itab_insp-bldat_trn    = p_book-bldat_trn.  "Date
        itab_insp-monat_begin  = sel_mona-low.      "Period str
        itab_insp-monat_end    = sel_mona-high.     "Period end
        if itab_insp-belnr is initial.
          itab_insp-belnr      = p_book-belnr_inv. "N1326572: 0% tax
          itab_insp-gjahr      = p_book-gjahr_inv.
        endif.
* Fill Personal Index
* READ TABLE itab_insh.
        itab_insp-laufi = j_3rbue_book_ind-laufi.
        itab_insp-laufd = j_3rbue_book_ind-laufd.
*            nlin = cnt_max_number. "current max num
        if ( belnr_pay_old <> p_book-belnr_pay or
             gjahr_pay_old <> p_book-gjahr_pay or
             belnr_inv_old <> p_book-belnr_inv or
             gjahr_inv_old <> p_book-gjahr_inv ) or
           ( belnr_trn_old = p_book-belnr_trn and
             gjahr_trn_old = p_book-gjahr_trn and
             buzei_test_old = p_book-buzei_test ) or
             bldat_trn_old <> p_book-bldat_trn or
             budat_trn_old <> p_book-budat_trn.
          nlin = nlin + 1.
        endif.

        belnr_pay_old = p_book-belnr_pay.
        gjahr_pay_old = p_book-gjahr_pay.
        belnr_inv_old = p_book-belnr_inv.
        gjahr_inv_old = p_book-gjahr_inv.
        belnr_trn_old = p_book-belnr_trn.
        gjahr_trn_old = p_book-gjahr_trn.
        buzei_test_old = p_book-buzei_test.
        bldat_trn_old = p_book-bldat_trn.
        budat_trn_old = p_book-budat_trn.
        p_book-num_line = nlin.
        modify p_book index sy-tabix.
        itab_insp-belnr_transp = nlin.
        append itab_insp.
      endif.
    endloop.
    insert j_3rbue_book_num from table itab_insp.
    commit work.
    free itab_insp.
  endif.
endform.                                         "add_transparent_number
*----------------------------------------------------------------------*
*       FORM Delete_EXTRACT
*----------------------------------------------------------------------*
form delete_extract
  using v_extract like disextract.

  data: t_lines like popuptext occurs 1 with header line,
        i_answer.

  data: it_book   type j_3r_t_buy,
        wa_book   type j_3rbuy,
        wa_totals type j_3rbuy.

  refresh t_lines.
  t_lines-text = text-300. append t_lines.
  t_lines-text = text-301. append t_lines.
  t_lines-text = v_extract-exname. append t_lines.
  t_lines-text = text-303. append t_lines.
  t_lines-text = text-300. append t_lines.

  call function 'DD_POPUP_WITH_INFOTEXT'
    exporting
      titel        = text-304
      start_column = 25
      start_row    = 6
    importing
      answer       = i_answer
    tables
      lines        = t_lines.

  if i_answer = 'Y'.
    perform load_extract using    v_extract
                         changing it_book wa_totals.
* Authority check before deletion
    refresh temp_t001.
    loop at it_book into wa_book.
      temp_t001-bukrs = wa_book-bukrs.
      collect temp_t001.
    endloop.

    free it_book.

    loop at temp_t001 into wa_temp_t001.
      authority-check object 'F_BKPF_BUK'
                  id 'BUKRS' field wa_temp_t001-bukrs
                  id 'ACTVT' field '03'.
      if sy-subrc <> 0.
        message e800(fr) with wa_temp_t001-bukrs.
      endif.
    endloop.

    if gc_customizing-extract_size > 0.
*     1907361 delete multiple extracts
      perform delete_extract_ex using v_extract.
    else.
    call function 'REUSE_ALV_EXTRACT_DELETE'
      exporting
        is_extract  = v_extract
      exceptions
        not_deleted = 1
        others      = 2.
    if sy-subrc <> 0.
      message id sy-msgid type sy-msgty number sy-msgno
              with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    endif.
    endif.

    clear: p_del, p_ex3, p_ext3.
    message s899(f4) with text-403. "Extract deleted!
  else.
    message s899(f4) with text-404. "Delete EXTRACT cancelled!
  endif.
endform.                                                 "Delete_EXTRACT
*----------------------------------------------------------------------*
*       FORM View_EXTRACT
*----------------------------------------------------------------------*
form view_extract
  using    v_extract   like disextract
  changing ct_book     type j_3r_t_buy
           cs_totals   type j_3rbuy.

  data:
*        it_book type j_3r_t_buy,
*        wa_book type j_3rbuy,
        num_line_add(10) type n value '1',
        num_line_bas(10) type n,
        lv_delete type abap_bool.

  FIELD-SYMBOLS: <fs_book> like line of ct_book.

  num_line_bas = num_strt. " 1695097
  if num_line_bas is initial.
    num_line_bas = 1.
  endif.

  num_line_add = ad_doc_n.  "N1472064
  if num_line_add is initial.
    num_line_add = 1.
  endif.

  perform load_extract using    v_extract
                       changing ct_book cs_totals.
* Apply selections
  loop at ct_book ASSIGNING <fs_book>.
    if not ( <fs_book>-bukrs      in br_bukrs and
          <fs_book>-mwskz      in sel_mwkz and
          <fs_book>-lifnr_cred in s_lifnr ).
      clear <fs_book>-bukrs.
      lv_delete = abap_true.
      CONTINUE.
    endif.
* In case of 0% taxes there may be no tax transfer document
    if not <fs_book>-belnr_trn is initial.
*      check <fs_book>-belnr_trn in br_belnr and        "Tax Relevant Doc
*            <fs_book>-gjahr_trn in br_gjahr and
*            <fs_book>-budat_trn in br_budat and
*            <fs_book>-monat_trn in sel_mona and
*            <fs_book>-bldat_trn in sel_bldt.
      if not ( <fs_book>-belnr_trn in br_belnr and        "Tax Relevant Doc
            <fs_book>-gjahr_trn in br_gjahr and
            <fs_book>-budat_trn in br_budat and
            <fs_book>-monat_trn in sel_mona and
            <fs_book>-bldat_trn in sel_bldt ).
        clear <fs_book>-bukrs.
        lv_delete = abap_true.
        CONTINUE.
      endif.
    else.
*      check <fs_book>-belnr_inv in br_belnr.
      if not ( <fs_book>-belnr_inv in br_belnr ).
        clear <fs_book>-bukrs.
        lv_delete = abap_true.
        CONTINUE.
      endif.
    endif.
    clear <fs_book>-num_line.
* Look for report's line number in numbering pool
    if num_auto = 'X' and <fs_book>-belnr_add is initial. " 1702300
      <fs_book>-num_line = num_line_bas.
      num_line_bas     = num_line_bas + 1.
    elseif <fs_book>-belnr_add is initial and
       <fs_book>-belnr_trn is initial. "N1326572: 0% tax
      select single belnr_transp into <fs_book>-num_line
        from j_3rbue_book_num
        where bukrs = <fs_book>-bukrs and
              belnr = <fs_book>-belnr_inv and
              gjahr = <fs_book>-gjahr_inv and
              buzei = <fs_book>-buzei_test.
    elseif <fs_book>-belnr_add is initial.
      select single belnr_transp into <fs_book>-num_line
        from j_3rbue_book_num
        where bukrs = <fs_book>-bukrs and
              belnr = <fs_book>-belnr_trn and
              gjahr = <fs_book>-gjahr_trn and
              buzei = <fs_book>-buzei_test.
    else.
*     N1334957 - sequential numbering for additional sheets
*      select single belnr_transp into wa_book-num_line
*        from j_3rbue_book_num
*        where bukrs = wa_book-bukrs and
*              belnr = wa_book-belnr_add and
*              gjahr = wa_book-gjahr_add and
*              buzei = wa_book-buzei_add.
      <fs_book>-num_line = num_line_add.
      num_line_add     = num_line_add + 1.
    endif.

    perform fill_add_fields changing <fs_book>.
*    append wa_book to ct_book.
  endloop.

  if lv_delete is not INITIAL.
    delete ct_book where bukrs is INITIAL.
  endif.

*  free it_book.
endform.                                                   "View_EXTRACT
*----------------------------------------------------------------------*
*       FORM CREATE_EXTRACT
*----------------------------------------------------------------------*
form create_extract
  using v_extract like disextract
        ct_book   type j_3r_t_buy
        cs_totals type j_3rbuy.

  data: it_book_old  type standard table of j_3rbuy_old,
        wa_book_old  type j_3rbuy_old,
        it_book_283  type standard table of j_3rbuy_283,
        wa_book_283  type j_3rbuy_283,
        wa_totals283 type j_3rbuy_283,
        it_book_ex   type standard table of j_3rbuy_ex,
        wa_book_ex   type j_3rbuy_ex,
        wa_totals_ex type j_3rbuy_ex,
        wa_book      type j_3rbuy,
        l_lines      type i,   " 1902051
        l_start      type i,
        l_end        type i,
        l_dynnum(2)  type n,
        l_dynname(3) type c.

  if ct_book[] is initial. " 2138132
    message i082(9p).
  endif.

  if ver2012 = 'X'.       " 1695097 - Structure per Order 1137
    l_lines = LINES( ct_book ).   " 1902051

*   delete extract
    if gc_customizing-extract_size > 0.
      perform delete_extract_ex using v_extract.
    endif.

    l_start = 1.
    while l_start <= l_lines.
      if gc_customizing-extract_size > 0.
        l_end = l_start + gc_customizing-extract_size - 1.
        if l_end < l_lines and " init name index
           l_dynname is initial.
          l_dynname = '~00'.
        endif.
      else.
        l_end = l_lines.
      endif.
      if l_end > l_lines.
        l_end = l_lines.
      endif.
*     build name
      if not l_dynname is initial.
        l_dynnum = l_dynnum + 1.
        concatenate '~' l_dynnum into l_dynname.
        v_extract-exname+9(3) = l_dynname.
        if l_dynnum = '99'. " overflow
          l_end = l_lines.
        endif.
      endif.

      refresh it_book_ex[].

    loop at ct_book into wa_book from l_start to l_end.
      move-corresponding wa_book to wa_book_ex.
      append wa_book_ex to it_book_ex.
    endloop.
    l_start = l_end + 1.
    move-corresponding cs_totals to wa_totals_ex.
    call function 'REUSE_ALV_EXTRACT_SAVE'
      exporting
        is_extract         = v_extract
        i_get_selinfos     = 'X'
        i_exp01            = wa_totals_ex "cs_totals
      tables
        it_exp01           = it_book_ex "ct_book
      exceptions
        wrong_relid        = 1
        no_report          = 2
        no_exname          = 3
        no_extract_created = 4
        others             = 5.
    if sy-subrc <> 0.
      message id sy-msgid type sy-msgty number sy-msgno
            with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    endif.
    endwhile.

    free: it_book_ex, wa_totals_ex.
  elseif p_add = 'X'.
    loop at ct_book into wa_book.
      move-corresponding wa_book to wa_book_283.
      append wa_book_283 to it_book_283.
    endloop.
    move-corresponding cs_totals to wa_totals283.
    call function 'REUSE_ALV_EXTRACT_SAVE'
      exporting
        is_extract         = v_extract
        i_get_selinfos     = 'X'
        i_exp01            = wa_totals283 "cs_totals
      tables
        it_exp01           = it_book_283 "ct_book
      exceptions
        wrong_relid        = 1
        no_report          = 2
        no_exname          = 3
        no_extract_created = 4
        others             = 5.
    if sy-subrc <> 0.
      message id sy-msgid type sy-msgty number sy-msgno
            with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    endif.
    free: it_book_283, wa_totals283.
  else.
    loop at ct_book into wa_book.
      move-corresponding wa_book to wa_book_old.  "#EC ENHOK
      append wa_book_old to it_book_old.
    endloop.
    call function 'REUSE_ALV_EXTRACT_SAVE'
      exporting
        is_extract         = v_extract
        i_get_selinfos     = 'X'
      tables
        it_exp01           = it_book_old
      exceptions
        wrong_relid        = 1
        no_report          = 2
        no_exname          = 3
        no_extract_created = 4
        others             = 5.
    if sy-subrc <> 0.
      message id sy-msgid type sy-msgty number sy-msgno
            with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    endif.
    free it_book_old.
  endif.
endform.                                                 "CREATE_EXTRACT
*----------------------------------------------------------------------*
*       FORM call_cleared_items
*----------------------------------------------------------------------*
form call_cleared_items using
               v_bukrs like bkpf-bukrs
               v_belnr like bkpf-belnr
               v_gjahr like bkpf-gjahr.

  clear: postab[], kontab[].

  call function 'GET_CLEARED_ITEMS'
       exporting
            i_bvorg   = ''
            i_bukrs   = v_bukrs
            i_belnr   = v_belnr
            i_gjahr   = v_gjahr
       tables
            t_items   = postab
*            t_accnt   = yaccnt
       exceptions
            not_found = 1
            others    = 2.
  case sy-subrc.
    when 1.
      message e598(fs) with v_bukrs v_belnr v_gjahr.
    when 2.
      message id sy-msgid type 'E' number sy-msgno
              with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  endcase.
  kontab-name1 = space.
  kontab-refkt = space.
  loop at postab.
    kontab-konto = postab-konto.
    kontab-bukrs = postab-bukrs.
    kontab-koart = postab-koart.
    if postab-koart eq 'S'.
      kontab-sakan = postab-konto.
    endif.
    collect kontab.
  endloop.
  epos-errnr = space.
  epos-gjahr = v_gjahr.
  if epos-varnr eq space.
    select single * from t021v
      where tcode eq 'FBRA'
      and   koart eq space.
    if sy-subrc eq 0 and t021v-varnr ne space.
      epos-varnr = t021v-varnr.
    else.
      message e018(f4) with 'FBRA'.
    endif.
  endif.

  epos-title = ''.                                          "text-210.
  epos-info1 = ''.                                          "text-211.
  replace '&AUGBL' with v_belnr into epos-info1.
  export postab kontab epos to memory id 'RFEPOS00/POSTAB'.
  case kontab-koart.
    when 'K'.
      call transaction 'FBL1' and skip first screen.    "#EC CI_CALLTA
    when 'D'.
      call transaction 'FBL5' and skip first screen.    "#EC CI_CALLTA
    when others.
      call transaction 'FBL3' and skip first screen.    "#EC CI_CALLTA
  endcase.
endform.                                             "call_cleared_items

*----------------------------------------------------------------------*
*       FORM restart_program
*----------------------------------------------------------------------*
form restart_program.
  data: seltab    like standard table of rsparams,
        t_repid   like rsvar-report,
        wa_seltab like line of seltab.

  move sy-repid to t_repid.
  br_belnr[] = save_br_belnr[].
* Read select options
  call function 'RS_REFRESH_FROM_SELECTOPTIONS'
    exporting
      curr_report     = t_repid
    tables
      selection_table = seltab
    exceptions
      not_found       = 1
      others          = 2.
  if sy-subrc = 2.
* Internal error
    message id sy-msgid type 'A'      number sy-msgno
            with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  elseif sy-subrc = 1.
    clear seltab.
  endif.
** Error with address version preparation
*  IF errmess <> space.
  read table seltab with key selname = 'S_ADR'
        into wa_seltab.
  wa_seltab-low = space.
  modify seltab from wa_seltab index sy-tabix.
*  ENDIF.
*
  submit (t_repid) via selection-screen with selection-table seltab."#EC CI_SUBMIT
endform.                                                "restart_program
**----------------------------------------------------------------------*
**       FORM ONHELP - On line help for objects scr1000
**----------------------------------------------------------------------*
*FORM onhelp USING txx LIKE line01 ffname LIKE help_info-fieldname.
*  DATA:  links LIKE TABLE OF tline,
*         hdr   LIKE dsyst-doktitle,
*         repp  LIKE sy-repid.
*  hdr = text-001.
*
*  repp = sy-repid.
*  CALL FUNCTION 'HELP_OBJECT_SHOW'
*       EXPORTING
*            dokclass                      = 'TX'
*            doklangu                      = sy-langu
*            dokname                       = txx
*            doktitle                      = hdr
*            called_by_program             = repp
*            called_by_dynp                = sy-dynnr
*            called_for_tab                = 'SCREEN'
*            called_for_field              = ffname
**             called_for_tab_fld_btch_input = ' '
*       TABLES
*            links                         = links.
*  CLEAR repp.
*ENDFORM.                                                         "onhelp
*----------------------------------------------------------------------*
*       FORM SELECT_VARIANT                        #### ####### ######
*----------------------------------------------------------------------*
form select_variant.
  clear g_variant.
  g_variant-report = sy-repid.
  g_variant-variant = p_vari.
  gx_variant        = g_variant.
  perform e01_fieldcat_init using gt_fieldcat[] 'X' ver2012 ver2014.
  call function 'REUSE_ALV_VARIANT_SELECT'
       exporting
            i_dialog            = ' '
      it_default_fieldcat = gt_fieldcat[]
      i_layout            = gs_layout
      i_user_specific     = 'X'              "N1335366
    importing
      et_sort             = gt_sort[]  "info about sort,filters...
       changing
            cs_variant          = gx_variant
       exceptions
            wrong_input         = 1
            fc_not_complete     = 2
            not_found           = 3
            program_error       = 4
            others              = 5.
  refresh:  gt_fieldcat.
endform.                                                 "select_variant
*----------------------------------------------------------------------*
*       FORM SORT_FIELDS_BY_VARIANT
*----------------------------------------------------------------------*
form sort_fields_by_variant using ct_book type j_3r_t_buy.
*
  data: ls_sort type slis_sortinfo_alv,
        it_sort type ABAP_SORTORDER_TAB.

  field-symbols: <so> type ABAP_SORTORDER.

* Sort itab with info about range of sorted fields
  sort gt_sort by spos.

* build sorting table
  loop at gt_sort into ls_sort.
    APPEND INITIAL LINE TO it_sort ASSIGNING <so>.
    <so>-name       = ls_sort-fieldname.
    <so>-DESCENDING = ls_sort-down.
    " sort is case-independent for TEXT fields only: NAME*_CRED or NAME*_CRED_R
    if ( ls_sort-fieldname(4) = 'NAME') AND (
       ( ls_sort-fieldname+5 = '_CRED' ) OR ( ls_sort-fieldname+5 = '_CRED_R' ) ).
      <so>-ASTEXT     = 'X'.
    endif.
  endloop.

* append default sort order by gjahr_add monat_add belnr_add
  APPEND INITIAL LINE TO it_sort ASSIGNING <so>.
  <so>-name = 'GJAHR_INV'.
  APPEND INITIAL LINE TO it_sort ASSIGNING <so>.
  <so>-name = 'BELNR_INV'.
  APPEND INITIAL LINE TO it_sort ASSIGNING <so>.
  <so>-name = 'BELNR_TRN'.
  APPEND INITIAL LINE TO it_sort ASSIGNING <so>.
  <so>-name = 'GJAHR_ADD'.
  APPEND INITIAL LINE TO it_sort ASSIGNING <so>.
  <so>-name = 'MONAT_ADD'.
  APPEND INITIAL LINE TO it_sort ASSIGNING <so>.
  <so>-name = 'BELNR_ADD'.

* sort by user keys
  sort ct_book by (it_sort).

endform.                                         "sort_fields_by_variant
*----------------------------------------------------------------------*
*       FORM M_BELNR_OLD
*----------------------------------------------------------------------*
form mm_belnr_old
  using
    it_bkpf type j3rdl_t_bkpf_cache
    it_bseg type j3rdl_t_bseg_cache
  changing
    ct_book    type j_3r_t_buy
    l_long_gtd type string.

  constants: mm_transaction type bkpf-glvor value 'RMRP'.

  data: flag_re_l(1), flag_re(1), flag_lerf(1) type c,
        wa_bkpf type j3rdl_bkpf_cache,
        wa_bseg type j3rdl_bseg_cache,
        wa_book type j_3rbuy.

  data: tab_gtd type eipo.

  data: begin of tab_gtd_header occurs 0,
        ebeln like bseg-ebeln,
        ebelp like bseg-ebelp,                                       "!!!
        exnum like ekko-exnum,
        end of tab_gtd_header.

  data: tab_gtd_pos type ty_t_tab_gtd_pos with header line.

  data: begin of itab_ekbe occurs 0,
        ebeln like ekbe-ebeln,
        ebelp like ekbe-ebelp,
        bewtp like ekbe-bewtp,
        belnr like ekbe-belnr,
        budat like ekbe-budat,
        shkzg like ekbe-shkzg,
        lfbnr like ekbe-lfbnr,
        end of itab_ekbe.

  data: begin of wa_ekbe occurs 0,
        ebeln like ekbe-ebeln,
        ebelp like ekbe-ebelp,
        bewtp like ekbe-bewtp,
        belnr like acchd-awref,
        lfbnr like ekbe-lfbnr,
        fibnr like bkpf-belnr,
        budat like ekbe-budat,
        end of wa_ekbe.

  data: begin of wa_invv occurs 0,
        lfbnr like ekbe-lfbnr,
        budat type ekbe-budat,
        end of wa_invv.

  data: itab_rseg type ty_t_itab_rseg with header line.
  data: l_belnr_inv type bkpf-belnr,
        l_gjahr_inv type bkpf-gjahr.
  types: ty_ekbe    type ty_s_ekbe.
  data: lt_ekbe     type standard table of ty_ekbe,  " 1702300
        lt_ekbe_tmp type standard table of ty_ekbe,
        lt_lfbnr    type standard table of ekbe-lfbnr,
        lt_ekbe_rev type standard table of ty_ekbe,
        ls_ekbe     type ty_ekbe,
        ls_ekbe2    type ty_ekbe,
        ls_ekbe3    type ty_ekbe,
        ls_ekbe_rev type ty_ekbe,
        l_lfbnr     type ekbe-lfbnr,
        l_tabix     type sy-tabix,
        l_tabix1    type sy-tabix,
        l_tabix2    type sy-tabix,
        l_found     type c,
        ls_bkpf_tmp type j3rdl_bkpf_cache,
        l_found_gr  type c,
        ls_essr     type essr. " 1761164

  loop at ct_book into wa_book where glvor = mm_transaction. "'RMRP'.

    clear:   tab_gtd_pos, tab_gtd_header, itab_ekbe, wa_ekbe.
    refresh: tab_gtd_pos, tab_gtd_header, itab_ekbe, wa_ekbe.
    clear:   l_long_gtd.

*   1626029 - invoice
    l_belnr_inv = wa_book-belnr_inv.
    l_gjahr_inv = wa_book-gjahr_inv.
    if wa_book-bktxt_inv(4) = gc_rcu_xblnr.
      l_belnr_inv = wa_book-bktxt_inv+4(10).
      l_gjahr_inv = wa_book-bktxt_inv+14(4).
    endif.

    loop at it_bseg into wa_bseg
       where bukrs = wa_book-bukrs and
             belnr = l_belnr_inv and
             gjahr = l_gjahr_inv.
* Apparently there are also some other lines containing Purchase Order number
* (e.g. bseg-buzid = 'F' or 'M') so we should not stick to bseg-buzid = 'W'
*      check ( it_bseg-buzid = 'W' ).
      check ( not wa_bseg-ebeln is initial ).
      check ( not wa_bseg-ebelp is initial ). "N1384356
      tab_gtd_pos-ebeln = wa_bseg-ebeln.
      tab_gtd_pos-ebelp = wa_bseg-ebelp.
*      append tab_gtd_pos.
      collect tab_gtd_pos.

      tab_gtd_header-ebeln = wa_bseg-ebeln.
      collect tab_gtd_header.
    endloop.

* Get the Purchase Order Number
* If there are more than one Purchase Order (is it possible at all?) - take the first one
    sort tab_gtd_header by ebeln.
    read table tab_gtd_header index 1.

*   N1343104: get PO numbers from MM Invoice
    if not sy-subrc is initial.
      " read AWKEY
      read table it_bkpf into wa_bkpf
        with table key bukrs = wa_book-bukrs
                       belnr = l_belnr_inv
                       gjahr = l_gjahr_inv.
      if sy-subrc is initial and
         wa_bkpf-awtyp       = mm_transaction and
         wa_bkpf-awkey+10(4) = l_gjahr_inv.
        " read PO references from MM invoice
*       select ebeln ebelp from rseg
*         into corresponding fields of table itab_rseg
*         where belnr = wa_bkpf-awkey(10) and
*               gjahr = wa_bkpf-awkey+10(4).
*       N1718196 - split architcure
        perform select_mm_rseg_po USING    wa_bkpf-awkey
                                  CHANGING itab_rseg[].
        if sy-subrc is initial.
          loop at itab_rseg
             where not ebeln is initial.

            " collect PO items
            tab_gtd_pos-ebeln = itab_rseg-ebeln.
            tab_gtd_pos-ebelp = itab_rseg-ebelp.
            collect tab_gtd_pos.

            " collect PO headers
            tab_gtd_header-ebeln = itab_rseg-ebeln.
            collect tab_gtd_header.
          endloop.
          sort tab_gtd_header by ebeln.
        endif.
      endif.
      " update sy-subrc
      read table tab_gtd_header index 1.
    endif.
*   end of N1343104

    if sy-subrc = 0.
      wa_book-mm_inv_ebln = tab_gtd_header-ebeln.
    endif.

*---  TAB_GTD_HEADER--------------------------------------------------*
* Header Purchase Order: Num export data
*----------------------------------------------------------------------
    loop at tab_gtd_header.
* First read Customs Declaration Number and Country of Origin from the header:
* that is relevant if all the items are covered by the same declaration
      clear tab_gtd.
*     select single b~voiso b~vornu b~exnum b~kennu b~vorda b~behoe
*       into (tab_gtd-voiso, tab_gtd-vornu, tab_gtd_header-exnum,
*             tab_gtd-kennu, tab_gtd-vorda, tab_gtd-behoe)
*       from ekko as a inner join eikp as b on a~exnum = b~exnum
*       where a~ebeln = tab_gtd_header-ebeln.
*     N1718196 - split architcure
      perform select_mm_ekko_eikp using    tab_gtd_header-ebeln
                                  changing tab_gtd
                                           tab_gtd_header-exnum.
      if sy-subrc = 0.
        wa_book-mm_inv_voiso = tab_gtd-voiso.
        "N1334140 - get GTD from MM
        PERFORM get_gtd_from_mm
           USING    tab_gtd
           CHANGING l_long_gtd.
      endif.
* Then read Customs Declaration Number and Country of Origin from the line item:
* that is relevant if different items are covered by different declarations
      loop at tab_gtd_pos where ebeln = tab_gtd_header-ebeln.
        clear tab_gtd.
*       select single voiso vornu kennu vorda behoe
*         into (tab_gtd-voiso, tab_gtd-vornu, tab_gtd-kennu, tab_gtd-vorda,
*               tab_gtd-behoe)
*         from eipo
*         where exnum = tab_gtd_header-exnum and
*               expos = tab_gtd_pos-ebelp.
*       N1718196 - split architcure
        perform select_mm_eipo using    tab_gtd_header-exnum
                                        tab_gtd_pos-ebelp
                               changing tab_gtd.
        if sy-subrc = 0.
          "N1334140 - get GTD from MM
          PERFORM get_gtd_from_mm
             USING    tab_gtd
             CHANGING l_long_gtd.
        endif.
      endloop.
    endloop.
*
* SELECT Date of Goods Receipt
    refresh: itab_ekbe, lt_ekbe.
    if not tab_gtd_pos[] is initial.
*     select * from ekbe
*       into corresponding fields of table lt_ekbe
*       for all entries in tab_gtd_pos
*       where ebeln = tab_gtd_pos-ebeln and
*             ebelp = tab_gtd_pos-ebelp.
*     N1718196 - split architcure
      perform select_mm_ekbe using    tab_gtd_pos[]
                             changing lt_ekbe[].
      loop at lt_ekbe into ls_ekbe.
        move-corresponding ls_ekbe to itab_ekbe.
        append itab_ekbe.
      endloop.
    endif.

*   1733508 - search GR for invoice, if not found then search original for correction
    clear l_found_gr.
    do 2 times.
*     corrections
      check l_found_gr is initial.
      if sy-index = 2.
        check wa_book-corr_type = j3rdl_corr_flag_corr   OR
              wa_book-corr_type = j3rdl_corr_flag_corr_rev.
        check wa_book-belnr_orig_inv <> space  AND
              wa_book-belnr_orig_inv <> l_belnr_inv.
        l_belnr_inv = wa_book-belnr_orig_inv.
        l_gjahr_inv = wa_book-gjahr_orig_inv.
*       read original invoice
        read table it_bkpf into ls_bkpf_tmp
          with table key bukrs = wa_bkpf-bukrs
                         belnr = l_belnr_inv
                         gjahr = l_gjahr_inv.
        if sy-subrc <> 0.
          SELECT SINGLE
            bukrs gjahr belnr bktxt xblnr awtyp awkey stblg stjah xreversal monat blart bldat budat waers vatdate bstat cpudt awsys tcode stgrd wwert kursf hwaer glvor usnam cputm
            FROM bkpf
            INTO CORRESPONDING FIELDS OF ls_bkpf_tmp
            WHERE bukrs = wa_bkpf-bukrs AND
                  belnr = l_belnr_inv   AND
                  gjahr = l_gjahr_inv.
          check sy-subrc = 0.
          insert ls_bkpf_tmp into table it_bkpf.
        endif.
      endif.

* Check if BEWTP = 'R'-[RE] Invoice receipt
    read table itab_ekbe with key belnr = l_belnr_inv
                                  bewtp = 'R'
      transporting no fields.
    if sy-subrc = 0.
      flag_re = 'X'.
    endif.
* MM invoices : case of service preparation      LERF
    read table itab_ekbe with key bewtp = 'D'
      transporting no fields.
    if sy-subrc = 0.
      flag_lerf = 'X'.
    endif.
* MM invoices : case of control MM               RE-L
    read table itab_ekbe with key bewtp = 'Q'
      transporting no fields.
    if sy-subrc = 0.
      flag_re_l = 'X'.
    endif.

    if flag_re = 'X'.
      refresh wa_ekbe.
      loop at itab_ekbe where belnr = l_belnr_inv and
                              bewtp = 'R'.
        move-corresponding itab_ekbe to wa_ekbe.
        append wa_ekbe.
      endloop.

      loop at wa_ekbe.
        read table itab_ekbe with key lfbnr = wa_ekbe-lfbnr
                                      bewtp = 'E'
                                      shkzg = 'S'.
        if sy-subrc = 0.
          if wa_book-ekbe_belnr_mm is initial or
             wa_book-mm_inv_budat < itab_ekbe-budat.
            wa_book-mm_inv_budat  = itab_ekbe-budat.
          endif.
          wa_book-ekbe_belnr_mm = itab_ekbe-lfbnr.
          l_found_gr = 'X'.
        endif.
      endloop.
      clear: flag_re.
      refresh: itab_ekbe, wa_ekbe.
    endif.

* MM invoices : Delivery Number, Date of Goods Receipt with GR-based
    if flag_re_l = 'X'.
      refresh wa_ekbe.
      loop at itab_ekbe where bewtp = 'Q'.
        move-corresponding itab_ekbe to wa_ekbe.
        append wa_ekbe.
      endloop.

      read table it_bkpf into wa_bkpf
        with table key bukrs = wa_book-bukrs
                       belnr = l_belnr_inv
                       gjahr = l_gjahr_inv.
      check wa_bkpf-awtyp       = mm_transaction and
            wa_bkpf-awkey+10(4) = l_gjahr_inv.
      loop at wa_ekbe where belnr = wa_bkpf-awkey(10).
        wa_ekbe-fibnr = l_belnr_inv.
        modify wa_ekbe.
        read table itab_ekbe with key belnr = wa_ekbe-lfbnr
                                      bewtp = 'E'
                                      shkzg = 'S'.
        if sy-subrc = 0.
          if wa_book-ekbe_belnr_mm is initial or
             wa_book-mm_inv_budat < itab_ekbe-budat.
            wa_book-mm_inv_budat  = itab_ekbe-budat.
          endif.
          wa_book-ekbe_belnr_mm = itab_ekbe-belnr.
          l_found_gr = 'X'.
        endif.
      endloop.
      clear: flag_re_l.
    endif.

* Service entry
    if flag_lerf = 'X'.
      refresh wa_invv. clear wa_invv.   "N1554650
      delete wa_ekbe where fibnr = space. clear wa_ekbe.
      delete adjacent duplicates from wa_ekbe.

*     1702300 - process reversals of service entries
      refresh lt_ekbe_rev.
      loop at tab_gtd_pos.
*       collect service entries
        refresh lt_lfbnr.
        loop at lt_ekbe into ls_ekbe
                        where ebeln = tab_gtd_pos-ebeln and
                              ebelp = tab_gtd_pos-ebelp and
                              bewtp = 'D'               and
                              lfbnr <> space.
          collect ls_ekbe-lfbnr into lt_lfbnr.
        endloop.

*       1761164 - delete non-accepted entries
        loop at lt_lfbnr into l_lfbnr.
          l_tabix = sy-tabix.
          perform select_mm_essr using    l_lfbnr
                                 changing ls_essr.
          if sy-subrc = 0          and
             ls_essr-kzabn <> 'X'.
            delete lt_lfbnr index l_tabix.
          endif.
        endloop.

*       process service entries
        loop at lt_lfbnr into l_lfbnr.

*         collect goods receipts for the service entry
          clear: lt_ekbe_tmp[], l_found.
          loop at lt_ekbe into ls_ekbe
                          where ebeln = tab_gtd_pos-ebeln and
                                ebelp = tab_gtd_pos-ebelp and
                                lfbnr = l_lfbnr           and
                                bewtp = 'E'.
            collect ls_ekbe into lt_ekbe_tmp.
            if ls_ekbe-shkzg = 'H'.
*              ls_ekbe-xwsbr = 'X'.   " 1761164
              l_found = 'X'. " reversal is found
            endif.
          endloop.
          check l_found = 'X'.

*         search full reversals
          clear ls_ekbe_rev.
          loop at lt_ekbe_tmp into ls_ekbe.
            l_tabix = sy-tabix.

            if ls_ekbe-shkzg = 'S'.
              if ( ls_ekbe_rev-belnr is initial or
                   ls_ekbe_rev-belnr <> ls_ekbe-belnr or
                   ls_ekbe_rev-gjahr <> ls_ekbe-gjahr ).
                l_tabix1    = l_tabix.
                ls_ekbe_rev = ls_ekbe.
              endif.
            elseif ls_ekbe-shkzg = 'H'                  and
*                  ls_ekbe-xwsbr = 'X'                  and  " 1761164
                   ls_ekbe_rev-belnr <> space           and
                   ( ls_ekbe_rev-belnr <> ls_ekbe-belnr or
                     ls_ekbe_rev-gjahr <> ls_ekbe-gjahr ).
*             check exact match
              l_tabix2 = l_tabix - 1.
              clear l_found.
              loop at lt_ekbe_tmp into ls_ekbe2 from l_tabix1 to l_tabix2.
                read table lt_ekbe_tmp into ls_ekbe3
                  with key belnr = ls_ekbe-belnr
                           gjahr = ls_ekbe-gjahr
                           buzei = ls_ekbe2-buzei.     " search same position
                if sy-subrc      <> 0              or  " if not found
                  ls_ekbe2-bwart = ls_ekbe3-bwart  or  " the same movement
                  ls_ekbe2-menge <> ls_ekbe3-menge or  " not the same quantity
                  ls_ekbe2-dmbtr <> ls_ekbe3-dmbtr.    " not the same amount
                  l_found = 'E'.                       " then set to error
                  exit.
                endif.
                l_found = 'X'. " record matched
              endloop.
*             if record found then collect reversed document
              if l_found = 'X'.
                append ls_ekbe_rev to lt_ekbe_rev.
              endif.
            else.
              clear ls_ekbe_rev-belnr.
            endif.
          endloop.
        endloop.
      endloop.
      sort lt_ekbe_rev by belnr ebeln ebelp lfbnr.

      loop at itab_ekbe where bewtp = 'E' and
                              shkzg = 'S'.
*       1702300 - ignore reversed documents
        if not lt_ekbe_rev is initial.
          read table lt_ekbe_rev transporting no fields
            binary search
            with key belnr = itab_ekbe-belnr
                     ebeln = itab_ekbe-ebeln
                     ebelp = itab_ekbe-ebelp
                     lfbnr = itab_ekbe-lfbnr.
          check sy-subrc <> 0.
        endif.

        read table wa_ekbe with key lfbnr = itab_ekbe-lfbnr.
        if sy-subrc = 0.
          wa_invv-lfbnr = itab_ekbe-belnr.
          wa_invv-budat = itab_ekbe-budat.
          collect wa_invv.
        endif.
      endloop.
*     N1552655 - get the latest acceptance act
      sort wa_invv by budat descending lfbnr descending.
      read table wa_invv index 1.
      read table itab_ekbe with key belnr = wa_invv-lfbnr
                                    bewtp = 'E'
                                    shkzg = 'S'.
      if sy-subrc = 0.
        wa_book-mm_inv_budat  = itab_ekbe-budat.
        wa_book-ekbe_belnr_mm = itab_ekbe-belnr.
        l_found_gr = 'X'.
      endif.
      clear flag_lerf.
    endif.
    enddo. " 1733508

    PERFORM remove_gtd_duplicates CHANGING l_long_gtd. " 1924130

    wa_book-mm_inv_vornu = l_long_gtd. "N1326787
    modify ct_book from wa_book.
  endloop.
endform.                                                   "mm_belnr_old
*----------------------------------------------------------------------*
*       FORM SELECT_ADDR
*----------------------------------------------------------------------*
form select_addr using name1 like addr1_val-name1
                       name2 like addr1_val-name2
                       name3 like addr1_val-name3
                       name4 like addr1_val-name4
                       name1_r like addr1_val-name1
                       name2_r like addr1_val-name2
                       name3_r like addr1_val-name3
                       name4_r like addr1_val-name4
                       value(in_bukrs).

  data: adrnr_tmp like t001-adrnr,
        butxt_tmp like t001-butxt.
  data: addr_selection like addr1_sel,
        addr_value     like addr1_val.

  select single adrnr butxt into (adrnr_tmp, butxt_tmp)
  from t001 where bukrs = in_bukrs.
  if sy-subrc = 0.
    name1   = butxt_tmp.
    name1_r = butxt_tmp.
    clear addr_selection-nation.
    addr_selection-addrnumber = adrnr_tmp.
    call function 'ADDR_GET'
      exporting
        address_selection = addr_selection
      importing
        address_value     = addr_value
      exceptions
        address_not_exist = 2
        version_not_exist = 3
        others            = 5.
    if sy-subrc = 0.
      name1 = addr_value-name1.
      name2 = addr_value-name2.
      name3 = addr_value-name3.
      name4 = addr_value-name4.
      name1_r = name1.
      name2_r = name2.
      name3_r = name3.
      name4_r = name4.
    endif.
    if s_adr = 'X'.
      addr_selection-nation = 'R'.
      call function 'ADDR_GET'
        exporting
          address_selection = addr_selection
        importing
          address_value     = addr_value
        exceptions
          address_not_exist = 2
          version_not_exist = 3
          others            = 5.
      if sy-subrc = 0.
        name1_r = addr_value-name1.
        name2_r = addr_value-name2.
        name3_r = addr_value-name3.
        name4_r = addr_value-name4.
      endif.
    endif.
  endif.
endform.                                                    "select_addr
*&---------------------------------------------------------------------*
*&      Form  PRINT_LEGAL_FORM
*&---------------------------------------------------------------------*
form print_legal_form.
  perform prepare_interface.

  free p_book. "note 2140917

  if p_adobe = 'X'.
    perform call_adobe.
  elseif p_xml = 'X'.
    perform call_xml.
  endif.
endform.                    " PRINT_LEGAL_FORM
*&---------------------------------------------------------------------*
*&      Form  prepare_interface
*&---------------------------------------------------------------------*

form prepare_interface.
  data: name1 like addr1_val-name1,
        name2 like addr1_val-name2,
        name3 like addr1_val-name3,
        name4 like addr1_val-name4,
        name1_r like addr1_val-name1,
        name2_r like addr1_val-name2,
        name3_r like addr1_val-name3,
        name4_r like addr1_val-name4.

  data: wa_book  type j_3rbuy,
        wa_buff  type ty_adobe_line,
        wa_total type ty_adobe_total.

  data: it_customs type table of string.
  data: l_len      type i,
        l_str      like line of it_customs.
  data: num_line_add(10) type n value '1'.
  data: l_value    type string.
  DATA: l_monat_low    type bkpf-monat,           " 2056387
        l_monat_high   type bkpf-monat,           " 2056387
        ls_add_quart_r like line of ad_monat[].   " 2056387
  " maximum length of BKPF-XBLNR
  data: max_xblnr_len type i value 16,  " 2099024
        l_long_gtd    type string.      " 2099024
  describe field wa_book-xblnr_inv  length max_xblnr_len in character mode.
  describe field wa_buff-ext_number length l_len         in character mode.
  if max_xblnr_len >= l_len.
    clear max_xblnr_len. " do not read external texts
  endif.

  refresh: buff_tab, buff_tab_add.
  clear: in_totals.

  num_line_add = ad_doc_n.  "N1472064
  if num_line_add is initial.
    num_line_add = 1.
  endif.

  loop at p_book_alv1 into wa_book. " where belnr_add eq space.
    clear wa_buff.
    move-corresponding wa_book to wa_buff.       "#EC ENHOK

    "N1351387: fill FLG_BELNR_TRN for new format only
    clear wa_buff-flg_belnr_trn.
    if ( wa_book-belnr_add EQ space AND p_dp_bas NE space ) OR
       ( wa_book-belnr_add NE space AND p_dp_add NE space ).
      wa_buff-flg_belnr_trn = wa_book-flg_belnr_trn.
      if wa_buff-flg_belnr_trn = 'A'.
        clear wa_buff-mm_inv_vornu." no gtd
        clear wa_buff-hwbas2_trn.  " no totals
        clear wa_buff-hwbas5_trn.
* Note 2003630
        clear wa_buff-hwbas3_trn.
        clear wa_buff-hwbas4_trn.
      endif.
    endif.

    "N1362831 - get long external number
    l_len = STRLEN( wa_buff-ext_number ).
    if p_extnum EQ 'X'           and
       wa_book-journal IS INITIAL and " 1711413 - do not overwrite journal data
       lcl_buy_book=>if_ext_number( is_book  = wa_book     " 1802867
                                    i_xblnr  = p_xblnr
                                    i_xblnr1 = p_xblnr1
                                    i_xblnr2 = p_xblnr2 ) = 'X' AND
       l_len    EQ max_xblnr_len and
       par_ext  EQ 'X'           and
       par_extn NE space         and
       max_xblnr_len > 0.      " 2099024 - do not load texts.

      PERFORM read_texts
         USING wa_book-bukrs
               wa_book-belnr_inv
               wa_book-gjahr_inv
               par_extn
         CHANGING l_value.
      if l_value NE space.
        wa_buff-ext_number = l_value.
      endif.
    endif.

* Replace ; with space for better output in PDF form
    if not wa_buff-mm_inv_vornu is initial and
       p_xml is initial.
      refresh it_customs.
      split wa_buff-mm_inv_vornu at ';' into table it_customs.
      concatenate lines of it_customs into wa_buff-mm_inv_vornu separated by space.
    endif.

    "N1326787: for long GTDs - load directly from the database
    refresh it_customs.
    l_len = STRLEN( wa_buff-mm_inv_vornu ).
    if p_lnggtd EQ 'X' AND l_len > max_gtd_len.
      perform get_long_gtd
        tables  it_customs
        using   wa_book
        changing l_long_gtd. " 2099024
*     2099024 - use full string
*     read table it_customs into l_str index 1.
*     if sy-subrc IS INITIAL.
*       wa_buff-mm_inv_vornu = l_str.
*     endif.
      refresh it_customs.
      wa_buff-mm_inv_vornu = l_long_gtd.
    endif.

    "N1329299: print actual payment date
    IF p_actpay EQ 'X'.
      wa_buff-bldat_pay = wa_book-bldat_origpay.
    ENDIF.

*   1695097 - re-arrange columns for correction and revision invoices
    IF ver2012 = 'X'.
      PERFORM adobe_fill_inv USING    wa_book
                             CHANGING wa_buff.
    ENDIF.

    if wa_book-belnr_add eq space.
      append wa_buff to buff_tab.
    else.
      "N1334957 - always renumerate additional sheets
      wa_buff-num_line = num_line_add.
      num_line_add     = num_line_add + 1.

      append wa_buff to buff_tab_add.
    endif.

    "N1326787: now append GTD rows
    if not it_customs is initial.
      clear wa_buff.
      loop at it_customs into l_str from 2.
        wa_buff-mm_inv_vornu = l_str.
        if wa_book-belnr_add eq space.
      append wa_buff to buff_tab.
    else.
      append wa_buff to buff_tab_add.
        endif.
      endloop.
    endif.
  endloop.

* The code below has been moved to form FILL_ADD_FIELDS
*  loop at p_book_alv1 into wa_book where belnr_add ne space.
*    if ( wa_book-flag_add = 'X' ) and
*       ( wa_book-belnr_inv = wa_book-belnr_trn ).
*      wa_book-belnr_inv = wa_book-belnr_add.
**      wa_pbook-xblnr_inv = wa_pbook-xblnr_test.
*    endif.
*    move-corresponding wa_book to wa_buff.
*    append wa_buff to buff_tab_add.
*  endloop.

  select single * from t001  where bukrs = mainbuk.
* INN
  select single paval into buff_header-inn
    from t001z where bukrs = mainbuk and party = 'SAPR01'.
* KPP
  select single paval into buff_header-kpp
    from t001z where bukrs = mainbuk and party = 'SAPR10'.
* Chief Accountant
  if ver2012 is initial. " 1969939 - signature is chief accountant
  select single paval into buff_header-chief_accountant
    from t001z where bukrs = mainbuk and party = 'SAPR15'.
  else.                  " 1969939 - signature is CEO
  select single paval into buff_header-chief_accountant
    from t001z where bukrs = mainbuk and party = 'SAPR13'.
  endif.

  buff_header-gjahr       = br_gjahr-low.
  buff_header-budat_begin = br_budat-low.
  buff_header-budat_end   = br_budat-high.
  PERFORM get_calendar_period USING    sel_mona-low
                              CHANGING buff_header-monat_begin.
  PERFORM get_calendar_period USING    sel_mona-high
                              CHANGING buff_header-monat_end.

* 1695097 - format from..  to.. dates
  if ver2012 = 'X'.
    PERFORM adobe_period CHANGING buff_header-gjahr
                                  buff_header-monat_begin
                                  buff_header-monat_end
                                  buff_header-budat_begin
                                  buff_header-budat_end.
  endif.

  perform select_addr using name1
                            name2
                            name3
                            name4
                            name1_r
                            name2_r
                            name3_r
                            name4_r
                            mainbuk.
  if s_adr <> 'X'.
* Check if the Russian version of address is requested
    concatenate name1 name2 name3 name4
      into buff_header-company_name separated by space.
  else.
    concatenate name1_r name2_r name3_r name4_r
      into buff_header-company_name separated by space.
  endif.

  if not buff_tab_add is initial.
    move-corresponding buff_header to buff_header_add.
    buff_header_add-gjahr       = ad_gjahr-low.
    buff_header_add-budat_begin = ad_budat-low.
    buff_header_add-budat_end   = ad_budat-high.
    IF buff_header_add-budat_begin IS INITIAL AND
       buff_header_add-budat_end   IS INITIAL AND
       NOT ad_vtdat-low IS INITIAL AND
       NOT ad_vtdat-high IS INITIAL.
      buff_header_add-budat_begin = ad_vtdat-low.
      buff_header_add-budat_end   = ad_vtdat-high.
    ENDIF.

    l_monat_low  = ad_monat-low.
    l_monat_high = ad_monat-high.
*   2056387 - tax period from quarter
    if not p_quart       is initial AND
       not add_quart_r[] is initial AND
       p_xml             is initial.  " for XML - use months
      loop at add_quart_r into ls_add_quart_r.
        l_monat_low  = ls_add_quart_r-low.
        l_monat_high = ls_add_quart_r-high.
        exit.
      endloop.
    endif.

    PERFORM get_calendar_period USING    l_monat_low  " 2056387
                                CHANGING buff_header_add-monat_begin.
    PERFORM get_calendar_period USING    l_monat_high  " 2056387
                                CHANGING buff_header_add-monat_end.
    IF NOT buff_header_add-monat_begin IS INITIAL AND  " 2056387
       buff_header_add-monat_end IS INITIAL.
      buff_header_add-monat_end = buff_header_add-monat_begin.
    ENDIF.
    buff_header_add-ad_sheet    = ad_sheet.
    buff_header_add-date_output = ad_date.
    move-corresponding gs_totals to in_totals.       "#EC ENHOK
  endif.
  buff_header-decree451     = p_dp_bas.
  buff_header_add-decree451 = p_dp_add.
endform.                    " prepare_interface

*&---------------------------------------------------------------------*
*&      Form  call_adobe
*&---------------------------------------------------------------------*
form call_adobe.
  data: languages(60) type c,
        w_cx_root     type ref to cx_root,
        mesg          type string.

* Get the name of the generated function module
  try.
    call function 'FP_FUNCTION_MODULE_NAME'
      exporting
        i_name     = pa_adobe
      importing
        e_funcname = fm_name.
  catch cx_static_check into w_cx_root.
      mesg = w_cx_root->get_text( ).
      write:/ mesg.
  endtry.

* Set default output parameters
  perform set_default_output_params.

* verify form interface
  PERFORM check_adobe_interface USING fm_name.

* Open job
  call function 'FP_JOB_OPEN'
    changing
      ie_outputparams = fp_outputparams
    exceptions
      cancel          = 1
      usage_error     = 2
      system_error    = 3
      internal_error  = 4
      others          = 5.
  if sy-subrc <> 0.
    message id sy-msgid type sy-msgty number sy-msgno
            with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  endif.

* N1441947 - set PDF archiving parameters
  if fp_outputparams-arcmode = '2' or
     fp_outputparams-arcmode = '3'.
    perform set_archiving_params
            using    fp_outputparams
                     pa_adobe
            changing fp_docparams.
  endif.

* Check if the Russian language is installed
  call function 'SYSTEM_INSTALLED_LANGUAGES'
    importing
      languages       = languages
    exceptions
      sapgparam_error = 1
      others          = 2.
  if sy-subrc = 0.
    find first occurrence of 'R' in languages.
    if sy-subrc = 0.
      fp_docparams-langu = 'R'.
      fp_docparams-country = 'RU'.
    endif.
  endif.

  try.
* Split and process basic sheets
    perform print_form using buff_header buff_tab pages_per_package abap_true.
* Split and process additional sheets
    perform print_form using buff_header_add buff_tab_add pages_per_package abap_false.
  catch cx_dynamic_check into w_cx_root.
      mesg = w_cx_root->get_text( ).
      write:/ mesg.
  endtry.

* Close job
  call function 'FP_JOB_CLOSE'
    exceptions
      usage_error    = 1
      system_error   = 2
      internal_error = 3
      others         = 4.
  if sy-subrc <> 0.
    message id sy-msgid type sy-msgty number sy-msgno
            with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  endif.

* Free memory
  free: buff_header, buff_tab, buff_header_add, buff_tab_add, in_totals.
endform.                    " call_adobe

*&--------------------------------------------------------------------*
*&      Form  TEXT_UPDATE
*&--------------------------------------------------------------------*
form text_update using value(v_par) r_text like disvariant-text.

  data: ls_d020s like d020s.
  data: lt_dynpread like dynpread occurs 1 with header line.

  ls_d020s-prog = sy-cprog.
  ls_d020s-dnum = sy-dynnr.
  clear lt_dynpread.
  refresh lt_dynpread.
  lt_dynpread-fieldname  = v_par.

  lt_dynpread-fieldvalue = r_text.
  append lt_dynpread.

  call function 'DYNP_VALUES_UPDATE'
    exporting
      dyname     = ls_d020s-prog
      dynumb     = ls_d020s-dnum
    tables
      dynpfields = lt_dynpread.
endform.                    " TEXT_UPDATE

*&--------------------------------------------------------------------*
*&      Form  clear_num_tab
*&--------------------------------------------------------------------*
*       text
*---------------------------------------------------------------------*
form delete_num_data
  using i_burks like bkpf-bukrs
        i_laufd like umsv-laufd
        i_laufi like j_3rbue_book_num-laufi.

  data: answer.
  if i_laufd is initial.
    message e899(f4) with text-950.
  endif.
  if i_laufi = space.
    message e899(f4) with text-951.
  endif.

  call function 'POPUP_TO_CONFIRM_LOSS_OF_DATA'         "#EC FB_OLDED
    exporting
      textline1     = text-953
      titel         = text-954
      defaultoption = 'N'
    importing
      answer        = answer.
  if answer eq 'J'.
* delete data from tables
    delete from j_3rbue_book_ind client specified
     where mandt = sy-mandt and
           laufd = i_laufd and
           laufi = i_laufi.
    if sy-subrc <> 0.
      message e899(f4) with text-955.
    endif.
*
    delete from j_3rbue_book_num client specified
      where mandt = sy-mandt and
            laufd = i_laufd and
            laufi = i_laufi.
  endif.
endform.                                                  "clear_num_tab
*&---------------------------------------------------------------------*
*&      Form  f4_mainbuk
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form f4_mainbuk.
  data: fc type slis_t_fieldcat_alv,
        ls type slis_fieldcat_alv,
        gs_selfield type slis_selfield,
        g_exit(1)   type c.

  data: begin of ty_outtab,
            burks like t001-bukrs,
            butxt like t001-butxt,
        end of ty_outtab.

  data: outtab_trn like table of t001 with header line.

  refresh fc[] .

  clear ls.
  ls-fieldname     = 'BUKRS'.
  ls-ref_tabname   = 'T001'.
  ls-ref_fieldname = 'BUKRS'.
  append ls to fc.

  clear ls.
  ls-fieldname     = 'BUTXT'.
  ls-ref_tabname   = 'T001'.
  ls-ref_fieldname = 'BUTXT'.
  append ls to fc.

  select * into table outtab_trn
    from t001
    where bukrs in br_bukrs.

  if sy-subrc = 0.
    call function 'REUSE_ALV_POPUP_TO_SELECT'
      exporting
        i_tabname     = '1'
        it_fieldcat   = fc
      importing
        es_selfield   = gs_selfield
        e_exit        = g_exit
      tables
        t_outtab      = outtab_trn[]
      exceptions
        program_error = 1
        others        = 2.
    if sy-subrc <> 0.
      message id sy-msgid type sy-msgty number sy-msgno
              with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    endif.

    if g_exit ne 'X'.
      read table outtab_trn index gs_selfield-tabindex.
      mainbuk = outtab_trn-bukrs.
    endif.
  endif.
endform.                                                    "f4_mainbuk
*----------------------------------------------------------------------*
* FORM compress_data
*----------------------------------------------------------------------*
form compress_data.
  field-symbols: <fs_book1> type j_3rbuy.

  if par_col  = 'X'.
    loop at p_book.
      "N1357363 - 0% tax transfers
      if p_book-dmbtr_wrs_trn is initial and
         ( not p_book-fwbas3_wrs_trn is initial OR
           not p_book-hwbas4_wrs_trn is initial ).
*       1626029 - non taxable items
        if p_book-fwbas3_wrs_trn is initial.
          p_book-fwbas3_wrs_trn = p_book-hwbas4_wrs_trn.
        endif.
        p_book-dmbtr_wrs_trn = p_book-fwbas3_wrs_trn.

        if p_book-bldat_trn is initial.
          p_book-bldat_trn = p_book-bldat_inv.
        endif.
        if p_book-budat_trn is initial.
          p_book-budat_trn = p_book-budat_inv.
        endif.
        if p_book-vatdate_trn is initial.
          p_book-vatdate_trn = p_book-vatdate_inv.
        endif.
        if p_book-monat_trn is initial and
           not p_book-budat_trn is initial.
          " get monat
          CALL FUNCTION 'GET_CURRENT_YEAR'
           EXPORTING
             BUKRS         = p_book-bukrs
             DATE          = p_book-budat_trn
           IMPORTING
             CURRM         = p_book-monat_trn.
        endif.
      endif.
      "end of N1357363
      if p_book-fwbas_wrs_trn is initial.
        p_book-fwbas_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-hwbas_wrs_trn is initial.
        p_book-hwbas_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-hwbas2_wrs_trn is initial.
        p_book-hwbas2_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-fwbas2_wrs_trn is initial.
        p_book-fwbas2_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-fwbas2_wrs_trn is initial.
        p_book-fwbas2_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-hwbas3_wrs_trn is initial.
        p_book-hwbas3_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-fwbas3_wrs_trn is initial.
        p_book-fwbas3_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-hwbas4_wrs_trn is initial.
        p_book-hwbas4_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-fwbas4_wrs_trn is initial.
        p_book-fwbas4_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-hwbas5_wrs_trn is initial.
        p_book-hwbas5_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-fwbas5_wrs_trn is initial.
        p_book-fwbas5_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-hwste_wrs_trn is initial.
        p_book-hwste_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-fwste_wrs_trn is initial.
        p_book-fwste_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-hwste2_wrs_trn is initial.
        p_book-hwste2_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-fwste2_wrs_trn is initial.
        p_book-fwste2_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-hwste3_wrs_trn is initial.
        p_book-hwste3_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-fwste3_wrs_trn is initial.
        p_book-fwste3_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-hwste5_wrs_trn is initial.
        p_book-hwste5_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-fwste5_wrs_trn is initial.
        p_book-fwste5_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.
      if p_book-wrbtr_wrs_trn is initial.
         p_book-wrbtr_wrs_trn = p_book-dmbtr_wrs_trn.
      endif.

************************************************************************
* Filling p_book_shadow for further Custom declaration number processing
************************************************************************
      modify p_book index sy-tabix.

      clear p_book-basgruno.
      clear p_book-stegruno.
      clear p_book-buzei_test.
      "clear p_book-flg_belnr_trn. "N1351387
      clear p_book-bktxt_trn.
      clear p_book-mwskz.
      clear p_book-lights.
      clear p_book-box.
      clear p_book-flg_belnr_pay.
      clear p_book-flg_belnr_inv.
      clear p_book-flg_xragl_pay.
      clear p_book-flg_belnr_all.
      clear p_book-mm_inv_ebln.
      clear p_book-mm_inv_ebelp.
      clear p_book-mm_inv_voiso.
      clear p_book-mwskz_inv.
      clear p_book-gjahr_trn.
      clear p_book-butxt.
      clear p_book-paval.
      clear p_book-dat_begin.
      clear p_book-dat_end.
      clear p_book-ekbe_belnr_mm.
      clear p_book-xref1_inv.
      clear p_book-xref1_dat.
      clear p_book-hkont_inv.
      clear p_book-hkont_trn.
      clear p_book-hkont_cln.
*     clear p_book-usnam_inv.  " 1757376
      clear p_book-usnam_pay.
      clear p_book-usnam_pay_fct.
*      CLEAR p_book-col22.
*      CLEAR p_book-flg_tax_sum.
*      CLEAR p_book-flg_belnr_pay_storno.
*      CLEAR p_book-flg_vornu.
*      CLEAR p_book-tmp_zuonr_belnr.
*      CLEAR p_book-tmp_zuonr_gjahr.
      clear p_book-glvor.
*      CLEAR p_book-reserv.
      clear p_book-belnr_trn.
      clear p_book-gsber_trn.
      clear p_book-one_time_acc.
      clear p_book-buzei_add.  "N1346018
      clear p_book-xblnr_test. "N1909868

      "N1345674 - clear numbering - should be regenerated
      if not p_book-belnr_add is initial OR
         num_auto = 'X'.
        clear p_book-num_line.
      endif.
      if p_book-flg_belnr_trn NE 'A'.
        clear p_book-flg_belnr_trn.
      endif.
*     1909868 - group tax transfers in additional sheet
      if not p_book-belnr_add is initial.
*       search the same invoice in additional sheet (use index)
        loop at p_book_alv1 assigning <fs_book1>
                            where bukrs         = p_book-bukrs
                              and belnr_inv     = p_book-belnr_inv
                              and xblnr_inv     = p_book-xblnr_inv
                              and bldat_inv     = p_book-bldat_inv
                              and budat_inv     = p_book-budat_inv
                              and dmbtr_inv     = p_book-dmbtr_inv
                              and dmbtr_wrs_inv = p_book-dmbtr_wrs_inv
                              and wrbtr_inv     = p_book-wrbtr_inv
                              and wrbtr_wrs_inv = p_book-wrbtr_wrs_inv
                              and bktxt_inv     = p_book-bktxt_inv
                              and belnr_pay     = p_book-belnr_pay
                              and xblnr_pay     = p_book-xblnr_pay
                              and bldat_pay     = p_book-bldat_pay
                              and budat_pay     = p_book-budat_pay
                              and dmbtr_wrs_pay = p_book-dmbtr_wrs_pay
                              and wrbtr_wrs_pay = p_book-wrbtr_wrs_pay
                              and bktxt_pay     = p_book-bktxt_pay
                              and belnr_origpay = p_book-belnr_origpay.

*         additional checks
          check <fs_book1>-belnr_add      <> space                and
                <fs_book1>-lifnr_cred     = p_book-lifnr_cred     and
                <fs_book1>-belnr_orig_inv = p_book-belnr_orig_inv and
                <fs_book1>-belnr_corr_inv = p_book-belnr_corr_inv.

*         copy fields
          if <fs_book1>-belnr_add <> p_book-belnr_add.
            p_book-belnr_add  = <fs_book1>-belnr_add.
            p_book-gjahr_add  = <fs_book1>-gjahr_add.
            p_book-monat_add  = <fs_book1>-monat_add.
          endif.
          exit.
        endloop.
      endif.
      collect p_book into p_book_alv1.
    endloop.

    "N1345674 - sequential numbering for additional sheets
    data: num_line_add(10) type n.
    field-symbols: <fs_book> type j_3rbuy.

*   renumerate basic sheet
    if num_auto = 'X'.
      num_line_add = num_strt.
      if num_line_add is initial.
        num_line_add = 1.
      endif.
      loop at p_book_alv1 assigning <fs_book>
                     where belnr_add is initial.
        <fs_book>-num_line = num_line_add.
        num_line_add = num_line_add + 1.
      endloop.
    endif.

    num_line_add = ad_doc_n.  "N1472064
    if num_line_add is initial.
      num_line_add = 1.
    endif.
    loop at p_book_alv1 assigning <fs_book>
        where not belnr_add is initial.
      <fs_book>-num_line = num_line_add.
      num_line_add       = num_line_add + 1.
    endloop.
    " end of N1345674
  else.
    p_book_alv1[] = p_book[].
  endif.
  perform compress_gtd.
endform.                    "compress_data
*----------------------------------------------------------------------*
*   FORM check_selection_screen                                        *
*----------------------------------------------------------------------*
form check_selection_screen.

  perform select_variant.
  if not mainbuk in br_bukrs and not mainbuk is initial.
    message e301(j3tse).
  elseif mainbuk is initial.
    mainbuk = br_bukrs-low.
  endif.

  if p_adobe = 'X' and p_load <> 'X'.
    message: e280(f4) .
  endif.
  if p_adobe = 'X' and p_load = 'X'.
    select single name into fpname from fpcontext
      where name = pa_adobe and state = 'A'.
    if sy-subrc ne 0.
      message: e281(f4) .
    endif.
  endif.

  call function 'REUSE_ALV_EXTRACT_AT_SELSCREEN'
    exporting
      i_p_save    = p_save
      i_p_load    = p_load
    changing
      c_p_ex1     = p_ex1
      c_p_ex2     = p_ex2
      c_p_ext1    = p_ext1
      c_p_ext2    = p_ext2
      cs_extract1 = gs_extract1
      cs_extract2 = gs_extract2.

  call function 'REUSE_ALV_EXTRACT_AT_SELSCREEN'
    exporting
      i_p_save    = space
      i_p_load    = space
    changing
      c_p_ex1     = p_ex1
      c_p_ex2     = p_ex1c
      c_p_ext1    = p_ext1
      c_p_ext2    = p_ext1c
      cs_extract1 = gs_extract1
      cs_extract2 = gs_extract1c.

  if p_del = 'X'.
    if p_ex3 is initial.
      message e284(f4).
    else.
      gs_extract3-exname = p_ex3.

      call function 'REUSE_ALV_EXTRACT_EXISTENCE'
        changing
          cs_extract = gs_extract3
        exceptions
          not_found  = 1
          others     = 2.

      if sy-subrc <> 0.
        message e283(f4) with gs_extract3-exname.
      endif.
    endif.
  endif.

* 1902051 for create extract - check extract name
  if p_save = 'X' AND
     gc_customizing-extract_size > 0.
    perform check_extract_name using    gs_extract3
                               changing p_ex1.
  endif.

* check company code
  if p_noex = 'X' or p_save = 'X'.

    if br_bukrs eq space.
      message e899(f4) with text-580.
    endif.

    if br_gjahr eq space.
      message e899(f4) with text-581.
    endif.

    if br_belnr eq space and
       br_budat eq space and
       sel_mona eq space.
      message e899(f4) with text-583.
    endif.
  endif.

  select * into table temp_t001 from t001
    where bukrs in br_bukrs.
  loop at temp_t001 into wa_temp_t001.

    authority-check object 'F_BKPF_BUK'
                id 'BUKRS' field wa_temp_t001-bukrs
                id 'ACTVT' field '03'.
    if sy-subrc <> 0.
      message e800(fr) with wa_temp_t001-bukrs.
    endif.
  endloop.

* Check Flag Numbering 'Active'
  if num_cret <> space.
    perform create_num_data.
* if CHECKs = ok THEN result => X
  endif.
  if num_del <> space.
    perform delete_num_data using mainbuk laufd laufi.
  endif.
* Check code Page var par_cpag.
  if par_cpag = 'X'. set language 'R'. endif.
*----------------------------------------------------------------------
  if br_budat-low  <> '00000000'  and br_budat-high = '00000000'.
    br_budat-high = br_budat-low.
  endif.
  if sel_mona-low  <> space and sel_mona-high = space.
    sel_mona-high = sel_mona-low.
  endif.
*
  if sel_mona-low <> 0 or sel_mona-high <> 0.
    if br_gjahr-low = 0 and br_gjahr-high = 0.
      set cursor field 'BR_GJAHR-LOW'.
      message id 'F7' type 'E' number '246'.
    endif.
  endif.
  if sel_mona-low = 0 and sel_mona-high = 0 and
     sel_bldt-low = 0 and sel_bldt-high = 0 and
     br_budat-low = 0 and br_budat-high = 0 and
     sel_vtdt-low = 0 and sel_vtdt-high = 0 and
     p_load = space.
    message id 'F7' type 'E' number '241'.
  endif.
  if ( sel_ukrs-low <> space or sel_ukrs-high <> space ) and
     ( br_bukrs-low <> space or br_bukrs-high <> space ).
    message id 'F7' type 'E' number '236'.
  endif.
* LC283 -------------------------- *
* Create additional sheets
  if p_add = 'X'.
    if ad_monat-low <> space. " OR ad_monat-high <> space.
      if ad_gjahr-low = space. " AND ad_gjahr-high = space.
        set cursor field 'AD_GJAHR-LOW'.
        message id 'F7' type 'E' number '246'.
      endif.
    else.
      if ad_gjahr-low <> space. " OR ad_gjahr-high <> space.
        set cursor field 'AD_MONAT-LOW'.
        message id 'F7' type 'E' number '241'.
      endif.
    endif.

    if p_sum = 'X' or p_save = 'X'. " OR p_adobe = 'X'.
      if sel_mona-low is initial and br_budat-low is initial.
        set cursor field 'SEL_MONA-LOW'.
        message id 'F7' type 'E' number '239'.
      endif.
      if ad_monat-low is initial and ad_budat-low is initial.
        set cursor field 'AD_MONAT-LOW'.
        message id 'F7' type 'E' number '239'.
      endif.
    endif.

    if ad_budat-low  <> '00000000' and ad_budat-high = '00000000'.
      ad_budat-high = ad_budat-low.
    endif.
    if ad_vtdat-low  <> '00000000' and ad_vtdat-high = '00000000'.
      ad_vtdat-high = ad_vtdat-low.
    endif.
    if ad_monat-low  <> space and ad_monat-high = space.
      ad_monat-high = ad_monat-low.
    endif.

    if p_sum = 'X'.
* Check if single values have been chosen for fiscal years and periods
      p_mode = 0.
      data ln type i.
      if not ( br_budat-low is initial and ad_budat is initial ).
        p_mode = 1.
      endif.
      describe table br_gjahr lines ln.
      if not ( ( br_gjahr-option = 'EQ' ) or
               ( br_gjahr-low = br_gjahr-high ) and
                 br_gjahr-sign = 'I' and ln = 1 ).
        p_mode = 1.
      endif.
      describe table sel_mona lines ln.
      IF NOT ( sel_mona-sign = 'I' AND ln = 1 AND
             ( sel_mona-option = 'EQ' OR
               sel_mona-option = 'BT' ) )." N1294845 - between is allowed
        p_mode = 1.
      endif.
      describe table ad_gjahr lines ln.
      if not ( ( ad_gjahr-option = 'EQ' ) or
               ( ad_gjahr-low = ad_gjahr-high ) and
                 ad_gjahr-sign = 'I' and ln = 1 ).
        p_mode = 1.
      endif.
      describe table ad_monat lines ln.
      if not ( ( ad_monat-option = 'EQ' ) or
               ( ad_monat-low = ad_monat-high ) and
                 ad_monat-sign = 'I' and ln = 1 ).
        p_mode = 1.
      endif.
      " basic and add sheet periods cannot be equal
      " because basic documents will be removed from add sheet
      if br_gjahr-low EQ ad_gjahr-low AND
         sel_mona-low EQ ad_monat-low.
        p_mode = 1.
      endif.
      if p_mode = 1.
        message id '9P' type 'W' number '511'.
        if p_update = 'X'.
          clear p_update.
          message id '9P' type 'W' number '510'.
        endif.
      endif.
    endif. " p_sum = 'X'
  endif. " p_add = 'X'
* LC283 -------------------------- *
  perform pai_of_selection_screen.

* 2120495 - VAT Return checks
  perform pl_check_decl_screen.

* XML checks
  perform check_xml_screen.

* Description Text IDs
  PERFORM get_textids_desc USING    par_extn
                           CHANGING t_extn.
  PERFORM get_textids_desc USING    par_extd
                           CHANGING t_extd.
  PERFORM get_textids_desc USING    par_payn  " 2085792
                           CHANGING t_payn.
  PERFORM get_textids_desc USING    par_payd
                           CHANGING t_payd.

* 2131494 - merge extracts
  PERFORM check_merge_extracts USING 'X'.

endform.                    "check_selection_screen
*----------------------------------------------------------------------*
*      FORM get_documents                                              *
*----------------------------------------------------------------------*
form get_documents
  tables
    ran_belnr structure belnr_ran
    ran_gjahr structure cora_gjahr
    ran_budat structure j_3rf_budat_ran
    ran_xblnr structure bxlnr_ran
    ran_monat structure j_3rf_monat_ran
    ran_bldat structure bldat_ran
    ran_vtdat structure bldat_ran
  changing
    ct_bkpf    type j3rdl_t_bkpf_cache
    ct_bseg    type j3rdl_t_bseg_cache
    ct_doclist type j3rdl_t_doclist_ext.

  data: it_doclist type j3rdl_t_doclist with header line.
  data: wa_doclist like line of ct_doclist.

  refresh: ct_bkpf, ct_bseg, ct_doclist.
  call function 'J_3RF_BUILD_DOCLIST_DTI'
    exporting
      tax_version                = 'PUR'
      i_koart                    = 'K'
      i_chkref                   = chk_ref            "chk ref field
      i_findpayment              = sel_opay           "find real pay
      i_add_sheet                = p_add
      i_decl2012                 = ver2012
      i_decl2014                 = ver2014   " 2074991
      i_params                   = gc_params
    importing
      et_bkpf                    = ct_bkpf[]
      et_bseg                    = ct_bseg[]
      et_doclist                 = it_doclist[]
    tables
      it_bukrs_ran               = br_bukrs
      it_belnr_ran               = ran_belnr "br_belnr
      it_gjahr_ran               = ran_gjahr "br_gjahr
      it_budat_ran               = ran_budat "br_budat
      it_xblnr_ran               = ran_xblnr "br_xblnr
      it_umkrs_ran               = sel_ukrs
      it_monat_ran               = ran_monat "sel_mona
      it_bldat_ran               = ran_bldat "sel_bldt
      it_vtdat_ran               = ran_vtdat "sel_vtdt
      it_mwskz_ran               = sel_mwkz
      it_kunnr_ran               = s_lifnr
      it_blart_ran               = sel_blrt
    exceptions
      county_doesnt_eixts        = 1
      jurisdiction_not_permitted = 2
      no_taxes_exists            = 3
      no_company_codes_exits     = 4
      multiple_countrues         = 5
      others                     = 6.
  if sy-subrc ne 0.
    message id sy-msgid type 'E' number sy-msgno
      with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  endif.
  loop at it_doclist.
    move-corresponding it_doclist to wa_doclist.
    append wa_doclist to ct_doclist.
  endloop.
  free it_doclist.
endform.                                                  "GET_DOCUMENTS
*----------------------------------------------------------------------*
*      FORM main_processing                                            *
*----------------------------------------------------------------------*
form main_processing
  changing
    ct_bkpf    type j3rdl_t_bkpf_cache
    ct_bseg    type j3rdl_t_bseg_cache
    ct_doclist type j3rdl_t_doclist_ext
    ct_totals  type j_3r_t_totals
    ct_book    type j_3r_t_buy.

  data: i_basic(1) type c value 'X'.

* Get documents comprising basic sheets
  perform get_documents
    tables
      br_belnr
      br_gjahr
      br_budat
      br_xblnr
      sel_mona
      sel_bldt
      sel_vtdt
    changing
      ct_bkpf[]
      ct_bseg[]
      ct_doclist[].

  if p_add = 'X'. " create additional sheets
* Look for corrective and reversing documents
* Set BUKRS_ADD, BELNR_ADD, GJAHR_ADD, FLAG_ADD
    perform get_add_documents
      tables
        br_gjahr
        br_budat
        sel_mona
      using
        i_basic
      changing
        ct_bkpf[]
        ct_doclist[].
  endif. " p_add = 'X'

* Process the list of documents to be output in ALV
  perform process_doclist
    using
      ct_bkpf[]
      ct_bseg[]
      ct_doclist[]
    changing
      ct_book[]
      ct_totals[].
endform.                                                "MAIN_PROCESSING
*----------------------------------------------------------------------*
*      FORM get_add_documents                                          *
*----------------------------------------------------------------------*
form get_add_documents
  tables
* Current fiscal period:
* - depends on the place where this subroutine is called, and
* - = current fiscal period when v_basic = 'X'
* - = closed fiscal period when v_basic <> 'X'
    ran_gjahr  structure cora_gjahr
    ran_budat  structure j_3rf_budat_ran
    ran_monat  structure j_3rf_monat_ran
  using
    v_basic   type c
  changing
    ct_bkpf     type j3rdl_t_bkpf_cache
    ct_doclist  type j3rdl_t_doclist_ext.

  data: wa_bkpf_add like line of ct_bkpf,
        wa_bkpf     like line of ct_bkpf,
        wa_bsas     type bsas,
        wa_doclist  like line of ct_doclist,
        l_vatdate   type bkpf-vatdate.

  data: begin of wa_bseg,
          bukrs like bseg-bukrs,
          belnr like bseg-belnr,
          gjahr like bseg-gjahr,
          buzei like bseg-buzei,
          zuonr like bseg-zuonr,
          hkont like bseg-hkont,
          ktosl like bseg-ktosl,
          augdt like bseg-augdt,
          augbl like bseg-augbl,
          xnegp like bseg-xnegp.
  data: end of wa_bseg.

  data: wa_stgrp like t007b-stgrp,
        e_gjahr  type bkpf-gjahr,
        e_monat  type bkpf-monat,
        e_gjahr1 type bkpf-gjahr,
        e_monat1 type bkpf-monat,
        l_tabix  type sy-tabix,
        l_check  type flag,
        l_bldat  type bkpf-bldat,
        l_fgtn   type flag,  " 2037120
        l_belnr_trn type bkpf-belnr,  " 2094488
        l_gjahr_trn type bkpf-gjahr.  " 2094488

* Reversed/corrected documents
  loop at ct_doclist into wa_doclist.
    l_tabix = sy-tabix.

    l_belnr_trn = wa_doclist-belnr_trn.
    l_gjahr_trn = wa_doclist-gjahr_trn.
*   2094488 - for 0% without document - use invoice document
    IF wa_doclist-belnr_trn IS INITIAL.
      CHECK wa_doclist-corr_type IS INITIAL.
      l_belnr_trn = wa_doclist-belnr_inv.
      l_gjahr_trn = wa_doclist-gjahr_inv.
    ENDIF.

    read table ct_bkpf with table key
      bukrs = wa_doclist-bukrs
      belnr = l_belnr_trn
      gjahr = l_gjahr_trn into wa_bkpf.
* Check whether the tax transfer document exists
    check sy-subrc = 0.

* N1301916 - detect xreversal flag for documents imported from v.4.6
    IF NOT wa_bkpf-stblg IS INITIAL AND
       NOT wa_bkpf-stjah IS INITIAL AND
           wa_bkpf-xreversal IS INITIAL.
      PERFORM if_reversed
         USING    wa_bkpf
         CHANGING wa_bkpf-xreversal.
    ENDIF.

*   2037120 - forgotten invoice by document type
    if  ad_blart[] is not initial and
        wa_bkpf-blart in ad_blart.
      wa_doclist-xforgotten = 'X'. " 2037120 - forgotten by prefix in BKTXT
    endif.
    if wa_doclist-xforgotten = 'X'. " remove prefix
      PERFORM remove_forgotten_prefix CHANGING wa_bkpf.
    endif.

*   1711413 - revisions and corections
    IF ver2012 = 'X' AND
       NOT wa_doclist-bldat_orig_inv IS INITIAL.
      CLEAR l_check.
      l_bldat = wa_doclist-bldat_orig_inv. " date of original invoice

*     1892122 - if document is in adjustment range then always use date of transfer document
      if ( wa_doclist-xforgotten = 'X'        ) and   " 2037120 - forgotten invoice
         ( strlen( wa_bkpf-bktxt ) = 14       ) and         " 1702300
         ( wa_bkpf-bktxt+5(9) co '0123456789' ) and " at least 5 digits in number
         ( wa_bkpf-bldat <> wa_bkpf-budat ).
*       period in document and posting dates must be different in tax transfer
        call function 'FI_PERIOD_DETERMINE'
          exporting
            i_budat = wa_bkpf-bldat
            i_bukrs = wa_doclist-bukrs
          importing
            e_gjahr = e_gjahr
            e_monat = e_monat
          exceptions
            others  = 8.
        if sy-subrc = 0.
          call function 'FI_PERIOD_DETERMINE'
            exporting
              i_budat = wa_bkpf-budat
              i_bukrs = wa_doclist-bukrs
            importing
              e_gjahr = e_gjahr1
              e_monat = e_monat1
            exceptions
              others  = 8.
          if sy-subrc = 0.
            if e_gjahr <> e_gjahr1 or
               e_monat <> e_monat1.
              l_bldat = wa_bkpf-bldat.
              l_check = 'X'.
            endif.
          endif.
        endif.
      endif.

*     use new logic of detecting additional sheet
      if l_check = 'X'.
*       already defined - continue
      elseif wa_doclist-journal CO j3rdl_journal_rev_all.
*       process revisions via storno
        l_check = 'X'.
      elseif wa_doclist-corr_type = j3rdl_corr_flag_rev    OR
             wa_doclist-corr_type = j3rdl_corr_flag_corr_rev.
*       for purchase ledger - revisions in closed period are reflected in basic sheet
        continue.
*     for purchase ledger - all corrections in closed period
*     are reflected in basic sheet, but stornos in additional sheet
      elseif wa_doclist-corr_type = j3rdl_corr_flag_corr AND
*            wa_doclist-corr_book = 'X'                  AND  " 1718328 - all corrections
             wa_doclist-flg_belnr_trn <> 'S'.
        continue.
      elseif wa_doclist-corr_type = j3rdl_corr_flag_corr AND
             wa_doclist-flg_belnr_trn = 'S'              AND
             wa_bkpf-xreversal = '1'. " reversed by
*       reversed correction in closed period - in main sheet
        continue.
      elseif wa_doclist-corr_type = j3rdl_corr_flag_corr AND
             wa_doclist-flg_belnr_trn = 'S'              AND
             wa_bkpf-xreversal = '2'. " reversal
*       storno correction in closed period - in additional sheet
        l_check = 'X'.
        if not wa_doclist-bldat_corr_inv is initial.
          l_bldat = wa_doclist-bldat_corr_inv. " date of correction invoice
        endif.
      endif.

      if l_check = 'X'. " journal of revisions and corrections

*       show revision in basic sheet
        check wa_doclist-journal <> j3rdl_journal_rev3.

        call function 'FI_PERIOD_DETERMINE'
          exporting
            i_budat = l_bldat
            i_bukrs = wa_doclist-bukrs
          importing
            e_gjahr = e_gjahr
            e_monat = e_monat
          exceptions
            others  = 8.
        if sy-subrc = 0.
          if ( e_gjahr in ran_gjahr ) and
             ( e_monat in ran_monat ) and
             ( l_bldat in ran_budat ).    " 1902051
            continue.
          endif.
          if ( v_basic = 'X' ) and
             ( e_gjahr in ad_gjahr ) and
             ( e_monat in ad_monat ) and
             ( l_bldat in ad_budat ) and    " 1902051
             ( wa_bkpf-vatdate           in ad_vtdat ).
            wa_doclist-bukrs_add = wa_bkpf-bukrs.
            wa_doclist-belnr_add = wa_bkpf-belnr.
            wa_doclist-gjahr_add = e_gjahr.
            wa_doclist-monat_add = e_monat.
            wa_doclist-flag_add  = ''.
*           revision of correction - reffers to correction invoice
            if wa_doclist-corr_type = j3rdl_corr_flag_corr_rev AND
               wa_doclist-belnr_corr_inv <> space.
              wa_doclist-belnr_add = wa_doclist-belnr_corr_inv.
              wa_doclist-gjahr_add = wa_doclist-gjahr_corr_inv.
            endif.
            modify ct_doclist from wa_doclist index l_tabix.
          else.
            delete ct_doclist index l_tabix.
          endif.
        endif.
        continue.
      endif.
*     do not use old logic for revisions
      if wa_doclist-corr_type = j3rdl_corr_flag_rev OR
         wa_doclist-corr_type = j3rdl_corr_flag_corr_rev.
        continue.
      endif.
    endif. " IF ver2012 = 'X'.

* Look for corrected documents
    if     ( wa_doclist-xforgotten = 'X'        ) and " 2037120 - forgotten invoice
           ( strlen( wa_bkpf-bktxt ) = 14       ) and " 1702300
           ( wa_bkpf-bktxt+5(9) co '0123456789' ).    " at least 5 digits in number
* "Forgotten" documents processing
      if ( wa_bkpf-belnr = wa_bkpf-bktxt(10) ) and
         ( wa_bkpf-gjahr = wa_bkpf-bktxt+10(4) ).

        clear: e_gjahr, e_monat.

        call function 'FI_PERIOD_DETERMINE'
          exporting
            i_budat = wa_bkpf-bldat
            i_bukrs = wa_bkpf-bukrs
          importing
            e_gjahr = e_gjahr
            e_monat = e_monat
          exceptions
            others  = 8.
        if sy-subrc = 0.
          if ( e_gjahr in ran_gjahr ) and
             ( e_monat in ran_monat ) and
             ( wa_bkpf-bldat in ran_budat ).
            continue.
          endif.
          if ( v_basic = 'X' ) and
             ( e_gjahr in ad_gjahr ) and
             ( e_monat in ad_monat ) and
             ( wa_bkpf-bldat   in ad_budat ) and
             ( wa_bkpf-vatdate in ad_vtdat ).
            wa_doclist-bukrs_add = wa_bkpf-bukrs.
            wa_doclist-belnr_add = wa_bkpf-belnr.
            wa_doclist-gjahr_add = e_gjahr.
            wa_doclist-monat_add = e_monat.
            wa_doclist-flag_add  = ''.
            modify ct_doclist from wa_doclist.
          else.
            delete ct_doclist.
          endif.
        endif.
* Process credit memos as reversals
      else.
        select single
          bukrs gjahr belnr bktxt xblnr awtyp awkey stblg stjah xreversal monat blart bldat budat waers vatdate bstat cpudt awsys tcode stgrd wwert kursf hwaer glvor usnam cputm
          from bkpf into CORRESPONDING FIELDS OF wa_bkpf_add
          where bukrs = wa_bkpf-bukrs and
                belnr = wa_bkpf-bktxt(10) and
                gjahr = wa_bkpf-bktxt+10(4).
        if sy-subrc = 0.
          if ( wa_bkpf_add-gjahr in ran_gjahr ) and
             ( wa_bkpf_add-monat in ran_monat ) and
             ( wa_bkpf_add-budat in ran_budat ).
            continue.
          endif.
          perform change_fiscal_period changing wa_bkpf_add
                                                l_fgtn.
          if ( v_basic = 'X' ) and
             ( wa_bkpf_add-gjahr in ad_gjahr ) and
             ( wa_bkpf_add-monat in ad_monat ) and
             ( wa_bkpf_add-budat   in ad_budat ) and
             ( wa_bkpf_add-vatdate in ad_vtdat ).
            wa_doclist-bukrs_add = wa_bkpf_add-bukrs.
            wa_doclist-belnr_add = wa_bkpf_add-belnr.
            wa_doclist-gjahr_add = wa_bkpf_add-gjahr.
            wa_doclist-monat_add = wa_bkpf_add-monat.
* Should be treated as reversals - hence FLAG_ADD = 'X'
            wa_doclist-flag_add  = 'X'.
            if l_fgtn = 'X'.     " 2037120
              wa_doclist-xforgotten = 'X'.
            endif.
            modify ct_doclist from wa_doclist.
          else.
            delete ct_doclist.
          endif.
        endif. " sy-subrc = 0
      endif. " wa_bkpf-belnr = wa_bkpf-bktxt(10) etc.
* Look for reverse documents
    elseif ( wa_bkpf-stblg ne space ) and ( wa_bkpf-xreversal = '2' ).
* XREVERSAL = 2 => this is a reversal document;
* XREVERSAL = 1 => this is a reversed document;
      select single
        bukrs gjahr belnr bktxt xblnr awtyp awkey stblg stjah xreversal monat blart bldat budat waers vatdate bstat cpudt awsys tcode stgrd wwert kursf hwaer glvor usnam cputm
        from bkpf into CORRESPONDING FIELDS OF wa_bkpf_add
        where bukrs = wa_bkpf-bukrs and
              belnr = wa_bkpf-stblg and
              gjahr = wa_bkpf-stjah.
      if sy-subrc = 0.
        perform change_fiscal_period changing wa_bkpf_add
                                              l_fgtn.
        if ( v_basic = 'X' ) and
           ( wa_bkpf_add-gjahr in ad_gjahr ) and
           ( wa_bkpf_add-monat in ad_monat ) and
           ( wa_bkpf_add-budat   in ad_budat ) and
           ( wa_bkpf_add-vatdate in ad_vtdat ).
          wa_doclist-bukrs_add = wa_bkpf_add-bukrs.
          wa_doclist-belnr_add = wa_bkpf_add-belnr.
          wa_doclist-gjahr_add = wa_bkpf_add-gjahr.
          wa_doclist-monat_add = wa_bkpf_add-monat.
          wa_doclist-flag_add  = 'X'. " reversed
          if l_fgtn = 'X'.     " 2037120
            wa_doclist-xforgotten = 'X'.
          endif.
          modify ct_doclist from wa_doclist.
        else.
          delete ct_doclist.
        endif.
      endif.
* Look for reversed MM documents
    elseif ( wa_bkpf-glvor = 'RMRP' ).
      clear wa_bseg.
      select * from bseg into corresponding fields of wa_bseg
        where bukrs = wa_doclist-bukrs and
              belnr = wa_doclist-belnr_trn and
              gjahr = wa_doclist-gjahr_trn and
              mwskz = wa_doclist-mwskz and
              mwart ne space.
        select single stgrp from t007b into wa_stgrp
          where ktosl = wa_bseg-ktosl and
              ( stgrp = '1' or stgrp = '2' ).
        if sy-subrc = 0.
          exit.
        endif.
      endselect.
      if ( wa_bseg-augbl ne space ) and ( wa_bseg-xnegp = 'X' ).
        select single * from bsas into wa_bsas
        where bukrs = wa_bseg-bukrs and
              hkont = wa_bseg-hkont and
              augdt = wa_bseg-augdt and
              augbl = wa_bseg-augbl and
              buzei = wa_bseg-buzei and
              belnr ne wa_bseg-belnr and
              xnegp eq space.
        if sy-subrc = 0.
*         1571917: get VAT reporting date
          clear l_vatdate.
          if not ad_vtdat[] is initial.
            select single vatdate from bkpf into l_vatdate
              where bukrs = wa_bsas-bukrs AND
                    belnr = wa_bsas-belnr AND
                    gjahr = wa_bsas-gjahr.
          endif.
          if ( v_basic = 'X' ) and
             ( wa_bsas-gjahr in ad_gjahr ) and
             ( wa_bsas-monat in ad_monat ) and
             ( wa_bsas-budat in ad_budat ) and
             ( l_vatdate     in ad_vtdat ).
            wa_doclist-bukrs_add = wa_bsas-bukrs.
            wa_doclist-belnr_add = wa_bsas-belnr.
            wa_doclist-gjahr_add = wa_bsas-gjahr.
            wa_doclist-monat_add = wa_bsas-monat.
            wa_doclist-flag_add  = 'X'. " reversed
            modify ct_doclist from wa_doclist.
          else.
            delete ct_doclist.
          endif.
        endif.
      endif.
    endif. " Reversed FI and MM documents
  endloop.
endform.                                              "GET_ADD_DOCUMENTS
*----------------------------------------------------------------------*
*      FORM process_doclist                                            *
*----------------------------------------------------------------------*
form process_doclist
  using
    ct_bkpf    type j3rdl_t_bkpf_cache
    ct_bseg    type j3rdl_t_bseg_cache
    ct_doclist type j3rdl_t_doclist_ext
  changing
    ct_book    type j_3r_t_buy
    ct_totals  type j_3r_t_totals.

  field-symbols: <fs> type j3rdl_doclist_ext.

  data: wa_bkpf     type j3rdl_bkpf_cache,
        wa_bseg     type j3rdl_bseg_cache,
        wa_bseg_tmp type j3rdl_bseg_cache,
        wa_book     type j_3rbuy,
        wa_totals   type j_3r_totals.

  data: ls_lfa1 like lfa1,
        ls_kna1 like kna1,
        alt_payee like lfa1-xzemp,"Alternative payee in document
        adr_sel like addr1_sel,
        adr_val like addr1_val.

  data: begin of ls_address,
        bukrs     type bkpf-bukrs,
        paval_inn type t001z-paval,
        paval_kpp type t001z-paval,
        name1     type addr1_val-name1,
        name2     type addr1_val-name2,
        name3     type addr1_val-name3,
        name4     type addr1_val-name4,
        name1_r   type addr1_val-name1,
        name2_r   type addr1_val-name2,
        name3_r   type addr1_val-name3,
        name4_r   type addr1_val-name4,
        end of ls_address.

  data: lt_address like hashed table of ls_address
        with unique key bukrs.

  data: check_flag_it_bseg type c,
        is_vendor(1)       type c,
        l_land             type bsec-land1.

* N1626029 - Customs Union VAT Invoices
  data: l_rcu_flag  type flag,
        l_belnr     type bkpf-belnr,
        l_gjahr     type bkpf-gjahr,
        wa_bkpf_rcu type j3rdl_bkpf_cache.
  data: l_journal(1)   type c, " 1704703
        l_corr_book(1) type c. " 1752472

* 2074991 fill operation types
  if par_col  = 'X' AND
     ver2014  = 'X'.
    PERFORM fill_oper_typ USING ct_doclist.
  endif.

* Start output structure preparation
  loop at ct_doclist assigning <fs>.
    clear wa_book.
    move-corresponding <fs> to wa_book.       "#EC ENHOK
*    IF wa_book-flg_belnr_inv = 'S'.
*      wa_book-lights = '1'.
*      IF <fs>-flg_xragl_pay = 'X'.          "Clearing cancellation
*        wa_book-lights  = '2'.
*        wa_book-col22   = 'C21'.
*      ENDIF.
*    ENDIF.
    if <fs>-belnr_pay = <fs>-belnr_trn.
      wa_book-flg_belnr_all = 'X'.
    elseif <fs>-flg_belnr_pay = 'S'.
      wa_book-flg_belnr_all = 'R'.
    elseif <fs>-flg_belnr_inv = 'S'.
      wa_book-flg_belnr_all = 'D'.
    endif.

    wa_book-buzei_test   = <fs>-buzei_bset.
    wa_book-lifnr_cred   = <fs>-debcr.
    wa_book-name1_cred   = <fs>-name1.
    wa_book-name1_cred_r = <fs>-name1.
    wa_book-stcd1_cred   = <fs>-stcd1.
    wa_book-stcd3_cred   = <fs>-stcd3.

*   1704703 - get data from Invoice Journal
    clear l_journal.
    if ver2012 = 'X'.
      PERFORM get_invoice_journal USING    <fs>
                                  CHANGING wa_book
                                           l_journal.
    endif.

*   1752472 - change buyer to company for negative corrections
    clear l_corr_book.
    if <fs>-corr_book = 'X'.
      if not ( <fs>-debcr is initial or <fs>-opera = '-' ).
        l_corr_book = 'X'.
      endif.
    endif.
    if <fs>-opera = gc_opera_agent OR           " 1897808 - VAT agent
       <fs>-opera = '-'.                        " 2099024 - always our company
      l_corr_book = 'X'.
    endif.

    clear: adr_sel, adr_val, ls_address. " 1779619
    if l_journal is initial or
       l_corr_book = 'X'.  " 1752472 - overwrite customer data
    if not ( <fs>-debcr is initial or
             <fs>-opera = '-'      or
             <fs>-opera = gc_opera_agent  " 1897808 - VAT agent
            ) and
       l_corr_book is initial.
      clear: is_vendor, ls_lfa1, ls_kna1.

      loop at ct_bseg into wa_bseg where bukrs = <fs>-bukrs and
                                         belnr = <fs>-belnr_inv and
                                         gjahr = <fs>-gjahr_inv.
        check ( wa_bseg-koart = 'D' or wa_bseg-koart = 'K' ).
        <fs>-buzei_inv = wa_bseg-buzei.
        if wa_bseg-lifnr = <fs>-debcr.
          is_vendor = 'X'.
        elseif wa_bseg-kunnr = <fs>-debcr.
          is_vendor = space.
        elseif wa_bseg-koart = 'K'.
          is_vendor = 'X'.
        endif.

        if not wa_bseg-filkd is initial.
* In case there is an invoice from a Vendor/Customer's branch
* we display the Vendor/Customer's name and INN but the branch's KPP
          if wa_bseg-koart = 'D'.
            perform read_kna1 using    wa_bseg-filkd   "1542984
                              changing ls_kna1.
            if sy-subrc = 0.
              wa_book-stcd3_cred = ls_kna1-stcd3.
              is_vendor = space.
            endif.
          elseif wa_bseg-koart = 'K'.
            perform read_lfa1 using    wa_bseg-filkd   "1542984
                              changing ls_lfa1.
            if sy-subrc = 0.
              wa_book-stcd3_cred = ls_lfa1-stcd3.
              is_vendor = 'X'.
            endif.
          endif.
        endif.
      endloop.

      CLEAR alt_payee.

      if is_vendor is initial.
        perform read_kna1 using    <fs>-debcr   "1542984
                          changing ls_kna1.
        if sy-subrc = 0.
          adr_sel-addrnumber   = ls_kna1-adrnr.
          wa_book-name1_cred   = ls_kna1-name1.
          wa_book-name2_cred   = ls_kna1-name2.
          wa_book-name3_cred   = ls_kna1-name3.
          wa_book-name4_cred   = ls_kna1-name4.
          wa_book-one_time_acc = ls_kna1-xcpdk.
          alt_payee            = ls_kna1-xzemp.
        endif.
      else.
        perform read_lfa1 using    <fs>-debcr   "1542984
                          changing ls_lfa1.
        if sy-subrc = 0.
          adr_sel-addrnumber   = ls_lfa1-adrnr.
          wa_book-name1_cred   = ls_lfa1-name1.
          wa_book-name2_cred   = ls_lfa1-name2.
          wa_book-name3_cred   = ls_lfa1-name3.
          wa_book-name4_cred   = ls_lfa1-name4.
          wa_book-one_time_acc = ls_lfa1-xcpdk.
          alt_payee            = ls_lfa1-xzemp.
        endif.
      endif.

* Get address from One-Time Account Data Document Segment
      if wa_book-one_time_acc = 'X'
         or alt_payee <> space.
        select single name1 name2 name3 name4 stcd1 stcd3 land1
          into (wa_book-name1_cred, wa_book-name2_cred,
                wa_book-name3_cred, wa_book-name4_cred,
                wa_book-stcd1_cred, wa_book-stcd3_cred,
                l_land)
          from bsec where bukrs = <fs>-bukrs and
                          belnr = <fs>-belnr_inv and
                          gjahr = <fs>-gjahr_inv and
                          buzei = <fs>-buzei_inv.
        if sy-subrc = 0. "N1372290
          "1560625 - Non resident
          if l_land <> space AND l_land <> 'RU'.
            CLEAR: wa_book-stcd1_cred, wa_book-stcd3_cred.
          endif.
          wa_book-name1_cred_r = wa_book-name1_cred.
          wa_book-name2_cred_r = wa_book-name2_cred.
          wa_book-name3_cred_r = wa_book-name3_cred.
          wa_book-name4_cred_r = wa_book-name4_cred.
        endif.
      else.
        clear adr_sel-nation.
        call function 'ADDR_GET'
          exporting
            address_selection = adr_sel
          importing
            address_value     = adr_val
          exceptions
            address_not_exist = 2
            version_not_exist = 3
            others            = 5.
        if sy-subrc = 0.
          wa_book-name1_cred   = adr_val-name1.
          wa_book-name2_cred   = adr_val-name2.
          wa_book-name3_cred   = adr_val-name3.
          wa_book-name4_cred   = adr_val-name4.
          wa_book-name1_cred_r = adr_val-name1.
          wa_book-name2_cred_r = adr_val-name2.
          wa_book-name3_cred_r = adr_val-name3.
          wa_book-name4_cred_r = adr_val-name4.
        endif.
        if s_adr = 'X'.
          adr_sel-nation = 'R'.
          call function 'ADDR_GET'
            exporting
              address_selection = adr_sel
            importing
              address_value     = adr_val
            exceptions
              address_not_exist = 2
              version_not_exist = 3
              others            = 5.
          if sy-subrc = 0.
            wa_book-name1_cred_r = adr_val-name1.
            wa_book-name2_cred_r = adr_val-name2.
            wa_book-name3_cred_r = adr_val-name3.
            wa_book-name4_cred_r = adr_val-name4.
          endif.
        endif.
      endif.
    else.
* Get address from Company Code Data
      read table lt_address into ls_address
        with table key bukrs = wa_book-bukrs.
      if sy-subrc <> 0.
* Add an entry into lt_address
        ls_address-bukrs = wa_book-bukrs.
        perform select_addr using ls_address-name1
                                  ls_address-name2
                                  ls_address-name3
                                  ls_address-name4
                                  ls_address-name1_r
                                  ls_address-name2_r
                                  ls_address-name3_r
                                  ls_address-name4_r
                                  ls_address-bukrs.
* Get INN from Company Code Data
        select single paval into ls_address-paval_inn from t001z
          where bukrs = ls_address-bukrs and party = 'SAPR01'.
* Get KPP from Company Code Data
        select single paval into ls_address-paval_kpp from t001z
          where bukrs = ls_address-bukrs and party = 'SAPR10'.
        insert ls_address into table lt_address.
      endif.
      if <fs>-debcr is initial.
        wa_book-lifnr_cred   = wa_book-bukrs.
      endif.
      wa_book-name1_cred   = ls_address-name1.
      wa_book-name2_cred   = ls_address-name2.
      wa_book-name3_cred   = ls_address-name3.
      wa_book-name4_cred   = ls_address-name4.
      wa_book-name1_cred_r = ls_address-name1_r.
      wa_book-name2_cred_r = ls_address-name2_r.
      wa_book-name3_cred_r = ls_address-name3_r.
      wa_book-name4_cred_r = ls_address-name4_r.
      wa_book-stcd1_cred   = ls_address-paval_inn.
      wa_book-stcd3_cred   = ls_address-paval_kpp.
    endif.
    endif.  " if l_journal is initial.

    read table ct_bkpf into wa_bkpf with table key bukrs = <fs>-bukrs
                                                   belnr = <fs>-belnr_trn
                                                   gjahr = <fs>-gjahr_trn.
    if sy-subrc = 0.
* Tax relevant do
      wa_book-monat_trn     = wa_bkpf-monat.
      wa_book-bktxt_trn     = wa_bkpf-bktxt.
      wa_book-budat_trn     = wa_bkpf-budat.
      wa_book-bldat_trn     = wa_bkpf-bldat.
      wa_book-vatdate_trn   = wa_bkpf-vatdate.
      wa_book-gjahr_trn     = <fs>-gjahr_trn.
*    p_book-buzei_test    = it_doclist-buzei_trn.
      wa_book-dmbtr_wrs_trn = wa_bkpf-hwaer.
      wa_book-wrbtr_wrs_trn = wa_bkpf-waers.
      wa_book-dmbtr_trn     = <fs>-hwbas + <fs>-hwste.
      wa_book-wrbtr_trn     = <fs>-fwbas + <fs>-fwste.
    elseif <fs>-belnr_trn is initial. " N1559418 0% without tax transfer
      PERFORM get_sec_item_trn USING <fs>-bukrs
                                     <fs>-belnr_inv
                                     <fs>-gjahr_inv
                                     ct_bkpf
                            CHANGING wa_book-monat_trn
                                     wa_book-gjahr_trn
                                     wa_book-budat_trn
                                     wa_book-bldat_trn
                                     wa_book-dmbtr_wrs_trn.
    endif.
* VAT
    case <fs>-stegruno.
      when 1.
        wa_book-hwste_trn     = <fs>-hwste.   "Tax amount in LC
        wa_book-hwste_wrs_trn = wa_bkpf-hwaer."LC
        wa_book-fwste_trn     = <fs>-fwste.   "Tax amount in DC
        wa_book-fwste_wrs_trn = wa_bkpf-waers."DC
      when 2.
        wa_book-hwste2_trn     = <fs>-hwste.   "Tax amount in LC
        wa_book-hwste2_wrs_trn = wa_bkpf-hwaer."LC
        wa_book-fwste2_trn     = <fs>-fwste.   "Tax amount in DC
        wa_book-fwste2_wrs_trn = wa_bkpf-waers."DC
      when 3.
        wa_book-hwste3_trn     = <fs>-hwste.   "Tax amount in LC
        wa_book-hwste3_wrs_trn = wa_bkpf-hwaer."LC
        wa_book-fwste3_trn     = <fs>-fwste.   "Tax amount in DC
        wa_book-fwste3_wrs_trn = wa_bkpf-waers."DC
      when 5.
        wa_book-hwste5_trn     = <fs>-hwste.   "Tax amount in LC
        wa_book-hwste5_wrs_trn = wa_bkpf-hwaer."LC
        wa_book-fwste5_trn     = <fs>-fwste.   "Tax amount in DC
        wa_book-fwste5_wrs_trn = wa_bkpf-waers."DC
    endcase.
* VAT BASE
    case <fs>-basgruno.
      when 1.
        wa_book-hwbas_trn      = <fs>-hwbas.   "TAX base in LC
        wa_book-hwbas_wrs_trn  = wa_bkpf-hwaer."LC
        wa_book-fwbas_trn      = <fs>-fwbas.   "Tax base in DC
        wa_book-fwbas_wrs_trn  = wa_bkpf-waers."DC
      when 2.
        wa_book-hwbas2_trn     = <fs>-hwbas.   "TAX base in LC
        wa_book-hwbas2_wrs_trn = wa_bkpf-hwaer."LC
        wa_book-fwbas2_trn     = <fs>-fwbas.   "Tax base in DC
        wa_book-fwbas2_wrs_trn = wa_bkpf-waers."DC
      when 3.
        wa_book-hwbas3_trn     = <fs>-hwbas.   "TAX base in LC
        wa_book-hwbas3_wrs_trn = wa_bkpf-hwaer."LC
        wa_book-fwbas3_trn     = <fs>-fwbas.   "Tax base in DC
        wa_book-fwbas3_wrs_trn = wa_bkpf-waers."DC
      when 4.
        wa_book-hwbas4_trn     = <fs>-hwbas.   "TAX base in LC
        wa_book-hwbas4_wrs_trn = wa_bkpf-hwaer."LC
        wa_book-fwbas4_trn     = <fs>-fwbas.   "Tax base in DC
        wa_book-fwbas4_wrs_trn = wa_bkpf-waers."DC
*       1592674 - append tax amount for opera '+'
        if <fs>-opera = '+' OR <fs>-opera = 'N'.
          wa_book-hwbas4_trn     = <fs>-hwbas + <fs>-hwste.
          wa_book-fwbas4_trn     = <fs>-fwbas + <fs>-fwste.
        endif.
      when 5.
        wa_book-hwbas5_trn     = <fs>-hwbas.   "TAX base in LC
        wa_book-hwbas5_wrs_trn = wa_bkpf-hwaer."LC
        wa_book-fwbas5_trn     = <fs>-fwbas.   "Tax base in DC
        wa_book-fwbas5_wrs_trn = wa_bkpf-waers."DC
    endcase.

* Business Area
    loop at ct_bseg into wa_bseg where bukrs = <fs>-bukrs and
                                       belnr = <fs>-belnr_trn and
                                       gjahr = <fs>-gjahr_trn.
      check not wa_bseg-mwart is initial.
      wa_book-gsber_trn = wa_bseg-gsber.
    endloop.
* Business area
    data bs(1) type c.
    if wa_book-gsber_trn = space.
      bs = 'X'.
    endif.

    data: payment_does_not_exist.
    clear payment_does_not_exist. "1422566

* Payment document
    read table ct_bkpf into wa_bkpf with table key bukrs = <fs>-bukrs
                                                   belnr = <fs>-belnr_pay
                                                   gjahr = <fs>-gjahr_pay.
    if sy-subrc = 0.
      wa_book-wrbtr_wrs_pay = wa_bkpf-waers.
      wa_book-dmbtr_wrs_pay = wa_bkpf-hwaer.
      wa_book-xblnr_pay     = wa_bkpf-xblnr.
      wa_book-budat_pay     = wa_bkpf-budat.
      wa_book-bldat_pay     = wa_bkpf-bldat.
      IF gc_customizing-date_payment_budat = 'X'.  " 1743956
        wa_book-bldat_pay = wa_bkpf-budat.
      ENDIF.
      wa_book-usnam_pay     = wa_bkpf-usnam. "sysuser
      wa_book-bktxt_pay     = wa_bkpf-bktxt.
    else.
      payment_does_not_exist = 'X'.
    endif.
* Invoice document
    CLEAR l_rcu_flag. " 1626029
    read table ct_bkpf into wa_bkpf with table key bukrs = <fs>-bukrs
                                                   belnr = <fs>-belnr_inv
                                                   gjahr = <fs>-gjahr_inv.
    if sy-subrc = 0.
      wa_book-wrbtr_wrs_inv = wa_bkpf-waers.
      wa_book-dmbtr_wrs_inv = wa_bkpf-hwaer.
      wa_book-xblnr_inv     = wa_bkpf-xblnr.
      wa_book-budat_inv     = wa_bkpf-budat.
      wa_book-bldat_inv     = wa_bkpf-bldat.
      wa_book-vatdate_inv   = wa_bkpf-vatdate.
      wa_book-glvor         = wa_bkpf-glvor.
      wa_book-usnam_inv     = wa_bkpf-usnam. "sysuser
      wa_book-bktxt_inv     = wa_bkpf-bktxt.
      wa_book-mm_inv_budat  = wa_bkpf-bldat.
      IF gc_customizing-date_goods_budat = 'X'.  " 1743956
        wa_book-mm_inv_budat  = wa_bkpf-budat.
      ENDIF.
*     N1626029 - VAT invoice for Customs Union
      if wa_bkpf-xblnr(4) = gc_rcu_xblnr.
        l_rcu_flag = 'X'.
*       get data from the original invoice
        if wa_bkpf-bktxt(4) = gc_rcu_xblnr.
          l_belnr = wa_bkpf-bktxt+4(10).
          l_gjahr = wa_bkpf-bktxt+14(4).
          READ TABLE ct_bkpf INTO wa_bkpf_rcu
            WITH TABLE KEY bukrs = <fs>-bukrs
                           belnr = l_belnr
                           gjahr = l_gjahr.
          IF sy-subrc = 0.
*           fill XBLNR, BUDAT, BLDAT, GLVOR
            wa_book-xblnr_inv = wa_bkpf_rcu-xblnr.
            wa_book-budat_inv = wa_bkpf_rcu-budat.
            wa_book-bldat_inv = wa_bkpf_rcu-bldat.
            wa_book-vatdate_inv = wa_bkpf_rcu-vatdate.
            wa_book-glvor     = wa_bkpf_rcu-glvor.
          ENDIF.
        endif.
      endif.

*     1704703 - invoice data from journal
      if ver2012 = 'X'.
*       invoice date
        if <fs>-corr_type = j3rdl_corr_flag_none and
           not <fs>-bldat_orig_inv is initial.
          wa_book-bldat_inv = <fs>-bldat_orig_inv.
        elseif not <fs>-bldat_rev_inv is initial.
          wa_book-bldat_inv = <fs>-bldat_rev_inv.
        elseif not <fs>-bldat_corr_inv is initial.
          wa_book-bldat_inv = <fs>-bldat_corr_inv.
        endif.

*       replace ext number from journal
        if not <fs>-xblnr_corr_inv is initial.
          wa_book-xblnr_inv = <fs>-xblnr_corr_inv.
        elseif <fs>-corr_type = j3rdl_corr_flag_none and  " 1732482
               not <fs>-xblnr_orig_inv is initial.
          wa_book-xblnr_inv = <fs>-xblnr_orig_inv.
        endif.
      endif.
    endif.

*   1711413 - revisions
    if ver2012 = 'X'.
      if <fs>-journal CO j3rdl_journal_rev_all.
        wa_book-budat_trn = <fs>-bldat_rev_inv.
        wa_book-bldat_trn = <fs>-bldat_rev_inv.

*       get monat
        call function 'GET_CURRENT_YEAR'
          exporting
            BUKRS         = wa_book-bukrs
            DATE          = wa_book-budat_trn
          importing
            CURRM         = wa_book-monat_trn.
      endif.
      if <fs>-corr_type = j3rdl_corr_flag_corr_rev AND
         NOT <fs>-number_rev IS INITIAL.
        wa_book-number_corr_rev = <fs>-number_rev.
        CLEAR wa_book-number_rev.
      endif.
*     1734773 - set revision number of original invoice
      if NOT <fs>-number_orig_rev IS INITIAL         AND
         ( <fs>-corr_type = j3rdl_corr_flag_corr_rev OR
           <fs>-corr_type = j3rdl_corr_flag_corr ).
         wa_book-number_rev = <fs>-number_orig_rev.
      endif.
*     1740628 set revision document numbers
      if <fs>-corr_type = j3rdl_corr_flag_rev AND
         wa_book-belnr_orig_rev is initial.
        wa_book-belnr_orig_rev = <fs>-belnr_inv.
        wa_book-gjahr_orig_rev = <fs>-gjahr_inv.
      elseif <fs>-corr_type = j3rdl_corr_flag_corr_rev AND
             wa_book-belnr_corr_rev is initial.
        wa_book-belnr_corr_rev = <fs>-belnr_inv.
        wa_book-gjahr_corr_rev = <fs>-gjahr_inv.
      endif.
*     1718328 - fill BELNR_ORIG_INV for correct sorting order
      if wa_book-corr_type = j3rdl_corr_flag_none  AND
         wa_book-belnr_orig_inv is initial.

        if ( <fs>-belnr_add is not initial ) and
           ( <fs>-flag_add = 'X' ) and
           ( <fs>-belnr_inv = <fs>-belnr_trn ).
          wa_book-belnr_orig_inv = <fs>-belnr_add.
          wa_book-gjahr_orig_inv = <fs>-gjahr_add.
        else.
          wa_book-belnr_orig_inv = <fs>-belnr_inv.
          wa_book-gjahr_orig_inv = <fs>-gjahr_inv.
        endif.

*       fill dates
        if wa_book-bldat_orig_inv is initial.
          wa_book-bldat_orig_inv = wa_book-bldat_inv.
        endif.
        if wa_book-budat_orig_inv is initial.
          wa_book-budat_orig_inv = wa_book-budat_inv.
        endif.
      endif.

*     1720794 - fill BELNR_CORR_INV for correct sorting order
      if ( wa_book-corr_type = j3rdl_corr_flag_corr  OR
           wa_book-corr_type = j3rdl_corr_flag_corr_rev ) AND
         wa_book-belnr_corr_inv is initial.
        wa_book-belnr_corr_inv = <fs>-belnr_inv.
        wa_book-gjahr_corr_inv = <fs>-gjahr_inv.
        if wa_book-bldat_corr_inv is initial.  " 1740628
          wa_book-bldat_corr_inv = wa_book-bldat_inv.
        endif.
      endif.

*     2044268 - fill date of original invoice for ALV
      if wa_book-bldat_orig_inv is initial AND
         wa_book-corr_type = j3rdl_corr_flag_none.
        wa_book-bldat_orig_inv = wa_book-bldat_inv.
      endif.
*     2044268 - fill revision date for ALV
      if wa_book-corr_type = j3rdl_corr_flag_rev AND
         not wa_book-belnr_orig_inv is initial   AND
         not wa_book-number_rev     is initial   AND
         wa_book-bldat_orig_rev is initial.
        wa_book-bldat_orig_rev = wa_book-bldat_inv.
      endif.
*     2044268 - fill revision of correction date for ALV
      if wa_book-corr_type = j3rdl_corr_flag_corr_rev AND
         not wa_book-belnr_orig_inv  is initial       AND
         not wa_book-number_corr_rev is initial       AND
         wa_book-bldat_corr_rev is initial.
        wa_book-bldat_corr_rev  = wa_book-bldat_inv.
      endif.

    endif.

    if not <fs>-belnr_add is initial.
      select single buzei from bset into wa_book-buzei_add
        where bukrs = <fs>-bukrs_add and
              belnr = <fs>-belnr_add and
              gjahr = <fs>-gjahr_add and
              mwskz = <fs>-mwskz and
              hkont = <fs>-hkont_trn.
    endif.

* Down Payment Determination
*   N2117081 - billing plans are not downpayments
    if not ( wa_bkpf-belnr = <fs>-belnr_inv AND
             wa_bkpf-awtyp = 'VBRK' ).
    loop at ct_bseg into wa_bseg where bukrs = <fs>-bukrs and
                                       belnr = <fs>-belnr_inv and
                                       gjahr = <fs>-gjahr_inv.
      check wa_bseg-umsks = 'A'.
      wa_book-flg_belnr_trn = 'A'.
* For down payments this date needs to be reassigned
      wa_book-mm_inv_budat = wa_book-budat_pay.
      "N1328441 - clear G/R date for down payments (not DPC)
      if <fs>-belnr_inv EQ <fs>-belnr_trn OR
         gc_customizing-date_goods_downpayment = 'X'.  " 1743956
        clear wa_book-mm_inv_budat.
      endif.
      exit.
    endloop.
    endif. " N2117081

*   1720794 - tax transfer can refer to incorrect position
    if <fs>-belnr_inv <> <fs>-belnr_trn AND
       not <fs>-buzei_inv is initial.
      read table ct_bseg into wa_bseg
        with table key bukrs = <fs>-bukrs
                       belnr = <fs>-belnr_inv
                       gjahr = <fs>-gjahr_inv
                       buzei = <fs>-buzei_inv.
      if sy-subrc = 0 AND
         wa_bseg-koart NA 'DK'.
        clear <fs>-buzei_inv.
      endif.
    endif.

* Look for Customer/Vendor Position
    if <fs>-buzei_inv is initial.
      loop at ct_bseg into wa_bseg where bukrs = <fs>-bukrs and
                                         belnr = <fs>-belnr_inv and
                                         gjahr = <fs>-gjahr_inv.
        check ( wa_bseg-koart = 'D' or wa_bseg-koart = 'K' ).
        <fs>-buzei_inv = wa_bseg-buzei.
        exit.
      endloop.

      if <fs>-buzei_inv is initial.
        <fs>-buzei_inv = 1.
      endif.
    endif.

    read table ct_bseg into wa_bseg with table key bukrs = <fs>-bukrs
                                                   belnr = <fs>-belnr_inv
                                                   gjahr = <fs>-gjahr_inv
                                                   buzei = <fs>-buzei_inv.
    if sy-subrc = 0.
* Business area check
      if bs = 'X' and wa_bseg-gsber <> space.
        wa_book-gsber_trn = wa_bseg-gsber.
        clear bs.
      endif.
* document with several customer/vendor positions
* do not process Down payment
*     N1626029 - VAT invoice for Customs Union
      if l_rcu_flag = 'X'.
        wa_bseg-wrbtr = wa_book-wrbtr_inv.
        wa_bseg-dmbtr = wa_book-dmbtr_inv.
      elseif ( wa_bseg-koart = 'D' or wa_bseg-koart = 'K' ) and
         ( wa_bseg-umsks <> 'A' ).

        loop at ct_bseg into wa_bseg_tmp where bukrs = <fs>-bukrs and
                                               belnr = <fs>-belnr_inv and
                                               gjahr = <fs>-gjahr_inv.
          check ( wa_bseg_tmp-koart = wa_bseg-koart and
                  wa_bseg_tmp-buzei <> <fs>-buzei_inv and
                  wa_bseg_tmp-umsks <> 'A' ).
          if wa_bseg_tmp-shkzg <> wa_bseg-shkzg.
            wa_bseg-wrbtr = wa_bseg-wrbtr - wa_bseg_tmp-wrbtr.
            wa_bseg-dmbtr = wa_bseg-dmbtr - wa_bseg_tmp-dmbtr.
          else.
            wa_bseg-wrbtr = wa_bseg-wrbtr + wa_bseg_tmp-wrbtr.
            wa_bseg-dmbtr = wa_bseg-dmbtr + wa_bseg_tmp-dmbtr.
          endif.
        endloop.
      endif.

      wa_book-wrbtr_inv = wa_bseg-wrbtr. " doc currency amount
      wa_book-dmbtr_inv = wa_bseg-dmbtr. " loc currency amount
*     1247554: set sign of the invoice doc to tax base
      if <fs>-hwbas < 0 AND wa_book-dmbtr_inv > 0.
*       1758924 - if tax transfer for negative correction item, but
*       full invoice amount is positive, then do not change sign
*       Quick fix for single tax code
        data: l_change_sign(1) type c,
              l_bset_shkzg     type bset-shkzg,
              lt_bset_tmp      type standard table of bset,
              ls_bset_tmp      type bset.
        l_change_sign = 'X'.
        if <fs>-belnr_inv <> <fs>-belnr_trn or
           <fs>-gjahr_inv <> <fs>-gjahr_trn.
          CLEAR: lt_bset_tmp, l_bset_shkzg.
          SELECT * FROM bset
            INTO TABLE lt_bset_tmp
            WHERE bukrs = <fs>-bukrs      AND
                  belnr = <fs>-belnr_inv  AND
                  gjahr = <fs>-gjahr_inv.
          LOOP AT lt_bset_tmp INTO ls_bset_tmp.
            IF l_bset_shkzg IS INITIAL.
              l_bset_shkzg = ls_bset_tmp-shkzg.
            ELSEIF l_bset_shkzg <> ls_bset_tmp-shkzg.
              l_bset_shkzg = '-'.
              EXIT.
            ENDIF.
          ENDLOOP.
          IF l_bset_shkzg = 'S'. " if only one positive sign then do not change
            CLEAR l_change_sign.
          ENDIF.
        endif.
        if l_change_sign = 'X'.
          wa_book-wrbtr_inv = - wa_book-wrbtr_inv.
          wa_book-dmbtr_inv = - wa_book-dmbtr_inv.
        endif.
      endif.

* External invoice
      wa_book-xref1_inv = wa_bseg-xref1+6.
* Vendor's account
      wa_book-hkont_cln = wa_bseg-hkont.
* External invoice date
      data: hyear(4) type n, wdate type d.
      if wa_bseg-xref1 is initial or not wa_bseg-xref1(6) co '0123456789'.
        hyear = space. clear wa_book-xref1_dat.
      else.
        hyear = wa_bseg-xref1(2).
        if hyear ge  0.
          if hyear le 49.
            hyear = hyear + 2000.
          endif.
        endif.
        if hyear ge 50.
          if hyear le 99.
            hyear = hyear + 1900.
          endif.
        endif.
        wdate+6(2) = wa_bseg-xref1+4(2).
        wdate+4(2) = wa_bseg-xref1+2(2).
        wdate(4)   = hyear.
        move wdate to wa_book-xref1_dat.
      endif.
*
      if wa_bseg-augbl = wa_book-belnr_pay.
        wa_book-flg_belnr_all = 'A'.
      endif.
*   Document is not cleared
      if wa_bseg-augbl is initial and wa_book-flg_belnr_all is initial.
        wa_book-flg_belnr_all = 'D'.
      endif.
    endif.

*   Payment amount
    wa_book-dmbtr_pay = <fs>-dmbtr_pay.
    wa_book-wrbtr_pay = <fs>-wrbtr_pay.

*   2108349 - copy invoice amount to payment column for OPERA='I'
    if <fs>-opera = gc_opera_payinv and
       not wa_book-belnr_inv is initial.
      wa_book-dmbtr_pay      = wa_book-dmbtr_inv.
      wa_book-wrbtr_pay      = wa_book-wrbtr_inv.
      payment_does_not_exist = 'X'. " copy curency
    endif.

*   N1298004: payment document is not found, copy data from invoice
    if payment_does_not_exist = 'X'.
      read table ct_bkpf into wa_bkpf
        with table key bukrs = wa_book-bukrs
                       belnr = wa_book-belnr_inv
                       gjahr = wa_book-gjahr_inv.
      if sy-subrc = 0.
        wa_book-wrbtr_wrs_pay = wa_bkpf-waers.
        wa_book-dmbtr_wrs_pay = wa_bkpf-hwaer.
        "N1328308: do not copy invoice data
        "wa_book-xblnr_pay     = wa_bkpf-xblnr.
        "wa_book-budat_pay     = wa_bkpf-budat.
        "wa_book-bldat_pay     = wa_bkpf-bldat.
        "wa_book-bktxt_pay     = wa_bkpf-bktxt.
        "wa_book-usnam_pay     = wa_bkpf-usnam.
        "wa_book-belnr_pay     = wa_book-belnr_inv.
        "wa_book-gjahr_pay     = wa_book-gjahr_inv.
      else.
        CLEAR: wa_book-dmbtr_pay,
               wa_book-wrbtr_pay.
      endif.
    endif.
*   end of N1298004

*   Proccessing unposted tax sum
    if <fs>-opera = space or <fs>-opera = '-' or <fs>-opera = gc_opera_bset
      or <fs>-opera = gc_opera_pay.
      wa_book-hwste3_trn =
      abs( wa_book-dmbtr_pay ) -
      abs( wa_book-hwbas_trn  +  wa_book-hwste_trn  ) -
      abs( wa_book-hwbas2_trn +  wa_book-hwste2_trn ) -
      abs( wa_book-hwbas5_trn +  wa_book-hwste5_trn ) -
      abs( wa_book-hwbas3_trn ) -
      abs( wa_book-hwbas4_trn ).
    endif.
*
    if <fs>-opera = 'G'.
      wa_book-dmbtr_pay  = <fs>-hwbas .  " 1798472 gross amount tax
      wa_book-hwste3_trn =
      abs( wa_book-dmbtr_pay )  -
      abs( wa_book-hwbas_trn )  -
      abs( wa_book-hwbas2_trn ) -
      abs( wa_book-hwbas3_trn ) -
      abs( wa_book-hwbas4_trn ) -
      abs( wa_book-hwbas5_trn ).
    endif.
*
    data wrk_n like wa_book-hwste_trn.
* Assign sign for payment incl.VAT the same as sign VAT
    wrk_n = wa_book-hwste_trn  + wa_book-hwste2_trn + wa_book-hwste5_trn +
            wa_book-hwbas3_trn + wa_book-hwbas4_trn.
    if wrk_n < 0.
      wa_book-dmbtr_pay  = - abs( wa_book-dmbtr_pay ).
      wa_book-hwste3_trn = - wa_book-hwste3_trn.
    endif.
* Excluding of unposted TAX
    if all_incl = 'X'.
      wa_book-dmbtr_pay = wa_book-dmbtr_pay - wa_book-hwste3_trn.
    endif.
    if wa_book-hwste3_trn <> 0.
      wa_book-hwste3_wrs_trn = wa_book-dmbtr_wrs_trn. "wa_bkpf-hwaer.   "LC
    endif.
* Check code for CUSTOM house
    if <fs>-opera = 'T'.                        "AND par_tam = 'X'.
      wa_book-dmbtr_pay = - <fs>-hwste.
      wa_book-wrbtr_pay = - <fs>-fwste.
    endif.
* Processing partial payments
    clear check_flag_it_bseg.
    loop at ct_bseg into wa_bseg where bukrs = <fs>-bukrs and
                                       belnr = <fs>-belnr_inv and
                                       gjahr = <fs>-gjahr_inv.
      check wa_bseg-mwskz = <fs>-mwskz_def and
            not wa_bseg-mwart is initial.
      check_flag_it_bseg = 'X'.
      exit.
    endloop.

    if check_flag_it_bseg = 'X'.
      if <fs>-hwste <> 0.                " check 0 % taxes
        wrk_n = abs( <fs>-fwste ).
      else.
        wrk_n = abs( <fs>-fwbas ).                          " 0 % taxes
      endif.
      if wa_bseg-wrbtr = wrk_n.
        wa_book-part_payment = space.
      else.
        wa_book-part_payment = 'X'.
      endif.
    endif.

* Processing down payments
    if <fs>-opera = '+'.
      wa_book-hwbas_trn   =  wa_book-hwbas_trn  + wa_book-hwste_trn.
      wa_book-hwbas2_trn  =  wa_book-hwbas2_trn + wa_book-hwste2_trn.
      wa_book-hwbas5_trn  =  wa_book-hwbas5_trn + wa_book-hwste5_trn.
      wa_book-fwbas_trn   =  wa_book-fwbas_trn  + wa_book-fwste_trn.
      wa_book-fwbas2_trn  =  wa_book-fwbas2_trn + wa_book-fwste2_trn.
      wa_book-fwbas5_trn  =  wa_book-fwbas5_trn + wa_book-fwste5_trn.
    endif.
    if <fs>-opera = '-' or <fs>-opera = gc_opera_pay or
       ( <fs>-opera = '+' and wa_book-flg_belnr_trn = 'A' ).  " 1955737
      clear: wa_book-hwbas_trn,
             wa_book-hwbas2_trn,
             wa_book-hwbas5_trn,
             wa_book-fwbas_trn,
             wa_book-fwbas2_trn,
             wa_book-fwbas5_trn,
* Note 2003630
             wa_book-hwbas3_trn,
             wa_book-hwbas4_trn,
             wa_book-fwbas3_trn,
             wa_book-fwbas4_trn.
    endif.

*   2074991 - for virtual curency use local currency and value
    DATA: l_altwr TYPE tcurc-altwr,
          l_ltext TYPE ltext.
    CLEAR l_altwr. " 2085792
    IF ver2014 = 'X'           AND
       wa_book-wrbtr_wrs_pay <> wa_book-dmbtr_wrs_pay.
      PERFORM get_curr_code USING    wa_book-wrbtr_wrs_pay
                            CHANGING l_altwr
                                     l_ltext.
    ENDIF.
    IF wa_book-wrbtr_wrs_pay = wa_book-dmbtr_wrs_pay OR
       ( ver2014 = 'X' AND l_altwr IS INITIAL ).
      wa_book-wrbtr_wrs_pay = wa_book-dmbtr_wrs_pay.
      wa_book-wrbtr_pay     = wa_book-dmbtr_pay.
    ENDIF.

*   1676378 - change sign of all fields
    if <fs>-opera = 'N'.
      wa_book-dmbtr_inv  = - wa_book-dmbtr_inv.
      wa_book-wrbtr_inv  = - wa_book-wrbtr_inv.
      wa_book-dmbtr_pay  = - wa_book-dmbtr_pay.
      wa_book-wrbtr_pay  = - wa_book-wrbtr_pay.
      wa_book-dmbtr_trn  = - wa_book-dmbtr_trn.
      wa_book-wrbtr_trn  = - wa_book-wrbtr_trn.
      wa_book-hwbas_trn  = - wa_book-hwbas_trn.
      wa_book-fwbas_trn  = - wa_book-fwbas_trn.
      wa_book-hwbas2_trn = - wa_book-hwbas2_trn.
      wa_book-fwbas2_trn = - wa_book-fwbas2_trn.
      wa_book-hwbas3_trn = - wa_book-hwbas3_trn.
      wa_book-fwbas3_trn = - wa_book-fwbas3_trn.
      wa_book-hwbas4_trn = - wa_book-hwbas4_trn.
      wa_book-fwbas4_trn = - wa_book-fwbas4_trn.
      wa_book-hwbas5_trn = - wa_book-hwbas5_trn.
      wa_book-fwbas5_trn = - wa_book-fwbas5_trn.
      wa_book-hwste_trn  = - wa_book-hwste_trn.
      wa_book-hwste2_trn = - wa_book-hwste2_trn.
      wa_book-hwste3_trn = - wa_book-hwste3_trn.
      wa_book-hwste5_trn = - wa_book-hwste5_trn.
      wa_book-fwste_trn  = - wa_book-fwste_trn.
      wa_book-fwste2_trn = - wa_book-fwste2_trn.
      wa_book-fwste3_trn = - wa_book-fwste3_trn.
      wa_book-fwste5_trn = - wa_book-fwste5_trn.
    endif.

*   2074991 total VAT
    wa_book-hwste_pay = wa_book-hwste5_trn + wa_book-hwste2_trn.

    read table ct_bkpf into wa_bkpf with table key bukrs = <fs>-bukrs
                                                   belnr = <fs>-belnr_origpay
                                                   gjahr = <fs>-gjahr_origpay.
    if sy-subrc = 0.
      wa_book-belnr_origpay = wa_bkpf-belnr.
      wa_book-gjahr_origpay = wa_bkpf-gjahr.
      wa_book-bldat_origpay = wa_bkpf-bldat.
      IF gc_customizing-date_payment_budat = 'X'.  " 1743956
        wa_book-bldat_origpay = wa_bkpf-budat.
      ENDIF.
      wa_book-budat_origpay = wa_bkpf-budat.
      wa_book-xblnr_origpay = wa_bkpf-xblnr.
      wa_book-bktxt_origpay = wa_bkpf-bktxt.
      wa_book-usnam_pay_fct = wa_bkpf-usnam. "sysuser
    endif.

    read table ct_bkpf into wa_bkpf with table key bukrs = <fs>-bukrs_add
                                                   belnr = <fs>-belnr_add
                                                   gjahr = <fs>-gjahr_add.
    if sy-subrc = 0.
      wa_book-xblnr_test = wa_bkpf-xblnr.
    endif.

    if p_update = 'X'.
      if <fs>-belnr_add ne space.
        move-corresponding wa_book to wa_totals.       "#EC ENHOK
        clear wa_totals-dmbtr_inv.
        read table ct_bkpf into wa_bkpf with table key bukrs = <fs>-bukrs
                                                       belnr = <fs>-belnr_trn
                                                       gjahr = <fs>-gjahr_trn.
        wa_totals-bukrs     = wa_bkpf-bukrs.
        wa_totals-waers     = wa_bkpf-hwaer.
        wa_totals-gjahr_bas = wa_bkpf-gjahr.
        wa_totals-monat_bas = wa_bkpf-monat.
        wa_totals-gjahr_add = <fs>-gjahr_add.
        wa_totals-monat_add = <fs>-monat_add.
        wa_totals-basic = ''.
        if not p_quart is initial.          " 1263723
           wa_totals-monat_bas = main_quart." 1263723
           wa_totals-monat_add = add_quart. " 1263723
        endif.                              " 1263723
        collect wa_totals into ct_totals.
      endif.
    endif.

    perform fill_add_fields changing wa_book.

    append wa_book to ct_book.
  endloop.
endform.                                                "PROCESS_DOCLIST
*----------------------------------------------------------------------*
*      FORM get_incoming_totals                                        *
*----------------------------------------------------------------------*
form get_incoming_totals
  changing
    ct_totals  type j_3r_t_totals
    cs_totals  type j_3rbuy.

  field-symbols: <fs> type j_3r_totals.

  data: begin of wa_t001,
          bukrs type t001-bukrs,   "company code
          periv type t001-periv,   "fiscal year variant
          permin type t009b-poper, "min period number
          permax type t009b-poper, "max period number
        end of wa_t001,

        it_t001 like standard table of wa_t001.

  data: it_totals    type j_3r_t_totals,
        db_totals    type j_3r_t_totals,
        wa_totals    type j_3r_totals,
        it_bk_totals type standard table of j_3rf_bk_totals,
        wa_bk_totals type j_3rf_bk_totals.

  data: not_found(1) type c,
        i_answer(1)  type c.

  data: curr_year type bkpf-gjahr,
        curr_per  type bkpf-monat.

  clear: cs_totals, not_found.

  if p_mode = 0.
      PERFORM GET_LAST_SAVED_TOTALS(J_3RF_SELL_BOOK_02)
            tables   it_t001
                     it_bk_totals
                     it_totals
            USING br_bukrs[]
                  br_gjahr-low
                  ad_gjahr[]
                  ad_monat[]
                  add_quart_r[]
                  sel_mona-low
                  'P'
                  p_quart
                  main_quart
                  add_quart
            CHANGING not_found.
  endif.                                                    "p_mode = 0

* No incoming totals could be retrieved from the table
* j_3rf_bk_totals so the totals have to be calculated
  if p_mode = 1 or not_found = 'X'.
    refresh it_totals.
    data: ran_bldat type table of bldat_ran,
          ran_vtdat type table of bldat_ran,
          ran_xblnr type table of bxlnr_ran,
          ran_gjahr type table of cora_gjahr,
          ran_monat type table of j_3rf_monat_ran,
          ran_budat type table of j_3rf_budat_ran.
    data: it_bkpf_tmp    type j3rdl_t_bkpf_cache,
          it_bseg_tmp    type j3rdl_t_bseg_cache,
          it_doclist_tmp type j3rdl_t_doclist_ext.
    data: i_basic(1) type c value 'X'.

    perform get_documents
      tables
        ad_belnr
        ad_gjahr
        ad_budat
        ran_xblnr "empty
        ad_monat
        ran_bldat "empty
        ad_vtdat  " 1872311   ran_vtdat "empty
      changing
        it_bkpf_tmp[]
        it_bseg_tmp[]
        it_doclist_tmp[].

*   free it_bseg_tmp. "1786536

    perform get_add_documents
      tables
        ad_gjahr
        ad_budat
        ad_monat
      using
        i_basic
      changing
        it_bkpf_tmp[]
        it_doclist_tmp[].

*   commented by N1294845
*    perform get_corrective_documents
*      tables
*        ran_belnr "empty
*        ran_gjahr "empty
*        ran_budat "empty
*        ran_xblnr "empty
*        ran_monat "empty
*        ran_bldat "empty
*      changing
*        it_bkpf_tmp[]
*        it_doclist_tmp[].

    perform process_totals
      using
        it_bkpf_tmp[]
        it_bseg_tmp[]
        it_doclist_tmp[]
      changing
        it_totals[].
    free: it_bkpf_tmp, it_doclist_tmp, it_bseg_tmp.

   " N1294845 - to calculate incomming total we should
    " select all documents for period between end of
    " additional sheet and up to the start of main sheet
    PERFORM get_incomming_period
      TABLES br_gjahr
             br_budat
             sel_mona
             ad_gjahr
             ad_budat
             ad_monat
             ran_gjahr
             ran_monat
             ran_budat
      USING  mainbuk.

    PERFORM get_documents
      TABLES
        ad_belnr
        ran_gjahr
        ran_budat
        ran_xblnr "empty
        ran_monat
        ran_bldat "empty
        ran_vtdat "empty
      CHANGING
        it_bkpf_tmp[]
        it_bseg_tmp[]
        it_doclist_tmp[].
*   FREE it_bseg_tmp. "1786536

    PERFORM get_add_documents
      TABLES
        ran_gjahr
        ran_budat
        ran_monat
      USING
        i_basic
      changing
        it_bkpf_tmp[]
        it_doclist_tmp[].

    " leave only corrective documents
    DELETE it_doclist_tmp WHERE belnr_add IS INITIAL.

    PERFORM process_totals
      using
        it_bkpf_tmp[]
        it_bseg_tmp[]
        it_doclist_tmp[]
      changing
        it_totals[].
    FREE: it_bkpf_tmp, it_doclist_tmp, it_bseg_tmp.
  endif. "p_mode = 1 OR not_found = 'X'

* N1379637: save incomming and outgoing totals
  if p_update = 'X'.
*   update periods
    loop at it_totals assigning <fs>.
      <fs>-monat_bas = sel_mona-low.
      <fs>-monat_add = ad_monat-low.
      if not p_quart is initial. " 1286763
         <fs>-monat_bas = main_quart.
         <fs>-monat_add = add_quart.
      endif.
    endloop.

*   save incomming totals
    IF not_found = 'X'.
      " basic sheet period should be > additional sheet period
      IF br_gjahr-low NE ad_gjahr-low OR
         sel_mona-low NE ad_monat-low.
        PERFORM save_incomming_totals(J_3RF_SELL_BOOK_02)
          USING p_quart
                'X'
                'P'
                br_gjahr-low
                sel_mona-low
                ad_monat-low
                main_quart
                add_quart
                it_totals.
      endif.
    endif.

*   collect outgoing totals
    DATA: it_out_totals TYPE j_3r_t_totals.
    it_out_totals[] = it_totals[].
    loop at ct_totals into wa_totals.
      wa_totals-monat_bas = sel_mona-low.
      wa_totals-monat_add = ad_monat-low.
      if not p_quart is initial. " 1286763
         wa_totals-monat_bas = main_quart.
         wa_totals-monat_add = add_quart.
      endif.
      collect wa_totals into it_out_totals.
    endloop.

*   initiate high periods
    if sel_mona-high is initial.
      sel_mona-high = sel_mona-low.
    endif.
    if main_quart_hi is initial.
      main_quart_hi = main_quart.
    endif.

*   save outgoing totals
    PERFORM save_incomming_totals(J_3RF_SELL_BOOK_02)
      USING p_quart
            space
            'P'
            br_gjahr-low
            sel_mona-high
            ad_monat-low
            main_quart_hi
            add_quart
            it_out_totals.
*   free memory
    REFRESH it_out_totals.

  endif. "p_update = 'X'

* Get the incoming totals line for the additional sheet output
  loop at it_totals assigning <fs>.
    cs_totals-dmbtr_pay  = cs_totals-dmbtr_pay  + <fs>-dmbtr_pay.
    cs_totals-dmbtr_trn  = cs_totals-dmbtr_trn  + <fs>-dmbtr_trn.
    cs_totals-hwbas_trn  = cs_totals-hwbas_trn  + <fs>-hwbas_trn.
    cs_totals-hwbas2_trn = cs_totals-hwbas2_trn + <fs>-hwbas2_trn.
    cs_totals-hwbas3_trn = cs_totals-hwbas3_trn + <fs>-hwbas3_trn.
    cs_totals-hwbas4_trn = cs_totals-hwbas4_trn + <fs>-hwbas4_trn.
    cs_totals-hwbas5_trn = cs_totals-hwbas5_trn + <fs>-hwbas5_trn.
    cs_totals-hwste_trn  = cs_totals-hwste_trn  + <fs>-hwste_trn.
    cs_totals-hwste2_trn = cs_totals-hwste2_trn + <fs>-hwste2_trn.
    cs_totals-hwste3_trn = cs_totals-hwste3_trn + <fs>-hwste3_trn.
    cs_totals-hwste5_trn = cs_totals-hwste5_trn + <fs>-hwste5_trn.
    cs_totals-dmbtr_wrs_inv = <fs>-waers.
  endloop.
  cs_totals-dmbtr_wrs_pay  =
  cs_totals-dmbtr_wrs_trn  =
  cs_totals-hwbas_wrs_trn  =
  cs_totals-hwbas2_wrs_trn =
  cs_totals-hwbas3_wrs_trn =
  cs_totals-hwbas4_wrs_trn =
  cs_totals-hwbas5_wrs_trn =
  cs_totals-hwste_wrs_trn  =
  cs_totals-hwste2_wrs_trn =
  cs_totals-hwste3_wrs_trn =
  cs_totals-hwste5_wrs_trn = cs_totals-dmbtr_wrs_inv.
  cs_totals-belnr_add      = text-441.
  free: ct_totals, it_t001, it_totals, it_bk_totals.

* 2074991 total VAT
  cs_totals-hwste_pay = cs_totals-hwste5_trn + cs_totals-hwste2_trn.
endform.                                            "GET_INCOMING_TOTALS
*----------------------------------------------------------------------*
*      FORM get_intermediate_period                                    *
*----------------------------------------------------------------------*
form get_intermediate_period
  TABLES br_gjahr  STRUCTURE cora_gjahr       " basic sheet
         br_budat  STRUCTURE j_3rf_budat_ran
         sel_mona  STRUCTURE j_3rf_monat_ran
         ad_gjahr  STRUCTURE cora_gjahr       " additional sheet
         ad_budat  STRUCTURE j_3rf_budat_ran
         ad_monat  STRUCTURE j_3rf_monat_ran
         ran_budat STRUCTURE j_3rf_budat_ran " output date range
  USING  mainbuk      TYPE      bkpf-bukrs
    i_periv type t001-periv.

  data: min_year like t009b-bdatj,
        max_year like t009b-bdatj,
        min_per  like t009b-poper,
        max_per  like t009b-poper,
        date_beg like sy-datum,
        date_end like sy-datum.

* Year range
  if not ad_gjahr is initial.
    read table ad_gjahr index 1.
    if ad_gjahr-high <> space.
      min_year = ad_gjahr-high.
    else.
      min_year = ad_gjahr-low.
    endif.
  endif.

  if not br_gjahr is initial.
    read table br_gjahr index 1.
    max_year = br_gjahr-low.
  endif.

* Fiscal period range
  if not ad_monat is initial.
    read table ad_monat index 1.
    if ad_monat-high <> space.
      min_per = ad_monat-high.
    else.
      min_per = ad_monat-low.
    endif.
  endif.

  if not sel_mona is initial.
    read table sel_mona index 1.
    max_per = sel_mona-low.
  endif.

  if not ( min_year is initial or min_per is initial ).
    call function 'G_POSTING_DATE_OF_PERIOD_GET'
      exporting
        period  = min_per
        variant = i_periv
        year    = min_year
      importing
        to_date = date_beg.
    date_beg = date_beg + 1.
  endif.

  if not ( max_year is initial or max_per is initial ).
    call function 'G_POSTING_DATE_OF_PERIOD_GET'
      exporting
        period    = max_per
        variant   = i_periv
        year      = max_year
      importing
        from_date = date_end.
    date_end = date_end - 1.
  endif.

* Posting date range
  if not ad_budat is initial.
    read table ad_budat index 1.
    if ad_budat-high <> space.
      ran_budat-low = ad_budat-high + 1.
    else.
      ran_budat-low = ad_budat-low + 1.
    endif.
  endif.

  if not br_budat is initial.
    read table br_budat index 1.
    ran_budat-high = br_budat-low - 1.
  endif.

  ran_budat-sign   = 'I'.
  ran_budat-option = 'BT'.
  if not date_beg is initial.
    if ( ran_budat-low is initial ) or ( ran_budat-low > date_beg ).
      ran_budat-low = date_beg.
    endif.
  endif.
  if not date_end is initial.
    if ( ran_budat-high is initial ) or ( ran_budat-high < date_end ).
      ran_budat-high = date_end.
    endif.
  endif.
  append ran_budat.

endform.                                        "GET_INTERMEDIATE_PERIOD
*----------------------------------------------------------------------*
*      FORM get_corrective_documents                                   *
*----------------------------------------------------------------------*
form get_corrective_documents
  tables
    ran_belnr structure belnr_ran
    ran_gjahr structure cora_gjahr
    ran_budat structure j_3rf_budat_ran
    ran_xblnr structure bxlnr_ran
    ran_monat structure j_3rf_monat_ran
    ran_bldat structure bldat_ran
    ran_vtdat structure bldat_ran
  changing
    ct_bkpf    type j3rdl_t_bkpf_cache
    ct_doclist type j3rdl_t_doclist_ext.

  data: it_bkpf_add type j3rdl_t_bkpf_cache,
        it_wait_list type j3rdl_t_bkpf_cache,
        wa_bkpf_tmp like line of ct_bkpf,
        wa_bkpf     like line of ct_bkpf,
        wa_bsas     type bsas,
        wa_doclist  like line of ct_doclist.

  data: begin of wa_bseg,
          bukrs like bseg-bukrs,
          belnr like bseg-belnr,
          gjahr like bseg-gjahr,
          buzei like bseg-buzei,
          zuonr like bseg-zuonr,
          hkont like bseg-hkont,
          ktosl like bseg-ktosl,
          augdt like bseg-augdt,
          augbl like bseg-augbl,
          xnegp like bseg-xnegp,
        end of wa_bseg,

        begin of wa_doc_rev,
          bukrs like bseg-bukrs,
          belnr like bseg-belnr,
          gjahr like bseg-gjahr,
        end of wa_doc_rev,

        it_doc_rev like hashed table of wa_doc_rev
          with unique key bukrs belnr gjahr,

        begin of wa_bktxt,
         bukrs like bkpf-bukrs,
         bktxt like bkpf-bktxt,
        end of wa_bktxt,

        it_bktxt like hashed table of wa_bktxt
          with unique key bukrs bktxt.

  data: wa_stgrp like t007b-stgrp,
        i_periv  type t001-periv.

  select single periv from t001 into i_periv
    where bukrs = mainbuk.
  perform get_intermediate_period
    TABLES   br_gjahr
             br_budat
             sel_mona
             ad_gjahr
             ad_budat
             ad_monat
             ran_budat
    USING    mainbuk
      i_periv.

  loop at ct_doclist into wa_doclist.
    read table ct_bkpf with table key
      bukrs = wa_doclist-bukrs
      belnr = wa_doclist-belnr_trn
      gjahr = wa_doclist-gjahr_trn into wa_bkpf.
* Check whether the tax transfer document exists
    if sy-subrc = 0.
* Store document's details for further BKTXT processing
    if ( not ad_blart is initial ).
      clear wa_bkpf-bktxt.
      concatenate wa_bkpf-belnr(10) wa_bkpf-gjahr(4) into wa_bkpf-bktxt.
      read table it_bktxt transporting no fields
        with table key bukrs = wa_bkpf-bukrs
                       bktxt = wa_bkpf-bktxt.
* Entry not found - insert into IT_BKTXT table
      if sy-subrc <> 0.
        wa_bktxt-bukrs = wa_bkpf-bukrs.
        wa_bktxt-bktxt = wa_bkpf-bktxt.
        insert wa_bktxt into table it_bktxt.
      endif.
    endif. " NOT ad_blart IS INITIAL

    clear wa_doc_rev.

* Look for reversed documents
    if ( wa_bkpf-stblg ne space ) and ( wa_bkpf-xreversal = '1' ).
* XREVERSAL = 2 => this is a reversal document;
* XREVERSAL = 1 => this is a reversed document;
      select single bukrs gjahr belnr bktxt xblnr awtyp awkey stblg stjah xreversal monat blart bldat budat waers vatdate bstat cpudt awsys tcode stgrd wwert kursf hwaer glvor usnam cputm
        from bkpf into CORRESPONDING FIELDS OF wa_bkpf_tmp
        where bukrs = wa_bkpf-bukrs and
              belnr = wa_bkpf-stblg and
              gjahr = wa_bkpf-stjah and
              budat in ran_budat.
      if sy-subrc = 0.
        wa_doc_rev-bukrs = wa_bkpf-bukrs.
        wa_doc_rev-belnr = wa_bkpf-stblg.
        wa_doc_rev-gjahr = wa_bkpf-stjah.
        delete ct_doclist.
      endif.
* MM reversals
    elseif ( wa_bkpf-glvor = 'RMRP' ).
      clear wa_bseg.
      select * from bseg into corresponding fields of wa_bseg
        where bukrs = wa_doclist-bukrs and
              belnr = wa_doclist-belnr_trn and
              gjahr = wa_doclist-gjahr_trn and
              mwskz = wa_doclist-mwskz and
              mwart ne space.
        select single stgrp from t007b into wa_stgrp
          where ktosl = wa_bseg-ktosl and
              ( stgrp = '1' or stgrp = '2' ).
        if sy-subrc = 0.
          exit.
        endif.
      endselect.
      if ( wa_bseg-augbl ne space ) and ( wa_bseg-xnegp eq space ).
        select single * from bsas into wa_bsas
        where bukrs = wa_bseg-bukrs and
              hkont = wa_bseg-hkont and
              augdt = wa_bseg-augdt and
              augbl = wa_bseg-augbl and
              buzei = wa_bseg-buzei and
              belnr ne wa_bseg-belnr and
              xnegp = 'X' and
              budat in ran_budat.
        if sy-subrc = 0.
          wa_doc_rev-bukrs = wa_bsas-bukrs.
          wa_doc_rev-belnr = wa_bsas-belnr.
          wa_doc_rev-gjahr = wa_bsas-gjahr.
          delete ct_doclist.
        endif.
      endif.
    endif.

* Add reversal of the document to be deleted for further checks
    if not wa_doc_rev is initial.
      read table it_doc_rev transporting no fields
        with table key bukrs = wa_doc_rev-bukrs
                       belnr = wa_doc_rev-belnr
                       gjahr = wa_doc_rev-gjahr.
      if sy-subrc <> 0.
        insert wa_doc_rev into table it_doc_rev.
      endif.
    endif.
    endif. " sy-subrc = 0
  endloop. " LOOP AT ct_doclist

* Look for credit memos that are processed as reversals &
* documents that should have been posted in the closed fiscal period
* but have been posted in the ran_budat period
* For them:
* - BKTXT(10) = BELNR,
* - BKTXT+10(4) = GJAHR,
* - BUDAT in ran_budat, and
* - BLDAT in closed fiscal period - ad_gjahr, ad_monat and ad_budat

  data: e_gjahr type bkpf-gjahr,
        e_monat type bkpf-monat.

  if not ad_blart is initial.
    select bukrs gjahr belnr bktxt xblnr awtyp awkey stblg stjah xreversal monat blart bldat budat waers vatdate bstat cpudt awsys tcode stgrd wwert kursf hwaer glvor usnam cputm
      from bkpf into CORRESPONDING FIELDS OF wa_bkpf
      where bukrs in br_bukrs and
            bstat eq space and
            blart in ad_blart and
            budat in ran_budat and
            bktxt ne space.

*     check ( wa_bkpf-bktxt(14) co '0123456789' ).
      check ( strlen( wa_bkpf-bktxt ) = 14       ) and " 1702300
            ( wa_bkpf-bktxt+5(9) co '0123456789' ).    " at least 5 digits in number

* Check that no document is picked up
* which reverse the document deleted in the loop above
      read table it_doc_rev transporting no fields
        with table key bukrs = wa_bkpf-bukrs
                       belnr = wa_bkpf-belnr
                       gjahr = wa_bkpf-gjahr.
      check ( sy-subrc <> 0 ).

      if ( wa_bkpf-belnr = wa_bkpf-bktxt(10) ) and
         ( wa_bkpf-gjahr = wa_bkpf-bktxt+10(4) ).

        clear: e_gjahr, e_monat.

        call function 'FI_PERIOD_DETERMINE'
          exporting
            i_budat = wa_bkpf-bldat
            i_bukrs = wa_bkpf-bukrs
          importing
            e_gjahr = e_gjahr
            e_monat = e_monat
          exceptions
            others  = 8.
        if not ( sy-subrc = 0 and
                 e_gjahr in ad_gjahr and
                 e_monat in ad_monat and
                 wa_bkpf-bldat in ad_budat ).
          continue.
        endif.
      else.
        read table it_bktxt transporting no fields
          with table key bukrs = wa_bkpf-bukrs
                         bktxt = wa_bkpf-bktxt.
        if sy-subrc <> 0.
* Put this entry onto the waiting list
          read table it_wait_list transporting no fields
            with table key bukrs = wa_bkpf-bukrs
                           belnr = wa_bkpf-belnr
                           gjahr = wa_bkpf-gjahr.
          if sy-subrc ne 0.
            insert wa_bkpf into table it_wait_list.
          endif.
          continue.
        endif.
      endif.
      read table it_bkpf_add transporting no fields
        with table key bukrs = wa_bkpf-bukrs
                       belnr = wa_bkpf-belnr
                       gjahr = wa_bkpf-gjahr.
      if sy-subrc ne 0.
        insert wa_bkpf into table it_bkpf_add.
      endif.
    endselect.

* Process the waiting list
    loop at it_wait_list into wa_bkpf.
* The entry in the waiting list correct another corrective document
      read table it_bkpf_add into wa_bkpf_tmp
        with table key bukrs = wa_bkpf-bukrs
                       belnr = wa_bkpf-bktxt(10)
                       gjahr = wa_bkpf-bktxt+10(4).
      if sy-subrc = 0.
* Check that it corrects a "forgotten" invoice
        check ( wa_bkpf_tmp-belnr = wa_bkpf_tmp-bktxt(10) ) and
              ( wa_bkpf_tmp-gjahr = wa_bkpf_tmp-bktxt+10(4) ).
* Add the entry from the waiting list
        read table it_bkpf_add transporting no fields
          with table key bukrs = wa_bkpf-bukrs
                         belnr = wa_bkpf-belnr
                         gjahr = wa_bkpf-gjahr.
        if sy-subrc ne 0.
          insert wa_bkpf into table it_bkpf_add.
        endif.
      endif.
    endloop.

    free: it_bktxt, it_doc_rev, it_wait_list.

  endif. " NOT ad_blart IS INITIAL

  if not it_bkpf_add is initial.

    data: it_bkpf_tmp type j3rdl_t_bkpf_cache,
          it_bseg_tmp type j3rdl_t_bseg_cache,
          it_doclist_tmp type j3rdl_t_doclist,
          wa_doclist_tmp type j3rdl_doclist.

    call function 'J_3RF_BUILD_DOCLIST_DTI'
      exporting
        tax_version                = 'PUR'
        i_koart                    = 'K'
        i_chkref                   = chk_ref            "chk ref field
        i_findpayment              = sel_opay           "find real pay
        it_bkpf                    = it_bkpf_add[]
      importing
        et_bkpf                    = it_bkpf_tmp[]
        et_bseg                    = it_bseg_tmp[]
        et_doclist                 = it_doclist_tmp[]
      tables
        it_bukrs_ran               = br_bukrs
        it_belnr_ran               = ran_belnr
        it_gjahr_ran               = ran_gjahr
        it_budat_ran               = ran_budat
        it_xblnr_ran               = ran_xblnr
        it_umkrs_ran               = sel_ukrs
        it_monat_ran               = ran_monat
        it_bldat_ran               = ran_bldat
        it_vtdat_ran               = ran_vtdat
        it_mwskz_ran               = sel_mwkz
        it_kunnr_ran               = s_lifnr
        it_blart_ran               = sel_blrt
      exceptions
        county_doesnt_eixts        = 1
        jurisdiction_not_permitted = 2
        no_taxes_exists            = 3
        no_company_codes_exits     = 4
        multiple_countrues         = 5
        others                     = 6.
    if sy-subrc <> 0.
      message id sy-msgid type 'E' number sy-msgno
        with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    endif.

    loop at it_doclist_tmp into wa_doclist_tmp.
      move-corresponding wa_doclist_tmp to wa_doclist.
      append wa_doclist to ct_doclist.
    endloop.

    loop at it_bkpf_tmp into wa_bkpf.
      read table ct_bkpf transporting no fields
        with table key bukrs = wa_bkpf-bukrs
                       belnr = wa_bkpf-belnr
                       gjahr = wa_bkpf-gjahr.
      if sy-subrc <> 0.
        insert wa_bkpf into table ct_bkpf.
      endif.
    endloop.
  endif.
  free: it_bkpf_add, it_bkpf_tmp, it_bseg_tmp, it_doclist_tmp.
endform.                                       "GET_CORRECTIVE_DOCUMENTS
*----------------------------------------------------------------------*
*      FORM process_totals                                             *
*----------------------------------------------------------------------*
form process_totals
  using
    ct_bkpf    type j3rdl_t_bkpf_cache
    ct_bseg    type j3rdl_t_bseg_cache
    ct_doclist type j3rdl_t_doclist_ext
  changing
    ct_totals  type j_3r_t_totals.

  field-symbols: <fs> type j3rdl_doclist_ext.

  data: wa_bkpf     like line of ct_bkpf,
        wa_totals like line of ct_totals,
        lv_waers  type t001-waers,
        l_opera   type t007l-opera.  " 1786536

  select single waers from t001 into lv_waers where bukrs = mainbuk.

* Do not add up DMBTR_INV because:
* 1) this amount is not required in the legal form;
* 2) if an invoice is paid for by several payments
* the amount adds up several times => that leads to rapid overflow
  loop at ct_doclist assigning <fs>.
    clear wa_totals.
    read table ct_bkpf into wa_bkpf with table key bukrs = <fs>-bukrs
                                                   belnr = <fs>-belnr_trn
                                                   gjahr = <fs>-gjahr_trn.
    if sy-subrc = 0.
      wa_totals-bukrs = wa_bkpf-bukrs.
      wa_totals-waers = wa_bkpf-hwaer.
    else.
      wa_totals-bukrs = mainbuk.
      wa_totals-waers = lv_waers.
    endif.

    wa_totals-dmbtr_trn = <fs>-hwbas + <fs>-hwste.

    case <fs>-stegruno.
      when 1.
        wa_totals-hwste_trn  = <fs>-hwste.
      when 2.
        wa_totals-hwste2_trn = <fs>-hwste.
      when 3.
        wa_totals-hwste3_trn = <fs>-hwste.
      when 5.
        wa_totals-hwste5_trn = <fs>-hwste.
    endcase.
*
    case <fs>-basgruno.
      when 1.
        wa_totals-hwbas_trn  = <fs>-hwbas.
      when 2.
        wa_totals-hwbas2_trn = <fs>-hwbas.
      when 3.
        wa_totals-hwbas3_trn = <fs>-hwbas.
      when 4.
        wa_totals-hwbas4_trn = <fs>-hwbas.
      when 5.
        wa_totals-hwbas5_trn = <fs>-hwbas.
    endcase.

    wa_totals-dmbtr_pay = <fs>-dmbtr_pay.

*   Proccessing unposted tax sum
    if <fs>-opera = space or <fs>-opera = '-' or <fs>-opera = gc_opera_bset
      or <fs>-opera = gc_opera_pay.
      wa_totals-hwste3_trn =
        abs( wa_totals-dmbtr_pay ) -
        abs( wa_totals-hwbas_trn + wa_totals-hwste_trn ) -
        abs( wa_totals-hwbas2_trn + wa_totals-hwste2_trn ) -
        abs( wa_totals-hwbas5_trn + wa_totals-hwste5_trn ) -
        abs( wa_totals-hwbas3_trn ) -
        abs( wa_totals-hwbas4_trn ).
    endif.
*
    if <fs>-opera = 'G'.
      wa_totals-dmbtr_pay  = <fs>-hwbas .  " 1798472 gross amount tax
      wa_totals-hwste3_trn =
        abs( wa_totals-dmbtr_pay ) -
        abs( wa_totals-hwbas_trn ) -
        abs( wa_totals-hwbas2_trn ) -
        abs( wa_totals-hwbas3_trn ) -
        abs( wa_totals-hwbas4_trn ) -
        abs( wa_totals-hwbas5_trn ).
    endif.
*
    data wrk_n like wa_totals-hwste_trn.
* Assign sign for payment incl.VAT the same as sign VAT
    wrk_n = wa_totals-hwste_trn +
            wa_totals-hwste2_trn +
            wa_totals-hwste5_trn +
            wa_totals-hwbas3_trn +
            wa_totals-hwbas4_trn.
    if wrk_n < 0.
      wa_totals-dmbtr_pay  = - abs( wa_totals-dmbtr_pay ).
      wa_totals-hwste3_trn = - wa_totals-hwste3_trn.
    endif.
* Excluding of unposted TAX
    if all_incl = 'X'.
      wa_totals-dmbtr_pay = wa_totals-dmbtr_pay - wa_totals-hwste3_trn.
    endif.

* Check code for CUSTOM house
    if <fs>-opera = 'T'.                      "AND par_tam = 'X'.
      wa_totals-dmbtr_pay = - <fs>-hwste.
    endif.

* Processing down payments
    if <fs>-opera = '+'.
      wa_totals-hwbas_trn  = wa_totals-hwbas_trn  + wa_totals-hwste_trn.
      wa_totals-hwbas2_trn = wa_totals-hwbas2_trn + wa_totals-hwste2_trn.
      wa_totals-hwbas5_trn = wa_totals-hwbas5_trn + wa_totals-hwste5_trn.
    endif.

*   1786536 - downpayments logic according to 1137 / decree 451
    l_opera = <fs>-opera.
    if l_opera <> '-'      AND
       l_opera <> gc_opera_pay AND
       ( ver2012  = 'X' OR
         p_dp_add = 'X' ).
      read table ct_bseg transporting no fields
        with key bukrs = <fs>-bukrs
                 belnr = <fs>-belnr_inv
                 gjahr = <fs>-gjahr_inv
                 umsks = 'A'.
      if sy-subrc is initial.
        l_opera = '-'.  " flg_belnr_trn = 'A' -  this is downpayment
      endif.
    endif.

    if l_opera = '-' OR l_opera = gc_opera_pay.
      clear: wa_totals-hwbas_trn,
             wa_totals-hwbas2_trn,
             wa_totals-hwbas5_trn,
* Note 2003630
             wa_totals-hwbas3_trn,
             wa_totals-hwbas4_trn.
    endif.

*   2074991 total VAT
    wa_totals-hwste_pay = wa_totals-hwste5_trn + wa_totals-hwste2_trn.

    wa_totals-gjahr_bas = br_gjahr-low.
    wa_totals-monat_bas = sel_mona-low.
    wa_totals-gjahr_add = ad_gjahr-low.
    wa_totals-monat_add = ad_monat-low.
    wa_totals-basic = ''.
    collect wa_totals into ct_totals.
  endloop.
endform.                                                 "PROCESS_TOTALS
*----------------------------------------------------------------------*
*      FORM load_extract                                               *
*----------------------------------------------------------------------*
form load_extract
  using    v_extract   like disextract
  changing ct_book     type j_3r_t_buy
           cs_totals   type j_3rbuy.

  data: it_book_old  type standard table of j_3rbuy_old,
        wa_book_old  type j_3rbuy_old,
        it_book_283  type standard table of j_3rbuy_283,
        wa_book_283  type j_3rbuy_283,
        wa_totals283 type j_3rbuy_283,
        it_book_ex   type standard table of j_3rbuy_ex,
        wa_book_ex   type j_3rbuy_ex,
        wa_totals_ex type j_3rbuy_ex,
        wa_book      type j_3rbuy,
        l_extract    type disextract,
        lt_exnames   type j3rdl_t_exname,
        l_read       type flag.

*  1902051 - for extract splitting - get extract names
   l_extract = v_extract.
   perform get_extract_name using    l_extract
                            changing lt_exnames.

   loop at lt_exnames into l_extract-exname.
     clear: it_book_ex[], wa_totals_ex.

* 1695097 - use new structures by default
    call function 'REUSE_ALV_EXTRACT_LOAD'
      exporting
        is_extract         = l_extract
      tables
        et_exp01           = it_book_ex "ct_book[]
      changing
        c_exp01            = wa_totals_ex "cs_totals
    exceptions
      not_found          = 1
      wrong_relid        = 2
      no_report          = 3
      no_exname          = 4
      no_import_possible = 5
      others             = 6.
  if sy-subrc = 0.
    read table it_book_ex into wa_totals_ex with key belnr_add = text-441.
    if sy-subrc = 0.
      delete it_book_ex index sy-tabix.
    endif.
    loop at it_book_ex into wa_book_ex.
      move-corresponding wa_book_ex to wa_book.
      append wa_book to ct_book.
    endloop.
    move-corresponding wa_totals_ex to cs_totals.
    l_read = 'X'.
  else.
    exit. " exit from loop
  endif.

  endloop.

  free: it_book_ex, wa_totals_ex.
  if l_read = 'X'.
    return.
  endif.

* 1817834 - try to load extract using dynamic structures
  IF ver2012 = 'X'.
    DATA: l_subrc TYPE sy-subrc.
    PERFORM load_extract_dyn USING    v_extract
                                      lt_exnames
                             CHANGING ct_book
                                      cs_totals
                                      l_subrc.
    CHECK l_subrc <> 0. " if processed then exit
  ENDIF.

  if p_add = 'X'.
    call function 'REUSE_ALV_EXTRACT_LOAD'
      exporting
        is_extract         = v_extract
      tables
        et_exp01           = it_book_283 "ct_book[]
      changing
        c_exp01            = wa_totals283 "cs_totals
      exceptions
        not_found          = 1
        wrong_relid        = 2
        no_report          = 3
        no_exname          = 4
        no_import_possible = 5
        others             = 6.
    if sy-subrc <> 0.
      call function 'REUSE_ALV_EXTRACT_LOAD'
        exporting
          is_extract         = v_extract
        tables
          et_exp01           = it_book_283 "ct_book[]
        exceptions
          not_found          = 1
          wrong_relid        = 2
          no_report          = 3
          no_exname          = 4
          no_import_possible = 5
          others             = 6.
      if sy-subrc <> 0.
        message id sy-msgid type 'I' number sy-msgno
              with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      endif.
      read table it_book_283 into wa_totals283 with key belnr_add = text-441.
      if sy-subrc = 0.
        delete it_book_283 index sy-tabix.
      endif.
    endif.
    loop at it_book_283 into wa_book_283.
      move-corresponding wa_book_283 to wa_book.
      append wa_book to ct_book.
    endloop.
    move-corresponding wa_totals283 to cs_totals.
    free: it_book_283, wa_totals283.
  else.
    call function 'REUSE_ALV_EXTRACT_LOAD'
      exporting
        is_extract         = v_extract
      tables
        et_exp01           = it_book_old[]
      exceptions
        not_found          = 1
        wrong_relid        = 2
        no_report          = 3
        no_exname          = 4
        no_import_possible = 5
        others             = 6.
    if sy-subrc <> 0.
      message id sy-msgid type 'I' number sy-msgno
              with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    endif.
    loop at it_book_old into wa_book_old.
      move-corresponding wa_book_old to wa_book.       "#EC ENHOK
      append wa_book to ct_book.
    endloop.
    free it_book_old.
  endif.
endform.                                                   "LOAD_EXTRACT
*----------------------------------------------------------------------*
*      FORM E06_SORT_BUILD                                             *
*----------------------------------------------------------------------*
form e06_sort_build using e06_lt_sort type slis_t_sortinfo_alv.
  data: ls_sort type slis_sortinfo_alv.

  clear ls_sort.
  ls_sort-fieldname = 'GJAHR_ADD'.
  ls_sort-tabname   = 'P_BOOK_ALV'.
  ls_sort-spos      = 1.
  ls_sort-up        = 'X'.
  ls_sort-group     = ''."'*'.
  append ls_sort to e06_lt_sort.

  clear ls_sort.
  ls_sort-fieldname = 'MONAT_ADD'.
  ls_sort-tabname   = 'P_BOOK_ALV'.
  ls_sort-spos      = 2.
  ls_sort-up        = 'X'.
  ls_sort-group     = ''."'*'.
  append ls_sort to e06_lt_sort.

*  CLEAR LS_SORT.
*  LS_SORT-FIELDNAME = 'NUM_LINE'.
*  LS_SORT-TABNAME   = 'P_BOOK_ALV'.
*  LS_SORT-SPOS      = 3.
*  LS_SORT-UP        = 'X'.
*  LS_SORT-GROUP     = ''.
*  APPEND LS_SORT TO E06_LT_SORT.

  clear ls_sort.
  ls_sort-fieldname = 'BELNR_ADD'.
  ls_sort-tabname   = 'P_BOOK_ALV'.
  ls_sort-spos      = 3.
  ls_sort-up        = 'X'.
  ls_sort-group     = ''.
  append ls_sort to e06_lt_sort.
endform.                                                 "E06_SORT_BUILD
*---------------------------------------------------------------------*
*       FORM HTML_END_OF_LIST                                         *
*---------------------------------------------------------------------*
form html_end_of_list
  using cl_dd type ref to cl_dd_document.                   "#EC CALLED

  refresh lt_footer.

  append text-602 to lt_footer.

  call method cl_dd->add_text
    exporting
      text         = space
      text_table   = lt_footer
      fix_lines    = 'X'
      sap_fontsize = cl_dd_document=>medium.
endform.                                               "HTML_END_OF_LIST
*----------------------------------------------------------------------*
* FORM INIT_ALV_NO_CYCLE - is called only ONCE!
*----------------------------------------------------------------------*
form init_alv_no_cycle.
  perform e05_layout_build using gs_layout.
  perform events_init      using gt_events[].
  clear g_add_mode.
  header_form   = 'HTML_TOP_OF_PAGE'.
  footer_form   = 'HTML_END_OF_LIST'.
  header_height = 20.
  footer_height = 7.
endform.                                                       "INIT_ALV
*----------------------------------------------------------------------*
* FORM CALL_ALV_NO_CYCLE - is called a number of times
*----------------------------------------------------------------------*
form call_alv_no_cycle.
  data: wa_book type j_3rbuy_alv,
        i_exit(1) type c,
        is_exit   type slis_exit_by_user,
        wa_col    type lvc_s_scol.
  field-symbols: <fs_book>     type j_3rbuy,
                 <fs_fieldcat> type slis_fieldcat_alv.

  IF g_compare_extract = 'X'.
    gs_layout-coltab_fieldname = 'TABCOL'.
  ENDIF.

  refresh p_book_alv.
  if g_add_mode = space.
* Basic sheets
    perform e01_fieldcat_init using gt_fieldcat[] 'X' ver2012 ver2014.
    loop at p_book_alv1 assigning <fs_book> where belnr_add eq space.
      move-corresponding <fs_book> to wa_book.

      IF g_compare_extract = 'X'.
        clear: wa_book-tabcol[], wa_col.
        IF wa_book-ext_text_orig EQ 'X'.
          wa_col-color-col = gc_color_highlight.
        ENDIF.
        LOOP AT gt_fieldcat ASSIGNING <fs_fieldcat>.
          wa_col-fname = <fs_fieldcat>-fieldname.
          append wa_col to wa_book-tabcol.
        ENDLOOP.
      ENDIF.
      append wa_book to p_book_alv.
    endloop.
  else.
* Additional sheets
    perform e01_fieldcat_init using gt_fieldcat[] '' ver2012 ver2014.
    loop at p_book_alv1 assigning <fs_book> where belnr_add ne space.
      refresh wa_book-tabcol.
      move-corresponding <fs_book> to wa_book.
      IF g_compare_extract = 'X'.
        clear wa_col.
        IF wa_book-ext_text_orig EQ 'X'.
          wa_col-color-col = gc_color_highlight.
        ENDIF.
        LOOP AT gt_fieldcat ASSIGNING <fs_fieldcat>.
          wa_col-fname = <fs_fieldcat>-fieldname.
          append wa_col to wa_book-tabcol.
        ENDLOOP.
      ENDIF.
      append wa_book to p_book_alv.
    endloop.
    if gs_totals is not initial.
      clear wa_book.
      move-corresponding gs_totals to wa_book.
      insert wa_book into p_book_alv index 1.
    endif.
  endif.

  call function 'REUSE_ALV_GRID_DISPLAY'
    exporting
      i_callback_program          = g_repid            "Report
      i_callback_pf_status_set    = g_status           "Menu
      i_callback_user_command     = g_user_command     "USR Commands
      i_callback_html_top_of_page = header_form
      i_callback_html_end_of_list = footer_form
      i_html_height_top           = header_height
      i_html_height_end           = footer_height
      i_structure_name            = gc_j_3rbuy_alv "Structure name
      i_save                      = 'A'
      it_fieldcat                 = gt_fieldcat[]      "Field catalog
      is_variant                  = g_variant          "ALV variant
      it_events                   = gt_events[]
      is_layout                   = gs_layout
      is_print                    = gs_print
      it_sort                     = gt_sort[]
    importing
      e_exit_caused_by_caller     = i_exit
      es_exit_caused_by_user      = is_exit
    tables
      t_outtab                    = p_book_alv.
endform.                                                       "CALL_ALV
*&---------------------------------------------------------------------*
*&      Form  set_default_output_params
*&---------------------------------------------------------------------*
form set_default_output_params.
  data: pri_params like pri_params.
  call function 'GET_PRINT_PARAMETERS'
    exporting
      no_dialog      = 'X'
      mode           = 'CURRENT'
    importing
      out_parameters = pri_params.
  fp_outputparams-copies    = pri_params-prcop.
  fp_outputparams-dest      = pri_params-pdest.
  fp_outputparams-reqnew    = pri_params-prnew.
  fp_outputparams-reqimm    = pri_params-primm.
  fp_outputparams-reqdel    = pri_params-prrel.
  fp_outputparams-lifetime  = pri_params-pexpi.
  fp_outputparams-title     = pri_params-prtxt.
  fp_outputparams-cover     = pri_params-prsap.
  fp_outputparams-covtitle  = pri_params-prtxt.
  fp_outputparams-receiver  = pri_params-prrec.
  fp_outputparams-division  = pri_params-prabt.
  fp_outputparams-authority = pri_params-prber.
  if not p_tagged is initial.
    fp_outputparams-pdftagged = 'X'.
  else.
    clear fp_outputparams-pdftagged.
  endif.
  fp_outputparams-preview   = 'X'.
endform.                    " set_default_output_params
*----------------------------------------------------------------------*
*       FORM change_fiscal_period                                      *
*----------------------------------------------------------------------*
* Check whether it is a forgotten invoice (BKTXT, BLDAT)               *
* If so, special fiscal period determination is needed                 *
*----------------------------------------------------------------------*
form change_fiscal_period changing cs_bkpf type j3rdl_bkpf_cache
                                   e_flag  type flag.

  data: e_gjahr type bkpf-gjahr,
        e_monat type bkpf-monat.

  clear e_flag.
  perform check_forgotten_invoices changing cs_bkpf
                                            e_flag.
  if ( not ad_blart is initial   ) and
     ( cs_bkpf-blart in ad_blart ).
    e_flag = 'X'.
  endif.

  if ( e_flag = 'X'                       ) and " 2037120
*    ( cs_bkpf-bktxt(14) co '0123456789' ) and
     ( strlen( cs_bkpf-bktxt ) = 14       ) and " 1702300
     ( cs_bkpf-bktxt+5(9) co '0123456789' ) and " at least 5 digits in number
     ( cs_bkpf-belnr = cs_bkpf-bktxt(10) ) and
     ( cs_bkpf-gjahr = cs_bkpf-bktxt+10(4) ).

    call function 'FI_PERIOD_DETERMINE'
      exporting
        i_budat = cs_bkpf-bldat
        i_bukrs = cs_bkpf-bukrs
      importing
        e_gjahr = e_gjahr
        e_monat = e_monat
      exceptions
        others  = 8.
    if sy-subrc = 0.
      cs_bkpf-gjahr = e_gjahr.
      cs_bkpf-monat = e_monat.
      cs_bkpf-budat = cs_bkpf-bldat.
    endif.
  endif.
endform.                                           "CHANGE_FISCAL_PERIOD
*---------------------------------------------------------------------*
*       FORM top_of_list
*---------------------------------------------------------------------*
form top_of_list.
  data: ls_line  type slis_listheader,
        lt_line  type slis_t_listheader,
        ls_texts type sdydo_text_element.

  loop at lt_header into ls_texts.
    clear ls_line.
    ls_line-info = ls_texts.
    ls_line-typ  = 'H'.
    append ls_line to lt_line.
  endloop.

  call function 'REUSE_ALV_COMMENTARY_WRITE'
    exporting
      it_list_commentary = lt_line.

endform.                                                    "top_of_list
*---------------------------------------------------------------------*
*       FORM end_of_list
*---------------------------------------------------------------------*
form end_of_list.
  data: ls_line  type slis_listheader,
        lt_line  type slis_t_listheader,
        ls_texts type sdydo_text_element.

  loop at lt_footer into ls_texts.
    clear ls_line.
    ls_line-info = ls_texts.
    ls_line-typ  = 'H'.
    append ls_line to lt_line.
  endloop.

  call function 'REUSE_ALV_COMMENTARY_WRITE'
    exporting
      it_list_commentary = lt_line.

endform.                                                    "end_of_list

form change_values changing ct_book type j_3r_t_buy.
  data: it_modif type j_3r_sp_ledger_modif_tab,
        wa_modif type j_3r_sp_ledger_modif_line,
        cx_oref type ref to cx_root,
        cx_text type string,
        num_line(10) type n.

  field-symbols: <wa_book> type j_3rbuy.

* Fill in all fields of the modifiable table IT_MODIF
* with values from the source table CT_BOOK
* IT_MODIF-TAB_INDEX = sy-tabix in the loop over CT_BOOK

* The value of TAB_INDEX may NOT be modified in the BAdI implementation method!

  loop at ct_book assigning <wa_book>.
    wa_modif-tab_index = sy-tabix.
    move-corresponding <wa_book> to wa_modif.       "#EC ENHOK
    append wa_modif to it_modif.
  endloop.

  try.
    call badi ref->pl_change_before_output
      exporting
        it_purchase_ledger = ct_book
      changing
        ct_changed_values  = it_modif.
    catch cx_sy_dyn_call_illegal_method into cx_oref.
      cx_text = cx_oref->get_text( ).
      write / cx_text.
  endtry.

* Transfer changed values from IT_MODIF back to CT_BOOK
* Table match: index of CT_BOOK = IT_MODIF-TAB_INDEX
  loop at ct_book assigning <wa_book>.
    read table it_modif index sy-tabix into wa_modif.
    move-corresponding wa_modif to <wa_book>.       "#EC ENHOK
    modify ct_book from <wa_book>.
  endloop.

  free it_modif. "note 2140917

* 2070322 call BADI with full structure
  try.
    call badi ref->pl_change
      changing
        ct_purchase_ledger = ct_book.
    catch cx_sy_dyn_call_illegal_method into cx_oref.
      cx_text = cx_oref->get_text( ).
      write / cx_text.
  endtry.

* Sort CT_BOOK once again in case the changes have affected the sorting order
  perform sort_fields_by_variant using ct_book.

* 1731117 - renumerate items after badi
  if num_auto = 'X'.
*   renumerate basic sheet
    num_line = num_strt.
    if num_line is initial.
      num_line = 1.
    endif.
    loop at ct_book assigning <wa_book>
                    where belnr_add is initial.
      <wa_book>-num_line = num_line.
      num_line = num_line + 1.
    endloop.

*   renumerate additional sheet
    num_line = ad_doc_n.
    if num_line is initial.
      num_line = 1.
    endif.
    loop at ct_book assigning <wa_book>
                    where not belnr_add is initial.
      <wa_book>-num_line = num_line.
      num_line           = num_line + 1.
    endloop.
  endif.

endform. " change values
*&---------------------------------------------------------------------*
*&      Form  FILL_ADD_FIELDS
*&---------------------------------------------------------------------*
*       Fill in additional fields EXT_NUMBER and FULLNAME
*----------------------------------------------------------------------*
form fill_add_fields changing cs_book type j_3rbuy.
  DATA: l_name(30) TYPE c,
        l_n(1)     TYPE n,
        l_len      TYPE i.
  FIELD-SYMBOLS: <fs> TYPE j_3rbuy-name1_cred.

  if ( cs_book-belnr_add is not initial ) and
     ( cs_book-flag_add = 'X' ) and
     ( cs_book-belnr_inv = cs_book-belnr_trn ).
*   cs_book-ext_number = cs_book-belnr_add.
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
      EXPORTING   input  = cs_book-belnr_add
      IMPORTING   output = cs_book-ext_number.
  else.
*   cs_book-ext_number = cs_book-belnr_inv.
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
      EXPORTING   input  = cs_book-belnr_inv
      IMPORTING   output = cs_book-ext_number.
  endif.

  if lcl_buy_book=>if_ext_number( is_book  = cs_book     " 1802867
                                  i_xblnr  = p_xblnr
                                  i_xblnr1 = p_xblnr1
                                  i_xblnr2 = p_xblnr2 ) = 'X' AND
     cs_book-xblnr_inv <> '' AND
    ( cs_book-flg_belnr_trn <> 'A' OR par_ext = 'X' OR p_xblnr2 = 'X' ). "N1305870
    cs_book-ext_number = cs_book-xblnr_inv.
  endif.

*  if s_adr <> 'X'.
*    concatenate cs_book-name1_cred
*                cs_book-name2_cred
*                cs_book-name3_cred
*    into cs_book-fullname separated by space.
*  else.
*    concatenate cs_book-name1_cred_r
*                cs_book-name2_cred_r
*                cs_book-name3_cred_r
*    into cs_book-fullname separated by space.
*  endif.

  CLEAR: l_len, cs_book-fullname.
  DO 4 TIMES. " 1748086
    l_n = sy-index.
    if s_adr <> 'X'.
      CONCATENATE 'CS_BOOK-NAME' l_n '_CRED' INTO l_name.
    else.
      CONCATENATE 'CS_BOOK-NAME' l_n '_CRED_R' INTO l_name.
    endif.
    ASSIGN (l_name) TO <fs>.
    IF l_n = 1.
      cs_book-fullname = <fs>.
    ELSEIF l_len = 35. " no space
      CONCATENATE cs_book-fullname <fs> INTO cs_book-fullname.
    ELSE.
      CONCATENATE cs_book-fullname <fs> INTO cs_book-fullname SEPARATED BY space.
    ENDIF.
    l_len = STRLEN( <fs> ).
  ENDDO.
  CONDENSE cs_book-fullname.

* 2119301 - move pay_ext_bldat to pay_ext_number
  IF NOT cs_book-pay_ext_bldat IS INITIAL.
    PERFORM format_pay_ext_number CHANGING cs_book-pay_ext_bldat
                                           cs_book-pay_ext_number.
  ENDIF.

endform.                    " FILL_ADD_FIELDS

form print_form using is_header type j_3r_sp_ledger_header
                      it_table  type ty_adobe_tab
                      iv_pages_per_package type i
                      iv_basic  type abap_bool
                raising cx_dynamic_check.

  constants: lines_first_page  type i value 39,
             lines_middle_page type i value 50,
             lines_last_page   type i value 39,
             lines_single_page type i value 28.

  data: lv_lines_remained   type i,
        lv_total_lines_1pdf type i,
        lv_read_index_start type i,
        lv_read_index_end   type i,
        lt_data_part        type ty_adobe_tab,
        lt_data_old         type j_3r_purchase_ledger_tab,
        ls_totals           type ty_adobe_total,
        lv_page_start       type i,
        lv_first_package    type abap_bool,
        lv_last_package     type abap_bool,
        ls_formoutput       type fpformoutput.
  field-symbols: <fs_line> type ty_adobe_line,
                 <fs_old>  type j_3r_purchase_ledger_line.

  clear ls_totals.
  if iv_basic = abap_false.
    ls_totals = in_totals.
  endif.
  lv_page_start = 0.
  lv_read_index_start = 1.
  lv_first_package = abap_true.

  describe table it_table lines lv_lines_remained.
  lv_total_lines_1pdf = ( iv_pages_per_package * lines_middle_page ).

  while lv_lines_remained > 0.
    refresh lt_data_part.
    if lv_lines_remained > lv_total_lines_1pdf.
      lv_last_package = abap_false.
      lv_read_index_end = lv_read_index_start + lv_total_lines_1pdf - 1.
    else.
      lv_last_package  = abap_true.
      lv_read_index_end = lv_read_index_start + lv_lines_remained - 1.
    endif.

* Get lines for output
    append lines of it_table from lv_read_index_start to lv_read_index_end to lt_data_part.

* Calculate totals
    perform get_totals using lt_data_part changing ls_totals.

    IF gv_xml_enabled_p14 IS INITIAL.
*     2099024 old interface
      refresh lt_data_old.
      loop at lt_data_part assigning <fs_line>.
        append initial line to lt_data_old assigning <fs_old>.
        move-corresponding <fs_line> to <fs_old>.
      endloop.

* Output package
    call function fm_name
      exporting
        /1bcdwb/docparams  = fp_docparams
        is_header          = is_header
        is_total           = in_totals
        it_table           = lt_data_old  " lt_data_part
        is_footer          = ls_totals
        first_package      = lv_first_package
        last_package       = lv_last_package
        page_start         = lv_page_start
        basic              = iv_basic
      importing
        /1bcdwb/formoutput = ls_formoutput
      exceptions
        usage_error        = 1
        system_error       = 2
        internal_error     = 3
        others             = 4.
    ELSE.
*     2099024 New interface
* Output package
    call function fm_name
      exporting
        /1bcdwb/docparams  = fp_docparams
        is_header          = is_header
        is_total           = in_totals
        it_table           = lt_data_part
        is_footer          = ls_totals
        first_package      = lv_first_package
        last_package       = lv_last_package
        page_start         = lv_page_start
        basic              = iv_basic
      importing
        /1bcdwb/formoutput = ls_formoutput
      exceptions
        usage_error        = 1
        system_error       = 2
        internal_error     = 3
        others             = 4.
    ENDIF.
    if sy-subrc <> 0.
      message id sy-msgid type sy-msgty number sy-msgno
              with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    endif.

* Next package
    lv_lines_remained = lv_lines_remained - ( lv_read_index_end - lv_read_index_start + 1 ).
    lv_read_index_start = lv_read_index_start + lv_total_lines_1pdf.
    lv_page_start = lv_page_start + ls_formoutput-pages.
    lv_first_package = abap_false.
  endwhile.
  refresh lt_data_old. " 2099024
endform.                    " print_form

*&---------------------------------------------------------------------*
*&      Form  get_totals
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->LT_DATA_PART  text
*      -->LS_TOTALS     text
*----------------------------------------------------------------------*
form get_totals using lt_data_part type ty_adobe_tab
                changing ls_totals type ty_adobe_total.

  data: ls_data_part type ty_adobe_line.

  loop at lt_data_part into ls_data_part.
* Currency fields
    if ls_totals-dmbtr_wrs_pay is initial.
      ls_totals-dmbtr_wrs_pay = ls_data_part-dmbtr_wrs_pay.
    endif.
    if ls_totals-hwbas_wrs_trn is initial.
      ls_totals-hwbas_wrs_trn = ls_data_part-hwbas_wrs_trn.
    endif.
    if ls_totals-hwbas2_wrs_trn is initial.
      ls_totals-hwbas2_wrs_trn = ls_data_part-hwbas2_wrs_trn.
    endif.
    if ls_totals-hwbas3_wrs_trn is initial.
      ls_totals-hwbas3_wrs_trn = ls_data_part-hwbas3_wrs_trn.
    endif.
    if ls_totals-hwbas4_wrs_trn is initial.
      ls_totals-hwbas4_wrs_trn = ls_data_part-hwbas4_wrs_trn.
    endif.
    if ls_totals-hwbas5_wrs_trn is initial.
      ls_totals-hwbas5_wrs_trn = ls_data_part-hwbas5_wrs_trn.
    endif.
    if ls_totals-hwste_wrs_trn is initial.
      ls_totals-hwste_wrs_trn = ls_data_part-hwste_wrs_trn.
    endif.
    if ls_totals-hwste2_wrs_trn is initial.
      ls_totals-hwste2_wrs_trn = ls_data_part-hwste2_wrs_trn.
    endif.
    if ls_totals-hwste5_wrs_trn is initial.
      ls_totals-hwste5_wrs_trn = ls_data_part-hwste5_wrs_trn.
    endif.
* Amount fields
    ls_totals-dmbtr_pay = ls_totals-dmbtr_pay + ls_data_part-dmbtr_pay.
    ls_totals-hwbas_trn = ls_totals-hwbas_trn + ls_data_part-hwbas_trn.
    ls_totals-hwbas2_trn = ls_totals-hwbas2_trn + ls_data_part-hwbas2_trn.
    ls_totals-hwbas3_trn = ls_totals-hwbas3_trn + ls_data_part-hwbas3_trn.
    ls_totals-hwbas4_trn = ls_totals-hwbas4_trn + ls_data_part-hwbas4_trn.
    ls_totals-hwbas5_trn = ls_totals-hwbas5_trn + ls_data_part-hwbas5_trn.
    ls_totals-hwste_trn = ls_totals-hwste_trn + ls_data_part-hwste_trn.
    ls_totals-hwste2_trn = ls_totals-hwste2_trn + ls_data_part-hwste2_trn.
    ls_totals-hwste5_trn = ls_totals-hwste5_trn + ls_data_part-hwste5_trn.
  endloop.
* 2074991 total VAT
  ls_totals-hwste_pay = ls_totals-hwste5_trn + ls_totals-hwste2_trn.
endform.                    "get_totals
*&---------------------------------------------------------------------*
*&      Form  get_incomming_period
*&---------------------------------------------------------------------*
* N1294845 - the period for calculating incomming total
* is the period from end of additional sheet and up to
* start of main sheet
*----------------------------------------------------------------------*
*      -->pa_bas_gjahr - basic sheet
*      -->pa_bas_budat
*      -->pa_bas_monat
*      -->pa_add_gjahr - additional sheet
*      -->pa_add_budat
*      -->pa_add_monat
*      -->mainbuk
*      <--pa_out_gjahr - output range
*      <--pa_out_monat
*      <--pa_out_budat
*----------------------------------------------------------------------*
FORM get_incomming_period
  TABLES pa_bas_gjahr STRUCTURE cora_gjahr       " basic sheet
         pa_bas_budat STRUCTURE j_3rf_budat_ran
         pa_bas_monat STRUCTURE j_3rf_monat_ran
         pa_add_gjahr STRUCTURE cora_gjahr       " additional sheet
         pa_add_budat STRUCTURE j_3rf_budat_ran
         pa_add_monat STRUCTURE j_3rf_monat_ran
         pa_out_gjahr STRUCTURE cora_gjahr       " output range
         pa_out_monat STRUCTURE j_3rf_monat_ran
         pa_out_budat STRUCTURE j_3rf_budat_ran
  USING  mainbuk      TYPE      bkpf-bukrs.

  DATA: i_periv  TYPE t001-periv,
        min_year TYPE bkpf-gjahr,
        max_year TYPE bkpf-gjahr.

  REFRESH: pa_out_gjahr, pa_out_monat, pa_out_budat.

  SELECT SINGLE periv FROM t001 INTO i_periv
    WHERE bukrs = mainbuk.

  PERFORM get_intermediate_period
    TABLES pa_bas_gjahr
           pa_bas_budat
           pa_bas_monat
           pa_add_gjahr
           pa_add_budat
           pa_add_monat
           pa_out_budat
    USING  mainbuk
           i_periv.

  READ TABLE pa_out_budat INDEX 1.
  IF sy-subrc IS INITIAL.
    min_year = pa_out_budat-low(4).
    max_year = pa_out_budat-high(4).
    IF NOT min_year IS INITIAL AND
       NOT max_year IS INITIAL.

      pa_out_gjahr-sign   = 'I'.
      pa_out_gjahr-low    = min_year.
      IF min_year EQ max_year.
        pa_out_gjahr-option = 'EQ'.
      ELSE.
        pa_out_gjahr-option = 'BT'.
        pa_out_gjahr-high   = max_year.
      ENDIF.
      APPEND pa_out_gjahr.

    ENDIF.
  ENDIF.

  IF LINES( pa_out_gjahr ) IS INITIAL.
    pa_out_gjahr[] = pa_add_gjahr[].
    pa_out_monat[] = pa_add_monat[].
    pa_out_budat[] = pa_out_budat[].
  ENDIF.

ENDFORM.                    " get_incomming_period

*&---------------------------------------------------------------------*
*&      Form  add_fi_xblnr
*&---------------------------------------------------------------------*
* Replace external number and date with pattern from FI document
*----------------------------------------------------------------------*
*      -->par_extn - range for external number
*      -->par_extd - range for external date
*      -->it_bseg - BSEG table
*      -->ct_book - changed CT_BOOK
*----------------------------------------------------------------------*
FORM add_fi_xblnr
  USING
    pa_ext_num TYPE j_3rf_sgtxt_ext_num
    pa_ext_dat TYPE j_3rf_sgtxt_ext_dat
  CHANGING
    ct_book    TYPE j_3r_t_buy.

  DATA:          l_value   TYPE string,
                 l_bldat   TYPE j_3rbuy-bldat_inv,
                 l_belnr   TYPE bkpf-belnr,
                 l_gjahr   TYPE bkpf-gjahr,
                 l_index   TYPE sy-index.
  FIELD-SYMBOLS: <fs_book> TYPE j_3rbuy.

  LOOP AT ct_book ASSIGNING <fs_book>.

*   1711413 - do not overwrite journal data
    CHECK <fs_book>-journal IS INITIAL.

*   1695097 - read texts from original and correction invoices
    DO 2 TIMES.
      l_index = sy-index.
      IF l_index = 1.
        l_belnr = <fs_book>-belnr_inv.
        l_gjahr = <fs_book>-gjahr_inv.
      ELSEIF ver2012 = 'X' AND      " 1718328 - reuse data
             <fs_book>-belnr_orig_inv = <fs_book>-belnr_inv AND
             <fs_book>-gjahr_orig_inv = <fs_book>-gjahr_inv.
        if <fs_book>-xblnr_orig_inv is initial.
          <fs_book>-xblnr_orig_inv = <fs_book>-xblnr_inv.
        endif.
        if <fs_book>-bldat_orig_inv is initial.
          <fs_book>-bldat_orig_inv = <fs_book>-bldat_inv.
        endif.
        EXIT.
      ELSEIF ver2012 = 'X' AND
             NOT <fs_book>-belnr_orig_inv IS INITIAL.
        l_belnr = <fs_book>-belnr_orig_inv.
        l_gjahr = <fs_book>-gjahr_orig_inv.
      ELSE.
        EXIT.
      ENDIF.

    IF NOT pa_ext_num IS INITIAL.
      PERFORM read_texts
         USING <fs_book>-bukrs
               l_belnr
               l_gjahr
               pa_ext_num
         CHANGING l_value.

      IF NOT l_value IS INITIAL.
        IF <fs_book>-belnr_orig_inv = l_belnr AND  " 1740628
           <fs_book>-gjahr_orig_inv = l_gjahr AND
           ( <fs_book>-xblnr_orig_inv IS INITIAL OR    " 1941007
              NOT <fs_book>-belnr_corr_inv IS INITIAL ).
          <fs_book>-xblnr_orig_inv = l_value.
          <fs_book>-ext_text_orig  = 'X'. " 1909868
        ENDIF.

        IF <fs_book>-belnr_corr_inv = l_belnr AND  " 1740628
           <fs_book>-gjahr_corr_inv = l_gjahr AND
           <fs_book>-xblnr_corr_inv IS INITIAL.
          <fs_book>-xblnr_corr_inv = l_value.
          <fs_book>-ext_text_corr  = 'X'. " 1909868
        ENDIF.

        IF <fs_book>-ext_text_corr = SPACE. " 1909868
          <fs_book>-ext_text_orig = 'X'.
        ENDIF.

*       1909868 - moved after ext_text_
        IF l_index = 1.
          <fs_book>-xblnr_inv = l_value.
*         if "reference number" checked then change
          IF lcl_buy_book=>if_ext_number( is_book  = <fs_book>    " 1802867
                                          i_xblnr  = p_xblnr
                                          i_xblnr1 = p_xblnr1
                                          i_xblnr2 = p_xblnr2 ) = 'X'.
            <fs_book>-ext_number = l_value.
          ENDIF.
        ENDIF.

      ENDIF.
    ENDIF.

    IF NOT pa_ext_dat IS INITIAL.
      " read document's text
      PERFORM read_texts
         USING <fs_book>-bukrs
               l_belnr
               l_gjahr
               pa_ext_dat
         CHANGING l_value.

      " convert to date
      PERFORM convert_text_to_date
         USING    l_value
         CHANGING l_bldat.

      IF NOT l_bldat IS INITIAL.
        IF l_index = 1.
          <fs_book>-bldat_inv = l_bldat.
        ENDIF.
        IF <fs_book>-belnr_orig_inv = l_belnr AND  " 1740628
           <fs_book>-gjahr_orig_inv = l_gjahr.
          <fs_book>-bldat_orig_inv = l_bldat.  " 1817834
        ENDIF.
        IF <fs_book>-belnr_corr_inv = l_belnr AND  " 1740628
           <fs_book>-gjahr_corr_inv = l_gjahr.
          <fs_book>-bldat_corr_inv = l_bldat.
        ENDIF.
      ENDIF.
    ENDIF.

    ENDDO.

  ENDLOOP.

ENDFORM.                    " add_fi_xblnr

*&---------------------------------------------------------------------*
*&      Form  read_texts
*&---------------------------------------------------------------------*
*  Read document texts
*----------------------------------------------------------------------*
*      -->pa_bukrs - company code
*      -->pa_belnr - document number
*      -->pa_gjahr - fiscal year
*      -->pa_tdid  - text id
*      <--pa_value - returns document's text
*----------------------------------------------------------------------*
FORM read_texts
  USING pa_bukrs TYPE bkpf-bukrs
        pa_belnr TYPE bkpf-belnr
        pa_gjahr TYPE bkpf-gjahr
        pa_tdid  TYPE thead-tdid
  CHANGING pa_value TYPE string.

  CONSTANTS:
    gc_tdobject     TYPE thead-tdobject VALUE 'BELEG'.

  DATA: it_lines    TYPE TABLE OF tline,
        it_spras    TYPE TABLE OF thead-tdspras,
        wa_head     TYPE thead,
        l_tdname    TYPE thead-tdname,
        it_reftext  TYPE TABLE OF thead,
        txt_entries LIKE sy-tfill.    "#EC NEEDED
  FIELD-SYMBOLS:
    <fs_wa_spras>   LIKE LINE OF it_spras,
    <fs_wa_reftext> LIKE LINE OF it_reftext,
    <fs_wa_lines>   LIKE LINE OF it_lines.

* get tdname
  CLEAR l_tdname.
  l_tdname+00(04) = pa_bukrs.
  l_tdname+04(10) = pa_belnr.
  l_tdname+14(04) = pa_gjahr.

* read language keys
  CALL FUNCTION 'SELECT_TEXT'
    EXPORTING
      object     = gc_tdobject
      name       = l_tdname
      id         = pa_tdid
      language   = '*'
    IMPORTING
       entries    = txt_entries
    TABLES
       selections = it_reftext.

  LOOP AT it_reftext ASSIGNING <fs_wa_reftext>.
    CHECK NOT <fs_wa_reftext> IS INITIAL.

    READ TABLE it_spras
      TRANSPORTING NO FIELDS
      WITH KEY <fs_wa_reftext>-tdspras.

    IF NOT sy-subrc IS INITIAL.
      APPEND <fs_wa_reftext>-tdspras TO it_spras.
    ENDIF.

  ENDLOOP.

* read lines
  CLEAR pa_value.
  LOOP AT it_spras ASSIGNING <fs_wa_spras>.
    CALL FUNCTION 'READ_TEXT'
      EXPORTING
        id       = pa_tdid
        language = <fs_wa_spras>
        name     = l_tdname
        object   = gc_tdobject
      TABLES
        lines    = it_lines
      EXCEPTIONS
        OTHERS   = 8.
    IF sy-subrc IS INITIAL.
      LOOP AT it_lines ASSIGNING <fs_wa_lines>.
        CONCATENATE pa_value <fs_wa_lines>-tdline
            INTO    pa_value
            SEPARATED BY ' '.
      ENDLOOP.

      " if text is found then exit
      CONDENSE pa_value.
      IF NOT pa_value IS INITIAL.
        EXIT.
      ENDIF.
    ENDIF.
  ENDLOOP.

ENDFORM.                    "select_texts

*&---------------------------------------------------------------------*
*&      Form  convert_text_to_date
*&---------------------------------------------------------------------*
*  Convert text string to date
*----------------------------------------------------------------------*
*      -->pa_text   - input text
*      -->pa_date   - output date
*----------------------------------------------------------------------*
FORM convert_text_to_date
  USING    pa_text type string
  CHANGING pa_date type d.

  DATA: s    TYPE string,
        d(2) TYPE n,
        m(2) TYPE n,
        y(4) TYPE n.

  CLEAR pa_date.
  CHECK NOT pa_text IS INITIAL.
  CHECK numofchar( pa_text ) >= 8.

  s = pa_text.
  CONDENSE s.

* limit to 10 chars
  if numofchar( s ) > 10.
    s = s(10).
  ENDIF.
  CHECK NOT s IS INITIAL.
  CHECK s CO '0123456789.'.

* if "." is found then convert dd.mm.yyyy to yyyymmdd
  IF s CA '.'.
    SPLIT s at '.' into d m y.
    IF NOT sy-subrc IS INITIAL.
      CLEAR s.
    ELSE.
      IF y < 50.
        y = y + 2000.
      ELSEIF y < 100.
        y = y + 1900.
      ENDIF.
      CONCATENATE y m d INTO s.
    ENDIF.
  ENDIF.
  CHECK numofchar( s ) >= 8.

  WRITE s(8) to pa_date.
  IF NOT sy-subrc IS INITIAL.
    CLEAR pa_date.
  ENDIF.

ENDFORM.                 " convert_text_to_date

*----------------------------------------------------------------------*
*       FORM if_reversed                                               *
*----------------------------------------------------------------------*
* This function is used to detect reversal or reversed flag for        *
* documents imported from v.4.6C                                       *
*----------------------------------------------------------------------*
* The algorithm and commentary below have been borrowed from           *
* the module pool SAPMF05L, include MF05LO00                           *
*----------------------------------------------------------------------*
* Try to find out if a document is a reversal or a reversed document.  *
* That's not that easy, as in releases < 4.70 an unequivocal indicator *
* for reversal or reversed documents is missing. We cannot use         *
* BKPF-STGRD as an indicator, as it's sometimes missing or filled the  *
* other way round than in FB08. From 4.70 on, there's BKPF-XREVERSAL.  *
*----------------------------------------------------------------------*
FORM if_reversed USING    cs_bkpf   TYPE j3rdl_bkpf_cache
                 CHANGING xreversal TYPE c.

  TYPES: BEGIN OF T_TIME,
           DATE LIKE SY-DATUM,
           TIME LIKE SY-UZEIT,
         END OF T_TIME.

  DATA: t_reversal TYPE t_time,
        t_reversed TYPE t_time.

  CLEAR xreversal.

  SELECT SINGLE cpudt cputm INTO (t_reversed-date, t_reversed-time)
    FROM bkpf WHERE bukrs EQ cs_bkpf-bukrs AND
                    belnr EQ cs_bkpf-stblg AND
                    gjahr EQ cs_bkpf-stjah AND
                    stblg EQ cs_bkpf-belnr.

* Document might have been archived.....
  IF sy-subrc EQ 0.
    t_reversal-date = cs_bkpf-cpudt.
    t_reversal-time = cs_bkpf-cputm.
  ENDIF.

* Compare time stamps, might not always be true in distributed systems
  IF t_reversal GT t_reversed
* Here, we cannot do anything else than checking for transaction
* codes....
* ---- FI
    OR cs_bkpf-tcode EQ 'FB08'
*  --- MM Invoice Verification
  OR ( cs_bkpf-tcode EQ 'MR01' AND NOT cs_bkpf-stgrd IS INITIAL )
* ---- Treasury, Financial Services....
    OR cs_bkpf-tcode EQ 'TBB2'
    OR cs_bkpf-tcode EQ 'TBB5'
    OR cs_bkpf-tcode EQ 'TBB8'
    OR cs_bkpf-tcode EQ 'FWACR'
    OR cs_bkpf-tcode EQ 'FWAS'
    OR cs_bkpf-tcode EQ 'FWPR'
    OR cs_bkpf-tcode EQ 'FWER_STORNO'
    OR cs_bkpf-tcode EQ 'FWKS'
    OR cs_bkpf-tcode EQ 'FWOE'
    OR cs_bkpf-tcode EQ 'FWOS'
    OR cs_bkpf-tcode EQ 'FWSS'
    OR cs_bkpf-tcode EQ 'FWSU'
    OR cs_bkpf-tcode EQ 'FWDS'
    OR cs_bkpf-tcode EQ 'TS07'
    OR cs_bkpf-tcode EQ 'TPM29'
    OR cs_bkpf-tcode EQ 'TPM16'
    OR cs_bkpf-tcode EQ 'TPM2'
* ---- Billing (SD)
    OR cs_bkpf-tcode EQ 'VF11'
    OR cs_bkpf-tcode EQ 'VF26'.
* To be continued in case something still doesn't work.....
    xreversal = '2'.          " Reversal for ......
  ELSE.
    xreversal = '1'.          " Reversed by ....
  ENDIF.
ENDFORM.                                                    "IF_REVERSED

*&---------------------------------------------------------------------*
*&      Form  show_text_id_f4
*&---------------------------------------------------------------------*
*       Show popup dialog for Text IDs
*----------------------------------------------------------------------*
FORM show_text_id_f4
  CHANGING value TYPE TDID.

  DATA: it_ret TYPE TABLE OF ddshretval,
        s_shlp TYPE SHLP_DESCR.
  FIELD-SYMBOLS: <fs>     LIKE LINE OF s_shlp-interface,
                 <fs_ret> LIKE LINE OF it_ret.
  CONSTANTS: c_shlpname TYPE SHLPNAME   VALUE 'H_TTXID',
             c_tdobject TYPE DDSHLPSFLD VALUE 'TDOBJECT',
             c_tdid     TYPE DDSHLPSFLD VALUE 'TDID'.

* get search help description
  CALL FUNCTION 'F4IF_GET_SHLP_DESCR'
    EXPORTING
     shlpname = c_shlpname
    IMPORTING
      shlp     = s_shlp
    EXCEPTIONS
      OTHERS   = 1.
  CHECK sy-subrc IS INITIAL.

* hide TDOBJECT
  READ TABLE s_shlp-interface ASSIGNING <fs> WITH KEY SHLPFIELD = c_tdobject.
  IF sy-subrc IS INITIAL.
    <fs>-VALUE = 'BELEG'.
    CLEAR <fs>-VALFIELD.
  ENDIF.
* TDID is a field for selection
  READ TABLE s_shlp-interface ASSIGNING <fs> WITH KEY SHLPFIELD = c_tdid.
  IF sy-subrc IS INITIAL.
    <fs>-VALFIELD = 'X'.
  ENDIF.

* rise a dialog
  CALL FUNCTION 'F4IF_START_VALUE_REQUEST'
    EXPORTING
      shlp          = s_shlp
    TABLES
      return_values = it_ret.

* get result
  READ TABLE it_ret ASSIGNING <fs_ret>
    WITH KEY SHLPNAME  = c_shlpname
             FIELDNAME = c_tdid.
  IF sy-subrc IS INITIAL.
    value = <fs_ret>-FIELDVAL.
  ENDIF.

ENDFORM.                    "SHOW_STGRD_F4

*----------------------------------------------------------------------*
*    F4 help for Text IDs
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR par_extn.
  PERFORM show_text_id_f4 CHANGING par_extn.

*----------------------------------------------------------------------*
*    F4 help for Text IDs
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR par_extd.
  PERFORM show_text_id_f4 CHANGING par_extd.

*&---------------------------------------------------------------------*
*&      Form  get_long_gtd                         Note 1326787
*&---------------------------------------------------------------------*
*       Read long GTDs directly from database
*     wa_book     -->   purchase book record
*     it_customs  <--   table of GTDs - one record per PDF row
*----------------------------------------------------------------------*
FORM get_long_gtd
  TABLES it_customs
  USING  wa_book     type j_3rbuy
  CHANGING e_long_gtd type string. " 2099024

  DATA: it_bkpf    type j3rdl_t_bkpf_cache,
        it_bseg    type j3rdl_t_bseg_cache,
        it_book    type j_3r_t_buy,
        it_gtd     type table of string,
        wa_bkpf    like line of it_bkpf,
        wa_bseg    like line of it_bseg,
        wa_gtd     like line of it_gtd,
        l_long_gtd type string,
        position   type i,
        l_sep      type c, "string,
        l_str      type string.

  IF p_xml IS INITIAL.
    l_sep = space.
  ELSE.
    l_sep = ';'.
  ENDIF.

  CLEAR: l_long_gtd, e_long_gtd.
  REFRESH it_customs.

* the procedure is run for extract only, so we have
* to load BKPF and BSEG directly from the database
*  load BKPF
  SELECT * FROM BKPF
    INTO CORRESPONDING FIELDS OF TABLE it_bkpf
    WHERE bukrs = wa_book-bukrs     and
          belnr = wa_book-belnr_inv and
          gjahr = wa_book-gjahr_inv.
  CHECK sy-subrc IS INITIAL.

*  load BSEG
  SELECT * FROM BSEG
    INTO CORRESPONDING FIELDS OF TABLE it_bseg
    WHERE bukrs = wa_book-bukrs     and
          belnr = wa_book-belnr_inv and
          gjahr = wa_book-gjahr_inv.
  CHECK sy-subrc IS INITIAL.

* N1328970: override GLVOR from BKPF
  READ TABLE it_bkpf INTO wa_bkpf
    WITH KEY bukrs = wa_book-bukrs
             belnr = wa_book-belnr_inv
             gjahr = wa_book-gjahr_inv.
  IF sy-subrc IS INITIAL.
    wa_book-glvor = wa_bkpf-glvor.
  ENDIF.

  APPEND wa_book TO it_book.

*  load GTD from MM
  perform mm_belnr_old using it_bkpf it_bseg changing it_book l_long_gtd.

* process BKPF texts
  LOOP AT it_bseg INTO wa_bseg.
    check wa_bseg-mwart is initial and
          not wa_bseg-sgtxt is initial and
          wa_bseg-sgtxt in par_fi.
    LOOP AT par_fi.
      replace all occurrences of '*' in par_fi-low with space.
      if par_fi-low is initial.
        continue.
      endif.
      find first occurrence of par_fi-low in wa_bseg-sgtxt match offset position.
      if sy-subrc = 0.
        replace all occurrences of par_fi-low in wa_bseg-sgtxt with space.
        wa_bseg-sgtxt = wa_bseg-sgtxt+position.
        condense wa_bseg-sgtxt.
        exit.
      endif.
    ENDLOOP.
    concatenate l_long_gtd
                wa_bseg-sgtxt ';'
        into l_long_gtd. " separated by space.
    condense l_long_gtd.
  ENDLOOP.

  CHECK NOT l_long_gtd IS INITIAL.

* split GTDs (separator: ';')
  split l_long_gtd at ';' into table it_gtd.
  SORT it_gtd.
  DELETE ADJACENT DUPLICATES FROM it_gtd.

* now split GTD in blocks (up to 250 chars)
  clear l_long_gtd.
  loop at it_gtd into wa_gtd.
    position = STRLEN( l_long_gtd ) + STRLEN( wa_gtd ).
    if position > max_gtd_len.
      condense l_long_gtd.
      if not l_long_gtd is initial.
        append l_long_gtd to it_customs.
      endif.
      clear l_long_gtd.
    endif.
    concatenate l_long_gtd wa_gtd into l_long_gtd
       separated by l_sep.

  endloop.

  condense l_long_gtd.
  if not l_long_gtd is initial.
    append l_long_gtd to it_customs.
  endif.

*  concatenate e_long_gtd l_str into e_long_gtd separated by l_sep. " 2099024 build result string
  concatenate lines of it_gtd into e_long_gtd separated by l_sep.

ENDFORM.                    "get_long_gtd

*&---------------------------------------------------------------------*
*&      Form  get_country_from_iso               N1330087
*&---------------------------------------------------------------------*
*       Returns country name for iso code
*----------------------------------------------------------------------*
FORM get_country_from_iso
  USING    pa_voiso TYPE voiso
           pa_ver2012 TYPE c
  CHANGING pa_landx TYPE landx.

  TYPES: BEGIN OF s_iso_country,
           land1 TYPE t005-land1,
           intca TYPE t005-intca,
           landx TYPE t005t-landx,
           intcn3 TYPE t005-intcn3, "1695097 - country code
         END OF s_iso_country.

  STATICS: it_iso_country TYPE STANDARD TABLE OF s_iso_country.

  DATA   : wa_iso_country TYPE s_iso_country,
           l_spras        TYPE spras,
           it_t005t       TYPE STANDARD TABLE OF t005t.
  FIELD-SYMBOLS: <fs>     TYPE s_iso_country,
                 <fst>    TYPE t005t.

* load countries
  IF it_iso_country IS INITIAL.
    " check if RU is installed
    select single spras from T002C into l_spras
      where spras = 'R'
        and lainst = 'X'.
    if sy-subrc ne 0.
      l_spras = sy-langu.
    endif.

    " get iso codes
    SELECT land1 intca intcn3 FROM t005
      INTO CORRESPONDING FIELDS OF TABLE it_iso_country.
    SORT it_iso_country BY land1.

    " get country names
    SELECT * FROM t005t
       INTO TABLE it_t005t
       FOR ALL ENTRIES IN it_iso_country
       WHERE spras = l_spras
       AND   land1 = it_iso_country-land1.

    LOOP AT it_iso_country ASSIGNING <fs>.
      READ TABLE it_t005t ASSIGNING <fst>
         WITH KEY land1 = <fs>-land1.
      IF sy-subrc IS INITIAL.
        <fs>-landx = <fst>-landx.
      ENDIF.

      IF <fs>-landx IS INITIAL.
        <fs>-landx = <fs>-intca.
      ENDIF.
    ENDLOOP.

    SORT it_iso_country BY intca.
  ENDIF.

  pa_landx = pa_voiso.
  CHECK NOT pa_voiso IS INITIAL.

  READ TABLE it_iso_country INTO wa_iso_country
    WITH KEY intca = pa_voiso BINARY SEARCH.
  IF sy-subrc IS INITIAL.
    IF pa_ver2012 IS INITIAL.
      pa_landx = wa_iso_country-landx.
    ELSE.
      pa_landx = wa_iso_country-intcn3.
    ENDIF.
  ENDIF.

ENDFORM.                    "get_country_from_iso

*&---------------------------------------------------------------------*
*&      Form  get_gtd_from_mm                         N1334140
*&---------------------------------------------------------------------*
*       Appends GTD from MM document to l_long_gtd.
*       Format: eikp-voiso:eikp-kennu(eikp-behoe)/eikp-vorda/eikp-vornu
*----------------------------------------------------------------------*
FORM get_gtd_from_mm
   USING    tab_gtd    type eipo
   CHANGING l_long_gtd type string.

  DATA: l_landx   TYPE landx,
        l_gtd     TYPE string,
        l_date(8) TYPE c,
        l_len     TYPE i,
        country_delimiter TYPE c,
        field_delimiter   TYPE c.

  " at least one IMPORT field should be filled
  CHECK NOT tab_gtd-voiso IS INITIAL OR
        NOT tab_gtd-kennu IS INITIAL OR
        NOT tab_gtd-behoe IS INITIAL OR
        NOT tab_gtd-vorda IS INITIAL OR
        NOT tab_gtd-vornu IS INITIAL.

  IF ver2012 IS INITIAL.
    country_delimiter = ':'.
    field_delimiter   = '/'.
  ELSE.
    country_delimiter = ','.
    field_delimiter   = '/'.  " ','.  " 1924130
  ENDIF.

  " get country name
  PERFORM get_country_from_iso
    USING    tab_gtd-voiso
             ver2012
    CHANGING l_landx.
  IF NOT l_landx IS INITIAL.
    CONCATENATE l_landx country_delimiter INTO l_gtd.
  ENDIF.

  " Customs office/ID number for import"
  IF NOT tab_gtd-kennu IS INITIAL.
    CONCATENATE l_gtd tab_gtd-kennu field_delimiter into l_gtd.
  ELSEIF NOT tab_gtd-behoe IS INITIAL. "N1362831
    CONCATENATE l_gtd tab_gtd-behoe field_delimiter into l_gtd.
  ENDIF.

  " Date of prelimina doc" (DDMMYY)
  IF NOT tab_gtd-vorda IS INITIAL.
    WRITE tab_gtd-vorda TO l_date DDMMYY.
    CONCATENATE l_gtd l_date field_delimiter into l_gtd.
  ENDIF.

  " Number of Preliminary document
  IF NOT tab_gtd-vornu IS INITIAL.
    CONCATENATE l_gtd tab_gtd-vornu field_delimiter into l_gtd.
  ENDIF.

  CHECK NOT l_gtd IS INITIAL.

  " delete last '/'
  l_len = STRLEN( l_gtd ).
  l_len = l_len - 1.
  l_gtd = l_gtd(l_len).
  CONDENSE l_gtd.
  CHECK NOT l_gtd IS INITIAL.

  CONCATENATE l_long_gtd l_gtd ';'
     INTO l_long_gtd.
ENDFORM.                    "get_gtd_from_mm

*&---------------------------------------------------------------------*
*&      Form  set_archiving_params                     N1441947
*&---------------------------------------------------------------------*
*       Setting the parameters for PDF Archiving
*----------------------------------------------------------------------*
FORM set_archiving_params USING iv_out_params TYPE sfpoutputparams
                                iv_form_name  TYPE fpwbformname
                       CHANGING cv_docparams  TYPE sfpdocparams.

  DATA: ls_daratab TYPE toa_dara,
        arc_params LIKE arc_params,
        arc_report TYPE arc_params-report.

* call dialog to get archiving parameters
  arc_report = sy-cprog.
  CALL FUNCTION 'GET_ARCHIVE_PARAMETERS'
    EXPORTING
      archive_info           = '000'
      archive_text           = arc_report
      report                 = arc_report
      printer                = iv_out_params-dest
      mode                   = iv_out_params-arcmode
      no_dialog              = space
      abap_list              = space
    IMPORTING
      out_parameters         = arc_params
    EXCEPTIONS
      archive_info_not_found = 2
      OTHERS                 = 99.

  CHECK sy-subrc IS INITIAL.

  CHECK NOT arc_params-sap_object IS INITIAL AND
        NOT arc_params-ar_object  IS INITIAL.

  ls_daratab-function   = 'DARA'.
  ls_daratab-mandant    = sy-mandt.
  ls_daratab-sap_object = arc_params-sap_object.
  ls_daratab-ar_object  = arc_params-ar_object.
  ls_daratab-notiz      = 'PDF'.

* we have to commit work in PDF engine to create archive
  IF sy-batch IS INITIAL AND sy-binpt IS INITIAL.
    ls_daratab-reserve    = 'COMMIT'.
  ENDIF.

  CONCATENATE iv_form_name '-' sy-uname '-' sy-datum sy-uzeit
            INTO ls_daratab-object_id.

  REFRESH cv_docparams-daratab.
  APPEND ls_daratab TO cv_docparams-daratab.

ENDFORM.                    "set_archiving_params

*&---------------------------------------------------------------------*
*&      Form  get_calendar_period                        N1450325
*&---------------------------------------------------------------------*
*       Returns calendar period
*----------------------------------------------------------------------*
FORM get_calendar_period USING    pv_monat TYPE bkpf-monat
                         CHANGING cv_monat TYPE bkpf-monat.
  cv_monat = pv_monat.
  IF cv_monat > 12. " special periods
    cv_monat = 12.
  ENDIF.
ENDFORM.                    "get_calendar_period
*&---------------------------------------------------------------------*
*&      Form  READ_KNA1
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->i_kunnr text
*      <--ct_kna1 text
*      <--cs_kna1 text
*----------------------------------------------------------------------*
FORM read_kna1 USING    i_kunnr TYPE kna1-kunnr
               CHANGING cs_kna1 TYPE kna1.
  STATICS: st_kna1 TYPE HASHED TABLE OF kna1 WITH UNIQUE KEY kunnr.

* read from buffer
  READ TABLE st_kna1 INTO cs_kna1
    WITH TABLE KEY kunnr = i_kunnr.
  IF sy-subrc <> 0.
    SELECT SINGLE * FROM kna1 INTO cs_kna1
       WHERE kunnr = i_kunnr.
    IF sy-subrc = 0.
*     1560625 - validate country
      IF cs_kna1-land1 <> space AND cs_kna1-land1 <> 'RU'.
        CLEAR: cs_kna1-stcd1, cs_kna1-stcd3.
      ENDIF.
      INSERT cs_kna1 INTO TABLE st_kna1.
      CLEAR sy-subrc.
    ENDIF.
  ENDIF.

ENDFORM.                    " READ_KNA1
*&---------------------------------------------------------------------*
*&      Form  READ_LFA1
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->I_LIFNR text
*      <--CS_LFA1 text
*----------------------------------------------------------------------*
FORM read_lfa1 USING    i_lifnr TYPE lfa1-lifnr
               CHANGING cs_lfa1 TYPE lfa1.
  STATICS: st_lfa1 TYPE HASHED TABLE OF lfa1 WITH UNIQUE KEY lifnr.

* read from buffer
  READ TABLE st_lfa1 INTO cs_lfa1
    WITH TABLE KEY lifnr = i_lifnr.
  IF sy-subrc <> 0.
    SELECT SINGLE * FROM lfa1 INTO cs_lfa1
       WHERE lifnr = i_lifnr.
    IF sy-subrc = 0.
*     1560625 - validate country
      IF cs_lfa1-land1 <> space AND cs_lfa1-land1 <> 'RU'.
        CLEAR: cs_lfa1-stcd1, cs_lfa1-stcd3.
      ENDIF.
      INSERT cs_lfa1 INTO TABLE st_lfa1.
      CLEAR sy-subrc.
    ENDIF.
  ENDIF.

ENDFORM.                    " READ_LFA1
*&---------------------------------------------------------------------*
*&      Form  GET_SEC_ITEM_TRN                                 N1559418
*&---------------------------------------------------------------------*
*       Get parameters of tax transfer for 0% without tax document
*----------------------------------------------------------------------*
*      -->i_bukrs  text
*      -->i_belnr  text
*      -->i_gjahr  text
*      <--c_monat  text
*      <--c_budat  text
*      <--c_bldat  text
*----------------------------------------------------------------------*
FORM GET_SEC_ITEM_TRN  USING    i_bukrs TYPE bkpf-bukrs
                                i_belnr TYPE bkpf-belnr
                                i_gjahr TYPE bkpf-gjahr
                                it_bkpf TYPE j3rdl_t_bkpf_cache
                       CHANGING c_monat TYPE bkpf-monat
                                c_gjahr TYPE bkpf-gjahr
                                c_budat TYPE bkpf-budat
                                c_bldat TYPE bkpf-bldat
                                c_hwaer TYPE bkpf-hwaer.
  DATA:  lt_sec_item TYPE STANDARD TABLE OF j_3rfsec_item,
         ls_bkpf     TYPE j3rdl_bkpf_cache,
         l_monat     TYPE bkpf-monat,
         l_gjahr     TYPE bkpf-gjahr.
  FIELD-SYMBOLS: <fs_sec_item> TYPE j_3rfsec_item.

* read J_3RFSEC_ITEM
  SELECT * FROM j_3rfsec_item
     INTO TABLE lt_sec_item
     WHERE bukrs = i_bukrs    AND
           belnr = i_belnr    AND
           gjahr = i_gjahr    AND
           line_type = 'RL'   AND
           tbeln = space      AND
           tgjah = space      AND
           kbetr = 0          AND
           stmd2 > '00000000'.
  LOOP AT lt_sec_item ASSIGNING <fs_sec_item>.
    CALL FUNCTION 'FI_PERIOD_DETERMINE'
      EXPORTING
        i_budat        = <fs_sec_item>-stmd2
        i_bukrs        = i_bukrs
      IMPORTING
        e_gjahr        = l_gjahr
        e_monat        = l_monat
      EXCEPTIONS
        OTHERS         = 1.
    IF sy-subrc = 0.
      c_monat      = l_monat.
      c_gjahr      = l_gjahr.
      c_bldat      = <fs_sec_item>-stmd2.
      c_budat      = c_bldat.
      c_budat(4)   = l_gjahr.
      c_budat+4(2) = l_monat.
      EXIT.
    ENDIF.
  ENDLOOP.

* if 0% transfer is found then fill waers
  IF NOT c_monat IS INITIAL.
    READ TABLE it_bkpf INTO ls_bkpf
      WITH TABLE KEY bukrs = i_bukrs
                     belnr = i_belnr
                     gjahr = i_gjahr.
    IF sy-subrc = 0.
      c_hwaer = ls_bkpf-hwaer.
    ENDIF.
  ENDIF.

ENDFORM.                    " GET_SEC_ITEM_TRN
*&---------------------------------------------------------------------*
*&      Form  ADOBE_FILL_INV                                   N1695097
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->WA_BOOK  text
*      <--WA_BUFF  text
*----------------------------------------------------------------------*
FORM ADOBE_FILL_INV  USING    wa_book TYPE j_3rbuy
                     CHANGING wa_buff TYPE ty_adobe_line.

* 1710598 - clear correction fields
  CLEAR: wa_buff-bldat_corr, wa_buff-ext_number_corr, wa_buff-bldat_rev,
         wa_buff-number_rev, wa_buff-bldat_corr_rev,  wa_buff-ext_number_corr,
         wa_buff-number_corr_rev.

   IF wa_book-corr_type      <> j3rdl_corr_flag_none  AND
      wa_book-belnr_orig_inv <> space.

      CASE wa_book-corr_type.
        WHEN j3rdl_corr_flag_corr.
*         move invoice to correction column
          wa_buff-bldat_corr      = wa_buff-bldat_inv.
          wa_buff-ext_number_corr = wa_buff-ext_number.
*         IF ( ( p_xblnr  = 'X' AND wa_book-corr_book = space ) OR   " 1739523
*              ( p_xblnr1 = 'X' AND wa_book-corr_book = 'X' ) ) AND
          IF lcl_buy_book=>if_ext_number( is_book  = wa_book     " 1802867
                                          i_xblnr  = p_xblnr
                                          i_xblnr1 = p_xblnr1
                                          i_xblnr2 = p_xblnr2 ) = 'X' AND
             NOT wa_book-xblnr_corr_inv IS INITIAL.
            wa_buff-ext_number_corr = wa_book-xblnr_corr_inv.
          ENDIF.
          IF NOT wa_book-bldat_orig_rev IS INITIAL. " 1734773 - revised invoice
            wa_buff-number_rev = wa_book-number_rev.
            wa_buff-bldat_rev  = wa_book-bldat_orig_rev.
          ENDIF.
         if wa_book-flag_add = 'X'  and
            wa_book-belnr_inv = wa_book-belnr_trn.
*           IF ( p_xblnr  = 'X' AND wa_book-corr_book = space ) OR
*              ( p_xblnr1 = 'X' AND wa_book-corr_book = 'X' ).
           IF lcl_buy_book=>if_ext_number( is_book  = wa_book     " 1802867
                                           i_xblnr  = p_xblnr
                                           i_xblnr1 = p_xblnr1
                                           i_xblnr2 = p_xblnr2 ) = 'X'.
*             do nothing
           ELSEIF NOT wa_book-belnr_corr_inv IS INITIAL.
              CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
                EXPORTING   input  = wa_book-belnr_corr_inv
                IMPORTING   output = wa_buff-ext_number_corr.
           ELSE.
              CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
                EXPORTING   input  = wa_book-belnr_inv
                IMPORTING   output = wa_buff-ext_number_corr.
           ENDIF.
         endif.

        WHEN j3rdl_corr_flag_rev.
*         move invoice to revision column
          wa_buff-bldat_rev  = wa_buff-bldat_inv.
          wa_buff-number_rev = wa_book-number_rev.
        WHEN j3rdl_corr_flag_corr_rev.
*         move invoice to revision of corection column
          wa_buff-bldat_corr_rev  = wa_buff-bldat_inv.
          wa_buff-ext_number_corr = wa_buff-ext_number.
*          IF ( ( p_xblnr  = 'X' AND wa_book-corr_book = space ) OR   " 1739523
*               ( p_xblnr1 = 'X' AND wa_book-corr_book = 'X' ) ) AND
          IF lcl_buy_book=>if_ext_number( is_book  = wa_book     " 1802867
                                          i_xblnr  = p_xblnr
                                          i_xblnr1 = p_xblnr1
                                          i_xblnr2 = p_xblnr2 ) = 'X' AND
             NOT wa_book-xblnr_corr_inv IS INITIAL.
            wa_buff-ext_number_corr = wa_book-xblnr_corr_inv.
          ENDIF.
          wa_buff-bldat_corr      = wa_book-bldat_corr_inv.
          wa_buff-number_corr_rev = wa_book-number_corr_rev.
          IF wa_buff-number_corr_rev IS INITIAL.
            wa_buff-number_corr_rev = wa_book-number_rev.
          ENDIF.
          IF NOT wa_book-bldat_orig_rev IS INITIAL. " 1734773 - revised invoice
            wa_buff-number_rev = wa_book-number_rev.
            wa_buff-bldat_rev  = wa_book-bldat_orig_rev.
          ENDIF.
*         1720794 - correction number
          IF NOT wa_book-belnr_corr_inv IS INITIAL.
*            IF ( p_xblnr  = 'X' AND wa_book-corr_book = space ) OR
*               ( p_xblnr1 = 'X' AND wa_book-corr_book = 'X' ).
            IF lcl_buy_book=>if_ext_number( is_book  = wa_book     " 1802867
                                            i_xblnr  = p_xblnr
                                            i_xblnr1 = p_xblnr1
                                            i_xblnr2 = p_xblnr2 ) = 'X'.
*             external number
            ELSE.
              CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
                EXPORTING   input  = wa_book-belnr_corr_inv
                IMPORTING   output = wa_buff-ext_number_corr.
            ENDIF.
          ENDIF.
      ENDCASE.

*     display original invoice in the first column
      wa_buff-bldat_inv    = wa_book-bldat_orig_inv.
*      IF ( p_xblnr  = 'X' AND wa_book-corr_book = space ) OR
*         ( p_xblnr1 = 'X' AND wa_book-corr_book = 'X' ).
      IF lcl_buy_book=>if_ext_number( is_book  = wa_book     " 1802867
                                      i_xblnr  = p_xblnr
                                      i_xblnr1 = p_xblnr1
                                      i_xblnr2 = p_xblnr2 ) = 'X'.
        wa_buff-ext_number = wa_book-xblnr_orig_inv.
      ELSE.
        CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
          EXPORTING   input  = wa_book-belnr_orig_inv
          IMPORTING   output = wa_buff-ext_number.
      ENDIF.
   ENDIF.

*  1711413 - copy external number from journal
   IF wa_book-corr_type      = j3rdl_corr_flag_none  AND
      wa_book-journal        <> space                AND
      wa_book-xblnr_orig_inv <> space.
*     IF ( p_xblnr  = 'X' AND wa_book-corr_book = space ) OR
*        ( p_xblnr1 = 'X' AND wa_book-corr_book = 'X' ).
     IF lcl_buy_book=>if_ext_number( is_book  = wa_book     " 1802867
                                     i_xblnr  = p_xblnr
                                     i_xblnr1 = p_xblnr1
                                     i_xblnr2 = p_xblnr2 ) = 'X'.
       wa_buff-ext_number = wa_book-xblnr_orig_inv.
     ENDIF.
   ENDIF.

*  2074991 - fill currency column
   IF ver2014 = 'X'.
     PERFORM adobe_currency_column USING    wa_book-wrbtr_wrs_pay
                                            wa_book-dmbtr_wrs_pay
                                   CHANGING wa_buff-curtxt
                                            wa_buff-altwr. " 2099024

*    2085792 - commented: clear WRBTR_PAY for local currencies
*     IF wa_book-wrbtr_wrs_pay = wa_book-dmbtr_wrs_pay.
*       CLEAR wa_buff-wrbtr_pay.
*     ENDIF.
   ENDIF.
ENDFORM.                    " ADOBE_FILL_INV
*&---------------------------------------------------------------------*
*&      Form  ADOBE_PERIOD                                     N1695097
*&---------------------------------------------------------------------*
*       Prepare reported period for ADOBE output for Decree 1137
*----------------------------------------------------------------------*
*      <--P_GJAHR        text
*      <--P_MONAT_BEGIN  text
*      <--P_MONAT_END    text
*      <--P_BUDAT_BEGIN  text
*      <--P_BUDAT_END    text
*----------------------------------------------------------------------*
FORM ADOBE_PERIOD CHANGING p_gjahr       TYPE bkpf-gjahr
                           p_monat_begin TYPE bkpf-monat
                           p_monat_end   TYPE bkpf-monat
                           p_budat_begin TYPE bkpf-budat
                           p_budat_end   TYPE bkpf-budat.

  if p_budat_begin is initial.
    p_budat_begin(4)   = p_gjahr.
    p_budat_begin+4(2) = p_monat_begin.
    p_budat_begin+6(2) = '01'.
  endif.

  if p_budat_end is initial.
    p_budat_end(4)   = p_gjahr.
    if p_monat_end is initial.
      p_budat_end+4(2) = p_monat_begin.
    else.
      p_budat_end+4(2) = p_monat_end.
    endif.
    p_budat_end+6(2) = '01'.
    p_budat_end      = p_budat_end + 31.
    p_budat_end+6(2) = '01'.
    p_budat_end      = p_budat_end - 1.
  endif.
  clear: p_gjahr, p_monat_begin, p_monat_end.

ENDFORM.                    " ADOBE_PERIOD
*&---------------------------------------------------------------------*
*&      Form  GET_INVOICE_JOURNAL                              N1704703
*&---------------------------------------------------------------------*
*       Fill data from Invoice Journal
*----------------------------------------------------------------------*
*      -->is_doclist text
*      <--cs_book    text
*      <--e_journal  text
*----------------------------------------------------------------------*
FORM GET_INVOICE_JOURNAL  using    is_doclist type j3rdl_doclist_ext
                          changing cs_book    type j_3rbuy
                                   e_journal  type c.
  DATA: l_name(30) TYPE c,
        l_n(1)     TYPE n,
        l_fullname TYPE j3rdl_doclist_ext-journal_name.
  FIELD-SYMBOLS: <fs> TYPE j_3rbuy-name1_cred.
  CLEAR e_journal.

* data from journal
  CHECK NOT is_doclist-journal_name IS INITIAL.

  e_journal = 'X'.

  IF NOT is_doclist-stcd1 IS INITIAL.
    cs_book-stcd1_cred = is_doclist-stcd1.
  ENDIF.

  IF NOT is_doclist-stcd3 IS INITIAL.
    cs_book-stcd3_cred = is_doclist-stcd3.
  ENDIF.

* split name
  l_fullname = is_doclist-journal_name.
  DO 4 TIMES.
    l_n = sy-index.
    CONCATENATE 'CS_BOOK-NAME' l_n '_CRED' INTO l_name.
    ASSIGN (l_name) TO <fs>.
    <fs> = l_fullname(35).
    CONCATENATE 'CS_BOOK-NAME' l_n '_CRED_R' INTO l_name.
    ASSIGN (l_name) TO <fs>.
    <fs> = l_fullname(35).

    l_fullname = l_fullname+35.
    IF l_fullname IS INITIAL.
      EXIT.
    ENDIF.
  ENDDO.

ENDFORM.                    " GET_INVOICE_JOURNAL
*&---------------------------------------------------------------------*
*&      Form  SELECT_MM_RSEG_PO                                N1718196
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->i_awkey       text
*      <--et_itab_rseg  text
*----------------------------------------------------------------------*
FORM SELECT_MM_RSEG_PO  USING    i_awkey      TYPE bkpf-awkey
                     CHANGING et_itab_rseg TYPE ty_t_itab_rseg.

  select ebeln ebelp from rseg
    into corresponding fields of table et_itab_rseg
    where belnr = i_awkey(10) and
          gjahr = i_awkey+10(4).

ENDFORM.                    " SELECT_MM_RSEG_PO
*&---------------------------------------------------------------------*
*&      Form  SELECT_MM_EKKO_EIKP                              N1718196
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_TAB_GTD_HEADER_EBELN  text
*      <--P_TAB_GTD  text
*----------------------------------------------------------------------*
FORM SELECT_MM_EKKO_EIKP  USING    i_ebeln TYPE bseg-ebeln
                          CHANGING tab_gtd TYPE eipo
                                   e_exnum TYPE ekko-exnum.

  select single b~voiso b~vornu b~exnum b~kennu b~vorda b~behoe
    from ekko as a inner join eikp as b on a~exnum = b~exnum
    into (tab_gtd-voiso, tab_gtd-vornu, e_exnum,
          tab_gtd-kennu, tab_gtd-vorda, tab_gtd-behoe)
    where a~ebeln = i_ebeln.

ENDFORM.                    " SELECT_MM_EKKO_EIKP
*&---------------------------------------------------------------------*
*&      Form  SELECT_MM_EIPO                                   N1718196
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->I_EXNUM  text
*      -->I_EBELP  text
*      <--TAB_GTD  text
*----------------------------------------------------------------------*
FORM SELECT_MM_EIPO  USING    i_exnum TYPE eipo-exnum
                              i_ebelp TYPE bseg-ebelp
                     CHANGING tab_gtd TYPE eipo.

  select single voiso vornu kennu vorda behoe
    into (tab_gtd-voiso, tab_gtd-vornu, tab_gtd-kennu, tab_gtd-vorda,
          tab_gtd-behoe)
    from eipo
    where exnum = i_exnum and
          expos = i_ebelp.

ENDFORM.                    " SELECT_MM_EIPO
*&---------------------------------------------------------------------*
*&      Form  SELECT_MM_EKBE                                   N1718196
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->TAB_GTD_POS[]  text
*      <--ET_EKBE[]  text
*----------------------------------------------------------------------*
FORM SELECT_MM_EKBE  using    tab_gtd_pos type ty_t_tab_gtd_pos
                     changing et_ekbe     type ty_t_ekbe.

  select * from ekbe
    into corresponding fields of table et_ekbe
    for all entries in tab_gtd_pos
    where ebeln = tab_gtd_pos-ebeln and
          ebelp = tab_gtd_pos-ebelp.

ENDFORM.                    " SELECT_MM_EKBE
*&---------------------------------------------------------------------*
*&      Form  SELECT_MM_ESSR                                   N1761164
*&---------------------------------------------------------------------*
*       Select from ESSR
*----------------------------------------------------------------------*
*      -->i_lblni  text
*      <--es_essr  text
*----------------------------------------------------------------------*
form SELECT_MM_ESSR  using    i_lblni type essr-lblni
                     changing es_essr type essr.
  select single * from essr
    into es_essr
    where lblni = i_lblni.

endform.                    " SELECT_MM_ESSR
*&---------------------------------------------------------------------*
*&      Form  REMOVE_GTD_DUPLICATES                             1924130
*&---------------------------------------------------------------------*
*       Remove GTD duplicates from result
*----------------------------------------------------------------------*
FORM remove_gtd_duplicates  CHANGING c_long_gtd TYPE string.
  DATA: lt_customs TYPE TABLE OF string.

  CHECK NOT c_long_gtd IS INITIAL.
  CHECK c_long_gtd CS ';'.

  SPLIT c_long_gtd AT ';' INTO TABLE lt_customs.

  SORT lt_customs.
  DELETE ADJACENT DUPLICATES FROM lt_customs.

  CONCATENATE LINES OF lt_customs INTO c_long_gtd SEPARATED BY ';'.

ENDFORM.                    " REMOVE_GTD_DUPLICATES
*&---------------------------------------------------------------------*
*&      Form  REMOVE_FORGOTTEN_PREFIX                       2037120
*&---------------------------------------------------------------------*
*       Remove prefix
*----------------------------------------------------------------------*
FORM remove_forgotten_prefix  CHANGING cs_bkpf TYPE j3rdl_bkpf_cache.

  DATA: l_pos   TYPE sy-fdpos,
        l_bktxt TYPE bkpf-bktxt.

  CHECK cs_bkpf-bktxt CS j3rdl_forgotten_prefix.

* !!!  this is forgotten invoice !!!

* remove prefix
  l_pos = sy-fdpos.
  IF l_pos > 0.
    l_bktxt = cs_bkpf-bktxt(l_pos).
  ENDIF.
  l_pos = l_pos + j3rdl_forgotten_len.
  cs_bkpf-bktxt = cs_bkpf-bktxt+l_pos.
  CONCATENATE l_bktxt cs_bkpf-bktxt INTO cs_bkpf-bktxt.

ENDFORM.             " remove_forgotten_prefix
*&---------------------------------------------------------------------*
*&      Form  check_forgotten_invoices                      2037120
*&---------------------------------------------------------------------*
*       Check forgotten invoices
*----------------------------------------------------------------------*
FORM check_forgotten_invoices CHANGING cs_bkpf TYPE j3rdl_bkpf_cache
                                       e_flag  TYPE flag.

  CHECK cs_bkpf-bktxt CS j3rdl_forgotten_prefix.

* remove prefix
  PERFORM remove_forgotten_prefix  CHANGING cs_bkpf.

  e_flag = 'X'.

ENDFORM.             " check_forgotten_invoices
*&---------------------------------------------------------------------*
*&      Form  FILL_OPER_TYP
*&---------------------------------------------------------------------*
*       Group operation type by invoice
*----------------------------------------------------------------------*
FORM fill_oper_typ USING ct_doclist type j3rdl_t_doclist_ext.
  TYPES:
    BEGIN OF ty_oper_typ,
      bukrs           TYPE j3rdl_doclist_ext-bukrs,
      belnr_orig_inv  TYPE j3rdl_doclist_ext-belnr_orig_inv,
      gjahr_orig_inv  TYPE j3rdl_doclist_ext-gjahr_orig_inv,
      belnr_corr_inv  TYPE j3rdl_doclist_ext-belnr_corr_inv,
      gjahr_corr_inv  TYPE j3rdl_doclist_ext-gjahr_corr_inv,
      number_rev      TYPE j3rdl_doclist_ext-number_rev,
      number_orig_rev TYPE j3rdl_doclist_ext-number_orig_rev,
      oper_typ        TYPE j3rdl_doclist_ext-oper_typ,
    END OF ty_oper_typ,
    ty_t_oper_typ TYPE SORTED TABLE OF ty_oper_typ WITH UNIQUE KEY
      bukrs belnr_orig_inv gjahr_orig_inv belnr_corr_inv gjahr_corr_inv
      number_rev number_orig_rev.

  DATA: lt_oper TYPE ty_t_oper_typ,
        ls_oper TYPE ty_oper_typ,
        l_len   TYPE sy-index.
  FIELD-SYMBOLS: <fs>      TYPE j3rdl_doclist_ext,
                 <fs_oper> TYPE ty_oper_typ.

* collect operation types
  LOOP AT ct_doclist ASSIGNING <fs> WHERE NOT oper_typ IS INITIAL.
    MOVE-CORRESPONDING <fs> TO ls_oper.
    CONCATENATE ';' ls_oper-oper_typ ';' INTO ls_oper-oper_typ. " add delimiters

*   2085792 fill original invoice
    IF ls_oper-belnr_orig_inv IS INITIAL.
      ls_oper-belnr_orig_inv = <fs>-belnr_inv.
      ls_oper-gjahr_orig_inv = <fs>-gjahr_inv.
    ENDIF.

*   search
    READ TABLE lt_oper ASSIGNING <fs_oper>
      WITH TABLE KEY bukrs           = ls_oper-bukrs
                     belnr_orig_inv  = ls_oper-belnr_orig_inv
                     gjahr_orig_inv  = ls_oper-gjahr_orig_inv
                     belnr_corr_inv  = ls_oper-belnr_corr_inv
                     gjahr_corr_inv  = ls_oper-gjahr_corr_inv
                     number_rev      = ls_oper-number_rev
                     number_orig_rev = ls_oper-number_orig_rev.
    IF sy-subrc <> 0.
      INSERT ls_oper INTO TABLE lt_oper.
    ELSE.
*     merge operation types
      CHECK <fs_oper>-oper_typ NS ls_oper-oper_typ.
      ls_oper-oper_typ = ls_oper-oper_typ+1.
      CONCATENATE <fs_oper>-oper_typ ls_oper-oper_typ INTO <fs_oper>-oper_typ.
    ENDIF.
  ENDLOOP.

  CHECK NOT lt_oper[] IS INITIAL.

* remove start/stop delimiters
  LOOP AT lt_oper ASSIGNING <fs_oper>.
    l_len = STRLEN( <fs_oper>-oper_typ ) - 2.
    <fs_oper>-oper_typ = <fs_oper>-oper_typ+1(l_len).
  ENDLOOP.

* save operation types back
  LOOP AT ct_doclist ASSIGNING <fs>.
    ls_oper-belnr_orig_inv = <fs>-belnr_orig_inv.
    ls_oper-gjahr_orig_inv = <fs>-gjahr_orig_inv.
    IF ls_oper-belnr_orig_inv IS INITIAL.
      ls_oper-belnr_orig_inv = <fs>-belnr_inv.
      ls_oper-gjahr_orig_inv = <fs>-gjahr_inv.
    ENDIF.

    READ TABLE lt_oper ASSIGNING <fs_oper>
      WITH TABLE KEY bukrs           = <fs>-bukrs
                     belnr_orig_inv  = ls_oper-belnr_orig_inv
                     gjahr_orig_inv  = ls_oper-gjahr_orig_inv
                     belnr_corr_inv  = <fs>-belnr_corr_inv
                     gjahr_corr_inv  = <fs>-gjahr_corr_inv
                     number_rev      = <fs>-number_rev
                     number_orig_rev = <fs>-number_orig_rev.
    IF sy-subrc = 0.
      <fs>-oper_typ = <fs_oper>-oper_typ.
    ENDIF.
  ENDLOOP.

ENDFORM.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR par_payn.
  PERFORM show_text_id_f4 CHANGING par_payn.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR par_payd.
  PERFORM show_text_id_f4 CHANGING par_payd.

*Text symbol text
*001:PURCHASE LEDGER
*002:Appendix No 2
*003:to the Regulations for Keeping Incoming and Outgoing Invoice Registers,
*004:Sales and Purchase Ledgers in Value Added Tax Settlements
*005:(in accordance with the Government Regulation No 283 of 11 May 2006)
*006:ADDITIONAL SHEET OF PURCHASE LEDGER NO
*007:Fiscal period (month, quarter), year the invoice was registered in before corrections were made
*008:Additional sheet printed out on
*009:Appendix No 4
*011:Customer:
*012:Buyer's Taxpayer Identification Number and Code of Reason for Registration
*013:Purchases within a given period:
*017:Reset Clearing Items
*018:Page:
*019:Req:
*020:CoCo:
*021:Company Code
*022:Use Address Name
*023:Options
*024:Adr.
*030:Appendix No 4
*031:in accordance with the
*032:Government Regulation
*033:of 26.12.2011 No 1137
*034:Approved by Government Regulation
*035:of 26.12.2011 No 1137
*036:Fiscal period, year, the invoice was registered in before corrections were made
*039:(as amended by RF Government Decree of 24.10.2013 No 952)
*040:Fiscal period, year
*041:Version per Decree 735 (Extension of 1137)
*042:(as amended by RF Government Decree of 30.07.2014 No 735)
*100:Invoice
*101:Ref. Invoice
*102:Invoice Date
*103:Payment
*104:Payment Date
*105:Ref. Pyt/Cred
*106:Tax Transfer
*107:Date
*108:Vendor
*109:Vendor Name
*110:INN
*111:Invoice Amt Incl. VAT
*112:DC
*113:Payt Amt Incl. VAT
*114:DC
*115:VAT Base (&tx1)
*116:DC
*117:VAT (&tx1)
*118:DC
*119: Tax Code
*120:TAX/2
*121:Inv. Amt DC
*122:TAX-0
*123:DC
*124:Base Tax/2
*125:DC
*126:Base Tax-0
*127:DC
*128:Document cleared:  &clrdoc
*129:C
*131:O
*132:Own:
*133:Ver:
*134:Date:
*135:Amt incl. VAT
*136:G/R Date
*137:PO
*138:Year
*139:CD No.
*140:Country
*141:Line
*142:YInv
*143:YPay
*144:Nontaxable
*145:Cur
*146:YTrn
*147:pPO
*148:Vendor Name 2
*150:Text (Transf. Doc.)
*151:Text (Invoice)
*152:Text (Payment)
*153:PoDa.TRN
*154:Pr
*155:PoDa.INV
*156:PoDa.PAY
*157:BuSf
*158:FactDcPay
*159:DtFctPay
*160:DPFctPay
*161:ByFctPay
*162:FctDcDc
*163:YFct
*164:RefFct
*165:TxtFct
*171:Orig. doc.
*172:Period
*173:Year
*174:Ref.Orig.Doc.
*200:Amt.TAX DC
*201:Bas20% DC
*202:Amt.TRN DC
*203:DeliveryDc
*204:Payment Currency
*205:Invoice Currency
*206:Add info for doc's
*207:Line Number
*208:Posting Date (Invoice)
*209:VAT Base Curency
*210:VAT Currency
*211:Tax code
*212:VAT curr code
*213:DocCur(Invoice)
*214:Document Currency
*215:UnpTxDocCur
*216:Goods Receipt Date
*217:Country of Origin
*218:Document curr amount (invoice)
*219:Posting date (payment)
*220:DocCurrAmnt (paym)
*221:Document curr (payment)
*222:Actual paymnt doc
*223:Fiscal year of actual paymnt doc
*224:Actual paymnt doc date
*225:Actual paymnt doc posting date
*226:Actual paymnt doc reference
*227:Actual paymnt doc header text
*228:Transfer document post. date
*229:Transfer document post. per.
*230:Transfer doc local curr amn
*231:Local curr (transf doc)
*232:Docum curr (transfer doc)
*233:Reversal (payment)
*234:Reversal (transfer doc)
*235:Reversal (invoice)
*236:Date extrnal invoce
*237:Vendor invoice
*238:Defferd tax acc
*239:Settlment tax acc
*240:Purchase order
*241:Position of purchase order
*242:Partial payment
*243:Delivery of PurOrder
*244:Doc curr (transf doc)
*245:Doc curr base of VAT &tx1
*246:Document currency tax exempt
*247:Doc curr tax exempt
*248:DocCurrVAT &tx1
*249:Name of vendor version (Russian)
*250:User name (invoice)
*251:UserInvoice
*252:UsrPayment
*253:User name (payment)
*254:UsrNamActPay
*255:User name (Actual payment)
*256:CC
*257:InvClear cancelation
*258:Pict of status
*259:PcSts
*260:Tax Transfer
*261:Tax Transfer Date
*262:GL deferred VAT
*263:GL VAT settelment
*264:GL of vendor
*265:External invoce (reference code 1)
*266:Date of external invoce (reference code 1)
*267:InvRepDate
*268:Invoice Tax Reporting Date
*269:TrnRepDate
*270:Transfer Tax Reporting Date
*271:PayConfNum
*272:Payment Confirmation Number
*273:PayConfDat
*274:Payment Confirmation Date
*275:Total VAT
*276:Total VAT
*300:************************
*301:ALL DATA FROM EXTRACT
*302:FOR SELECTED PERIOD RANGE
*303:WILL BE DELETED!
*304:Warning
*305:            From &m1 to &m2 &m3
*306:Extract: &m1 Set: &m2
*310:EXTRACT: YES
*311:EXTRACT:  NO
*400:EXTRACT IS ALREADY CREATED !
*401:EXTRACT DOES NOT EXIST !
*402:EXTRACT CREATED
*403:EXTRACT IS DELETED !
*404:Delete EXCTRACT is canceled
*405:EXTRACT does not exist or was deleted.
*406:This operation only for background
*407:Input EXTRACT name !
*408:EXTRACT EXIST.
*409:Input period
*410:Total Lines:
*411:EX:Documents for selected period does not exist
*412:DB:Documents for selected period does not exist
*413:D't use range of period for delete extract operation
*414:Duplicate key found.
*440:Additional Sheets Selections
*441:=>TOTAL
*442:Incoming totals have not been found
*443:Calculate totals anew?
*444:Message
*450:EXTRACT(*-parameters for selection)
*451:For this DATE TRN Docs does not exist.
*452:Create
*453:Delete
*454:View
*455:ALV-GRID(4.6B+)
*457:P
*460:Show Header in ALV List
*461:No Extract
*464:About
*465:PURCHASE LEDGER
*466:ExtrInv
*467:DtExtInv
*468:GLdeferVAT
*469:GLsettVAT
*470:GLcustomer
*471:&p1
*501:Input Document data
*502:For Period
*503:Documents on EXTRACT exist.
*504:or
*505:Input Year
*506:Dynamic selection is not possible!
*507:Input Company Code
*508:Input name of extrct
*580:Enter a company code
*581:Enter a fiscal year
*583:Enter a posting date, period, or tax-relevant document
*600:Tax transfer document
*601:This function only works from R/3 4.6C
*602:Chief Accountant: ___________________________________
*603:Print of SmartForm only for extracts
*604:Purchase Ledger (Data Extract)
*605:Print SmFrm PurBook work only with one extract selected
*606:Customer/Vendor Fullname
*607:External Invoice No
*700:Values for this field is uniname and will be created with "Create line index" function
*701:For Co &buk version RU of address does not exist
*800:Field
*801:ShName
*802:Long Name
*803:Field Cat.
*804:KPP No
*805:KPP Number
*806:Business Area
*807:Buyer
*809:Purchase over period
*810:from
*811:to
*901:Futher Selections
*910:Reporting Hierarchy Code For Basic Sheet
*911:Reporting Hierarchy Code For Additional Sheet
*912:XML Signatory
*913:Auth. Document
*914:Adobe and XML Options
*915:Adobe Options
*916:Version
*950:Specify Run Date of  numbering to be deleted
*951:Specify identificator of numbering to be deleted
*953:Line numbering will be deleted! Do you want to proceed?
*954:Warning!
*955:Line numbeting doesn't exist!
*A15:VAT Base (20%)
*A17:VAT (20%)
*A45:Doc curr base of VAT 20%
*A48:DocCurrVAT 20%
*B15:VAT Base (10%)
*B17:VAT (10%)
*B45:Doc curr base of VAT 10%
*B48:DocCurrVAT 10%
*C15:VAT Base (0%)
*C17:UnPstTax
*C45:Doc curr base of VAT 0%
*C48:DocCurrVAT 0%
*E15:VAT Base (18%)
*E17:VAT (18%)
*E45:Doc curr base of VAT 18%
*E48:DocCurrVAT 18%
*EXD:Reference Number for Outgoing Down Payments
*EXL:Reference Number for Sales Corrections

*EXT:Output Control
*Selection text
*AD_BELNR:        Tax-Relevant Document
*AD_BLART:        Adjustment Document Type
*AD_BUDAT:D       .
*AD_DATE:        Date of Output
*AD_DOC_N:D       .
*AD_GJAHR:D       .
*AD_MONAT:        Closed Fiscal Period
*AD_SHEET:D       .
*AD_VTDAT:D       .
*ALL_INCL:D       .
*BK_VERS:D       .
*BR_BELNR:        Tax-Relevant Document
*BR_BUDAT:D       .
*BR_BUKRS:D       .
*BR_GJAHR:D       .
*LAUFD:        Run Date
*LAUFI:        Additional Identification
*MAINBUK:        Main Company Code
*NUM_AUTO:D       .
*NUM_CRET:D       .
*NUM_DEL:D       .
*NUM_STRT:D       .
*PAR_COL:D       .
*PAR_CPAG:        Output Language RU
*PAR_EXT:D       .
*PAR_EXTD:D       .
*PAR_EXTN:D       .
*PAR_EXTP:D       .
*PAR_FI:        Abbr. for Customs Decl. No.
*PA_ADOBE:        Form
*PA_XML:D       .
*PA_XMLA:D       .
*P_A1_NAM:D       .
*P_ACTPAY:D       .
*P_ADD:D       .
*P_ADOBE:D       .
*P_ALV:D       .
*P_DEL:D       .
*P_DP_ADD:D       .
*P_DP_BAS:D       .
*P_EX1:D       .
*P_EX1C:        Compare with Extract
*P_EX2:D       .
*P_EX3:D       .
*P_EXT1:        Short Description
*P_EXTNUM:D       .
*P_EX_DST:        Destination Extract
*P_EX_SRC:        Sources Extracts
*P_LNGGTD:D       .
*P_LOAD:D       .
*P_MERGE:D       .
*P_NOEX:D       .
*P_QUART:D       .
*P_S1_NAM:D       .
*P_S1_TYP:D       .
*P_SAVE:D       .
*P_SUM:D       .
*P_TAGGED:        PDF Tagged
*P_UPDATE:D       .
*P_VARI:D       .
*P_XBLNR:D       .
*P_XBLNR1:D       .
*P_XBLNR2:D       .
*P_XML:D       .
*P_XMLDIR:        Folder
*P_XMLTAX:D       .
*SEL_BLDT:D       .
*SEL_BLRT:D       .
*SEL_MONA:D       .
*SEL_MWKZ:D       .
*SEL_VTDT:D       .
*S_ADR:D       .
*S_LIFNR:D       .
*VER2012:D       .
*VER2014:D       .
*VR_CNUM:D       .
*VR_GJAHR:D       .
*VR_PERIO:D       .
*VR_PROD:D       .
*VR_TYPE:D       .
