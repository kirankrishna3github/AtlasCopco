*----------------------------------------------------------------------*
* Author  : Yang Lei                                                   *
* Created : 03.29.2017                                                 *
* Dev.nbr: CR4177 Open contract Syn                                    *
*----------------------------------------------------------------------*
*                                                                      *
************************************************************************
* Mod-xxx | xxxx.xx.xx | xxxx xxxx     | CD1Kxxxxxxxxxx  |   CRXXXX    *
************************************************************************
************************************************************************
REPORT  YSE_CS_OPENCONTR_SYN.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES:
  vbak.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:
  BEGIN OF ty_contr,
    vbeln TYPE vbak-vbeln,                 "Contract No.
    vgbel TYPE vbak-vgbel,                 "Reference No.
    qmnum TYPE vbak-qmnum,                 "Notification No.
  END OF ty_contr,

  BEGIN OF ty_contr_item,
    vbeln TYPE vbap-vbeln,                 "Contract No.
    objnr TYPE vbap-objnr,                 "Object No.
  END OF ty_contr_item,

  BEGIN OF ty_qmel,
    qmnum TYPE qmel-qmnum,                 "Notification No
    aufnr TYPE qmel-aufnr,                 "Order Number
  END OF ty_qmel.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_contr TYPE STANDARD TABLE OF ty_contr,
  gt_contr_item TYPE STANDARD TABLE OF ty_contr_item,
  gt_servi TYPE STANDARD TABLE OF ty_contr,
  gt_qmel  TYPE STANDARD TABLE OF ty_qmel,
  gt_map   TYPE STANDARD TABLE OF zsrm_im_stu.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_contr TYPE ty_contr,
  gs_contr_item TYPE ty_contr_item,
  gs_servi TYPE ty_contr,
  gs_qmel  TYPE ty_qmel,
  gs_map   TYPE zsrm_im_stu.

************************************************************************
* VARIABLES                                                            *
************************************************************************
*DATA:

************************************************************************
* CONSTANTS                                                            *
************************************************************************
*CONSTANTS:

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01 .
  SELECT-OPTIONS:
    s_vkorg FOR vbak-vkorg DEFAULT 'CN01' OBLIGATORY,
    s_vtweg FOR vbak-vtweg DEFAULT '11'   OBLIGATORY,
    s_spart FOR vbak-spart DEFAULT '01'   OBLIGATORY,
    s_auart FOR vbak-auart DEFAULT 'ZC02' OBLIGATORY,
    s_erdat FOR vbak-erdat,
    s_vbeln FOR vbak-vbeln.
SELECTION-SCREEN END OF BLOCK b1.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initilization
  PERFORM frm_init.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* Get data
  PERFORM get_data.
* Mapping data
  PERFORM edit_data.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.

* Transfer data
  PERFORM trans_data.

*&---------------------------------------------------------------------*
*& TOP-OF-PAGE                                                  *
*&---------------------------------------------------------------------*
TOP-OF-PAGE.

  WRITE:
    001 'Contract Number',
    017 'Notification No.',
    035 'Service Order'.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do initilization
*----------------------------------------------------------------------*
FORM frm_init .

* initialize the internal table,workareas and variables
  CLEAR:
    gt_contr,
    gt_contr_item,
    gt_servi,
    gt_qmel,
    gt_map,
    gs_contr,
    gs_contr_item,
    gs_servi,
    gs_qmel,
    gs_map.

ENDFORM.                    " FRM_INIT
*&---------------------------------------------------------------------*
*&      Form  GET_DATA
*&---------------------------------------------------------------------*
*       Get data
*----------------------------------------------------------------------*
FORM get_data .
  DATA:
    lt_servi TYPE STANDARD TABLE OF ty_contr.

* Get contract
  SELECT vbak~vbeln             "Contract No.
    FROM vbak
    INTO CORRESPONDING FIELDS OF TABLE gt_contr
   WHERE vbak~vkorg IN s_vkorg
     AND vbak~vtweg IN s_vtweg
     AND vbak~spart IN s_spart
     AND vbak~auart IN s_auart
     AND vbak~erdat IN s_erdat
     AND vbak~vbeln IN s_vbeln.
  IF sy-subrc <> 0.
    MESSAGE s001(00) DISPLAY LIKE 'E' WITH text-001.
    LEAVE LIST-PROCESSING.
  ENDIF.

* Get Contract Items
  SELECT vbeln                  "Contract No.
         objnr                  "Object No.
    FROM vbap
    INTO TABLE gt_contr_item
     FOR ALL ENTRIES IN gt_contr
   WHERE vbeln = gt_contr-vbeln.

* Get service sales order
  SELECT vbeln                  "service sales order
         vgbel                  "Reference No.
         qmnum                  "Notification No.
    FROM vbak
    INTO TABLE gt_servi
     FOR ALL ENTRIES IN gt_contr
   WHERE vgbel = gt_contr-vbeln.

* Get notification
  IF gt_servi IS NOT INITIAL.
    lt_servi = gt_servi.
    SORT lt_servi BY qmnum ASCENDING.
    DELETE ADJACENT DUPLICATES FROM lt_servi COMPARING qmnum.

    SELECT qmnum                "Notification No.
           aufnr                "Order Number
      FROM qmel
      INTO TABLE gt_qmel
       FOR ALL ENTRIES IN lt_servi
     WHERE qmnum = lt_servi-qmnum.
    SORT gt_qmel
      BY qmnum ASCENDING.

  ENDIF.

ENDFORM.                    " GET_DATA
*&---------------------------------------------------------------------*
*&      Form  EDIT_DATA
*&---------------------------------------------------------------------*
*       Mapping data
*----------------------------------------------------------------------*
FORM EDIT_DATA .

  DATA:
    lt_tj02t  TYPE STANDARD TABLE OF tj02t,
    ls_tj02t  TYPE tj02t,
    lt_status TYPE STANDARD TABLE OF jstat,
    lt_range  TYPE RANGE OF tj02t-txt04,
    ls_range  LIKE LINE OF lt_range,
    l_flg     TYPE flag.

  ls_range-sign = 'I'.
  ls_range-option = 'EQ'.
  ls_range-low = 'TECO'.
  APPEND ls_range TO lt_range.
  ls_range-sign = 'I'.
  ls_range-option = 'EQ'.
  ls_range-low = 'CLSD'.
  APPEND ls_range TO lt_range.

  LOOP AT gt_contr INTO gs_contr.
    CLEAR l_flg.
    LOOP AT gt_contr_item INTO gs_contr_item
                         WHERE vbeln = gs_contr-vbeln.
*     Get status
      CLEAR lt_status.
      CALL FUNCTION 'STATUS_READ'
        EXPORTING
          objnr                  =  gs_contr_item-objnr
          only_active            = 'X'
        TABLES
          status                 =  lt_status
        EXCEPTIONS
          object_not_found       = 1
          OTHERS                 = 2
                .
      IF sy-subrc = 0.
        SELECT *
          FROM tj02t
          INTO TABLE lt_tj02t
          FOR ALL ENTRIES IN lt_status
          WHERE istat = lt_status-stat
            AND spras = 'E'.
        IF sy-subrc = 0.
          LOOP AT lt_tj02t INTO ls_tj02t
                          WHERE txt04 IN lt_range.
            EXIT.
          ENDLOOP.
          IF sy-subrc <> 0.
            l_flg = 'X'.
            EXIT.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDLOOP.

    CHECK l_flg = 'X'.

    LOOP AT gt_servi INTO gs_servi
                    WHERE vgbel = gs_contr-vbeln.
      CLEAR gs_qmel.
      READ TABLE gt_qmel INTO gs_qmel
            WITH KEY qmnum = gs_servi-qmnum
            BINARY SEARCH.

      gs_map-vbeln = gs_contr-vbeln.
      gs_map-qmnum = gs_servi-qmnum.
      gs_map-aufnr = gs_qmel-aufnr.
      APPEND gs_map TO gt_map.
      CLEAR gs_map.
    ENDLOOP.
    IF sy-subrc <> 0.
      gs_map-vbeln = gs_contr-vbeln.
      APPEND gs_map TO gt_map.
      CLEAR gs_map.
    ENDIF.
  ENDLOOP.

  IF gt_map IS INITIAL.
    MESSAGE s001(00) DISPLAY LIKE 'E' WITH text-001.
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " EDIT_DATA
*&---------------------------------------------------------------------*
*&      Form  TRANS_DATA
*&---------------------------------------------------------------------*
*       Transfer data
*----------------------------------------------------------------------*
FORM TRANS_DATA .

  LOOP AT gt_map INTO gs_map.
    CALL FUNCTION 'YSE_SRM_SEO_OUTCALL_NEW'
      EXPORTING
        im_stu        = gs_map.
*      IMPORTING
*        ex_flag       = .

    WRITE:
      001 gs_map-vbeln,
      017 gs_map-qmnum,
      035 gs_map-aufnr.

  ENDLOOP.

ENDFORM.                    " TRANS_DATA

*Text symbol text£º
*001:No process data

*T01:Selection Input
*Selection text£º
*S_AUART:        Order Type
*S_ERDAT:        Order Creation Date
*S_SPART:        Division
*S_VBELN:        Order Number
*S_VKORG:        Sales Organization
*S_VTWEG:        Distribution Channel
