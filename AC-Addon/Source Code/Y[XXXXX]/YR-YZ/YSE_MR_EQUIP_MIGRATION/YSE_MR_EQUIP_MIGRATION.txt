*----------------------------------------------------------------------*
* PROGRAM ID    : YSE_MR_EQUIP_MIGRATION                               *
* PROGRAM TITLE : Equipment master data migration to new MR company    *
* AUTHOR        : Anda Wu                                              *
* DATE          : 28/10/2014                                           *
* DEVELOPMENT ID:                                                      *
*                                                                      *
* CHANGE REQUEST NUMBER:                                               *
*                                                                      *
* Program Description:Equipment master data migration to new MR company*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
*  DATE    | NAME     |TRANSPORT  | CR# and short Description          *
*----------------------------------------------------------------------*
*28/10/2014|Anda Wu   |CD1K983498 |CR3329: Initial version             *
*----------------------------------------------------------------------*
REPORT  yse_mr_equip_migration NO STANDARD PAGE HEADING LINE-SIZE 255.
************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

TABLES: equi,itob.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:
  BEGIN OF ty_input_file,
    qmart         TYPE qmel-qmart,    "Notification Type
    qmtxt         TYPE qmel-qmtxt,    "Noti. description
    equnr         TYPE equz-equnr,    "Equipment
    bstkd         TYPE vbkd-bstkd,    "P0 Number
    matnr         TYPE mara-matnr,    "Material
    bemot         TYPE vbkd-bemot,    "Accounting indicator
    ktext         TYPE caufvd-ktext,  "SEO description
    dauno         TYPE afvgd-dauno,   "Work Plan hour ZAM001
    dauno2        TYPE afvgd-dauno,   "Work Plan expense ZAM063
    dauno3        TYPE afvgd-dauno,   "Work Plan expense ZAM051
  END OF ty_input_file,

  BEGIN OF ty_sernr,
    old           TYPE itob-sernr,     "Maintplant old
    new           TYPE itob-sernr,     "Maintplant new
  END OF ty_sernr,

  BEGIN OF ty_equz_iloa,
    equnr         TYPE equz-equnr,    "equip No.
    datbi         TYPE equz-datbi,
    eqlfn         TYPE equz-eqlfn,
    iloan         TYPE equz-iloan,
    vkorg         TYPE iloa-vkorg,    "Sales Organization
    vtweg         TYPE iloa-vtweg,    "Distribution Channel
    spart         TYPE iloa-spart,    "Division
  END OF ty_equz_iloa,

  BEGIN OF ty_order_flow,
    bstkd         TYPE vbkd-bstkd,    "P0 Number
    qmnum         TYPE qmel-qmnum,    "Notification No
    aufnr         TYPE qmel-aufnr,    "Sevice Order No
    vbeln         TYPE qmel-vbeln,    "Sales Order Number
    iw32_err      TYPE string,
  END OF ty_order_flow,

  BEGIN OF ty_update_log,
    equnr         TYPE char18,    "Equipment No.
    msgty         TYPE sy-msgty,      "Message type
    mess          TYPE string,        "Update message
  END OF ty_update_log,

  BEGIN OF ty_vbkd,
    vbeln         TYPE vbkd-vbeln,
    posnr         TYPE vbkd-posnr,
    bstkd         TYPE vbkd-bstkd,    "P0 Number
  END OF ty_vbkd,

  BEGIN OF ty_alv_output,
    bstkd         TYPE char35,        "P0 Number
    aufnr         TYPE char12,        "Sevice Order No
    mess          TYPE string,        "Process message
  END OF ty_alv_output.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_sernr        TYPE STANDARD TABLE OF ty_sernr,
  gt_equi         TYPE STANDARD TABLE OF equi,
  gt_fieldcat     TYPE slis_t_fieldcat_alv,
  gt_update_log   TYPE STANDARD TABLE OF ty_update_log."update process log

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_swerk_old    TYPE itob-swerk,             "Maintplant old
  gv_swerk_new    TYPE itob-swerk,             "Maintplant new
  gv_iwerk_old    TYPE itob-iwerk,             "Planning old
  gv_iwerk_new    TYPE itob-iwerk,             "Planning new
  gv_flag_bdc     TYPE char1,                  "Use bdc change SerNo
  gv_vkorg_old    TYPE itob-vkorg,             "Sales organization old
  gv_vkorg_new    TYPE itob-vkorg,             "Sales organization new
  gv_arbpl_old    TYPE crhd-arbpl,             "Main Workctr old
  gv_arbpl_new    TYPE crhd-arbpl,             "Main Workctr new
  gv_vkbur_old    TYPE itob-vkbur,             "Sales Office old
  gv_vkbur_new    TYPE itob-vkbur,             "Sales Office new
  gv_vkgrp_old    TYPE itob-vkgrp,             "Sales Group old
  gv_vkgrp_new    TYPE itob-vkgrp,             "Sales Group new
*  gs_orderflow    TYPE ty_order_flow,
  gs_layout       TYPE slis_layout_alv.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_type_e       TYPE c VALUE 'E',
  gc_type_s       TYPE c VALUE 'S',
*  gc_hifi         TYPE c VALUE '-',
*  gc_colon        TYPE c VALUE ':',
  gc_x            TYPE c VALUE 'X'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.

SELECT-OPTIONS:  s_equnr  FOR equi-equnr.
SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-t02.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (29) text-s02.
PARAMETERS: p_swerk1 TYPE itob-swerk .
SELECTION-SCREEN COMMENT 50(5) text-s01.
PARAMETERS: p_swerk2 TYPE itob-swerk .
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (29) text-s03.
PARAMETERS: p_iwerk1 TYPE itob-iwerk .
SELECTION-SCREEN COMMENT 50(5) text-s01.
PARAMETERS: p_iwerk2 TYPE itob-iwerk .
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (29) text-s04.
PARAMETERS: p_vkorg1 TYPE itob-vkorg .
SELECTION-SCREEN COMMENT 50(5) text-s01.
PARAMETERS: p_vkorg2 TYPE itob-vkorg .
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK b2.

SELECTION-SCREEN BEGIN OF BLOCK b3 WITH FRAME TITLE text-t03.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (29) text-s05.
PARAMETERS: p_arbpl1 TYPE crhd-arbpl MATCHCODE OBJECT cram.
SELECTION-SCREEN COMMENT 50(5) text-s01.
PARAMETERS: p_arbpl2 TYPE crhd-arbpl MATCHCODE OBJECT cram.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK b3.

SELECTION-SCREEN BEGIN OF BLOCK b4 WITH FRAME TITLE text-t04.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (29) text-s06.
PARAMETERS: p_vkbur1 TYPE itob-vkbur .
SELECTION-SCREEN COMMENT 50(5) text-s01.
PARAMETERS: p_vkbur2 TYPE itob-vkbur .
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (29) text-s07.
PARAMETERS: p_vkgrp1 TYPE itob-vkgrp .
SELECTION-SCREEN COMMENT 50(5) text-s01.
PARAMETERS: p_vkgrp2 TYPE itob-vkgrp.
SELECTION-SCREEN END OF LINE.
SELECT-OPTIONS: s_sernr   FOR itob-sernr.
SELECTION-SCREEN END OF BLOCK b4.

SELECTION-SCREEN END OF BLOCK b1.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initialization
  PERFORM frm_do_init.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN OUTPUT                                          *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.
* Set selection screen
  PERFORM set_screen.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* Get values from selection screen
  PERFORM get_old_data.
* Gain data
  PERFORM gain_equi_data.
* do migration
  PERFORM do_equi_magration.
** ALV display
  PERFORM show_alv.

*&---------------------------------------------------------------------*
*&      Form  FRM_DO_INIT
*&---------------------------------------------------------------------*
*       Do initialization
*----------------------------------------------------------------------*
FORM frm_do_init .

  REFRESH:
    gt_update_log   .                           "update process log
  gv_flag_bdc = 'X'.

ENDFORM.                    " FRM_DO_INIT
*&---------------------------------------------------------------------*
*&      Form  SET_SCREEN
*&---------------------------------------------------------------------*
*       Set selection screen
*----------------------------------------------------------------------*
FORM set_screen .

* Set editable for parameter [p_class],[p_charno],[p_hflg]
  LOOP AT SCREEN.
    IF screen-group1 = 'M1'.
      screen-input = '0'.
      screen-invisible = '1'.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " SET_SCREEN
*
**&---------------------------------------------------------------------*
**&      Form  SHOW_ALV
**&---------------------------------------------------------------------*
**       show ALV
**----------------------------------------------------------------------*
FORM show_alv .

* FIELDCAT set
  PERFORM alv_fieldcat_set.
* LAYOUT set
  PERFORM alv_layout_set.
* ALV display
  PERFORM alv_display.

ENDFORM.                    " SHOW_ALV
**&---------------------------------------------------------------------*
**&      Form  ALV_FIELDCAT_SET
**&---------------------------------------------------------------------*
**       text
**----------------------------------------------------------------------*
FORM alv_fieldcat_set .
  DATA:
     lv_linecnt  TYPE i,
     ls_fieldcat TYPE slis_fieldcat_alv.

  CLEAR lv_linecnt.
* P0 Number
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'EQUNR'.
  ls_fieldcat-seltext_s = 'Equip.No'(050).
  ls_fieldcat-seltext_m = 'Equipment No.'(055).
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* Sevice Order No
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'MSGTY'.
  ls_fieldcat-seltext_s = 'Msg.Type'(060).
  ls_fieldcat-seltext_m = 'Message Type'(065).
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* SO item
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'MESS'.
  ls_fieldcat-seltext_s = 'Pro.Msg'(070).
  ls_fieldcat-seltext_m = 'Process Message'(075).
  APPEND ls_fieldcat TO gt_fieldcat.
ENDFORM.                    " ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*      LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .
  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   " Auto optimize column width
  gs_layout-detail_popup = gc_x.        " Show detail screen
*  gs_layout-box_fieldname  = 'SEL'.
ENDFORM.                    " ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .
  CHECK gt_update_log IS NOT INITIAL.
  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program = sy-repid
      is_layout          = gs_layout
      it_fieldcat        = gt_fieldcat
    TABLES
      t_outtab           = gt_update_log
    EXCEPTIONS
      program_error      = 1
      OTHERS             = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
ENDFORM.                    " ALV_DISPLAY
*&---------------------------------------------------------------------*
*&      Form  GET_OLD_DATA
*&---------------------------------------------------------------------*
*       Get data from selection screen
*----------------------------------------------------------------------*
FORM get_old_data .

  DATA: ls_sernr  TYPE ty_sernr,
        lv_tabix  TYPE sy-tabix,
        lr_sernr  LIKE LINE OF s_sernr.

  gv_swerk_old    =  p_swerk1.       "Maintplant old
  gv_swerk_new    =  p_swerk2.      "Maintplant new
  gv_iwerk_old    =  p_iwerk1.       "Planning old
  gv_iwerk_new    =  p_iwerk2.      "Planning new
  gv_vkorg_old    =  p_vkorg1.       "Sales organization old
  gv_vkorg_new    =  p_vkorg2.      "Sales organization new
  gv_arbpl_old    =  p_arbpl1.       "Main Workctr old
  gv_arbpl_new    =  p_arbpl2.      "Main Workctr new
  gv_vkbur_old    =  p_vkbur1.       "Sales Office old
  gv_vkbur_new    =  p_vkbur2.      "Sales Office new
  gv_vkgrp_old    =  p_vkgrp1.       "Sales Group old
  gv_vkgrp_new    =  p_vkgrp2.      "Sales Group new

  LOOP AT s_sernr INTO lr_sernr WHERE sign = 'I'
                                  AND option = 'EQ'.
    ls_sernr-old = lr_sernr-low.
    APPEND ls_sernr TO gt_sernr.
  ENDLOOP.
  lv_tabix = 0.
  LOOP AT s_sernr INTO lr_sernr WHERE sign = 'E'
                                  AND option = 'EQ'.
    lv_tabix =  lv_tabix + 1.
    READ TABLE gt_sernr INTO ls_sernr INDEX lv_tabix.
    ls_sernr-new = lr_sernr-low.
    MODIFY gt_sernr FROM ls_sernr INDEX lv_tabix.
  ENDLOOP.

ENDFORM.                    " GET_OLD_DATA
*&---------------------------------------------------------------------*
*&      Form  DO_EQUI_MAGRATION
*&---------------------------------------------------------------------*
*       do migration
*----------------------------------------------------------------------*
FORM do_equi_magration .

  DATA: ls_equi         TYPE equi,
        ls_sernr        TYPE ty_sernr,
        ls_general_exp  TYPE bapi_itob,
        ls_generalx     TYPE bapi_itobx,
        ls_specific_exp TYPE bapi_itob_eq_only,
        ls_specificx    TYPE bapi_itob_eq_onlyx,
        ls_update_log   TYPE ty_update_log,
        ls_return       TYPE bapiret2,
        lv_lgwid        TYPE lgwid,
        lv_arbpl        TYPE crhd-arbpl,
        lv_werks        TYPE crhd-werks.

  LOOP AT gt_equi INTO ls_equi .
    CLEAR: ls_update_log.
    ls_update_log-equnr       = ls_equi-equnr.
    CALL FUNCTION 'BAPI_EQUI_GETDETAIL'
      EXPORTING
        equipment         = ls_equi-equnr
      IMPORTING
        data_general_exp  = ls_general_exp
        data_specific_exp = ls_specific_exp
        return            = ls_return.
    IF gv_swerk_old IS NOT INITIAL
      AND ls_general_exp-maintplant <> gv_swerk_old.
      ls_update_log-msgty       = gc_type_e.
      CONCATENATE 'Maintplant is not'(010) gv_swerk_old '.'
        INTO ls_update_log-mess
        SEPARATED BY space.
      APPEND ls_update_log TO gt_update_log.
      CONTINUE.
    ENDIF.
    IF ls_general_exp-planplant <> gv_iwerk_old
      AND gv_iwerk_old IS NOT INITIAL.
      ls_update_log-msgty       = gc_type_e.
      CONCATENATE 'Planning plant is not'(015) gv_iwerk_old '.'
        INTO ls_update_log-mess
        SEPARATED BY space.
      APPEND ls_update_log TO gt_update_log.
      CONTINUE.
    ENDIF.
    IF ls_general_exp-sales_org <> gv_vkorg_old
      AND gv_vkorg_old IS NOT INITIAL.
      ls_update_log-msgty       = gc_type_e.
      CONCATENATE 'Sales Organization is not'(020) gv_vkorg_old '.'
        INTO ls_update_log-mess
        SEPARATED BY space.
      APPEND ls_update_log TO gt_update_log.
      CONTINUE.
    ENDIF.
    SELECT SINGLE arbpl
           werks
      FROM crhd
      INTO (lv_arbpl,lv_werks)
      WHERE objty = 'A'
        AND objid = ls_general_exp-work_ctr.
    IF lv_arbpl <> gv_arbpl_old
      AND gv_arbpl_old IS NOT INITIAL.
      ls_update_log-msgty       = gc_type_e.
      CONCATENATE 'Main WorkCtr is not'(025) gv_arbpl_old '.'
        INTO ls_update_log-mess
        SEPARATED BY space.
      APPEND ls_update_log TO gt_update_log.
      CONTINUE.
    ENDIF.
    IF ls_general_exp-sales_off <> gv_vkbur_old
      AND gv_vkbur_old IS NOT INITIAL.
      ls_update_log-msgty       = gc_type_e.
      CONCATENATE 'Sales Office is not'(030) gv_vkbur_old '.'
        INTO ls_update_log-mess
        SEPARATED BY space.
      APPEND ls_update_log TO gt_update_log.
      CONTINUE.
    ENDIF.
    IF ls_general_exp-sales_grp <> gv_vkgrp_old
      AND gv_vkgrp_old IS NOT INITIAL.
      ls_update_log-msgty       = gc_type_e.
      CONCATENATE 'Sales Group is not'(035) gv_vkgrp_old '.'
        INTO ls_update_log-mess
        SEPARATED BY space.
      APPEND ls_update_log TO gt_update_log.
      CONTINUE.
    ENDIF.
    CLEAR: ls_return.
    IF    NOT ( gv_swerk_new IS  INITIAL
      AND gv_swerk_old IS INITIAL ) .
      ls_general_exp-maintplant = gv_swerk_new.
      ls_generalx-maintplant = 'X'.
    ENDIF.
    IF  NOT ( gv_iwerk_new IS INITIAL
      AND gv_iwerk_old IS INITIAL ).
      ls_general_exp-planplant  = gv_iwerk_new.
      ls_generalx-planplant  = 'X'.
    ENDIF.
    IF    gv_vkorg_new IS NOT INITIAL
      AND gv_vkorg_old IS NOT INITIAL.
      ls_general_exp-sales_org  = gv_vkorg_new.
      ls_generalx-sales_org  = 'X'.
    ENDIF.
    IF NOT ( gv_arbpl_new IS INITIAL AND gv_arbpl_old IS INITIAL ).
      IF gv_iwerk_new IS NOT INITIAL.
        SELECT SINGLE objid
          FROM crhd
          INTO lv_lgwid
          WHERE objty = 'A'
            AND arbpl = gv_arbpl_new
            AND werks = gv_iwerk_new.
        IF sy-subrc <> 0.
          ls_update_log-msgty       = gc_type_e.
          CONCATENATE 'The work center'(080) gv_arbpl_new
                      'not exist in plant'(085) gv_iwerk_new '.'
            INTO ls_update_log-mess
            SEPARATED BY space.
          APPEND ls_update_log TO gt_update_log.
          CONTINUE.
        ELSE.
          ls_general_exp-work_ctr = lv_lgwid.
          ls_generalx-work_ctr = 'X'.
        ENDIF.
      ELSE.
        SELECT SINGLE objid
          FROM crhd
          INTO lv_lgwid
          WHERE objty = 'A'
            AND arbpl = gv_arbpl_new
            AND werks = gv_arbpl_new(4).
        IF sy-subrc <> 0.
          ls_update_log-msgty       = gc_type_e.
          CONCATENATE 'The work center'(080) gv_arbpl_new
                      'not exist in plant'(085) gv_arbpl_new(4) '.'
            INTO ls_update_log-mess
            SEPARATED BY space.
          APPEND ls_update_log TO gt_update_log.
          CONTINUE.
        ELSE.
          ls_general_exp-work_ctr = lv_lgwid.
          ls_generalx-work_ctr = 'X'.
        ENDIF.
      ENDIF.
    ENDIF.
    IF gv_vkbur_new IS NOT INITIAL
      AND gv_vkbur_old IS NOT INITIAL.
      ls_general_exp-sales_off = gv_vkbur_new.
      ls_generalx-sales_off = 'X'.
    ENDIF.
    IF gv_vkgrp_new IS NOT INITIAL
      AND gv_vkgrp_old IS NOT INITIAL.
      ls_general_exp-sales_grp = gv_vkgrp_new.
      ls_generalx-sales_grp = 'X'.
    ENDIF.

    IF ls_specific_exp-read_floc IS NOT INITIAL.
      CALL FUNCTION 'BAPI_EQUI_DISMANTLE'
        EXPORTING
          equipment               =  ls_equi-equnr
*         FUNCLOC                 =
*         SUPEQUI                 =
*         DISM_DATE               = SY-DATUM
*         DISM_TIME               = SY-UZEIT
*       IMPORTING
*         DATA_GENERAL_EXP        =
*         DATA_SPECIFIC_EXP       =
*         RETURN                  =
                .
*      CLEAR ls_specific_exp-read_floc.
*      ls_specificx-read_floc = 'X'.
    ENDIF.

    READ TABLE gt_sernr INTO ls_sernr
      WITH KEY old = ls_specific_exp-serialno.
    IF sy-subrc = 0.
      IF ls_sernr-new IS NOT INITIAL.
        ls_specific_exp-serialno = ls_sernr-new.
        IF gv_flag_bdc IS NOT INITIAL.
          PERFORM frm_update_sernr USING  ls_equi-equnr
                                          ls_sernr-new.
        ENDIF.
        ls_specificx-serialno = 'X'.
      ENDIF.
    ENDIF.

    IF ls_generalx IS INITIAL AND ls_specificx IS INITIAL.
      ls_update_log-msgty       = gc_type_s.
      CONCATENATE 'No changes on the equipment'(110)
        ls_equi-equnr
        INTO ls_update_log-mess
        SEPARATED BY space.
      APPEND ls_update_log TO gt_update_log.
      CONTINUE.
    ELSE.
      ls_generalx-plangroup = 'X'.
    ENDIF.

    CALL FUNCTION 'BAPI_EQUI_CHANGE'
      EXPORTING
        equipment      = ls_equi-equnr
        data_general   = ls_general_exp
        data_generalx  = ls_generalx
        data_specific  = ls_specific_exp
        data_specificx = ls_specificx
      IMPORTING
        return         = ls_return.
    IF ls_return-type = gc_type_e.
      ls_update_log-msgty       = gc_type_e.
      MESSAGE ID ls_return-id TYPE ls_return-type
        NUMBER ls_return-number WITH ls_return-message_v1
        ls_return-message_v2 ls_return-message_v3 ls_return-message_v4
        INTO ls_update_log-mess.
      APPEND ls_update_log TO gt_update_log.
    ELSE.
      ls_update_log-msgty       = gc_type_s.
      CONCATENATE 'Successful to update the equipment'(040)
        ls_equi-equnr '.' INTO ls_update_log-mess
        SEPARATED BY space.
      APPEND ls_update_log TO gt_update_log.
      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
        EXPORTING
          wait = 'X'.
    ENDIF.

  ENDLOOP.

ENDFORM.                    " DO_EQUI_MAGRATION
*&---------------------------------------------------------------------*
*&      Form  GAIN_EQUI_DATA
*&---------------------------------------------------------------------*
*       Gain data
*----------------------------------------------------------------------*
FORM gain_equi_data .
  SELECT *
    FROM equi
    INTO TABLE gt_equi
    WHERE equnr IN s_equnr.
ENDFORM.                    " GAIN_EQUI_DATA
*&---------------------------------------------------------------------*
*&      Form  FRM_UPDATE_SERNR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_LS_SERNR_NEW  text
*----------------------------------------------------------------------*
FORM frm_update_sernr  USING  p_equnr
                              p_sernr_new.

  DATA: lt_bdcdata      TYPE STANDARD TABLE OF bdcdata,
        ls_bdcdata      TYPE   bdcdata,
        ls_update_log   TYPE ty_update_log,
        ls_option        TYPE ctu_params ,
        lt_bdcmsgcoll TYPE STANDARD TABLE OF bdcmsgcoll,
        ls_bdcmsg       TYPE bdcmsgcoll.

  ls_option-dismode   = 'N'.
  ls_option-updmode   = 'S'.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    'SAPMIEQ0'  '0100'  'X'  ''  ''
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_CURSOR'  'RM63E-EQUNR'
          CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_OKCODE'  '/00'
          CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'RM63E-EQUNR'  p_equnr
          CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    'SAPMIEQ0'  '0101'  'X'  ''  ''
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_OKCODE'  '=SC'
          CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'ITOB-EQART'  'EQUIPMENT'
          CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    'SAPMIEQ0'  '1200'  'X'  ''  ''
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_CURSOR'  'RISA0-SERNEU'
          CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_OKCODE'  '=SCA'
          CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'RISA0-SERNEU'  p_sernr_new
          CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM  fill_bdcdata IN PROGRAM yam_common_routines
             USING    'SAPMIEQ0'  '0101'  'X'  ''  ''
             CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CLEAR  ls_bdcdata.
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_OKCODE'  '=BU'
          CHANGING ls_bdcdata.
  APPEND ls_bdcdata  TO lt_bdcdata.

  CALL TRANSACTION 'IE02'
    USING lt_bdcdata
    OPTIONS FROM ls_option
    MESSAGES INTO lt_bdcmsgcoll.

  READ TABLE lt_bdcmsgcoll INTO ls_bdcmsg
        WITH KEY msgtyp = gc_type_e.
  IF sy-subrc = 0.
    ls_update_log-msgty       = gc_type_s.
    MESSAGE ID ls_bdcmsg-msgid TYPE ls_bdcmsg-msgtyp
      NUMBER ls_bdcmsg-msgnr
      WITH ls_bdcmsg-msgv1 ls_bdcmsg-msgv2
           ls_bdcmsg-msgv3 ls_bdcmsg-msgv4
      INTO ls_update_log-mess .
    APPEND ls_update_log TO gt_update_log.
  ENDIF.

ENDFORM.                    " FRM_UPDATE_SERNR

*Text symbol text£º
*010:Maintplant is not
*015:Planning plant is not
*020:Sales Organization is not
*025:Main WorkCtr is not
*030:Sales Office is not
*035:Sales Group is not
*040:Successful to update the equipment
*050:Equip.No
*055:Equipment No.
*060:Msg.Type
*065:Message Type
*070:Pro.Msg
*075:Process Message
*080:The work center
*085:not exist in plant
*110:No changes on the equipment
*S01:to
*S02:Maintenance plant
*S03:Planning plant
*S04:Sales Organization
*S05:Main WorkCtr
*S06:Sales Office
*S07:Sales Group
*T01:Selection Screen
*T02:Orgnization
*T03:Responsibilities

*T04:Sales Office/Sales Group
*Selection text£º
*S_EQUNR:D       .
*S_SERNR:D       .
