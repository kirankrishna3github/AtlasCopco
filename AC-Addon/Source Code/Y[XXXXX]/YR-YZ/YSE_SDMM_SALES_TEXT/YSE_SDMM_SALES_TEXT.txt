REPORT yse_sdmm_sales_text NO STANDARD PAGE HEADING LINE-SIZE 255.

*----------------------------------------------------------------------*
* Author  : Anda Wu                                                    *
* Created : 22.12.2014                                                 *
* Dev.nbr: CR3329-Material Sales Texts Display                         *
*----------------------------------------------------------------------*
* This report  is a reporting to show the sales text of materials      *
************************************************************************
* Mod-xxx | xxxx.xx.xx | xxxx xxxx     | CD1Kxxxxxxxxxx  |   CRXXXX    *
************************************************************************
************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES: mvke,t002.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:

BEGIN OF ty_alv_output,
  langu   TYPE sy-langu,
  matnr   TYPE mvke-matnr,
  vkorg   TYPE mvke-vkorg,
  vtweg   TYPE mvke-vtweg,
  stext   TYPE string,
END OF ty_alv_output.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_alvdata  TYPE STANDARD TABLE OF ty_alv_output,
  gt_t002     TYPE STANDARD TABLE OF t002,
  gt_fieldcat TYPE slis_t_fieldcat_alv.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_alvdata  TYPE ty_alv_output,
  gs_disvar   TYPE disvariant,
  gs_layout   TYPE slis_layout_alv.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_col(10)  TYPE n,
  gv_text     TYPE string,
  gv_sep      TYPE char2,
  gv_save     TYPE c,
  gv_callback TYPE slis_formname.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x                TYPE c VALUE 'X',
  gc_type_e           TYPE c VALUE 'E',
  gc_callback_routine TYPE slis_formname VALUE 'USER_COMMAND'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.

SELECT-OPTIONS: s_matnr    FOR mvke-matnr,
                s_vkorg    FOR mvke-vkorg,
                s_vtweg    FOR mvke-vtweg,
                s_langu    FOR t002-spras OBLIGATORY DEFAULT sy-langu.

SELECTION-SCREEN END OF BLOCK b1.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initilization
  PERFORM frm_init.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* progress indicator
  PERFORM process_info.
* get data
  PERFORM get_alv_data.
* alv property set
  PERFORM alv_prop_set.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
* ALV display
  PERFORM alv_display.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do initilization
*----------------------------------------------------------------------*
FORM frm_init .

* initialize the internal table,workareas and variables
  REFRESH:
    gt_alvdata,
    gt_fieldcat.

  gv_sep =  '||'.

ENDFORM.                    " FRM_INIT
*&---------------------------------------------------------------------*
*&      Form  PROCESS_INFO
*&---------------------------------------------------------------------*
*       progress indicator
*----------------------------------------------------------------------*
FORM process_info .

  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    CONCATENATE 'Start report:'(t52) sy-repid
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t53.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 5
        text       = gv_text.
  ENDIF.

ENDFORM.                    " PROCESS_INFO
*&---------------------------------------------------------------------*
*&      Form  GET_ALV_DATA
*&---------------------------------------------------------------------*
*       get data
*----------------------------------------------------------------------*
FORM get_alv_data .
* Gain language key
  PERFORM get_langu.
* Gain data from table VBAK - Sales Document: Header Data
  PERFORM get_data_mvke.
* Generate ALV data for output
  PERFORM generate_alv_data.

ENDFORM.                    " GET_ALV_DATA
*&---------------------------------------------------------------------*
*&      Form  ALV_PROP_SET
*&---------------------------------------------------------------------*
*       alv property set
*----------------------------------------------------------------------*
FORM alv_prop_set .

* Display ALV process indicator
  PERFORM alv_progress_indicator.

* FIELDCAT set
  PERFORM alv_fieldcat_set.

* LAYOUT set
  PERFORM alv_layout_set.

* Set Others
  PERFORM alv_others_set.

ENDFORM.                    " ALV_PROP_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_lines(10)  TYPE c.

  IF gt_alvdata IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-t61.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  IF sy-batch IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-t63.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 80
        text       = gv_text.
  ELSE.
    CLEAR: gv_text.
    DESCRIBE TABLE gt_alvdata LINES lv_lines.
    CONCATENATE 'Totally'(t62) lv_lines 'entries are generated'(t63)
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ENDIF.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = sy-repid
      i_callback_user_command = gv_callback
      is_layout               = gs_layout
      it_fieldcat             = gt_fieldcat
      i_save                  = gv_save
      is_variant              = gs_disvar
    TABLES
      t_outtab                = gt_alvdata
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM alv_fieldcat_set .

** Create Fieldcatalogue from internal table
  CLEAR gv_col.
* Sales Org.
  PERFORM add_field USING 'LANGU'
                          'Lang'(t44)
                          'Lanuage'(t54)
                          'Lanuage'(t54) ''.

  CLEAR gv_col.
* Sales Org.
  PERFORM add_field USING 'VKORG'
                          'Sales.Org'(t45)
                          'Sales Org'(t55)
                          'Sales Organization'(t65) ''.
* Distr. Chl
  PERFORM add_field USING 'VTWEG'
                          'Distr.Chl'(t46)
                          'Distr.Chel'(t56)
                          'Distrbute Channel'(t66)
                          ''.
* Material
  PERFORM add_field USING 'MATNR'
                          'Mat.No'(t47)
                          'Mat.Number'(t57)
                          'Material Number'(t67)
                          'X'.
* Sales Text
  PERFORM add_field USING 'STEXT'
                          'Sales.Text'(t68)
                          'Sales.Text'(t68)
                          'Sales.Text'(t68)
                           ' '.

ENDFORM.                    " ALV_FIELDCAT_SET

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_descr_s
                         p_descr_m
                         p_descr_l
                         p_hotsp.
  DATA:
     ls_fieldcat TYPE slis_fieldcat_alv.

  gv_col = gv_col + 1.

  CLEAR ls_fieldcat.
  ls_fieldcat-col_pos    = gv_col.
  ls_fieldcat-fieldname  = p_field.
  ls_fieldcat-seltext_l  = p_descr_l.
  ls_fieldcat-seltext_m  = p_descr_m.
  ls_fieldcat-seltext_s  = p_descr_s.
  ls_fieldcat-fix_column = 'X'.
  ls_fieldcat-emphasize  = 'X'.
  ls_fieldcat-hotspot    = p_hotsp.

* Suppress leading zeros for the material number
  IF p_field = 'MATNR'.
    ls_fieldcat-edit_mask = '==MATN1'.
  ENDIF.

  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   "Auto optimize column width
  gs_layout-detail_popup = gc_x.        "Show detail screen

ENDFORM.                    " ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*&      Form  GENERATE_ALV_DATA
*&---------------------------------------------------------------------*
*       Generate ALV data for output
*----------------------------------------------------------------------*
FORM generate_alv_data .
  DATA: lv_name     TYPE thead-tdname,
        lv_object   TYPE thead-tdobject,
        lv_id       TYPE thead-tdid,
        lv_langu    TYPE thead-tdspras,
        ls_lines    TYPE tline,
        ls_t002     TYPE t002,
        lt_alvdata  TYPE STANDARD TABLE OF ty_alv_output,
        lt_lines    TYPE STANDARD TABLE OF tline.
  FIELD-SYMBOLS: <lfs_alvdata> TYPE ty_alv_output.

  lv_id = '0001'.
  lv_langu = sy-langu.
  lv_object = 'MVKE'.

  APPEND LINES OF gt_alvdata TO lt_alvdata.
  REFRESH: gt_alvdata.

  LOOP AT lt_alvdata ASSIGNING <lfs_alvdata>.
    CONCATENATE   <lfs_alvdata>-matnr
                  <lfs_alvdata>-vkorg
                  <lfs_alvdata>-vtweg
          INTO    lv_name.
    LOOP AT gt_t002 INTO ls_t002.
      <lfs_alvdata>-langu = ls_t002-spras.
      lv_langu = ls_t002-spras.
      CALL FUNCTION 'READ_TEXT'
        EXPORTING
          id                      = lv_id
          language                = lv_langu
          name                    = lv_name
          object                  = lv_object
        TABLES
          lines                   = lt_lines
        EXCEPTIONS
          id                      = 1
          language                = 2
          name                    = 3
          not_found               = 4
          object                  = 5
          reference_check         = 6
          wrong_access_to_archive = 7
          OTHERS                  = 8.
      IF sy-subrc = 0.
*        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
*                WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
*      ELSE.
        LOOP AT   lt_lines INTO ls_lines.
          IF <lfs_alvdata>-stext IS INITIAL.
            <lfs_alvdata>-stext = ls_lines-tdline.
          ELSE.
            IF ls_lines-tdline IS NOT INITIAL.
              CONCATENATE <lfs_alvdata>-stext
                          ls_lines-tdline
                     INTO <lfs_alvdata>-stext
                     SEPARATED BY gv_sep.
            ENDIF.
          ENDIF.
        ENDLOOP.
      ENDIF.
      APPEND <lfs_alvdata> TO gt_alvdata.
    ENDLOOP.
  ENDLOOP.

ENDFORM.                    " GENERATE_ALV_DATA

*&---------------------------------------------------------------------*
*&      Form  ALV_PROGRESS_INDICATOR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM alv_progress_indicator .

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to set ALV property'(t60).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t60.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 75
        text       = gv_text.
  ENDIF.

ENDFORM.                    " ALV_PROGRESS_INDICATOR

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       Set Others
*----------------------------------------------------------------------*
FORM alv_others_set .

  gv_callback = gc_callback_routine.

ENDFORM.                    " ALV_OTHERS_SET

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       USER_COMMAND
*----------------------------------------------------------------------*
FORM user_command USING u_ucomm     LIKE sy-ucomm           "#EC CALLED
                        p_selfield  TYPE slis_selfield.

  CASE u_ucomm.
    WHEN '&IC1'.        " SAP standard code for double-clicking
      IF p_selfield-fieldname = 'MATNR'.
        IF p_selfield-value IS NOT INITIAL.
          SET PARAMETER ID 'MAT' FIELD p_selfield-value.
          CALL TRANSACTION 'MM03'.
        ENDIF.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.

ENDFORM.                    " USER_COMMAND

*&---------------------------------------------------------------------*
*&      Form  GET_DATA_MVKE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_data_mvke .

  SELECT  matnr
          vkorg
          vtweg
    FROM mvke
    INTO CORRESPONDING FIELDS OF TABLE gt_alvdata
    WHERE matnr IN s_matnr
      AND vkorg IN s_vkorg
      AND vtweg IN s_vtweg.

ENDFORM.                    " GET_DATA_MVKE
*&---------------------------------------------------------------------*
*&      Form  GET_LANGU
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_langu .
  SELECT *
    FROM t002
    INTO TABLE gt_t002
    WHERE spras IN s_langu.
ENDFORM.                    " GET_LANGU

*Text symbol text£º
*T01:Selection Screen
*T44:Lang

*T54:Lanuage
*Selection text£º
*S_LANGU:D       .
*S_MATNR:D       .
*S_VKORG:D       .
*S_VTWEG:D       .
