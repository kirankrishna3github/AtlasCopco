*&---------------------------------------------------------------------*
*& Report  YSE_RECOVERY_LANDED_COST
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& LCR Russia : Report: Recovery of landed costs                       *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 09.08.2010
*  Change Request Number : CR1377
*  Transport request Nr. : CD1K958493
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME           | CORRECTION NR. | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 18.10.2011 | Nanda S.       | CD1K968442     | CR2116      *
*         | Bug fix for reference key to pick OC vendors correclty.    *
*----------------------------------------------------------------------*
* MOD-002 | 05.01.2012 | Jules Smets    | CD1K969720     | CR2155      *
*         | - Addition of GAC field at line item level                 *
*         | - Addition of freight amount                               *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_recovery_landed_cost.

TABLES: ekbe,
        bkpf,
        bseg,
        lfa1,
        lfb1,
        yse_prctr_bl,
        t163c.

* ALV grid
TYPE-POOLS: slis.

* Internal table with PO history categories
DATA: BEGIN OF t_pohc  OCCURS 0,
        bewtp      TYPE bewtp,
        bewtk      TYPE bewtk,
      END OF t_pohc.

* Internal table with return for F4 (value selection)
DATA: BEGIN OF it_return_tab  OCCURS 0.
        INCLUDE STRUCTURE ddshretval.
DATA: END OF it_return_tab.

* Internal table with PO
DATA: BEGIN OF t_po  OCCURS 0,
        ebeln      TYPE ebeln,
        ebelp      TYPE ebelp,
        bewtp      TYPE bewtp,
        budat      TYPE budat,
        mblnr      TYPE mblnr,
        matnr      TYPE matnr,                    "MOD-002
      END OF t_po.

* Internal table with posting documents
DATA: BEGIN OF t_doc  OCCURS 0,
        belnr      TYPE belnr_d,
        ebeln      TYPE ebeln,
        ebelp      TYPE ebelp,
        bewtp      TYPE bewtp,
        budat      TYPE budat,
        bldat      TYPE bldat,
        blart      TYPE blart,
        hwaer      TYPE hwaer,
        matnr      TYPE matnr,                    "MOD-002
        mblnr      TYPE mblnr,                    "MOD-002
      END OF t_doc.

* Internal table with amounts
DATA: BEGIN OF t_amt  OCCURS 0,
        ebeln      TYPE ebeln,
        belnr      TYPE belnr_d,
        bschl      TYPE bschl,
        segment    TYPE fb_segment,
        bewtp      TYPE bewtp,
        budat      TYPE budat,
        bldat      TYPE bldat,
        blart      TYPE blart,
        lifnr      TYPE lifnr,
        dmbtr      TYPE dmbtr,
        hwaer      TYPE hwaer,
        shkzg      TYPE shkzg,
        matnr      TYPE matnr,                    "MOD-002
        mblnr      TYPE mblnr,                    "MOD-002
      END OF t_amt.
DATA: h_amt  LIKE LINE OF t_amt.

* Internal table with output lines
DATA: BEGIN OF t_out  OCCURS 0,
        ebeln      TYPE ebeln,
        belnr      TYPE belnr_d,
        segment    TYPE fb_segment,
        bewtk      TYPE bewtk,
        budat      TYPE budat,
        bldat      TYPE bldat,
        blart      TYPE blart,
        lifnr      TYPE lifnr,
        name1      TYPE name1,
        akont      TYPE akont,
        dmbtr      TYPE dmbtr,
        hwaer      TYPE hwaer,
        bukrs      TYPE bukrs,
        gjahr      TYPE gjahr,
        gac        TYPE prodh_d,                  "MOD-002
      END OF t_out.
DATA: h_out  LIKE LINE OF t_out.

*** MOD-002 * begin ***
DATA: t_prctr TYPE HASHED TABLE OF yse_prctr_bl
                   WITH UNIQUE KEY prctr
                   WITH HEADER LINE.

TYPES: BEGIN OF ty_bseg,
        belnr      TYPE belnr_d,
        ebeln      TYPE ebeln,
        ebelp      TYPE ebelp,
        buzei      TYPE buzei,
        koart      TYPE koart,
        hkont      TYPE hkont,
        dmbtr      TYPE dmbtr,
        shkzg      TYPE shkzg,
        prctr      TYPE prctr,
        bschl      TYPE bschl,
       END OF ty_bseg.

DATA: t_bseg TYPE SORTED TABLE OF ty_bseg
                  WITH NON-UNIQUE KEY belnr ebeln ebelp
                  WITH HEADER LINE.

TYPES: BEGIN OF ty_lifnr,
        belnr      TYPE belnr_d,
        lifnr      TYPE lifnr,
END OF ty_lifnr.

DATA : t_lifnr TYPE HASHED TABLE OF ty_lifnr
                    WITH UNIQUE KEY belnr
                    WITH HEADER LINE,
       t_lifnri TYPE STANDARD TABLE OF ty_lifnr.

TYPES: BEGIN OF ty_lfa,
        lifnr      TYPE lifnr,
        name1      TYPE name1_gp,
        akont      TYPE akont,
      END OF ty_lfa.

DATA : t_lfa TYPE HASHED TABLE OF ty_lfa
                   WITH UNIQUE KEY lifnr
                   WITH HEADER LINE,
       t_lfai TYPE STANDARD TABLE OF ty_lfa.

TYPES: BEGIN OF ty_mara,
        matnr      TYPE matnr,
        prdha      TYPE prodh_d,
      END OF ty_mara.

DATA : t_mara TYPE HASHED TABLE OF ty_mara
                   WITH UNIQUE KEY matnr
                   WITH HEADER LINE.
*** MOD-002 * end ***

* Internal table with classificaton data
DATA: BEGIN OF t_clobjdat OCCURS 0.
        INCLUDE STRUCTURE clobjdat.
DATA: END OF t_clobjdat.

* Internal table with classes
DATA: BEGIN OF t_class OCCURS 0.
        INCLUDE STRUCTURE sclass.
DATA: END OF t_class.

DATA: xv_variant         LIKE disvariant,
      xv_variant_flag    TYPE c,
      xv_sd_alv_variant  LIKE disvariant,
      xt_fcat            TYPE slis_t_fieldcat_alv,
      ls_fcat            LIKE LINE OF xt_fcat,
      xt_alv_sort        TYPE slis_t_sortinfo_alv,
      xv_user_command    TYPE slis_formname  VALUE 'USER_COMMAND',
      xv_variant_save    TYPE c              VALUE 'U',
      xv_layout          TYPE slis_layout_alv.

DATA: h_text             TYPE char80,
      h_awkey            TYPE awkey,
      h_awkey_2          TYPE awkey,              "MOD-002
      h_lifnr            TYPE lifnr,
      h_hkont            TYPE hkont,
      h_dmbtr            TYPE dmbtr,
      h_prctr            TYPE prctr,
      h_segment          TYPE fb_segment,
      h_shkzg            TYPE shkzg,
      h_col(10)          TYPE n,
      h_repid            LIKE sy-repid.

* Selections
SELECTION-SCREEN  BEGIN OF BLOCK sel  WITH FRAME  TITLE text-s01.
PARAMETERS:  p_bukrs  TYPE bukrs  MEMORY ID buk  OBLIGATORY,
             p_gjahr  TYPE gjahr  OBLIGATORY  DEFAULT sy-datum(4).
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_bewtp  FOR ekbe-bewtp  NO INTERVALS,
                s_budat  FOR ekbe-budat,
                s_lifnr  FOR bseg-lifnr,
                s_segm   FOR yse_prctr_bl-segment,
                s_ebeln  FOR ekbe-ebeln.
SELECTION-SCREEN  END OF BLOCK sel.


*----------------------------------------------------------------------*
* On the selection screen                                              *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

  PERFORM check_authorization.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR s_bewtp-low.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield        = 'BEWTP'
      window_title    = text-h01
      value_org       = 'S'
    TABLES
      value_tab       = t_pohc
      return_tab      = it_return_tab
    EXCEPTIONS
      parameter_error = 1
      no_values_found = 2
      OTHERS          = 3.

  IF sy-subrc EQ 0.
    READ TABLE it_return_tab INDEX 1.
    s_bewtp-low = it_return_tab-fieldval.
  ENDIF.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON s_bewtp.

  CHECK NOT s_bewtp-low IS INITIAL.

  READ TABLE t_pohc WITH KEY bewtp = s_bewtp-low.

  IF sy-subrc NE 0.
    MESSAGE e398(00) WITH 'PO history category not valid:'(e01)
                          s_bewtp-low.
  ENDIF.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*
INITIALIZATION.

* Fill PO history categories.
  PERFORM fill_po_histcat.

*----------------------------------------------------------------------*
START-OF-SELECTION.

* Statistics for transaction / program
  CALL METHOD ycl_statistics=>record_transaction.

* Build & display list
  PERFORM select_data.
  PERFORM alv_fcat.
  PERFORM alv_layout.
  PERFORM alv_display.


*&---------------------------------------------------------------------*
*&  Forms                                                              *
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  AUTHORITY-CHECK OBJECT 'YAM_BUKRS'
           ID 'BUKRS' FIELD p_bukrs.

  IF sy-subrc NE 0.
    MESSAGE e101(yam_sls) WITH p_bukrs.
  ENDIF.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      FORM  FILL_PO_HISTCAT
*&---------------------------------------------------------------------*
*       Fill PO history categories
*----------------------------------------------------------------------*
FORM fill_po_histcat .

  REFRESH t_pohc.

  SELECT bewtp bewtk INTO TABLE t_pohc
         FROM t163c
         WHERE bewtp IN ('E','N','Q')
           AND spras = 'E'.
  SORT t_pohc.

ENDFORM.                    " FILL_PO_HISTCAT

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Progress indicator
  h_text = 'Data is being selected'(i01).
  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      text = h_text.

* Limit selection for PO history categories
  IF s_bewtp[] IS INITIAL.
    s_bewtp-sign   = 'I'.
    s_bewtp-option = 'EQ'.
    LOOP AT t_pohc.
      s_bewtp-low = t_pohc-bewtp.
      APPEND s_bewtp.
    ENDLOOP.
  ENDIF.

* Select PO
  SELECT DISTINCT ebeln ebelp bewtp budat belnr
                  matnr                           "MOD-002
         INTO TABLE t_po
         FROM ekbe
         WHERE ebeln IN s_ebeln
           AND bewtp IN s_bewtp
           AND budat IN s_budat
           AND gjahr = p_gjahr.
  SORT t_po.
  CHECK NOT t_po[] IS INITIAL.

*** MOD-002 * begin ***
  SELECT * INTO TABLE t_prctr
           FROM yse_prctr_bl
           WHERE prctr NE ' '.

  SELECT DISTINCT matnr prdha
                  INTO TABLE t_mara
                  FROM mara
                  FOR ALL ENTRIES IN t_po
                  WHERE matnr = t_po-matnr.
*** MOD-002 * end ***

* Select FI documents
  LOOP AT t_po.

*  Begin of MOD-001.
*    h_awkey(10)   = t_po-mblnr.
*    h_awkey+10(4) = p_gjahr.
*    SELECT SINGLE belnr bldat blart hwaer
*           INTO CORRESPONDING FIELDS OF t_doc
*           FROM bkpf
*           WHERE awkey = h_awkey
*             AND bukrs = p_bukrs
*             AND gjahr = p_gjahr.

* The way the reference key is entered is changed from March 2011.
* It has BELNR + BUKRS + GJAHR.
*   The program should work for older dates as well with
*   previous logic for reference key of BELNR + GJAHR

    CLEAR h_awkey.
    h_awkey(10)   = t_po-mblnr.
    h_awkey+10(4) = p_gjahr.

    CLEAR h_awkey_2.
    h_awkey_2(10)   = t_po-mblnr.
    h_awkey_2+10(4) = p_bukrs.
    h_awkey_2+14(4) = p_gjahr.

    SELECT SINGLE belnr bldat blart hwaer
           INTO CORRESPONDING FIELDS OF t_doc
           FROM bkpf
           WHERE ( awkey = h_awkey OR awkey = h_awkey_2 )
             AND bukrs = p_bukrs
             AND gjahr = p_gjahr.
*  End of MOD-001.

    IF sy-subrc = 0.
      MOVE-CORRESPONDING t_po TO t_doc.
      APPEND t_doc.
    ENDIF.
  ENDLOOP.
  SORT t_doc.
  FREE t_po.
  CHECK NOT t_doc[] IS INITIAL.

*** MOD-002 * begin ***
* Get document lines
  SELECT belnr ebeln ebelp buzei koart hkont dmbtr shkzg prctr bschl
         INTO TABLE t_bseg
         FROM bseg
    FOR ALL ENTRIES IN t_doc
         WHERE bukrs = p_bukrs
           AND belnr = t_doc-belnr
           AND gjahr = p_gjahr
           AND ebeln = t_doc-ebeln
           AND ebelp = t_doc-ebelp
           AND bschl IN ('86', '89', '96', '99')
           AND koart IN ('A', 'M', 'S').

* Get vendors
  SELECT belnr lifnr
         INTO TABLE t_lifnri
         FROM bseg
    FOR ALL ENTRIES IN t_doc
         WHERE bukrs = p_bukrs
           AND belnr = t_doc-belnr
           AND gjahr = p_gjahr
           AND lifnr NE ' '.
  SORT t_lifnri BY belnr.
  DELETE ADJACENT DUPLICATES FROM t_lifnri COMPARING belnr.
  t_lifnr[] = t_lifnri[].
  FREE t_lifnri.

  IF NOT t_lifnr[] IS INITIAL.
    SELECT lfa1~lifnr lfa1~name1 lfb1~akont
           INTO TABLE t_lfai
           FROM lfa1
           INNER JOIN lfb1
                 ON lfa1~lifnr = lfb1~lifnr
           FOR ALL entries IN t_lifnr
           WHERE lfa1~lifnr = t_lifnr-lifnr
             AND lfb1~bukrs = p_bukrs.
    SORT t_lfai BY lifnr.
    DELETE ADJACENT DUPLICATES FROM t_lfai COMPARING lifnr.
    t_lfa[] = t_lfai[].
    FREE t_lfai.
  ENDIF.
*** MOD-002 * end ***

* Select other document data
  LOOP AT t_doc.

*   Progress indicator
    CONCATENATE 'Document'(i02) t_doc-belnr 'is being selected'(i03)
                INTO h_text  SEPARATED BY space.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        text = h_text.

*   Document details
*** MOD-002 * begin ***
*    CLEAR: h_lifnr, h_hkont, h_dmbtr, h_prctr, h_shkzg.
*    SELECT SINGLE lifnr hkont dmbtr prctr shkzg
*           INTO (h_lifnr, h_hkont, h_dmbtr, h_prctr, h_shkzg)
*           FROM bseg
*           WHERE bukrs = p_bukrs
*             AND belnr = t_doc-belnr
*             AND gjahr = p_gjahr
*             AND ebeln = t_doc-ebeln
*             AND ebelp = t_doc-ebelp
*             AND koart IN ('A', 'M', 'S').
**   Vendor
*    IF h_lifnr IS INITIAL.
*      SELECT SINGLE lifnr INTO h_lifnr
*             FROM bseg
*             WHERE bukrs = p_bukrs
*               AND belnr = t_doc-belnr
*               AND gjahr = p_gjahr
*               AND lifnr NE ' '.
*    ENDIF.

*   Get segment
    CLEAR: h_lifnr, t_lifnr.
    READ TABLE t_lifnr WITH TABLE KEY belnr = t_doc-belnr.
    IF sy-subrc = 0.
      h_lifnr = t_lifnr-lifnr.
    ENDIF.
*    SELECT SINGLE segment INTO h_segment
*           FROM yse_prctr_bl
*           WHERE prctr = h_prctr.
*** MOD-002 * end ***

*    IF sy-subrc = 0.                             "MOD-002
*    CHECK h_segment IN s_segm.                   "MOD-002
    CHECK h_lifnr   IN s_lifnr.
*   Fill table with amounts
    PERFORM fill_amount.
*    ENDIF.                                       "MOD-002
  ENDLOOP.

  SORT t_amt.
  FREE t_doc.

* Fill output table
  PERFORM fill_output.
  SORT t_out.
  FREE t_amt.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  FILL_AMOUNT
*&---------------------------------------------------------------------*
*       Fill table with amounts
*----------------------------------------------------------------------*
FORM fill_amount .

*  CHECK h_dmbtr NE 0.                            "MOD-002

  CLEAR t_amt.

  MOVE-CORRESPONDING t_doc TO t_amt.

*** MOD-002 * begin ***
*  t_amt-segment = h_segment.
*  t_amt-lifnr   = h_lifnr.
*  t_amt-dmbtr   = h_dmbtr.
*  t_amt-shkzg   = h_shkzg.
*  APPEND t_amt.

  IF t_doc-bewtp = 'E'.
    LOOP AT t_bseg WHERE belnr = t_doc-belnr
                     AND ebeln = t_doc-ebeln
                     AND ebelp = t_doc-ebelp
                     AND ( bschl = '89' OR bschl = '99' )
                     AND dmbtr NE 0.
      PERFORM add_amount.
    ENDLOOP.
  ELSE.
    LOOP AT t_bseg WHERE belnr = t_doc-belnr
                     AND ebeln = t_doc-ebeln
                     AND ebelp = t_doc-ebelp
                     AND dmbtr NE 0.
      PERFORM add_amount.
    ENDLOOP.
  ENDIF.
*** MOD-002 * end ***

ENDFORM.                    " FILL_AMOUNT

*&---------------------------------------------------------------------*
*&      Form  ADD_AMOUNT                          "MOD-002
*&---------------------------------------------------------------------*
*       Add entry in table with amounts
*----------------------------------------------------------------------*
FORM add_amount .

  CLEAR: h_segment, t_prctr.

  READ TABLE t_prctr WITH TABLE KEY prctr = t_bseg-prctr.
  IF sy-subrc = 0.
    h_segment = t_prctr-segment.
  ENDIF.

  IF t_doc-bewtp = 'Q'.
    t_amt-bschl = t_bseg-bschl.
  ELSE.
    CLEAR t_amt-bschl.
  ENDIF.

  CHECK h_segment IN s_segm.
  t_amt-segment = h_segment.
  t_amt-lifnr   = h_lifnr.
  t_amt-dmbtr   = t_bseg-dmbtr.
  t_amt-shkzg   = t_bseg-shkzg.
  APPEND t_amt.

ENDFORM.                    " ADD_AMOUNT

*&---------------------------------------------------------------------*
*&      Form  FILL_OUTPUT
*&---------------------------------------------------------------------*
*       Fill output table
*----------------------------------------------------------------------*
FORM fill_output .

  LOOP AT t_amt.
*   New segment
    AT NEW segment.
      CLEAR: t_out,
             h_dmbtr.
    ENDAT.

*   Detail.
*** MOD-002 * begin ***
*   Progress indicator
    CONCATENATE 'Purchase Order'(i04) t_amt-ebeln
                'is being prepared for output'(i05)
                INTO h_text  SEPARATED BY space.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        text = h_text.
*** MOD-002 * end ***
    h_amt = t_amt.
*   Debit (S) / Credit (H)
    IF t_amt-shkzg = 'H'.
      t_amt-dmbtr = t_amt-dmbtr * -1.
    ENDIF.
    h_dmbtr = h_dmbtr + t_amt-dmbtr.

*   Total per segment
    AT END OF segment.
      CHECK h_dmbtr NE 0.                         "MOD-002

      MOVE-CORRESPONDING h_amt TO t_out.
      t_out-dmbtr = h_dmbtr.
      t_out-gjahr = p_gjahr.
      t_out-bukrs = p_bukrs.
*     PO history category
      READ TABLE t_pohc WITH KEY bewtp = h_amt-bewtp.
      IF sy-subrc = 0.
        t_out-bewtk = t_pohc-bewtk.
      ELSE.
        t_out-bewtk = h_amt-bewtp.
      ENDIF.

*     Vendor data.
*** MOD-002 * begin ***
*      SELECT SINGLE lfa1~lifnr lfa1~name1 lfb1~akont
*             INTO (t_out-lifnr, t_out-name1, t_out-akont)
*             FROM lfa1
*             INNER JOIN lfb1
*                   ON lfa1~lifnr = lfb1~lifnr
*             WHERE lfa1~lifnr = h_amt-lifnr
*               AND lfb1~bukrs = p_bukrs.
      CLEAR t_lfa.
      READ TABLE t_lfa WITH TABLE KEY lifnr = h_amt-lifnr.
      IF sy-subrc = 0.
        t_out-lifnr = t_lfa-lifnr.
        t_out-name1 = t_lfa-name1.
        t_out-akont = t_lfa-akont.
      ENDIF.
*** MOD-002 * end ***

* Start of Mod-001
*     GAC
      CLEAR t_mara.
      READ TABLE t_mara WITH TABLE KEY matnr = h_amt-matnr.
      IF sy-subrc = 0.
        t_out-gac = t_mara-prdha+0(4).
      ENDIF.
* End of Mod-001

      APPEND t_out.
    ENDAT.

  ENDLOOP.

ENDFORM.                    " FILL_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  ALV_FCAT
*&---------------------------------------------------------------------*
*       ALV field catalog
*----------------------------------------------------------------------*
FORM alv_fcat .

** Create Fieldcatalogue from internal table
** CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE' ...

  CLEAR h_col.

* Field definitions
  PERFORM add_field USING 'EBELN'   11 'Purch.Order'(t01)          'X'.
  PERFORM add_field USING 'BELNR'   11 'Document Nr'(t02)          'X'.
  PERFORM add_field USING 'SEGMENT' 10 'Segment'(t03)              ' '.
  PERFORM add_field USING 'BEWTK'   07 'PO h.c.'(t04)              ' '.
  PERFORM add_field USING 'BUDAT'   10 'Post. Date'(t05)           ' '.
  PERFORM add_field USING 'BLDAT'   10 'Doc. Date'(t06)            ' '.
  PERFORM add_field USING 'BLART'   05 'Type'(t07)                 ' '.
  PERFORM add_field USING 'LIFNR'   11 'Vendor'(t08)               ' '.
  PERFORM add_field USING 'NAME1'   35 'Name'(t09)                 ' '.
  PERFORM add_field USING 'AKONT'   11 'Rec. Account'(t10)         ' '.
  PERFORM add_field USING 'DMBTR'   20 'Amount LC (excl.VAT)'(t12) ' '.
  PERFORM add_field USING 'HWAER'   05 'Curr.'(t13)                ' '.
  PERFORM add_field USING 'BUKRS'   06 'Comp.'(t14)                ' '.
  PERFORM add_field USING 'GJAHR'   05 'Year'(t15)                 ' '.
* Start of Mod-001
  PERFORM add_field USING 'GAC'   04 'GAC'(t16)                     ' '.
* End of Mod-001

ENDFORM.                    " ALV_FCAT

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr
                         p_hotsp.

  h_col = h_col + 1.

  CLEAR ls_fcat.
  ls_fcat-col_pos    = h_col.
  ls_fcat-fieldname  = p_field.
  ls_fcat-outputlen  = p_len.
  ls_fcat-seltext_l  = p_descr.
  ls_fcat-seltext_m  = p_descr.
  ls_fcat-seltext_s  = p_descr.
  ls_fcat-fix_column = 'X'.
  ls_fcat-emphasize  = 'X'.
  ls_fcat-hotspot    = p_hotsp.

* Suppress leading zeros
  IF p_field = 'EBELN'   OR
     p_field = 'BELNR'   OR
     p_field = 'SEGMENT' OR
     p_field = 'LIFNR'   OR
     p_field = 'AKONT'.
    ls_fcat-edit_mask = '==ALPHA'.
  ENDIF.

  APPEND ls_fcat TO xt_fcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT
*&---------------------------------------------------------------------*
*       Modify ALV layout
*----------------------------------------------------------------------*
FORM alv_layout .

  xv_layout-zebra = 'X'.
*  xv_layout-colwidth_optimize = 'X'.
*  xv_layout-smalltitle = 'X'.
*  xv_layout-grid_title = 'title'.

ENDFORM.                    " ALV_LAYOUT

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       Display ALV grid
*----------------------------------------------------------------------*
FORM alv_display .

*  xv_sd_alv_variant = xv_variant.
  h_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = h_repid
      i_callback_user_command = xv_user_command
*      i_callback_top_of_page  = 'ALV_TOP'
      i_grid_title            = 'Recovery of landed costs'(h02)
      is_layout               = xv_layout
      it_fieldcat             = xt_fcat
      it_sort                 = xt_alv_sort
      i_default               = 'X'
      i_save                  = 'A'
*      is_variant              = xv_sd_alv_variant
      i_screen_start_column   = 0
      i_screen_start_line     = 0
      i_screen_end_column     = 0
      i_screen_end_line       = 0
    TABLES
      t_outtab                = t_out
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.

  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&   Form  USER_COMMAND
*&---------------------------------------------------------------------*
*    Process user command
*----------------------------------------------------------------------*
FORM user_command USING ucomm    LIKE sy-ucomm
                        selfield TYPE slis_selfield.

* Check function code
  CASE ucomm.
    WHEN '&IC1'.
*     Select hotspot
      IF NOT selfield-value IS INITIAL.
        CASE selfield-fieldname.
          WHEN 'EBELN'.
            SET PARAMETER ID 'BES' FIELD selfield-value.
            CALL TRANSACTION 'ME23N' AND SKIP FIRST SCREEN.
          WHEN 'BELNR'.
*           Show document
            CLEAR h_out.
            READ TABLE t_out INDEX selfield-tabindex INTO h_out.
            SET PARAMETER ID 'BLN' FIELD selfield-value.
            SET PARAMETER ID 'BUK' FIELD h_out-bukrs.
            SET PARAMETER ID 'GJR' FIELD h_out-gjahr.
            CALL TRANSACTION 'FB03' AND SKIP FIRST SCREEN.
        ENDCASE.
      ENDIF.
  ENDCASE.

ENDFORM.                    " USER_COMMAND

*Text symbol text£º
*E01:PO history category not valid:
*H01:PO history categories
*H02:Recovery of landed costs
*I01:Data is being selected
*I02:Document
*I03:is being selected
*I04:Purchase Order
*I05:is being prepared for output
*S01:Selection
*T01:Purch.Order
*T02:Document Nr
*T03:Segment
*T04:PO h.c.
*T05:Post. Date
*T06:Doc. Date
*T07:Type
*T08:Vendor
*T09:Name
*T10:Rec. Account
*T11:Tax
*T12:Amount LC (excl.VAT)
*T13:Curr.
*T14:Comp.
*T15:Year

*T16:GAC
*Selection text£º
*P_BUKRS:D       .
*P_GJAHR:D       .
*S_BEWTP:D       .
*S_BUDAT:D       .
*S_EBELN:D       .
*S_LIFNR:D       .
*S_SEGM:D       .
