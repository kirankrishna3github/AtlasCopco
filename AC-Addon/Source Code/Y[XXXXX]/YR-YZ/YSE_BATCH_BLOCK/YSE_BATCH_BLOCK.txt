*&---------------------------------------------------------------------*
* PROGRAM ID           : YSE_BATCH_BLOCK                               *
* PROGRAM TITLE        :                                               *
* AUTHOR               : Anda Wu                                       *
* DATE                 : 20/12/2016                                    *
* DEVELOPMENT ID       : CD1K989889                                    *
* CHANGE REQUEST NUMBER: CR4052                                        *
* PROGRAM DESCRIPTION  :New GAC adaption  tool                         *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
REPORT  yse_batch_block NO STANDARD PAGE HEADING
                             LINE-SIZE 200.
************************************************************************
* TABLES
************************************************************************
TABLES: mcha.

************************************************************************
* TYPE POOLS  DECLARATIONS
************************************************************************
TYPE-POOLS : slis. "truxs.

TYPES: BEGIN OF ty_batches,
        matnr   TYPE marc-matnr,
        werks   TYPE marc-werks,
        charg   TYPE mcha-charg,
        block   TYPE char1,
       END OF ty_batches,

       BEGIN OF ty_mcha,
         matnr      TYPE mcha-matnr,
         werks      TYPE mcha-werks,
         charg      TYPE mcha-charg,
         lvorm      TYPE mcha-lvorm,
       END OF ty_mcha,

       BEGIN OF ty_update,
         objkey     TYPE bapi1003_key-object,
         valuesnum  TYPE numeric_allocation_tt,
         valueschar TYPE char_allocation_tt,
         valuescurr TYPE currency_allocation_tt,
       END OF ty_update,

       BEGIN OF ty_error,
         objkey     TYPE bapi1003_key-object,
         message    TYPE bapi_msg,
       END OF ty_error.

************************************************************************
* VARIABLES DECLARATIONS
************************************************************************
*DATA:   gv_ucomm    TYPE sy-ucomm.

************************************************************************
* WORKAERA
************************************************************************
DATA:   gs_layout       TYPE slis_layout_alv.

************************************************************************
* CONSTANTS DECLARATIONS
************************************************************************
CONSTANTS: gc_x     TYPE char1 VALUE 'X',
           gc_callback_routine TYPE slis_formname
                              VALUE 'USER_COMMAND',
           gc_class         TYPE klasse_d VALUE 'ZRU_BATCH',
           gc_block_gm      TYPE atnam    VALUE 'ZRU_BLOCK_GM',
           gc_022(3)        TYPE c        VALUE '022',
           gc_mcha          TYPE tabelle  VALUE 'MCHA'.
************************************************************************
* INTERNAL TABLE DECLARATIONS
************************************************************************
DATA: gt_fieldcat       TYPE slis_t_fieldcat_alv,
      gt_mcha           TYPE STANDARD TABLE OF ty_mcha,
      gt_error          TYPE STANDARD TABLE OF ty_error,
      gt_batch          TYPE STANDARD TABLE OF ty_batches,
      gt_update_blk     TYPE STANDARD TABLE OF ty_update,
      gt_update_ubk     TYPE STANDARD TABLE OF ty_update.

************************************************************************
* SELECTION SCREEN
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.
SELECT-OPTIONS: s_matnr FOR mcha-matnr MODIF ID m1,
                s_werks FOR mcha-werks MODIF ID m1 OBLIGATORY,
                s_charg FOR mcha-charg MODIF ID m1.
SELECTION-SCREEN END OF BLOCK b1.

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-002.
PARAMETERS: rb_list   RADIOBUTTON GROUP g1 DEFAULT 'X',
            rb_lista  RADIOBUTTON GROUP g1,
            rb_tick   RADIOBUTTON GROUP g1,
            rb_untk   RADIOBUTTON GROUP g1.
SELECTION-SCREEN END OF BLOCK b2.

************************************************************************
* INITIALIZATION.
************************************************************************
INITIALIZATION.
* Do initialization
  PERFORM do_init.

************************************************************************
* START-OF-SELECTION
************************************************************************
START-OF-SELECTION.
* Get the batches which have the block flag ticked on
  PERFORM do_get_object.

  IF rb_list IS NOT INITIAL or rb_lista IS NOT INITIAL.
    PERFORM show_block_list.
  ELSE.
    IF rb_untk IS NOT INITIAL.
*     Update the materail batch block
      PERFORM do_update TABLES gt_update_blk.
    ELSEIF rb_tick IS NOT INITIAL.
*     Update the materail batch unblock
      PERFORM do_update TABLES gt_update_ubk.
    ENDIF.
  ENDIF.

END-OF-SELECTION.
* Show the procee error log
  PERFORM show_log.


*&---------------------------------------------------------------------*
*&      Form  DO_INIT
*&---------------------------------------------------------------------*
*       Do initialization
*----------------------------------------------------------------------*
FORM do_init .

ENDFORM.                    " DO_INIT
*&---------------------------------------------------------------------*
*&      Form  DO_GET_OBJECT
*&---------------------------------------------------------------------*
*       Get the batches which have the block flag ticked on
*----------------------------------------------------------------------*
FORM do_get_object.

  DATA: lt_ngac_log TYPE STANDARD TABLE OF yse_mat_ngac_log,
        lt_num      TYPE STANDARD TABLE OF bapi1003_alloc_values_num,
        lt_char     TYPE STANDARD TABLE OF bapi1003_alloc_values_char,
        lt_curr     TYPE STANDARD TABLE OF bapi1003_alloc_values_curr,
        ls_batch    TYPE ty_batches,
        ls_update   TYPE ty_update,
        ls_char     TYPE bapi1003_alloc_values_char,
        ls_mcha     TYPE ty_mcha,
        lt_return   TYPE STANDARD TABLE OF bapiret2,
        lv_objkey   TYPE bapi1003_key-object.

  SELECT *
    FROM yse_mat_ngac_log
    INTO TABLE lt_ngac_log
    WHERE matnr IN s_matnr
      AND werks IN s_werks
      AND upd_ind  = 'Y'.
  DELETE lt_ngac_log WHERE werks = space.

  CHECK lt_ngac_log IS NOT INITIAL.

  SELECT matnr
         werks
         charg
         lvorm
    FROM mcha
    INTO TABLE gt_mcha
    FOR ALL ENTRIES IN lt_ngac_log
    WHERE matnr = lt_ngac_log-matnr
      AND werks = lt_ngac_log-werks
      AND charg IN s_charg.
  DELETE gt_mcha WHERE lvorm <> space.

  CHECK gt_mcha IS NOT INITIAL.
  LOOP AT gt_mcha INTO ls_mcha.
    REFRESH: lt_num,
             lt_char,
             lt_curr,
             lt_return.
    CLEAR: lv_objkey.
    MOVE ls_mcha-matnr TO lv_objkey+00(18).
    MOVE ls_mcha-werks TO lv_objkey+18(04).
    MOVE ls_mcha-charg TO lv_objkey+22(10).
    CALL FUNCTION 'BAPI_OBJCL_GETDETAIL'
      EXPORTING
        objectkey        = lv_objkey
        objecttable      = gc_mcha
        classnum         = gc_class
        classtype        = gc_022
        unvaluated_chars = gc_x
      TABLES
        allocvaluesnum   = lt_num
        allocvalueschar  = lt_char
        allocvaluescurr  = lt_curr
        return           = lt_return.
    READ TABLE lt_char INTO ls_char WITH KEY
      charact = gc_block_gm.
    IF sy-subrc = 0.
      IF ls_char-value_char = gc_x.
        IF rb_untk IS NOT INITIAL.
          ls_update-objkey      = lv_objkey.
          ls_update-valuesnum   = lt_num.
          ls_update-valuescurr  = lt_curr.
          CLEAR: ls_char-value_char,ls_char-value_neutral.
          MODIFY lt_char INDEX sy-tabix
            FROM ls_char.
          ls_update-valueschar  = lt_char.
          APPEND ls_update TO gt_update_blk.
        ENDIF.
        IF rb_list IS NOT INITIAL or rb_lista IS NOT INITIAL.
          ls_batch-matnr   = ls_mcha-matnr.
          ls_batch-werks   = ls_mcha-werks.
          ls_batch-charg   = ls_mcha-charg.
          ls_batch-block   = gc_x.
          APPEND ls_batch TO gt_batch.
        ENDIF.
      ELSE.
        IF rb_tick IS NOT INITIAL.
          ls_update-objkey      = lv_objkey.
          ls_update-valuesnum   = lt_num.
          ls_update-valuescurr  = lt_curr.
          ls_char-value_char    = gc_x.
          ls_char-value_neutral = gc_x.
          MODIFY lt_char INDEX sy-tabix
            FROM ls_char.
          ls_update-valueschar  = lt_char.
          APPEND ls_update TO gt_update_ubk.
        ENDIF.
        IF rb_lista IS NOT INITIAL.
          ls_batch-matnr   = ls_mcha-matnr.
          ls_batch-werks   = ls_mcha-werks.
          ls_batch-charg   = ls_mcha-charg.
          CLEAR ls_batch-block.
          APPEND ls_batch TO gt_batch.
        ENDIF.
      ENDIF.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " DO_GET_OBJECT
*&---------------------------------------------------------------------*
*&      Form  DO_UPDATE
*&---------------------------------------------------------------------*
*       Update the materail master date
*----------------------------------------------------------------------*
FORM do_update TABLES pt_update_tab.
  DATA: ls_update   TYPE ty_update,
        ls_return   TYPE bapiret2,
        ls_error    TYPE ty_error,
        lt_return   TYPE STANDARD TABLE OF bapiret2.

  CHECK pt_update_tab[] IS NOT INITIAL.
  LOOP AT pt_update_tab[] INTO ls_update.
    REFRESH: lt_return.
*   Update characteristics
    CALL FUNCTION 'BAPI_OBJCL_CHANGE'
      EXPORTING
        objectkey          = ls_update-objkey
        objecttable        = gc_mcha
        classnum           = gc_class
        classtype          = gc_022
*        STATUS             = '1'
*        STANDARDCLASS      =
*        CHANGENUMBER       =
*        KEYDATE            = SY-DATUM
*        NO_DEFAULT_VALUES  = ' '
*      IMPORTING
*        CLASSIF_STATUS     =
      TABLES
        allocvaluesnumnew  = ls_update-valuesnum
        allocvaluescharnew = ls_update-valueschar
        allocvaluescurrnew = ls_update-valuescurr
        return             = lt_return.
    READ TABLE lt_return INTO ls_return
      WITH KEY type = 'E'.
    IF sy-subrc = 0.
      ls_error-objkey   = ls_update-objkey.
      MESSAGE ID ls_return-id TYPE ls_return-type
          NUMBER ls_return-number WITH ls_return-message_v1
          ls_return-message_v2 ls_return-message_v3
          ls_return-message_v4 INTO ls_error-message.
      APPEND ls_error TO gt_error.
    ELSE.
      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
        EXPORTING
          WAIT          = gc_x.
    ENDIF.
  ENDLOOP.
ENDFORM.                    " DO_UPDATE
*&---------------------------------------------------------------------*
*&      Form  SHOW_BLOCK_LIST
*&---------------------------------------------------------------------*
*       Show the block batches
*----------------------------------------------------------------------*
FORM show_block_list .
* FIELDCAT set
  PERFORM alv_fieldcat_set.
* LAYOUT set
  PERFORM alv_layout_set.
* ALV display
  PERFORM alv_display.
ENDFORM.                    " SHOW_BLOCK_LIST

*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM alv_fieldcat_set .

  DATA:
     lv_linecnt  TYPE i,
     ls_fieldcat TYPE slis_fieldcat_alv.

  CLEAR lv_linecnt.
* Material Number
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'MATNR'.
  ls_fieldcat-seltext_s = 'Mat.No'(030).
  ls_fieldcat-seltext_m = 'Material Number'(031).
  ls_fieldcat-hotspot = gc_x.
  ls_fieldcat-ref_fieldname =  'MATNR'.
  ls_fieldcat-ref_tabname   = 'MARA'.
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* Plant
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'WERKS'.
  ls_fieldcat-seltext_s = 'Plant'(032).
  ls_fieldcat-seltext_m = 'Plant'(032).
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* Batch
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'CHARG'.
  ls_fieldcat-seltext_s = 'Batch'(033).
  ls_fieldcat-seltext_m = 'Batch'(033).
  APPEND ls_fieldcat TO gt_fieldcat.

* Block Indicator
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'BLOCK'.
  ls_fieldcat-seltext_s = 'Blocked'(034).
  ls_fieldcat-seltext_m = 'Blocked'(034).
  ls_fieldcat-checkbox = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ALV_FIELDCAT_SET

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   " Auto optimize column width
  gs_layout-detail_popup = gc_x.        " Show detail screen

ENDFORM.                    " ALV_LAYOUT_SET

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_callback TYPE slis_formname.

  CHECK gt_batch IS NOT INITIAL.
  lv_callback = gc_callback_routine.
  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = sy-repid
      is_layout               = gs_layout
      i_callback_user_command = lv_callback
      it_fieldcat             = gt_fieldcat
    TABLES
      t_outtab                = gt_batch
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       USER_COMMAND
*----------------------------------------------------------------------*
FORM user_command USING u_ucomm     LIKE sy-ucomm           "#EC CALLED
                        p_selfield  TYPE slis_selfield.
  DATA: ls_batch TYPE ty_batches.
  CASE u_ucomm.
    WHEN '&IC1'.        " SAP standard code for double-clicking
      READ TABLE  gt_batch INTO ls_batch INDEX p_selfield-tabindex.
      IF p_selfield-fieldname = 'MATNR'.
        IF p_selfield-value IS NOT INITIAL.
          SET PARAMETER ID 'MAT' FIELD p_selfield-value.
          SET PARAMETER ID 'WRK' FIELD ls_batch-werks.
          SET PARAMETER ID 'CHA' FIELD ls_batch-charg.
          CALL TRANSACTION 'MSC3N' AND SKIP FIRST SCREEN.
        ENDIF.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.

ENDFORM.                    " USER_COMMAND
*&---------------------------------------------------------------------*
*&      Form  SHOW_LOG
*&---------------------------------------------------------------------*
*       Show the procee error log
*----------------------------------------------------------------------*
FORM show_log .
  DATA: ls_error TYPE ty_error.
  CHECK gt_error IS NOT INITIAL.
  WRITE: 3(35) 'Material-Plant-Batch'(l01),
         sy-vline, 40 'Error Message'(l02).
  LOOP AT gt_error INTO ls_error.
    ULINE.
    WRITE: /3(35) ls_error-objkey,sy-vline, 40 ls_error-message.
  ENDLOOP.
ENDFORM.                    " SHOW_LOG

*Text symbol text£º
*001:Selection screen
*002:Options
*030:Mat.No
*031:Material Number
*032:Plant
*033:Batch
*034:Blocked
*L01:Material-Plant-Batch

*L02:Error Message
*Selection text£º
*RB_LIST:        List blocked batches
*RB_LISTA:        List all batches
*RB_TICK:        Block batches
*RB_UNTK:        Unblock batches
*S_CHARG:D       .
*S_MATNR:D       .
*S_WERKS:D       .
