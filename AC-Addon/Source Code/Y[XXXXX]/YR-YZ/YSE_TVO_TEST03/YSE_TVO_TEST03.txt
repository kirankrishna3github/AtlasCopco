REPORT YSE_TVO_TEST03 MESSAGE-ID YSEPLBRD.

* ********************************************************************* *
* ATLAS COPCO RENTAL PLANNING BOARD *********************************** *
* ********************************************************************* *
* Author(s): Tom Van Oevelen (CSC)
* Date     : 10/11/2006
* ********************************************************************* *
* ! ! ! ! ! ! IMPORTANT REMARK ! ! ! ! ! !
*  * * * * * *                * * * * * *
*  Development continued in program with name YSE_REN_PLBRD. Message class
*  has been renamed and therefore messages may fail here...
* ********************************************************************* *
* Comments, improvements, etc.
* ----------------------------
* - Search for the tag TVOTBA for extra comments or missing pieces
* - Try to replace all the SELECT...ENDSELECT 's by a SELECT FOR ALL
*   ENTRIES
* - In case the user provides a specific model on the selection screen
*   together with some characteristics (doesn't seem to make much sense),
*   the class. system will be searched first. Only afterwards the
*   provided model will come into play. This could be improved
* - When an item has contract enddate 16/12/2006, the current coding stops
*   the node in the graph at 00:00 the 16'th, which means it's showing
*   1 day short
* - Sales org: can it be derived and hence removed from the selection screen?
* ********************************************************************* *

*&---------------------------------------------------------------------*
*& INCLUDES -- Data types, data variables, constants ... --------------*
*&---------------------------------------------------------------------*
INCLUDE: SGRCCNST,              " General Graphics Constants
         LBARCCON,              " Constants for Network/Hierarchy (barchart) Graphics
         BARCDATA,              " General Data Declaration for the Bar Chart
         <ctldef>,              " General Definitions For Controls
         <symbol>,              " Assign Symbol Font Characters for Lists to ASCII Codes
         <icon>,                " Assign Icon Font Characters for Lists to ASCII Codes
         YSE_RENT_PLBRD_GT,     " Globally defined types
         YSE_RENT_PLBRD_GD01,   " Global data variables
         YSE_RENT_PLBRD_GC.     " Global constants

*&---------------------------------------------------------------------*
*& SELECTION SCREEN ---------------------------------------------------*
*&---------------------------------------------------------------------*
* Document / Items
SELECTION-SCREEN BEGIN OF BLOCK B60 WITH FRAME TITLE TEXT-924.
  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 1(20) TEXT-924 FOR FIELD P_VBELN.
    SELECTION-SCREEN POSITION 21.
    PARAMETER P_VBELN TYPE VBELN.                      " Doc Nr
    SELECTION-SCREEN COMMENT 35(8) TEXT-927 FOR FIELD P_DOCENQ.
    PARAMETER P_DOCENQ RADIOBUTTON GROUP R20.          " Radio Button Enquiry
    SELECTION-SCREEN COMMENT 50(8) TEXT-928 FOR FIELD P_DOCQUO.
    PARAMETER P_DOCQUO RADIOBUTTON GROUP R20.          " Radio Button Quotation
  SELECTION-SCREEN END OF LINE.
  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 3(18) TEXT-926 FOR FIELD P_POSNR.
    SELECTION-SCREEN POSITION 21.
    PARAMETER P_POSNR TYPE POSNR.                      " Position
    PARAMETER P_PMATNR(18) TYPE C        MODIF ID POS. " Pos Material
    PARAMETER P_PKWMEN     TYPE KWMENG
                      VISIBLE LENGTH 7   MODIF ID POS. " Pos Quant
    PARAMETER P_PVRKME     TYPE VRKME    MODIF ID POS. " Pos SU
    PARAMETER P_PGRPOS     TYPE GRPOS    MODIF ID POS. " Pos Alt Item
    PARAMETER P_PARKTX     TYPE ARKTX    MODIF ID POS. " Description
  SELECTION-SCREEN END OF LINE.
  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 5(15) TEXT-901 FOR FIELD P_BEGDAT.
    PARAMETER P_BEGDAT LIKE SY-DATUM.                  " Start date
    SELECTION-SCREEN COMMENT 35(15) TEXT-902 FOR FIELD P_ENDDAT.
    PARAMETER P_ENDDAT LIKE SY-DATUM.                  " End date
  SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK B60.
* Models / Equipments
SELECTION-SCREEN BEGIN OF BLOCK B10 WITH FRAME TITLE TEXT-915.
* General characteristics
* Model  /  Plant
  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 1(9) TEXT-917 FOR FIELD P_WERKS.
    PARAMETER P_WERKS TYPE WERKS_D MEMORY ID WRK.                    " Plant
    SELECTION-SCREEN COMMENT 18(13) TEXT-919 FOR FIELD P_BUKRS.
    PARAMETER P_BUKRS TYPE BUKRS MEMORY ID BUK.                      " CompCode
    SELECTION-SCREEN COMMENT 39(9) TEXT-925 FOR FIELD P_VKORG.
    PARAMETER P_VKORG TYPE VKORG MEMORY ID VKO.                      " SalesOrg
    SELECTION-SCREEN COMMENT 57(4) TEXT-934 FOR FIELD P_VTWEG.
    PARAMETER P_VTWEG TYPE VTWEG MEMORY ID VTW.                      " DCh
  SELECTION-SCREEN END OF LINE.
  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 1(6) TEXT-916 FOR FIELD SO_MODEL.
    SELECT-OPTIONS SO_MODEL FOR GV_MODEL NO INTERVALS.               " Model
  SELECTION-SCREEN END OF LINE.
  SELECTION-SCREEN SKIP.
* Compressor characteristics
  SELECTION-SCREEN BEGIN OF LINE.
    PARAMETER P_CLCOMP TYPE C AS CHECKBOX USER-COMMAND RUSR DEFAULT GC_CHAR_X.
    SELECTION-SCREEN COMMENT 23(30) TEXT-914 FOR FIELD P_CLCOMP.     " Check box
  SELECTION-SCREEN END OF LINE.
  SELECTION-SCREEN BEGIN OF BLOCK B30 WITH FRAME TITLE TEXT-914.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 1(20) TEXT-922 FOR FIELD SO_PRESS MODIF ID CMP.
      SELECT-OPTIONS SO_PRESS FOR GV_PRESS MODIF ID CMP.             " Pressure (bar)
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 1(20) TEXT-923 FOR FIELD SO_CAPAC MODIF ID CMP.
      SELECT-OPTIONS SO_CAPAC FOR GV_CAPAC MODIF ID CMP.             " Capacity (m3/min)
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN SKIP.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 1(15) TEXT-920 FOR FIELD P_OFREE MODIF ID CMP.
      PARAMETER P_OFREE RADIOBUTTON GROUP R10 MODIF ID CMP.         " Oil free
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 1(15) TEXT-921 FOR FIELD P_OINJD MODIF ID CMP.
      PARAMETER P_OINJD RADIOBUTTON GROUP R10 MODIF ID CMP.         " Oil injected
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(15) TEXT-929 FOR FIELD P_CPAFCL MODIF ID CMP.
      PARAMETER P_CPAFCL(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID CMP. " Aftercooler
      SELECTION-SCREEN COMMENT 45(15) TEXT-930 FOR FIELD P_CPGENR MODIF ID CMP.
      PARAMETER P_CPGENR(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID CMP. " Generator
    SELECTION-SCREEN END OF LINE.
  SELECTION-SCREEN END OF BLOCK B30.
SELECTION-SCREEN END OF BLOCK B10.

*&---------------------------------------------------------------------*
*& INCLUDES -- Supporting Forms, Screen Logic ... ---------------------*
*&---------------------------------------------------------------------*
INCLUDE: YSE_RENT_PLBRD_F01,    " Supporting Forms
         YSE_RENT_PLBRD_F02,    " Supporting Forms Barchart Data Preparation
         YSE_RENT_PLBRD_F03,    " Supporting Forms Barchart Processing Logic
         YSE_RENT_PLBRD_F99,    " Temporary supporting forms
         YSE_RENT_PLBRD_SCR0300," Screen0300 processing (PBO, PAI)
         YSE_RENT_PLBRD_SCR0200," Screen0200 processing (PBO, PAI)
         YSE_RENT_PLBRD_SCR0100," Screen0100 processing (PBO, PAI)
         YSE_RENT_PLBRD_SCR0101," Screen0101 processing (PBO, PAI)
         YSE_RENT_PLBRD_SCR0102," Screen0102 processing (PBO, PAI)
         YSE_RENT_PLBRD_SCR0103," Screen0103 processing (PBO, PAI)
         YSE_RENT_PLBRD_SCR0104," Screen0104 processing (PBO, PAI)
         YSE_RENT_PLBRD_SCR0105," Screen0105 processing (PBO, PAI)
         YSE_RENT_PLBRD_SCR0106." Screen0106 processing (PBO, PAI)

*&---------------------------------------------------------------------*
*& INITIALIZATION -----------------------------------------------------*
*&---------------------------------------------------------------------*
INITIALIZATION.

* READ PARAMETER-ID'S HOLDING THE VIEW MODE FOR TOP AND BOTTOM ***
* ************************************************************ ***
  GET PARAMETER ID 'YSE_RPB_ST' FIELD GV_ST.
  IF SY-SUBRC <> 0. GV_ST = GC_ST_MD. ENDIF.
  GET PARAMETER ID 'YSE_RPB_SB' FIELD GV_SB.
  IF SY-SUBRC <> 0. GV_SB = GC_SB_DO. ENDIF.

* INITIALIZE THE TIME FRAME TO BE SHOWN ON PLANNING BOARD ***
* ******************************************************* ***
  DATA: LV_NOW  LIKE SY-DATUM,
        LV_DATE LIKE SY-DATUM,
        LV_MONT(2) TYPE C,
        LV_YEAR(4) TYPE C.
* Take current date in a local variable.
  LV_NOW = SY-DATUM.
* Put the first of the current month in the "Start date"
  CONCATENATE LV_NOW(6) '01' INTO LV_DATE.
  P_BEGDAT = LV_DATE.
* Take out the current month
  LV_MONT = LV_NOW+4(2).
* If it's the last month of the year, go to the first month of next year
  IF LV_MONT = 12.
    LV_MONT = 1.
    LV_YEAR = LV_NOW(4).
    LV_YEAR = LV_YEAR + 1.
* Else, just go to the next month of this year
  ELSE.
    LV_YEAR = LV_NOW(4).
    LV_MONT = LV_MONT + 1.
  ENDIF.
* Padd the month with a zero at the front if required
  IF LV_MONT < 10.
    CONCATENATE '0' LV_MONT INTO LV_MONT.
  ENDIF.
* Compose a date which is the first of the month following the current (be it next year or not)
  CONCATENATE LV_YEAR LV_MONT '01' INTO LV_DATE.
* Subtract 1 of the date calculated, and you now have the end of the current month
  LV_DATE = LV_DATE - 1.
* Put the end of the current month in the "end date"
  P_ENDDAT = LV_DATE.

* INITIALIZE THE DROP DOWN VALUES ***
* ******************************* ***
  PERFORM PREPARE_COMBO USING 'P_CPAFCL' GC_CLS_COM_AFTRCL. " Fill control P_CPAFCL with char. values for "Aftercooler"
  PERFORM PREPARE_COMBO USING 'P_CPGENR' GC_CLS_COM_GENERT. " Fill control P_CPGENR with char. values for "Generator"

*&---------------------------------------------------------------------*
*& AT SELECTION SCREEN OUTPUT -----------------------------------------*
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.

  DATA: LV_CLCOMP   TYPE I.

* Set flag for Compressor characteristics
  IF P_CLCOMP = GC_CHAR_X.
    LV_CLCOMP = 1.
  ELSE.
    LV_CLCOMP = 0.
  ENDIF.

* Loop at screen and de(activate) blocks according to flags
  LOOP AT SCREEN.
    CASE SCREEN-GROUP1.
      WHEN GC_CLCOMP.
        SCREEN-ACTIVE = LV_CLCOMP.
      WHEN GC_POS.
        SCREEN-INPUT = 0.
    ENDCASE.
    MODIFY SCREEN.
  ENDLOOP.

*&---------------------------------------------------------------------*
*& AT SELECTION SCREEN ON VALUE-REQUEST FOR P_POSNR -------------------*
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_POSNR.

  CALL SCREEN 200 STARTING AT 40 5 ENDING AT 135 17.

*&---------------------------------------------------------------------*
*& AT SELECTION SCREEN ON VALUE-REQUEST FOR P_VBELN -------------------*
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_VBELN.

  DATA: IT_RETURN TYPE DDSHRETVAL OCCURS 0 WITH HEADER LINE.

  CALL FUNCTION 'F4IF_FIELD_VALUE_REQUEST'
    EXPORTING
      TABNAME                   = 'VBAK'
      FIELDNAME                 = 'VBELN'
      SEARCHHELP                = 'VMVA'
    TABLES
      RETURN_TAB                = IT_RETURN.

  IF SY-SUBRC = 0.
    READ TABLE IT_RETURN INDEX 1.
    P_VBELN = IT_RETURN-FIELDVAL.
  ENDIF.

*&---------------------------------------------------------------------*
*& AT SELECTION SCREEN ------------------------------------------------*
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.

  DATA: LV_AUART TYPE AUART.

* Empty the internal tables of the document items
  CLEAR: IT_VBAP, IT_VBAP[].

* Determine the document type of the order number entered
  SELECT SINGLE AUART
           INTO LV_AUART
           FROM VBAK
          WHERE VBELN = P_VBELN.

* Set correct radio button which indicates the document type
  IF SY-SUBRC = 0.
    CASE LV_AUART.
      WHEN GC_AUART_ENQU.
        P_DOCQUO = SPACE.
        P_DOCENQ = GC_CHAR_X.
      WHEN GC_AUART_QUOT.
        P_DOCENQ = SPACE.
        P_DOCQUO = GC_CHAR_X.
      WHEN OTHERS.
        P_DOCQUO = SPACE.
        P_DOCENQ = GC_CHAR_X.
    ENDCASE.
  ELSE.
    IF NOT P_VBELN IS INITIAL.
      MESSAGE E002 WITH P_VBELN.
    ENDIF.
    P_DOCQUO = SPACE.
    P_DOCENQ = GC_CHAR_X.
  ENDIF.

* Select the items from VBAP
  SELECT * FROM VBAP
       WHERE VBELN = P_VBELN.
    MOVE-CORRESPONDING VBAP TO IT_VBAP.
    APPEND IT_VBAP.
  ENDSELECT.
* Read the item chosen by user, if it fails, just read the first item
  READ TABLE IT_VBAP WITH KEY POSNR = P_POSNR.
  IF SY-SUBRC <> 0.
    READ TABLE IT_VBAP INDEX 1.
  ENDIF.
* Show the selected item
  P_POSNR = IT_VBAP-POSNR.
  PERFORM SC_SET_ITEM_PROP.

* Find the start- and enddate for the item
  SELECT SINGLE VBEGDAT VENDDAT
           INTO (P_BEGDAT, P_ENDDAT)
           FROM VEDA
          WHERE VBELN = P_VBELN
            AND VPOSN = P_POSNR.
  IF SY-SUBRC <> 0.
  ENDIF.

*&---------------------------------------------------------------------*
*& START OF PROGRAM ---------------------------------------------------*
*&---------------------------------------------------------------------*
START-OF-SELECTION.

  PERFORM CHECK_WIN32.        " Check that the client is on a win32 platform
* PERFORM LOAD_DUMMY_DATA.    " Load dummy data
  PERFORM LOAD_DATA.          " Load the data (documents, models, ...)
  PERFORM CREATE_CHARTCONTROL." Prepare a barchart control
  PERFORM PREPARE_CHART_DATA. " Prepare the data to be passed to the barchart control

  CALL SCREEN 300.

*Text symbol text£º
*001:Graphics Profile
*002:End of loading process
*003:Box/node duplicated
*004:Box already exists in the line
*005:Box inserted
*006:Node inserted
*007:Cannot change plant
*008:Fld content changed
*012:Layer inserted
*013:No free layer type available
*014:No free text index available
*016:Box
*025:No Object Selected
*026:Not possible to move boxes between charts
*201:Models
*203:Documents
*901:Startdate
*902:Enddate
*903:   Available
*904:   Soft Reservation
*905:View Top
*906:View Bottom
*907::
*908:(Soft Reserved)
*909:(Available)
*910:Quantity
*911:A Quotation
*912:Quotation already exists
*913:already exists (possibly more than one), create new document?
*914:Compressor Characteristics
*915:Models
*916:Model
*917:Plant
*918:General
*919:Company Code
*920:Oil free
*921:Oil injected
*922:Pressure (bar)
*923:Capacity (m#/min)
*924:Document
*925:SalesOrg
*926:Item
*927:Enquiry
*928:Quotation
*929:Aftercooler
*930:Generator
*931:Owning CC
*932:Current Location
*933:Current Status
*934:DCh
