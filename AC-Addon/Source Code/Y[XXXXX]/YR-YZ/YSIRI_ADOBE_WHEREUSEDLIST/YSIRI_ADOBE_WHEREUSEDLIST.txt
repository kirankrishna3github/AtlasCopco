*&---------------------------------------------------------------------*
*& Report  Z_ADOBEFORM_WHERE_USED_LIST
*&
*&---------------------------------------------------------------------*
*& Developer/Requestor : Shireesha Depala
*& Date of Creation    : 21-10-2014
*& Transport Req No    : xxxxxxxxxx
*& Description         : This program gives Where-Used List for Adobe objects
*&                     Like Adobe Form, Interface.
*&                     program also does global search and return list.
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*

REPORT  ysiri_adobe_whereusedlist NO STANDARD PAGE HEADING.

TYPE-POOLS: slis.

**********************************************
*      Types & Data Declaration
**********************************************
TYPES : BEGIN OF ty_output,
                program       TYPE char30 ,          " program name
                description   TYPE char100,          " program description
            END OF ty_output ,

            BEGIN OF ty_otpg,
                kschl	TYPE na_kschl,              "Output Type
                nacha	TYPE na_nacha,              "Message transmission medium
                kappl	TYPE kappl,                 "Application
                pgnam	TYPE na_pgnam,              "Program name
                ronam	TYPE na_ronam,              "Name of routine
                fonam	TYPE tdform,                "Form Name
            END OF ty_otpg,

            BEGIN OF ty_fp,
               name           TYPE  fpname,
               interface      TYPE fpname,
            END OF ty_fp ,

            BEGIN OF ty_pp,
              autyp	TYPE auftyp,                   " Order category
              rbart	TYPE fauart,                   " Reference order type
              lstid	TYPE listid,                   " List name
              repid	TYPE repid,                    " ABAP Program Name
              vbform  TYPE co_vbform,              " Form routine of the print program
              form_pdf  TYPE form_pdf,             "Name of PDF-Based Form
            END OF ty_pp.

DATA :it_output TYPE TABLE OF ty_output,
      wa_output TYPE ty_output,
      it_fp  TYPE TABLE OF ty_fp,
      wa_fp TYPE ty_fp,
      it_tnapr TYPE TABLE OF tnapr,
      wa_tnapr LIKE LINE OF it_tnapr,
      it_t496f TYPE TABLE OF t496f,
      wa_t496f TYPE t496f,
      it_t496r TYPE TABLE OF t496r,
      wa_t496r TYPE t496r,
      it_otpg TYPE TABLE OF ty_otpg,
      wa_otpg TYPE ty_otpg,
      it_pp TYPE TABLE OF ty_pp,
      wa_pp TYPE ty_pp.

DATA: it_fieldcat TYPE  slis_t_fieldcat_alv,
      it_fieldcat_alv2 TYPE  slis_t_fieldcat_alv,
      it_fieldcat_alv3 TYPE  slis_t_fieldcat_alv,
      it_fieldcat_alv4 TYPE  slis_t_fieldcat_alv,
      wa_fieldcat TYPE slis_fieldcat_alv,
      wa_layout TYPE slis_layout_alv,
      gt_xevents TYPE slis_t_event,
      gt_print TYPE slis_print_alv.

**********************************************
*  Macro for field Catalog
***********************************************
DEFINE m_fieldcat.

  clear wa_fieldcat.
  wa_fieldcat-col_pos = &1.
  wa_fieldcat-fieldname = &2.
  wa_fieldcat-ref_tabname = &3.
  wa_fieldcat-seltext_l =  &4.

END-OF-DEFINITION.

**********************************************
*             Selection Screen
**********************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.
*  Search for Adobe Form
PARAMETERS :  p1  RADIOBUTTON GROUP opr DEFAULT 'X' USER-COMMAND us1,
              p_adb TYPE  fpcontext-name.
* Search For Interface
PARAMETERS :  p2  RADIOBUTTON GROUP opr ,
              p_inf TYPE fpcontext-interface.
SELECTION-SCREEN END OF BLOCK b1.

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-005.
* Global search with hardcode text
PARAMETERS :  p3  AS CHECKBOX USER-COMMAND us1 ,
              p_gs TYPE  fpcontext-name.
SELECTION-SCREEN END OF BLOCK b2.

**********************************************
AT SELECTION-SCREEN OUTPUT.
*********************************************
  LOOP AT SCREEN.
    CASE screen-name.
      WHEN 'P1'.
      WHEN 'P_ADB' .
        IF p1 IS INITIAL.
          screen-input = '0'.
        ELSE.
          screen-input = '1'.
        ENDIF.
        MODIFY SCREEN.
      WHEN 'P2'.
      WHEN :  'P_INF' .
        IF p2 IS INITIAL.
          screen-input = '0'.
        ELSE.
          screen-input = '1'.
        ENDIF.
        MODIFY SCREEN.
      WHEN 'P3'.
      WHEN :  'P_GS' .
        IF p3 IS INITIAL.
          screen-input = '0'.
        ELSE.
          screen-input = '1'.
        ENDIF.
        MODIFY SCREEN.
*        WHEN OTHERS.
    ENDCASE.
    MODIFY SCREEN.
  ENDLOOP.

*********************************************
*         At selection events & validations
*********************************************
AT SELECTION-SCREEN.
  PERFORM validate_name.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_adb.
  PERFORM p_adb_f4.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_inf.
  PERFORM p_inf_f4.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_gs.
  PERFORM p_adb_f4.

*********************************************
START-OF-SELECTION.
*********************************************
  IF p3 IS NOT INITIAL.
    p_adb = p_gs.
    PERFORM gsearch_for_hardcode_form.
  ELSE.
    PERFORM fetch_where_used_data.      "*    Fetch data
  ENDIF.

*********************************************
END-OF-SELECTION.
*********************************************
  PERFORM display.       "*      Display data.

*********************************************
*       Subroutines
*********************************************
*&---------------------------------------------------------------------*
*&      Form  display
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM display .

  PERFORM f_layout.
  PERFORM f_fieldcat.

  IF p1 IS NOT INITIAL.

    CALL FUNCTION 'REUSE_ALV_BLOCK_LIST_INIT'
      EXPORTING
        i_callback_program      = sy-repid
        i_callback_user_command = 'USER_COMMAND'.

*     print output types
    CALL FUNCTION 'REUSE_ALV_BLOCK_LIST_APPEND'
      EXPORTING
        it_fieldcat = it_fieldcat
        is_layout   = wa_layout
        i_tabname   = 'IT_TNAPR'
        it_events   = gt_xevents
        i_text      = text-0b1
      TABLES
        t_outtab    = it_tnapr.

*    print programs
    CALL FUNCTION 'REUSE_ALV_BLOCK_LIST_APPEND'
      EXPORTING
        it_fieldcat = it_fieldcat_alv2
        is_layout   = wa_layout
        i_tabname   = 'IT_OTPG'
        it_events   = gt_xevents
        i_text      = text-al2
      TABLES
        t_outtab    = it_otpg.

*  Interface Details
    CALL FUNCTION 'REUSE_ALV_BLOCK_LIST_APPEND'
      EXPORTING
        it_fieldcat = it_fieldcat_alv3
        is_layout   = wa_layout
        i_tabname   = 'IT_OUTPUT'
        it_events   = gt_xevents
        i_text      = text-al2
      TABLES
        t_outtab    = it_output.

*   PP configuration for pdf if available
    IF it_pp[] IS NOT INITIAL.
      CALL FUNCTION 'REUSE_ALV_BLOCK_LIST_APPEND'
        EXPORTING
          it_fieldcat = it_fieldcat_alv4
          is_layout   = wa_layout
          i_tabname   = 'IT_PP'
          it_events   = gt_xevents
          i_text      = text-al3
        TABLES
          t_outtab    = it_pp.
    ENDIF.

    gt_print-reserve_lines = 2.
    CALL FUNCTION 'REUSE_ALV_BLOCK_LIST_DISPLAY'
      EXPORTING
        is_print = gt_print.

  ELSEIF p2 IS NOT INITIAL.

    CALL FUNCTION 'REUSE_ALV_LIST_DISPLAY'
      EXPORTING
        i_callback_program      = syst-cprog
        is_layout               = wa_layout
        it_fieldcat             = it_fieldcat
        i_callback_user_command = 'EXE_USER_COMMAND'
      TABLES
        t_outtab                = it_output.
    IF sy-subrc <> 0.

    ENDIF.
  ENDIF.

ENDFORM.                    " display
*&---------------------------------------------------------------------*
*&      Form  f_fieldcat
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM f_fieldcat .

  IF p1 IS NOT INITIAL.
*    ALV1 : Display Output Types
    m_fieldcat   '1'   'KSCHL'   'IT_TNAPR'  text-0b1.            "" Output Types
    APPEND wa_fieldcat TO it_fieldcat.
    m_fieldcat   '2'  'KAPPL'   'IT_TNAPR'  text-0b3.             "" Application Area
    APPEND wa_fieldcat TO it_fieldcat.

*    ALV2 : Display Print programs
    m_fieldcat   '1'   'PGNAM'   'IT_OTPG'  text-0b4.       " " program name
    APPEND wa_fieldcat TO it_fieldcat_alv2.
    m_fieldcat   '2'   'RONAM'   'IT_OTPG'  text-0b5.       " " Form Routine
    APPEND wa_fieldcat TO it_fieldcat_alv2.
    m_fieldcat   '3'   'FONAM'   'IT_OTPG'  text-0b6.       " " Form Name
    APPEND wa_fieldcat TO it_fieldcat_alv2.

*  ALV3 : Display Interface Details
    m_fieldcat   '1'   'PROGRAM'   'IT_OUTPUT'  text-0b7 .         " program name
    wa_fieldcat-outputlen = 30.
    APPEND wa_fieldcat TO it_fieldcat_alv3.
    m_fieldcat   '2'   'DESCRIPTION'   'IT_OUTPUT'  text-004 .    " Description
    wa_fieldcat-outputlen = 50.
    APPEND wa_fieldcat TO it_fieldcat_alv3.

*    ALV4 : Display (if any) PP Configuration data for given adobe form
    m_fieldcat   '1'   'AUTYP'   'IT_PP'  text-0b8 .     " Order category
    APPEND wa_fieldcat TO it_fieldcat_alv4.
    m_fieldcat   '2'   'LSTID'   'IT_PP'  text-0c1 .      " List name
    APPEND wa_fieldcat TO it_fieldcat_alv4.
    m_fieldcat   '3'   'REPID'   'IT_PP'  text-0b4.       " ABAP Program Name
    APPEND wa_fieldcat TO it_fieldcat_alv4.
    m_fieldcat   '4'   'VBFORM'   'IT_PP'  text-0b5 .    " Form routine
    APPEND wa_fieldcat TO it_fieldcat_alv4.
    m_fieldcat   '5'   'FORM_PDF'   'IT_PP'  text-0b6 .  " AdobeForm Name
    APPEND wa_fieldcat TO it_fieldcat_alv4.

  ELSEIF p2 IS NOT INITIAL.

    m_fieldcat   '1'   'PROGRAM'   'IT_OUTPUT'  text-003 .
    wa_fieldcat-outputlen = 30.
    APPEND wa_fieldcat TO it_fieldcat.
    m_fieldcat   '2'   'DESCRIPTION'   'IT_OUTPUT'  text-004.
    wa_fieldcat-outputlen = 50.
    APPEND wa_fieldcat TO it_fieldcat.

  ENDIF.

ENDFORM.                    " f_fieldcat
*&---------------------------------------------------------------------*
*&      Form  f_layout
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM f_layout .

  wa_layout-colwidth_optimize = 'X'.

ENDFORM.                    " f_layout

*&---------------------------------------------------------------------*
*&      Form  FETCH_WHERE_USED_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM fetch_where_used_data .

  DATA : it_fptext TYPE TABLE OF fpcontextt ,
        wa_fptext  TYPE fpcontextt ,
        adobe_fname TYPE SFORM.

  IF p1 IS NOT INITIAL.       " search for adobe where used list
    adobe_fname = p_adb.
    SELECT * FROM tnapr INTO TABLE  it_tnapr
    WHERE fonam = adobe_fname
    OR fonam2 = adobe_fname
    OR fonam3 = adobe_fname
    OR fonam4 = adobe_fname
    OR fonam5 = adobe_fname
    or ( SFORM =   adobe_fname AND FORMTYPE = '2')
    or ( SFORM2 =   adobe_fname AND FORMTYPE2 = '2')
    or ( SFORM3 =   adobe_fname AND FORMTYPE3 = '2')
    or ( SFORM4 =   adobe_fname AND FORMTYPE4 = '2')
    or ( SFORM5 =   adobe_fname AND FORMTYPE5 = '2')
      .
    IF sy-subrc <> 0.
*      message 'No Form exist with given name' type 'E'  .
    ENDIF.
    LOOP AT it_tnapr INTO wa_tnapr.
      IF wa_tnapr-fonam = adobe_fname.
        wa_otpg-pgnam = wa_tnapr-pgnam .
        wa_otpg-ronam =  wa_tnapr-ronam .
        wa_otpg-fonam =  wa_tnapr-fonam .
        APPEND wa_otpg TO it_otpg.
      ELSEIF wa_tnapr-fonam2 = adobe_fname.
        wa_otpg-pgnam = wa_tnapr-pgnam2 .
        wa_otpg-ronam =  wa_tnapr-ronam2 .
        wa_otpg-fonam =  wa_tnapr-fonam2 .
        APPEND wa_otpg TO it_otpg.
      ELSEIF wa_tnapr-fonam3 = adobe_fname.
        wa_otpg-pgnam = wa_tnapr-pgnam3 .
        wa_otpg-ronam =  wa_tnapr-ronam3 .
        wa_otpg-fonam =  wa_tnapr-fonam3.
        APPEND wa_otpg TO it_otpg.
      ELSEIF wa_tnapr-fonam4 = adobe_fname.
        wa_otpg-pgnam = wa_tnapr-pgnam4 .
        wa_otpg-ronam =  wa_tnapr-ronam4 .
        wa_otpg-fonam =  wa_tnapr-fonam4 .
        APPEND wa_otpg TO it_otpg.
      ELSEIF wa_tnapr-fonam5 = adobe_fname.
        wa_otpg-pgnam = wa_tnapr-pgnam5 .
        wa_otpg-ronam =  wa_tnapr-ronam5 .
        wa_otpg-fonam =  wa_tnapr-fonam5 .
        APPEND wa_otpg TO it_otpg.
      ENDIF.
    ENDLOOP.

    SELECT    name   interface
    INTO CORRESPONDING FIELDS OF TABLE it_fp
    FROM fpcontext
    WHERE name EQ adobe_fname.
    IF sy-subrc = 0.
      SELECT * FROM    fpinterfacet  INTO TABLE it_fptext
      FOR ALL ENTRIES IN it_fp
      WHERE name = it_fp-interface
      AND language = sy-langu
      AND id = space.
    ENDIF.
*    * merge data in final output table
    LOOP AT it_fp INTO wa_fp.
      wa_output-program = wa_fp-interface.
      READ TABLE it_fptext INTO wa_fptext WITH KEY name = wa_fp-interface.
      wa_output-description = wa_fptext-text.
      APPEND wa_output TO it_output.
    ENDLOOP.

*  search for PP module (OPK8)
    SELECT * FROM t496f INTO TABLE it_t496f
    WHERE form_pdf EQ adobe_fname.
    IF sy-subrc = 0.
      CHECK it_t496f[] IS NOT INITIAL.
      SELECT * FROM t496r INTO TABLE it_t496r
      FOR ALL ENTRIES IN it_t496f
      WHERE   autyp  = it_t496f-autyp
      AND  rbart = it_t496f-rauart
      AND  lstid  = it_t496f-listid .

      LOOP AT it_t496f INTO wa_t496f.
        MOVE-CORRESPONDING wa_t496f TO wa_pp.
        READ TABLE it_t496r INTO wa_t496r
        WITH KEY autyp = wa_t496f-autyp
        rbart = wa_t496f-rauart
        lstid = wa_t496f-listid.
        MOVE-CORRESPONDING wa_t496r TO wa_pp.
        APPEND wa_pp TO it_pp.
      ENDLOOP.
      DELETE ADJACENT DUPLICATES FROM it_pp.

    ENDIF.

  ELSEIF p2 IS  NOT INITIAL .         " search for interface where used list

    SELECT    name   interface
    INTO CORRESPONDING FIELDS OF TABLE it_fp
    FROM fpcontext
    WHERE interface EQ p_inf.

    IF sy-subrc <> 0.
*      message 'No Form exist with given name' type 'E'  .
    ENDIF.

    CHECK it_fp IS NOT INITIAL.
    SELECT * FROM    fpcontextt  INTO TABLE it_fptext
    FOR ALL ENTRIES IN it_fp
    WHERE name = it_fp-name
    AND language = sy-langu
    AND id = space.

* merge data in final output table
    LOOP AT it_fp INTO wa_fp.
      wa_output-program = wa_fp-name.
      READ TABLE it_fptext INTO wa_fptext WITH KEY name = wa_fp-name.
      wa_output-description = wa_fptext-text.
      APPEND wa_output TO it_output.
    ENDLOOP.

  ENDIF.

ENDFORM.                    " FETCH_WHERE_USED_DATA
*&---------------------------------------------------------------------*
*&      Form  P_ADB_F4
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM p_adb_f4 .

  TYPES: BEGIN OF ty_adb,
  name TYPE  fpname,
  END OF ty_adb.

  DATA   :   it_adb TYPE TABLE OF ty_adb,
        itab_ret TYPE STANDARD TABLE OF ddshretval,
        wa_ret TYPE ddshretval.

  SELECT name FROM fpcontext
  INTO TABLE it_adb.

  SORT it_adb ASCENDING.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield        = 'NAME'
      value_org       = 'S'
    TABLES
      value_tab       = it_adb
      return_tab      = itab_ret
    EXCEPTIONS
      parameter_error = 1
      no_values_found = 2
      OTHERS          = 3.
  IF sy-subrc <> 0.
* Implement suitable error handling here
  ENDIF.

  READ TABLE itab_ret INTO wa_ret INDEX 1.
  IF sy-subrc EQ 0.
    p_adb = wa_ret-fieldval.
    p_gs =  wa_ret-fieldval.
  ENDIF.

ENDFORM.                                                    " P_ADB_F4
*&---------------------------------------------------------------------*
*&      Form  P_INF_F4
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM p_inf_f4 .

  TYPES: BEGIN OF ty_adb,
  name TYPE  fpname,
  END OF ty_adb.

  DATA   :   it_adb TYPE TABLE OF ty_adb,
        itab_ret TYPE STANDARD TABLE OF ddshretval,
        wa_ret TYPE ddshretval.

  SELECT name FROM fpinterface
  INTO TABLE it_adb.

  SORT it_adb ASCENDING.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      retfield        = 'NAME'
      value_org       = 'S'
    TABLES
      value_tab       = it_adb
      return_tab      = itab_ret
    EXCEPTIONS
      parameter_error = 1
      no_values_found = 2
      OTHERS          = 3.
  IF sy-subrc <> 0.
* Implement suitable error handling here
  ENDIF.
*
  READ TABLE itab_ret INTO wa_ret INDEX 1.
  IF sy-subrc EQ 0.
    p_inf = wa_ret-fieldval.
  ENDIF.
ENDFORM.                                                    " P_INF_F4
*&---------------------------------------------------------------------*
*&      Form  VALIDATE_NAME
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM validate_name .

  DATA : adobe_form TYPE fpname,
        interface_name TYPE fpname.

  IF p1 IS NOT INITIAL.
    CHECK p_adb IS NOT INITIAL.
    adobe_form = p_adb.

    CALL FUNCTION 'EFG_ADOBEFORM_EXISTS'
    EXPORTING
      i_adform            =  adobe_form
*     IMPORTING
*       E_DEVCLASS          =
    EXCEPTIONS
      not_qualified       = 1
      not_found           = 2
      OTHERS              = 3
      .
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
      WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.

  ELSEIF p2 IS NOT INITIAL.
    CHECK p_inf IS NOT INITIAL.
    interface_name = p_inf.

    CALL FUNCTION 'EFG_ADOBEINTF_EXISTS'
    EXPORTING
      i_adintf            = interface_name
*     IMPORTING
*       E_DEVCLASS          =
    EXCEPTIONS
      not_qualified       = 1
      not_found           = 2
      OTHERS              = 3
      .
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
      WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.

  ELSEIF p1 IS NOT INITIAL.
    CHECK p_gs IS NOT INITIAL.
    adobe_form = p_gs.

    CALL FUNCTION 'EFG_ADOBEFORM_EXISTS'
    EXPORTING
      i_adform            =  adobe_form
*     IMPORTING
*       E_DEVCLASS          =
    EXCEPTIONS
      not_qualified       = 1
      not_found           = 2
      OTHERS              = 3
      .
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
      WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.

  ENDIF.

ENDFORM.                    " VALIDATE_NAME

*&---------------------------------------------------------------------*
*&      Form  EXE_USER_COMMAND
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->UCOMM      text
*      -->SELFIELD   text
*----------------------------------------------------------------------*
FORM exe_user_command USING ucomm     LIKE sy-ucomm
      selfield  TYPE slis_selfield.

  CASE ucomm.
**Double-Click on any of the fields.
    WHEN '&IC1'.
      READ TABLE it_output INTO wa_output INDEX selfield-tabindex.
      SET PARAMETER ID 'FPWBFORM' FIELD wa_output-program.
      CALL TRANSACTION 'SFP' AND SKIP FIRST SCREEN.
  ENDCASE.

ENDFORM.                    "EXE_USER_COMMAND
*&---------------------------------------------------------------------*
*&      Form  GSEARCH_FOR_HARDCODE_FORM
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM gsearch_for_hardcode_form .

  SET PARAMETER ID 'UFI' FIELD p_gs.
  CALL TRANSACTION 'EWK1' ." AND SKIP FIRST SCREEN.

ENDFORM.                    " GSEARCH_FOR_HARDCODE_FORM

*Text symbol text£º
*001:Where-Used List for Adobe Form
*002:Where-Used List for Interface
*003:Adobe form
*004:Short description
*005:Search as Hard code text in programs
*0B1:Output Type
*0B2:Message transmission medium
*0B3:Application Area
*0B4:Program name
*0B5:Form Routine
*0B6:Form Name
*0B7:Interface Name
*0B8:Order category
*0B9:Ref. order typ
*0C1:List name
*AL2:Print Program Details

*AL3:PP Config Forms
*Selection text£º
*P1:        Adobe Form
*P2:        Interface
*P3:        Global Search
*P_ADB:        Adobe Form Name
*P_GS:        Adobe Form Name
*P_INF:        Interface
