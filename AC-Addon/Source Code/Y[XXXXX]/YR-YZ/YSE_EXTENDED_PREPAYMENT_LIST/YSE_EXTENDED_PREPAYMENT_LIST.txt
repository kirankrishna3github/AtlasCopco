************************************************************************
* Report  : YSE_EXTENDED_PREPAYMENT_LIST                               *
*----------------------------------------------------------------------*
* Author      : Luc Mertens  USG Innotiv                               *
* Created     : 27.04.2010                                             *
*Development : CR1336                                                  *
* Corr.num.   : CD1K956306                                             *
************************************************************************
* Description : Extension of program YSE_CS_OTH_PREPAYMENT_LIST        *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME                  |CORR.NUMBER| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 |22.10.2010|L. Mertens  USG Innotiv|CD1K960382 | CR1700      *
* MOD-002 |20.06.2011|G. Rutten|CD1K965793 | CR1821                    *
************************************************************************
REPORT yse_extended_prepayment_list NO STANDARD PAGE HEADING.

*----------------------------------------------------------------------*
* Data declarations                                                    *
*----------------------------------------------------------------------*

*--- Dictionary tables
TABLES: bsid.                          " Accounting Document Segment
TABLES: vbak.                          " Sales Document: Header Data

*--- Type pools
TYPE-POOLS slis.

*--- Internal tables
DATA: it_bsid      LIKE bsid             OCCURS 0 WITH HEADER LINE.
DATA: it_vbak      LIKE vbak             OCCURS 0 WITH HEADER LINE.
DATA: it_vbak2     LIKE vbak             OCCURS 0 WITH HEADER LINE.

* Begin of insert MOD-002
DATA: lv_netwr_tot TYPE vbak-netwr,
      lv_fwbas_tot TYPE bseg-fwbas,
      lv_vbeln TYPE vbak-vbeln.
* End of insert MOD-002

DATA: BEGIN OF i_logtab OCCURS 0,
        cod(1)     TYPE c,
        msg(119)   TYPE c,
      END OF i_logtab.

DATA: BEGIN OF it_data OCCURS 0,
        sel          TYPE c,
        bukrs        LIKE bsid-bukrs,
        kunnr        LIKE bsid-kunnr,
        xref2        LIKE bsid-xref2,
        belnr        LIKE bsid-belnr,
        gjahr        LIKE bsid-gjahr,
        dmbtr        LIKE bsid-dmbtr,
        wrbtr        LIKE bsid-wrbtr,
        mwsts        LIKE bsid-mwsts,
        wmwst        LIKE bsid-wmwst,
        fwbas        LIKE bseg-fwbas,
        zfbdt        LIKE bsid-zfbdt,
        vbeln        LIKE vbak-vbeln,
        erdat        LIKE vbak-erdat,
        ernam        LIKE vbak-ernam,
        angdt        LIKE vbak-angdt,
        bnddt        LIKE vbak-bnddt,
        netwr        LIKE vbak-netwr,
        waerk        LIKE vbak-waerk,
        vkorg        LIKE vbak-vkorg,
        vtweg        LIKE vbak-vtweg,
        spart        LIKE vbak-spart,
        vkgrp        LIKE vbak-vkgrp,
        vkbur        LIKE vbak-vkbur,
        cust_po      LIKE vbkd-bstkd,
        pctpaid      LIKE anla-antei,
        salesord     LIKE vbak-vbeln,
        contract     LIKE vbak-vbeln,
        cmgst        LIKE vbuk-cmgst,
        spstg(7)     TYPE c,
        zterm        LIKE vbkd-zterm,
        aufnr        LIKE vbak-aufnr,
        vaplz        LIKE viord-vaplz,
        ingpr        LIKE viord-ingpr,
        sttxt        LIKE bsvx-sttxt,
        auart        LIKE vbak-auart.
DATA: END OF it_data.

*--- Variables
DATA: gv_xref2       LIKE bsid-xref2.
DATA: x_repid        LIKE sy-repid.
DATA: wa_bsid        TYPE bsid.
DATA: gv_retcd       LIKE sy-subrc.
DATA: gv_vbeln       LIKE vbak-vbeln.
DATA: sv_vbeln       LIKE vbak-vbeln.
DATA: ls_events      TYPE slis_alv_event.
DATA: it_events      TYPE slis_t_event.
DATA: gv_log         TYPE c.
DATA: gv_handle      TYPE balloghndl.
DATA: ls_msg         TYPE bal_s_msg.
DATA: gv_msg(120)    TYPE c.
DATA: wa_errtab      TYPE bdcmsgcoll.
DATA: gv_ukurs       TYPE ukurs_curr.
DATA: gv_mestx       LIKE t100-text.
DATA: gv_bldat       TYPE bldat.
DATA: i_bdcdata      LIKE bdcdata OCCURS 0 WITH HEADER LINE,
      struct_bdcdata TYPE bdcdata.
DATA: gt_err         LIKE bdcmsgcoll OCCURS 0 WITH HEADER LINE.
DATA: gv_so          LIKE vbak-vbeln.
DATA: gv_mode(1)     TYPE c VALUE 'N'.
DATA: gv_objnr       TYPE j_objnr.
DATA: gv_spstg       TYPE vbuk-spstg.
DATA: gv_first(1)    TYPE c,
      gv_posnr       TYPE vbap-posnr.

*--- Constants
CONSTANTS : c_q(1)       TYPE c            VALUE 'Q'.
CONSTANTS : c_x(1)       TYPE c            VALUE 'X'.
CONSTANTS : c_0(1)       TYPE c            VALUE '0'.
CONSTANTS : c_aktyp(2)   TYPE c            VALUE '03'.
CONSTANTS : c_a(1)       TYPE c            VALUE 'A'.

*----------------------------------------------------------------------*
* Data declarations concerning ALV-output                              *
*----------------------------------------------------------------------*

*--- Internal tables
DATA: it_fieldcat       TYPE slis_t_fieldcat_alv.
DATA: lt_fieldcat       TYPE slis_t_fieldcat_alv.

*--- Structures
DATA: gs_layout         TYPE slis_layout_alv.
DATA: ls_fieldcat       TYPE slis_fieldcat_alv.

*--- Variables
DATA: h_exit            TYPE c.

*--- Variables with default value
DATA: g_user_command    TYPE slis_formname  VALUE 'USER_COMMAND'.

* Begin of insert MOD-002
DATA: gv_calledfrom(30) TYPE C.
* End of insert MOD-002

*----------------------------------------------------------------------*
* Selection screen                                                     *
*----------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK b01 WITH FRAME.

SELECT-OPTIONS: s_vbtyp  FOR  vbak-vbtyp OBLIGATORY,
                s_auart  FOR  vbak-auart NO INTERVALS.
SELECT-OPTIONS: s_kunnr  FOR  bsid-kunnr.
SELECT-OPTIONS: s_aufnr  FOR  vbak-aufnr.
SELECT-OPTIONS: s_vbeln  FOR  vbak-vbeln.
SELECT-OPTIONS: s_erdat  FOR  vbak-erdat.
SELECT-OPTIONS: s_ernam  FOR  vbak-ernam.
SELECT-OPTIONS: s_angdt  FOR  vbak-angdt.
SELECT-OPTIONS: s_bnddt  FOR  vbak-bnddt.
SELECT-OPTIONS: s_netwr  FOR  vbak-netwr.
SELECT-OPTIONS: s_waerk  FOR  vbak-waerk.
SELECT-OPTIONS: s_vkorg  FOR  vbak-vkorg OBLIGATORY.
SELECT-OPTIONS: s_vtweg  FOR  vbak-vtweg OBLIGATORY.
SELECT-OPTIONS: s_spart  FOR  vbak-spart OBLIGATORY.
SELECT-OPTIONS: s_vkgrp  FOR  vbak-vkgrp.
SELECT-OPTIONS: s_vkbur  FOR  vbak-vkbur.

SELECTION-SCREEN END OF BLOCK b01.

* authority check
AUTHORITY-CHECK OBJECT 'V_VBAK_VKO'
       ID 'VKORG' FIELD s_vkorg-low
       ID 'VTWEG' FIELD s_vtweg-low
       ID 'SPART' FIELD s_spart-low
       ID 'ACTVT' FIELD c_aktyp.

IF sy-subrc <> 0.
  MESSAGE e001(00) WITH text-e02 s_vkorg-low.
ENDIF.
*----------------------------------------------------------------------*
* Initialization of the selection screen                               *
*----------------------------------------------------------------------*
INITIALIZATION.

AT SELECTION-SCREEN OUTPUT.

************************************************************************
* START MAIN PROGRAM                                                   *
************************************************************************
START-OF-SELECTION.

  PERFORM initialization.

  PERFORM select_data.

  PERFORM process_data.

  PERFORM display_data.

************************************************************************
* SUBROUTINES  LEVEL 01                                                *
************************************************************************
*----------------------------------------------------------------------*
*   Form  INITIALIZE_DATA                                              *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM initialization.

* Clear of internal tables also necessary at routine 'reset_data'
  CLEAR: it_bsid, it_vbak, it_data.
  REFRESH: it_bsid, it_vbak, it_data.

  DATA: ls_log    TYPE bal_s_log.

  ls_log-object = 'YSE_EXT_PREPAYM_LIST'.
* Begin of insert MOD-002
  gv_calledfrom = ls_log-object.
  EXPORT  gv_calledfrom TO MEMORY ID 'PROG_NAME'.
* End of insert MOD-002

* Create application log
  CALL FUNCTION 'BAL_LOG_CREATE'
    EXPORTING
      i_s_log                 = ls_log
    IMPORTING
      e_log_handle            = gv_handle
    EXCEPTIONS
      log_header_inconsistent = 1
      OTHERS                  = 2.

  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " INITIALIZE_DATA


*----------------------------------------------------------------------*
*   Form  SELECT_DATA                                                  *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM select_data.

* Select sales documents
  SELECT * FROM vbak INTO TABLE it_vbak
                       WHERE vbeln IN s_vbeln
                         AND auart IN s_auart
                         AND kunnr IN s_kunnr
                         AND erdat IN s_erdat
                         AND ernam IN s_ernam
                         AND angdt IN s_angdt
                         AND bnddt IN s_bnddt
                         AND netwr IN s_netwr
                         AND waerk IN s_waerk
                         AND vkorg IN s_vkorg
                         AND vtweg IN s_vtweg
                         AND spart IN s_spart
                         AND vkgrp IN s_vkgrp
                         AND vkbur IN s_vkbur
                         AND vbtyp IN s_vbtyp.

  IF it_vbak[] IS INITIAL.
    MESSAGE s001(00) WITH text-e03.
    EXIT.
  ENDIF.

* added on 31.08.2010 spec V1.2 (multiple SDI)
  LOOP AT it_vbak WHERE vbtyp = 'C'
                    AND vtweg = '11'
                    AND auart = 'ZO01'.
    gv_first = 'X'.
    SELECT posnr netwr
      INTO (it_vbak-ps_psp_pnr, it_vbak-netwr)
      FROM vbap WHERE vbeln = it_vbak-vbeln.

      IF gv_first = 'X'.
        MODIFY it_vbak TRANSPORTING ps_psp_pnr netwr.
        CLEAR gv_first.
      ELSE.
        MOVE it_vbak TO it_vbak2.
        APPEND it_vbak2.
      ENDIF.
    ENDSELECT.
  ENDLOOP.
*
  LOOP AT it_vbak2.
    MOVE it_vbak2 TO it_vbak.
    APPEND it_vbak.
  ENDLOOP.
  SORT it_vbak.
  FREE it_vbak2.

* Difference to check VBEL2 between quotations and the other sales docs

  LOOP AT it_vbak.

    SHIFT it_vbak-vbeln LEFT DELETING LEADING '0'.
    IF it_vbak-vbtyp = 'B'.                          " quotations
      CONCATENATE 'Q' it_vbak-vbeln INTO gv_xref2.
      SELECT * FROM bsid
          APPENDING CORRESPONDING FIELDS OF TABLE it_bsid
            WHERE bukrs EQ it_vbak-bukrs_vf
              AND kunnr EQ it_vbak-kunnr
              AND umsks EQ c_a
              AND umskz EQ c_a
* begin of change MOD-001
*              and blart eq 'DZ'
              AND blart IN ('DZ', 'ZC')
* end of change MOD-001
              AND xref2 EQ gv_xref2.
    ELSE.
      gv_xref2 = it_vbak-vbeln.
      SELECT * FROM bsid
          APPENDING CORRESPONDING FIELDS OF TABLE it_bsid
            WHERE bukrs EQ it_vbak-bukrs_vf
              AND kunnr EQ it_vbak-kunnr
              AND umsks EQ c_a
              AND umskz EQ c_a
              AND blart EQ 'RU'
              AND xref2 EQ gv_xref2.
    ENDIF.

  ENDLOOP.


  SORT it_bsid.
* Begin of insert MOD-002
  DELETE ADJACENT DUPLICATES FROM it_bsid.
* End of insert MOD-002
ENDFORM.                    " SELECT_DATA


*----------------------------------------------------------------------*
*   Form  PROCESS_DATA                                                 *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM process_data.



  LOOP AT it_vbak.
    sv_vbeln = it_vbak-vbeln.
    SHIFT it_vbak-vbeln LEFT DELETING LEADING '0'.

    IF it_vbak-vbtyp = 'B'.
      CONCATENATE 'Q' it_vbak-vbeln INTO gv_xref2.
    ELSE.
      gv_xref2 = it_vbak-vbeln.
    ENDIF.

    MOVE-CORRESPONDING it_vbak TO it_data.
    MOVE sv_vbeln TO it_data-vbeln.
    SELECT SINGLE bstkd zterm
      INTO (it_data-cust_po, it_data-zterm)
      FROM vbkd WHERE vbeln = sv_vbeln
                  AND posnr = 0.

    CLEAR gv_spstg.
    SELECT SINGLE cmgst spstg
       INTO (it_data-cmgst, gv_spstg)
       FROM vbuk WHERE vbeln = sv_vbeln.

    IF sy-subrc = 0 AND NOT gv_spstg IS INITIAL.
      it_data-spstg = 'Blocked'.
    ENDIF.

    CASE it_vbak-vbtyp.
      WHEN 'G'.
        it_data-contract = sv_vbeln.
        CLEAR it_data-vbeln.

        SELECT SINGLE vbegdat venddat
          INTO (it_data-angdt, it_data-bnddt)
          FROM veda WHERE vbeln = sv_vbeln
                    AND   vposn = '000000'.
      WHEN 'C'.
        it_data-salesord = sv_vbeln.
        CLEAR it_data-vbeln.

        IF it_vbak-auart = 'ZO01'.
          IF it_vbak-vtweg = '11'.
            gv_posnr = it_vbak-ps_psp_pnr.

            SELECT SINGLE aufnr
              INTO it_data-aufnr
              FROM viord WHERE kdauf = sv_vbeln
                           AND kdpos = gv_posnr.
          ELSE.
            SELECT SINGLE aufnr
              INTO it_data-aufnr
              FROM viord WHERE kdauf = sv_vbeln.
          ENDIF.
        ENDIF.

*...... Eliminate service order numbers
*         - which are not according to SEO from selection screen
        IF it_data-aufnr NOT IN s_aufnr.
          CONTINUE.
        ENDIF.
      WHEN 'B'.
        SELECT SINGLE vbeln INTO it_data-salesord
          FROM vbak WHERE vgbel EQ sv_vbeln.
    ENDCASE.

*.. Get service order and status text
    IF NOT it_data-aufnr IS INITIAL.
      SELECT SINGLE vaplz ingpr
         INTO (it_data-vaplz, it_data-ingpr)
         FROM viord WHERE aufnr = it_data-aufnr.

      CONCATENATE 'OR' it_data-aufnr INTO gv_objnr.
      PERFORM get_status USING gv_objnr.
    ENDIF.

* Begin of insert MOD-002
* Get the total paid value from bsid

    CLEAR lv_fwbas_tot.

    LOOP AT it_bsid INTO wa_bsid WHERE xref2 = gv_xref2.
      IF it_data-waerk <> 'RUB'.
        CLEAR: gv_bldat, gv_ukurs.
        SELECT SINGLE bldat INTO gv_bldat FROM bkpf
         WHERE bukrs = wa_bsid-bukrs
           AND belnr = wa_bsid-belnr
           AND gjahr = wa_bsid-gjahr.
        gv_bldat = 99999999 - gv_bldat .

        SELECT SINGLE ukurs  FROM tcurr INTO gv_ukurs
           WHERE kurst = 'DRU'
             AND tcurr = 'RUB'
             AND fcurr = it_data-waerk
             AND gdatu >= gv_bldat.
        lv_fwbas_tot = lv_fwbas_tot +
        ( ( wa_bsid-dmbtr - wa_bsid-mwsts ) / gv_ukurs ).
      ELSE.
        lv_fwbas_tot = lv_fwbas_tot +
        ( wa_bsid-dmbtr - wa_bsid-mwsts ).
      ENDIF.
    ENDLOOP.

* get the total netvalue
    CLEAR: lv_netwr_tot, lv_vbeln.


    IF gv_xref2(1) = 'Q'.
      lv_vbeln = gv_xref2+1(9).
    ELSE.
      lv_vbeln = gv_xref2.
    ENDIF.

    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        input  = lv_vbeln
      IMPORTING
        output = lv_vbeln.

    SELECT SINGLE netwr FROM vbak INTO lv_netwr_tot
      WHERE vbeln = lv_vbeln.
* End of insert MOD-002

    LOOP AT it_bsid INTO wa_bsid WHERE xref2 = gv_xref2.
      MOVE wa_bsid-bukrs TO it_data-bukrs.
      MOVE wa_bsid-kunnr TO it_data-kunnr.
      MOVE wa_bsid-xref2 TO it_data-xref2.
      MOVE wa_bsid-belnr TO it_data-belnr.
      MOVE wa_bsid-gjahr TO it_data-gjahr.
      MOVE wa_bsid-dmbtr TO it_data-dmbtr.
      MOVE wa_bsid-wrbtr TO it_data-wrbtr.
      MOVE wa_bsid-mwsts TO it_data-mwsts.
      MOVE wa_bsid-wmwst TO it_data-wmwst.
      MOVE wa_bsid-zfbdt TO it_data-zfbdt.


* Begin of change MOD-002
*      IF NOT it_data-netwr IS INITIAL.
      IF NOT lv_netwr_tot IS INITIAL.
* End of change MOD-002
        CATCH SYSTEM-EXCEPTIONS arithmetic_errors = 1.
* Begin of delete MOD-002
          it_data-fwbas = it_data-dmbtr - it_data-mwsts.
*          IF it_data-waerk <> 'RUB'.
*            SELECT SINGLE bldat INTO gv_bldat FROM bkpf
*             WHERE bukrs = it_data-bukrs
*               AND belnr = it_data-belnr
*               AND gjahr = it_data-gjahr.
*            gv_bldat = 99999999 - gv_bldat .
*
*            SELECT SINGLE ukurs  FROM tcurr INTO gv_ukurs
*               WHERE kurst = 'DRU'
*                 AND tcurr = 'RUB'
*                 AND fcurr = it_data-waerk
*                 AND gdatu >= gv_bldat.
*            it_data-pctpaid = it_data-fwbas / gv_ukurs /
*                              it_data-netwr * 100.
*
*          ELSE.
* End of delete MOD-002
* Begin of change MOD-002
*            it_data-pctpaid = it_data-fwbas / it_data-netwr * 100.
          it_data-pctpaid = lv_fwbas_tot / lv_netwr_tot * 100.
* End of change MOD-002

* Begin of delete MOD-002
*        ENDIF.
* End of delete MOD-002
        ENDCATCH.
      ENDIF.

      APPEND it_data.
    ENDLOOP.

    IF sy-subrc = 0.
      CLEAR it_data.
    ELSE.
      APPEND it_data.
      CLEAR  it_data.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " PROCESS_DATA

*----------------------------------------------------------------------*
*   Form  DISPLAY_DATA                                                 *
*----------------------------------------------------------------------*
*   .....                                                              *
*----------------------------------------------------------------------*
FORM display_data.

  PERFORM fill_field_catalog.
  PERFORM change_catalog.
  PERFORM alv_output.

ENDFORM.                    " DISPLAY_DATA

************************************************************************
* SUBROUTINES  LEVEL 02                                                *
************************************************************************


*----------------------------------------------------------------------*
*       Form  FILL_FIELD_CATALOG                                       *
*----------------------------------------------------------------------*
*       text                                                           *
*----------------------------------------------------------------------*
FORM fill_field_catalog.

  x_repid = sy-repid.
  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
       EXPORTING
            i_program_name         = x_repid
            i_internal_tabname     = 'IT_DATA'
*           i_structure_name       =
*           i_client_never_display = 'X'
            i_inclname             = x_repid
*           i_bypassing_buffer     =
*           i_buffer_active        =
       CHANGING
            ct_fieldcat            = it_fieldcat
       EXCEPTIONS
            inconsistent_interface = 1
            program_error          = 2
            OTHERS                 = 3.

  IF sy-subrc <> 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  LOOP AT it_fieldcat INTO ls_fieldcat.
    CASE ls_fieldcat-fieldname.
      WHEN 'VBELN'.
        ls_fieldcat-seltext_l = text-t02.
        ls_fieldcat-seltext_m = text-t02.
        ls_fieldcat-seltext_s = text-t02.
        ls_fieldcat-reptext_ddic = text-t02.
        MODIFY it_fieldcat FROM ls_fieldcat.
      WHEN 'PCTPAID'.
        ls_fieldcat-seltext_l = text-t03.
        ls_fieldcat-seltext_m = text-t03.
        ls_fieldcat-seltext_s = text-t03.
        ls_fieldcat-reptext_ddic = text-t03.
        MODIFY it_fieldcat FROM ls_fieldcat.
      WHEN 'SALESORD'.
        ls_fieldcat-seltext_l = text-t04.
        ls_fieldcat-seltext_m = text-t04.
        ls_fieldcat-seltext_s = text-t04.
        ls_fieldcat-reptext_ddic = text-t04.
        MODIFY it_fieldcat FROM ls_fieldcat.
      WHEN 'CONTRACT'.
        ls_fieldcat-seltext_l = text-t05.
        ls_fieldcat-seltext_m = text-t05.
        ls_fieldcat-seltext_s = text-t05.
        ls_fieldcat-reptext_ddic = text-t05.
        MODIFY it_fieldcat FROM ls_fieldcat.
      WHEN 'AUFNR'.
        ls_fieldcat-seltext_l = text-t06.
        ls_fieldcat-seltext_m = text-t06.
        ls_fieldcat-seltext_s = text-t06.
        ls_fieldcat-reptext_ddic = text-t06.
        MODIFY it_fieldcat FROM ls_fieldcat.
      WHEN 'SPSTG'.
        ls_fieldcat-seltext_l = text-t07.
        ls_fieldcat-seltext_m = text-t07.
        ls_fieldcat-seltext_s = text-t07.
        ls_fieldcat-reptext_ddic = text-t07.
        MODIFY it_fieldcat FROM ls_fieldcat.
    ENDCASE.

  ENDLOOP.

ENDFORM.                    " FILL_FIELD_CATALOG

*----------------------------------------------------------------------*
*       Form  CHANGE_CATALOG                                           *
*----------------------------------------------------------------------*
*       text                                                           *
*----------------------------------------------------------------------*
FORM change_catalog.

  CLEAR: ls_fieldcat.
  CLEAR: lt_fieldcat.
  REFRESH: lt_fieldcat.

  LOOP AT it_fieldcat INTO ls_fieldcat.
    CASE ls_fieldcat-fieldname.
      WHEN 'BELNR'.
        ls_fieldcat-hotspot = 'X'.
        ls_fieldcat-key = 'X'.
      WHEN 'VBELN'.
        ls_fieldcat-hotspot = 'X'.
        ls_fieldcat-key = 'X'.
      WHEN 'SALESORD'.
        ls_fieldcat-hotspot = 'X'.
        ls_fieldcat-key = 'X'.
      WHEN 'CONTRACT'.
        ls_fieldcat-hotspot = 'X'.
        ls_fieldcat-key = 'X'.
      WHEN 'AUFNR'.
        ls_fieldcat-hotspot = 'X'.
        ls_fieldcat-key = 'X'.
      WHEN OTHERS.
        CLEAR ls_fieldcat-key.
    ENDCASE.
    MODIFY it_fieldcat FROM ls_fieldcat.
  ENDLOOP.

*.. Fill events
  REFRESH it_events.
  ls_events-name = 'PF_STATUS_SET'.
  ls_events-form = 'PF_STATUS_SET_PREP'.
  APPEND ls_events TO it_events.
  ls_events-form = ls_events-name = 'END_OF_LIST'.
  APPEND ls_events TO it_events.

  gs_layout-colwidth_optimize   = 'X'.
  gs_layout-confirmation_prompt = 'X'.
  gs_layout-detail_titlebar     = 'PREPAYMENT LIST'.
  gs_layout-box_fieldname       = 'SEL'.


ENDFORM.                    " CHANGE_CATALOG


*----------------------------------------------------------------------*
*       Form  ALV_OUTPUT                                               *
*----------------------------------------------------------------------*
*       text                                                           *
*----------------------------------------------------------------------*
FORM alv_output.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
       EXPORTING
*           i_interface_check           = ' '
*           i_bypassing_buffer          =
*           i_buffer_active             = ' '
            i_callback_program          = 'YSE_EXTENDED_PREPAYMENT_LIST'
*           i_callback_pf_status_set    = ' '
            i_callback_user_command     = g_user_command
*           i_callback_top_of_page      = ' '
*           i_callback_html_top_of_page = ' '
*           i_callback_html_end_of_list = ' '
*           i_structure_name            =
*           i_background_id             = ' '
            i_grid_title                = text-t01
*           i_grid_settings             =
            is_layout                   = gs_layout
            it_fieldcat                 = it_fieldcat
*           it_excluding                =
*           it_special_groups           =
*           it_sort                     =
*           it_filter                   =
*           is_sel_hide                 =
            i_default                   = 'X'
            i_save                      = 'U'
*           is_variant                  = gs_sd_alv_variant
            it_events                   = it_events
*           it_event_exit               =
*           is_print                    =
*           is_reprep_id                =
            i_screen_start_column       = 0
            i_screen_start_line         = 0
            i_screen_end_column         = 0
            i_screen_end_line           = 0
*           it_alv_graphics             =
*           it_add_fieldcat             =
*           it_hyperlink                =
*      importing
*           e_exit_caused_by_caller     =
*           es_exit_caused_by_user      =
       TABLES
            t_outtab                    = it_data
       EXCEPTIONS
            program_error               = 1
            OTHERS                      = 2.

  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_OUTPUT



************************************************************************
* SUBROUTINES  LEVEL 03                                                *
************************************************************************

*------------------------------------------------------------------*
*   Form  USER_COMMAND                                             *
*------------------------------------------------------------------*
*   --> R_UCOMM                                                    *
*   --> RS_SELFIELD                                                *
*------------------------------------------------------------------*
FORM user_command USING ucomm    LIKE sy-ucomm
                        selfield TYPE slis_selfield.

*-- Check function code
  CASE ucomm.
    WHEN '&IC1'.
      IF NOT selfield-value IS INITIAL.
        CASE selfield-fieldname.
          WHEN 'BELNR'.
            SET PARAMETER ID 'BLN' FIELD selfield-value.
            CALL TRANSACTION 'FB03' AND SKIP FIRST SCREEN.
          WHEN 'VBELN'.
            SET PARAMETER ID 'AGN' FIELD selfield-value.
            CALL TRANSACTION 'VA23' AND SKIP FIRST SCREEN.
          WHEN 'SALESORD'.
            SET PARAMETER ID 'AUN' FIELD selfield-value.
            CALL TRANSACTION 'VA03' AND SKIP FIRST SCREEN.
          WHEN 'CONTRACT'.
            SET PARAMETER ID 'KTN' FIELD selfield-value.
            CALL TRANSACTION 'VA43' AND SKIP FIRST SCREEN.
          WHEN 'AUFNR'.
            SET PARAMETER ID 'ANR' FIELD selfield-value.
            CALL TRANSACTION 'IW32' AND SKIP FIRST SCREEN.

        ENDCASE.
      ENDIF.
    WHEN 'CREA'.
      gv_mode = 'N'.
      LOOP AT it_data WHERE sel = c_x.
        PERFORM create_so CHANGING gv_retcd.
        IF gv_retcd <> 0.
          MESSAGE e001(00) WITH text-e01 it_data-vbeln INTO gv_msg.
          PERFORM add_message_to_log .
          PERFORM get_trans_messages TABLES gt_err.
        ELSE.
          PERFORM get_trans_messages TABLES gt_err.
          GET PARAMETER ID 'AUN' FIELD gv_so.
          MESSAGE i001(00) WITH text-i01 gv_so INTO gv_msg.
          PERFORM add_message_to_log .
        ENDIF.
      ENDLOOP.
    WHEN 'LOG'.
      PERFORM display_log.
    WHEN 'REFR'.
      PERFORM initialization.
      PERFORM select_data.
      PERFORM process_data.
      selfield-refresh = c_x.
  ENDCASE.

ENDFORM.                    " USER_COMMAND

*&---------------------------------------------------------------------*
*&      Form  pf_status_set_prep
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->RT_EXTAB   text
*----------------------------------------------------------------------*
FORM pf_status_set_prep USING rt_extab TYPE slis_t_extab.

  SET PF-STATUS 'YSE_PREP'.

ENDFORM.                    "pf_status_set_cont


*&---------------------------------------------------------------------*
*&      Form  CREATE_SO
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_RETCD    text
*----------------------------------------------------------------------*
FORM create_so  CHANGING p_retcd.

  DATA: lv_auart TYPE auart.

  REFRESH: i_bdcdata,
           gt_err.

* first screen
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
            USING    'SAPMV45A'  '0101'  'X'  ''   ''
            CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  IF it_data-auart = 'ZQ01'.
    lv_auart = 'ZO01'.
  ELSEIF it_data-auart = 'ZQT1'.
    lv_auart = 'ZOR'.
  ENDIF.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    ''  ''  ''  'VBAK-AUART' lv_auart
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    ''  ''  ''  'VBAK-VKORG' it_data-vkorg
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    ''  ''  ''  'VBAK-VTWEG' it_data-vtweg
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    ''  ''  ''  'VBAK-SPART' it_data-spart
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  ''  ''  'VBAK-VKBUR' it_data-vkbur
        CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  ''  ''  'VBAK-VKGRP' it_data-vkgrp
        CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_OKCODE'  'COPY'
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    'SAPLV45C'  '0100'  'X'  ''   ''
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    ''  ''  ''  'LV45C-VBELN' it_data-vbeln
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_OKCODE'  '=UEBR'
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    'SAPMV45A'  '4001'  'X'  ''   ''
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
         USING    ''  ''  ''  'VBKD-BSTKD'  it_data-cust_po
  CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  ''  'BDC_OKCODE'  '=KBUC'
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    'SAPMV45A'  '4002'  'X'  ''   ''
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  WRITE it_data-vbeln TO gv_vbeln NO-ZERO.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
         USING    ''  ''  ''  'VBAK-XBLNR'  gv_vbeln
  CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
           USING    ''  ''  ''  'BDC_OKCODE'  'SICH'
           CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  CALL TRANSACTION 'VA01' USING i_bdcdata
           MODE gv_mode UPDATE 'S' MESSAGES INTO gt_err.

  p_retcd = sy-subrc.

ENDFORM.                    " CREATE_SO

*&---------------------------------------------------------------------*
*&      Form  add_message_to_tab
*&---------------------------------------------------------------------*
*       Add messages to log
*----------------------------------------------------------------------*
FORM add_message_to_tab USING p_msg p_cod.

  i_logtab-msg = p_msg.
  i_logtab-cod = p_cod.
  APPEND i_logtab.
  CLEAR i_logtab.

ENDFORM.                    " add_message_to_tab

*&---------------------------------------------------------------------*
*&      Form  GET_STATUS
*&---------------------------------------------------------------------*
*       get status text
*----------------------------------------------------------------------*
*      -->P_OBJNR  object number                                       *
*----------------------------------------------------------------------*
FORM get_status USING    p_objnr.

  DATA: sttxt LIKE rihafvr-sttxt.      " system status

  CALL FUNCTION 'STATUS_TEXT_EDIT'
    EXPORTING
      flg_user_stat    = 'X'
      objnr            = p_objnr
      spras            = sy-langu
    IMPORTING
      line             = sttxt
    EXCEPTIONS
      object_not_found = 1
      OTHERS           = 2.

  MOVE sttxt TO it_data-sttxt.

ENDFORM.                               " GET_STATUS

*&---------------------------------------------------------------------*
*&      Form  get_trans_messages
*&---------------------------------------------------------------------*
*       Get the errormessages from the performed transaction
*----------------------------------------------------------------------*
*  -->  p_errtab    Table with errormessages
*----------------------------------------------------------------------*
FORM get_trans_messages TABLES gt_err.

  DATA : wa_err TYPE bdcmsgcoll.
  LOOP AT gt_err INTO wa_err.
    IF wa_err-msgtyp = 'E' OR wa_err-msgtyp = 'A'.

      CALL FUNCTION 'RH_MESSAGE_GET'
        EXPORTING
*       SPRSL                   = SY-LANGU
          arbgb                   = wa_err-msgid
          msgnr                   = wa_err-msgnr
          msgv1                   = wa_err-msgv1
          msgv2                   = wa_err-msgv2
          msgv3                   = wa_err-msgv3
          msgv4                   = wa_err-msgv4
        IMPORTING
          msgtext                 = gv_mestx
        EXCEPTIONS
          message_not_found       = 1
          OTHERS                  = 2.

      IF sy-subrc = 0.
        ls_msg-msgty = wa_err-msgtyp.
        ls_msg-msgid = wa_err-msgid.
        ls_msg-msgno = wa_err-msgnr.
        ls_msg-msgv1 = wa_err-msgv1.
        ls_msg-msgv2 = wa_err-msgv2.
        ls_msg-msgv3 = wa_err-msgv3.
        ls_msg-msgv4 = wa_err-msgv4.

        CALL FUNCTION 'BAL_LOG_MSG_ADD'
    EXPORTING
*     I_LOG_HANDLE              =
      i_s_msg                   = ls_msg
*   IMPORTING
*     E_S_MSG_HANDLE            =
*     E_MSG_WAS_LOGGED          =
*     E_MSG_WAS_DISPLAYED       =
    EXCEPTIONS
      log_not_found             = 1
      msg_inconsistent          = 2
      log_is_full               = 3
      OTHERS                    = 4
            .
        IF sy-subrc <> 0.
          MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                  WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
        ENDIF.

*.. Flag log as having entries
        IF  gv_log IS INITIAL.
          gv_log = c_x.
        ENDIF.
      ENDIF.

    ENDIF.
  ENDLOOP.

ENDFORM.                    " get_trans_messages

*&---------------------------------------------------------------------*
*&      Form  add_message_to_log
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM add_message_to_log.

  MOVE-CORRESPONDING syst TO ls_msg.
  CALL FUNCTION 'BAL_LOG_MSG_ADD'
    EXPORTING
*     I_LOG_HANDLE              =
      i_s_msg                   = ls_msg
*   IMPORTING
*     E_S_MSG_HANDLE            =
*     E_MSG_WAS_LOGGED          =
*     E_MSG_WAS_DISPLAYED       =
    EXCEPTIONS
      log_not_found             = 1
      msg_inconsistent          = 2
      log_is_full               = 3
      OTHERS                    = 4
            .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

*.. Flag log as having entries
  IF  gv_log IS INITIAL.
    gv_log = c_x.
  ENDIF.

ENDFORM.                    "add_message_to_log

*&---------------------------------------------------------------------*
*&      Form  display_log
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM display_log .

* Local variables
  DATA: ls_prof TYPE bal_s_prof.

* Reset log indicator once displayed
  CLEAR gv_log.

* Get a prepared profile
  CALL FUNCTION 'BAL_DSP_PROFILE_POPUP_GET'
    IMPORTING
      e_s_display_profile = ls_prof
    EXCEPTIONS
      OTHERS              = 1.

  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
             WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

* Use grid for display if wanted
  ls_prof-use_grid = c_x.
  ls_prof-start_row = 1.
  ls_prof-end_row = 25.
  CLEAR ls_prof-pop_adjst.

* When you use also other ALV lists in your report,
* please specify a handle to distinguish between the display
* variants of these different lists, e.g:
  ls_prof-disvariant-handle = 'LOG'.

* Call display function module
  CALL FUNCTION 'BAL_DSP_LOG_DISPLAY'
    EXPORTING
      i_s_display_profile = ls_prof
    EXCEPTIONS
      OTHERS              = 1.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
             WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " display_log

*&---------------------------------------------------------------------*
*&      Form  end_of_list
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM end_of_list .

* Output message to indicate log entries exist
  IF gv_log EQ c_x.
*.. log contains errors
    MESSAGE i001(00) WITH text-e05.
  ENDIF.

ENDFORM.                    " end_of_list

*Text symbol text£º
*E01:Creation of sales order failed for quotation :
*E02:You are not authorized for sales organisation:
*E03:No sales documents selected !
*E05:Log contains entries. Please check
*I01:Sales Order created with number :
*T01:List of Prepayments
*T02:Quotation
*T03:Pct Paid
*T04:Sales Order
*T05:Contract
*T06:Service Order

*T07:Overall block status
*Selection text£º
*S_ANGDT:        Valid from
*S_AUART:        Document Type
*S_AUFNR:        Service Order
*S_BNDDT:        Valid to
*S_ERDAT:        Created on
*S_ERNAM:D       .
*S_KUNNR:        Customer Account
*S_NETWR:        Net Value
*S_SPART:        Division
*S_VBELN:        Document Number
*S_VBTYP:        Document Category
*S_VKBUR:D       .
*S_VKGRP:D       .
*S_VKORG:D       .
*S_VTWEG:D       .
*S_WAERK:        Doc. Currency
