*&---------------------------------------------------------------------*
*& Report  YSE_ACT_CONTACT_ADDRESS_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Contact Address extraction                                   *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 26.07.2012
*  Change Request Number :
*  Transport request Nr. : CD1K972700
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                 | CD1K9.....    |             *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_act_contact_address_extr.

TABLES: knvk,
        kna1,
        knvv,
        adrc,
        adrp,
        adcp,
        adr2,
        adr6.

TYPES: BEGIN OF ty_knvk,
         kunnr       TYPE kunnr,
         parnr       TYPE parnr,
         namel       TYPE name1_gp,
         namev       TYPE namev_vp,
         anred       TYPE anred_ap,
         parla       TYPE parla,
         parge       TYPE parge,
         parvo       TYPE parvo,
         pavip       TYPE pavip,
         titel_ap    TYPE titel_ap,
         abtnr       TYPE abtnr_pa,
         abtpa       TYPE abtei_pa,
         gbdat       TYPE gbdat,
         prsnr       TYPE ad_persnum,
         adrnd       TYPE adrnd,
         adrnp_2     TYPE ad_addrnum,
       END OF ty_knvk.

TYPES: BEGIN OF ty_kna1,
         kunnr       TYPE kunnr,
         sortl       TYPE sortl,
         ktokd       TYPE ktokd,
         name1       TYPE name1_gp,
         name2       TYPE name2_gp,
       END OF ty_kna1.

TYPES: BEGIN OF ty_adrc,
         addrnumber  TYPE ad_addrnum,
         street      TYPE ad_street,
         str_suppl1  TYPE ad_strspp1,
         post_code1  TYPE ad_pstcd1,
         city1       TYPE ad_city1,
         country     TYPE land1,
       END OF ty_adrc.

TYPES: BEGIN OF ty_adrp,
         persnumber  TYPE ad_persnum,
         title_aca1  TYPE ad_title1,
       END OF ty_adrp.

TYPES: BEGIN OF ty_adcp,
         persnumber  TYPE ad_persnum,
         addrnumber  TYPE ad_addrnum,
         tel_number  TYPE ad_tlnmbr1,
         tel_extens  TYPE ad_tlxtns1,
         fax_number  TYPE ad_fxnmbr1,
         deflt_comm  TYPE ad_comm,
       END OF ty_adcp.

TYPES: BEGIN OF ty_adr2,
         addrnumber  TYPE ad_addrnum,
         persnumber  TYPE ad_persnum,
         consnumber  TYPE ad_consnum,
         tel_number  TYPE ad_tlnmbr,
       END OF ty_adr2.

TYPES: BEGIN OF ty_adr6,
         addrnumber  TYPE ad_addrnum,
         persnumber  TYPE ad_persnum,
         consnumber  TYPE ad_consnum,
         smtp_addr   TYPE ad_smtpadr,
       END OF ty_adr6.

DATA: gt_knvk   TYPE TABLE OF ty_knvk
                     WITH HEADER LINE.

DATA: gt_kna1   TYPE HASHED TABLE OF ty_kna1
                     WITH UNIQUE KEY kunnr
                     WITH HEADER LINE,
      gt_kna1i  TYPE TABLE OF ty_kna1
                     WITH HEADER LINE.

DATA: gt_adrc   TYPE HASHED TABLE OF ty_adrc
                     WITH UNIQUE KEY addrnumber
                     WITH HEADER LINE,
      gt_adrci  TYPE TABLE OF ty_adrc
                     WITH HEADER LINE.

DATA: gt_adrp   TYPE HASHED TABLE OF ty_adrp
                     WITH UNIQUE KEY persnumber
                     WITH HEADER LINE.

DATA: gt_adcp   TYPE HASHED TABLE OF ty_adcp
                     WITH UNIQUE KEY persnumber
                     WITH HEADER LINE,
      gt_adcpi  TYPE TABLE OF ty_adcp
                     WITH HEADER LINE.

DATA: gt_adr2   TYPE HASHED TABLE OF ty_adr2
                     WITH UNIQUE KEY addrnumber persnumber
                     WITH HEADER LINE,
      gt_adr2i  TYPE TABLE OF ty_adr2
                     WITH HEADER LINE.

DATA: gt_adr6   TYPE HASHED TABLE OF ty_adr6
                     WITH UNIQUE KEY addrnumber persnumber
                     WITH HEADER LINE,
      gt_adr6i  TYPE TABLE OF ty_adr6
                     WITH HEADER LINE.

DATA: BEGIN OF gt_out  OCCURS 0,
        vkorg       TYPE vkorg,
        tab01(1)    TYPE c,
        kunnr       TYPE kunnr,
        tab02(1)    TYPE c,
        sortl       TYPE sortl,
        tab03(1)    TYPE c,
        ktokd       TYPE ktokd,
        tab04(1)    TYPE c,
        name1       TYPE name1_gp,
        tab05(1)    TYPE c,
        name2       TYPE name2_gp,
        tab06(1)    TYPE c,
        parnr       TYPE parnr,
        tab07(1)    TYPE c,
        namel       TYPE name1_gp,
        tab08(1)    TYPE c,
        namev       TYPE namev_vp,
        tab09(1)    TYPE c,
        tel_number  TYPE ad_tlnmbr1,
        tab10(1)    TYPE c,
        tel_extens  TYPE ad_tlxtns1,
        tab11(1)    TYPE c,
        mob_number  TYPE ad_tlnmbr,     "Mobile
        tab12(1)    TYPE c,
        anred       TYPE anred_ap,
        tab13(1)    TYPE c,
        title_aca1  TYPE ad_title1,
        tab14(1)    TYPE c,
        parla       TYPE parla,
        tab15(1)    TYPE c,
        parge       TYPE parge,
        tab16(1)    TYPE c,
        parvo       TYPE parvo,
        tab17(1)    TYPE c,
        pavip       TYPE pavip,
        tab18(1)    TYPE c,
        titel_ap    TYPE titel_ap,
        tab19(1)    TYPE c,
        abtnr       TYPE abtnr_pa,
        tab20(1)    TYPE c,
        abtpa       TYPE abtei_pa,
        tab21(1)    TYPE c,
        gbdat       TYPE gbdat,
        tab22(1)    TYPE c,
        fax_number  TYPE ad_fxnmbr1,
        tab23(1)    TYPE c,
        smtp_addr(70)   TYPE c,
        tab24(1)    TYPE c,
        deflt_comm  TYPE ad_comm,
        tab25(1)    TYPE c,
        street_b    TYPE ad_street,     "Business
        tab26(1)    TYPE c,
        str_sup_b   TYPE ad_strspp1,
        tab27(1)    TYPE c,
        city1_b     TYPE ad_city1,
        tab28(1)    TYPE c,
        country_b   TYPE land1,
        tab29(1)    TYPE c,
        street_p    TYPE ad_street,     "Private
        tab30(1)    TYPE c,
        post_cd_p   TYPE ad_pstcd1,
        tab31(1)    TYPE c,
        city1_p     TYPE ad_city1,
        tab32(1)    TYPE c,
        country_p   TYPE land1,
        tab33(1)    TYPE c,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        vkorg       TYPE fieldname,
        tab01(1)    TYPE c,
        kunnr       TYPE fieldname,
        tab02(1)    TYPE c,
        sortl       TYPE fieldname,
        tab03(1)    TYPE c,
        ktokd       TYPE fieldname,
        tab04(1)    TYPE c,
        name1       TYPE fieldname,
        tab05(1)    TYPE c,
        name2       TYPE fieldname,
        tab06(1)    TYPE c,
        parnr       TYPE fieldname,
        tab07(1)    TYPE c,
        namel       TYPE fieldname,
        tab08(1)    TYPE c,
        namev       TYPE fieldname,
        tab09(1)    TYPE c,
        tel_number  TYPE fieldname,
        tab10(1)    TYPE c,
        tel_extens  TYPE fieldname,
        tab11(1)    TYPE c,
        mob_number  TYPE fieldname,     "Mobile
        tab12(1)    TYPE c,
        anred       TYPE fieldname,
        tab13(1)    TYPE c,
        title_aca1  TYPE fieldname,
        tab14(1)    TYPE c,
        parla       TYPE fieldname,
        tab15(1)    TYPE c,
        parge       TYPE fieldname,
        tab16(1)    TYPE c,
        parvo       TYPE fieldname,
        tab17(1)    TYPE c,
        pavip       TYPE fieldname,
        tab18(1)    TYPE c,
        titel_ap    TYPE fieldname,
        tab19(1)    TYPE c,
        abtnr       TYPE fieldname,
        tab20(1)    TYPE c,
        abtpa       TYPE fieldname,
        tab21(1)    TYPE c,
        gbdat       TYPE fieldname,
        tab22(1)    TYPE c,
        fax_number  TYPE fieldname,
        tab23(1)    TYPE c,
        smtp_addr   TYPE fieldname,
        tab24(1)    TYPE c,
        deflt_comm  TYPE fieldname,
        tab25(1)    TYPE c,
        street_b    TYPE fieldname,     "Business
        tab26(1)    TYPE c,
        str_sup_b   TYPE fieldname,
        tab27(1)    TYPE c,
        city1_b     TYPE fieldname,
        tab28(1)    TYPE c,
        country_b   TYPE fieldname,
        tab29(1)    TYPE c,
        street_p    TYPE fieldname,     "Private
        tab30(1)    TYPE c,
        post_cd_p   TYPE fieldname,
        tab31(1)    TYPE c,
        city1_p     TYPE fieldname,
        tab32(1)    TYPE c,
        country_p   TYPE fieldname,
        tab33(1)    TYPE c,
      END OF gt_outh.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_error         TYPE xfeld.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
*PARAMETERS: p_vkorg  LIKE knvp-vkorg  OBLIGATORY  MEMORY ID vko.
PARAMETERS: p_bukrs  TYPE aufk-bukrs OBLIGATORY MEMORY ID buk,
            p_vkorg  LIKE knvp-vkorg MEMORY ID vko.
*SELECT-OPTIONS: s_kunnr  FOR knvk-kunnr,
*                s_parnr  FOR knvk-parnr.
SELECTION-SCREEN  END OF BLOCK input.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_knvk[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No Contacts selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

  DATA: lv_where TYPE string.
  lv_where = 'tvko~bukrs EQ p_bukrs'.
  IF p_vkorg IS NOT INITIAL.
    CONCATENATE lv_where 'AND s~vkorg = p_vkorg' INTO lv_where SEPARATED BY space.
  ENDIF.
  CONCATENATE lv_where 'AND c~loevm = '' ''' INTO lv_where SEPARATED BY space.

* Select business partners
*  SELECT c~kunnr c~sortl c~ktokd c~name1 c~name2
*         INTO TABLE gt_kna1i
*         FROM kna1 AS c
*         INNER JOIN knvv AS s
*                         ON c~kunnr = s~kunnr
*         WHERE s~vkorg = p_vkorg
**           AND c~kunnr IN s_kunnr
*           AND c~loevm = ' '.
  SELECT c~kunnr c~sortl c~ktokd c~name1 c~name2
         INTO TABLE gt_kna1i
         FROM kna1 AS c
         INNER JOIN knvv AS s
                         ON c~kunnr = s~kunnr
         INNER JOIN tvko AS tvko
           ON tvko~vkorg EQ s~vkorg
         WHERE (lv_where).
  SORT gt_kna1i BY kunnr.
  DELETE ADJACENT DUPLICATES FROM gt_kna1i
         COMPARING kunnr.
  gt_kna1[] = gt_kna1i[].
  FREE gt_kna1i.
  CHECK NOT gt_kna1[] IS INITIAL.

* Select contact persons
  SELECT kunnr parnr name1 namev anred
         parla parge parvo pavip titel_ap
         abtnr abtpa gbdat prsnr
         adrnd adrnp_2
         INTO TABLE gt_knvk
         FROM knvk
         FOR ALL ENTRIES IN gt_kna1
         WHERE kunnr = gt_kna1-kunnr
*           AND parnr IN s_parnr
           AND loevm = ' '.
  SORT gt_knvk BY kunnr parnr.
  CHECK NOT gt_knvk[] IS INITIAL.

* Addresses (Business & Private)
  SELECT addrnumber street str_suppl1 post_code1 city1 country
         INTO TABLE gt_adrci
         FROM adrc
         FOR ALL ENTRIES IN gt_knvk
         WHERE addrnumber = gt_knvk-adrnd
            OR addrnumber = gt_knvk-adrnp_2.
  SORT gt_adrci BY addrnumber.
  DELETE ADJACENT DUPLICATES FROM gt_adrci
         COMPARING addrnumber.
  gt_adrc[] = gt_adrci[].
  FREE gt_adrci.

* Persons
  SELECT persnumber title_aca1
         INTO TABLE gt_adrp
         FROM adrp
         FOR ALL ENTRIES IN gt_knvk
         WHERE persnumber = gt_knvk-prsnr.

* Person/Address Assignments
  CHECK NOT gt_adrp[] IS INITIAL.
  SELECT persnumber addrnumber tel_number tel_extens
         fax_number deflt_comm
         INTO TABLE gt_adcpi
         FROM adcp
         FOR ALL ENTRIES IN gt_adrp
         WHERE persnumber = gt_adrp-persnumber
           AND comp_pers  = 'C'.
  SORT gt_adcpi BY persnumber addrnumber DESCENDING.
  DELETE ADJACENT DUPLICATES FROM gt_adcpi
         COMPARING persnumber.
  gt_adcp[] = gt_adcpi[].
  FREE gt_adrci.
  CHECK NOT gt_adcp[] IS INITIAL.

* Telephone Numbers (Mobile)
  SELECT addrnumber persnumber consnumber tel_number
         INTO TABLE gt_adr2i
         FROM adr2
         FOR ALL ENTRIES IN gt_adcp
         WHERE addrnumber = gt_adcp-addrnumber
           AND persnumber = gt_adcp-persnumber
           AND r3_user    = '3'.
  SORT gt_adr2i BY addrnumber persnumber
                   consnumber DESCENDING.
  DELETE ADJACENT DUPLICATES FROM gt_adr2i
         COMPARING addrnumber persnumber.
  gt_adr2[] = gt_adr2i[].
  FREE gt_adr2i.

* E-Mail Addresses
  SELECT addrnumber persnumber consnumber smtp_addr
         INTO TABLE gt_adr6i
         FROM adr6
         FOR ALL ENTRIES IN gt_adcp
         WHERE addrnumber = gt_adcp-addrnumber
           AND persnumber = gt_adcp-persnumber.
  SORT gt_adr6i BY addrnumber persnumber
                   consnumber DESCENDING.
  DELETE ADJACENT DUPLICATES FROM gt_adr6i
         COMPARING addrnumber persnumber.
  gt_adr6[] = gt_adr6i[].
  FREE gt_adr6i.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_knvk.

    CLEAR gt_out.

    MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03,
                 gt_out-tab04, gt_out-tab05, gt_out-tab06,
                 gt_out-tab07, gt_out-tab08, gt_out-tab09,
                 gt_out-tab10, gt_out-tab11, gt_out-tab12,
                 gt_out-tab13, gt_out-tab14, gt_out-tab15,
                 gt_out-tab16, gt_out-tab17, gt_out-tab18,
                 gt_out-tab19, gt_out-tab20, gt_out-tab21,
                 gt_out-tab22, gt_out-tab23, gt_out-tab24,
                 gt_out-tab25, gt_out-tab26, gt_out-tab27,
                 gt_out-tab28, gt_out-tab29, gt_out-tab30,
                 gt_out-tab31, gt_out-tab32, gt_out-tab33.
    gt_out-vkorg = p_vkorg.

*   Partner
    READ TABLE gt_kna1 WITH TABLE KEY kunnr = gt_knvk-kunnr.
    IF sy-subrc = 0.
      gt_out-sortl = gt_kna1-sortl.
      gt_out-ktokd = gt_kna1-ktokd.
      gt_out-name1 = gt_kna1-name1.
      gt_out-name2 = gt_kna1-name2.
    ELSE.
      CONTINUE.
    ENDIF.

*   Contact
    MOVE-CORRESPONDING gt_knvk TO gt_out.

    READ TABLE gt_adrp WITH TABLE KEY persnumber = gt_knvk-prsnr.
    IF sy-subrc NE 0.
      CONTINUE.
    ENDIF.

*   Person/Address Assignments
    READ TABLE gt_adcp WITH TABLE KEY persnumber = gt_adrp-persnumber.
    IF sy-subrc = 0.
      gt_out-tel_number = gt_adcp-tel_number.
      gt_out-tel_extens = gt_adcp-tel_extens.
      gt_out-fax_number = gt_adcp-fax_number.
      gt_out-deflt_comm = gt_adcp-deflt_comm.
    ENDIF.
*   Telephone Numbers (Mobile)
    READ TABLE gt_adr2 WITH TABLE KEY addrnumber = gt_adcp-addrnumber
                                      persnumber = gt_adcp-persnumber.
    IF sy-subrc = 0.
      gt_out-mob_number = gt_adr2-tel_number.
    ENDIF.
*   E-Mail Addresses
    READ TABLE gt_adr6 WITH TABLE KEY addrnumber = gt_adcp-addrnumber
                                      persnumber = gt_adcp-persnumber.
    IF sy-subrc = 0.
      gt_out-smtp_addr = gt_adr6-smtp_addr.
    ENDIF.

*   Addresses
*   Business
    READ TABLE gt_adrc WITH TABLE KEY addrnumber = gt_knvk-adrnd.
    IF sy-subrc = 0.
      gt_out-street_b  = gt_adrc-street.
      gt_out-str_sup_b = gt_adrc-str_suppl1.
      gt_out-city1_b   = gt_adrc-city1.
      gt_out-country_b = gt_adrc-country.
    ENDIF.
*   Private
    READ TABLE gt_adrc WITH TABLE KEY addrnumber = gt_knvk-adrnp_2.
    IF sy-subrc = 0.
      gt_out-street_p  = gt_adrc-street.
      gt_out-post_cd_p = gt_adrc-post_code1.
      gt_out-city1_p   = gt_adrc-city1.
      gt_out-country_p = gt_adrc-country.
    ENDIF.

    APPEND gt_out.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

*  CONCATENATE 'CONTACT' 'ADDRESS' p_vkorg
  CONCATENATE 'CONTACT' 'ADDRESS' p_bukrs
             INTO gv_ofile SEPARATED BY '_'.
  IF p_vkorg IS NOT INITIAL.
   CONCATENATE gv_ofile p_vkorg INTO gv_ofile SEPARATED BY '_'.
  ENDIF.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM build_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

* Fill output
  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  BUILD_HEADER
*&---------------------------------------------------------------------*
*       Build header (in output table)
*----------------------------------------------------------------------*
FORM build_header .

  CLEAR gt_outh.

  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03,
               gt_outh-tab04, gt_outh-tab05, gt_outh-tab06,
               gt_outh-tab07, gt_outh-tab08, gt_outh-tab09,
               gt_outh-tab10, gt_outh-tab11, gt_outh-tab12,
               gt_outh-tab13, gt_outh-tab14, gt_outh-tab15,
               gt_outh-tab16, gt_outh-tab17, gt_outh-tab18,
               gt_outh-tab19, gt_outh-tab20, gt_outh-tab21,
               gt_outh-tab22, gt_outh-tab23, gt_outh-tab24,
               gt_outh-tab25, gt_outh-tab26, gt_outh-tab27,
               gt_outh-tab28, gt_outh-tab29, gt_outh-tab30,
               gt_outh-tab31, gt_outh-tab32, gt_outh-tab33.

  gt_outh-vkorg      = 'VKORG'.
  gt_outh-kunnr      = 'KUNNR'.
  gt_outh-sortl      = 'SORTL'.
  gt_outh-ktokd      = 'KTOKD'.
  gt_outh-name1      = 'NAME1_GP'.
  gt_outh-name2      = 'NAME2_GP'.
  gt_outh-parnr      = 'PARNR'.
  gt_outh-namel      = 'NAME1_GP'.
  gt_outh-namev      = 'NAMEV_VP'.
  gt_outh-tel_number = 'AD_TLNMBR1'.
  gt_outh-tel_extens = 'AD_TLXTNS1'.
  gt_outh-mob_number = 'AD_TLNMBR'.
  gt_outh-anred      = 'ANRED_AP'.
  gt_outh-title_aca1 = 'AD_TITLE1'.
  gt_outh-parla      = 'PARLA'.
  gt_outh-parge      = 'PARGE'.
  gt_outh-parvo      = 'PARVO'.
  gt_outh-pavip      = 'PAVIP'.
  gt_outh-titel_ap   = 'TITEL_AP'.
  gt_outh-abtnr      = 'ABTNR_PA'.
  gt_outh-abtpa      = 'ABTEI_PA'.
  gt_outh-gbdat      = 'GBDAT'.
  gt_outh-fax_number = 'AD_FXNMBR1'.
  gt_outh-smtp_addr  = 'AD_SMTPADR'.
  gt_outh-deflt_comm = 'AD_COMM'.
  gt_outh-street_b   = 'AD_STREET_b'.
  gt_outh-str_sup_b  = 'AD_STRSPP1_b'.
  gt_outh-city1_b    = 'AD_CITY1_b'.
  gt_outh-country_b  = 'LAND1_b'.
  gt_outh-street_p   = 'AD_STREET_p'.
  gt_outh-post_cd_p  = 'AD_PSTCD1_p'.
  gt_outh-city1_p    = 'AD_CITY1_p'.
  gt_outh-country_p  = 'LAND1_p'.
  APPEND gt_outh.

  gt_outh-vkorg      = 'Sales Org.'.
  gt_outh-kunnr      = 'Partner'.
  gt_outh-sortl      = 'Search Term'.
  gt_outh-ktokd      = 'Acc. Gr.'.
  gt_outh-name1      = 'Name1'.
  gt_outh-name2      = 'Name2'.
  gt_outh-parnr      = 'Contact'.
  gt_outh-namel      = 'Last Name'.
  gt_outh-namev      = 'First Name'.
  gt_outh-tel_number = 'Telephone'.
  gt_outh-tel_extens = 'Extension'.
  gt_outh-mob_number = 'Mobile Tel.'.
  gt_outh-anred      = 'Title'.
  gt_outh-title_aca1 = 'Acad. Title'.
  gt_outh-parla      = 'Lang.'.
  gt_outh-parge      = 'Gender'.
  gt_outh-parvo      = 'Author.'.
  gt_outh-pavip      = 'VIP'.
  gt_outh-titel_ap   = 'Function'.
  gt_outh-abtnr      = 'Dep.'.
  gt_outh-abtpa      = 'Department'.
  gt_outh-gbdat      = 'Birth Date'.
  gt_outh-fax_number = 'Fax'.
  gt_outh-smtp_addr  = 'E-mail Address'.
  gt_outh-deflt_comm = 'Communication'.
  gt_outh-street_b   = 'Street (Business)'.
  gt_outh-str_sup_b  = 'Street 2 (Bus.)'.
  gt_outh-city1_b    = 'City (Bus.)'.
  gt_outh-country_b  = 'Country (Bus.)'.
  gt_outh-street_p   = 'Street (Private)'.
  gt_outh-post_cd_p  = 'Postc. (Priv.)'.
  gt_outh-city1_p    = 'City (Priv.)'.
  gt_outh-country_p  = 'Country (Priv.)'.
  APPEND gt_outh.

ENDFORM.                    " BUILD_HEADER

*Text symbol text£º
*E01:No Contacts selected
*E02:Could not open output file
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_BUKRS:D       .
*P_VKORG:D       .
