*&---------------------------------------------------------------------*
* PROGRAM ID           : YSE_SDMM_ASC_LIST                             *
* PROGRAM TITLE        : RDC: ASC comparing                            *
* AUTHOR               : Anda Wu                                       *
* DATE                 : 12/05/2014                                    *
* DEVELOPMENT ID       :  CD1K981252                                   *
* CHANGE REQUEST NUMBER:    CR3231                                     *
* PROGRAM DESCRIPTION  : Report in SAP SEED to execute the result and
*                        compare with the data from ASC.
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG
*----------------------------------------------------------------------*
* MOD. NO.|  DATE      | NAME               | CORR. NR.  | CHANGE REF. *
* MOD-001 |2014.09.18  | Anda Wu            | CD1K983090 | CR3377 *
************************************************************************
REPORT  yse_sdmm_asc_list.

************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES:   vbrk,
          vbrp,
          vbfa.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:
  BEGIN OF ty_vbrk,
    vbeln         TYPE vbrk-vbeln,
    fkart         TYPE vbrk-fkart,
    fkdat         TYPE vbrk-fkdat,
    rfbsk         TYPE vbrk-rfbsk,
    vkorg         TYPE vbrk-vkorg,
*   Begin of MOD-001 INSERT
    knumv         TYPE vbrk-knumv,
*   End  of MOD-001 INSERT
    kunag         TYPE vbrk-kunag,
  END OF ty_vbrk,

  BEGIN OF ty_vbrp,
    vbeln         TYPE vbrp-vbeln,
    posnr         TYPE vbrp-posnr,
    aubel         TYPE vbrp-aubel,
    aupos         TYPE vbrp-aupos,
  END OF ty_vbrp,

  BEGIN OF ty_vbap,
    vbeln         TYPE vbak-vbeln,
    waerk         TYPE vbak-waerk,
    knumv         TYPE vbak-knumv,
    posnr         TYPE vbap-posnr,
  END OF ty_vbap,

  BEGIN OF ty_vbkd,
    vbeln         TYPE vbkd-vbeln,
    posnr         TYPE vbkd-posnr,
    bstkd         TYPE vbkd-bstkd,
  END OF ty_vbkd,

  BEGIN OF ty_konv,
    knumv         TYPE konv-knumv,
    kposn         TYPE konv-kposn,
    kschl         TYPE konv-kschl,
    kawrt         TYPE konv-kawrt,
    kbetr         TYPE konv-kbetr,
    waers         TYPE konv-waers,
    kwert         TYPE konv-kwert,
  END OF ty_konv,

  BEGIN OF ty_vbfa,
    vbelv         TYPE vbfa-vbelv,
    posnv         TYPE vbfa-posnv,
    vbeln         TYPE vbfa-vbeln,
    posnn         TYPE vbfa-posnn,
    vbtyp_n       TYPE vbfa-vbtyp_n,
  END OF ty_vbfa,

  BEGIN OF  ty_alv_output,
    aubel         TYPE vbrp-aubel,                "SO No.
    aupos         TYPE vbrp-aupos,                "SO Item
    vbeln_b       TYPE vbrp-vbeln,                "Billing
    bstkd         TYPE vbkd-bstkd,                "PO No.
    posnr         TYPE vbkd-posnr,                "PO Item
    pamnt         TYPE konv-kbetr,                "Parts amount
    pwaers        TYPE konv-waers,                "Currency Key
    famnt         TYPE konv-kbetr,                "Freight amount
    fwaers        TYPE konv-waers,                "Currency Key
    iamnt         TYPE konv-kbetr,                "Insurance amount
    iwaers        TYPE konv-waers,                "Currency Key
    vbeln         TYPE vbfa-vbeln,                "Shipment
  END OF ty_alv_output.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_vbrk         TYPE STANDARD TABLE OF ty_vbrk,
  gt_vbrp         TYPE STANDARD TABLE OF ty_vbrp,
  gt_vbap         TYPE STANDARD TABLE OF ty_vbap,
  gt_konv         TYPE STANDARD TABLE OF ty_konv,
  gt_vbkd         TYPE STANDARD TABLE OF ty_vbkd,
  gt_vbfa_j       TYPE STANDARD TABLE OF ty_vbfa,
  gt_vbfa_8       TYPE STANDARD TABLE OF ty_vbfa,
  gt_fieldcat     TYPE slis_t_fieldcat_alv,
  gt_alv_output   TYPE STANDARD TABLE OF ty_alv_output.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_disvar           TYPE disvariant,
  gs_layout           TYPE slis_layout_alv.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_text             TYPE string,
  gv_save             TYPE char1,
  gv_callback         TYPE slis_formname.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x                TYPE char1 VALUE 'X',
  gc_zn00             TYPE konv-kschl   VALUE 'ZN00',
  gc_zf01             TYPE konv-kschl   VALUE 'ZF01',
  gc_zisu             TYPE konv-kschl   VALUE 'ZISU',
  gc_cn06             TYPE vbrk-vkorg   VALUE 'CN06',
  gc_asc              TYPE kuagv-kunnr  VALUE '0102000157',
  gc_fkart_zf4        TYPE vbrk-fkart   VALUE 'ZF4',
  gc_vbtyp_j          TYPE char1 VALUE 'J',
  gc_vbtyp_8          TYPE char1 VALUE '8',
  gc_btype_a          TYPE char1 VALUE 'A',
  gc_type_e           TYPE char1 VALUE 'E'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.

PARAMETERS:
  p_vari    TYPE disvariant-variant
            MODIF ID m1.         "Dispaly Variant

SELECT-OPTIONS:
  s_fkdat     FOR   vbrk-fkdat,             "Cut off date
  s_aubel     FOR   vbrp-aubel,             "SO No.
  s_aupos     FOR   vbrp-aupos,             "SO item
  s_vbeln     FOR   vbfa-vbeln.             "Shipment number

SELECTION-SCREEN END OF BLOCK b1.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initilization
  PERFORM frm_init.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN OUTPUT                                          *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.
* screen initial set
  PERFORM frm_screen_set.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN                                                 *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.
* selection screen check
  PERFORM check_parameter.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN ON VALUE-REQUEST                                *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
* F4 help of ALV layout
  PERFORM f4_for_variant.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* progress indicator
  PERFORM process_info.
* get data
  PERFORM get_master_data.
* generate alv data
  PERFORM gen_alv_data.
* alv property set
  PERFORM alv_prop_set.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
* ALV display
  PERFORM alv_display.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do initilization
*----------------------------------------------------------------------*
FORM frm_init .

  REFRESH:  gt_vbrk,
            gt_vbrp,
            gt_vbap,
            gt_konv,
            gt_vbkd,
            gt_alv_output.

  CLEAR:    gs_disvar,
            gs_layout,
            gv_text,
            gv_save,
            gv_callback.

ENDFORM.                    " FRM_INIT

*&---------------------------------------------------------------------*
*&      Form  FRM_SCREEN_SET
*&---------------------------------------------------------------------*
*       screen initial set
*----------------------------------------------------------------------*
FORM frm_screen_set .

*  s_fkdat-sign    = 'I'.
*  s_fkdat-option  = 'BT'.
*  s_fkdat-high = sy-datum.
*  APPEND s_fkdat.

  LOOP AT SCREEN.
    IF screen-group1 = 'M1'.
      screen-invisible = '1'.
      screen-input   = '0'.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " FRM_SCREEN_SET

*&---------------------------------------------------------------------*
*&      Form  CHECK_PARAMETER
*&---------------------------------------------------------------------*
*       selection screen check
*----------------------------------------------------------------------*
FORM check_parameter .

  IF    s_fkdat  IS INITIAL
    AND s_aubel  IS INITIAL
    AND s_aupos  IS INITIAL
    AND s_vbeln  IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-008.
    MESSAGE e000(yse_sales_log) WITH gv_text.
  ENDIF.

ENDFORM.                    " CHECK_PARAMETER

*&---------------------------------------------------------------------*
*&      Form  F4_FOR_VARIANT
*&---------------------------------------------------------------------*
*       F4 help of ALV layout
*----------------------------------------------------------------------*
FORM f4_for_variant .

  DATA: ls_disvar TYPE disvariant,
        lv_exit   TYPE char1.

  ls_disvar-report = sy-repid.

  CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
    EXPORTING
      is_variant    = ls_disvar
      i_save        = gv_save
    IMPORTING
      e_exit        = lv_exit
      es_variant    = gs_disvar
    EXCEPTIONS
      not_found     = 1
      program_error = 2
      OTHERS        = 3.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    IF lv_exit = space.
      p_vari = gs_disvar-variant.
    ENDIF.
  ENDIF.

ENDFORM.                    " F4_FOR_VARIANT

*&---------------------------------------------------------------------*
*&      Form  PROCESS_INFO
*&---------------------------------------------------------------------*
*       progress indicator
*----------------------------------------------------------------------*
FORM process_info .

  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    CONCATENATE 'Start report:'(004) sy-repid
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-005.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 1
        text       = gv_text.
  ENDIF.

ENDFORM.                    " PROCESS_INFO
*&---------------------------------------------------------------------*
*&      Form  GET_MASTER_DATA
*&---------------------------------------------------------------------*
*       get data
*----------------------------------------------------------------------*
FORM get_master_data .

  DATA: lt_vbrp   TYPE STANDARD TABLE OF ty_vbrp,
        ls_konv   TYPE ty_konv,
        lt_konv   TYPE STANDARD TABLE OF ty_konv.
*   Begin of MOD-001 INSERT
  DATA: ls_vbrk TYPE ty_vbrk,
        ls_vbrp TYPE ty_vbrp.
  FIELD-SYMBOLS: <lfs_vbap> TYPE ty_vbap.
*   End   of MOD-001 INSERT
  SELECT  vbeln
          fkart
          fkdat
          rfbsk
          vkorg
*   Begin of MOD-001 INSERT
          knumv
*   End  of MOD-001 INSERT
          kunag
    FROM vbrk
    INTO TABLE gt_vbrk
    WHERE fkart =   gc_fkart_zf4
      AND fkdat IN  s_fkdat
      AND rfbsk =   gc_btype_a
      AND vkorg =   gc_cn06
      AND kunag =   gc_asc.
  IF sy-subrc <> 0.
    CLEAR: gv_text.
    gv_text = text-001.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  SELECT  vbeln
          posnr
          aubel
          aupos
    FROM  vbrp
    INTO TABLE gt_vbrp
    FOR ALL ENTRIES IN gt_vbrk
    WHERE vbeln = gt_vbrk-vbeln
      AND aubel IN s_aubel
      AND aupos IN s_aupos.
  IF sy-subrc <> 0.
    CLEAR: gv_text.
    gv_text = text-002.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  APPEND LINES OF gt_vbrp TO lt_vbrp.
  SORT lt_vbrp BY aubel aupos.
  DELETE ADJACENT DUPLICATES FROM lt_vbrp COMPARING aubel.

  SELECT  vbak~vbeln
          vbak~waerk
          vbak~knumv
          vbap~posnr
    FROM vbak
      INNER JOIN vbap
      ON vbak~vbeln = vbap~vbeln
    INTO TABLE gt_vbap
    FOR ALL ENTRIES IN lt_vbrp
    WHERE vbak~vbeln = lt_vbrp-aubel.
  IF sy-subrc <> 0.
    CLEAR: gv_text.
    gv_text = text-003.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
*   Begin of MOD-001 INSERT
  ELSE.
    LOOP AT gt_vbap ASSIGNING <lfs_vbap> .
      CLEAR: ls_vbrp, ls_vbrk.
      READ TABLE gt_vbrp INTO ls_vbrp
        with key aubel = <lfs_vbap>-vbeln
                 aupos = <lfs_vbap>-posnr.
      IF sy-subrc = 0.
        READ TABLE gt_vbrk INTO ls_vbrk
          with key vbeln = ls_vbrp-vbeln.
        IF sy-subrc = 0.
          <lfs_vbap>-knumv = ls_vbrk-knumv.
        ENDIF.
      ENDIF.
    ENDLOOP.
*   End   of MOD-001 INSERT
  ENDIF.

  SELECT  vbeln
          posnr
          bstkd
    FROM vbkd
    INTO TABLE gt_vbkd
    FOR ALL ENTRIES IN gt_vbap
    WHERE vbeln = gt_vbap-vbeln
      AND posnr = gt_vbap-posnr.

  SELECT  knumv
          kposn
          kschl
          kawrt
          kbetr
          waers
          kwert
    FROM konv
    INTO TABLE lt_konv
    FOR ALL ENTRIES IN gt_vbap
    WHERE knumv = gt_vbap-knumv
      AND kposn = gt_vbap-posnr.

  LOOP AT lt_konv INTO ls_konv.
    IF    ls_konv-kschl = gc_zn00
      OR  ls_konv-kschl = gc_zf01
      OR  ls_konv-kschl = gc_zisu.
      APPEND ls_konv TO gt_konv.
    ENDIF.
  ENDLOOP.

  SELECT  vbelv
          posnv
          vbeln
          posnn
          vbtyp_n
    FROM vbfa
    INTO TABLE gt_vbfa_j
    FOR ALL ENTRIES IN gt_vbap
    WHERE vbelv = gt_vbap-vbeln
      AND posnv = gt_vbap-posnr
      AND vbtyp_n = gc_vbtyp_j.
  IF sy-subrc = 0.
    SELECT  vbelv
            posnv
            vbeln
            posnn
            vbtyp_n
      FROM vbfa
      INTO TABLE gt_vbfa_8
      FOR ALL ENTRIES IN gt_vbfa_j
      WHERE vbelv = gt_vbfa_j-vbeln
*        AND posnv = gt_vbfa_j-posnn
        AND vbtyp_n = gc_vbtyp_8
        AND vbeln IN s_vbeln.
    IF    s_vbeln IS NOT INITIAL
      AND sy-subrc <> 0.
      CLEAR: gv_text.
      gv_text = text-060.
      MESSAGE s000(yse_sales_log) WITH gv_text
        DISPLAY LIKE gc_type_e.
      LEAVE LIST-PROCESSING.
    ENDIF.
  ENDIF.

ENDFORM.                    " GET_MASTER_DATA
*&---------------------------------------------------------------------*
*&      Form  ALV_PROP_SET
*&---------------------------------------------------------------------*
*       alv property set
*----------------------------------------------------------------------*
FORM alv_prop_set .

* Display ALV process indicator
  PERFORM alv_progress_indicator.

* FIELDCAT set
  PERFORM alv_fieldcat_set.

* LAYOUT set
  PERFORM alv_layout_set.
** Set Others
*  PERFORM alv_others_set.

ENDFORM.                    " ALV_PROP_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_lines(10)  TYPE c.

  IF gt_alv_output IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-060.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  IF sy-batch IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-010.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 80
        text       = gv_text.
  ELSE.
    CLEAR: gv_text.
    DESCRIBE TABLE gt_alv_output LINES lv_lines.
    CONCATENATE 'Totally'(015) lv_lines 'entries are generated'(020)
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ENDIF.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = sy-repid
      i_callback_user_command = gv_callback
      is_layout               = gs_layout
      it_fieldcat             = gt_fieldcat
      i_save                  = gv_save
      is_variant              = gs_disvar
    TABLES
      t_outtab                = gt_alv_output
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY
*&---------------------------------------------------------------------*
*&      Form  ALV_PROGRESS_INDICATOR
*&---------------------------------------------------------------------*
*       Display ALV process indicator
*----------------------------------------------------------------------*
FORM alv_progress_indicator .

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to set ALV property'(011).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-011.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 75
        text       = gv_text.
  ENDIF.

ENDFORM.                    " ALV_PROGRESS_INDICATOR
*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM alv_fieldcat_set .

  DATA:
     lv_linecnt  TYPE i,
     ls_fieldcat TYPE slis_fieldcat_alv.

  CLEAR lv_linecnt.
* SO No.
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'AUBEL'.
  ls_fieldcat-ref_fieldname = 'AUBEL'.
  ls_fieldcat-ref_tabname = 'VBRP'.
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* SO Item
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'AUPOS'.
  ls_fieldcat-ref_fieldname = 'AUPOS'.
  ls_fieldcat-ref_tabname = 'VBRP'.
  APPEND ls_fieldcat TO gt_fieldcat.

* Billing
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'VBELN_B'.
  ls_fieldcat-ref_fieldname = 'VBELN'.
  ls_fieldcat-ref_tabname = 'VBRP'.
  APPEND ls_fieldcat TO gt_fieldcat.

* PO No. bstkd
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'BSTKD'.
  ls_fieldcat-ref_fieldname = 'BSTKD'.
  ls_fieldcat-ref_tabname = 'VBKD'.
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* PO Item
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'POSNR'.
  ls_fieldcat-seltext_s = 'PO.Item'(025).
  ls_fieldcat-seltext_m = 'PO Item'(026).
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* Parts amount
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'PAMNT'.
*  ls_fieldcat-ref_fieldname = 'KBETR'.
*  ls_fieldcat-ref_tabname = 'KONV'.
  ls_fieldcat-seltext_s = 'PartsAmt'(030).
  ls_fieldcat-seltext_m = 'Parts Amount'(031).
  ls_fieldcat-cfieldname  = 'PWAERS'.
*         ctabname
  APPEND ls_fieldcat TO gt_fieldcat.

* Currency Key
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'PWAERS'.
  ls_fieldcat-ref_fieldname = 'WAERS'.
  ls_fieldcat-ref_tabname = 'KONV'.
  APPEND ls_fieldcat TO gt_fieldcat.

* Freight amount
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'FAMNT'.
*  ls_fieldcat-ref_fieldname = 'KBETR'.
*  ls_fieldcat-ref_tabname = 'KONV'.
  ls_fieldcat-seltext_s = 'FretAmt'(035).
  ls_fieldcat-seltext_m = 'Freight Amount'(036).
  ls_fieldcat-cfieldname  = 'FWAERS'.
  APPEND ls_fieldcat TO gt_fieldcat.

* Currency Key
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'FWAERS'.
  ls_fieldcat-ref_fieldname = 'WAERS'.
  ls_fieldcat-ref_tabname = 'KONV'.
  APPEND ls_fieldcat TO gt_fieldcat.

* Insurance amount
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'IAMNT'.
*  ls_fieldcat-ref_fieldname = 'KBETR'.
*  ls_fieldcat-ref_tabname = 'KONV'.
  ls_fieldcat-seltext_s = 'InsAmt'(040).
  ls_fieldcat-seltext_m = 'Insurance Amount'(041).
  ls_fieldcat-cfieldname  = 'IWAERS'.
  APPEND ls_fieldcat TO gt_fieldcat.

* Currency Key
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'IWAERS'.
  ls_fieldcat-ref_fieldname = 'WAERS'.
  ls_fieldcat-ref_tabname = 'KONV'.
  APPEND ls_fieldcat TO gt_fieldcat.

* Shipment
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'VBELN'.
  ls_fieldcat-seltext_s = 'Shpmnt'(021).
  ls_fieldcat-seltext_m = 'Shipment No.'(022).
  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   " Auto optimize column width
  gs_layout-detail_popup = gc_x.        " Show detail screen

ENDFORM.                    " ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*&      Form  GEN_ALV_DATA
*&---------------------------------------------------------------------*
*       generate alv data
*----------------------------------------------------------------------*
FORM gen_alv_data .

  DATA: ls_vbrp     TYPE ty_vbrp,
        ls_vbap     TYPE ty_vbap,
        ls_vbfa_j   TYPE ty_vbfa,
        ls_vbfa_8   TYPE ty_vbfa,
        ls_vbkd     TYPE ty_vbkd,
        ls_konv     TYPE ty_konv,
        ls_alv_data TYPE ty_alv_output.

  LOOP AT gt_vbrp INTO ls_vbrp.
    CLEAR: ls_alv_data.
    ls_alv_data-aubel = ls_vbrp-aubel.
    ls_alv_data-aupos = ls_vbrp-aupos.
    ls_alv_data-vbeln_b = ls_vbrp-vbeln.

    READ TABLE gt_vbap INTO ls_vbap WITH KEY
      vbeln = ls_vbrp-aubel
      posnr = ls_vbrp-aupos.
    IF sy-subrc <> 0.
      CONTINUE.
    ENDIF.
    CLEAR ls_konv.
*   Parts amount
    READ TABLE gt_konv INTO ls_konv WITH KEY
        knumv = ls_vbap-knumv
        kposn = ls_vbap-posnr
        kschl = gc_zn00.
    IF sy-subrc = 0.
*      ls_alv_data-pamnt   = ls_konv-kbetr.
*      ls_alv_data-pwaers  = ls_konv-waers.
      ls_alv_data-pamnt   = ls_konv-kwert.
      ls_alv_data-pwaers  = ls_vbap-waerk.
    ENDIF.
    CLEAR ls_konv.
*   Freight amount
    READ TABLE gt_konv INTO ls_konv WITH KEY
        knumv = ls_vbap-knumv
        kposn = ls_vbap-posnr
        kschl = gc_zf01.
    IF sy-subrc = 0.
*      ls_alv_data-famnt   = ls_konv-kbetr.
*      ls_alv_data-fwaers  = ls_konv-waers.
      ls_alv_data-famnt   = ls_konv-kwert.
      ls_alv_data-fwaers  = ls_vbap-waerk.
    ENDIF.
    CLEAR ls_konv.
*   Insurance amount
    READ TABLE gt_konv INTO ls_konv WITH KEY
        knumv = ls_vbap-knumv
        kposn = ls_vbap-posnr
        kschl = gc_zisu.
    IF sy-subrc = 0.
*      ls_alv_data-iamnt   = ls_konv-kbetr.
*      ls_alv_data-iwaers  = ls_konv-waers.
      ls_alv_data-iamnt   = ls_konv-kwert.
      ls_alv_data-iwaers  = ls_vbap-waerk.
    ENDIF.
    CLEAR: ls_vbfa_j.
    READ TABLE gt_vbfa_j INTO ls_vbfa_j WITH KEY
      vbelv = ls_vbap-vbeln
      posnv = ls_vbap-posnr.
    IF sy-subrc = 0.
      CLEAR: ls_vbfa_8.
      READ TABLE gt_vbfa_8 INTO ls_vbfa_8 WITH KEY
        vbelv = ls_vbfa_j-vbeln.
*        posnv = ls_vbfa_j-posnn.
      IF sy-subrc = 0.
        ls_alv_data-vbeln  = ls_vbfa_8-vbeln.
      ENDIF.
    ENDIF.
    READ TABLE gt_vbkd  INTO ls_vbkd WITH KEY
      vbeln = ls_vbap-vbeln
      posnr = ls_vbap-posnr.
    IF sy-subrc = 0.
      ls_alv_data-bstkd = ls_vbkd-bstkd.
      ls_alv_data-posnr = ls_vbkd-posnr / 10.
    ENDIF.
    APPEND ls_alv_data TO gt_alv_output.
  ENDLOOP.

ENDFORM.                    " GEN_ALV_DATA

*Text symbol text£º
*001:No data [VBRK] exist within the cut off date
*002:No data [VBRP] exist
*003:No data [VBAK] exist
*004:Start report:
*005:Start Processing...
*008:Please enter at least 1 parameter
*010:Start to display ALV
*011:Begin to set ALV property
*015:Totally
*020:entries are generated
*021:Shpmnt
*022:Shipment No.
*025:PO.Item
*026:PO Item
*030:PartsAmt
*031:Parts Amount
*035:FretAmt
*036:Freight Amount
*040:InsAmt
*041:Insurance Amount
*060:No data extracted by selection screen

*T01:Selection Conditions
*Selection text£º
*P_VARI:        ALV Layout
*S_AUBEL:        SO No.
*S_AUPOS:        SO Item
*S_FKDAT:        Cut Off Date
*S_VBELN:        Shipment No.
