*&---------------------------------------------------------------------*
*& Report  YSE_ACT_CONTRACT_ITM_COND_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Contract Item Conditions extraction                          *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 15.10.2012
*  Change Request Number :
*  Transport request Nr. : CD1K973505
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                 | CD1K9.....    |             *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_act_contract_itm_cond_extr.

TABLES: vbak,
        vbap,
        veda,
        vbup,
        konv.

* Types
TYPES: BEGIN OF ty_vbap,
         vbeln      TYPE vbeln_va,
         posnr      TYPE posnr_va,
         knumv      TYPE knumv,
         vkuegru    TYPE vkgru_veda,
         veindat    TYPE vedat_veda,
      END OF ty_vbap.

TYPES: BEGIN OF ty_konv,
         knumv      TYPE knumv,
         kposn      TYPE kposn,
         kschl      TYPE kscha,
         kbetr      TYPE kbetr,
         waers      TYPE waers,
         kpein      TYPE kpein,
         kmein      TYPE kvmei,
         kwert      TYPE kwert,
         krech      TYPE krech,
      END OF ty_konv.

* Internal tables
DATA: gt_vbap  TYPE STANDARD TABLE OF ty_vbap
                    WITH HEADER LINE.

DATA: gt_konv  TYPE SORTED TABLE OF ty_konv
                    WITH NON-UNIQUE KEY knumv kposn
                    WITH HEADER LINE.

DATA: BEGIN OF gt_out  OCCURS 0,
        vbeln       TYPE vbeln_va,
        tab01(1)    TYPE c,
        posnr       TYPE posnr_va,
        tab02(1)    TYPE c,
        kschl       TYPE kscha,
        tab03(1)    TYPE c,
        kbetr       TYPE char16,
        tab04(1)    TYPE c,
        waers       TYPE waers,
        tab05(1)    TYPE c,
        kpein       TYPE char5,
        tab06(1)    TYPE c,
        kmein       TYPE kvmei,
        tab07(1)    TYPE c,
        kwert       TYPE char18,
        tab08(1)    TYPE c,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        vbeln       TYPE fieldname,
        tab01(1)    TYPE c,
        posnr       TYPE fieldname,
        tab02(1)    TYPE c,
        kschl       TYPE fieldname,
        tab03(1)    TYPE c,
        kbetr       TYPE fieldname,
        tab04(1)    TYPE c,
        waers       TYPE fieldname,
        tab05(1)    TYPE c,
        kpein       TYPE fieldname,
        tab06(1)    TYPE c,
        kmein       TYPE fieldname,
        tab07(1)    TYPE c,
        kwert       TYPE fieldname,
        tab08(1)    TYPE c,
      END OF gt_outh.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_ersda(14)     TYPE c,                      "YYYYMMDDHHMMSS
      gv_error         TYPE xfeld.

*CONSTANTS: gc_stat_dl  TYPE j_status  VALUE 'I0076'.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
*PARAMETERS: p_vkorg  LIKE vbak-vkorg  OBLIGATORY  MEMORY ID vko.
PARAMETERS: p_bukrs  TYPE aufk-bukrs OBLIGATORY MEMORY ID buk,
            p_vkorg  LIKE vbak-vkorg MEMORY ID vko.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_vkbur  FOR vbak-vkbur,
                s_vkgrp  FOR vbak-vkgrp,
                s_vbdat  FOR veda-vbegdat,
                s_vndat  FOR veda-venddat,
                s_auart  FOR vbak-auart,
                s_vbeln  FOR vbak-vbeln,
                s_abgru  FOR vbap-abgru,
                s_gbsta  FOR vbup-gbsta,
                s_fksaa  FOR vbup-fksaa,
                s_kschl  FOR konv-kschl.
SELECTION-SCREEN SKIP.
PARAMETERS: p_canc_r   AS CHECKBOX,
            p_canc_d   AS CHECKBOX.
SELECTION-SCREEN  END OF BLOCK input.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Timestamp
  CONCATENATE sy-datum sy-uzeit INTO gv_ersda.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Condition selection
  PERFORM condition_selection.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_vbap[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No contracts selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  CONDITION_SELECTION
*&---------------------------------------------------------------------*
*       Condition selection
*----------------------------------------------------------------------*
FORM condition_selection .

* Selection
  IF s_kschl[] IS INITIAL.
*    PERFORM add_sel_kschl USING 'Z100'.
*    PERFORM add_sel_kschl USING 'Z101'.
*    PERFORM add_sel_kschl USING 'Z102'.
*    PERFORM add_sel_kschl USING 'Z103'.
*    PERFORM add_sel_kschl USING 'Z104'.
*    PERFORM add_sel_kschl USING 'ZN02'.
*    PERFORM add_sel_kschl USING 'ZN03'.
*    PERFORM add_sel_kschl USING 'EDI1'.
    PERFORM add_sel_kschl USING 'ZEK2'.
    PERFORM add_sel_kschl USING 'MWST'.
    PERFORM add_sel_kschl USING 'VPRS'.
    PERFORM add_sel_kschl USING 'SKTO'.
    PERFORM add_sel_kschl USING 'ZCIQ'.
    PERFORM add_sel_kschl USING 'ZK00'.
    PERFORM add_sel_kschl USING 'ZPN0'.
    PERFORM add_sel_kschl USING 'ZPPS'.
    PERFORM add_sel_kschl USING 'ZMIN'.
  ENDIF.

ENDFORM.                    " CONDITION_SELECTION

*&---------------------------------------------------------------------*
*&      Form  ADD_SEL_KSCHL
*&---------------------------------------------------------------------*
*       Add Condition Type to selection
*----------------------------------------------------------------------*
*      -->P_KSCHL : Condition Type
*----------------------------------------------------------------------*
FORM add_sel_kschl  USING    p_kschl.

  s_kschl-low    = p_kschl.
  s_kschl-sign   = 'I'.
  s_kschl-option = 'EQ'.
  APPEND s_kschl.

ENDFORM.                    " ADD_SEL_KSCHL

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

  DATA: lv_where TYPE string.
  lv_where = 'tvko~bukrs EQ p_bukrs'.
  IF p_vkorg IS NOT INITIAL.
    CONCATENATE lv_where 'AND h~vkorg = p_vkorg' INTO lv_where SEPARATED BY space.
  ENDIF.
  CONCATENATE lv_where 'AND h~vkbur   IN s_vkbur'
                       'AND h~vkgrp   IN s_vkgrp'
                       'AND c~vbegdat IN s_vbdat'
                       'AND c~venddat IN s_vndat'
                       'AND h~auart   IN s_auart'
                       'AND h~vbeln   IN s_vbeln'
                       'AND i~abgru   IN s_abgru'
                       'AND s~gbsta   IN s_gbsta'
                       'AND s~fksaa   IN s_fksaa'
                       'AND h~vbtyp   =  ''G'''
                       'AND c~vposn   = 0'
    INTO lv_where SEPARATED BY space.

* Contract items
*  SELECT i~vbeln i~posnr h~knumv c~vkuegru c~veindat
*         INTO TABLE gt_vbap
*         FROM vbap AS i
*         INNER JOIN vbak AS h
*                    ON h~vbeln = i~vbeln
*         INNER JOIN veda AS c
*                    ON c~vbeln = i~vbeln
*         INNER JOIN vbup AS s
*                    ON s~vbeln = i~vbeln  AND
*                       s~posnr = i~posnr
*         WHERE h~vkorg   =  p_vkorg
*           AND h~vkbur   IN s_vkbur
*           AND h~vkgrp   IN s_vkgrp
*           AND c~vbegdat IN s_vbdat
*           AND c~venddat IN s_vndat
*           AND h~auart   IN s_auart
*           AND h~vbeln   IN s_vbeln
*           AND i~abgru   IN s_abgru
*           AND s~gbsta   IN s_gbsta
*           AND s~fksaa   IN s_fksaa
*           AND h~vbtyp   =  'G'
*           AND c~vposn   = 0.
  SELECT i~vbeln i~posnr h~knumv c~vkuegru c~veindat
         INTO TABLE gt_vbap
         FROM vbap AS i
         INNER JOIN vbak AS h
                    ON h~vbeln = i~vbeln
         INNER JOIN veda AS c
                    ON c~vbeln = i~vbeln
         INNER JOIN vbup AS s
                    ON s~vbeln = i~vbeln  AND
                       s~posnr = i~posnr
         INNER JOIN tvko AS tvko
           ON tvko~vkorg EQ h~vkorg
         WHERE (lv_where).
  CHECK NOT gt_vbap[] IS INITIAL.

* Check cancellation
  IF NOT p_canc_r IS INITIAL.
    DELETE gt_vbap WHERE NOT vkuegru IS INITIAL.
  ENDIF.
  IF NOT p_canc_d IS INITIAL.
    DELETE gt_vbap WHERE NOT veindat IS INITIAL.
  ENDIF.

  SORT gt_vbap BY vbeln posnr.

* Item condition data
  SELECT knumv kposn kschl kbetr waers kpein kmein kwert krech
         INTO TABLE gt_konv
         FROM konv
         FOR ALL ENTRIES IN gt_vbap
         WHERE knumv  = gt_vbap-knumv
           AND kposn  = gt_vbap-posnr
           AND kschl IN s_kschl.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_vbap.

*   Conditions
    LOOP AT gt_konv WHERE knumv = gt_vbap-knumv
                      AND kposn = gt_vbap-posnr.

      CLEAR: gt_out.
      MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03,
                   gt_out-tab04, gt_out-tab05, gt_out-tab06,
                   gt_out-tab07, gt_out-tab08.

*     Percentage
      IF gt_konv-krech = 'A'.
        gt_konv-kbetr = gt_konv-kbetr / 10.
      ENDIF.

      gt_out-vbeln = gt_vbap-vbeln.
      gt_out-posnr = gt_vbap-posnr.
      gt_out-kschl = gt_konv-kschl.
      gt_out-waers = gt_konv-waers.
      gt_out-kmein = gt_konv-kmein.
*     Packed fields
      WRITE gt_konv-kbetr TO gt_out-kbetr CURRENCY gt_konv-waers.
      WRITE gt_konv-kpein TO gt_out-kpein UNIT gt_konv-kmein.
      WRITE gt_konv-kwert TO gt_out-kwert CURRENCY gt_konv-waers.

      APPEND gt_out.

    ENDLOOP.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

* Open file
*  CONCATENATE 'CONTRACT_ITM_COND' p_vkorg gv_ersda
  CONCATENATE 'CONTRACT_ITM_COND' p_bukrs
             INTO gv_ofile SEPARATED BY '_'.
  IF p_vkorg IS NOT INITIAL.
    CONCATENATE gv_ofile p_vkorg
               INTO gv_ofile SEPARATED BY '_'.
  ENDIF.
  CONCATENATE gv_ofile gv_ersda
             INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM fill_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

* Fill output
  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  FILL_HEADER
*&---------------------------------------------------------------------*
*       Fill header
*----------------------------------------------------------------------*
FORM fill_header .

  CLEAR: gt_outh.
  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03,
               gt_outh-tab04, gt_outh-tab05, gt_outh-tab06,
               gt_outh-tab07, gt_outh-tab08.

  gt_outh-vbeln  = 'VBELN'.
  gt_outh-posnr  = 'POSNR'.
  gt_outh-kschl  = 'KSCHL'.
  gt_outh-kbetr  = 'KBETR'.
  gt_outh-waers  = 'WAERS'.
  gt_outh-kpein  = 'KPEIN'.
  gt_outh-kmein  = 'KMEIN'.
  gt_outh-kwert  = 'KWERT'.
  APPEND gt_outh.

  gt_outh-vbeln  = 'Contract'.
  gt_outh-posnr  = 'Item'.
  gt_outh-kschl  = 'Type'.
  gt_outh-kbetr  = 'Amount'.
  gt_outh-waers  = 'Curr.'.
  gt_outh-kpein  = 'Pric. Unit'.
  gt_outh-kmein  = 'UoM'.
  gt_outh-kwert  = 'Cond. Value'.
  APPEND gt_outh.

ENDFORM.                    " FILL_HEADER

*Text symbol text£º
*E01:No contracts selected
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_BUKRS:D       .
*P_CANC_D:        Check cancellation receipt
*P_CANC_R:        Check cancellation reason
*P_VKORG:D       .
*S_ABGRU:D       .
*S_AUART:D       .
*S_FKSAA:D       .
*S_GBSTA:D       .
*S_KSCHL:D       .
*S_VBDAT:D       .
*S_VBELN:D       .
*S_VKBUR:D       .
*S_VKGRP:D       .
*S_VNDAT:D       .
