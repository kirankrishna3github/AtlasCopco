*&---------------------------------------------------------------------*
*& Report  YSE_MONITORING_VT_SAP_VTID
*&
*&---------------------------------------------------------------------*
*&
*& Monitoring tool VisiTour vs. SAP : VTID
*&
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 10.12.2012
*  Change Request Number : BUGFIX
*  Transport request Nr. : CD1K974167
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. |   DATE     | NAME               | CORRECT. NR| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                    | CD1K9..... | CR....      *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_monitoring_vt_sap_vtid.

* ALV grid
TYPE-POOLS: slis.

*----------------------------------------------------------------------*
* BDC Tables                                                           *
*----------------------------------------------------------------------*
DATA: i_bdcdata      LIKE bdcdata OCCURS 0 WITH HEADER LINE,
      struct_bdcdata TYPE bdcdata.

*BDC Structure for Messages
DATA : BEGIN OF i_messtab OCCURS 0.
        INCLUDE STRUCTURE bdcmsgcoll.
DATA : END OF i_messtab.

*----------------------------------------------------------------------*
* Internal tables for BAPI-ALM_ORDER_GETDETAIL                         *
*----------------------------------------------------------------------*
DATA: BEGIN OF i_es_header OCCURS 100.
        INCLUDE STRUCTURE bapi_alm_order_header_e.
DATA: END OF i_es_header.

DATA: BEGIN OF i_operations OCCURS 100.
        INCLUDE STRUCTURE bapi_alm_order_operation_e.
DATA: END OF i_operations.

DATA: BEGIN OF i_return OCCURS 100.
        INCLUDE STRUCTURE bapiret2.
DATA: END OF i_return.

DATA: lt_methods      LIKE bapi_alm_order_method  OCCURS 0
                           WITH HEADER LINE,
      lt_component    LIKE bapi_alm_order_component  OCCURS 0
                           WITH HEADER LINE,
      lt_operation    LIKE bapi_alm_order_operation  OCCURS 0
                           WITH HEADER LINE.

* Return
DATA: lt_return   TYPE STANDARD TABLE OF bapiret2 WITH HEADER LINE,
      ls_return   LIKE bapiret2.

DATA: BEGIN OF it_vtin  OCCURS 0,
        line(50)   TYPE c,
      END OF it_vtin.

DATA: BEGIN OF it_vt  OCCURS 0,
        extid        TYPE yextid,
        vtid         TYPE yvtid,
      END OF it_vt.

DATA: BEGIN OF it_out  OCCURS 0,
        aufnr        TYPE aufnr,
        oper         TYPE vornr,
        vtid_vt      TYPE yvtid,
        vtid_sap     TYPE yvtid,
        stat_clsd    TYPE xfeld,
        selkz        TYPE xfeld,
        zzcol        TYPE col_code,
      END OF it_out.

* ALV
DATA: xv_variant         LIKE disvariant,
      xv_variant_flag    TYPE c,
      xv_sd_alv_variant  LIKE disvariant,
      xt_fcat            TYPE slis_t_fieldcat_alv,
      ls_fcat            LIKE LINE OF xt_fcat,
      xt_alv_sort        TYPE slis_t_sortinfo_alv,
      xv_user_command    TYPE slis_formname  VALUE 'USER_COMMAND',
      xv_variant_save    TYPE c              VALUE 'U',
      xv_layout          TYPE slis_layout_alv,
      xv_grid            TYPE lvc_s_glay,
      xt_events          TYPE slis_t_event,
      ls_events          TYPE slis_alv_event,
      xt_event_exits     TYPE slis_t_event_exit.

DATA: gv_status          TYPE usrflag,
      gv_aufnr           TYPE aufnr,
      gv_oper            TYPE vornr,
      gv_error           TYPE xfeld,
      gv_oper_index      TYPE sytabix,
      gv_count           TYPE sytabix,
      gv_col(10)         TYPE n,
      gv_repid           LIKE sy-repid,
      gv_answer          TYPE c,
      gv_text            TYPE char80,
      gv_stat_nok        TYPE xfeld,
      gv_stat_rel        TYPE xfeld,
      gv_stat_clsd       TYPE xfeld,
      gv_objnr           TYPE j_objnr,
      gv_aufpl           TYPE co_aufpl,
      wa_jest            TYPE jest,
      gv_cnt_upd         TYPE i,
      gv_mode(1)         TYPE c  VALUE 'N'.

CONSTANTS:  c_x                         VALUE 'X',
            c_msgty_e                   VALUE 'E',
            c_msgty_s                   VALUE 'S',
            c_msgty_w                   VALUE 'W',
            c_blank                     VALUE ' ',
            c_0010(4)                   VALUE '0010',
            c_stat_rel   TYPE j_status  VALUE 'I0002',
            c_stat_cnf   TYPE j_status  VALUE 'I0009',
            c_stat_teco  TYPE j_status  VALUE 'I0045',
            c_stat_clsd  TYPE j_status  VALUE 'I0046',
            c_z000001(7)                VALUE 'Z000001'.


*&---------------------------------------------------------------------*
*&  Selection screen                                                   *
*&---------------------------------------------------------------------*
* Input file
PARAMETERS: p_infile TYPE  rlgrap-filename LOWER CASE.
*                 DEFAULT 'C:\VT.MONITOR.xls'.


AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_infile.

* Get the filename
  PERFORM get_filename USING p_infile.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

* Read input file
  PERFORM read_file.

* Compare input from VisiTour with SEO in SAP
  PERFORM compare_data.

* Display differences
  IF it_out[] IS INITIAL.
    MESSAGE i001(00) WITH 'No differences found'(i01).
  ELSE.
    PERFORM alv_fcat.
    PERFORM alv_layout.
    PERFORM alv_events.
    PERFORM alv_display.
  ENDIF.

*&---------------------------------------------------------------------*
*&  Forms                                                              *
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  GET_FILENAME
*&---------------------------------------------------------------------*
*       Get the filename
*----------------------------------------------------------------------*
*      -->P_INFILE : Input file
*----------------------------------------------------------------------*
FORM get_filename  USING    p_infile.

  CALL FUNCTION 'WS_FILENAME_GET'
    EXPORTING
      def_path         = 'C:\'
      mask             = ',*.XLS.'
    IMPORTING
      filename         = p_infile
    EXCEPTIONS
      inv_winsys       = 1
      no_batch         = 2
      selection_cancel = 3
      selection_error  = 4
      OTHERS           = 5.

  IF sy-subrc <> 0.
  ENDIF.

ENDFORM.                    " GET_FILENAME

*&---------------------------------------------------------------------*
*&      Form  READ_FILE
*&---------------------------------------------------------------------*
*       Read input file
*----------------------------------------------------------------------*
FORM read_file .

* Read EXCEL file
  CALL FUNCTION 'FAA_FILE_UPLOAD_EXCEL'
    EXPORTING
      i_filename                 = p_infile
*      I_TEST                     =
*      I_UCOMM                    =
*      I_MAXCOLS                  =
      i_delimiter                = '|'
    TABLES
      et_filecontent             = it_vtin
    EXCEPTIONS
      error_accessing_file       = 1
      OTHERS                     = 2
            .
  IF sy-subrc <> 0.
*   MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*           WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

* Store in an internal table
  LOOP AT it_vtin.
    CLEAR it_vt.
    SPLIT it_vtin AT '|' INTO it_vt-extid it_vt-vtid.
    APPEND it_vt.
  ENDLOOP.

ENDFORM.                    " READ_FILE

*&---------------------------------------------------------------------*
*&      Form  COMPARE_DATA
*&---------------------------------------------------------------------*
*       Compare input from VisiTour with SEO in SAP
*----------------------------------------------------------------------*
FORM compare_data .

  LOOP AT it_vt.
*   Split external id in ordernumber & operation
    SPLIT it_vt-extid AT '/' INTO gv_aufnr gv_oper.
    PERFORM add_zeros USING gv_aufnr.
    PERFORM add_zeros USING gv_oper.
*   Progress indicator
    gv_text = 'Operation & of Order & is being processed'(i02).
    REPLACE '&' WITH gv_oper  INTO gv_text.
    REPLACE '&' WITH gv_aufnr INTO gv_text.
    CONDENSE gv_text.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        text = gv_text.

*   Get SAP data
    PERFORM get_sap_data USING gv_aufnr gv_oper.
    CHECK gv_error IS INITIAL.
*   VTID
    IF it_vt-vtid NE i_operations-usr03.
      gv_error = c_x.
    ENDIF.

*   Error
    IF gv_error = c_x.
*     Get status 'Closed'
      PERFORM get_stat_clsd.
*     Fill internal table for output
      PERFORM fill_output.
    ENDIF.

  ENDLOOP.

ENDFORM.                    " COMPARE_DATA

*&---------------------------------------------------------------------*
*&      Form  ADD_ZEROS
*&---------------------------------------------------------------------*
*       Add leading zeros
*----------------------------------------------------------------------*
*      -->P_FIELD : field
*----------------------------------------------------------------------*
FORM add_zeros  USING    p_field.

  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
    EXPORTING
      input  = p_field
    IMPORTING
      output = p_field.

ENDFORM.                    " ADD_ZEROS

*&---------------------------------------------------------------------*
*&      Form  GET_SAP_DATA
*&---------------------------------------------------------------------*
*       Get SAP data
*----------------------------------------------------------------------*
*      -->P_AUFNR : Service order number
*      -->P_OPER  : Operation number
*----------------------------------------------------------------------*
FORM get_sap_data  USING  p_aufnr TYPE aufnr
                          p_oper  TYPE vornr.

  CLEAR gv_error.

  CALL FUNCTION 'BAPI_ALM_ORDER_GET_DETAIL'
    EXPORTING
      number        = p_aufnr
    IMPORTING
      es_header     = i_es_header
    TABLES
      et_operations = i_operations
      return        = i_return[].

*---------------------------------------------------------------*
* Check if Ordernumber is available in SAP                      *
*---------------------------------------------------------------*
  IF NOT i_return[] IS INITIAL.
    READ TABLE i_return INDEX 1.
    IF i_return-type NE c_msgty_s.
      gv_error = c_x.
      RETURN.
    ENDIF.
  ENDIF.

*---------------------------------------------------------------*
* Check if operation exists in SAP                              *
*---------------------------------------------------------------*
  SORT i_operations[] BY activity.
  IF NOT i_operations[] IS INITIAL AND
     NOT p_oper IS INITIAL.
    READ TABLE i_operations WITH KEY activity = p_oper
                            BINARY SEARCH.
    IF sy-subrc <> 0.
      gv_error = c_x.
      RETURN.
    ELSE.
      gv_oper_index = sy-tabix.
    ENDIF.
  ENDIF.

ENDFORM.                    " GET_SAP_DATA

*&---------------------------------------------------------------------*
*&      Form  GET_STAT_CLSD
*&---------------------------------------------------------------------*
*       Get status 'Closed'
*----------------------------------------------------------------------*
FORM get_stat_clsd .

  CLEAR gv_stat_clsd.

* Get status CLSD (Closed)
  CALL FUNCTION 'STATUS_CHECK'
    EXPORTING
      objnr             = i_es_header-object_no
      status            = c_stat_clsd
    EXCEPTIONS
      object_not_found  = 1
      status_not_active = 2
      OTHERS            = 3.

  IF sy-subrc = 0.
    gv_stat_clsd = 'X'.
    RETURN.
  ELSE.
*    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

ENDFORM.                    " GET_STAT_CLSD

*&---------------------------------------------------------------------*
*&      Form  FILL_OUTPUT
*&---------------------------------------------------------------------*
*       Fill internal table for output
*----------------------------------------------------------------------*
FORM fill_output .

  it_out-aufnr      = gv_aufnr.
  it_out-oper       = gv_oper.
  it_out-vtid_vt    = it_vt-vtid.
  it_out-vtid_sap   = i_operations-usr03.
  it_out-stat_clsd  = gv_stat_clsd.
  APPEND it_out.

ENDFORM.                    " FILL_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  ALV_FCAT
*&---------------------------------------------------------------------*
*       ALV field catalog
*----------------------------------------------------------------------*
FORM alv_fcat .

** Create Fieldcatalogue from internal table
** CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE' ...

  CLEAR gv_col.

* Field definitions
  PERFORM add_field USING 'AUFNR'      12 'Service Order'(t01)    'X'.
  PERFORM add_field USING 'OPER'        8 'Operation'(t02)        ' '.
  PERFORM add_field USING 'VTID_VT'    10 'VTID VT'(t03)          ' '.
  PERFORM add_field USING 'VTID_SAP'   10 'VTID SAP'(t04)         ' '.
  PERFORM add_field USING 'STAT_CLSD'  15 'Stat. Closed SAP'(t05) ' '.

ENDFORM.                    " ALV_FCAT

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr
                         p_key.

  gv_col = gv_col + 1.

  CLEAR ls_fcat.
  ls_fcat-col_pos    = gv_col.
  ls_fcat-fieldname  = p_field.
  ls_fcat-outputlen  = p_len.
  ls_fcat-seltext_l  = p_descr.
  ls_fcat-seltext_m  = p_descr.
  ls_fcat-seltext_s  = p_descr.
  ls_fcat-fix_column = 'X'.
  ls_fcat-emphasize  = 'X'.
  ls_fcat-hotspot    = p_key.
  ls_fcat-key        = p_key.

* Checkbox
  IF p_field = 'STAT_CLSD'.
    ls_fcat-checkbox   = 'X'.
  ENDIF.

* Suppress leading zeros
  IF p_field = 'AUFNR' OR
     p_field = 'OPER'.
    ls_fcat-edit_mask = '==ALPHA'.
  ENDIF.

  APPEND ls_fcat TO xt_fcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT
*&---------------------------------------------------------------------*
*       Modify ALV layout
*----------------------------------------------------------------------*
FORM alv_layout .

* Define layout
  xv_layout-zebra               = c_x.
  xv_layout-get_selinfos        = c_x.
  xv_layout-detail_popup        = c_x.
  xv_layout-box_fieldname       = 'SELKZ'.
  xv_layout-no_keyfix           = c_x.
  xv_layout-group_change_edit   = c_x.
  xv_layout-info_fieldname      = 'ZZCOL'.
*  xv_layout-totals_before_items = c_x.
*  xv_layout-key_hotspot         = c_x.
*  xv_layout-colwidth_optimize   = c_x.
*  xv_layout-smalltitle          = c_x.
*  xv_layout-grid_title          = 'title'.

* Define grid settings
  xv_grid-coll_end_l = c_x.

ENDFORM.                    " ALV_LAYOUT

*&---------------------------------------------------------------------*
*&      Form  ALV_EVENTS
*&---------------------------------------------------------------------*
*       Define ALV events
*----------------------------------------------------------------------*
FORM alv_events .

* Fill events
  ls_events-form = ls_events-name = 'USER_COMMAND'.
  APPEND ls_events TO xt_events.
  ls_events-form = ls_events-name = 'PF_STATUS_SET'.
  APPEND ls_events TO xt_events.

ENDFORM.                    " ALV_EVENTS

*&---------------------------------------------------------------------*
*&      Form  USER_COMMAND
*&---------------------------------------------------------------------*
*       User Command callback routine for ALV
*----------------------------------------------------------------------*
FORM user_command USING r_ucomm LIKE sy-ucomm
                        rs_selfield TYPE slis_selfield.

  CASE r_ucomm.

    WHEN 'UPD_SEO'.
*     Update Service Orders
      PERFORM update_service_orders.
      rs_selfield-refresh = c_x.

    WHEN '&IC1'.
*     Selection
      IF NOT rs_selfield-value IS INITIAL.
        CASE rs_selfield-fieldname.
          WHEN 'AUFNR'.
*           Display Service Order
            SET PARAMETER ID 'ANR' FIELD rs_selfield-value.
            CALL TRANSACTION 'IW33' AND SKIP FIRST SCREEN.
        ENDCASE.
      ENDIF.

  ENDCASE.

ENDFORM.                    " user_command

*&---------------------------------------------------------------------*
*&      Form  PF_STATUS_SET
*&---------------------------------------------------------------------*
*       PF-status callback routine for ALV
*----------------------------------------------------------------------*
FORM pf_status_set USING rt_extab TYPE slis_t_extab.

  SET PF-STATUS 'YSE_MON'.

ENDFORM.                    " PF_STATUS_SET

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       Display ALV grid
*----------------------------------------------------------------------*
FORM alv_display .

*  xv_sd_alv_variant = xv_variant.
  gv_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = gv_repid
      i_callback_user_command = xv_user_command
*      i_callback_top_of_page  = 'ALV_TOP'
      i_grid_title            = 'Monitoring VisiTour vs. SAP (VTID)'(h01)
      is_layout               = xv_layout
      it_fieldcat             = xt_fcat
      i_grid_settings         = xv_grid
      it_events               = xt_events
      it_sort                 = xt_alv_sort
      i_default               = 'X'
      i_save                  = 'A'
*      is_variant              = xv_sd_alv_variant
      i_screen_start_column   = 0
      i_screen_start_line     = 0
      i_screen_end_column     = 0
      i_screen_end_line       = 0
    TABLES
      t_outtab                = it_out
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.

  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  UPDATE_SERVICE_ORDERS
*&---------------------------------------------------------------------*
*       Update Service Orders
*----------------------------------------------------------------------*
FORM update_service_orders .

* Records selected ?
  LOOP AT it_out WHERE selkz = c_x.
    EXIT.
  ENDLOOP.
  CHECK sy-subrc = 0.

* Confirm reprocessing
  CALL FUNCTION 'POPUP_TO_CONFIRM'
    EXPORTING
      titlebar              = text-h01
      text_question         = text-q01
      icon_button_1         = 'ICON_OKAY'
      icon_button_2         = 'ICON_CANCEL'
      default_button        = '2'
      display_cancel_button = space
    IMPORTING
      answer                = gv_answer
    EXCEPTIONS
      text_not_found        = 1
      OTHERS                = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  CHECK gv_answer = '1'.

  CLEAR gv_cnt_upd.

* Loop on the selected IDocs
  LOOP AT it_out WHERE selkz = c_x.

*   Progress indicator
    gv_text = 'Operation & of Order & is being processed'(i02).
    REPLACE '&' WITH it_out-oper  INTO gv_text.
    REPLACE '&' WITH it_out-aufnr INTO gv_text.
    CONDENSE gv_text.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        text = gv_text.

*   Get SAP data
    PERFORM get_sap_data USING it_out-aufnr it_out-oper.
    CHECK gv_error IS INITIAL.

*   Check if status is not CNF, TECO or CLSD
    CLEAR gv_stat_nok.
    PERFORM check_status.
    CHECK gv_stat_nok IS INITIAL.

*   Check status REL
    PERFORM check_status_rel.

*   Update operation data
    PERFORM update_operation.

*   Update OK ?
    IF gv_error IS INITIAL.
*     Count
      gv_cnt_upd = gv_cnt_upd + 1.
*     Remove from table
      DELETE it_out.
    ENDIF.

  ENDLOOP.

  MESSAGE i001(00) WITH 'Number of Service Orders updated :'(i03)
                        gv_cnt_upd.

ENDFORM.                    " UPDATE_SERVICE_ORDERS

*&---------------------------------------------------------------------*
*&      Form  CHECK_STATUS
*&---------------------------------------------------------------------*
*       Check if status is not CNF, TECO or CLSD
*----------------------------------------------------------------------*
FORM check_status .

  CONCATENATE 'OR' it_out-aufnr INTO gv_objnr.

* Check status CNF (Confirmed)
  CALL FUNCTION 'STATUS_CHECK'
    EXPORTING
      objnr             = gv_objnr
      status            = c_stat_cnf
    EXCEPTIONS
      object_not_found  = 1
      status_not_active = 2
      OTHERS            = 3.
  IF sy-subrc = 0.
    gv_stat_nok = 'X'.
    RETURN.
  ELSE.
*    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

* Check status TECO (Technical Completed)
  CALL FUNCTION 'STATUS_CHECK'
    EXPORTING
      objnr             = gv_objnr
      status            = c_stat_teco
    EXCEPTIONS
      object_not_found  = 1
      status_not_active = 2
      OTHERS            = 3.
  IF sy-subrc = 0.
    gv_stat_nok = 'X'.
    RETURN.
  ELSE.
*    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

* Check status CLSD (Closed)
  CALL FUNCTION 'STATUS_CHECK'
    EXPORTING
      objnr             = gv_objnr
      status            = c_stat_clsd
    EXCEPTIONS
      object_not_found  = 1
      status_not_active = 2
      OTHERS            = 3.
  IF sy-subrc = 0.
    gv_stat_nok = 'X'.
    RETURN.
  ELSE.
*    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

ENDFORM.                    " CHECK_STATUS

*&---------------------------------------------------------------------*
*&      Form  CHECK_STATUS_REL
*&---------------------------------------------------------------------*
*       Check status REL (Released)
*----------------------------------------------------------------------*
FORM check_status_rel .

  CONCATENATE 'OR' it_out-aufnr INTO gv_objnr.

  CLEAR gv_stat_rel.

  CALL FUNCTION 'STATUS_CHECK'
    EXPORTING
      objnr             = gv_objnr
      status            = c_stat_rel
    EXCEPTIONS
      object_not_found  = 1
      status_not_active = 2
      OTHERS            = 3.
  IF sy-subrc = 0.
    gv_stat_rel = 'X'.
  ELSE.
*    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

ENDFORM.                    " CHECK_STATUS_REL

*&---------------------------------------------------------------------*
*&      Form  UPDATE_OPERATION
*&---------------------------------------------------------------------*
*       Update operation data
*----------------------------------------------------------------------*
FORM update_operation .

  REFRESH: i_bdcdata, i_messtab.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                 USING    'SAPLCOIH'  '0101'  'X' ' ' ' '
                 CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'CAUFVD-AUFNR' it_out-aufnr
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    ''  '' ' ' 'BDC_OKCODE'  '/00'
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

*----------------------------------------------------------------------*
*   Operations
*----------------------------------------------------------------------*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
               USING    'SAPLCOIH'  '3000'  'X' ''  ''
               CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
               USING    ''  ''  '' 'BDC_OKCODE'  '=VGUE'
               CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

*----------------------------------------------------------------------*
*   Select all operations
*----------------------------------------------------------------------*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    'SAPLCOIH'  '3000'  'X' ''  ''
       CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
      USING    ''  ''  '' 'BDC_OKCODE' '=AMAK'
      CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

*----------------------------------------------------------------------*
*   General data
*----------------------------------------------------------------------*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
       USING    'SAPLCOIH'  '3000'  'X' ''  ''
       CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
      USING    ''  ''  '' 'BDC_OKCODE' '=VGD0'
      CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

*----------------------------------------------------------------------*
*   Hit the next operation button as many times as necessary to find
*   the correct operation
*----------------------------------------------------------------------*
  IF gv_oper_index > 1.
    gv_count = gv_oper_index - 1.
    DO gv_count TIMES.
      PERFORM fill_bdcdata IN PROGRAM yam_common_routines
           USING    'SAPLCOIH'  '3000'  'X' ''  ''
           CHANGING struct_bdcdata.
      APPEND struct_bdcdata  TO i_bdcdata.
      CLEAR  struct_bdcdata.

      PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  '' 'BDC_OKCODE' '=VG+'
          CHANGING struct_bdcdata.
      APPEND struct_bdcdata  TO i_bdcdata.
      CLEAR  struct_bdcdata.
    ENDDO.
  ENDIF.
**----------------------------------------------------------------------*
**   Change workcenter (eventually) and go to the tab dates
**----------------------------------------------------------------------*
*  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
*       USING    'SAPLCOIH'  '3000'  'X' ''  ''
*       CHANGING struct_bdcdata.
*  APPEND struct_bdcdata  TO i_bdcdata.
*  CLEAR  struct_bdcdata.
*
*  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
*          USING    ''  ''  '' 'BDC_OKCODE' '=VGD1'
*          CHANGING struct_bdcdata.
*  APPEND struct_bdcdata  TO i_bdcdata.
*  CLEAR  struct_bdcdata.
*
*  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
*               USING    'SAPLCOIH'  '3000'  'X' ''  ''
*               CHANGING struct_bdcdata.
*  APPEND struct_bdcdata  TO i_bdcdata.
*  CLEAR  struct_bdcdata.
*
*  gv_dur_a = it_out-dur_vt.
*  gv_arbei_p =   gv_dur_a / 60.
*  IF NOT gv_arbei_p IS INITIAL.
*    WRITE gv_arbei_p TO gv_arbei.
*    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
*                        USING    ''  ''  '' 'AFVGD-ARBEI' gv_arbei
*                        CHANGING struct_bdcdata.
*    APPEND struct_bdcdata  TO i_bdcdata.
*    CLEAR  struct_bdcdata.
*  ENDIF.
*
*  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
*          USING    ''  ''  '' 'BDC_OKCODE' '=VGD3'
*          CHANGING struct_bdcdata.
*  APPEND struct_bdcdata  TO i_bdcdata.
*  CLEAR  struct_bdcdata.

*----------------------------------------------------------------------*
*   To the tab with the enhancement
*----------------------------------------------------------------------*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    'SAPLCOIH'  '3000'  'X' ''  ''
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
          USING    ''  ''  '' 'BDC_OKCODE' '=VGD5'
          CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.
*----------------------------------------------------------------------*
*   Update the field 'field key'
*----------------------------------------------------------------------*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
     USING    'SAPLCOIH'  '3000'  'X' ''  ''
     CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
      USING    ''  ''  '' 'AFVGD-SLWID' c_z000001
      CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
        USING    ''  ''  '' 'BDC_OKCODE' '/00'
        CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
           USING    'SAPLCOIH'  '3000'  'X' ''  ''
           CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

* VTID
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
    USING    ''  ''  '' 'AFVGD-USR03' it_out-vtid_vt
    CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

** Order alrady released ?
*  IF NOT gv_stat_rel IS INITIAL.
* Save
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
      USING    ''  ''  '' 'BDC_OKCODE' '=BU'
      CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.
*  ELSE.
**   Release the order
*    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
*         USING    ''  ''  '' 'BDC_OKCODE' '=IHKZ'
*         CHANGING struct_bdcdata.
*    APPEND struct_bdcdata  TO i_bdcdata.
*    CLEAR  struct_bdcdata.
*
*    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
*           USING    'SAPLCOIH'  '3000'  'X' ''  ''
*           CHANGING struct_bdcdata.
*    APPEND struct_bdcdata  TO i_bdcdata.
*    CLEAR  struct_bdcdata.
*
*    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
*          USING    ''  ''  '' 'BDC_OKCODE' '=FREI'
*          CHANGING struct_bdcdata.
*    APPEND struct_bdcdata  TO i_bdcdata.
*    CLEAR  struct_bdcdata.
**   Save
*    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
*           USING    'SAPLCOIH'  '3000'  'X' ''  ''
*           CHANGING struct_bdcdata.
*    APPEND struct_bdcdata  TO i_bdcdata.
*    CLEAR  struct_bdcdata.
*
*    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
*          USING    ''  ''  '' 'BDC_OKCODE' '=BU'
*          CHANGING struct_bdcdata.
*    APPEND struct_bdcdata  TO i_bdcdata.
*    CLEAR  struct_bdcdata.
*  ENDIF.
*----------------------------------------------------------------------*
*   Call transaction IW32
*----------------------------------------------------------------------*
  CALL TRANSACTION  'IW32'  USING i_bdcdata
                            MODE gv_mode
                            UPDATE 'S'
                            MESSAGES INTO i_messtab.

* Check if BDC error
  IF NOT i_messtab[] IS INITIAL.
    LOOP AT i_messtab WHERE msgtyp = 'E'  OR
                            msgtyp = 'A'.
      gv_error = 'X'.
      EXIT.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " UPDATE_OPERATION

*Text symbol text£º
*H01:Monitoring VisiTour vs. SAP (VTID)
*I01:No differences found
*I02:Operation & of Order & is being processed
*I03:Number of Service Orders updated :
*Q01:You are about to update the selected Service Orders. Are you sure to continue ?
*T01:Service Order
*T02:Operation
*T03:VTID VT
*T04:VTID SAP

*T05:Stat. Closed SAP
*Selection text£º
*P_INFILE:        Input file
