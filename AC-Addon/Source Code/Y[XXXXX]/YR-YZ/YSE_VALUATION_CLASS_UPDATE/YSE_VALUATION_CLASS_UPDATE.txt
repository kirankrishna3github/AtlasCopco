*----------------------------------------------------------------------
* PROGRAM ID           : YSE_VALUATION_CLASS_UPDATE                    *
* PROGRAM TITLE        : Mass update valuation class                   *
* AUTHOR               : LUC MERTENS                                   *
* DATE                 : 05/05/2009                                    *
* DEVELOPMENT ID       :                                               *
* CHANGE REQUEST NUMBER: CD1K948033                                    *
* PROGRAM DESCRIPTION  : This is a program to update the valuation     *
*                        class via input file.                         *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME                 |CORR.NR.  | CHANGE REF.   *
*----------------------------------------------------------------------*
* MOD-001 |13.04.2010|L. Mertens USG Innotiv|CD1K955983|               *
*      - translate valuation class to uppercase before the auth.check  *
*----------------------------------------------------------------------*
* MOD-002 |11.06.2010|L. Mertens USG Innotiv|CD1K955983| CR1333        *
*      - new valuation class for production materials                  *
************************************************************************
REPORT YSE_VALUATION_CLASS_UPDATE.

*---------------------------------------------------------------------*
* TABLES DECLARATION
*---------------------------------------------------------------------*


*---------------------------------------------------------------------*
* DATA DECLARATIONS                                                   *
*---------------------------------------------------------------------*
DATA: i_bdcdata      LIKE bdcdata OCCURS 0 WITH HEADER LINE,
      gv_mode(1)     type c value 'N',
      struct_bdcdata TYPE bdcdata,
      gt_err         LIKE BDCMSGCOLL occurs 0 with header line.

data: begin of gv_irec occurs 0,
        field(26),
      end of gv_irec.

DATA: begin of i_logtab occurs 0,
        matnr      type matnr,
        msg(102)   type c,
      end of i_logtab.

data: begin of gt_valclass_entry occurs 0,
        matnr type matnr,
        bwkey type bwkey,
        bklas type bklas,
      end of gt_valclass_entry.

data: begin of gt_valclass_error occurs 0,
        matnr type matnr,
        bwkey type bwkey,
        bklas type bklas,
        ebeln type ebeln,
        ebelp type ebelp,
        wemng type weemg,
        menge type menge_d,
        ermsg(15),
      end of gt_valclass_error.

data: begin of gt_valclass_stock occurs 0,
        matnr type matnr,
        bwkey type bwkey,
        bklas type bklas,
        lgort type lgort_d,
        labst type labst,
      end of gt_valclass_stock.

data: begin of gt_ekpo occurs 0.
        include structure ekpo.
data: end of gt_ekpo.

data: begin of gt_mard occurs 0.
        include structure mard.
data: end of gt_mard.

data: begin of gt_eket occurs 0,
        ebeln type ebeln,
        ebelp type ebelp,
        etenr type eeten,
        wemng type weemg,
      end of gt_eket.

data: begin of gt_eket_aggr occurs 0,
        ebeln type ebeln,
        ebelp type ebelp,
        wemng type weemg,
      end of gt_eket_aggr.

data: begin of gt_ekbe occurs 0,
        ebeln type ebeln,
        ebelp type ebelp,
        zekkn type eeten,
        gjahr type dzekkn,
        belnr type mblnr,
        buzei type mblpo,
        menge type menge_d,
        shkzg type shkzg,
      end of gt_ekbe.

data: begin of gt_ekbe_aggr occurs 0,
        ebeln type ebeln,
        ebelp type ebelp,
        menge type menge_d,
      end of gt_ekbe_aggr.

* begin of insert MOD-002
data: begin of it_mbew occurs 0,
        bwtar type bwtar_d,
      end of it_mbew.

data: begin of it_mchb occurs 0,
        matnr type matnr,
        werks type mchb-werks,
        lgort type lgort_d,
        charg type mchb-charg,
        clabs type mchb-clabs,
      end of it_mchb.
* end of insert MOD-002

DATA: gv_filename       type string,
      gv_bklas          type bklas,
* begin of insert MOD-002
      gv_bukrs          type bukrs,
      gv_batch(1)       type c,
      gv_diff(1)        type c,
* end of insert MOD-002
      gv_bwart          type bwart,
      gv_tabix          type sy-tabix,
      gv_mestx          like t100-text,
      gv_restrstock,
      gv_mbew_salk3     type salk3,
      gv_ebew_salk3     type salk3,
      wa_valclass_stock like gt_valclass_stock,
      wa_valclass_entry like gt_valclass_entry,
      wa_eket           like gt_eket,
      wa_eket_aggr      like gt_eket_aggr,
      wa_ekbe           like gt_ekbe,
      wa_ekbe_aggr      like gt_ekbe_aggr.

data: wa_string   TYPE string.

*---------------------------------------------------------------------*
* CONSTANT DECLARATIONS                                               *
*---------------------------------------------------------------------*
CONSTANTS: c_filetype(10)   TYPE c VALUE 'ASC',     " FILE TYPE
           c_blank          TYPE c VALUE ' ',       " no separator
           c_l              type c value 'L',       " deletion
           c_2              type c value '2',       " invoice receipt
           c_562(3)         type c value '562',     " Remove stock
           c_561(3)         type c value '561',     " Undo removal
           con_tab  type c value cl_abap_char_utilities=>HORIZONTAL_TAB.

*---------------------------------------------------------------------*
* PARAMETERS                                                          *
*---------------------------------------------------------------------*
parameters: P_INFILE LIKE /sapdmc/lsoinp-filename.  " FILE

*---------------------------------------------------------------------*
* AT selection-screen                                                 *
*---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_infile.

* get filename
  CALL FUNCTION 'WS_FILENAME_GET'
    EXPORTING
      DEF_PATH         = 'C:\'
      MASK             = ',*,*.txt.'
    IMPORTING
      FILENAME         = p_infile
    EXCEPTIONS
      INV_WINSYS       = 1
      NO_BATCH         = 2
      SELECTION_CANCEL = 3
      SELECTION_ERROR  = 4
      OTHERS           = 5.

*---------------------------------------------------------------------*
* START-OF-SELECTION                                                  *
*---------------------------------------------------------------------*
START-OF-SELECTION.

  gv_filename = p_infile.

* Read inputfile (text tab delimited .txt) from pres.server
  class cl_abap_char_utilities definition load.

  PERFORM get_from_pres IN PROGRAM yam_common_routines
                                  TABLES  gv_irec
                                  USING   gv_filename
                                          c_filetype
                                          c_blank.

  refresh gt_valclass_entry.

  loop at gv_irec INTO wa_string.
    SPLIT wa_string AT CON_TAB
     into gt_valclass_entry-matnr
          gt_valclass_entry-bwkey
          gt_valclass_entry-bklas.
    append gt_valclass_entry.
    clear gt_valclass_entry.
  endloop.

* begin of insert MOD-002
* Check if plant belongs to company which works with batches
  loop at gt_valclass_entry.
    SELECT SINGLE bukrs INTO gv_bukrs
           FROM t001k
           WHERE bwkey = gt_valclass_entry-bwkey.

    IF sy-subrc = 0.
      SELECT SINGLE bukrs INTO gv_bukrs
             FROM yse_cc_act_cost
             WHERE bukrs = gv_bukrs.

      IF sy-subrc = 0.
        gv_batch = 'X'.
      ENDIF.
    endif.
    exit.         " only once
  endloop.
* end of insert MOD-002

* Authority check on plants used in file and
* Verify valuation class of inputfile with existing values from material master
  loop at gt_valclass_entry.

*.. Convert to Internal Material Number
    CALL FUNCTION 'CONVERSION_EXIT_MATN1_INPUT'
      EXPORTING
        input  = gt_valclass_entry-matnr
      IMPORTING
        output = gt_valclass_entry-matnr.

* begin of insert MOD-002
    IF gv_batch = 'X'.
      clear: gv_bklas,
             gv_diff.
      SELECT bklas into gv_bklas
        from mbew where matnr = gt_valclass_entry-matnr
                    and bwkey = gt_valclass_entry-bwkey.

        IF gv_bklas <> gt_valclass_entry-bklas.
          gv_diff = 'X'.
          exit.
        ENDIF.
      ENDSELECT.

      IF gv_diff <> 'X'.
        delete gt_valclass_entry.
      else.
        TRANSLATE gt_valclass_entry-bwkey TO UPPER CASE.
        AUTHORITY-CHECK OBJECT 'I_IWERK'
                   ID 'TCD'   FIELD sy-tcode
                   ID 'IWERK' FIELD gt_valclass_entry-bwkey.

        if sy-subrc <> 0.
          write: / text-e01, gt_valclass_entry-bwkey.
          delete gt_valclass_entry.
        else.
          modify gt_valclass_entry transporting matnr.
        endif.
      ENDIF.
    else.
* end of insert MOD-002
      clear gv_bklas.
      select single bklas into gv_bklas
        from mbew where matnr = gt_valclass_entry-matnr
                    and bwkey = gt_valclass_entry-bwkey.

      if gv_bklas = gt_valclass_entry-bklas.
        delete gt_valclass_entry.
      else.
* begin of insert MOD-001
        TRANSLATE gt_valclass_entry-bwkey TO UPPER CASE.
* end of insert MOD-001
        AUTHORITY-CHECK OBJECT 'I_IWERK'
                   ID 'TCD'   FIELD sy-tcode
                   ID 'IWERK' FIELD gt_valclass_entry-bwkey.

        if sy-subrc <> 0.
          write: / text-e01, gt_valclass_entry-bwkey.
          delete gt_valclass_entry.
        else.
          modify gt_valclass_entry transporting matnr.
        endif.
      endif.
* begin of insert MOD-002
    endif.
* end of insert MOD-002
  endloop.

* Check for none open PO's to update
  loop at gt_valclass_entry.
    gv_tabix = sy-tabix.

    refresh gt_ekpo.
    select * into table gt_ekpo
      from ekpo where matnr =  gt_valclass_entry-matnr
                  and werks =  gt_valclass_entry-bwkey
                  and loekz <> c_l.

*.. calculate GR qty
    check not gt_ekpo[] is initial.
    refresh gt_eket.
    select ebeln ebelp etenr wemng
      from eket
      into table gt_eket
      for all entries in gt_ekpo
      where ebeln = gt_ekpo-ebeln
        and ebelp = gt_ekpo-ebelp.

    sort gt_eket by ebeln ebelp.

    loop at gt_eket into wa_eket.
      move-corresponding wa_eket to wa_eket_aggr.
      collect wa_eket_aggr into gt_eket_aggr.
    endloop.

*.. calculate IR qty
    refresh gt_ekbe.
    select ebeln ebelp zekkn gjahr belnr buzei menge shkzg
      from ekbe
      into corresponding fields of table gt_ekbe
      for all entries in gt_ekpo
      where ebeln = gt_ekpo-ebeln
        and ebelp = gt_ekpo-ebelp
        and vgabe = c_2.

    sort gt_ekbe by ebeln ebelp.

    loop at gt_ekbe into wa_ekbe.
      if wa_ekbe-shkzg = 'H'.
        wa_ekbe-menge = wa_ekbe-menge * -1.
      endif.
      move-corresponding wa_ekbe to wa_ekbe_aggr.
      collect wa_ekbe_aggr into gt_ekbe_aggr.
    endloop.

*.. Verify GR with IR qty
    loop at gt_eket_aggr.
      READ TABLE gt_ekbe_aggr
            WITH KEY ebeln = gt_eket_aggr-ebeln
                     ebelp = gt_eket_aggr-ebelp.

      IF sy-subrc <> 0.                        " IR not found
        DELETE gt_valclass_entry INDEX gv_tabix.

        move-corresponding gt_valclass_entry to gt_valclass_error.
        move-corresponding gt_eket_aggr      to gt_valclass_error.
        move text-e02                        to gt_valclass_error-ermsg.
        append gt_valclass_error.
        clear gt_valclass_error.
        exit.
      else.
        if gt_eket_aggr-wemng <> gt_ekbe_aggr-menge.     " GR <> IR
          DELETE gt_valclass_entry INDEX gv_tabix.

          move-corresponding gt_valclass_entry to gt_valclass_error.
          move-corresponding gt_eket_aggr      to gt_valclass_error.
          move-corresponding gt_ekbe_aggr      to gt_valclass_error.
          move text-e02                        to gt_valclass_error-ermsg.
          append gt_valclass_error.
          clear gt_valclass_error.
          exit.
        endif.
      endif.
    endloop.
  endloop.

* Check if valuated stock exists
  loop at gt_valclass_entry.
    gv_tabix = sy-tabix.
    clear: gv_mbew_salk3, gv_ebew_salk3.

    SELECT SINGLE salk3
         INTO gv_mbew_salk3
         FROM mbew WHERE matnr = gt_valclass_entry-matnr
* begin of change MOD-002
*                     AND bwkey = gt_valclass_entry-bwkey.
                     AND bwkey = gt_valclass_entry-bwkey
                     AND bwtar = space.
* end of change MOD-002

    IF sy-subrc = 0.
      SELECT salk3
           INTO gv_ebew_salk3
           FROM ebew WHERE matnr = gt_valclass_entry-matnr
                       AND bwkey = gt_valclass_entry-bwkey.
        ADD gv_ebew_salk3 TO gv_mbew_salk3.
      ENDSELECT.

      IF gv_mbew_salk3 <= 0.       " No stock
*...... valuation class can be changed
      else.
*...... only unrestricted stock can be removed,
*...... so verify no other stock than unrestricted stock exists
        refresh gt_mard.
        select * into table gt_mard
          from MARD where matnr = gt_valclass_entry-matnr
                      and werks = gt_valclass_entry-bwkey.

        clear gv_restrstock.
        loop at gt_mard.
          if gt_mard-umlme <> 0 or
             gt_mard-insme <> 0 or
             gt_mard-einme <> 0 or
             gt_mard-speme <> 0 or
             gt_mard-retme <> 0.
            gv_restrstock = 'X'.
          endif.
        endloop.

        if gv_restrstock = 'X'.
*........ so the valuation class will not be updated
          DELETE gt_valclass_entry INDEX gv_tabix.

          move-corresponding gt_valclass_entry to gt_valclass_error.
          move text-e03                        to gt_valclass_error-ermsg.
          append gt_valclass_error.
          clear gt_valclass_error.
        else.
*........ so the stock can be removed before changing the valuation class
          loop at gt_mard.
            move-corresponding gt_valclass_entry to gt_valclass_stock.
            move gt_mard-lgort                   to gt_valclass_stock-lgort.
            move gt_mard-labst                   to gt_valclass_stock-labst.
            append gt_valclass_stock.
            clear gt_valclass_stock.
          endloop.
        endif.
      endif.
    else.                          " No stock
*.... valuation class can be changed
    endif.

  endloop.

* begin of insert MOD-002
  IF gv_batch = 'X'.
    REFRESH it_mchb.
    LOOP at gt_valclass_stock.
      wa_valclass_stock = gt_valclass_stock.

      SELECT matnr werks lgort charg clabs
        APPENDING CORRESPONDING FIELDS OF TABLE it_mchb
        FROM mchb WHERE matnr = wa_valclass_stock-matnr
                    AND werks = wa_valclass_stock-bwkey
                    AND lgort = wa_valclass_stock-lgort.
    endloop.
    SORT it_mchb by matnr werks lgort charg.
  ENDIF.
* end of insert MOD-002

* Remove stock
  gv_bwart = c_562.
  loop at gt_valclass_stock.
    wa_valclass_stock = gt_valclass_stock.

*.. Prepare batch-inputdata
* begin of insert MOD-002
    IF gv_batch = 'X'.
      LOOP at it_mchb WHERE matnr = wa_valclass_stock-matnr
                        AND werks = wa_valclass_stock-bwkey
                        AND lgort = wa_valclass_stock-lgort.

        refresh i_bdcdata.
        perform move_stock using wa_valclass_stock
                                 gv_bwart
                                 it_mchb-charg
                                 it_mchb-clabs.

        refresh gt_err.
        call transaction 'MB1C' using i_bdcdata
                mode gv_mode update 'S' messages into gt_err.

*...... Write errors to log
        if not gt_err[] is initial.
          perform get_trans_messages tables gt_err
                                      using gt_valclass_stock-matnr.
        endif.
      endloop.
    ELSE.
* end of insert MOD-002
      refresh i_bdcdata.
* begin of change MOD-002
*      perform move_stock using wa_valclass_stock gv_bwart.
      perform move_stock using wa_valclass_stock
                               gv_bwart
                               it_mchb-charg
                               it_mchb-clabs.
* end of change MOD-002
      refresh gt_err.
      call transaction 'MB1C' using i_bdcdata
              mode gv_mode update 'S' messages into gt_err.

*.... Write errors to log
      if not gt_err[] is initial.
        perform get_trans_messages tables gt_err
                                    using gt_valclass_stock-matnr.
      endif.
* begin of insert MOD-002
    endif.
* end of insert MOD-002
  endloop.

* Change valuation class
  loop at gt_valclass_entry.
    wa_valclass_entry = gt_valclass_entry.

*.. Prepare batch-inputdata
* begin of insert MOD-002
    REFRESH it_mbew.
    SELECT bwtar
      APPENDING CORRESPONDING FIELDS OF TABLE it_mbew
      FROM mbew WHERE matnr = gt_valclass_entry-matnr
                  AND bwkey = gt_valclass_entry-bwkey.

    loop at it_mbew.
* end of insert MOD-002
      refresh i_bdcdata.
      perform change_valclass using wa_valclass_entry.

      refresh gt_err.
      call transaction 'MM02' using i_bdcdata
              mode gv_mode update 'S' messages into gt_err.

*.... Write errors to log
      if not gt_err[] is initial.
        perform get_trans_messages tables gt_err
                                    using gt_valclass_entry-matnr.
      endif.
* begin of insert MOD-002
    endloop.
* end of inser MOD-002
  endloop.

* Undo Removal stock
  gv_bwart = c_561.
  loop at gt_valclass_stock.
    wa_valclass_stock = gt_valclass_stock.

*.. Prepare batch-inputdata
* begin of insert MOD-002
    IF gv_batch = 'X'.
      LOOP at it_mchb WHERE matnr = wa_valclass_stock-matnr
                        AND werks = wa_valclass_stock-bwkey
                        AND lgort = wa_valclass_stock-lgort.

        refresh i_bdcdata.
        perform move_stock using wa_valclass_stock
                                 gv_bwart
                                 it_mchb-charg
                                 it_mchb-clabs.

        refresh gt_err.
        call transaction 'MB1C' using i_bdcdata
                mode gv_mode update 'S' messages into gt_err.

*...... Write errors to log
        if not gt_err[] is initial.
          perform get_trans_messages tables gt_err
                                      using gt_valclass_stock-matnr.
        endif.
      endloop.
    ELSE.
* end of insert MOD-002
      refresh i_bdcdata.
* begin of change MOD-002
*      perform move_stock using wa_valclass_stock gv_bwart.
      perform move_stock using wa_valclass_stock
                               gv_bwart
                               it_mchb-charg
                               it_mchb-clabs.
* end of change MOD-002
      refresh gt_err.
      call transaction 'MB1C' using i_bdcdata
              mode gv_mode update 'S' messages into gt_err.

*.... Write errors to log
      if not gt_err[] is initial.
        perform get_trans_messages tables gt_err
                                    using gt_valclass_stock-matnr.
      endif.
* begin of insert MOD-002
    endif.
* end of inser MOD-002
  endloop.

* Show the errors from the VALCLASS_ERROR table
  if not gt_valclass_error[] is initial.
    perform write_valclass_errors.
  endif.

  if i_logtab[] is initial.
    write: / 'Execution was successful, without errors'.
  else.
*.. Write errors faced during executing of transactions
    if not gt_valclass_error[] is initial.
      new-page.
    else.
      skip 2.
*      write: / text-002.
      uline.
    endif.
    perform write_tr_errors.
  endif.


*-- S U B R O U T I N E S ---------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  write_valclass_errors
*&---------------------------------------------------------------------*
*       Write valclass errors
*----------------------------------------------------------------------*
FORM write_valclass_errors.

  skip 2.
*  write: / text-001.
  uline.

  loop at gt_valclass_error.
    write: /1 gt_valclass_error-matnr no-zero,
           15 gt_valclass_error-bwkey,
           21 gt_valclass_error-bklas,
           26 gt_valclass_error-ebeln,
           38 gt_valclass_error-ebelp,
           46 gt_valclass_error-wemng,
           60 gt_valclass_error-menge,
           74 gt_valclass_error-ermsg.
  endloop.

ENDFORM.                    "write_valclass_errors

*&---------------------------------------------------------------------*
*&      Form  write_tr_errors
*&---------------------------------------------------------------------*
*       Write transaction errors
*----------------------------------------------------------------------*
FORM write_tr_errors.

  loop at i_logtab.
    write: /1 i_logtab-matnr.
    format color col_negative.
    write: /7 i_logtab-msg.
    format reset.
  endloop.

ENDFORM.                    "write_tr_errors

*&---------------------------------------------------------------------*
*&      Form Move_stock
*&---------------------------------------------------------------------*
*       Move stock
*----------------------------------------------------------------------*
FORM move_stock using r_wa_valclass_stock like wa_valclass_stock
* begin of change MOD-002
*                      r_bwart.
                      r_bwart
                      r_charg
                      r_clabs.
* end of change MOD-002

  data: lv_erfmg(13) type c.

* Initial screen Goods Movement
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPMM07M' '0400' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: '' '' '' 'RM07M-BWARTWA' r_bwart
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: '' '' '' 'RM07M-WERKS' r_wa_valclass_stock-bwkey
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: '' '' '' 'RM07M-LGORT' r_wa_valclass_stock-lgort
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '/00'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

* Selection List/List Entry: Goods Movement
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPMM07M' '0421' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: '' '' '' 'MSEG-MATNR(01)' r_wa_valclass_stock-matnr
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

* begin of change MOD-002
  IF gv_batch <> 'X'.
    write r_wa_valclass_stock-labst to lv_erfmg.
  ELSE.
    write r_clabs to lv_erfmg.
  ENDIF.
* end of change MOD-002
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: '' '' '' 'MSEG-ERFMG(01)' lv_erfmg
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

* begin of insert MOD-002
  IF gv_batch = 'X'.
    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                       USING: '' '' '' 'MSEG-CHARG(01)' r_charg
                       CHANGING struct_bdcdata.

    APPEND struct_bdcdata TO i_bdcdata.
    CLEAR  struct_bdcdata.
  ENDIF.
* end of insert MOD-002

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=BU'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPLKACB' '0002' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=ENTE'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPLKACB' '0002' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=ENTE'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

ENDFORM.                    " Move stock

*&---------------------------------------------------------------------*
*&      Form change_valclass
*&---------------------------------------------------------------------*
*       Change valuation class
*----------------------------------------------------------------------*
FORM change_valclass using r_wa_valclass_entry like wa_valclass_entry.

*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPLMGMM' '0060' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: '' '' '' 'RMMG1-MATNR' r_wa_valclass_entry-matnr
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=ENTR'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPLMGMM' '0070' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=SELA'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPLMGMM' '0070' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=ENTR'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPLMGMM' '0080' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: '' '' '' 'RMMG1-WERKS' r_wa_valclass_entry-bwkey
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

* begin of insert MOD-002
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: '' '' '' 'RMMG1-BWTAR' it_mbew-bwtar
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.
* end of insert MOD-002

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=ENTR'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPLMGMM' '4004' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=SP24'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

*
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPLMGMM' '4000' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: '' '' '' 'MBEW-BKLAS' r_wa_valclass_entry-bklas
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

* begin of insert MOD-002
  IF NOT it_mbew-bwtar IS INITIAL.
    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                       USING: '' '' '' 'MBEW-EKLAS' r_wa_valclass_entry-bklas
                       CHANGING struct_bdcdata.

    APPEND struct_bdcdata TO i_bdcdata.
    CLEAR  struct_bdcdata.

    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                       USING: '' '' '' 'MBEW-QKLAS' r_wa_valclass_entry-bklas
                       CHANGING struct_bdcdata.

    APPEND struct_bdcdata TO i_bdcdata.
    CLEAR  struct_bdcdata.
  ENDIF.
* end of insert MOD-002

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=BU'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

ENDFORM.                    "change_valclass

*&---------------------------------------------------------------------*
*&      Form  add_message_to_tab
*&---------------------------------------------------------------------*
*       Add messages to log
*----------------------------------------------------------------------*
FORM add_message_to_tab using p_msg r_matnr.

  i_logtab-matnr = r_matnr.
  i_logtab-msg   = p_msg.
  append i_logtab.
  clear i_logtab.

ENDFORM.                    " add_message_to_tab

*&---------------------------------------------------------------------*
*&      Form  get_trans_messages
*&---------------------------------------------------------------------*
*       Get the errormessages from the performed transaction
*----------------------------------------------------------------------*
*  -->  p_errtab    Table with errormessages
*  -->  p_matnr     materialnumber
*----------------------------------------------------------------------*
FORM get_trans_messages tables p_errtab structure gt_err
                         using p_matnr.

  loop at p_errtab where msgtyp = 'E' or
                         msgtyp = 'A'.

    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*       SPRSL                   = SY-LANGU
        arbgb                   = p_errtab-msgid
        msgnr                   = p_errtab-msgnr
        msgv1                   = p_errtab-msgv1
        msgv2                   = p_errtab-msgv2
        msgv3                   = p_errtab-msgv3
        msgv4                   = p_errtab-msgv4
      IMPORTING
        msgtext                 = gv_mestx
      EXCEPTIONS
        message_not_found       = 1
        OTHERS                  = 2.

    IF sy-subrc = 0.
      perform add_message_to_tab using gv_mestx p_matnr.
    endif.

  endloop.

ENDFORM.                    " get_trans_messages

*Text symbol text£º
*E01:You have no authorisation to update materials in valuation area:
*E02:GR<>IR

*E03:Restr.Stock
*Selection text£º
*P_INFILE:        Inputfile
