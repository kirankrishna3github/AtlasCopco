*&---------------------------------------------------------------------*
*&  Include           YSE_SD_CPL_DEL_AVAIL_S0100                       *
*&---------------------------------------------------------------------*
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-002 |17.03.2011|Lakshmi R |CD1K963789       |CR1995              *
************************************************************************

*&---------------------------------------------------------------------*
*&      Module  S0100_STATUS  OUTPUT
*&---------------------------------------------------------------------*
MODULE s0100_status OUTPUT.

  SET PF-STATUS '0100'.
  SET TITLEBAR  '0100'.

ENDMODULE.                 " S0100_STATUS  OUTPUT

*&---------------------------------------------------------------------*
*&      Module  S0100_CREATE_TABLE  OUTPUT
*&---------------------------------------------------------------------*
MODULE s0100_create_table OUTPUT.
* If the container object is initial
  IF go_cntr IS INITIAL.
*   Create the container
    CREATE OBJECT go_cntr
      EXPORTING
          repid           =  sy-repid
          dynnr           =  sy-dynnr
          lifetime        =  cntl_lifetime_dynpro
*         RATIO           =  90.
          extension       =  5000.

*   create the ALV control
    CREATE OBJECT go_alvg
      EXPORTING
        i_parent = go_cntr.

*   Set parameters regarding layout, etc.
    gs_var-report     = sy-cprog.
    gs_lay-no_toolbar = ' '.
    IF rb_sbas = 'X'.
      gs_lay-grid_title = text-s01. "'Service based'(S01).
    ELSE.
      gs_lay-grid_title = text-s07. "'Delivery based'(S07).
    ENDIF.
    gs_lay-zebra      = 'X'.
    gs_lay-cwidth_opt = 'X'.

*   Clear the field catalog table
    REFRESH: it_fcat.

*   Fetch the field catalog for structure
    IF rb_hlev = 'X'.
      CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
        EXPORTING
          i_buffer_active  = ' '
          i_structure_name = 'YSE_SD_CPL_DEL_OUT_HDR'
        CHANGING
          ct_fieldcat      = it_fcat.

*.... Keep only one line per sales order
      DELETE ADJACENT DUPLICATES FROM it_out COMPARING vbeln.
    ELSE.
      CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
        EXPORTING
          i_buffer_active  = ' '
          i_structure_name = 'YSE_SD_CPL_DEL_OUT_ITM'
        CHANGING
          ct_fieldcat      = it_fcat.
    ENDIF.

    gs_var-variant = p_var.             "insert MOD-002
*   Display the data in the ALV control
    go_alvg->set_table_for_first_display(
                EXPORTING
                    i_save        =  'A'
                    is_variant    =  gs_var
                    is_layout     =  gs_lay
                CHANGING
                    it_outtab         =  it_out[]
                    it_fieldcatalog   =  it_fcat ).

    SET HANDLER lcl_event_handler=>on_double_click
                    FOR go_alvg.
  ELSE.


*If ALV control already exists, refresh data without changing the layout -> 'soft'
    go_alvg->refresh_table_display(
      EXPORTING
          i_soft_refresh  =  'X' ).
  ENDIF.
ENDMODULE.                 " S0100_CREATE_TABLE  OUTPUT

*&---------------------------------------------------------------------*
*&      Module  S0100_EXIT_COMMAND  INPUT
*&---------------------------------------------------------------------*
MODULE s0100_exit_command INPUT.

  CASE ok_code.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN 'EXIT'.
      LEAVE TO SCREEN 0.
    WHEN 'CANCEL'.
      LEAVE TO SCREEN 0.
  ENDCASE.

ENDMODULE.                 " S0100_EXIT_COMMAND  INPUT
