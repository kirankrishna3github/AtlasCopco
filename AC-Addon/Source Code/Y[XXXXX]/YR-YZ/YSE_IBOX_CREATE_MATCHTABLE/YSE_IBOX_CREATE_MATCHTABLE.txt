*----------------------------------------------------------------------*
* PROGRAM ID           : YSE_IBOX_CREATE_MATCHTABLE                    *
* PROGRAM TITLE        : IBOX: Create matching table                   *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 30/11/2010                                    *
* DEVELOPMENT ID       :                                               *
*                                                                      *
* CHANGE REQUEST NUMBER: CD1K960547                                    *
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    |NAME                  | CORR. NR. | CHANGE REF#  *
*----------------------------------------------------------------------*
* MOD-001 |   /  /   |                      |           |              *
************************************************************************
REPORT  yse_ibox_create_matchtable.

*----------------------------------------------------------------------
TABLES: yse_ibox_iti,
        yse_ibox_hqt.

TYPES: BEGIN OF ty_curr,
         kurst     TYPE kurst,
         fcurr     TYPE fcurr,
         tcurr     TYPE tcurr_curr,
         gdatu     TYPE gdatu_inv,
         ukurs     TYPE ukurs_curr,
       END OF ty_curr.

DATA: lt_yse_ibox_iti TYPE TABLE OF yse_ibox_iti,
      ls_yse_ibox_iti TYPE yse_ibox_iti,
      lt_yse_ibox_hqt TYPE TABLE OF yse_ibox_hqt,
      ls_yse_ibox_hqt TYPE yse_ibox_hqt,
      wa_ibox_create  TYPE yse_ibox_create.

* Internal table with Exchange Rates
DATA: gt_curr    TYPE HASHED TABLE OF ty_curr
                      WITH UNIQUE KEY kurst fcurr tcurr
                      WITH HEADER LINE,
      gt_curri   TYPE TABLE OF ty_curr
                      WITH HEADER LINE.

DATA: gv_waers_int    TYPE waers,
      gv_waers_cc     TYPE waers,
      gv_zlcllang_cc  TYPE spras,
      gv_bukrs        TYPE bukrs,
      gv_gdatu        TYPE gdatu_inv,
      gv_kurst        TYPE kurst.

DATA: lv_length(4)   TYPE p,
      lv_length2(4)  TYPE c,
      lv_lengthx(6)  TYPE c,
      lv_width(4)    TYPE p,
      lv_width2(4)   TYPE c,
      lv_widthx(6)   TYPE c,
      lv_heigth(4)   TYPE p,
      lv_heigth2(4)  TYPE c,
      lv_heigthx(6)  TYPE c,
      lv_gac(4)      TYPE c,
      lv_ekorg       TYPE ekorg,
      lv_cost        TYPE stprs,
      lv_knumh       TYPE knumh,
      lv_kbetr       TYPE kbetr_kond.

*----------------------------------------------------------------------
SELECT-OPTIONS:
  s_vkorg FOR yse_ibox_iti-vkorg.

*----------------------------------------------------------------------
START-OF-SELECTION.

  SELECT * FROM yse_ibox_iti INTO TABLE lt_yse_ibox_iti
    WHERE vkorg IN s_vkorg.

  SORT lt_yse_ibox_iti BY matnr vkorg.

  SELECT * FROM yse_ibox_hqt INTO TABLE lt_yse_ibox_hqt
    WHERE vkorg IN s_vkorg.

  SORT lt_yse_ibox_hqt BY matnr lifnr vkorg.

*
  PERFORM initial.
*
  LOOP AT lt_yse_ibox_hqt INTO ls_yse_ibox_hqt.
    READ TABLE lt_yse_ibox_iti INTO ls_yse_ibox_iti
       WITH KEY mandt  = sy-mandt
                matnr  = ls_yse_ibox_hqt-matnr
                vkorg  = ls_yse_ibox_hqt-vkorg
                rpiven = ls_yse_ibox_hqt-dshqvend.

    IF sy-subrc = 0.
      CLEAR wa_ibox_create.

      wa_ibox_create-matnr = ls_yse_ibox_hqt-matnr.
      wa_ibox_create-vkorg = ls_yse_ibox_hqt-vkorg.

      CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
        EXPORTING
          input  = ls_yse_ibox_iti-rpiven
        IMPORTING
          output = wa_ibox_create-lifnr.

      SELECT SINGLE name1 INTO wa_ibox_create-zvendname
        FROM lfa1 WHERE lifnr = wa_ibox_create-lifnr.

      wa_ibox_create-ersda = ls_yse_ibox_iti-rpcrdt.
      wa_ibox_create-maktx = ls_yse_ibox_iti-rpdesc.

      CALL FUNCTION 'CONVERSION_EXIT_CUNIT_INPUT'
        EXPORTING
          input          = ls_yse_ibox_iti-rpums
          language       = sy-langu
        IMPORTING
          output         = wa_ibox_create-meins
        EXCEPTIONS
          unit_not_found = 1
          OTHERS         = 2.

      IF NOT ls_yse_ibox_iti-rpuom IS INITIAL.
        CALL FUNCTION 'CONVERSION_EXIT_CUNIT_INPUT'
          EXPORTING
            input          = ls_yse_ibox_iti-rpuom
            language       = sy-langu
          IMPORTING
            output         = wa_ibox_create-vrkme
          EXCEPTIONS
            unit_not_found = 1
            OTHERS         = 2.

        wa_ibox_create-umren = ls_yse_ibox_iti-rpumrc(9).
      ENDIF.

      IF ls_yse_ibox_iti-rpprin = 'M'.
        wa_ibox_create-matkl = '01'.
      ELSEIF ls_yse_ibox_iti-rpprin = 'S'.
        wa_ibox_create-matkl = '02'.
      ENDIF.

      wa_ibox_create-bismt = ls_yse_ibox_iti-matnr.
      wa_ibox_create-spart = '01'.

      wa_ibox_create-prdha+0(4) = ls_yse_ibox_iti-rppgac(4).
      lv_gac = ls_yse_ibox_iti-rppgac(4).
      wa_ibox_create-prdha+4(4) = ls_yse_ibox_iti-rppgc(4).

      wa_ibox_create-gewei = ls_yse_ibox_iti-rpwtum.
      wa_ibox_create-ntgew = ls_yse_ibox_iti-rpnwig.
      wa_ibox_create-brgew = ls_yse_ibox_iti-rpnwig.

      lv_length = ls_yse_ibox_iti-rppdim+0(4) / 10.
      WRITE lv_length TO lv_length2.
      CONCATENATE lv_length2 'CM' INTO lv_lengthx.

      lv_width  = ls_yse_ibox_iti-rppdim+5(4) / 10.
      WRITE lv_width TO lv_width2.
      CONCATENATE lv_width2 'CM' INTO lv_widthx.

      lv_heigth = ls_yse_ibox_iti-rppdim+10(4) / 10.
      WRITE lv_heigth TO lv_heigth2.
      CONCATENATE lv_heigth2 'CM' INTO lv_heigthx.

      CONCATENATE lv_lengthx 'X' lv_widthx 'X' lv_heigthx
        INTO wa_ibox_create-groes SEPARATED BY space.

      SELECT SINGLE dwerk INTO wa_ibox_create-dwerk
        FROM yse_vkorg_check WHERE vkorg = ls_yse_ibox_hqt-vkorg.

      wa_ibox_create-mtpos = 'NORM'.

      wa_ibox_create-mvgr2 = ls_yse_ibox_iti-rpsc.
      wa_ibox_create-mvgr4 = 'LCL'.

      IF ls_yse_ibox_iti-rpsafl = '1'.
*       leave empty
      ELSEIF ls_yse_ibox_iti-rpsafl = '2'.
        wa_ibox_create-sernp = 'Z001'.
      ENDIF.

*      wa_ibox_create-prat2.
      wa_ibox_create-herkl = ls_yse_ibox_iti-rporig.

*      wa_ibox_create-dismm.

      wa_ibox_create-lgrad = '60'.
*      wa_ibox_create-verpr = ls_yse_ibox_iti-rpacst / 100000.

*     Standard price = transfer price * landed cost factor based on the GAC.
      SELECT SINGLE ekorg INTO lv_ekorg
        FROM t024w WHERE werks = wa_ibox_create-dwerk.

*.... First search for vendor/plant/GAC combination
      CLEAR lv_knumh.
      SELECT SINGLE knumh INTO lv_knumh
        FROM a942 WHERE kappl  EQ 'M'
                    AND kschl  EQ 'ZFR1'
                    AND ekorg  EQ lv_ekorg
                    AND lifnr  EQ wa_ibox_create-lifnr
                    AND werks  EQ wa_ibox_create-dwerk
                    AND prodh1 EQ lv_gac
                    AND datbi  GE sy-datum
                    AND datab  LE sy-datum .
      IF sy-subrc <> 0.
*...... 2nd search for vendor/GAC combination
        SELECT SINGLE knumh INTO lv_knumh
          FROM a940 WHERE kappl  EQ 'M'
                      AND kschl  EQ 'ZFR1'
                      AND ekorg  EQ lv_ekorg
                      AND lifnr  EQ wa_ibox_create-lifnr
                      AND prodh1 EQ lv_gac
                      AND datbi  GE sy-datum
                      AND datab  LE sy-datum .
        IF sy-subrc <> 0.
*........ 3rd search for GAC only
          SELECT SINGLE knumh INTO lv_knumh
            FROM a943 WHERE kappl  EQ 'M'
                        AND kschl  EQ 'ZFR1'
                        AND ekorg  EQ lv_ekorg
                        AND prodh1 EQ lv_gac
                        AND datbi  GE sy-datum
                        AND datab  LE sy-datum .
        ENDIF.
      ENDIF.

      CLEAR lv_kbetr.
      SELECT SINGLE kbetr INTO lv_kbetr
        FROM konp WHERE knumh = lv_knumh.

      wa_ibox_create-stprs = ( ls_yse_ibox_hqt-dshqpr1 / 100000 ) * ( ( 1000 + lv_kbetr ) / 1000 ).

*      SELECT SINGLE bukrs INTO lv_bukrs
*        FROM tvko WHERE vkorg = ls_yse_ibox_hqt-vkorg.
*
*      SELECT SINGLE waers spras INTO (wa_ibox_create-waers, wa_ibox_create-zlcllang)
*        FROM t001 WHERE bukrs = lv_bukrs.
      wa_ibox_create-waers = gv_waers_cc.
      wa_ibox_create-zlcllang = gv_zlcllang_cc.

*.... Currency conversion needs to be done ??
      IF ls_yse_ibox_hqt-dshqcurr <> wa_ibox_create-waers.
        PERFORM convert_amount_int USING wa_ibox_create-stprs
                                         ls_yse_ibox_hqt-dshqcurr
                                         lv_cost
                                         wa_ibox_create-waers
                                         gv_kurst
                                         gv_waers_int.
        wa_ibox_create-stprs = lv_cost.
      ENDIF.

      IF NOT ls_yse_ibox_iti-rpump IS INITIAL.
        CALL FUNCTION 'CONVERSION_EXIT_CUNIT_INPUT'
          EXPORTING
            input          = ls_yse_ibox_iti-rpump
            language       = sy-langu
          IMPORTING
            output         = wa_ibox_create-bstme
          EXCEPTIONS
            unit_not_found = 1
            OTHERS         = 2.

        wa_ibox_create-pumren = ls_yse_ibox_iti-rpumcn(9).
      ENDIF.

      wa_ibox_create-netpr = ls_yse_ibox_hqt-dshqpr1 / 100000.
      wa_ibox_create-tpcurr = ls_yse_ibox_hqt-dshqcurr.
      wa_ibox_create-peinh = ls_yse_ibox_hqt-dshqqr1 / 1000.
      wa_ibox_create-datab = ls_yse_ibox_hqt-dshqeff.
      wa_ibox_create-datbi = ls_yse_ibox_hqt-dshqdis.

      CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
        EXPORTING
          input  = ls_yse_ibox_iti-rpven2
        IMPORTING
          output = wa_ibox_create-lifnr2.

      SELECT SINGLE name1 INTO wa_ibox_create-zsvendname
        FROM lfa1 WHERE lifnr = wa_ibox_create-lifnr2.

      wa_ibox_create-plifz = ls_yse_ibox_iti-rplead.

*     wa_ibox_create-mlprice
      wa_ibox_create-mlcurr = wa_ibox_create-waers.
      wa_ibox_create-rpip = ls_yse_ibox_iti-rpip / 1000.
      wa_ibox_create-rpcurr = 'EUR'.
*      wa_ibox_create-evers
      wa_ibox_create-rpccfm = ls_yse_ibox_iti-rpccfm.
      wa_ibox_create-zlcldescr = ls_yse_ibox_iti-rpdesc.
      wa_ibox_create-kondm     = 'ZZ'.
      wa_ibox_create-rpdcfm    = ls_yse_ibox_iti-rpdcfm.

      INSERT yse_ibox_create FROM wa_ibox_create.

      IF sy-subrc = 0.
        DELETE FROM yse_ibox_iti WHERE matnr  = ls_yse_ibox_hqt-matnr
                                   AND vkorg  = ls_yse_ibox_hqt-vkorg.

        DELETE FROM yse_ibox_hqt WHERE matnr  = ls_yse_ibox_hqt-matnr
                                   AND lifnr  = ls_yse_ibox_hqt-lifnr
                                   AND vkorg  = ls_yse_ibox_hqt-vkorg.
      ENDIF.
    ENDIF.
  ENDLOOP.

*&---------------------------------------------------------------------*
*&      Form  INITIAL
*&---------------------------------------------------------------------*
*       Initialization
*----------------------------------------------------------------------*
FORM initial .

  SELECT SINGLE bukrs INTO gv_bukrs
    FROM tvko WHERE vkorg = s_vkorg-low.

  SELECT SINGLE waers spras INTO (gv_waers_cc, gv_zlcllang_cc)
    FROM t001 WHERE bukrs = gv_bukrs.

* Invert current date
  CONVERT DATE sy-datum INTO INVERTED-DATE gv_gdatu.

** Exchange rate type
*  CASE p_bukrs.
*    WHEN 'MRUA'.
*      gv_kurst = 'DRU'.
*    WHEN 'POLA'.
*      gv_kurst = 'D'.
*    WHEN OTHERS.
  gv_kurst = 'EURX'.
*  ENDCASE.

** Intermediate currency
*  CASE p_bukrs.
*    WHEN 'MRUA'.
*      gv_waers_int = gv_waers_cc.
*    WHEN 'POLA'.
*      gv_waers_int = gv_waers_cc.
*    WHEN OTHERS.
  gv_waers_int = 'EUR'.
*  ENDCASE.

* Exchange rates
  SELECT kurst fcurr tcurr gdatu ukurs INTO TABLE gt_curri
         FROM tcurr WHERE kurst =  gv_kurst
                      AND tcurr =  gv_waers_int
                      AND gdatu GE gv_gdatu.

  SORT gt_curri BY kurst fcurr tcurr gdatu.
  DELETE ADJACENT DUPLICATES FROM gt_curri COMPARING kurst fcurr tcurr.
  gt_curr[] = gt_curri[].
  FREE gt_curri.

ENDFORM.                    " INITIAL

*&---------------------------------------------------------------------*
*&      Form  CONVERT_AMOUNT_INT
*&---------------------------------------------------------------------*
*       Convert amount via intermediate currency
*----------------------------------------------------------------------*
*      -->P_KWERT_IN  : Given Value
*      -->P_WAERS_IN  : Given Currency
*      -->P_KWERT_OUT : Converted Value
*      -->P_WAERS_RES : Result Currency
*      -->P_GV_WAERS_INT : Intermediate currency
*      -->P_GV_KURST     : Exchange rate type
*----------------------------------------------------------------------*
FORM convert_amount_int  USING    p_kwert_in
                                  p_waers_in
                                  p_kwert_out
                                  p_waers_res
                                  p_gv_kurst
                                  p_gv_waers_int.

  DATA: lv_value_int   TYPE wtgxxx.

* Input Currency = Result currency -> NO conversion
  IF p_waers_in = p_waers_res.
    p_kwert_out = p_kwert_in.
    RETURN.
  ENDIF.

* Intermediate exchange rate
  IF p_waers_in = p_gv_waers_int.
    lv_value_int = p_kwert_in.
  ELSE.
    CLEAR gt_curr.
    READ TABLE gt_curr WITH TABLE KEY kurst = p_gv_kurst
                                      fcurr = p_waers_in
                                      tcurr = p_gv_waers_int.
    CHECK sy-subrc = 0.
*   Calculate amount in intermediate currency
    IF gt_curr-ukurs = 0.
      gt_curr-ukurs = 1.
    ENDIF.
    lv_value_int = p_kwert_in * gt_curr-ukurs.
  ENDIF.

* Result exchange rate
  IF p_waers_res = p_gv_waers_int.
    p_kwert_out = lv_value_int.
  ELSE.
    CLEAR gt_curr.
    READ TABLE gt_curr WITH TABLE KEY kurst = p_gv_kurst
                                      fcurr = p_waers_res
                                      tcurr = p_gv_waers_int.
    CHECK sy-subrc = 0.
*   Calculate amount in result currency
    IF gt_curr-ukurs = 0.
      gt_curr-ukurs = 1.
    ENDIF.
    p_kwert_out = lv_value_int / gt_curr-ukurs.
  ENDIF.

ENDFORM.                    " CONVERT_AMOUNT_INT

*Selection text£º
*S_AUFNR:D       .
*S_VKORG:D       .
