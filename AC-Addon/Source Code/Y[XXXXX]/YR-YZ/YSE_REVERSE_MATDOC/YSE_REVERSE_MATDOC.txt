************************************************************************
* Program ID        : YSE_REVERSE_MATDOC               *
* Program Title     : Program title                                    *
* Author            : Anson Sun                                        *
* Date              : 20/08/2011                               	     *
* Change Request Number:2182                                           *
* Description       : The purpose of this program is to Reverse        *
*                                                      Material Doc.   *
*======================================================================*
* Copied From       : (Cloned Program)                               *
* Title             : (Program Title)                          	     *
* Other Related obj : (Object names)                               *
*======================================================================*
* Change History Log                                             	     *
*----------------------------------------------------------------------*
* Mod. no.|  Date    | Name           | Correction Number  | Change    *
*----------------------------------------------------------------------*
* MOD-001 |28/11/2016| Anda Wu        | XXXXxxxxxx         |           *
*                                                                      *
* Description: Add head text filter in selection screen                *
*----------------------------------------------------------------------*

REPORT  yse_reverse_matdoc MESSAGE-ID yse_cs_sd_msg.
*----------------------------------------------------------------------*
*       TABLES
*----------------------------------------------------------------------*
TABLES:
  mkpf.                      "Header: Material Document
*----------------------------------------------------------------------*
*       TYPE-POOLS
*----------------------------------------------------------------------*
TYPE-POOLS slis.
*----------------------------------------------------------------------*
*       TYPE
*----------------------------------------------------------------------*
TYPES:
  BEGIN OF ty_matdoc,
    mblnr   TYPE mkpf-mblnr, "Number of Material Document
    mjahr   TYPE mkpf-mjahr, "Material Document Year
    budat   TYPE mkpf-budat, "Posting Date in the Document
  END   OF ty_matdoc.
*----------------------------------------------------------------------*
*       DATA
*----------------------------------------------------------------------*
DATA:
  i_log       TYPE TABLE OF yse_reverse_matdoc_log,
  i_fieldcat  TYPE slis_t_fieldcat_alv.
*----------------------------------------------------------------------*
*       SELECTION-SCREEN
*----------------------------------------------------------------------*
SELECTION-SCREEN: BEGIN OF BLOCK b_sel1 WITH FRAME TITLE text-b01.
SELECT-OPTIONS:
  s_mblnr   FOR  mkpf-mblnr , "OBLIGATORY,
  s_mjahr   FOR  mkpf-mjahr NO-EXTENSION NO INTERVALS OBLIGATORY,
  s_budat   FOR  mkpf-budat NO-EXTENSION NO INTERVALS OBLIGATORY.
******Begin of mod-001 insert*************
SELECT-OPTIONS:
  s_bktxt   FOR mkpf-bktxt OBLIGATORY.
******End   of mod-001 insert*************
SELECTION-SCREEN: END   OF BLOCK b_sel1.

SELECTION-SCREEN: BEGIN OF BLOCK b_sel2 WITH FRAME TITLE text-b02.
SELECT-OPTIONS:
  s_cpudt   FOR  mkpf-cpudt,
  s_cputm   FOR  mkpf-cputm,
  s_usnam   FOR  mkpf-usnam.
SELECTION-SCREEN: END   OF BLOCK b_sel2.
*----------------------------------------------------------------------*
*       INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

* initial process
  PERFORM ini_process.
*----------------------------------------------------------------------*
*       START-OF-SELECTION
*----------------------------------------------------------------------*
START-OF-SELECTION.

* main process
  PERFORM main_process.
*----------------------------------------------------------------------*
*       END-OF-SELECTION
*----------------------------------------------------------------------*
END-OF-SELECTION.

* log process
  PERFORM display_log.
*&---------------------------------------------------------------------*
*&      Form  ini_process
*&---------------------------------------------------------------------*
*       initial process
*----------------------------------------------------------------------*
FORM ini_process .

  REFRESH:
    i_log,
    i_fieldcat.
ENDFORM.                    " ini_process
*&---------------------------------------------------------------------*
*&      Form  main_process
*&---------------------------------------------------------------------*
*       main process
*----------------------------------------------------------------------*
FORM main_process .

  DATA: li_matdoc   TYPE TABLE OF ty_matdoc,
        lrec_matdoc TYPE ty_matdoc.
  DATA: mat_doc     TYPE bapi2017_gm_head_ret,
        lrec_return TYPE bapiret2,
        return      TYPE TABLE OF bapiret2.
  DATA: lrec_log    TYPE yse_reverse_matdoc_log.

  SELECT * INTO CORRESPONDING FIELDS OF TABLE li_matdoc
    FROM mkpf   WHERE mblnr IN s_mblnr
                  AND mjahr IN s_mjahr
                  AND budat IN s_budat
                  AND cpudt IN s_cpudt
                  AND cputm IN s_cputm
                  AND usnam IN s_usnam
                  AND bktxt in s_bktxt .

  LOOP AT li_matdoc INTO lrec_matdoc.
    CALL FUNCTION 'BAPI_GOODSMVT_CANCEL'
      EXPORTING
        materialdocument    = lrec_matdoc-mblnr
        matdocumentyear     = lrec_matdoc-mjahr
        goodsmvt_pstng_date = lrec_matdoc-budat
      IMPORTING
        goodsmvt_headret    = mat_doc
      TABLES
        return              = return.
    LOOP AT return INTO lrec_return WHERE type = 'E'
                                       OR type = 'A'
                                       OR type = 'X'.
      lrec_log-mblnr   = lrec_matdoc-mblnr.
      lrec_log-mjahr   = lrec_matdoc-mjahr.
      lrec_log-type    = lrec_return-type.
      lrec_log-message = lrec_return-message.
      APPEND lrec_log TO i_log.
      CLEAR  lrec_log.
    ENDLOOP.
    IF sy-subrc <> 0.
      COMMIT WORK AND WAIT.
      lrec_log-mblnr   = lrec_matdoc-mblnr.
      lrec_log-mjahr   = lrec_matdoc-mjahr.
      lrec_log-revdoc  = mat_doc-mat_doc.
      lrec_log-revyear = mat_doc-doc_year.
      lrec_log-type    = 'S'.
      MESSAGE s007 WITH mat_doc-mat_doc INTO lrec_log-message.
      APPEND lrec_log TO i_log.
      CLEAR  lrec_log.
    ENDIF.
  ENDLOOP.
* no data process.
  IF li_matdoc[] IS INITIAL.
    MESSAGE s001.
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " main_process
*&---------------------------------------------------------------------*
*&      Form  display_log
*&---------------------------------------------------------------------*
*       display log
*----------------------------------------------------------------------*
FORM display_log .
  DATA: lrec_layout TYPE slis_layout_alv.

* edit alv fieldcat
  PERFORM edit_alv_fieldcat.

  lrec_layout-colwidth_optimize = 'X'.
  lrec_layout-zebra             = 'X'.
  CALL FUNCTION 'REUSE_ALV_LIST_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'SET_STATUS_10001'
      i_callback_user_command  = 'USER_COMMAND'
      is_layout                = lrec_layout
      it_fieldcat              = i_fieldcat
    TABLES
      t_outtab                 = i_log
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    EXIT.
  ELSE.
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " display_log
*&---------------------------------------------------------------------*
*&      Form  edit_alv_fieldcat
*&---------------------------------------------------------------------*
*       edit alv fieldcat
*----------------------------------------------------------------------*
FORM edit_alv_fieldcat.

  DATA: lrec_fieldcat TYPE slis_fieldcat_alv.
  REFRESH: i_fieldcat.

  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
    EXPORTING
      i_structure_name       = 'YSE_REVERSE_MATDOC_LOG'
      i_bypassing_buffer     = 'X'
    CHANGING
      ct_fieldcat            = i_fieldcat
    EXCEPTIONS
      inconsistent_interface = 1
      program_error          = 2
      OTHERS                 = 3.
  IF sy-subrc <> 0.
    EXIT.
  ENDIF.
*  LOOP AT i_fieldcat INTO lrec_fieldcat.
*    CASE lrec_fieldcat-fieldname.
*      WHEN ''.
*    ENDCASE.
*    MODIFY i_fieldcat FROM lrec_fieldcat.
*    CLEAR  lrec_fieldcat.
*  ENDLOOP.

ENDFORM.                    " edit_alv_fieldcat
*&---------------------------------------------------------------------*
*&      Form  set_status_10001
*&---------------------------------------------------------------------*
*       set screen status
*----------------------------------------------------------------------*
FORM set_status_10001 USING i_extab TYPE slis_t_extab.

  SET PF-STATUS '10001'.
  SET TITLEBAR  '10001'.

ENDFORM.                               " set_status_10001
*&---------------------------------------------------------------------*
*&      Form  user_command
*&---------------------------------------------------------------------*
*       user command
*----------------------------------------------------------------------*
FORM user_command  USING    i_ucomm    LIKE sy-ucomm
                            i_selfield TYPE slis_selfield.

  CASE i_ucomm.
    WHEN '&IC1'.
      PERFORM display_process USING i_selfield.
  ENDCASE.

ENDFORM.                               " user_command
*&---------------------------------------------------------------------*
*&      Form  display_process
*&---------------------------------------------------------------------*
*       display process
*----------------------------------------------------------------------*
FORM display_process  USING    selfield TYPE slis_selfield.

  DATA lrec_log TYPE yse_reverse_matdoc_log.
  READ TABLE i_log INTO lrec_log INDEX selfield-tabindex.
  IF sy-subrc = 0.
    CASE selfield-fieldname.
      WHEN 'MBLNR'.
        SET PARAMETER ID 'MBN' FIELD lrec_log-mblnr.
        SET PARAMETER ID 'MJA' FIELD lrec_log-mjahr.
        CALL TRANSACTION 'MB03' AND SKIP FIRST SCREEN.
      WHEN 'REVDOC'.
        SET PARAMETER ID 'MBN' FIELD lrec_log-revdoc.
        SET PARAMETER ID 'MJA' FIELD lrec_log-revyear.
        CALL TRANSACTION 'MB03' AND SKIP FIRST SCREEN.
    ENDCASE.
  ENDIF.

ENDFORM.                    " display_process

*Text symbol text£º
*B01:Material Doc. Data

*B02:Changed by
*Selection text£º
*S_BKTXT:D       .
*S_BUDAT:        Last Day of Previous
*S_CPUDT:        Entered on
*S_CPUTM:        Time
*S_MBLNR:        Material Doc.
*S_MJAHR:        Mat. Year
*S_USNAM:        User Name
