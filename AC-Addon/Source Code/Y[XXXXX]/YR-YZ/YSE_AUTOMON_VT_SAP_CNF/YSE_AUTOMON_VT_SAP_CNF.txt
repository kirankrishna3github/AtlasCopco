*&---------------------------------------------------------------------*
*& Report  YSE_AUTOMON_VT_SAP_CNF
*&
*&---------------------------------------------------------------------*
*&
*& Correct CNF status to make SAP alligned with VisiTour
*&
*&---------------------------------------------------------------------*
*  Author                : Anda Wu
*  Date                  : 23.12.2013
*  Change Request Number :
*  Transport request Nr. :
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. |   DATE     | NAME               | CORRECT. NR| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                    | CD1K9..... | CR....      *
*----------------------------------------------------------------------*
REPORT  yse_automon_vt_sap_cnf.

* Types
TYPES: BEGIN OF ty_aufk,
         aufnr        TYPE aufnr,
         objnr        TYPE j_objnr,
         iwerk        TYPE iwerk,
         ingpr        TYPE afih-ingpr,
       END OF ty_aufk.

TYPES: BEGIN OF ty_afko,
          aufnr       TYPE aufnr,
          aufpl       TYPE co_aufpl,
          rueck       TYPE afko-rueck,
          rmzhl       TYPE afko-rmzhl,
          aplzl       TYPE co_aplzl,
          vornr       TYPE vornr,
       END OF ty_afko.

TYPES: BEGIN OF ty_afru,
        rueck TYPE afru-rueck,
        rmzhl TYPE afru-rmzhl,
        aueru TYPE afru-aueru,
       END OF ty_afru.

TYPES: BEGIN OF ty_prev,
         text1(13)        TYPE c,
       END OF ty_prev,

       BEGIN OF ty_vt,
        aufnr        TYPE aufnr,
        servtype     TYPE yservtype,
      END OF ty_vt.

DATA: gt_prev   TYPE STANDARD TABLE OF ty_prev,
      gs_prev   TYPE ty_prev,
      gt_vt     TYPE STANDARD TABLE OF ty_vt,
      gs_vt     TYPE ty_vt.

DATA: gt_aufk   TYPE STANDARD TABLE OF ty_aufk,
      gt_afko   TYPE STANDARD TABLE OF ty_afko,
      gt_afru   TYPE STANDARD TABLE OF ty_afru,
      gs_aufk   TYPE ty_aufk.

DATA: gv_retcd  TYPE sy-subrc,                 " return code
      gv_error  TYPE xfeld,
      gv_text   TYPE char80.

DATA: g_directory     TYPE epsf-epsdirnam
                      VALUE '/var/load/xxx/UK/read/',
      p_logsys        TYPE tbdlst-logsys,
      g_pfile         TYPE epsf-epsfilnam.

* Constants
CONSTANTS:  gc_x(1)       TYPE c VALUE 'X',
            gc_stat_conf  TYPE j_status  VALUE 'I0009'.

*&---------------------------------------------------------------------*
*&  Selection screen                                                   *
*&---------------------------------------------------------------------*
* Input file
PARAMETERS: p_infile TYPE  epsfilnam LOWER CASE OBLIGATORY,
            p_mdate  TYPE  filemoddat DEFAULT sy-datum OBLIGATORY.

*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

* Read input file from Visitour
  PERFORM read_file.

* Get SAP data
  PERFORM get_sap_data.

* Compare input from VisiTour with SEO in SAP
  PERFORM compare_data.

*&---------------------------------------------------------------------*
*&      Form  READ_FILE
*&---------------------------------------------------------------------*
*       Read input file
*----------------------------------------------------------------------*
FORM read_file .

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = p_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE 'E' NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    LEAVE LIST-PROCESSING.
  ENDIF.

  REPLACE 'xxx' IN g_directory WITH p_logsys(3).
  CONCATENATE g_directory p_infile INTO g_pfile.

* Check the date of the file
  PERFORM check_date.
  IF NOT gv_error IS INITIAL.
    LEAVE LIST-PROCESSING.
  ENDIF.

* FILE READ FROM APPLICATION SERVER
  PERFORM get_from_appl TABLES  gt_prev
                        USING   g_pfile
                                gv_retcd.

* Store in an internal table
  LOOP AT gt_prev INTO gs_prev.
    CLEAR gs_vt.
    SPLIT gs_prev AT cl_abap_char_utilities=>horizontal_tab
        INTO gs_vt-aufnr gs_vt-servtype.
    PERFORM add_zeros USING gs_vt-aufnr.
    APPEND gs_vt TO gt_vt.
  ENDLOOP.

  SORT gt_vt BY aufnr.

ENDFORM.                    " READ_FILE

*&---------------------------------------------------------------------*
*&      Form  ADD_ZEROS
*&---------------------------------------------------------------------*
*       Add leading zeros
*----------------------------------------------------------------------*
*      -->P_FIELD : field
*----------------------------------------------------------------------*
FORM add_zeros  USING    p_field.

  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
    EXPORTING
      input  = p_field
    IMPORTING
      output = p_field.

ENDFORM.                    " ADD_ZEROS

*&---------------------------------------------------------------------*
*&      Form  GET_SAP_DATA
*&---------------------------------------------------------------------*
*       Get SAP data for Service Orders
*----------------------------------------------------------------------*
FORM get_sap_data .

* Get Service Order Header data
  SELECT s~aufnr s~objnr h~iwerk h~ingpr INTO TABLE gt_aufk
         FROM aufk AS s
         INNER JOIN afih AS h
                         ON h~aufnr = s~aufnr
         FOR ALL ENTRIES IN gt_vt
         WHERE s~aufnr = gt_vt-aufnr.

  CHECK NOT gt_aufk[] IS INITIAL.

  SELECT  afko~aufnr
          afko~aufpl
          afko~rueck
          afko~rmzhl
          afvc~aplzl
          afvc~vornr
    INTO TABLE gt_afko
    FROM  afko
      INNER JOIN afvc
      ON afko~aufpl = afvc~aufpl
    FOR ALL entries IN gt_aufk
    WHERE afko~aufnr = gt_aufk-aufnr.

  CHECK NOT gt_aufk[] IS INITIAL.

  SELECT  rueck
          rmzhl
          aueru
    FROM afru
    INTO TABLE gt_afru
    FOR ALL ENTRIES IN gt_afko
    WHERE rueck = gt_afko-rueck
      AND rmzhl = gt_afko-rmzhl.

ENDFORM.                    " GET_SAP_DATA

*&---------------------------------------------------------------------*
*&      Form  COMPARE_DATA
*&---------------------------------------------------------------------*
*       Compare input from VisiTour with SEO in SAP
*----------------------------------------------------------------------*
FORM compare_data .

  LOOP AT gt_vt INTO gs_vt.
*   Progress indicator
    gv_text = 'Order & is being processed'(i02).
    REPLACE '&' WITH gs_vt-aufnr INTO gv_text.
    CONDENSE gv_text.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        text = gv_text.

*   Get Service Order Header data (SAP)
    CLEAR gs_aufk.
    READ TABLE gt_aufk INTO gs_aufk
      WITH KEY aufnr = gs_vt-aufnr.
    CHECK sy-subrc = 0.

    PERFORM frm_do_conf USING gs_aufk-aufnr
                              gs_aufk-iwerk
                              gs_aufk-ingpr
                              gs_aufk-objnr.

  ENDLOOP.

ENDFORM.                    " COMPARE_DATA


*&---------------------------------------------------------------------*
*&      Form  get_from_appl
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->I_INFILE   text
*      -->P_INFILE   text
*      -->P_SUBRC    text
*----------------------------------------------------------------------*
FORM get_from_appl TABLES i_infile
                   USING p_infile p_subrc.

  OPEN DATASET p_infile FOR INPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, p_infile.
    p_subrc = 4.
    EXIT.
  ENDIF.

  REFRESH i_infile.

  DO.
    READ DATASET p_infile INTO gs_prev.
    IF sy-subrc <> 0.
      EXIT.
    ENDIF.
    APPEND gs_prev TO i_infile.
  ENDDO.
  CLOSE DATASET p_infile.

ENDFORM.                    " GET_FROM_APPL

*&---------------------------------------------------------------------*
*&      Form  CHECK_DATE
*&---------------------------------------------------------------------*
*       Check the date of the file
*----------------------------------------------------------------------*
FORM check_date .

  DATA: lv_mtime    TYPE p  DECIMALS 0,
        lv_time(10) TYPE c,
        lv_mdate    TYPE sydatum.

  CLEAR gv_error.

  CALL FUNCTION 'EPS_GET_FILE_ATTRIBUTES'
    EXPORTING
      file_name              = p_infile
      dir_name               = g_directory
    IMPORTING
      file_mtime             = lv_mtime
    EXCEPTIONS
      read_directory_failed  = 1
      read_attributes_failed = 2
      OTHERS                 = 3.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    LEAVE LIST-PROCESSING.
  ENDIF.

  PERFORM p6_to_date_time_tz IN PROGRAM rstr0400
                             USING lv_mtime
                                   lv_time
                                   lv_mdate.

  IF lv_mdate NE p_mdate.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '000'
            WITH 'Wrong date of input file'(e02) lv_mdate.
    gv_error = gc_x.
  ENDIF.

ENDFORM.                    " CHECK_DATE
*&---------------------------------------------------------------------*
*&      Form  FRM_DO_CONF
*&---------------------------------------------------------------------*
*       Do confirm and send IDOC type YSE_VT_ORD_COMPL to VT
*----------------------------------------------------------------------*
*      -->P_AUFNR  text
*      -->P_IWERK  text
*      -->P_INGPR  text
*      -->P_OBJNR  text
*----------------------------------------------------------------------*
FORM frm_do_conf  USING    p_aufnr
                           p_iwerk
                           p_ingpr
                           p_objnr.

  DATA: lv_stat(4)       TYPE c,
        lv_ingpr         TYPE ingrp,
        ls_afru          TYPE ty_afru,
        ls_afko          TYPE ty_afko.

  CLEAR: lv_ingpr.
  SELECT SINGLE ingpr INTO lv_ingpr
    FROM yse_vt_sel WHERE iwerk = p_iwerk
                      AND ingpr = p_ingpr.

  IF sy-subrc <> 0.
    SELECT SINGLE ingpr INTO lv_ingpr
      FROM yse_vt_sel WHERE iwerk = p_iwerk
                        AND ingpr = '*'.
  ENDIF.

  IF NOT lv_ingpr IS INITIAL.

    CALL FUNCTION 'STATUS_CHECK'
      EXPORTING
        objnr             = p_objnr
        status            = gc_stat_conf      "Header Confirmed
      EXCEPTIONS
        object_not_found  = 1
        status_not_active = 2
        OTHERS            = 3.

    IF sy-subrc = 0.
      lv_stat = 'CNF'.
      CALL FUNCTION 'YSE_VT_OUT_CONF'
        EXPORTING
          im_aufnr = p_aufnr
          im_stat  = lv_stat
          im_plant = p_iwerk.
    ELSE.
      LOOP AT gt_afko INTO ls_afko
          WHERE aufnr = p_aufnr.
        READ TABLE gt_afru INTO ls_afru
          WITH KEY  rueck = ls_afko-rueck
                    rmzhl = ls_afko-rmzhl.
        IF ls_afru-aueru <> ' '.            "Operation - Final confirmation
          lv_stat = 'PCNF'.
          CALL FUNCTION 'YSE_VT_OUT_CONF'
            EXPORTING
              im_aufnr = p_aufnr
              im_vornr = ls_afko-vornr
              im_stat  = lv_stat
              im_plant = p_iwerk.
        ENDIF.
      ENDLOOP.
    ENDIF.
  ENDIF.
ENDFORM.                    " FRM_DO_CONF

*Text symbol text£º
*E02:Wrong date of input file
*E03:Failed to open the file

*I02:Order & is being processed
*Selection text£º
*P_INFILE:        Input fileName
*P_MDATE:        Date of last change
