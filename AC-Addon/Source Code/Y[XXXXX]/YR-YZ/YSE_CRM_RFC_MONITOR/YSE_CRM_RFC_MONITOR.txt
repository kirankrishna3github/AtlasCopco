*----------------------------------------------------------------------*
* PROGRAM ID    : YSE_BILL_OUTPUT                                      *
* PROGRAM TITLE : DISPALY CRM FM call                                  *
* AUTHOR        : LIN SHENG                                            *
* DATE          : 21/07/2014                                           *
* DEVELOPMENT ID:                                                      *
*                                                                      *
* CHANGE REQUEST NUMBER:                                               *
*                                                                      *
* Program Description:  DISPALY I/O of CRM FM call                     *
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
*  DATE    | NAME     |TRANSPORT  | CR# and short Description          *
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*

REPORT  YSE_CRM_RFC_MONITOR.
************************************************************************
* TABLES                                                              *
************************************************************************
TABLES:RS38L.
************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECT-OPTIONS: S_FMNAME FOR RS38L-NAME.
SELECT-OPTIONS: S_DATUM  FOR SY-DATUM.
SELECT-OPTIONS: S_UZEIT  FOR SY-UZEIT.


TYPES: BEGIN OF T_OUTPUT,
  FM_NAME  TYPE RS38L_FNAM,
  DATUM    TYPE SYDATUM,
  UZEIT    TYPE SYUZEIT,
  SEQUENCE TYPE YSE_SEQUEN,
  ITEM     TYPE STRING,
END OF T_OUTPUT.

DATA:IT_OUTTAB_I TYPE STANDARD TABLE OF T_OUTPUT.
DATA:IT_OUTTAB_O TYPE STANDARD TABLE OF T_OUTPUT.
DATA:WA_OUTTAB TYPE T_OUTPUT.

DATA:L_STY     TYPE LVC_S_STYL.
DATA:IT_STY    TYPE STANDARD TABLE OF LVC_S_STYL.

DATA:DG_DYNDOC_ID    TYPE REF TO CL_DD_DOCUMENT,
     DG_DYNDOC_ID2   TYPE REF TO CL_DD_DOCUMENT,
     DG_SPLITTER     TYPE REF TO CL_GUI_SPLITTER_CONTAINER,
     DG_PARENT_GRID  TYPE REF TO CL_GUI_CONTAINER,
     DG_PARENT_GRID2 TYPE REF TO CL_GUI_CONTAINER,
     DG_HTML_CNTRL   TYPE REF TO CL_GUI_HTML_VIEWER,
     DG_HTML_CNTRL2  TYPE REF TO CL_GUI_HTML_VIEWER,
     DG_PARENT_HTML  TYPE REF TO CL_GUI_CONTAINER,
     DG_PARENT_HTML2 TYPE REF TO CL_GUI_CONTAINER,
     GO_GRID         TYPE REF TO CL_GUI_ALV_GRID,
     GO_GRID2        TYPE REF TO CL_GUI_ALV_GRID,
     LS_VARI         TYPE DISVARIANT,
     LS_SLA          TYPE LVC_S_LAYO,
     IT_FIELDCAT_I   TYPE LVC_T_FCAT,"lvc_t_fcat,
     IT_FIELDCAT_O   TYPE LVC_T_FCAT,"lvc_t_fcat,
     IS_FIELDCAT     TYPE LVC_S_FCAT.

CLASS Z_ALV_EVENT_RECEIVER DEFINITION DEFERRED.
DATA: G_HANDLER      TYPE REF TO Z_ALV_EVENT_RECEIVER.
************************************************************************
CLASS Z_ALV_EVENT_RECEIVER DEFINITION.
  PUBLIC SECTION .
    METHODS:
    TOP_OF_PAGE FOR EVENT TOP_OF_PAGE              "event handler
                         OF CL_GUI_ALV_GRID
                         IMPORTING E_DYNDOC_ID.
ENDCLASS.               "Z_ALV_EVENT_RECEIVER

*----------------------------------------------------------------------*
*       CLASS Z_ALV_EVENT_RECEIVER IMPLEMENTATION
*----------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
CLASS Z_ALV_EVENT_RECEIVER IMPLEMENTATION.
  METHOD TOP_OF_PAGE.
    PERFORM EVENT_TOP_OF_PAGE USING E_DYNDOC_ID.
  ENDMETHOD.                    "TOP_OF_PAGE
ENDCLASS.                    "Z_ALV_EVENT_RECEIVER IMPLEMENTATION

*&---------------------------------------------------------------------*
*&  MAIN PROGRAM                                                       *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
  PERFORM GET_DATA.
  CALL SCREEN 9000.
*&---------------------------------------------------------------------*
*&      Module  STATUS_9000  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE STATUS_9000 OUTPUT.
  SET PF-STATUS 'BUT_9000'.
*  SET TITLEBAR 'xxx'.
ENDMODULE.                 " STATUS_9000  OUTPUT

*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_9000  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE USER_COMMAND_9000 INPUT.
  CASE SY-UCOMM.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN 'CANCEL' OR 'EXIT'.
      LEAVE PROGRAM.
  ENDCASE.
ENDMODULE.                 " USER_COMMAND_9000  INPUT
*&---------------------------------------------------------------------*
*&      Module  init_output  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE INIT_OUTPUT OUTPUT.

  PERFORM SPLIT_SCREEN.
  PERFORM BUILD_FIELDCATALOG.
  PERFORM DISPLAY_DATA.
*&---------------------------------------------------------------------*
ENDMODULE.                 " init_output  OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SPLIT_SCREEN
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM SPLIT_SCREEN .

  IF DG_DYNDOC_ID  IS INITIAL.
    CREATE OBJECT DG_DYNDOC_ID
      EXPORTING
        STYLE = 'ALV_GRID'.

    CREATE OBJECT DG_DYNDOC_ID2
      EXPORTING
        STYLE = 'ALV_GRID2'.
  ENDIF.

  IF DG_SPLITTER IS  INITIAL.
    CREATE OBJECT DG_SPLITTER
      EXPORTING
        PARENT  = CL_GUI_CONTAINER=>SCREEN0
        ROWS    = 2
        COLUMNS = 2.
  ENDIF.

  CALL METHOD DG_SPLITTER->GET_CONTAINER
    EXPORTING
      ROW       = 1
      COLUMN    = 1
    RECEIVING
      CONTAINER = DG_PARENT_HTML.

  CALL METHOD DG_SPLITTER->GET_CONTAINER
    EXPORTING
      ROW       = 1
      COLUMN    = 2
    RECEIVING
      CONTAINER = DG_PARENT_HTML2.

  CALL METHOD DG_SPLITTER->GET_CONTAINER
    EXPORTING
      ROW       = 2
      COLUMN    = 1
    RECEIVING
      CONTAINER = DG_PARENT_GRID.

  CALL METHOD DG_SPLITTER->GET_CONTAINER
    EXPORTING
      ROW       = 2
      COLUMN    = 2
    RECEIVING
      CONTAINER = DG_PARENT_GRID2.

  CALL METHOD DG_SPLITTER->SET_ROW_HEIGHT
    EXPORTING
      ID     = 1
      HEIGHT = 10.

ENDFORM.                    " SPLIT_SCREEN
*&---------------------------------------------------------------------*
*&      Form  BUILD_FIELDCATALOG
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM BUILD_FIELDCATALOG .
  PERFORM ADD_FILED USING 'FM_NAME'
                          'Function Name'
                           '9'
                            ''
                            ''
                            ''
                            '' .

  PERFORM ADD_FILED USING 'DATUM' 'Date' '8' ' ' ' ' ' ' ' ' .
  PERFORM ADD_FILED USING 'UZEIT' 'TIMS' '8' ' ' ' ' ' ' ' ' .
  PERFORM ADD_FILED USING 'SEQUENCE' 'Sequence No' '9' ' ' ' ' ' ' ' ' .
  PERFORM ADD_FILED USING 'ITEM' 'FM INPUT ' '8' ' ' ' ' ' ' '' .
  PERFORM ADD_FILED USING 'ITEM' 'FM OUTPUT' '8' ' ' ' ' ' ' 'X' .

ENDFORM.                    " BUILD_FIELDCATALOG
*&---------------------------------------------------------------------*
*&      Form  ADD_FILED
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM ADD_FILED USING FIELDNAME  TYPE LVC_S_FCAT-FIELDNAME
                     COLTEXT    TYPE LVC_S_FCAT-COLTEXT
                     OUTPUTLEN  TYPE LVC_S_FCAT-OUTPUTLEN
                     COL_POS    TYPE LVC_S_FCAT-COL_POS
                     JUST       TYPE LVC_JUST
                     EDIT       TYPE LVC_S_FCAT-EDIT
                     ZFIE_TZ    TYPE CHAR1 .

  IS_FIELDCAT-FIELDNAME    = FIELDNAME.
  IS_FIELDCAT-COLTEXT      = COLTEXT.
  IS_FIELDCAT-OUTPUTLEN    = OUTPUTLEN.
  IS_FIELDCAT-COL_POS      = COL_POS.
  IS_FIELDCAT-EDIT         = EDIT.
  IS_FIELDCAT-JUST         = JUST.
  IF ZFIE_TZ = ''.
    APPEND IS_FIELDCAT TO IT_FIELDCAT_I.
    APPEND IS_FIELDCAT TO IT_FIELDCAT_O.
  ELSE.
    MODIFY IT_FIELDCAT_O FROM IS_FIELDCAT TRANSPORTING COLTEXT WHERE FIELDNAME = FIELDNAME.
  ENDIF.
  CLEAR IS_FIELDCAT.
ENDFORM.                    "ADD_FILED
*&---------------------------------------------------------------------*
*&      Form  DISPLAY_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM DISPLAY_DATA.
  DATA:LT_SORT TYPE  LVC_T_SORT.
  DATA:LS_SORT TYPE  LVC_S_SORT.

  IF GO_GRID IS INITIAL.
    CREATE OBJECT GO_GRID
      EXPORTING
        I_PARENT = DG_PARENT_GRID.

    CREATE OBJECT GO_GRID2
      EXPORTING
        I_PARENT = DG_PARENT_GRID2.

    CREATE OBJECT G_HANDLER.
    SET HANDLER G_HANDLER->TOP_OF_PAGE FOR GO_GRID.

    LS_SLA-CWIDTH_OPT = 'X'.
    LS_SLA-TOTALS_BEF = 'X'.
    LS_VARI-REPORT = SY-REPID.
    LS_SLA-NO_TOOLBAR = ' '.

    CALL METHOD GO_GRID->SET_TABLE_FOR_FIRST_DISPLAY
      EXPORTING
        IS_VARIANT                    = LS_VARI
        I_SAVE                        = 'A'
        IS_LAYOUT                     = LS_SLA
      CHANGING
        IT_OUTTAB                     = IT_OUTTAB_I
        IT_FIELDCATALOG               = IT_FIELDCAT_I[]
*        IT_SORT                       = LT_SORT[]
      EXCEPTIONS
        INVALID_PARAMETER_COMBINATION = 1
        PROGRAM_ERROR                 = 2
        TOO_MANY_LINES                = 3
        OTHERS                        = 4.


    CALL METHOD GO_GRID2->SET_TABLE_FOR_FIRST_DISPLAY
      EXPORTING
        IS_VARIANT                    = LS_VARI
        I_SAVE                        = 'A'
        IS_LAYOUT                     = LS_SLA
      CHANGING
        IT_OUTTAB                     = IT_OUTTAB_O
        IT_FIELDCATALOG               = IT_FIELDCAT_O[]
*        IT_SORT                       = LT_SORT[]
      EXCEPTIONS
        INVALID_PARAMETER_COMBINATION = 1
        PROGRAM_ERROR                 = 2
        TOO_MANY_LINES                = 3
        OTHERS                        = 4.

  ELSE.
    CALL METHOD GO_GRID->REFRESH_TABLE_DISPLAY.
    CALL METHOD GO_GRID2->REFRESH_TABLE_DISPLAY.
  ENDIF.

* Initializing document
  CALL METHOD DG_DYNDOC_ID->INITIALIZE_DOCUMENT.
* Processing events
  CALL METHOD GO_GRID->LIST_PROCESSING_EVENTS
    EXPORTING
      I_EVENT_NAME = 'TOP_OF_PAGE'
      I_DYNDOC_ID  = DG_DYNDOC_ID.
ENDFORM.                    " DISPLAY_DATA
*&---------------------------------------------------------------------*
*&      Form  EVENT_TOP_OF_PAGE
*&---------------------------------------------------------------------*
*
*----------------------------------------------------------------------*
*      -->P_E_DYNDOC_ID  text
*----------------------------------------------------------------------*
FORM EVENT_TOP_OF_PAGE  USING    P_E_DYNDOC_ID.
  DATA : DL_TEXT_I(255) TYPE C.
  DATA : DL_TEXT_O(255) TYPE C.
  DATA : DL_LENGTH  TYPE I,                           " Length
         DL_BACKGROUND_ID TYPE SDYDO_KEY VALUE SPACE. " Background_id

  DL_TEXT_i = 'CRM FUNCTION INPUT'.
  DL_TEXT_o = 'CRM FUNCTION OUTPUT'.

  CALL METHOD DG_DYNDOC_ID->ADD_TEXT
    EXPORTING
      TEXT         = DL_TEXT_i
      SAP_EMPHASIS = CL_DD_AREA=>HEADING.

  CALL METHOD DG_DYNDOC_ID2->ADD_TEXT
    EXPORTING
      TEXT         = DL_TEXT_o
      SAP_EMPHASIS = CL_DD_AREA=>HEADING.
* Creating html control
  IF DG_HTML_CNTRL IS INITIAL.
    CREATE OBJECT DG_HTML_CNTRL
      EXPORTING
        PARENT = DG_PARENT_HTML.

    CREATE OBJECT DG_HTML_CNTRL2
      EXPORTING
        PARENT = DG_PARENT_HTML2.
  ENDIF.

  IF DG_DYNDOC_ID IS NOT INITIAL.
    CALL METHOD DG_DYNDOC_ID->MERGE_DOCUMENT.
* Set wallpaper
    CALL METHOD DG_DYNDOC_ID->SET_DOCUMENT_BACKGROUND
      EXPORTING
        PICTURE_ID = DL_BACKGROUND_ID.
* Connect TOP document to HTML-Control
    DG_DYNDOC_ID->HTML_CONTROL = DG_HTML_CNTRL.
* Display TOP document
    CALL METHOD DG_DYNDOC_ID->DISPLAY_DOCUMENT
      EXPORTING
        REUSE_CONTROL      = 'X'
        PARENT             = DG_PARENT_HTML
      EXCEPTIONS
        HTML_DISPLAY_ERROR = 1.
  ENDIF.
  IF SY-SUBRC NE 0.
*    MESSAGE I999 WITH 'Error in displaying top-of-page'(036).
  ENDIF.

  IF DG_DYNDOC_ID2 IS NOT INITIAL.
    CALL METHOD DG_DYNDOC_ID2->MERGE_DOCUMENT.
* Set wallpaper
    CALL METHOD DG_DYNDOC_ID2->SET_DOCUMENT_BACKGROUND
      EXPORTING
        PICTURE_ID = DL_BACKGROUND_ID.
* Connect TOP document to HTML-Control
    DG_DYNDOC_ID2->HTML_CONTROL = DG_HTML_CNTRL2.
* Display TOP document
    CALL METHOD DG_DYNDOC_ID2->DISPLAY_DOCUMENT
      EXPORTING
        REUSE_CONTROL      = 'X'
        PARENT             = DG_PARENT_HTML2
      EXCEPTIONS
        HTML_DISPLAY_ERROR = 1.
  ENDIF.
ENDFORM.                    " EVENT_TOP_OF_PAGE
*&---------------------------------------------------------------------*
*&      Form  GET_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM GET_DATA .
  DATA: IT_FM TYPE STANDARD TABLE OF YSE_CRM_IO.
  DATA: WA_FM TYPE  YSE_CRM_IO.
  DATA: LV_XML  TYPE STRING.
  DATA: LT_LFILE_I TYPE YSE_T_LFILE.
  DATA: LT_LFILE_O TYPE YSE_T_LFILE.
  DATA: LS_LFILE TYPE YSE_S_LFILE.
  DATA: LS_FM TYPE T_OUTPUT.

  SELECT *
    FROM YSE_CRM_IO
    INTO TABLE IT_FM
   WHERE FM_NAME IN S_FMNAME
     AND DATUM IN S_DATUM
     AND UZEIT IN S_UZEIT.

  CHECK IT_FM IS NOT INITIAL.
  LOOP AT IT_FM INTO WA_FM.
    MOVE-CORRESPONDING WA_FM TO LS_FM.
    LV_XML = WA_FM-ZINPUT.
    IF LV_XML is NOT INITIAL.
      CALL TRANSFORMATION YSE_TRANS_FILE SOURCE XML LV_XML RESULT LFILE = LT_LFILE_I.
    ENDIF.
    LOOP AT LT_LFILE_I INTO LS_LFILE.
      LS_FM-ITEM = LS_LFILE-TEXT.
      APPEND LS_FM TO IT_OUTTAB_I.
      CLEAR:LS_LFILE.
    ENDLOOP.
    IF SY-SUBRC <> 0.
      APPEND LS_FM TO IT_OUTTAB_I.
    ENDIF.

    CLEAR:LV_XML,LS_FM-ITEM.
    LV_XML = WA_FM-ZOUTPUT.
    IF LV_XML is NOT INITIAL.
      CALL TRANSFORMATION YSE_TRANS_FILE SOURCE XML LV_XML RESULT LFILE = LT_LFILE_O.
    ENDIF.
    LOOP AT LT_LFILE_O INTO LS_LFILE.
      LS_FM-ITEM = LS_LFILE-TEXT.
      APPEND LS_FM TO IT_OUTTAB_O.
      CLEAR:LS_LFILE.
    ENDLOOP.
    IF SY-SUBRC <> 0.
      APPEND LS_FM TO IT_OUTTAB_O.
    ENDIF.

    CLEAR:WA_FM,LV_XML,LT_LFILE_I,LT_LFILE_O,LS_FM.
  ENDLOOP.

ENDFORM.                    " GET_DATA

*Selection text£º
*S_DATUM:        Date
*S_FMNAME:        Function Name
*S_UZEIT:        Time
