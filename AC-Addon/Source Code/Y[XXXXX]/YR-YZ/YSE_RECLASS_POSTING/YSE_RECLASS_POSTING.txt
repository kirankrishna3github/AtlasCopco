************************************************************************
* PROGRAM ID           : YSE_RECLASS_POSTING                           *
* PROGRAM TITLE        : Reclass posting GR/IR                         *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 27/12/2011                                    *
* Change request nr.   : CR2365                                        *
* Transport REQUEST NR.: CD1K969594                                    *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE      | NAME               | CORR.NR.   | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 07/05/2012 | Jules Smets        | CD1K971694 | CR2365      *
*         | Copy fields also to second item                            *
************************************************************************
REPORT  yse_reclass_posting line-SIZE 132.

TABLES: bsis.

DATA: lt_yse_prctr_reclas TYPE SORTED TABLE OF yse_prctr_reclas
          WITH UNIQUE KEY prctr segment,
      wa_prctr_reclas LIKE LINE OF lt_yse_prctr_reclas.

DATA: BEGIN OF gt_bsis  OCCURS 0,
         segment   TYPE fb_segment,
         prctr     TYPE prctr,
         belnr     TYPE belnr_d,
         gjahr     TYPE gjahr,
         buzei     TYPE buzei,
         zuonr     TYPE dzuonr,
         dmbtr     TYPE dmbtr,
         shkzg     TYPE shkzg,
         waers     TYPE waers,
         mwskz     TYPE bsis-mwskz,
         wrbtr     TYPE wrbtr,
         hkont     TYPE hkont,
         nsegment  TYPE fb_segment,
         ebeln     TYPE ebeln,
         ebelp     TYPE ebelp,
       END OF gt_bsis,
       wa_bsis like gt_bsis.

* Document data
DATA: BEGIN OF gt_doc  OCCURS 0,
         belnr       TYPE belnr_d,
         belnr_orig  TYPE belnr_d,
         waers       TYPE waers,
         prctr       TYPE prctr,
         osegment    TYPE fb_segment,
         nsegment    TYPE fb_segment,
         value_lc    TYPE wogxxx,
         value_dc    TYPE wogxxx,
      END OF gt_doc.

DATA: i_bdcdata LIKE bdcdata  OCCURS 0  WITH HEADER LINE,
      struct_bdcdata TYPE bdcdata,
      gt_err    LIKE bdcmsgcoll  OCCURS 0  WITH HEADER LINE.

DATA: gv_waers_cc  TYPE waers,
      gv_date_eom  TYPE datum,
      gv_value_lc  TYPE wogxxx,
      gv_value_dc  TYPE wogxxx,
      gv_xintb     TYPE xfeld,
      gv_hkont     TYPE hkont,
      gv_msgtxt    LIKE t100-text,
      gv_errlst    TYPE xfeld     VALUE 'X',
      gv_detlst    TYPE xfeld     VALUE ' ',
      gv_mode(1)   TYPE c         VALUE 'N'.

CONSTANTS: gc_sa       TYPE blart       VALUE 'SA'.


* Selections
SELECTION-SCREEN BEGIN OF BLOCK sel WITH FRAME TITLE text-s01.
PARAMETERS:     p_bukrs  TYPE bukrs      OBLIGATORY MEMORY ID buk,
                p_budat  TYPE bsis-budat OBLIGATORY,
                p_acc1   TYPE bsis-hkont DEFAULT '0001402900' OBLIGATORY,
                p_acc2   TYPE bsis-hkont,
                p_acc3   TYPE bsis-hkont,
                p_acc4   TYPE bsis-hkont,
                p_acc5   TYPE bsis-hkont,
                p_acc6   TYPE bsis-hkont.
SELECTION-SCREEN SKIP.
PARAMETERS:     p_test AS CHECKBOX  DEFAULT 'X'.
SELECTION-SCREEN END OF BLOCK sel.


*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON p_bukrs.

  PERFORM  check_authorization.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*

INITIALIZATION.

  p_budat = sy-datum.
  p_budat+6(2) = '01'.
  CALL FUNCTION 'DATE_GET_MONTH_LASTDAY'
    EXPORTING
      i_date = p_budat
    IMPORTING
      e_date = gv_date_eom.

  p_budat = gv_date_eom.

*----------------------------------------------------------------------*
START-OF-SELECTION.

  PERFORM initial.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_bsis[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No documents selected'(e01).
    EXIT.
  ENDIF.

* Process data
  PERFORM process_data.

* List data
  PERFORM list_data.


*----------------------------------------------------------------------*
TOP-OF-PAGE.

  WRITE: /01 'Date:', sy-datum,
          25 'Reclass segment',
          75 'Company:', p_bukrs,
          97(4) sy-pagno.
  ULINE.

  IF NOT gv_errlst IS INITIAL.
    WRITE: /01 'Errors'.
  ELSE.
    WRITE: /01 'Reclass Doc',
            13 'Orig. Doc',
            25 'Doc.Curr',
            32 '        Amt Doc.Curr',
            54 '          Amt L.Curr',
            76 'Prof.Cent.',
            88 'Old segment',
           100 'New segment'.
  ENDIF.

  ULINE.


*----------------------------------------------------------------------*
*       FORMS
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  AUTHORITY-CHECK OBJECT 'F_BKPF_BUK'
           ID 'ACTVT' DUMMY
           ID 'BUKRS' FIELD p_bukrs.

  IF sy-subrc = 4.
*     No authorisation to display data
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH p_bukrs.
    EXIT.
  ELSEIF sy-subrc <> 0.
*     Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
    EXIT.
  ENDIF.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  INITIAL
*&---------------------------------------------------------------------*
*       Initialization
*----------------------------------------------------------------------*
FORM initial .

* Select profitcenters valid as per the keydate
  SELECT * FROM yse_prctr_reclas INTO TABLE lt_yse_prctr_reclas
    WHERE yvalfrom le p_budat
      AND yvalto   ge p_budat.

* Company currency
  SELECT SINGLE waers INTO gv_waers_cc
    FROM t001 WHERE bukrs = p_bukrs.

ENDFORM.                    " INITIAL

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Open items (G/L accounts)
  SELECT segment prctr belnr gjahr buzei zuonr dmbtr shkzg
         waers mwskz wrbtr hkont
         INTO TABLE gt_bsis
         FROM bsis
         WHERE bukrs   =  p_bukrs
           AND hkont   IN (p_acc1, p_acc2, p_acc3, p_acc4, p_acc5, p_acc6)
           AND budat   <= p_budat.

  CHECK NOT gt_bsis[] IS INITIAL.

  LOOP AT gt_bsis.
    READ TABLE lt_yse_prctr_reclas INTO wa_prctr_reclas
      WITH KEY prctr = gt_bsis-prctr.
    IF sy-subrc = 0.
      SELECT SINGLE ebeln ebelp INTO (gt_bsis-ebeln, gt_bsis-ebelp)
        FROM bseg WHERE belnr = gt_bsis-belnr
                    AND bukrs = p_bukrs
                    AND gjahr = gt_bsis-gjahr
                    AND buzei = gt_bsis-buzei.

      gt_bsis-nsegment = wa_prctr_reclas-segment.
      IF gt_bsis-segment = gt_bsis-nsegment.
        delete gt_bsis.
      ELSE.
        modify gt_bsis.
      ENDIF.
    ELSE.
      delete gt_bsis.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  PROCESS_DATA
*&---------------------------------------------------------------------*
*       Process data
*----------------------------------------------------------------------*
FORM process_data .

  SORT gt_bsis BY segment prctr belnr.

  LOOP AT gt_bsis.

*   Begin
    AT FIRST.
*     Open accounts
      gv_xintb = ' '.
      gv_hkont = p_acc1.
      PERFORM call_fss0.

      IF NOT p_acc2 IS INITIAL.
        gv_hkont = p_acc2.
        PERFORM call_fss0.
      ENDIF.
      IF NOT p_acc3 IS INITIAL.
        gv_hkont = p_acc3.
        PERFORM call_fss0.
      ENDIF.
      IF NOT p_acc4 IS INITIAL.
        gv_hkont = p_acc4.
        PERFORM call_fss0.
      ENDIF.
      IF NOT p_acc5 IS INITIAL.
        gv_hkont = p_acc5.
        PERFORM call_fss0.
      ENDIF.
      IF NOT p_acc6 IS INITIAL.
        gv_hkont = p_acc6.
        PERFORM call_fss0.
      ENDIF.
    ENDAT.

    wa_bsis = gt_bsis.

    CLEAR: gv_value_lc, gv_value_dc.

*   Detail.
    IF gt_bsis-shkzg = 'S'.
      gv_value_lc = gv_value_lc + wa_bsis-dmbtr.
      gv_value_dc = gv_value_dc + wa_bsis-wrbtr.
    ELSE.
      gv_value_lc = gv_value_lc - wa_bsis-dmbtr.
      gv_value_dc = gv_value_dc - wa_bsis-wrbtr.
    ENDIF.

    IF gv_value_lc NE 0.
      CLEAR gt_doc.
      IF p_test IS INITIAL.
        PERFORM post_reclass.
      ENDIF.
      gt_doc-belnr_orig = wa_bsis-belnr.
      gt_doc-waers      = wa_bsis-waers.
      gt_doc-prctr      = wa_bsis-prctr.
      gt_doc-osegment   = wa_bsis-segment.
      gt_doc-nsegment   = wa_bsis-nsegment.
      gt_doc-value_lc   = gv_value_lc.
      gt_doc-value_dc   = gv_value_dc.
      APPEND gt_doc.
    ENDIF.

*   End
    AT LAST.
*     Close accounts
      gv_xintb = 'X'.
      gv_hkont = p_acc1.
      PERFORM call_fss0.

      IF NOT p_acc2 IS INITIAL.
        gv_hkont = p_acc2.
        PERFORM call_fss0.
      ENDIF.
      IF NOT p_acc3 IS INITIAL.
        gv_hkont = p_acc3.
        PERFORM call_fss0.
      ENDIF.
      IF NOT p_acc4 IS INITIAL.
        gv_hkont = p_acc4.
        PERFORM call_fss0.
      ENDIF.
      IF NOT p_acc5 IS INITIAL.
        gv_hkont = p_acc5.
        PERFORM call_fss0.
      ENDIF.
      IF NOT p_acc6 IS INITIAL.
        gv_hkont = p_acc6.
        PERFORM call_fss0.
      ENDIF.
    ENDAT.

  ENDLOOP.

ENDFORM.                    " PROCESS_DATA

*&---------------------------------------------------------------------*
*&      Form  CALL_FSS0
*&---------------------------------------------------------------------*
*       Prepare & Call transaction FSS0
*----------------------------------------------------------------------*
FORM call_fss0 .

  REFRESH: i_bdcdata, gt_err.

  PERFORM bdc_dynpro      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=ACC_MOD'.
  PERFORM bdc_field       USING 'GLACCOUNT_SCREEN_KEY-SAKNR' gv_hkont.
  PERFORM bdc_field       USING 'GLACCOUNT_SCREEN_KEY-BUKRS' p_bukrs.

  PERFORM bdc_dynpro      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=TAB02'.

  PERFORM bdc_dynpro      USING 'SAPLGL_ACCOUNT_MASTER_MAINTAIN' '2001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=SAVE'.
  PERFORM bdc_field       USING 'GLACCOUNT_SCREEN_CCODE-XINTB' gv_xintb.

  CALL TRANSACTION 'FSS0' USING i_bdcdata
                          MODE gv_mode
                          UPDATE 'S'
                          MESSAGES INTO gt_err.

* Check for errors
  LOOP AT gt_err WHERE msgtyp = 'E'
                    OR msgtyp = 'A'.
    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*        SPRSL                   = SY-LANGU
        arbgb                   = gt_err-msgid
        msgnr                   = gt_err-msgnr
        msgv1                   = gt_err-msgv1
        msgv2                   = gt_err-msgv2
        msgv3                   = gt_err-msgv3
        msgv4                   = gt_err-msgv4
      IMPORTING
        msgtext                 = gv_msgtxt
      EXCEPTIONS
        message_not_found       = 1
        OTHERS                  = 2.

    WRITE: / gv_msgtxt.
  ENDLOOP.

ENDFORM.                    " CALL_FSS0

*----------------------------------------------------------------------*
*        Start new screen                                              *
*----------------------------------------------------------------------*
FORM bdc_dynpro USING program dynpro.

  CLEAR i_bdcdata.
  i_bdcdata-program  = program.
  i_bdcdata-dynpro   = dynpro.
  i_bdcdata-dynbegin = 'X'.
  APPEND i_bdcdata.

ENDFORM.                    "BDC_DYNPRO

*----------------------------------------------------------------------*
*        Insert field                                                  *
*----------------------------------------------------------------------*
FORM bdc_field USING fnam fval.

  CLEAR i_bdcdata.
  i_bdcdata-fnam = fnam.
  i_bdcdata-fval = fval.
  APPEND i_bdcdata.

ENDFORM.                    "bdc_field

*&---------------------------------------------------------------------*
*&      Form  POST_RECLASS
*&---------------------------------------------------------------------*
*       Reclass posting
*----------------------------------------------------------------------*
FORM post_reclass .

  DATA: lv_date(10)     TYPE c,
        lv_wrbtr_lc(16) TYPE c,
        lv_wrbtr_dc(16) TYPE c,
        lv_zuonr        TYPE dzuonr,
        lv_hkont        TYPE hkont.

  REFRESH: i_bdcdata, gt_err.

*  WRITE gv_date_eom TO lv_date.
  WRITE p_budat TO lv_date.
  WRITE gv_value_lc TO lv_wrbtr_lc
                       CURRENCY gv_waers_cc NO-SIGN.
  WRITE gv_value_dc TO lv_wrbtr_dc
                       CURRENCY wa_bsis-waers NO-SIGN.

* Header
  PERFORM bdc_dynpro      USING 'SAPMF05A' '0100'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '/00'.
  PERFORM bdc_field       USING 'BKPF-BLDAT' lv_date.
  PERFORM bdc_field       USING 'BKPF-BLART' gc_sa.
  PERFORM bdc_field       USING 'BKPF-BUKRS' p_bukrs.
  PERFORM bdc_field       USING 'BKPF-BUDAT' lv_date.
  PERFORM bdc_field       USING 'BKPF-WAERS' wa_bsis-waers.
  PERFORM bdc_field       USING 'BKPF-XBLNR' 'RECL.SEGM'.
  IF gv_value_lc > 0.
    PERFORM bdc_field       USING 'RF05A-NEWBS' '50'.
  ELSE.
    PERFORM bdc_field       USING 'RF05A-NEWBS' '40'.
  ENDIF.
  PERFORM bdc_field       USING 'RF05A-NEWKO' wa_bsis-hkont.

* Item 1
  PERFORM bdc_dynpro      USING 'SAPMF05A' '0300'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=ZK'.
  PERFORM bdc_field       USING 'BSEG-WRBTR' lv_wrbtr_dc.
  PERFORM bdc_field       USING 'BSEG-ZUONR' wa_bsis-zuonr.
  PERFORM bdc_field       USING 'BSEG-EBELN' wa_bsis-ebeln.
  PERFORM bdc_field       USING 'BSEG-EBELP' wa_bsis-ebelp.
  IF wa_bsis-waers NE gv_waers_cc.
    PERFORM bdc_field     USING 'BSEG-DMBTR' lv_wrbtr_lc.
  ENDIF.
  if NOT wa_bsis-mwskz IS INITIAL.
    PERFORM bdc_field       USING 'BSEG-MWSKZ' wa_bsis-mwskz.
  else.
    if wa_bsis-hkont = '0001402900' or
       wa_bsis-hkont = '0001790910' or
       wa_bsis-hkont = '0002463901' or
       wa_bsis-hkont = '0001511901' or
       wa_bsis-hkont = '0002441901' or
       wa_bsis-hkont = '0001402901' or
       wa_bsis-hkont = '0002444900'.
      PERFORM bdc_field       USING 'BSEG-MWSKZ' 'ZP'.
    endif.
  endif.
  PERFORM bdc_field       USING 'DKACB-FMORE' 'X'.

* Item 1: more date
  PERFORM bdc_dynpro      USING 'SAPLKACB' '0002'.
  PERFORM bdc_field       USING 'BDC_OKCODE' 'ENTE'.
  PERFORM bdc_field       USING 'COBL-PRCTR'   wa_bsis-prctr.
  PERFORM bdc_field       USING 'COBL-SEGMENT' wa_bsis-segment.

* Item 2
  PERFORM bdc_dynpro      USING 'SAPMF05A' '0330'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '/00'.
  IF gv_value_lc > 0.
    PERFORM bdc_field       USING 'RF05A-NEWBS' '40'.
  ELSE.
    PERFORM bdc_field       USING 'RF05A-NEWBS' '50'.
  ENDIF.
  PERFORM bdc_field       USING 'RF05A-NEWKO' wa_bsis-hkont.

  PERFORM bdc_dynpro      USING 'SAPMF05A' '0300'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=ZK'.
  PERFORM bdc_field       USING 'BSEG-WRBTR' lv_wrbtr_dc.
*  PERFORM bdc_field       USING 'BSEG-ZUONR' lv_zuonr.
*** MOD-001 * begin ***
  PERFORM bdc_field       USING 'BSEG-ZUONR' wa_bsis-zuonr.
  PERFORM bdc_field       USING 'BSEG-EBELN' wa_bsis-ebeln.
  PERFORM bdc_field       USING 'BSEG-EBELP' wa_bsis-ebelp.
*** MOD-001 * end ***
  IF wa_bsis-waers NE gv_waers_cc.
    PERFORM bdc_field     USING 'BSEG-DMBTR' lv_wrbtr_lc.
  ENDIF.
  if NOT wa_bsis-mwskz IS INITIAL.
    PERFORM bdc_field       USING 'BSEG-MWSKZ' wa_bsis-mwskz.
  else.
    if wa_bsis-hkont = '0001402900' or
       wa_bsis-hkont = '0001790910' or
       wa_bsis-hkont = '0002463901' or
       wa_bsis-hkont = '0001511901' or
       wa_bsis-hkont = '0002441901' or
       wa_bsis-hkont = '0001402901' or
       wa_bsis-hkont = '0002444900'.
      PERFORM bdc_field       USING 'BSEG-MWSKZ' 'ZP'.
    endif.
  endif.
  PERFORM bdc_field       USING 'DKACB-FMORE' 'X'.
  PERFORM bdc_field       USING 'DKACB-FMORE' 'X'.

* Item 2: more date
  PERFORM bdc_dynpro      USING 'SAPLKACB' '0002'.
  PERFORM bdc_field       USING 'BDC_OKCODE' 'ENTE'.
  PERFORM bdc_field       USING 'COBL-PRCTR'   wa_bsis-prctr.
  PERFORM bdc_field       USING 'COBL-SEGMENT' wa_bsis-nsegment.

* Save
  PERFORM bdc_dynpro      USING 'SAPMF05A' '0330'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=BU'.

  CALL TRANSACTION 'FB01' USING i_bdcdata
                          MODE gv_mode
                          UPDATE 'S'
                          MESSAGES INTO gt_err.

* Check for errors
  LOOP AT gt_err WHERE msgtyp = 'E'
                    OR msgtyp = 'A'.
    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*        SPRSL                   = SY-LANGU
        arbgb                   = gt_err-msgid
        msgnr                   = gt_err-msgnr
        msgv1                   = gt_err-msgv1
        msgv2                   = gt_err-msgv2
        msgv3                   = gt_err-msgv3
        msgv4                   = gt_err-msgv4
      IMPORTING
        msgtext                 = gv_msgtxt
      EXCEPTIONS
        message_not_found       = 1
        OTHERS                  = 2.

    WRITE: / gv_msgtxt.
  ENDLOOP.

* No errors
  IF sy-subrc NE 0.
    READ TABLE gt_err WITH KEY msgid = 'F5'
                               msgnr = '312'.
    IF sy-subrc NE 0.
      WRITE: / 'No posting made for', wa_bsis-nsegment, wa_bsis-prctr, wa_bsis-belnr,
*                                      lv_zuonr,
                                      gv_value_lc, gv_waers_cc.
    ELSE.
      CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
        EXPORTING
          input  = gt_err-msgv1
        IMPORTING
          output = gt_doc-belnr.
    ENDIF.
  ENDIF.

ENDFORM.                    " POST_RECLASS

*&---------------------------------------------------------------------*
*&      Form  LIST_DATA
*&---------------------------------------------------------------------*
*       List data
*----------------------------------------------------------------------*
FORM list_data .

  CLEAR gv_errlst.
  NEW-PAGE.

  IF gt_doc[] IS INITIAL.
    WRITE: /01 'NO documents posted'.
  ELSE.
*   Posted documents
    LOOP AT gt_doc.
      WRITE: /01 gt_doc-belnr,
              13 gt_doc-belnr_orig,
              25 gt_doc-waers,
              32(20) gt_doc-value_dc CURRENCY gt_doc-waers,
              54(20) gt_doc-value_lc CURRENCY gv_waers_cc,
              76 gt_doc-prctr,
              88 gt_doc-osegment,
             100 gt_doc-nsegment.
    ENDLOOP.
  ENDIF.

  ULINE.

ENDFORM.                    " LIST_DATA

*Selection text£º
*P_ACC1:        G/L Account 1
*P_ACC2:        G/L Account 2
*P_ACC3:        G/L Account 3
*P_ACC4:        G/L Account 4
*P_ACC5:        G/L Account 5
*P_ACC6:        G/L Account 6
*P_BUDAT:        Open at Key Date
*P_BUKRS:D       .
*P_TEST:        Test Mode
