*----------------------------------------------------------------------*
* PROGRAM ID    : YSE_UPLOAD_PIR_SL                                    *
* PROGRAM TITLE : Mass upload PIR and sourcing list for Nanjing CC     *
* AUTHOR        : Johnny Wu                                            *
* DATE          : 17/01/2012                                           *
* DEVELOPMENT ID:                                                      *
*                                                                      *
* CHANGE REQUEST NUMBER: CR2392                                        *
*                                                                      *
* Program Description: Mass upload PIR and sourcing list for Nanjing CC*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
*  DATE    | NAME     |TRANSPORT  | CR# and short Description          *
*----------------------------------------------------------------------*
*
*17/01/2012|Johnny Wu |CD1K969899 |CR2392: Initial version             *
*18/01/2012|Johnny Wu |CD1K969907 |CR2392: Buf Fix                     *
*18/01/2012|Johnny Wu |CD1K969909 |CR2392: Buf Fix                     *
*19/01/2012|Johnny Wu |CD1K969925 |CR2392: Buf Fix                     *
*20/01/2012|Johnny Wu |CD1K969961 |CR2392: Enable background upload    *
*21/01/2012|Johnny Wu |CD1K969993 |CR2392: Enable background upload    *
*03/07/2013|Anda   Wu |CD1K976744 |
*----------------------------------------------------------------------*
REPORT yse_upload_pir_sl
       NO STANDARD PAGE HEADING LINE-SIZE 255.

*---------------------------------------------------------------------*
* TYPES                                                               *
*---------------------------------------------------------------------*
TYPES: BEGIN OF ty_item,
          lifnr TYPE eord-lifnr,
          matnr TYPE eord-matnr,
          ekorg TYPE eine-ekorg,
          werks TYPE eord-werks,
          seperator(1) TYPE c,
          kbetr(11) TYPE c, "like konp-kbetr,"100
          konwa LIKE konp-konwa,"RMB
          kpein(5) TYPE c," like konp-kpein,"1
          kmein TYPE konp-kmein,"PC
          datab TYPE rv13a-datab,"Date Begin
       END OF ty_item.

DATA: gt_lfile TYPE yse_t_lfile."Local file name
*      gt_message TYPE scr_strings,                   "Message table

DATA: gv_process_code TYPE char1.

CONSTANTS: gc_x TYPE c VALUE 'X'.

*include bdcrecx1.
***********************************************************************
LOAD-OF-PROGRAM.
  INCLUDE yse_bdcrecx1.

*----------------------------------------------------------------------*
*                          SELECTION SCREEN VARIABLES                  *
*----------------------------------------------------------------------*
  SELECTION-SCREEN BEGIN OF BLOCK bk1 WITH FRAME TITLE text-t01.
  PARAMETERS: cb_upld AS CHECKBOX  DEFAULT 'X',      "Do upload
              cb_updt AS CHECKBOX  DEFAULT 'X'.      "Do update
  SELECTION-SCREEN END OF BLOCK bk1.

  SELECTION-SCREEN BEGIN OF BLOCK bk2 WITH FRAME TITLE text-t02.
  PARAMETERS:
     p_appl TYPE yse_up_appl-appl NO-DISPLAY DEFAULT '02',"App Type
     p_path(1024) TYPE c LOWER CASE,  "Local File Path,
     p_jname  TYPE btcjob NO-DISPLAY DEFAULT 'YSE_UPLOAD_PIR_SL' .
  SELECTION-SCREEN END OF BLOCK bk2.

  SELECTION-SCREEN BEGIN OF BLOCK bk3 WITH FRAME TITLE text-bk1.
  PARAMETERS: p_pir_up RADIOBUTTON GROUP gp1 MODIF ID m1,
              p_sl_del RADIOBUTTON GROUP gp1 MODIF ID m1,
              p_sl_cre RADIOBUTTON GROUP gp1 MODIF ID m1.
  PARAMETERS: cb_back AS CHECKBOX DEFAULT 'X' MODIF ID m1.
  SELECTION-SCREEN END OF BLOCK bk3.


AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_path.
* Get local file path
  PERFORM select_file.

AT SELECTION-SCREEN OUTPUT.
  PERFORM set_screen.

START-OF-SELECTION.
  PERFORM check_selection.
  PERFORM set_bdc_para.
  PERFORM set_proc_code.
* If the function of  'UPLOAD' is choosed
  IF cb_upld = gc_x.
    PERFORM read_file.
*   Write entry to datebase table
    PERFORM write_db.
  ENDIF.

  IF cb_updt = gc_x.
    PERFORM mass_update.
  ENDIF.

*&---------------------------------------------------------------------*
*&      Form  SET_BDC_PARA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM set_bdc_para .
*   Set default call transaction settings
  user = sy-uname.

  ctu = 'X'.
  ctumode = 'N'.
  cupdate = 'L'.

  smalllog = ''.
  nodata = '/'.
ENDFORM.                    " SET_BDC_PARA
*&---------------------------------------------------------------------*
*&      Form  CHECK_SELECTION
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM check_selection .
  IF    cb_upld IS NOT INITIAL.
    IF p_path IS INITIAL.
      MESSAGE s000(yse_general) WITH 'Please enter the file path'(081)
       DISPLAY LIKE 'E'.
      LEAVE LIST-PROCESSING.
    ENDIF.
*   Check if the file path and name exist
    PERFORM check_filename.
  ENDIF.
* Applicatin Type Check
  PERFORM check_appl.

ENDFORM.                    " CHECK_SELECTION
*&---------------------------------------------------------------------*
*&      Form  SELECT_FILE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM select_file .
  DATA:
    lv_title  TYPE string,     "Referrence title
    lt_file   TYPE filetable,  "Internal table of dialog filenames
    ls_file   TYPE file_table, "Working area for IT_FILE,
    lv_rc     TYPE i.          "Return Code

  lv_title = 'Select a file for upload'(001).
  CLEAR: lt_file[],
         ls_file.

  CALL METHOD cl_gui_frontend_services=>file_open_dialog
    EXPORTING
      window_title            = lv_title
    CHANGING
      file_table              = lt_file
      rc                      = lv_rc
    EXCEPTIONS
      file_open_dialog_failed = 1
      cntl_error              = 2
      error_no_gui            = 3
      not_supported_by_gui    = 4
      OTHERS                  = 5.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
  IF lv_rc = 1.
    READ TABLE lt_file INDEX 1 INTO ls_file.
    IF sy-subrc EQ 0.
      p_path = ls_file-filename.
    ENDIF.
  ENDIF.
ENDFORM.                    " SELECT_FILE
*&---------------------------------------------------------------------*
*&      Form  CHECK_FILENAME
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM check_filename .
  DATA:
    lv_exist TYPE c.
  CALL FUNCTION 'TMP_GUI_GET_FILE_EXIST'
    EXPORTING
      fname          = p_path
    IMPORTING
      exist          = lv_exist
    EXCEPTIONS
      fileinfo_error = 1
      OTHERS         = 2.
  IF sy-subrc <> 0 OR lv_exist IS INITIAL.
    MESSAGE s000(yse_general)
      WITH 'Please enter an invaild filename'(010)
    DISPLAY LIKE 'E'.
  ENDIF.
ENDFORM.                    " CHECK_FILENAME
*&---------------------------------------------------------------------*
*&      Form  READ_FILE
*&---------------------------------------------------------------------*
*       Read local file into a internal table
*----------------------------------------------------------------------*
FORM read_file .
  DATA: lv_filename TYPE string.

  lv_filename = p_path.
  PERFORM get_from_pres IN PROGRAM yam_common_routines
                                    TABLES  gt_lfile
                                    USING   lv_filename
                                            ycl_se_utility=>c_ft_asc
                                            ' '.
  IF gt_lfile IS NOT INITIAL.
    DELETE gt_lfile INDEX 1.
    DELETE gt_lfile
      WHERE text IS INITIAL.
  ENDIF.
ENDFORM.                    " READ_FILE
*&---------------------------------------------------------------------*
*&      Form  CHECK_APPL
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM check_appl .
  DATA: lt_up_appl TYPE TABLE OF yse_up_appl,
        ls_up_appl TYPE yse_up_appl.

  SELECT * FROM yse_up_appl INTO TABLE lt_up_appl
    WHERE appl = p_appl.                              "#EC CI_SGLSELECT

  IF sy-subrc <> 0 .
    IF p_appl = '02'.
      ls_up_appl-appl  = '02'.
      ls_up_appl-description = 'Upload PIR and SL for Nanjing CC'.
      INSERT yse_up_appl FROM ls_up_appl.
    ELSE.
      MESSAGE s000(yse_general)
        WITH 'Applicaiton'(014) p_appl
             ' does not exist!'(005)
      DISPLAY LIKE 'E'.
      LEAVE LIST-PROCESSING.
    ENDIF.
  ENDIF.
ENDFORM.                    " CHECK_APPL
*&---------------------------------------------------------------------*
*&      Form  SET_SCREEN
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM set_screen .
  LOOP AT SCREEN.
    IF    screen-name CS 'P_PATH'
      AND  cb_upld IS  INITIAL .
      screen-input = '0'.
      screen-invisible = '1'.
      MODIFY SCREEN.
    ENDIF.
    IF    cb_updt IS  INITIAL
      AND screen-group1 = 'M1'.
      screen-input = '0'.
      screen-invisible = '1'.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.
ENDFORM.                    " SET_SCREEN
*&---------------------------------------------------------------------*
*&      Form  WRITE_DB
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM write_db .
  DATA: lv_xml TYPE string,
        ls_up_file TYPE yse_up_file.

  IF gt_lfile IS INITIAL.
    MESSAGE w000(yse_general) WITH 'No Input Data'(002).
  ELSE.
    CALL TRANSFORMATION yse_trans_file SOURCE lfile = gt_lfile RESULT XML lv_xml.
  ENDIF.

  CLEAR: gt_lfile[].
  CALL TRANSFORMATION yse_trans_file SOURCE XML lv_xml RESULT lfile = gt_lfile.
  CALL FUNCTION 'SYSTEM_UUID_CREATE'
    IMPORTING
      uuid = ls_up_file-guid.
* Prepare entry for insert
  ls_up_file-appl = p_appl.
  ls_up_file-create_date = sy-datum.
  ls_up_file-create_time = sy-uzeit.
  ls_up_file-create_by   = sy-uname.
  ls_up_file-xml = lv_xml.
  INSERT yse_up_file FROM ls_up_file.

  IF sy-subrc = 0.
    MESSAGE s000(yse_general) WITH 'File uploaded successfully'(003)
                                     'for applicaiton'(080)
                                     p_appl.
  ENDIF.
ENDFORM.                    " WRITE_DB
*&---------------------------------------------------------------------*
*&      Form  MASS_UPDATE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM mass_update .
  DATA: lv_number     TYPE tbtcjob-jobcount,
        lv_text       TYPE char100,
        lv_jobname    TYPE btcjob.

  CONCATENATE p_jname '-' sy-datlo INTO lv_jobname.

  IF cb_back IS NOT INITIAL.
    CALL FUNCTION 'JOB_OPEN'
      EXPORTING
        jobname          = lv_jobname
      IMPORTING
        jobcount         = lv_number
      EXCEPTIONS
        cant_create_job  = 1
        invalid_job_data = 2
        jobname_missing  = 3
        OTHERS           = 4.
    IF sy-subrc <> 0.
      MESSAGE ID  sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.
    lv_text = 'Batch job & has been started...'(020).
    REPLACE '&' WITH lv_jobname INTO lv_text.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S'
      NUMBER '000' WITH lv_text(20)
                        lv_text+21(20)
                        lv_text+41(20)
                        lv_text+61(20).

    SUBMIT yse_update_pir_sl WITH  p_appl = p_appl
                             WITH  p_pcode = gv_process_code
               VIA JOB  lv_jobname NUMBER lv_number
               AND RETURN.

    IF sy-subrc = 0.
      CALL FUNCTION 'JOB_CLOSE'
        EXPORTING
          jobcount             = lv_number
          jobname              = lv_jobname
          strtimmed            = gc_x
        EXCEPTIONS
          cant_start_immediate = 1
          invalid_startdate    = 2
          jobname_missing      = 3
          job_close_failed     = 4
          job_nosteps          = 5
          job_notex            = 6
          lock_failed          = 7
          invalid_target       = 8
          OTHERS               = 9.
      IF sy-subrc <> 0.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      ENDIF.
    ENDIF.
  ELSE.
    SUBMIT yse_update_pir_sl WITH  p_appl = p_appl
                             WITH  p_pcode = gv_process_code.
  ENDIF.
ENDFORM.                    " MASS_UPDATE
*&---------------------------------------------------------------------*
*&      Form  SET_PROC_CODE
*&---------------------------------------------------------------------*
*       Set update process code
*----------------------------------------------------------------------*
FORM set_proc_code .
  IF p_pir_up = gc_x.
    gv_process_code = '1'.
  ENDIF.

  IF p_sl_del = gc_x.
    gv_process_code = '2'.
  ENDIF.

  IF p_sl_cre = gc_x.
    gv_process_code = '3'.
  ENDIF.
ENDFORM.                    " SET_PROC_CODE

*Text symbol text£º
*001:Select a file for upload
*002:No Input Data
*003:File uploaded successfully
*005: does not exist!
*010:Please enter an invaild filename
*014:Applicaiton
*020:Batch job & has been started...
*080:for applicaiton
*081:Please enter the file path
*BK1:Upload Selection
*E00:Error opening dataset, return code:
*I01:Create group
*I02:BDC_OPEN_GROUP
*I03:BDC_INSERT
*I04:BDC_CLOSE_GROUP
*I05:returncode:
*I06:Fehlermappe wurde erzeugt
*T01:Functions

*T02:Selection Criterial
*Selection text£º
*CB_BACK:        Process in background
*CB_UPDT:        Update
*CB_UPLD:        Upload
*P_PATH:        Local file path
*P_PIR_UP:        PIR Update
*P_SL_CRE:        Source List Creation
*P_SL_DEL:        Source List Deletion
