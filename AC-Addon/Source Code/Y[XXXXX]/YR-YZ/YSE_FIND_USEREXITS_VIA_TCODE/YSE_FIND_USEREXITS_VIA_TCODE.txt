************************************************************************
* Report  : YSE_FIND_USEREXITS_VIA_TCODE                               *
*----------------------------------------------------------------------*
* Author  : Kurt Clement                                               *
* Company : Atlas Copco                                                *
* Created : 21.11.2006                                                 *
*----------------------------------------------------------------------*
* Search which standard SAP userexits exists starting from a choosen   *
* transaction.                                                         *
************************************************************************
report yse_find_userexits_via_tcode no standard page heading.

*----------------------------------------------------------------------*
* Data declarations                                                    *
*----------------------------------------------------------------------*

*--- Dictionary tables
tables: enlfdir.   "Additional Attributes for Function Modules
tables: modact.    "Modifications
tables: modsapt.   "SAP Enhancements - Short Texts
tables: tadir.     "Directory of Repository Objects
tables: tfdir.     "Function Module
tables: trdir.     "System table TRDIR
tables: tstc.      "SAP Transaction Codes
tables: tstct.     "Transaction Code Texts

*--- Internal tables
data: it_jtab like tadir occurs 0 with header line.

*--- Variables
data: x_field1(30) type c.
data: x_devclass   like tadir-devclass.


*----------------------------------------------------------------------*
* Selection screen                                                     *
*----------------------------------------------------------------------*
selection-screen begin of block b01 with frame title text-001.
selection-screen skip.
parameters: p_tcode like sy-tcode obligatory.
selection-screen skip.
selection-screen end of block b01.



************************************************************************
* START MAIN PROGRAM                                                   *
************************************************************************
start-of-selection.

*--- Check if transaction code is valid
  select single * from tstc where tcode eq p_tcode.

  if sy-subrc eq 0.
*--- Find repository objects for transaction code
    clear: tadir.
    select single * from tadir where pgmid    = 'R3TR'
                                 and object   = 'PROG'
                                 and obj_name = tstc-pgmna.
    move: tadir-devclass to x_devclass.
    if sy-subrc ne 0.
      clear: trdir.
      select single * from trdir where name = tstc-pgmna.
      if trdir-subc eq 'F'.
        clear: tfdir.
        select single * from tfdir where pname = tstc-pgmna.
        clear: enlfdir.
        select single * from enlfdir where funcname = tfdir-funcname.
        clear: tadir.
        select single * from tadir where pgmid    = 'R3TR'
                                     and object   = 'FUGR'
                                     and obj_name = enlfdir-area.
        move: tadir-devclass to x_devclass.
      endif.
    endif.

* Find SAP modifactions
    clear: tadir.
    clear: it_jtab.
    refresh: it_jtab.
    select * from tadir into table it_jtab
                        where pgmid    = 'R3TR'
                          and object   = 'SMOD'
                          and devclass = x_devclass.
    clear: tstct.
    select single * from tstct where sprsl eq sy-langu
                                 and tcode eq p_tcode.

    format color col_positive intensified off.
    write: /(19)   'Transaction Code - ',
            20(20) p_tcode,
            45(50) tstct-ttext.
    skip.
    if not it_jtab[] is initial.
      write: /(95) sy-uline.
      format color col_heading intensified on.
      write: /01 sy-vline,
              02 'Exit Name',
              21 sy-vline ,
              22 'Description',
              95 sy-vline.
      write:/(95) sy-uline.
      loop at it_jtab.
        clear: modsapt.
        select single * from modsapt where sprsl = sy-langu
                                       and name  = it_jtab-obj_name.
        format color col_normal intensified off.
        write: /01 sy-vline,
                02 it_jtab-obj_name hotspot on,
                21 sy-vline,
                22 modsapt-modtext,
                95 sy-vline.
      endloop.
      write: /(95) sy-uline.
      describe table it_jtab.
      skip.
      format color col_total intensified on.
      write: / 'No of Exits:',
               sy-tfill.
    else.
      format color col_negative intensified on.
      write: /(95) 'No userexits found'.
    endif.
  else.
    format color col_negative intensified on.
    write: /(95) 'Transaction code does not exist'.
  endif.

* Take the user to SMOD for the Exit that was selected.
at line-selection.
  get cursor field x_field1.
  check x_field1(4) eq 'it_jtab'.
  set parameter id 'MON' field sy-lisel+1(10).
  call transaction 'SMOD' and skip first screen.

*Text symbol text£º

*001:Enter the transaction code you want to search through for userexits
*Selection text£º
*P_TCODE:D       Transaction code
