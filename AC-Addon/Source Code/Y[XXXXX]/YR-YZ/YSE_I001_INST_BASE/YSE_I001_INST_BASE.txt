*----------------------------------------------------------------------*
* PROGRAM ID           : YSE_I001_INST_BASE                            *
* PROGRAM TITLE        : SEED: I001 Installed Base CTP                 *
* AUTHOR               : Marc Jacobs                                   *
* DATE                 : 12/10/2007                                    *
* DEVELOPMENT ID       : I001                                          *
*                                                                      *
* CHANGE REQUEST NUMBER: CD1K921964                                    *
*                                                                      *
* Program Description:  Outbound program to send Installed Base to CTP *
*                       for Seed-countries                             *
*                       copy of YAM_I001_INST_BASE                     *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |27.02.2008|M.Jacobs  |CD1K926724       |extra fields in idoc*
*----------------------------------------------------------------------*
* MOD-002 |10.09.2008|CVM       |CD1K926724       |ship-to in the  idoc*
*----------------------------------------------------------------------*
* MOD-003 |20.10.2008|CVM       |CD1K944125       |email adress added  *
*                        and aquisition date changed into start-up date*
*----------------------------------------------------------------------*
* MOD-004 |29.01.2009|J. Smets  |CD1K946002       |selection on ship-to*
*                                               and always fill bill-to*
*----------------------------------------------------------------------*
* MOD-005 |17.07.2013|J. Smets  |CD1K976956       | Performance        *
************************************************************************
REPORT yse_i001_inst_base MESSAGE-ID yam_inf.

************************************************************************
*                   T A B L E S                                        *
************************************************************************
TABLES: yam_i001,          " Custom table Installed Base CTP
        v_equi.            " PM technical objects (EQUI, func.location)

************************************************************************
*                   C O N S T A N T S                                  *
************************************************************************
CONSTANTS :c_mestyp     LIKE edidc-mestyp VALUE 'YSE_OUT_I001' ,
           c_idoc_type  LIKE edidc-idoctp VALUE 'YSE_OUT_I001' ,
           c_segment(14) TYPE c    VALUE 'YSE_OUT_E1I001',
           c_equipment  TYPE eqart VALUE 'EQUIPMENT',
           c_99991231(8)           VALUE '99991231',
           c_yse_i001(8) TYPE c    VALUE 'YSE_I001',
           c_bp         TYPE parvw VALUE 'RE',  "Bill-to party
           c_ag         TYPE parvw VALUE 'AG',  "Sold-to party "mod-004
           c_rg         TYPE parvw VALUE 'RG',  "Payer party   "mod-004
           c_k          TYPE objvw VALUE 'K',   "Maintenance contract
           c_c(1)       TYPE c     VALUE 'C',   "Change
           c_a(1)       TYPE c     VALUE 'A',   "Add
           c_x(1)       TYPE c     VALUE 'X',
           c_run_hours  TYPE impt-atnam VALUE 'ZAM_RHRSTOTAL_ACT',
           c_underscore(1) TYPE c  VALUE '_',   "Underscore
           c_ls(2)                 VALUE 'LS',
           c_sc1(3)                VALUE 'SC1',
           c_input                 VALUE '0',
           c_0000(4)    TYPE c     VALUE '0000',
           c_001(3)     TYPE c     VALUE '001',
           c_e          TYPE spras VALUE 'E',   "Language
           c_clnt(4)    TYPE c     VALUE 'CLNT' ,
           c_wbi(3)     TYPE c     VALUE 'WBI'   ,
           c_bp_we      TYPE parvw VALUE 'WE'.  "Ship-to party
************************************************************************
*                   V A R I A B L E S                                  *
************************************************************************
DATA:  g_equnr  TYPE equi-equnr,                " Equip Number
       g_pfile  LIKE      /sapdmc/lsoinp-filename,
       g_ofile  LIKE      /sapdmc/lsoinp-filename,
       g_delsetupfile LIKE rlgrap-filename,
       g_swerk  TYPE v_equi-swerk,              " Plant
       g_ansdt  LIKE equi-ansdt,                " Acquisition date
       g_retcd  LIKE sy-subrc,                  " return code
       g_subrc  LIKE sy-subrc,                  " return code
       g_index  LIKE sy-tabix,
       g_stat   LIKE jest-stat,                 " System status
       g_directory(25) TYPE c VALUE '/var/load/xxx/UK/convert/',
       g_fm_atinn TYPE ausp-atinn,
       g_point  LIKE imptt-point,               " Measurement point
       g_ersda(12) TYPE c,                      " YYYYMMDDHHMM
       g_date   LIKE sy-datum,
       g_ktokd  LIKE kna1-ktokd,                            "mod-004
       g_parnr  TYPE i_parnr,                   " Partner Number
       shipto_parnr  TYPE i_parnr,              " Ship-to Partner Number
* begin of insertion MOD-001
       g_lief_nr LIKE ser01-lief_nr,
       g_posnr   LIKE ser01-posnr,
       g_netwr   LIKE vbap-netwr,
       g_waerk   LIKE vbap-waerk,
       g_vgbel   LIKE lips-vgbel,
       g_vgpos   LIKE lips-vgpos,
       g_obknr   LIKE objk-obknr,
       g_datlwb  LIKE equi-datlwb.
* end of insertion MOD-001
************************************************************************
*                  I N T E R N A L   T A B L E S                       *
************************************************************************
DATA : BEGIN OF i_prev OCCURS 0.
        INCLUDE STRUCTURE yse_out_e1i001.
DATA : END OF i_prev.

DATA : BEGIN OF i_equi OCCURS 0.
        INCLUDE STRUCTURE yse_out_e1i001.
DATA :    objnr LIKE v_equi-objnr,         " Object Number
          adrnr LIKE v_equi-adrnr,         " Address Number
          gewrk LIKE v_equi-gewrk,         " Work center ID
          pm_objty LIKE v_equi-pm_objty.   " Work center object type
DATA : END OF i_equi.

DATA : BEGIN OF i_delfiles OCCURS 0,
         mandt LIKE yam_i001-mandt,
         swerk LIKE yam_i001-swerk,
         ersda LIKE yam_i001-ersda,
       END OF i_delfiles.

DATA : BEGIN OF i_equi_delta OCCURS 0.
        INCLUDE STRUCTURE yse_out_e1i001.
DATA : END OF i_equi_delta.

*** mod-004 * begin ***
DATA: BEGIN OF i_par  OCCURS 0,
        parvw  LIKE ihpa-parvw,
        parnr  LIKE ihpa-parnr,
      END OF i_par.
*** mod-004 * end ***

DATA : BEGIN OF wa_yam_i001,
         mandt LIKE yam_i001-mandt,
         swerk LIKE yam_i001-swerk,
         ersda LIKE yam_i001-ersda,
       END OF wa_yam_i001.

*** mod-005 * begin ***
TYPES: BEGIN OF ty_crtx,
         objty     TYPE cr_objty,
         objid     TYPE cr_objid,
         ktext     TYPE cr_ktext,
       END OF ty_crtx.
DATA: gt_crtx   TYPE HASHED TABLE OF ty_crtx
                     WITH UNIQUE KEY objty objid
                     WITH HEADER LINE.
*** mod-005 * end ***

DATA: i_edidc_control_comm LIKE edidc OCCURS 1 WITH HEADER LINE ,
      i_edidd_data LIKE edidd OCCURS 0 WITH HEADER LINE         ,
      wa_edidc LIKE edidc                                       ,
      i_wa_point LIKE impt,
      i_wa_value LIKE imrg,
      wa_equi_delta LIKE i_equi_delta,
      p_logsys LIKE tbdlst-logsys                               .


************************************************************************
*       S E L E C T - O P T I O N S / P A R A M E T E R S              *
************************************************************************

SELECTION-SCREEN : BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.

SELECT-OPTIONS: s_equnr FOR g_equnr,                         "Equip.Nr
                s_swerk FOR g_swerk OBLIGATORY NO INTERVALS. "Plant
PARAMETERS: p_inwrk  LIKE g_swerk,    "Plant to be initial loaded
            p_mestyp LIKE edmsg-msgtyp DEFAULT c_mestyp MODIF ID sc1.

SELECTION-SCREEN: END OF BLOCK b1.

************************************************************************
INITIALIZATION.

  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = p_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
* ELSE.
*   REPLACE FIRST OCCURRENCE OF c_clnt IN p_logsys WITH c_wbi  .
  ENDIF.

************************************************************************
*    Disable Message Type Screen Field                                 *
************************************************************************
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-group1 = c_sc1.
      screen-input   = c_input.
      MODIFY SCREEN.
      CONTINUE.
    ENDIF.
  ENDLOOP.

************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

** Validate if the ALE Configuration is correctly setup
* PERFORM validate_ale_configuration .

** Get the information for selected plant(s)
  LOOP AT s_swerk.

    PERFORM get_detail USING s_swerk-low.

*   Create delta with new creations/updates by comparing the selected
*   data with this from the previous run. When a new plant
*   has to be loaded, no comparison has to take place.
    IF NOT i_equi[] IS INITIAL.

      IF s_swerk-low NE p_inwrk.

        SORT i_equi BY swerk equnr.

        CLEAR: g_retcd.
        PERFORM get_previous_file USING s_swerk-low
                               CHANGING g_retcd.

        IF g_retcd = 0.
          PERFORM create_delta.
        ELSE.
          CONTINUE.                      " Pass to next plant
        ENDIF.
      ELSE.
        LOOP AT i_equi.
          MOVE-CORRESPONDING i_equi TO i_equi_delta.
          MOVE c_a TO i_equi_delta-msgfn.
          APPEND i_equi_delta.
          CLEAR i_equi_delta.
        ENDLOOP.
      ENDIF.

*     write outputfile to application server
      CLEAR g_retcd.

      CONCATENATE sy-datum sy-uzeit(4) INTO g_ersda.
      CONCATENATE c_yse_i001 s_swerk-low g_ersda
                INTO g_ofile SEPARATED BY c_underscore.
      REPLACE 'xxx' IN g_directory WITH p_logsys(3).
      CONCATENATE g_directory g_ofile INTO g_ofile.

      PERFORM write_outputfile.

      IF g_retcd IS INITIAL.
*       update custom table YAM_I001
        PERFORM update_custom_table USING s_swerk-low.

*       Delete older entries in custom table YAM_I001
        PERFORM delete_old_table_entries USING s_swerk-low.

*       Delete older files on appl.server
        IF NOT i_delfiles[] IS INITIAL.
          PERFORM delete_old_files.
        ENDIF.

      ENDIF.

    ENDIF.

  ENDLOOP.


************************************************************************
*       E N D - O F - S E L E C T I O N    E V E N T                   *
************************************************************************
END-OF-SELECTION .

  IF i_equi_delta[] IS INITIAL .
    WRITE: / 'No Idocs created'(i03).
  ELSE.

** Generate Idoc's
    SORT i_equi_delta BY equnr.
    PERFORM create_idocs.

  ENDIF.

  WRITE: / 'Job finished'(i02).


************************************************************************
*--- S U B R O U T I N E S --------------------------------------------*
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  Validate_ALE_Configuration
*&---------------------------------------------------------------------*
FORM validate_ale_configuration .

  DATA: l_create_idoc     TYPE     c .

* CHECK IF IDOC CONFIGURATION IS READY AND IDOC CAN BE PROCESSED.
  CALL FUNCTION 'ALE_MODEL_DETERMINE_IF_TO_SEND'
    EXPORTING
      message_type           = p_mestyp
    IMPORTING
      idoc_must_be_sent      = l_create_idoc
    EXCEPTIONS
      own_system_not_defined = 1
      OTHERS                 = 2.

  IF sy-subrc <> 0.
    MESSAGE e029 WITH p_mestyp.
    EXIT.
  ENDIF.

ENDFORM.                    " Validate_ALE_Configuration

*&---------------------------------------------------------------------*
*&      Form  Get_Detail
*&---------------------------------------------------------------------*
*       To get the information for selected plant(s)
*----------------------------------------------------------------------*
FORM get_detail USING p_swerk.

  CLEAR i_equi.
  REFRESH i_equi.

** Select data based on Selection screen parameters
  SELECT equnr sernr matnr eqktx swerk objnr adrnr gewrk pm_objty
         vkorg spart vtweg
           INTO CORRESPONDING FIELDS OF TABLE i_equi
           FROM v_equi
           WHERE  equnr IN s_equnr
             AND  eqart = c_equipment
             AND  datbi = c_99991231
             AND  swerk = p_swerk
             AND  spras = sy-langu.                         "mod-004

** Process Error - No data found for the Selected Plant
  IF sy-subrc NE 0 .
    WRITE: / text-e01, p_swerk.
    EXIT.
  ENDIF .

*** mod-005 * begin ***
* Main work center description
  SELECT objty objid ktext INTO TABLE gt_crtx
         FROM crtx
         FOR ALL ENTRIES IN i_equi
         WHERE objty = i_equi-pm_objty
           AND objid = i_equi-gewrk
           AND spras = c_e.
*** mod-005 * end ***

** Get related info and populate data into Final Internal Table
  LOOP AT i_equi.

*   g_index = sy-tabix.

**  get acquisition date
**  this was not selected from v_equi because this field has
**  a different type than this from i_equi !!!
    CLEAR: g_ansdt,
           g_datlwb.

*** mod-004 * begin ***
*   Get partners
    REFRESH i_par.
    SELECT parvw parnr
         INTO  TABLE i_par
         FROM ihpa
         WHERE objnr = i_equi-objnr
           AND kzloesch <> 'X'.
    SORT i_par.
*** mod-004 * end ***

*CVM.sn
*    SELECT SINGLE ansdt datlwb INTO (g_ansdt, g_datlwb)
    SELECT SINGLE inbdt datlwb INTO (g_ansdt, g_datlwb)
*CVM.en
         FROM equi
         WHERE equnr = i_equi-equnr.
    MOVE g_ansdt TO i_equi-ansdt.
    IF NOT g_datlwb IS INITIAL.
      MOVE g_datlwb TO i_equi-datlwb.
    ENDIF.

**  get main work center description
*** mod-005 * begin ***
*    SELECT SINGLE ktext INTO i_equi-ktext
*         FROM crtx
*         WHERE objid = i_equi-gewrk
*           AND objty = i_equi-pm_objty
*           AND spras = c_e.
    READ TABLE gt_crtx
               WITH TABLE KEY objty = i_equi-pm_objty
                              objid = i_equi-gewrk.
    IF sy-subrc = 0.
      i_equi-ktext = gt_crtx-ktext.
    ENDIF.
*** mod-005 * end ***

**  get BPCS customer number and address of Bill-to-party
    CLEAR g_parnr.
*** mod-004 * begin ***
    CLEAR g_ktokd.
*    SELECT SINGLE parnr INTO g_parnr
*         FROM ihpa
*         WHERE objnr = i_equi-objnr
*           AND kzloesch <> 'X'
*           AND parvw = c_bp.
    READ TABLE i_par WITH KEY parvw = c_bp.
    g_parnr = i_par-parnr.

*   if no bill-to, get sold-to
    IF sy-subrc NE 0.
      READ TABLE i_par WITH KEY parvw = c_ag.
      g_parnr = i_par-parnr.
*     if no sold-to, get payer
      IF sy-subrc NE 0.
        READ TABLE i_par WITH KEY parvw = c_rg.
        g_parnr = i_par-parnr.
*       if no payer, get ship-to
        IF sy-subrc NE 0.
          READ TABLE i_par WITH KEY parvw = c_bp_we.
          g_parnr = i_par-parnr.
**         Customer account group for ship-to
*          IF sy-subrc = 0.
*            SELECT SINGLE ktokd
*                 INTO g_ktokd
*                 FROM kna1
*                 WHERE kunnr = g_parnr.
*          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.
*** mod-004 * end ***

    IF sy-subrc = 0.         "AND
*       g_ktokd NE 'Z002'.                                   "mod-004
      SELECT SINGLE sortl name1 stras ort01 pstlz regio land1
                    kunnr
           INTO CORRESPONDING FIELDS OF i_equi
           FROM kna1
           WHERE kunnr = g_parnr.
    ENDIF.

*** mod-004 * begin ***
***  get Ship-to number
*    SELECT SINGLE parnr
*         INTO  i_equi-kunwe
*         FROM ihpa
*         WHERE objnr = i_equi-objnr
*           AND kzloesch <> 'X'
*           AND parvw = c_bp_we.
    READ TABLE i_par WITH KEY parvw = c_bp_we.
    i_equi-kunwe = i_par-parnr.
*** mod-004 * end ***

**  get address of functional location
    SELECT name1 street city1 post_code1 region country tel_number name2
        INTO (i_equi-fl_name1, i_equi-street, i_equi-city1,
              i_equi-post_code1, i_equi-region, i_equi-country,
              i_equi-tel_number, i_equi-name2)
        FROM adrc
        WHERE addrnumber = i_equi-adrnr.
    ENDSELECT.

**  get product hierarchy
    SELECT SINGLE prdha INTO i_equi-prdha
         FROM mara
         WHERE matnr = i_equi-matnr.

**  get service contract number
    SELECT sdaufnr INTO i_equi-vbeln
         FROM viser02
         WHERE equnr EQ i_equi-equnr
           AND objvw EQ c_k
           AND datum LE sy-datum.
    ENDSELECT.

*CVM.sn
    SELECT SINGLE smtp_addr
    INTO  i_equi-ad_smtpadr
    FROM adr6
    WHERE addrnumber EQ   i_equi-adrnr.
*CVM.en

**  get running hours
*   convert ZAM_RHRSTOTAL into internal value
    CLEAR g_fm_atinn .
    CALL FUNCTION 'CONVERSION_EXIT_ATINN_INPUT'
      EXPORTING
        input  = c_run_hours
      IMPORTING
        output = g_fm_atinn.

*   get measurement point
    IF sy-subrc = 0.
      CLEAR g_point.
      SELECT point FROM imptt INTO g_point
         WHERE mpobj EQ i_equi-objnr
           AND atinn EQ g_fm_atinn.
      ENDSELECT.

      IF sy-subrc = 0.
*       read last counter
        CALL FUNCTION 'MEASUREM_POINT_LAST_VALUE'
          EXPORTING
            i_point           = g_point
          IMPORTING
            e_wa_point        = i_wa_point
            e_wa_value        = i_wa_value
          EXCEPTIONS
            pointer_not_found = 01.
*
        IF sy-subrc <> 0.
          WRITE:/ text-e04, g_point.    "No value found for measr.point
        ELSE.
*         convert value into display format
          PERFORM unit_conversion USING i_wa_point-msehi
                                        i_wa_value-readg
                                        i_equi-run_hours
                                        i_wa_point-decim
                                        i_wa_point-expon.
        ENDIF.
      ENDIF.
    ENDIF.

**  check deletion flag
    SELECT SINGLE stat INTO g_stat
       FROM jest
       WHERE objnr = i_equi-objnr
         AND stat EQ 'I0076'
         AND inact NE 'X' .
    IF sy-subrc EQ 0 .
      i_equi-delet = c_x.
    ENDIF.

*   MODIFY i_equi transporting ktext fl_name1 street city1
*            post_code1 region country tel_number name2
*            prdha vbeln run_hours delet.

* begin of insertion MOD-001
* get the sales order information
    CLEAR:g_netwr,
          g_waerk,
          g_vgbel,
          g_vgpos,
          g_lief_nr,
          g_posnr,
          g_obknr.
    SELECT obknr INTO g_obknr FROM objk
       WHERE equnr = i_equi-equnr
         AND taser = 'SER01'.
      SELECT lief_nr posnr INTO (g_lief_nr, g_posnr)
          FROM ser01 WHERE obknr = g_obknr.
        SELECT vgbel vgpos INTO (g_vgbel, g_vgpos)
            FROM lips
            WHERE vbeln = g_lief_nr
              AND posnr = g_posnr.
          SELECT netwr waerk INTO (g_netwr, g_waerk)
              FROM vbap
              WHERE vbeln = g_vgbel
                AND posnr = g_vgpos.
            IF sy-subrc = 0.
              IF g_netwr IS INITIAL.
                MOVE g_waerk TO i_equi-waerk.
                MOVE g_vgbel TO i_equi-vbeln.
                MOVE g_netwr TO i_equi-netwr.
              ENDIF.
            ENDIF.
          ENDSELECT.
        ENDSELECT.
      ENDSELECT.
    ENDSELECT.
* end of insertion MOD-001
    MODIFY i_equi.

  ENDLOOP.

ENDFORM.                    " Get_Detail

*---------------------------------------------------------------------
*  FORM UNIT_CONVERSION
*---------------------------------------------------------------------
FORM unit_conversion USING p_einheit
                           p_fltp_wert
                           p_char_wert
                           p_decimal
                           p_exponent.

  CLEAR p_char_wert.
  CHECK NOT ( p_fltp_wert IS INITIAL ).

  CALL FUNCTION 'FLTP_CHAR_CONVERSION_FROM_SI'
    EXPORTING
      char_unit       = p_einheit
      decimals        = p_decimal
      exponent        = p_exponent
      fltp_value_si   = p_fltp_wert
      indicator_value = c_x
      masc_symbol     = ' '
    IMPORTING
      char_value      = p_char_wert.

ENDFORM.                    "UNIT_CONVERSION

*&---------------------------------------------------------------------*
*&      Form  Create_delta
*&---------------------------------------------------------------------*
*       Create internal table with creations/updates
*----------------------------------------------------------------------*
FORM create_delta.

  LOOP AT i_equi.

    READ TABLE i_prev WITH KEY equnr = i_equi-equnr
                    BINARY SEARCH.

    IF sy-subrc = 0.
***** MUST BE UPDATED when structure of segment YAM_E1I001 is extended *
* begin of change MOD-001
*      IF i_equi(555) <> i_prev(555).
      IF i_equi(616) <> i_prev(616).
* end of change MOD-001
************************************************************************
        MOVE-CORRESPONDING i_equi TO i_equi_delta.
        MOVE c_c TO i_equi_delta-msgfn.
        APPEND i_equi_delta.
        CLEAR i_equi_delta.
      ENDIF.
    ELSE.
      MOVE-CORRESPONDING i_equi TO i_equi_delta.
      MOVE c_a TO i_equi_delta-msgfn.
      APPEND i_equi_delta.
      CLEAR i_equi_delta.
    ENDIF.

  ENDLOOP.

ENDFORM.                    " Create_delta

*&---------------------------------------------------------------------*
*&      Form  Get_previous_file
*&---------------------------------------------------------------------*
*       Get file from previous run
*----------------------------------------------------------------------*
FORM get_previous_file USING p_swerk
                    CHANGING p_retcd.

* prepare filename of previous run
  CLEAR g_ersda.
  SELECT ersda INTO g_ersda
      FROM yam_i001 WHERE swerk = p_swerk.
  ENDSELECT.

  IF sy-subrc <> 0.
    WRITE: / text-e02, p_swerk.       "No filename of previous run
*                                   available in custom table YAM_I001
    p_retcd = 4.
    EXIT.
  ENDIF.

  CONCATENATE c_yse_i001 p_swerk g_ersda
                      INTO g_pfile SEPARATED BY c_underscore.
  REPLACE 'xxx' IN g_directory WITH p_logsys(3).
  CONCATENATE g_directory g_pfile INTO g_pfile.

* FILE READ FROM APPLICATION SERVER
  PERFORM get_from_appl TABLES  i_prev
                        USING   g_pfile
                                p_retcd.

ENDFORM.                    " Get_previous_file

*&---------------------------------------------------------------------*
*&      Form  Get_from_appl
*&---------------------------------------------------------------------*
*       Get the file from application server into internal table
*----------------------------------------------------------------------*
FORM get_from_appl TABLES i_infile STRUCTURE i_prev
                   USING p_infile p_subrc.

  OPEN DATASET p_infile FOR INPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, p_infile.
    p_subrc = 4.
    EXIT.
  ENDIF.

  REFRESH i_infile.

  DO.
    READ DATASET p_infile INTO i_infile.
    IF sy-subrc <> 0.
      EXIT.
    ENDIF.
    APPEND i_infile.
  ENDDO.
  CLOSE DATASET p_infile.

ENDFORM.                    " GET_FROM_APPL

*&---------------------------------------------------------------------*
*&      Form  Write_outputfile
*&---------------------------------------------------------------------*
*       Write outputfile
*----------------------------------------------------------------------*
FORM write_outputfile.

  OPEN DATASET g_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, g_ofile.
*   message e011(YAM_DC).                      "Open dataset failed
    g_retcd = 4.
    EXIT.
  ENDIF.

  SORT i_equi BY equnr.

  LOOP AT i_equi.
***** MUST BE UPDATED when structure of segment YAM_E1I001 is extended
    TRANSFER i_equi(616) TO g_ofile.
************************************************************************
  ENDLOOP.

ENDFORM.                    " Write_outputfile

*&---------------------------------------------------------------------*
*&      Form  Update_custom_table
*&---------------------------------------------------------------------*
*       Update custom table YAM_I001
*----------------------------------------------------------------------*
FORM update_custom_table USING p_swerk.

  wa_yam_i001-mandt = sy-mandt.
  wa_yam_i001-swerk = p_swerk.
  wa_yam_i001-ersda = g_ersda.

  INSERT yam_i001 FROM wa_yam_i001.

ENDFORM.                    " Update_custom_table

*&---------------------------------------------------------------------*
*&      Form  Create_IDocs
*&---------------------------------------------------------------------*
*       Create Idoc's
*----------------------------------------------------------------------*
FORM create_idocs .

  DATA: g_created_comm_idocs TYPE sy-tabix .

  CLEAR : i_edidc_control_comm ,
          wa_edidc             ,
          i_edidd_data         .

** Polulate Control Record
  wa_edidc-mestyp =  p_mestyp.
  wa_edidc-idoctp =  c_idoc_type.
  wa_edidc-rcvprt =  c_ls.

** Create Idoc's for every new Equipment Number
  LOOP AT i_equi_delta.

    wa_equi_delta = i_equi_delta.

    AT NEW equnr .
      CLEAR i_edidd_data[] .
      i_edidd_data-segnam  = c_segment  .
      i_edidd_data-sdata   = wa_equi_delta.
      APPEND i_edidd_data .
    ENDAT .

    AT NEW swerk.
* FIND RECEIVING PARTNER
      CLEAR wa_edidc-rcvprn.
      SELECT SINGLE rcvprn INTO wa_edidc-rcvprn
             FROM edp13
             WHERE mestyp = p_mestyp.
    ENDAT.

    AT END OF equnr .
** Generate Idoc's

      CALL FUNCTION 'MASTER_IDOC_DISTRIBUTE'
        EXPORTING
          master_idoc_control            = wa_edidc
        TABLES
          communication_idoc_control     = i_edidc_control_comm
          master_idoc_data               = i_edidd_data
        EXCEPTIONS
          error_in_idoc_control          = 1
          error_writing_idoc_status      = 2
          error_in_idoc_data             = 3
          sending_logical_system_unknown = 4
          OTHERS                         = 5.

      IF sy-subrc <> 0.
        MESSAGE e025 .
      ELSE.
        DESCRIBE TABLE i_edidc_control_comm LINES sy-tfill.
        ADD sy-tfill TO g_created_comm_idocs.

        READ TABLE i_edidc_control_comm INDEX 1.

        REFRESH i_edidc_control_comm.
      ENDIF.

      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.

      CALL FUNCTION 'EDI_DOCUMENT_DEQUEUE_LATER'
        EXPORTING
          docnum                 = i_edidc_control_comm-docnum
        EXCEPTIONS
          idoc_is_not_to_dequeue = 1
          OTHERS                 = 2.

    ENDAT .

  ENDLOOP .

  WRITE: / text-i01, g_created_comm_idocs .

ENDFORM.                    " Create_IDocs

*&---------------------------------------------------------------------*
*&      Form  Delete_old_files
*&---------------------------------------------------------------------*
*       Delete files from former runs on application server
*----------------------------------------------------------------------*
FORM delete_old_files.

  DATA: g_dir_name LIKE epsf-epsdirnam,
        g_dfile    LIKE epsf-epsfilnam.

  LOOP AT i_delfiles.

    CONCATENATE c_yse_i001 i_delfiles-swerk i_delfiles-ersda
                         INTO g_dfile SEPARATED BY c_underscore.

    MOVE g_directory TO g_dir_name.
    REPLACE 'xxx' IN g_dir_name WITH p_logsys(3).
    TRANSLATE g_dir_name(10) TO LOWER CASE.
    TRANSLATE g_dir_name+17(8) TO LOWER CASE.

* change MJ 20090602
*    CALL FUNCTION 'EPS_DELETE_FILE'
    CALL FUNCTION 'YSE_EPS_DELETE_FILE'
      EXPORTING
        file_name = g_dfile
        dir_name  = g_dir_name.

    IF sy-subrc <> 0.
      WRITE: / text-e05, g_dfile.      "Could not delete file
    ENDIF.

  ENDLOOP.

ENDFORM.                    "delete_old_files

*&---------------------------------------------------------------------*
*&      Form  Delete_old_table_entries
*&---------------------------------------------------------------------*
*       Delete entries from former runs in custom table YAM_I001
*----------------------------------------------------------------------*
FORM delete_old_table_entries USING p_swerk.

  g_date = sy-datum - 7.
  CONCATENATE g_date c_0000 INTO g_ersda.

  REFRESH i_delfiles.

  SELECT * FROM yam_i001
      WHERE swerk EQ p_swerk
        AND ersda LT g_ersda.
    MOVE yam_i001 TO i_delfiles.
    APPEND i_delfiles.
    CLEAR i_delfiles.
  ENDSELECT.

  IF sy-subrc = 0.
    DELETE FROM yam_i001 WHERE swerk EQ p_swerk
                           AND ersda LT g_ersda.

    IF sy-subrc <> 0.
      WRITE: / text-e06, p_swerk.        "Could not delete entrie(s) in
*                                           table YAM_I001
    ENDIF.

  ENDIF.

ENDFORM.                    "delete_old_table_entries

*Text symbol text£º
*001:Selection Screen Input
*E01:No data available for the Selected Plant :
*E02:No previous run entry found in custom table YAM_I001 for plant :
*E03:Open dataset failed for :
*E04:No counter found for measurement point :
*E05:Could not delete file :
*E06:Could not delete entrie(s) in table YAM_I001 for :
*I01:Number of Idocs created :
*I02:Job finished

*I03:No Idocs created
*Selection text£º
*P_INWRK:        Plant to be initial loaded
*P_MESTYP:        Message Type
*S_EQUNR:D       Equipment
*S_SWERK:D       Maintenance plant
