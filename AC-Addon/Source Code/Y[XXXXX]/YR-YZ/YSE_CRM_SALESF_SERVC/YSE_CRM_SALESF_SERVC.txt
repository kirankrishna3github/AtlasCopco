*----------------------------------------------------------------------*
* PROGRAM ID           : YSE_CRM_SALESF_SERVC                          *
* PROGRAM TITLE        : Service Contracts for SalesForce              *
* AUTHOR               : Geert Rutten                                  *
* DATE                 : 22/12/2010                                    *
*                                                                      *
* CHANGE REQUEST NUMBER: CD1K961982                                    *
*                                                                      *
* Program Description:  Outbound program to send Service Contracts     *
*                       to SalesForce.                                 *
*                       Program copied from YSE_I003_SERVICE_CONTRACTS *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
*         |          |          |                 |                    *
************************************************************************
REPORT YSE_CRM_SALESF_SERVC MESSAGE-ID yam_inf.

************************************************************************
*                   T A B L E S                                        *
************************************************************************
TABLES: YSE_SLSF_SERVC,          " Custom table Service Contracts CTP
        VBAK.              " Sales Document: Header Data

************************************************************************
*                   C O N S T A N T S                                  *
************************************************************************
CONSTANTS :c_mestyp          LIKE edidc-mestyp VALUE 'YSE_CRM_SALESF_SERVC' ,
           c_idoc_type       LIKE edidc-idoctp VALUE 'YSE_SLSF_SERVC' ,
           c_hdr_segment(21) TYPE c     VALUE 'YSE_E1_SLSF_HDR_SERVC',
           c_itm_segment(21) TYPE c     VALUE 'YSE_E1_SLSF_DTL_SERVC',
           c_yse_salesf_servc(16)     type c     value 'YSE_SALESF_SERVC',
           c_sh              type parvw value 'WE',   " Ship-to-party
           c_ag              type parvw value 'AG',   " Sold-to-party
           c_g(1)            type c     value 'G',    " Contracts
           c_d(1)            type c     value 'D',    " Delete
           c_c(1)            type c     value 'C',    " Change
           c_a(1)            type c     value 'A',    " Add
           c_x(1)            type c     value 'X',
           c_underscore(1)   type c     value '_',    " Underscore
           c_ls(2)           type c     VALUE 'LS',
           c_sc1(3)          type c     VALUE 'SC1',
           c_input           type c     VALUE '0',
           c_0000(4)         type c     value '0000',
           c_001(3)          type c     value '001',
           c_000000(6)       type c     value '000000',
           c_99991231(8)     type c     value '99991231',
           c_zam00002        type j_stsma value 'ZAM00002',
           c_e               type spras   value 'E',    " Language
           c_run_hours       like impt-atnam value 'ZAM_RHRSTOTAL',
           c_clnt(4)         TYPE c       VALUE 'CLNT' ,
           c_wbi(3)          TYPE c       VALUE 'WBI'   .

************************************************************************
*                   V A R I A B L E S                                  *
************************************************************************
DATA:  g_pfile         like      /sapdmc/lsoinp-filename,
       g_ofile         like      /sapdmc/lsoinp-filename,
       g_delsetupfile  like rlgrap-filename,
       g_vbeln         like vbak-vbeln,
       g_vkorg         like vbak-vkorg,
       g_bukrs         like tvko-bukrs,
       g_land1         like t001-land1,
       g_spras         like t005-spras,
       g_kunnr         like vbpa-kunnr,
       g_parnr         type i_parnr,
       g_stat          like jest-stat,
       g_objnr         like equi-objnr,
       gv_objnr        like jest-objnr,
       g_warpl         like mmpt-warpl,
       g_gewrk         like equz-gewrk,
       g_fm_atinn      type ausp-atinn,
       g_point         like imptt-point,
       g_pm_objty      like equz-pm_objty,
       g_vbegdat       like veda-vbegdat,
       g_vbegdat_dtl   like veda-vbegdat,
       g_venddat       like veda-venddat,
       g_venddat_dtl   like veda-venddat,
       g_netwr         like vbap-netwr,
       g_netwr_tot     like vbap-netwr,
       g_retcd         like sy-subrc,                  " return code
       g_subrc         like sy-subrc,                  " return code
       g_directory(25) type c value '/var/load/xxx/UK/convert/',
       g_ersda(12)     type c,                      " YYYYMMDDHHMM
       g_date          like sy-datum,
       lv_netwr        type vbak-netwr.

************************************************************************
*                  I N T E R N A L   T A B L E S                       *
************************************************************************
DATA : BEGIN OF i_prev OCCURS 0.
        include structure YSE_E1_SLSF_HDR_SERVC.
        include structure YSE_E1_SLSF_DTL_SERVC.
DATA : END OF i_prev.

DATA : BEGIN OF i_ctr OCCURS 0.
        include structure YSE_E1_SLSF_HDR_SERVC.
        include structure YSE_E1_SLSF_DTL_SERVC.
*data :  auart like vbak-auart,
 data:  knumv like vbak-knumv,
        objnr like vbak-objnr.
DATA : END OF i_ctr.

DATA : BEGIN OF i_delfiles OCCURS 0,
         mandt like YSE_SLSF_SERVC-mandt,
         vkorg like YSE_SLSF_SERVC-vkorg,
         ersda like YSE_SLSF_SERVC-ersda,
       END OF i_delfiles.

DATA : BEGIN OF i_ctr_delta OCCURS 0.
        include structure YSE_E1_SLSF_HDR_SERVC.
        include structure YSE_E1_SLSF_DTL_SERVC.
DATA : END OF i_ctr_delta.

DATA : begin of gt_mhis occurs 0,
         abnum like mhis-abnum,
         nplda like mhis-nplda,
       end of gt_mhis.

DATA : begin of gt_warpl occurs 0,
         warpl like mmpt-warpl,
       end of gt_warpl.

DATA : begin of wa_YSE_SLSF_SERVC,
         mandt like YSE_SLSF_SERVC-mandt,
         vkorg like YSE_SLSF_SERVC-vkorg,
         ersda like YSE_SLSF_SERVC-ersda,
       end of wa_YSE_SLSF_SERVC.

DATA: i_edidc_control_comm LIKE edidc OCCURS 1 WITH HEADER LINE ,
      i_edidd_data LIKE edidd OCCURS 0 WITH HEADER LINE         ,
      wa_edidc LIKE edidc                                       ,
      wa_ctr_delta like i_ctr_delta,
      wa_ctr_hdr LIKE YSE_E1_SLSF_HDR_SERVC,
      wa_ctr_items LIKE YSE_E1_SLSF_DTL_SERVC,
      p_logsys LIKE tbdlst-logsys.

DATA: LV_FPLNR  TYPE FPLT-FPLNR.
DATA: it_fplt TYPE TABLE OF fplt with header line.

************************************************************************
*       S E L E C T - O P T I O N S / P A R A M E T E R S              *
************************************************************************

SELECTION-SCREEN : BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.

SELECT-OPTIONS: s_vbeln FOR g_vbeln,                         "Contract
                s_vkorg FOR g_vkorg OBLIGATORY NO INTERVALS. "S.O.
PARAMETERS: p_inorg  like g_vkorg,    "S.O. to be initial loaded
            p_mestyp LIKE edmsg-msgtyp DEFAULT c_mestyp MODIF ID sc1.

SELECTION-SCREEN: END OF BLOCK b1.

************************************************************************
INITIALIZATION.

  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = p_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
* ELSE.
*   REPLACE FIRST OCCURRENCE OF c_clnt IN p_logsys WITH c_wbi  .
  ENDIF.

************************************************************************
*    Disable Message Type Screen Field                                 *
************************************************************************
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-group1 = c_sc1.
      screen-input   = c_input.
      MODIFY SCREEN.
      CONTINUE.
    ENDIF.
  ENDLOOP.

************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

** Validate if the ALE Configuration is correctly setup
* PERFORM validate_ale_configuration .

** Get the information for selected plant(s)
  LOOP AT s_vkorg.

    PERFORM get_detail using s_vkorg-low.

*   Create delta with new creations/updates by comparing the selected
*   data with this from the previous run. When a new sales organization
*   has to be loaded, no comparison has to take place.
    if not i_ctr[] is initial.

      if s_vkorg-low ne p_inorg.

        sort i_ctr by vkorg vbeln posnr.

        clear: g_retcd.
        perform get_previous_file using s_vkorg-low
                               changing g_retcd.

        if g_retcd = 0.
          PERFORM create_delta.
        else.
          CONTINUE.                      " Pass to next S.O.
        endif.
      else.
        loop at i_ctr.
          move-corresponding i_ctr to i_ctr_delta.
          move c_a to i_ctr_delta-msgfn.
          append i_ctr_delta.
          clear i_ctr_delta.
        endloop.
      endif.

*     write outputfile to application server
      clear g_retcd.

      CONCATENATE sy-datum sy-uzeit(4) into g_ersda.
      CONCATENATE c_yse_salesf_servc s_vkorg-low g_ersda
                INTO g_ofile separated by c_underscore.

      REPLACE 'xxx' IN g_directory WITH p_logsys(3).
      CONCATENATE g_directory g_ofile INTO g_ofile.

      perform write_outputfile.

      if g_retcd is initial.
*       update custom table YSE_SLSF_SERVC
        perform update_custom_table using s_vkorg-low.

*       Delete older entries in custom table YSE_SLSF_SERVC
        PERFORM delete_old_table_entries using s_vkorg-low.

*       Delete older files on appl.server
        if NOT i_delfiles[] IS INITIAL.
          PERFORM delete_old_files.
        endif.

      endif.

    endif.

  ENDLOOP.


************************************************************************
*       E N D - O F - S E L E C T I O N    E V E N T                   *
************************************************************************
END-OF-SELECTION .

  IF i_ctr_delta[] IS INITIAL .
    write: / 'No Idocs created'(i03).
  else.

** Generate Idoc's
    sort i_ctr_delta by vbeln posnr.
    PERFORM create_idocs.

  ENDIF.

  write: / 'Job finished'(i02).

*--- S U B R O U T I N E S --------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  Validate_ALE_Configuration
*&---------------------------------------------------------------------*
FORM validate_ale_configuration .

  DATA: l_create_idoc     TYPE     c .

* CHECK IF IDOC CONFIGURATION IS READY AND IDOC CAN BE PROCESSED.
  CALL FUNCTION 'ALE_MODEL_DETERMINE_IF_TO_SEND'
    EXPORTING
      message_type           = p_mestyp
    IMPORTING
      idoc_must_be_sent      = l_create_idoc
    EXCEPTIONS
      own_system_not_defined = 1
      OTHERS                 = 2.

  IF sy-subrc <> 0.
    MESSAGE e029 WITH p_mestyp.
    EXIT.
  ENDIF.

ENDFORM.                    " Validate_ALE_Configuration
*&---------------------------------------------------------------------*
*&      Form  Get_Detail
*&---------------------------------------------------------------------*
*       To get the information for selected sales organizations
*----------------------------------------------------------------------*
FORM get_detail using p_vkorg.

  clear i_ctr.
  refresh i_ctr.

* Select contract data based on Selection screen parameters
  SELECT a~vbeln a~vkorg a~auart a~objnr b~posnr b~matnr b~arktx
         a~vbtyp
         a~knumv a~bstnk
           INTO CORRESPONDING FIELDS OF TABLE i_ctr
           FROM VBAK as a INNER JOIN VBAP as b
           ON a~vbeln eq b~vbeln
           WHERE a~vbeln in s_vbeln
             AND a~vkorg eq p_vkorg
             AND a~vbtyp eq c_g.

* Process Error - No data found for the Selected S.O.
  IF sy-subrc NE 0 .
    write: text-e01, p_vkorg.
    EXIT.
  ENDIF .

* Get related info and populate data into Final Internal Table
  LOOP AT i_ctr.

* get condition value ZPN0
    SELECT single kbetr INTO g_netwr
          FROM KONV
          WHERE knumv eq i_ctr-knumv
            AND kposn eq i_ctr-posnr
            AND kschl eq 'ZPN0'.
    if sy-subrc = 0.
      move g_netwr to i_ctr-netwr.
    endif.

* Get conditions value ZPN0 total for all lines

* get condition value ZPN0
    CLEAR g_netwr_tot.
    SELECT kbetr INTO g_netwr
          FROM KONV
          WHERE knumv eq i_ctr-knumv
          AND kschl eq 'ZPN0'.
      g_netwr_tot = g_netwr_tot + g_netwr.
    ENDSELECT.
*    move g_netwr_tot to i_ctr-kbetr.
    clear lv_netwr.
    SELECT SINGLE netwr from vbak into lv_netwr
     WHERE vbeln = i_ctr-VBELN.

    i_ctr-kbetr = lv_netwr.

* get billing date
      SELECT SINGLE FPLNR INTO LV_FPLNR
        FROM VBKD WHERE VBELN EQ i_ctr-vbeln
                    AND POSNR EQ i_ctr-posnr
                    AND FPLNR NE SPACE.

      SELECT * INTO TABLE it_fplt
        FROM  fplt
          WHERE  fplnr = lv_fplnr
            AND fksaf = 'A'.
        IF sy-subrc = 0.
         SORT it_fplt by afdat ascending.
         READ TABLE it_fplt index 1.
         MOVE it_fplt-afdat TO i_ctr-afdat.
        ENDIF.
*   get contract validity data
    select single vbegdat venddat vlaufz vlauez
        into (g_vbegdat, g_venddat, i_ctr-vlaufz, i_ctr-vlauez)
        from VEDA
        where vbeln = i_ctr-vbeln
          and vposn = c_000000.
    move g_vbegdat to i_ctr-vbegdat.
    move g_venddat to i_ctr-venddat.

    clear: g_venddat_dtl, g_vbegdat_dtl.
    select single vbegdat venddat
        into (g_vbegdat_dtl, g_venddat_dtl)
        from VEDA
        where vbeln = i_ctr-vbeln
          and vposn = i_ctr-posnr.
    if sy-subrc = 0.
     move g_vbegdat_dtl to i_ctr-vbegdat_dtl.
     move g_venddat_dtl to i_ctr-venddat_dtl.
    else.
     move g_vbegdat to i_ctr-vbegdat_dtl.
     move g_venddat to i_ctr-venddat_dtl.
    endif.

*   get the contract type description
    select single bukrs into g_bukrs
        from TVKO
        where vkorg = p_vkorg.

    if sy-subrc = 0.
      select single land1 into g_land1
          from T001
          where bukrs = g_bukrs.

      if sy-subrc = 0.
        select single spras into g_spras
            from T005
            where land1 = g_land1.

        if sy-subrc = 0.
          select single bezei into i_ctr-bezei
              from TVAKT
              where spras = g_spras
                and auart = i_ctr-auart.
        endif.
      endif.
    endif.

*   get BPCS customer number of Sold-to-party
    select single kunnr into i_ctr-kunnr
         from VBPA
         where vbeln = i_ctr-vbeln
           and posnr = c_000000
           and parvw = c_ag.

*   get BPCS customer number of Ship-to-party
    select single kunnr into g_kunnr
         from VBPA
         where vbeln = i_ctr-vbeln
           and posnr = c_000000
           and parvw = c_sh.

    if sy-subrc = 0.
      select single sortl into i_ctr-sortl
         from KNA1
         where kunnr = g_kunnr.
    endif.

*   get status
    SELECT SINGLE stat into g_stat
       FROM JEST
       WHERE objnr = i_ctr-objnr
         AND stat like 'E%   '
         AND inact NE 'X'.

    if sy-subrc = 0.
      select single txt04 into i_ctr-txt04
         from TJ30T
         where stsma = c_zam00002
           and estat = g_stat
           and spras = c_e.
    endif.

*   get equipnr., serialnr. and material nr.
    SELECT equnr matnr sernr
          INTO (i_ctr-equnr, i_ctr-artnr, i_ctr-sernr)
          FROM VISER02
          WHERE sdaufnr eq i_ctr-vbeln
            AND posnr   eq i_ctr-posnr.
    ENDSELECT.

*   get main work center description
    if not i_ctr-equnr is initial.
      select single gewrk pm_objty objnr
           into (g_gewrk, g_pm_objty, g_objnr)
           from v_equi
           where equnr = i_ctr-equnr
             and datbi = c_99991231.

      if sy-subrc = 0.
        select single ktext into i_ctr-ktext
             from crtx
             where objid = g_gewrk
               and objty = g_pm_objty
               and spras = c_e.
      endif.

*     get next planned visit date
**    convert ZAM_RHRSTOTAL into internal value
      clear g_fm_atinn .
      CALL FUNCTION 'CONVERSION_EXIT_ATINN_INPUT'
        EXPORTING
          INPUT  = c_run_hours
        IMPORTING
          OUTPUT = g_fm_atinn.

      if sy-subrc = 0.
**      get measurement point
        clear g_point.
        select point from IMPTT into g_point
           where mpobj eq g_objnr
             and atinn eq g_fm_atinn.
        endselect.

        if sy-subrc = 0.


          refresh gt_warpl.
          select warpl
             into corresponding fields of table gt_warpl
             from MMPT
             where point = g_point.

          if not gt_warpl[] is initial.
            loop at gt_warpl.

*............ Check if maintenance plan is deactivated, then skip
              concatenate 'WO' gt_warpl-warpl into gv_objnr.

              CALL FUNCTION 'STATUS_CHECK'
                  EXPORTING
*                   BYPASS_BUFFER           = ' '
*                   CLIENT                  = SY-MANDT
                    OBJNR                   = gv_objnr
                    STATUS                  = 'I0320'
                  EXCEPTIONS
                    OBJECT_NOT_FOUND        = 1
                    STATUS_NOT_ACTIVE       = 2
                    OTHERS                  = 3.

              if sy-subrc eq 0.
                continue.
              endif.
              g_warpl = gt_warpl-warpl.
            endloop.
          endif.

          if not g_warpl is initial.


**          get maintenance plan
            clear gt_mhis.
            refresh gt_mhis.
            SELECT abnum nplda
                INTO corresponding fields of table gt_mhis
                FROM MHIS
                WHERE warpl = g_warpl
                ORDER BY abnum.

            if sy-subrc = 0.
              loop at gt_mhis.

                if gt_mhis-nplda > sy-datum.
                  move gt_mhis-nplda to i_ctr-nplda.
                  exit.
                endif.

              endloop.
            endif.

          endif.

        endif.

      endif.

    endif.

    MODIFY i_ctr transporting netwr vbegdat venddat vlaufz
             vlauez bezei sortl txt04 kbetr equnr artnr sernr ktext nplda kunnr vbegdat_dtl venddat_dtl afdat.

  ENDLOOP.

ENDFORM.                    " Get_Detail
*&---------------------------------------------------------------------*
*&      Form  Create_delta
*&---------------------------------------------------------------------*
*       Create internal table with creates/updates/deletes
*----------------------------------------------------------------------*
FORM create_delta.

* creates/updates
  loop at i_ctr.

    read table i_prev with key vbeln = i_ctr-vbeln
                               posnr = i_ctr-posnr
                    binary search.

    if sy-subrc = 0.
***** MUST BE UPDATED when change in segments YSE_E1_SLSF_HDR_SERVC/YSE_E1_SLSF_DTL_SERVC
      if i_ctr(325) <> i_prev(325).
************************************************************************
        move-corresponding i_ctr to i_ctr_delta.
        move c_c to i_ctr_delta-msgfn.
        append i_ctr_delta.
        clear i_ctr_delta.
      endif.
    else.
      move-corresponding i_ctr to i_ctr_delta.
      move c_a to i_ctr_delta-msgfn.
      append i_ctr_delta.
      clear i_ctr_delta.
    endif.

  endloop.

* deletes
  clear i_prev.
  loop at i_prev.

    read table i_ctr with key vbeln = i_prev-vbeln
                              posnr = i_prev-posnr
                    binary search.

    if sy-subrc <> 0.
      move-corresponding i_prev to i_ctr_delta.
      move c_d to i_ctr_delta-msgfn.
      append i_ctr_delta.
      clear i_ctr_delta.
    endif.

  endloop.

ENDFORM.                    " Create_delta
*&---------------------------------------------------------------------*
*&      Form  Get_previous_file
*&---------------------------------------------------------------------*
*       Get file from previous run
*----------------------------------------------------------------------*
FORM get_previous_file using p_vkorg
                    changing p_retcd.

* prepare filename of previous run
  clear g_ersda.
  select ersda into g_ersda
      from YSE_SLSF_SERVC where vkorg = p_vkorg.
  endselect.

  if sy-subrc <> 0.
    write: / text-e02, p_vkorg.    "No filename of previous run
*                                   available in custom table YSE_SLSF_SERVC
    p_retcd = 4.
    exit.
  endif.

  CONCATENATE c_yse_salesf_servc p_vkorg g_ersda
                      INTO g_pfile separated by c_underscore.
  REPLACE 'xxx' IN g_directory WITH p_logsys(3).
  CONCATENATE g_directory g_pfile into g_pfile.

* FILE READ FROM APPLICATION SERVER
  PERFORM get_from_appl TABLES  i_prev
                        USING   g_pfile
                                p_retcd.

ENDFORM.                    " Get_previous_file
*&---------------------------------------------------------------------*
*&      Form  Get_from_appl
*&---------------------------------------------------------------------*
*       Get the file from application server into internal table
*----------------------------------------------------------------------*
FORM get_from_appl TABLES i_infile STRUCTURE i_prev
                   USING p_infile p_subrc.

  OPEN DATASET p_infile FOR INPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    write: / text-e03, p_infile.
    p_subrc = 4.
    exit.
  ENDIF.

  refresh i_infile.

  DO.
    READ DATASET p_infile INTO i_infile.
    IF sy-subrc <> 0.
      EXIT.
    ENDIF.
    APPEND i_infile.
  ENDDO.
  CLOSE DATASET p_infile.

ENDFORM.                    " GET_FROM_APPL
*&---------------------------------------------------------------------*
*&      Form  Write_outputfile
*&---------------------------------------------------------------------*
*       Write outputfile
*----------------------------------------------------------------------*
FORM write_outputfile.

  open dataset g_ofile for output in text mode encoding default.
  if sy-subrc <> 0.
    write: / text-e03, g_ofile.
*   message e011(YAM_DC).                      "Open dataset failed
    g_retcd = 4.
    exit.
  endif.

  sort i_ctr by vbeln posnr.

  loop at i_ctr.
***** MUST BE UPDATED when change in segments YSE_E1_SLSF_HDR_SERVC/YSE_E1_SLSF_DTL_SERVC
    transfer i_ctr(325) to g_ofile.
***********************************************************************
  endloop.

ENDFORM.                    " Write_outputfile
*&---------------------------------------------------------------------*
*&      Form  Update_custom_table
*&---------------------------------------------------------------------*
*       Update custom table YSE_SLSF_SERVC
*----------------------------------------------------------------------*
FORM update_custom_table using p_vkorg.

  wa_YSE_SLSF_SERVC-mandt = sy-mandt.
  wa_YSE_SLSF_SERVC-vkorg = p_vkorg.
  wa_YSE_SLSF_SERVC-ersda = g_ersda.

  insert YSE_SLSF_SERVC from wa_YSE_SLSF_SERVC.

ENDFORM.                    " Update_custom_table
*&---------------------------------------------------------------------*
*&      Form  Create_IDocs
*&---------------------------------------------------------------------*
*       Create Idoc's
*----------------------------------------------------------------------*
FORM create_idocs .

  DATA: g_created_comm_idocs TYPE sy-tabix .
  CLEAR : i_edidc_control_comm ,
          wa_edidc             ,
          i_edidd_data         .

** Polulate Control Record
  wa_edidc-mestyp =  p_mestyp.
  wa_edidc-idoctp =  c_idoc_type.
  wa_edidc-rcvprt =  c_ls.
* wa_edidc-rcvprn =  p_logsys .

** Create Idoc's for every new Contract Number
  LOOP AT i_ctr_delta.

    move-corresponding i_ctr_delta to wa_ctr_hdr.
    move-corresponding i_ctr_delta to wa_ctr_items.

    AT NEW vbeln .
      CLEAR i_edidd_data[] .
      i_edidd_data-segnam  = c_hdr_segment  .
      i_edidd_data-sdata   = wa_ctr_hdr.
      APPEND i_edidd_data .
    ENDAT .

    AT NEW vkorg.
      clear wa_edidc-rcvprn.
      SELECT SINGLE rcvprn INTO wa_edidc-rcvprn
             FROM edp13
             WHERE mestyp = p_mestyp.
    ENDAT.

    i_edidd_data-segnam  = c_itm_segment  .
    i_edidd_data-sdata   = wa_ctr_items.
    APPEND i_edidd_data .

    AT END OF vbeln .
** Generate Idoc's

      CALL FUNCTION 'MASTER_IDOC_DISTRIBUTE'
        EXPORTING
          master_idoc_control            = wa_edidc
        TABLES
          communication_idoc_control     = i_edidc_control_comm
          master_idoc_data               = i_edidd_data
        EXCEPTIONS
          error_in_idoc_control          = 1
          error_writing_idoc_status      = 2
          error_in_idoc_data             = 3
          sending_logical_system_unknown = 4
          OTHERS                         = 5.

      IF sy-subrc <> 0.
        MESSAGE e025 with sy-subrc.
      ELSE.
        DESCRIBE TABLE i_edidc_control_comm LINES sy-tfill.
        ADD sy-tfill TO g_created_comm_idocs.

        read table i_edidc_control_comm index 1.

        REFRESH i_edidc_control_comm.
      ENDIF.

      call function 'BAPI_TRANSACTION_COMMIT'.

      call function 'EDI_DOCUMENT_DEQUEUE_LATER'
        EXPORTING
          docnum                 = i_edidc_control_comm-docnum
        EXCEPTIONS
          idoc_is_not_to_dequeue = 1
          others                 = 2.

    ENDAT .

  ENDLOOP .

  write: / text-i01, g_created_comm_idocs .

ENDFORM.                    " Create_IDocs
*&---------------------------------------------------------------------*
*&      Form  Delete_old_files
*&---------------------------------------------------------------------*
*       Delete files from former runs on application server
*----------------------------------------------------------------------*
FORM delete_old_files.

  data: g_dir_name like EPSF-EPSDIRNAM,
        g_dfile    like EPSF-EPSFILNAM.

  loop at i_delfiles.

    CONCATENATE c_yse_salesf_servc i_delfiles-vkorg i_delfiles-ersda
                         INTO g_dfile separated by c_underscore.

    move g_directory to g_dir_name.
    REPLACE 'xxx' IN g_dir_name WITH p_logsys(3).
    TRANSLATE g_dir_name(10) to lower case.
    TRANSLATE g_dir_name+17(8) to lower case.

* change MJ 20090602
*    CALL FUNCTION 'EPS_DELETE_FILE'
     call function 'YSE_EPS_DELETE_FILE'
      EXPORTING
        FILE_NAME              = g_dfile
        DIR_NAME               = g_dir_name
      EXCEPTIONS
        INVALID_EPS_SUBDIR
        SAPGPARAM_FAILED
        BUILD_DIRECTORY_FAILED
        NO_AUTHORIZATION
        BUILD_PATH_FAILED
        DELETE_FAILED.

    if sy-subrc <> 0.
      write: / text-e05, g_dfile.      "Could not delete file
    endif.

  endloop.

ENDFORM.                    "delete_old_files
*&---------------------------------------------------------------------*
*&      Form  Delete_old_table_entries
*&---------------------------------------------------------------------*
*       Delete entries from former runs in custom table YSE_SLSF_SERVC
*----------------------------------------------------------------------*
FORM delete_old_table_entries using p_vkorg.

  g_date = sy-datum - 7.
  concatenate g_date c_0000 into g_ersda.

  refresh i_delfiles.

  select * from YSE_SLSF_SERVC
      where vkorg eq p_vkorg
        and ersda lt g_ersda.
    move YSE_SLSF_SERVC to i_delfiles.
    append i_delfiles.
    clear i_delfiles.
  endselect.

  if sy-subrc = 0.
    delete from YSE_SLSF_SERVC where vkorg eq p_vkorg
                           and ersda lt g_ersda.

    if sy-subrc <> 0.
      write: / text-e06, p_vkorg.        "Could not delete entrie(s) in
*                                         table YSE_SLSF_SERVC
    endif.

  endif.

ENDFORM.                    "delete_old_table_entries

*Text symbol text£º
*001:Selection Screen Input
*E01:No data available for the Selected Sales Organization :
*E02:No previous run entry found in custom table YSE_SLSF_SERV for S.O. :
*E03:Open dataset failed for :
*E05:Could not delete file :
*E06:Could not delete entrie(s) in table YSE_SLSF_SERVC for :
*I01:Number of Idocs created :
*I02:Job finished

*I03:No Idocs created
*Selection text£º
*P_INORG:        Sal. Org. to be initial loaded
*P_MESTYP:        Message Type
*S_VBELN:D       .
*S_VKORG:D       .
