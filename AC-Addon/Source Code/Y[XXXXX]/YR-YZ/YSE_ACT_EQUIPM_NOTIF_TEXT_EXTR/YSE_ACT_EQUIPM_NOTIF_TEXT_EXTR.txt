*&---------------------------------------------------------------------*
*& Report  YSE_ACT_EQUIPM_NOTIF_TEXT_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Equipments: Notification Item Texts extraction               *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 15.01.2013
*  Change Request Number :
*  Transport request Nr. : CD1K974692
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 19.03.2014 | Jules Smets     | CD1K980489    | CR1794 (Act)*
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_act_equipm_notif_text_extr.

TABLES: equi,
        equz,
        iloa,
        jest,
        qmih,
        qmel,
        qmfe,
        stxh,
        ttxid.

TYPES: BEGIN OF ty_equi,
         equnr      TYPE equnr,
         objnr      TYPE j_objnr,
      END OF ty_equi.

TYPES: BEGIN OF ty_dlfl,
         objnr      TYPE j_objnr,
      END OF ty_dlfl.

TYPES: BEGIN OF ty_qmih,
         equnr      TYPE equnr,
         qmnum      TYPE qmnum,
      END OF ty_qmih.

TYPES: BEGIN OF ty_qmel,
         qmnum      TYPE qmnum,
         qmdab      TYPE qmdab,
         fenum      TYPE felfd,
         fetxt      TYPE fetxt,
         aufnr      TYPE aufnr,
         objnr      TYPE j_objnr,
         indtx      TYPE indltx,
         fegrp      TYPE fegrp,
         fecod      TYPE fecod,
         otgrp      TYPE otgrp,
         oteil      TYPE oteil,
         tdname     TYPE tdobname,
      END OF ty_qmel.

TYPES: BEGIN OF ty_stxh,
         tdname     TYPE tdobname,
         tdid       TYPE tdid,
         tdspras    TYPE spras,
       END OF ty_stxh.

DATA: gt_equi  TYPE TABLE OF ty_equi
                    WITH HEADER LINE.

DATA: gt_dlfl  TYPE HASHED TABLE OF ty_dlfl
                    WITH UNIQUE KEY objnr
                    WITH HEADER LINE,
      gt_dlfli TYPE TABLE OF ty_dlfl
                    WITH HEADER LINE.

DATA: gt_qmih  TYPE SORTED TABLE OF ty_qmih
                    WITH NON-UNIQUE KEY equnr
                    WITH HEADER LINE.

DATA: gt_qmel  TYPE SORTED TABLE OF ty_qmel
                    WITH NON-UNIQUE KEY qmnum
                    WITH HEADER LINE.

DATA: gt_stxh  TYPE SORTED TABLE OF ty_stxh
                    WITH NON-UNIQUE KEY tdname
                    WITH HEADER LINE.

*DATA: gt_ttxid TYPE STANDARD TABLE OF ttxid
*                    WITH HEADER LINE.

DATA: gt_lines TYPE TABLE OF tline
                    WITH HEADER LINE.

DATA: BEGIN OF gt_out  OCCURS 0,
        equnr       TYPE equnr,
        tab01(1)    TYPE c,
        qmnum       TYPE qmnum,
        tab02(1)    TYPE c,
        qmdab       TYPE qmdab,
        tab03(1)    TYPE c,
        fenum       TYPE felfd,
        tab04(1)    TYPE c,
        aufnr       TYPE aufnr,
        tab05(1)    TYPE c,
        fetxt       TYPE fetxt,
        tab06(1)    TYPE c,
        tdid        TYPE tdid,
        tab07(1)    TYPE c,
        tdspras     TYPE spras,
        tab08(1)    TYPE c,
        tdline      TYPE tdline,
        tab09(1)    TYPE c,
        tdidx       TYPE numc5,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        equnr       TYPE fieldname,
        tab01(1)    TYPE c,
        qmnum       TYPE fieldname,
        tab02(1)    TYPE c,
        qmdab       TYPE fieldname,
        tab03(1)    TYPE c,
        fenum       TYPE fieldname,
        tab04(1)    TYPE c,
        aufnr       TYPE fieldname,
        tab05(1)    TYPE c,
        fetxt       TYPE fieldname,
        tab06(1)    TYPE c,
        tdid        TYPE fieldname,
        tab07(1)    TYPE c,
        tdspras     TYPE fieldname,
        tab08(1)    TYPE c,
        tdline      TYPE fieldname,
        tab09(1)    TYPE c,
        tdidx       TYPE fieldname,
      END OF gt_outh.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_ersda(14)     TYPE c,                      "YYYYMMDDHHMMSS
      gv_error         TYPE xfeld.

CONSTANTS: gc_stat_dl  TYPE j_status  VALUE 'I0076',
           gc_stat_in  TYPE j_status  VALUE 'I0320',
           gc_stat_scr TYPE j_status  VALUE 'E0005',
           gc_stat_nc  TYPE j_status  VALUE 'I0072'.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
*PARAMETERS: p_bukrs  LIKE iloa-bukrs  OBLIGATORY  MEMORY ID buk.
PARAMETERS: p_werk  LIKE iloa-swerk  OBLIGATORY . "Wouter - becaus ethis has a direct index
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_equnr  FOR equi-equnr.
*                s_tdid   FOR ttxid-tdid  NO INTERVALS  DEFAULT 'LTXT'.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_fegrp  FOR qmfe-fegrp  DEFAULT 'ZACD-038',
                s_fecod  FOR qmfe-fecod  DEFAULT '098',
                s_otgrp  FOR qmfe-otgrp  DEFAULT 'ZACO-038',
                s_oteil  FOR qmfe-oteil  DEFAULT '098'.
SELECTION-SCREEN  END OF BLOCK input.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Timestamp
  CONCATENATE sy-datum sy-uzeit INTO gv_ersda.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

** Text-ID selection
*  IF s_tdid[] IS INITIAL.
*    PERFORM tdid_selection.
*  ENDIF.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_equi[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No equipments selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.
  SORT gt_out BY equnr ASCENDING qmdab DESCENDING
                 qmnum DESCENDING fenum ASCENDING
                 tdidx ASCENDING.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  TDID_SELECTION
*&---------------------------------------------------------------------*
*       Text-ID selection
*----------------------------------------------------------------------*
*FORM tdid_selection .
*
** Text id's for equipment
*  SELECT * INTO TABLE gt_ttxid
*           FROM ttxid
*           WHERE tdobject = 'QMFE'.
*
*  LOOP AT gt_ttxid.
*    s_tdid-low    = gt_ttxid-tdid.
*    s_tdid-sign   = 'I'.
*    s_tdid-option = 'EQ'.
*    APPEND s_tdid.
*  ENDLOOP.
*
*  FREE gt_ttxid.
*
*ENDFORM.                    " TDID_SELECTION

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Equipment time segment
  SELECT e~equnr e~objnr
         INTO TABLE gt_equi
         FROM equi AS e
         INNER JOIN equz AS z
                    ON z~equnr = e~equnr
         INNER JOIN iloa AS l
                    ON l~iloan = z~iloan
         WHERE e~equnr IN s_equnr
           AND z~datbi =  '99991231'
*           AND l~bukrs =  p_bukrs.
           AND l~swerk =  p_werk.   "Wouter : better index
  CHECK NOT gt_equi[] IS INITIAL.

*** MOD-001 * begin ***
** Check for deleted/scrapped equipments
*  SELECT objnr
*         INTO TABLE gt_dlfli
*         FROM jest
*         FOR ALL ENTRIES IN gt_equi
*         WHERE objnr = gt_equi-objnr
*           AND ( stat = gc_stat_dl   OR
*                 stat = gc_stat_in   OR
*                 stat = gc_stat_scr )
*           AND inact = ' '.
*  SORT gt_dlfli BY objnr.
*  DELETE ADJACENT DUPLICATES FROM gt_dlfli
*                                  COMPARING objnr.
*  gt_dlfl[] = gt_dlfli[].
*  FREE gt_dlfli.
*
*  LOOP AT gt_equi.
*    READ TABLE gt_dlfl WITH TABLE KEY objnr = gt_equi-objnr.
*    IF sy-subrc = 0.
*      DELETE gt_equi.
*    ENDIF.
*  ENDLOOP.
*  FREE gt_dlfl.
*  CHECK NOT gt_equi[] IS INITIAL.
*** MOD-001 * end ***

* Notifications
  SELECT equnr qmnum
         INTO TABLE gt_qmih
         FROM qmih
         FOR ALL ENTRIES IN gt_equi
         WHERE equnr = gt_equi-equnr.
  CHECK NOT gt_qmih[] IS INITIAL.

  SELECT n~qmnum n~qmdab i~fenum i~fetxt n~aufnr n~objnr
         i~indtx i~fegrp i~fecod i~otgrp i~oteil
         INTO TABLE gt_qmel
         FROM qmel AS n
         INNER JOIN qmfe AS i
                    ON i~qmnum = n~qmnum
         FOR ALL ENTRIES IN gt_qmih
         WHERE n~qmnum =  gt_qmih-qmnum.

  DELETE gt_qmel WHERE indtx NE 'X'          OR
                       fegrp NOT IN s_fegrp  OR
                       fecod NOT IN s_fecod  OR
                       otgrp NOT IN s_otgrp  OR
                       oteil NOT IN s_oteil.
  CHECK NOT gt_qmel[] IS INITIAL.

* Check for completed notifications
  SELECT objnr
         INTO TABLE gt_dlfli
         FROM jest
         FOR ALL ENTRIES IN gt_qmel
         WHERE objnr = gt_qmel-objnr
           AND stat = gc_stat_nc
           AND inact = ' '.
  SORT gt_dlfli BY objnr.
  DELETE ADJACENT DUPLICATES FROM gt_dlfli
                                  COMPARING objnr.
  gt_dlfl[] = gt_dlfli[].
  FREE gt_dlfli.

  LOOP AT gt_qmel.
    READ TABLE gt_dlfl WITH TABLE KEY objnr = gt_qmel-objnr.
    IF sy-subrc = 0.
      CONCATENATE gt_qmel-qmnum gt_qmel-fenum INTO gt_qmel-tdname.
      MODIFY gt_qmel.
    ELSE.
      DELETE gt_qmel.
    ENDIF.
  ENDLOOP.
  FREE gt_dlfl.
  CHECK NOT gt_qmel[] IS INITIAL.

* Notification item texts
  SELECT tdname tdid tdspras
         INTO TABLE gt_stxh
         FROM stxh
         FOR ALL ENTRIES IN gt_qmel
         WHERE tdobject = 'QMFE'
           AND tdname   = gt_qmel-tdname
           AND tdid     = 'LTXT'.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_equi.

*   Notifications
    LOOP AT gt_qmih WHERE equnr = gt_equi-equnr.

      LOOP AT gt_qmel WHERE qmnum = gt_qmih-qmnum.

*       Text
        LOOP AT gt_stxh WHERE tdname = gt_qmel-tdname.

          CLEAR: gt_out.
          MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03,
                       gt_out-tab04, gt_out-tab05, gt_out-tab06,
                       gt_out-tab07, gt_out-tab08, gt_out-tab09.

          gt_out-equnr   = gt_equi-equnr.
          gt_out-qmnum   = gt_qmel-qmnum.
          gt_out-qmdab   = gt_qmel-qmdab.
          gt_out-fenum   = gt_qmel-fenum.
          gt_out-aufnr   = gt_qmel-aufnr.
          gt_out-fetxt   = gt_qmel-fetxt.
          gt_out-tdid    = gt_stxh-tdid.
          gt_out-tdspras = gt_stxh-tdspras.

          CALL FUNCTION 'READ_TEXT'
            EXPORTING
*          CLIENT                  = SY-MANDT
              id                      = gt_stxh-tdid
              language                = gt_stxh-tdspras
              name                    = gt_stxh-tdname
              object                  = 'QMFE'
*            ARCHIVE_HANDLE          = 0
*            LOCAL_CAT               = ' '
*          IMPORTING
*            HEADER                  =
            TABLES
              lines                   = gt_lines
            EXCEPTIONS
              id                      = 1
              language                = 2
              name                    = 3
              not_found               = 4
              object                  = 5
              reference_check         = 6
              wrong_access_to_archive = 7
              OTHERS                  = 8
                    .
          IF sy-subrc <> 0.
*        MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
          ELSE.
            LOOP AT gt_lines.
              gt_out-tdline = gt_lines-tdline.
              gt_out-tdidx  = sy-tabix.
              APPEND gt_out.
            ENDLOOP.

          ENDIF.

        ENDLOOP.

      ENDLOOP.

    ENDLOOP.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

  CONCATENATE 'EQUIPMENT' 'NOTIF_ITM' 'TXT' p_werk gv_ersda   "Wouter replaced bukrs by p_werk
              INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM fill_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  FILL_HEADER
*&---------------------------------------------------------------------*
*       Fill header
*----------------------------------------------------------------------*
FORM fill_header .

  CLEAR: gt_outh.
  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03,
               gt_outh-tab04, gt_outh-tab05, gt_outh-tab06,
               gt_outh-tab07, gt_outh-tab08, gt_outh-tab09.

  gt_outh-equnr    = 'EQUNR'.
  gt_outh-qmnum    = 'QMNUM'.
  gt_outh-qmdab    = 'QMDAB'.
  gt_outh-fenum    = 'FENUM'.
  gt_outh-aufnr    = 'AUFNR'.
  gt_outh-fetxt    = 'FETXT'.
  gt_outh-tdid     = 'TDID'.
  gt_outh-tdspras  = 'SPRAS'.
  gt_outh-tdline   = 'TDLINE'.
  gt_outh-tdidx    = 'NUMC5'.
  APPEND gt_outh.

  gt_outh-equnr    = 'Equipment'.
  gt_outh-qmnum    = 'Notification'.
  gt_outh-qmdab    = 'Completion'.
  gt_outh-fenum    = 'Item'.
  gt_outh-aufnr    = 'Serv. Order'.
  gt_outh-fetxt    = 'Item Short Text'.
  gt_outh-tdid     = 'Text ID'.
  gt_outh-tdspras  = 'Lang.'.
  gt_outh-tdline   = 'Text Line'.
  gt_outh-tdidx    = 'Line Nr.'.
  APPEND gt_outh.

ENDFORM.                    " FILL_HEADER

*Text symbol text£º
*E01:No equipments selected
*E02:Could not open output file
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_WERK:D       .
*S_EQUNR:D       .
*S_FECOD:D       .
*S_FEGRP:D       .
*S_OTEIL:D       .
*S_OTGRP:D       .
