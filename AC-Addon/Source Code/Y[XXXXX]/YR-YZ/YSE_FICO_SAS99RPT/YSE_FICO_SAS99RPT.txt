*&---------------------------------------------------------------------*
*& Report  YSE_FICO_SAS99RPT
*&
*&---------------------------------------------------------------------*
*& Request No  Date        Programmer  Revision
*&---------------------------------------------------------------------*
*& CD1K973290 17/09/2012   Johnny Wu   CR2680 Initial version
*&---------------------------------------------------------------------*

report  yse_fico_sas99rpt.

************************************************************************
*
*                   SAS 99 Data Extract Program
*
*
*
*   Prepared by:  Ketan Gohil
*
*                 RJT Compuquest.
*
*
*
*   Preparation date:  March 13, 2003
*
*
*
*   Purpose:  Extract data related to SAS 99 analysis from tables SKAT,
*             BSEG AND BKPF
*
*   How to Use ?
*   This program is dealing with very large tables BSEG and BKPF .
*   If it is run online with selection of more than 3 to 4 month data
*   will be taking much time in execution .It may time out
*        So it is advisable to run in  background . If it is run in
*   background system will not be able to  download the file
*   on presentation server.
*
*   This program has 3 options.
*   1. EXPORT: option will fatch the data from the database tables and
*       put them into database memory . if you are running the program
*       in backgaround select this option .
*   2. IMPORT : After running the program in background run the program
*       online with this selection . system will read data from database
*       memory and down load to presentation server
*   3. ONLINE : This option will fatch data from the table and download
*      to presenation server online . If you want to fatch data for
*      less than three months use this option .
*
*   How can I run this program efficiently?
*       let's say if you want to down load data for one year .
*       run program in background for 3 month each and export radio
*       button selected . Use CPU Date for the 3 month selection .
*       After job completed run program online with IMPORT radio button
*       selected and download each table in different file.
*       Performance of the program will be very good if it is run for
*       one company code and one fiscal year .
*
*   Fields Referenced:   bseg-mandt client,
*                        bseg-buzei line item,
*                        bseg-shkzg debit/credit indicator,
*                        bseg-dmbtr GL amount,
*                        bseg-sgtxt description2,
*                        bseg-kostl cost center,
*                        bseg-hkont GL account number,
*                        bseg-belnr document number,
*                        bseg-bukrs company code,
*                        skat-spras language key,
*                        skat-ktopl chart of accounts,
*                        skat-saknr GL account,
*                        skat-txt50 account description,
*                        bkpf-mandt client,
*                        bkpf-bukrs company code,
*                        bkpf-belnr document number,
*                        bkpf-gjahr fiscal year,
*                        bkpf-blart document type,
*                        bkpf-bldat effective date,
*                        bkpf-budat posting date,
*                        bkpf-monat posting period,
*                        bkpf-usnam user id,
*                        bkpf-bktxt description,
*                        bkpf-waers currency,
*                        bkpf-kursf exchange rate,
*                        bkpf-cpudt system date
************************************************************************
*         You can use or modify this report for your own work.
*      In no event will the author be liable for indirect, special,
*      incidental, or consequental damages (if any) arising out of
*                      the use of this report.
************************************************************************
************************************************************************


************************************************************************
*
*                  DATA DEFINITIONS
*
************************************************************************
tables:

   skat,
   bkpf,
   bseg.

tables indx.

data:

   begin of tempbseg occurs 0,
     mandt like bseg-mandt ,
     bukrs like bseg-bukrs ,
     belnr like bseg-belnr,
     gjahr like bseg-gjahr,
     buzei like bseg-buzei,
     shkzg like bseg-shkzg,
     dmbtr like bseg-dmbtr,
     sgtxt like bseg-sgtxt,
     kostl like bseg-kostl,
     hkont like bseg-hkont,
   end of tempbseg.

data:
    begin of tempskat occurs 0,
      spras like skat-spras,
      ktopl like skat-ktopl,
      saknr like skat-saknr,
      txt50 like skat-txt50,
    end of tempskat.

data:
    begin of tempbkpf occurs 0,
        mandt like bkpf-mandt,
        bukrs like bkpf-bukrs,
        belnr like bkpf-belnr,
        gjahr like bkpf-gjahr,
        blart like bkpf-blart,
        bldat like bkpf-bldat,
        budat like bkpf-budat,
        monat like bkpf-monat,
        usnam like bkpf-usnam,
        bktxt like bkpf-bktxt,
        waers like bkpf-waers,
        kursf like bkpf-kursf,
   end of tempbkpf.

data:
    begin of join occurs 0,
        mandt like bkpf-mandt,
        bukrs like bkpf-bukrs,
        belnr like bkpf-belnr,
        gjahr like bkpf-gjahr,
        blart like bkpf-blart,
        bldat like bkpf-bldat,
        budat like bkpf-budat,
        monat like bkpf-monat,
        usnam like bkpf-usnam,
        bktxt like bkpf-bktxt,
        waers like bkpf-waers,
        kursf like bkpf-kursf,
        buzei like bseg-buzei,
        shkzg like bseg-shkzg,
        dmbtr like bseg-dmbtr,
        sgtxt like bseg-sgtxt,
        kostl like bseg-kostl,
        hkont like bseg-hkont,
end of join.

*local fields .

data  i_index like sy-tabix .
data  l_bkpf type i .

data: skatkey like indx-srtfd value 'TEMPSKAT'.
data: bkpfkey like indx-srtfd value 'TEMPBKPF'.
data: bsegkey like indx-srtfd value 'TEMPBSEG'.
data: joinkey like indx-srtfd value 'JOIN'.

************************************************************************
*
*                SELECTION SCREEN
*
************************************************************************

selection-screen begin of block b1 with frame .

selection-screen begin of line.
selection-screen comment (20) text001.
select-options client for bseg-mandt default sy-mandt obligatory.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment (20) text002.
select-options co_code for bseg-bukrs obligatory.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment (20) text003.
select-options date for bkpf-bldat.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment (20) text004.
select-options lang for skat-spras default sy-langu.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment (20) text005.
select-options ch_accts for skat-ktopl.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment (20) text006.
select-options doc_type for bkpf-blart.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment (20) text007.
select-options user for bkpf-usnam.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment (20) text008.
select-options post_dt for bkpf-budat.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment (20) text009.
select-options doc_no for bkpf-belnr.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment (20) text010.
select-options fisc_yr for bkpf-gjahr obligatory.
selection-screen end of line.

selection-screen begin of line.
selection-screen comment (20) text011.
select-options cpu_dt for bkpf-cpudt .
selection-screen end of line.

selection-screen end of block b1 .

selection-screen skip 2.

selection-screen begin of block b2 with frame .
*Select data from tables and export to database memory
parameters : export radiobutton group rad1 .
* Import priviously selected from  database memory
parameters : import radiobutton group rad1 .
*Select data and download to excel
parameters : online radiobutton group rad1 .

selection-screen end of block b2 .

selection-screen begin of block b3 with frame .

parameters : skatfile like  rlgrap-filename
                           default 'c:\temp\tempskat.dat'.
parameters : bkpffile like  rlgrap-filename
                           default 'c:\temp\tempbkpf.dat'.
parameters : bsegfile like  rlgrap-filename
                           default 'c:\temp\tempbseg.dat'.
parameters : joinfile like  rlgrap-filename
                           default 'c:\temp\tempjoin.dat'.

selection-screen end of block b3 .

selection-screen begin of block b4 with frame .
*display Join data on sreen ? if this check box selected data from
*internal table will be displayed on the screen
parameter display as checkbox .
selection-screen end of block b4 .

initialization.
  perform f_initialize_texts.

*&---------------------------------------------------------------------*
*&      Form  f_initialize_texts
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form f_initialize_texts .
  text001 = '客户端'.
  text002 = '公司代码'.
  text003 = '凭证日期'.
  text004 = '语言代码'.
  text005 = '帐目表(COA)'.
  text006 = '凭证类型'.
  text007 = '用户名'.
  text008 = '过帐日期'.
  text009 = '凭证编号'.
  text010 = '会计年度'.
  text011 = '凭证输入日期'.
endform.                    "f_initialize_texts
************************************************************************
*                    DATA SELECTION
*
************************************************************************
start-of-selection .
  if online = 'X' .
    if sy-batch = 'X' .
      message e999(fh) with 'With ONLINE option' 'you cannot this run program'
                            'in background' .
    endif.
  endif.

  if export = 'X' or online = 'X' .
*selection from skat .
    perform select_skat .

*selection from bkpf .
    perform select_bkpf .

*check whether data was populated in table bkpf or not .
    describe table tempbkpf lines l_bkpf .

    if l_bkpf ne 0 .
*selection from bseg .
      perform select_bseg .
*populate internal table join with bkpf and bseg data.
      perform poulate_join .
    endif.
  endif.
  if export = 'X' .
*export internal tables to data base memory
    perform export_data .
  elseif import = 'X' .
*import internal tables to data base memory
    perform import_data .
  elseif online = 'X' .
*run online and download to excel file .
    perform online_data .

  endif.
*write data on the screen .

  if display = 'X' .
    perform write_data.
  endif.
************************************************************************
*
*                  SUBROUTINES
*
************************************************************************
*&---------------------------------------------------------------------*
*&      Form  SELECT_SKAT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form select_skat.
************************************************************************
*
*SELECT SUBSET OF DATA FROM TABLE SPRAS FOR DOWNLOAD AND OUTSIDE REVIEW
*
************************************************************************

  select spras ktopl saknr txt50 from skat into corresponding fields of
  table tempskat
      where spras in lang
      and ktopl in ch_accts.


endform.                    " SELECT_SKAT

*&---------------------------------------------------------------------*
*&      Form  SELECT_BKPF
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form select_bkpf.

************************************************************************
*
* SELECT SUBSET OF DATA FROM TABLE BKPF FOR DOWNLOAD AND OUTSIDE REVIEW
*
************************************************************************

  select mandt bukrs belnr gjahr blart bldat budat monat usnam bktxt
   waers kursf
 from bkpf client specified into corresponding fields of table tempbkpf
   where mandt in client
   and blart in doc_type
   and bldat in date
   and bukrs in co_code
   and gjahr in fisc_yr
   and belnr in doc_no
   and budat in post_dt
   and cpudt in cpu_dt
   and usnam in user .

* sort table
  sort tempbkpf by mandt bukrs belnr gjahr .

endform.                    " SELECT_BKPF

*&---------------------------------------------------------------------*
*&      Form  SELECT_BSEG
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form select_bseg.
************************************************************************
*
*  SELECT SUBSET OF DATA FROM TABLE BSEG FOR DOWNLOAD AND OUTSIDE REVIEW
*
************************************************************************

  select mandt buzei shkzg dmbtr sgtxt kostl hkont belnr bukrs gjahr
          from bseg
   client specified into corresponding fields of table tempbseg
   for all entries in tempbkpf

   where mandt in client
    and bukrs = tempbkpf-bukrs
    and gjahr = tempbkpf-gjahr
    and belnr = tempbkpf-belnr .

  sort tempbseg .

endform.                    " SELECT_BSEG

*&---------------------------------------------------------------------*
*&      Form  POULATE_JOIN
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form poulate_join.
  loop at tempbseg .


    read table tempbkpf with key mandt = tempbseg-mandt
                                 bukrs = tempbseg-bukrs
                                 belnr = tempbseg-belnr
                                 gjahr = tempbseg-gjahr binary search .
    if sy-subrc = 0 .

      move-corresponding tempbkpf to join.

      join-buzei = tempbseg-buzei.
      join-shkzg = tempbseg-shkzg.
      join-dmbtr = tempbseg-dmbtr.
      join-sgtxt = tempbseg-sgtxt.
      join-kostl = tempbseg-kostl.
      join-hkont = tempbseg-hkont.

      append join .
    endif.                                                              .
  endloop.

endform.                    " POULATE_JOIN

*&---------------------------------------------------------------------*
*&      Form  WRITE_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form write_data.
************************************************************************
*
*     WRITE CONTENTS OF TABLE JOIN TO THE SCREEN
*
************************************************************************

  write: 'Join Results'.

  loop at join.
    write: / join-mandt, join-bukrs, join-belnr,
             join-dmbtr, join-blart, join-bldat,
             join-budat, join-monat, join-usnam,
             join-bktxt, join-waers, join-kursf,
             join-buzei, join-shkzg, join-gjahr,
             join-sgtxt, join-kostl, join-hkont.
  endloop.


endform.                    " WRITE_DATA

*&---------------------------------------------------------------------*
*&      Form  EXPORT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form export_data.
*send internal tables to the database memory .

  export tempskat to  database indx(st) id skatkey.
  if sy-subrc = 0 .
    write :/ 'SKAT data sucessfully exported' .
  endif.

  export tempbkpf to  database indx(st) id bkpfkey.
  if sy-subrc = 0 .
    write :/ 'BKPF data sucessfully exported' .
  endif.
  export tempbseg to  database indx(st) id bsegkey.
  if sy-subrc = 0 .
    write :/ 'BSEG data sucessfully exported' .
  endif.
  export join to  database indx(st) id joinkey.
  if sy-subrc = 0 .
    write :/ 'JOIN data sucessfully exported' .
  endif.



endform.                    " EXPORT_DATA

*&---------------------------------------------------------------------*
*&      Form  ONLINE_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form online_data.
  perform skat_excel_download .
  perform bkpf_excel_download .
  perform bseg_excel_download .
  perform join_excel_download .

endform.                    " ONLINE_DATA

*&---------------------------------------------------------------------*
*&      Form  IMPORT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form import_data.
*import tables from the database  and down load to excel .

  import  tempskat from  database indx(st) id skatkey.
  if sy-subrc = 0 .
    delete from database indx(st) id skatkey.
  endif.

  import  tempbkpf from  database indx(st) id bkpfkey.
  if sy-subrc = 0 .
    delete from database indx(st) id bkpfkey.
  endif.

  import  tempbseg from  database indx(st) id bsegkey.
  if sy-subrc = 0 .
    delete from database indx(st) id bsegkey.
  endif.

  import  join from  database indx(st) id joinkey.
  if sy-subrc = 0 .
    delete from database indx(st) id joinkey.
  endif.

  perform skat_excel_download .
  perform bkpf_excel_download .
  perform bseg_excel_download .
  perform join_excel_download .





endform.                    " IMPORT_DATA
*&---------------------------------------------------------------------*
*&      Form  SKAT_EXCEL_DOWNLOAD
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_ENDFORM  text
*----------------------------------------------------------------------*
form skat_excel_download  .
***********************************
**************************************
*
*  DOWNLOAD RECORDS FROM INTERNAL TABLES INTO A FLAT FILE.
*
***********************************
**************************************

  call function 'WS_DOWNLOAD'
    exporting
      filename            = skatfile
      filetype            = 'DAT'
    tables
      data_tab            = tempskat
    exceptions
      file_open_error     = 1
      file_write_error    = 2
      invalid_filesize    = 3
      invalid_table_width = 4
      invalid_type        = 5
      no_batch            = 6
      unknown_error       = 7
      others              = 8.

**********************************************


endform.                    " SKAT_EXCEL_DOWNLOAD

*&---------------------------------------------------------------------*
*&      Form  BKPF_EXCEL_DOWNLOAD
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form bkpf_excel_download.
*********************************************
  call function 'WS_DOWNLOAD'
    exporting
      filename            = bkpffile
      filetype            = 'DAT'
    tables
      data_tab            = tempbkpf
    exceptions
      file_open_error     = 1
      file_write_error    = 2
      invalid_filesize    = 3
      invalid_table_width = 4
      invalid_type        = 5
      no_batch            = 6
      unknown_error       = 7
      others              = 8.

***********************************************************************

endform.                    " BKPF_EXCEL_DOWNLOAD

*&---------------------------------------------------------------------*
*&      Form  BSEG_EXCEL_DOWNLOAD
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form bseg_excel_download.
********************************************
  call function 'WS_DOWNLOAD'
    exporting
      filename            = bsegfile
      filetype            = 'DAT'
    tables
      data_tab            = tempbseg
    exceptions
      file_open_error     = 1
      file_write_error    = 2
      invalid_filesize    = 3
      invalid_table_width = 4
      invalid_type        = 5
      no_batch            = 6
      unknown_error       = 7
      others              = 8.

***********************************************************************

endform.                    " BSEG_EXCEL_DOWNLOAD

*&---------------------------------------------------------------------*
*&      Form  JOIN_EXCEL_DOWNLOAD
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
form join_excel_download.
*********************************************

  call function 'WS_DOWNLOAD'
    exporting
      filename            = joinfile
      filetype            = 'DAT'
    tables
      data_tab            = join
    exceptions
      file_open_error     = 1
      file_write_error    = 2
      invalid_filesize    = 3
      invalid_table_width = 4
      invalid_type        = 5
      no_batch            = 6
      unknown_error       = 7
      others              = 8.


endform.                    " JOIN_EXCEL_DOWNLOAD
************************************************************************
*
*                   END PROGRAM
*
************************************************************************
