*&---------------------------------------------------------------------*
*& Report  YSE_ACT_CONTRACT_ITM_OVHL_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Contract Item Overhaul data extraction                       *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 23.11.2012
*  Change Request Number :
*  Transport request Nr. : CD1K974018
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 02.07.2013 |  Geert Rutten     | CD1K976772  |             *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_act_contract_itm_ovhl_extr.

TABLES: vbak,
        vbap,
        veda,
        vbup,
        mpos,
        mpla,
        jest,
        plwp,
        t351x.

* Types
TYPES: BEGIN OF ty_vbap,
         vbeln      TYPE vbeln_va,
         posnr      TYPE posnr_va,
         auart      TYPE auart,
         vkuegru    TYPE vkgru_veda,
         veindat    TYPE vedat_veda,
      END OF ty_vbap.

TYPES: BEGIN OF ty_mpos,
         kdauf      TYPE vbeln_va,
         kdpos      TYPE posnr_va,
         warpl      TYPE warpl,
         wapos      TYPE wapos,
         equnr      TYPE equnr,
         wstra      TYPE strat,
         plnty      TYPE plnty,
         plnnr      TYPE plnnr,
         plnal      TYPE plnal,
      END OF ty_mpos.

TYPES: BEGIN OF ty_mpla,
         warpl      TYPE warpl,
       END OF ty_mpla.

TYPES: BEGIN OF ty_plwp,
         plnty      TYPE plnty,
         plnnr      TYPE plnnr,
         plnal      TYPE plnal,
         plnfl      TYPE plnfolge,
         plnkn      TYPE plnkn,
         paket      TYPE paketzaehl,
      END OF ty_plwp.

TYPES: BEGIN OF ty_t351x,
         strat      TYPE strat,
         paket      TYPE paketzaehl,
         kzyk1      TYPE ktextzyk,
      END OF ty_t351x.

* Internal tables
DATA: gt_vbap  TYPE STANDARD TABLE OF ty_vbap
                    WITH HEADER LINE.

DATA: gt_mpos  TYPE SORTED TABLE OF ty_mpos
                    WITH NON-UNIQUE KEY kdauf kdpos
                    WITH HEADER LINE.

DATA: gt_mpla  TYPE HASHED TABLE OF ty_mpla
                    WITH UNIQUE KEY warpl
                    WITH HEADER LINE,
      gt_mplai TYPE TABLE OF ty_mpla
                    WITH HEADER LINE.

DATA: gt_plwp  TYPE HASHED TABLE OF ty_plwp
                    WITH UNIQUE KEY plnty plnnr plnal
                    WITH HEADER LINE,
      gt_plwpi TYPE TABLE OF ty_plwp
                    WITH HEADER LINE.

DATA: gt_t351x TYPE HASHED TABLE OF ty_t351x
                    WITH UNIQUE KEY strat paket
                    WITH HEADER LINE.

DATA: BEGIN OF gt_out  OCCURS 0,
        vbeln       TYPE vbeln_va,
        tab01(1)    TYPE c,
        posnr       TYPE posnr_va,
        tab02(1)    TYPE c,
        auart       TYPE auart,
        tab03(1)    TYPE c,
        warpl       TYPE warpl,
        tab04(1)    TYPE c,
        wapos       TYPE wapos,
        tab05(1)    TYPE c,
        equnr       TYPE equnr,
        tab06(1)    TYPE c,
        wstra       TYPE strat,
        tab07(1)    TYPE c,
        paket       TYPE paketzaehl,
        tab08(1)    TYPE c,
        kzyk1       TYPE ktextzyk,
        tab09(1)    TYPE c,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        vbeln       TYPE fieldname,
        tab01(1)    TYPE c,
        posnr       TYPE fieldname,
        tab02(1)    TYPE c,
        auart       TYPE fieldname,
        tab03(1)    TYPE c,
        warpl       TYPE fieldname,
        tab04(1)    TYPE c,
        wapos       TYPE fieldname,
        tab05(1)    TYPE c,
        equnr       TYPE fieldname,
        tab06(1)    TYPE c,
        wstra       TYPE fieldname,
        tab07(1)    TYPE c,
        paket       TYPE fieldname,
        tab08(1)    TYPE c,
        kzyk1       TYPE fieldname,
        tab09(1)    TYPE c,
      END OF gt_outh.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_ersda(14)     TYPE c,                      "YYYYMMDDHHMMSS
      gv_error         TYPE xfeld.

*CONSTANTS: gc_stat_dl  TYPE j_status  VALUE 'I0076'.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
*PARAMETERS: p_vkorg  LIKE vbak-vkorg  OBLIGATORY  MEMORY ID vko.
PARAMETERS: p_bukrs  TYPE aufk-bukrs OBLIGATORY MEMORY ID buk,
            p_vkorg  LIKE vbak-vkorg MEMORY ID vko.
* Begin of delete MOD-001
*            p_auart  TYPE auart  OBLIGATORY  DEFAULT 'ZSM1'.
* End of delete MOD-001
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_vkbur  FOR vbak-vkbur,
                s_vkgrp  FOR vbak-vkgrp,
                s_vbdat  FOR veda-vbegdat,
                s_vndat  FOR veda-venddat,
*               s_auart  FOR vbak-auart,
* Begin of insert MOD-001
                s_auart  FOR vbak-auart,
* End of insert MOD-001
                s_vbeln  FOR vbak-vbeln,
                s_abgru  FOR vbap-abgru,
                s_gbsta  FOR vbup-gbsta,
                s_fksaa  FOR vbup-fksaa,
                s_kzyk1  FOR t351x-kzyk1,
* Begin of insert MOD-001
                s_wstra  FOR mpos-wstra.
* End of insert MOD-001
SELECTION-SCREEN SKIP.
PARAMETERS: p_canc_r   AS CHECKBOX,
            p_canc_d   AS CHECKBOX,
            p_mpl      AS CHECKBOX,
            p_mpl_a    AS CHECKBOX.
SELECTION-SCREEN  END OF BLOCK input.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Timestamp
  CONCATENATE sy-datum sy-uzeit INTO gv_ersda.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_vbap[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No contracts selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

  DATA: lv_where TYPE string.
  lv_where = 'tvko~bukrs EQ p_bukrs'.
  IF p_vkorg IS NOT INITIAL.
    CONCATENATE lv_where 'AND h~vkorg = p_vkorg' INTO lv_where SEPARATED BY space.
  ENDIF.
  CONCATENATE lv_where 'AND h~vkbur IN s_vkbur'
                       'AND h~vkgrp IN s_vkgrp'
                       'AND c~vbegdat IN s_vbdat'
                       'AND c~venddat IN s_vndat'
                       'AND h~vbeln IN s_vbeln'
                       'AND i~abgru IN s_abgru'
                       'AND s~gbsta IN s_gbsta'
                       'AND s~fksaa IN s_fksaa'
                       'AND h~auart IN s_auart'
                       'AND h~vbtyp =  ''G'''
                       'AND c~vposn = 0'
    INTO lv_where SEPARATED BY space.

* Contract items
*  SELECT i~vbeln i~posnr h~auart c~vkuegru c~veindat
*         INTO TABLE gt_vbap
*         FROM vbap AS i
*         INNER JOIN vbak AS h
*                    ON h~vbeln = i~vbeln
*         INNER JOIN veda AS c
*                    ON c~vbeln = i~vbeln
*         INNER JOIN vbup AS s
*                    ON s~vbeln = i~vbeln  AND
*                       s~posnr = i~posnr
*         WHERE h~vkorg   =  p_vkorg
** Begin of delete MOD-001
**           AND h~auart   =  p_auart
** End of delete MOD-001
*           AND h~vkbur   IN s_vkbur
*           AND h~vkgrp   IN s_vkgrp
*           AND c~vbegdat IN s_vbdat
*           AND c~venddat IN s_vndat
**           AND h~auart   IN s_auart
*           AND h~vbeln   IN s_vbeln
*           AND i~abgru   IN s_abgru
*           AND s~gbsta   IN s_gbsta
*           AND s~fksaa   IN s_fksaa
** Begin of insert MOD-001
*           AND h~auart   IN s_auart
** End of insert MOD-001
*           AND h~vbtyp   =  'G'
*           AND c~vposn   = 0.
  SELECT i~vbeln i~posnr h~auart c~vkuegru c~veindat
         INTO TABLE gt_vbap
         FROM vbap AS i
         INNER JOIN vbak AS h
                    ON h~vbeln = i~vbeln
         INNER JOIN veda AS c
                    ON c~vbeln = i~vbeln
         INNER JOIN vbup AS s
                    ON s~vbeln = i~vbeln  AND
                       s~posnr = i~posnr
         INNER JOIN tvko AS tvko
           ON tvko~vkorg EQ h~vkorg
         WHERE (lv_where).
  CHECK NOT gt_vbap[] IS INITIAL.

* Check cancellation
  IF NOT p_canc_r IS INITIAL.
    DELETE gt_vbap WHERE NOT vkuegru IS INITIAL.
  ENDIF.
  IF NOT p_canc_d IS INITIAL.
    DELETE gt_vbap WHERE NOT veindat IS INITIAL.
  ENDIF.

  SORT gt_vbap BY vbeln posnr.

* Maintenance Items
  SELECT kdauf kdpos warpl wapos equnr
         wstra plnty plnnr plnal
         INTO TABLE gt_mpos
         FROM mpos
         FOR ALL ENTRIES IN gt_vbap
         WHERE
* Begin of insert MOD-001
           wstra IN s_wstra AND
* End of insert MOD-001
           kdauf = gt_vbap-vbeln
           AND kdpos = gt_vbap-posnr
           AND inact = ' '.

* Check Maintenance Plans exist
  IF NOT p_mpl IS INITIAL.
    DELETE gt_mpos WHERE warpl IS INITIAL.
  ENDIF.
  CHECK NOT gt_mpos[] IS INITIAL.

* Check Maintenance Plans active
  IF NOT p_mpl_a IS INITIAL.
    SELECT m~warpl
           INTO TABLE gt_mplai
           FROM mpla as m
           INNER JOIN jest as s
                      ON s~objnr = m~objnr
           FOR ALL ENTRIES IN gt_mpos
           WHERE m~warpl = gt_mpos-warpl
             AND ( s~stat = 'I0076'  OR
                   s~stat = 'I0320' ).
    SORT gt_mplai BY warpl.
    DELETE ADJACENT DUPLICATES FROM gt_mplai
                                    COMPARING warpl.
    gt_mpla[] = gt_mplai[].
    FREE gt_mplai.
    LOOP AT gt_mpos.
      READ TABLE gt_mpla WITH TABLE KEY warpl = gt_mpos-warpl.
      IF sy-subrc = 0.
        DELETE gt_mpos.
      ENDIF.
    ENDLOOP.
  ENDIF.

* Allocation of maintenance packages to task list operations
  SELECT plnty plnnr plnal
         plnfl plnkn paket
         INTO TABLE gt_plwpi
         FROM plwp
         FOR ALL ENTRIES IN gt_mpos
         WHERE plnty = gt_mpos-plnty
           AND plnnr = gt_mpos-plnnr
           AND plnal = gt_mpos-plnal
           AND loekz = ' '.
  SORT gt_plwpi BY plnty plnnr plnal
                   plnfl DESCENDING plnkn DESCENDING.
  DELETE ADJACENT DUPLICATES FROM gt_plwpi
                                  COMPARING plnty plnnr plnal.
  gt_plwp[] = gt_plwpi[].
  FREE gt_plwpi.

* Maintenance strategy package texts
  SELECT strat paket kzyk1
         INTO TABLE gt_t351x
         FROM t351x
         WHERE spras = 'E'.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_vbap.

*   Maintenance Items
    LOOP AT gt_mpos WHERE kdauf = gt_vbap-vbeln
                      AND kdpos = gt_vbap-posnr.

      CLEAR: gt_out.
      MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03,
                   gt_out-tab04, gt_out-tab05, gt_out-tab06,
                   gt_out-tab07, gt_out-tab08, gt_out-tab09.

      gt_out-vbeln = gt_vbap-vbeln.
      gt_out-posnr = gt_vbap-posnr.
      gt_out-auart = gt_vbap-auart.

      MOVE-CORRESPONDING gt_mpos TO gt_out.

*     Allocation of maintenance packages to task list operations
      CLEAR gt_plwp.
      READ TABLE gt_plwp WITH TABLE KEY plnty = gt_mpos-plnty
                                        plnnr = gt_mpos-plnnr
                                        plnal = gt_mpos-plnal.
      CHECK sy-subrc = 0.
*     Maintenance strategy package
      CLEAR gt_t351x.
      READ TABLE gt_t351x WITH TABLE KEY strat = gt_mpos-wstra
                                         paket = gt_plwp-paket.
      CHECK sy-subrc = 0.
      CHECK gt_t351x-kzyk1 IN s_kzyk1.

      gt_out-paket = gt_plwp-paket.
      gt_out-kzyk1 = gt_t351x-kzyk1.

      APPEND gt_out.

    ENDLOOP.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

* Open file
*  CONCATENATE 'CONTRACT_ITM_OVHL' p_vkorg gv_ersda
  CONCATENATE 'CONTRACT_ITM_OVHL' p_bukrs
             INTO gv_ofile SEPARATED BY '_'.
  IF p_vkorg IS NOT INITIAL.
    CONCATENATE gv_ofile p_vkorg
               INTO gv_ofile SEPARATED BY '_'.
  ENDIF.
  CONCATENATE gv_ofile gv_ersda
             INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM fill_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

* Fill output
  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  FILL_HEADER
*&---------------------------------------------------------------------*
*       Fill header
*----------------------------------------------------------------------*
FORM fill_header .

  CLEAR: gt_outh.
  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03,
               gt_outh-tab04, gt_outh-tab05, gt_outh-tab06,
               gt_outh-tab07, gt_outh-tab08, gt_outh-tab09.

  gt_outh-vbeln  = 'VBELN'.
  gt_outh-posnr  = 'POSNR'.
  gt_outh-auart  = 'AUART'.
  gt_outh-warpl  = 'WARPL'.
  gt_outh-wapos  = 'WAPOS'.
  gt_outh-equnr  = 'EQUNR'.
  gt_outh-wstra  = 'STRAT'.
  gt_outh-paket  = 'PAKET'.
  gt_outh-kzyk1  = 'KZYK1'.
  APPEND gt_outh.

  gt_outh-vbeln  = 'Contract'.
  gt_outh-posnr  = 'Item'.
  gt_outh-auart  = 'Type'.
  gt_outh-warpl  = 'Service Plan'.
  gt_outh-wapos  = 'SP Item'.
  gt_outh-equnr  = 'Equipment'.
  gt_outh-wstra  = 'Strategy'.
  gt_outh-paket  = 'Package'.
  gt_outh-kzyk1  = 'Ov. Pack.'.
  APPEND gt_outh.

ENDFORM.                    " FILL_HEADER

*Text symbol text£º
*E01:No contracts selected
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_BUKRS:D       .
*P_CANC_D:        Check cancellation receipt
*P_CANC_R:        Check cancellation reason
*P_MPL:        Check Maintenance Plan exist
*P_MPL_A:        Check Maintenance Plan active
*P_VKORG:D       .
*S_ABGRU:D       .
*S_AUART:D       .
*S_FKSAA:D       .
*S_GBSTA:D       .
*S_KZYK1:        Overhaul package name
*S_VBDAT:D       .
*S_VBELN:D       .
*S_VKBUR:D       .
*S_VKGRP:D       .
*S_VNDAT:D       .
*S_WSTRA:        Maintenance Strategy
