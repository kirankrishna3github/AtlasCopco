*&---------------------------------------------------------------------*
*& Report  YSE_PULL_AC_CONNECT_SUP
*&
*&---------------------------------------------------------------------*
*&
*&
*&---------------------------------------------------------------------*

REPORT  YSE_PULL_AC_CONNECT_SUP MESSAGE-ID yam_inf.

TABLES : yse_acc_files,  " YSE : AC Connect files per message type
         kotd002,        " SOrg/DistrCh/Material Entered
         kondd,          " Material Substitution - Data Division
         konddp,         " Conditions: Additional Materials for
                         " Material Determination
         mvke.           " Sales Data for Material

DATA: g_retcd         LIKE sy-subrc,
      g_pfile         LIKE /sapdmc/lsoinp-filename,
      g_ofile         LIKE /sapdmc/lsoinp-filename,
      g_ersda(12)     TYPE c,
      g_date          LIKE sy-datum,
      gv_adrnr        TYPE adrnr,
      gv_pos          TYPE i,
      g_directory(25) TYPE c VALUE '/var/load/xxx/UK/convert/',
      p_logsys        LIKE tbdlst-logsys.
DATA: wa_edidc    LIKE edidc.
DATA: wa_adrc TYPE adrc.

DATA: i_edidc_control_comm LIKE edidc OCCURS 1 WITH HEADER LINE ,
      i_edidd_data LIKE edidd OCCURS 0 WITH HEADER LINE .
DATA: created_idocs LIKE sy-tabix.

DATA: BEGIN OF it_acc OCCURS 0,
        vkorg TYPE vkorg,
        vtweg TYPE vtweg,
        matwa TYPE matwa,
        knumh TYPE knumh,
        datab TYPE kodatab,
        datbi TYPE kodatbi,
        msgfn TYPE msgfn,
        data(500) TYPE c,
      END OF it_acc.

DATA: BEGIN OF it_acc_delta OCCURS 0,
        vkorg TYPE vkorg,
        vtweg TYPE vtweg,
        matwa TYPE matwa,
        knumh TYPE knumh,
        datab TYPE kodatab,
        datbi TYPE kodatbi,
        msgfn TYPE msgfn,
        data(500) TYPE c,
 END OF it_acc_delta.

DATA : BEGIN OF i_prev OCCURS 0,
       vkorg TYPE vkorg,
       vtweg TYPE vtweg,
       matwa TYPE matwa,
       knumh TYPE knumh,
       datab TYPE kodatab,
       datbi TYPE kodatbi,
       msgfn TYPE msgfn,
       data(500) TYPE c,
END OF i_prev.

DATA : BEGIN OF it_smatn OCCURS 0,
        smatn TYPE smatn,
END OF it_smatn.

TYPES: BEGIN OF t_sup.
        INCLUDE STRUCTURE yse_e1_acc_sup.
TYPES: END OF t_sup.
DATA: wa_sup TYPE t_sup.

TYPES: BEGIN OF t_supn.
        INCLUDE STRUCTURE yse_e1_acc_sup_nmat.
TYPES: END OF t_supn.
DATA: wa_supn TYPE t_supn.

DATA : BEGIN OF wa_yse_acc_files,
         mandt LIKE yse_acc_files-mandt,
         msgtyp LIKE yse_acc_files-msgtyp,
         vkorg LIKE yse_acc_files-vkorg,
         ersda LIKE yse_acc_files-ersda,
       END OF wa_yse_acc_files.

DATA : BEGIN OF i_delfiles OCCURS 0,
         mandt LIKE yse_acc_files-mandt,
         msgtyp LIKE yse_acc_files-msgtyp,
         vkorg LIKE yse_acc_files-vkorg,
         ersda LIKE yse_acc_files-ersda,
       END OF i_delfiles.

DATA: lv_index TYPE sy-tabix.

CONSTANTS : c_z002            TYPE kschl VALUE 'Z002',
            c_v(1)            TYPE c     VALUE 'V',
            c_input           TYPE c     VALUE '0',
            c_ls(2)           TYPE c     VALUE 'LS',
            c_mestyp_sup      LIKE edidc-mestyp VALUE 'YSE_ACC_SUP',
            c_idoc_type_sup   LIKE edidc-idoctp VALUE 'YSE_ACC_SUP',
            c_segment_sup(14) TYPE c            VALUE 'YSE_E1_ACC_SUP',
            c_segment_sup_nmat(19) TYPE c            VALUE
'YSE_E1_ACC_SUP_NMAT',
            c_a(1)            TYPE c     VALUE 'A',
            c_c(1)            TYPE c     VALUE 'C',    " Change
            c_d(1)            TYPE c     VALUE 'D',    " Delete
            c_x(1)            TYPE c     VALUE 'X',
            c_0000(4)         TYPE c     VALUE '0000',
            c_underscore(1)   TYPE c     VALUE '_',
            c_sc1(3)          TYPE c     VALUE 'SC1'.

* Selection
PARAMETERS: p_mess LIKE tbdme-mestyp OBLIGATORY DEFAULT 'YSE_ACC_SUP'
              MODIF ID sc1.

SELECTION-SCREEN: BEGIN OF BLOCK block1 WITH FRAME TITLE text-001.
PARAMETERS:     p_init TYPE xfeld,
                p_inorg  LIKE knvv-vkorg.
SELECT-OPTIONS: s_vkorg FOR kotd002-vkorg OBLIGATORY NO INTERVALS,
                s_vtweg FOR kotd002-vtweg,
                s_matwa FOR kotd002-matwa,
                s_sugrd FOR kondd-sugrd.
SELECTION-SCREEN SKIP.
PARAMETERS:     p_date  TYPE sy-datum DEFAULT sy-datum OBLIGATORY.

SELECTION-SCREEN: END OF BLOCK block1.

************************************************************************
*    Disable Message Type Screen Field                                 *
************************************************************************
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-group1 = c_sc1.
      screen-input   = c_input.
      MODIFY SCREEN.
      CONTINUE.
    ENDIF.
  ENDLOOP.

************************************************************************
*    Check Selection                                                   *
************************************************************************
AT SELECTION-SCREEN.

  IF p_date IS INITIAL.
    MESSAGE e000(yse_general) WITH text-e07.
  ENDIF.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = p_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Get the information for selected plant(s)
  LOOP AT s_vkorg.

    PERFORM get_detail USING s_vkorg-low.

*   Create delta with new creations/updates by comparing the selected
*   data with this from the previous run. When a new sales organization
*   has to be loaded, no comparison has to take place.
    IF NOT it_acc[] IS INITIAL.
      IF p_init IS INITIAL.
        IF s_vkorg-low NE p_inorg.
          CLEAR: g_retcd.
          PERFORM get_previous_file USING p_mess s_vkorg-low
                                 CHANGING g_retcd.
          IF g_retcd = 0.
            PERFORM create_delta.
          ELSE.
            CONTINUE.                      " Pass to next S.O.
          ENDIF.
        ENDIF.
      ELSE.
        IF NOT p_inorg IS INITIAL.
          LOOP AT it_acc.
            MOVE-CORRESPONDING it_acc TO it_acc_delta.
            MOVE c_a TO it_acc_delta-msgfn.
            APPEND it_acc_delta.
            CLEAR it_acc_delta.
          ENDLOOP.
        ELSE.
          CLEAR it_acc.
          REFRESH it_acc.
        ENDIF.
      ENDIF.

*     Write outputfile to application server
      CLEAR g_retcd.

      CONCATENATE sy-datum sy-uzeit(4) INTO g_ersda.
      CONCATENATE p_mess s_vkorg-low g_ersda
                INTO g_ofile SEPARATED BY c_underscore.

      REPLACE 'xxx' IN g_directory WITH p_logsys(3).
      CONCATENATE g_directory g_ofile INTO g_ofile.

      IF NOT it_acc[] IS INITIAL.
        PERFORM write_outputfile.
        IF g_retcd IS INITIAL.
* Update custom table YSE_ACC_FILES
          PERFORM update_custom_table USING s_vkorg-low.
* Delete older entries in custom table YSE_ACC_FILES
          PERFORM delete_old_table_entries USING s_vkorg-low.
* Delete older files on appl.server
          IF NOT i_delfiles[] IS INITIAL.
            PERFORM delete_old_files.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.

  ENDLOOP.

************************************************************************
*       E N D - O F - S E L E C T I O N    E V E N T                   *
************************************************************************
END-OF-SELECTION .

  IF it_acc_delta[] IS INITIAL .
    WRITE: / 'No Idocs created'.
  ELSE.
*   Generate Idoc's
    PERFORM create_idocs_cust USING p_mess.
    WRITE : /   created_idocs , ' Idocs created'.
  ENDIF.
  WRITE: / 'Job finished'.

************************************************************************
*--- S U B R O U T I N E S --------------------------------------------*
************************************************************************
*&---------------------------------------------------------------------*
*&      Form  Validate_ALE_Configuration
*&---------------------------------------------------------------------*
FORM validate_ale_configuration .

  DATA: l_create_idoc     TYPE     c .

* CHECK IF IDOC CONFIGURATION IS READY AND IDOC CAN BE PROCESSED.
  CALL FUNCTION 'ALE_MODEL_DETERMINE_IF_TO_SEND'
    EXPORTING
      message_type           = p_mess
    IMPORTING
      idoc_must_be_sent      = l_create_idoc
    EXCEPTIONS
      own_system_not_defined = 1
      OTHERS                 = 2.

  IF sy-subrc <> 0.
    MESSAGE e029 WITH p_mess.
    EXIT.
  ENDIF.

ENDFORM.                    "validate_ale_configuration

*&---------------------------------------------------------------------*
*&      Form  Get_Detail
*&---------------------------------------------------------------------*
*       To get the information for selected sales organizations
*----------------------------------------------------------------------*
FORM get_detail USING p_vkorg.

  CLEAR: it_acc.
  REFRESH: it_acc.
  IF NOT p_date IS INITIAL.
    SELECT vkorg vtweg matwa knumh datbi datab INTO
      CORRESPONDING FIELDS OF TABLE it_acc
           FROM kotd002
           WHERE kappl EQ c_v
             AND kschl EQ c_z002
             AND vkorg  = s_vkorg-low
             AND vtweg IN s_vtweg
             AND matwa IN s_matwa
             AND datab LE p_date
             AND datbi GE p_date.
  ENDIF.

* Process Error - No data found for the Selected S.O.
  IF sy-subrc NE 0 .
    WRITE: text-e01, p_vkorg.
    EXIT.
  ENDIF .

  LOOP at it_acc.

  lv_index = sy-tabix.

  SELECT single *
         FROM mvke
         WHERE matnr IN s_matwa
           AND vkorg = s_vkorg-low
           AND vtweg IN s_vtweg
           AND prat2 EQ 'X'.
  IF sy-subrc <> 0.
    DELETE it_acc INDEX lv_index.
  ENDIF.

  ENDLOOP.

  SORT it_acc.
  DELETE ADJACENT DUPLICATES FROM it_acc.

  IF NOT it_acc[] IS INITIAL.
    LOOP AT it_acc.
      MOVE-CORRESPONDING it_acc TO wa_sup.
      REFRESH it_smatn.
      CLEAR: it_smatn, wa_supn-smatn, wa_sup-sugrd.
      gv_pos = 44.
* get data from KONDD
      SELECT SINGLE smatn sugrd INTO (wa_supn-smatn, wa_sup-sugrd)
               FROM kondd
               WHERE knumh = it_acc-knumh
                AND smatn <> it_acc-matwa.
* get data from KONDDP
      SELECT smatn INTO CORRESPONDING FIELDS OF TABLE it_smatn
        FROM konddp
        WHERE knumh = it_acc-knumh
          AND smatn <> it_acc-matwa.

      IF NOT wa_supn-smatn IS INITIAL.
        it_smatn-smatn = wa_supn-smatn.
        APPEND it_smatn.
        CLEAR it_smatn.
      ENDIF.
* get old material number for old and new material
      SELECT SINGLE bismt INTO wa_sup-matwa
         FROM mara WHERE matnr = wa_sup-matwa.
      LOOP AT it_smatn.
        SELECT SINGLE bismt INTO wa_supn-smatn
            FROM mara WHERE matnr = it_smatn-smatn.

        IF it_acc-data IS INITIAL.
          CONCATENATE wa_sup wa_supn-smatn INTO it_acc-data.
        ELSE.
          gv_pos = gv_pos + 18.
          it_acc-data+gv_pos(18) = wa_supn-smatn.
        ENDIF.
      ENDLOOP.
      IF wa_supn-smatn IS INITIAL AND it_smatn[] IS INITIAL.
        DELETE it_acc.
      ELSE.
        MODIFY it_acc.
      ENDIF.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " Get_Detail

*&---------------------------------------------------------------------*
*&      Form  Create_delta
*&---------------------------------------------------------------------*
*       Create internal table with creates/updates/deletes
*----------------------------------------------------------------------*
FORM create_delta.

* Creates/Updates
  LOOP AT it_acc.

    READ TABLE i_prev WITH KEY vkorg = it_acc-vkorg
                               vtweg = it_acc-vtweg
                               matwa = it_acc-matwa
                    BINARY SEARCH.

    IF sy-subrc = 0.
      IF it_acc(153) <> i_prev(153).
        MOVE-CORRESPONDING it_acc TO it_acc_delta.
        MOVE c_c TO it_acc_delta-msgfn.
        APPEND it_acc_delta.
        CLEAR it_acc_delta.
      ENDIF.
    ELSE.
      MOVE-CORRESPONDING it_acc TO it_acc_delta.
      MOVE c_a TO it_acc_delta-msgfn.
      APPEND it_acc_delta.
      CLEAR it_acc_delta.
    ENDIF.

  ENDLOOP.

* Deletes
  IF NOT it_acc[] IS INITIAL.
    CLEAR i_prev.
    LOOP AT i_prev.
      READ TABLE it_acc WITH KEY vkorg = i_prev-vkorg
                                 vtweg = i_prev-vtweg
                                 matwa = i_prev-matwa
                      BINARY SEARCH.
      IF sy-subrc <> 0.
        MOVE-CORRESPONDING i_prev TO it_acc_delta.
        MOVE c_d TO it_acc_delta-msgfn.
        APPEND it_acc_delta.
        CLEAR it_acc_delta.
      ENDIF.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " Create_delta

*&---------------------------------------------------------------------*
*&      Form  Get_previous_file
*&---------------------------------------------------------------------*
*       Get file from previous run
*----------------------------------------------------------------------*
FORM get_previous_file USING p_mess p_vkorg
                    CHANGING p_retcd.

* Prepare filename of previous run
  CLEAR g_ersda.
  SELECT ersda INTO g_ersda
         FROM yse_acc_files
         WHERE msgtyp = p_mess
           AND vkorg = p_vkorg.
  ENDSELECT.

  IF sy-subrc <> 0.
* No filename of previous run available in table YSE_ACC_FILES
    WRITE: / text-e02,p_mess,  p_vkorg.
    p_retcd = 4.
    EXIT.
  ENDIF.

  CONCATENATE p_mess p_vkorg g_ersda
                      INTO g_pfile SEPARATED BY c_underscore.
  REPLACE 'xxx' IN g_directory WITH p_logsys(3).
  CONCATENATE g_directory g_pfile INTO g_pfile.

* FILE READ FROM APPLICATION SERVER
  PERFORM get_from_appl TABLES  i_prev
                        USING   g_pfile
                                p_retcd.

ENDFORM.                    " Get_previous_file

*&---------------------------------------------------------------------*
*&      Form  Get_from_appl
*&---------------------------------------------------------------------*
*       Get the file from application server into internal table
*----------------------------------------------------------------------*
FORM get_from_appl TABLES i_infile STRUCTURE i_prev
                   USING p_infile p_subrc.

  OPEN DATASET p_infile FOR INPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e03, p_infile.
    p_subrc = 4.
    EXIT.
  ENDIF.

  REFRESH i_infile.

  DO.
    READ DATASET p_infile INTO i_infile.
    IF sy-subrc <> 0.
      EXIT.
    ENDIF.
    APPEND i_infile.
  ENDDO.
  CLOSE DATASET p_infile.

ENDFORM.                    "get_from_appl

*&---------------------------------------------------------------------*
*&      Form  Write_outputfile
*&---------------------------------------------------------------------*
*       Write outputfile
*----------------------------------------------------------------------*
FORM write_outputfile.

  OPEN DATASET g_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e04, g_ofile.
    g_retcd = 4.
    EXIT.
  ENDIF.

  LOOP AT it_acc.
    TRANSFER it_acc(153) TO g_ofile.
  ENDLOOP.

ENDFORM.                    " Write_outputfile

*&---------------------------------------------------------------------*
*&      Form  Update_custom_table
*&---------------------------------------------------------------------*
*       Update custom table YSE_ACC_FILES
*----------------------------------------------------------------------*
FORM update_custom_table USING p_vkorg.

  wa_yse_acc_files-mandt = sy-mandt.
  wa_yse_acc_files-msgtyp = p_mess.
  wa_yse_acc_files-vkorg = p_vkorg.
  wa_yse_acc_files-ersda = g_ersda.

  INSERT yse_acc_files FROM wa_yse_acc_files.

ENDFORM.                    " Update_custom_table

*&---------------------------------------------------------------------*
*&      Form  Delete_old_files
*&---------------------------------------------------------------------*
*       Delete files from former runs on application server
*----------------------------------------------------------------------*
FORM delete_old_files.

  DATA: g_dir_name LIKE epsf-epsdirnam,
        g_dfile    LIKE epsf-epsfilnam.

  LOOP AT i_delfiles.

    CONCATENATE p_mess i_delfiles-vkorg i_delfiles-ersda
                         INTO g_dfile SEPARATED BY c_underscore.

    MOVE g_directory TO g_dir_name.
    REPLACE 'xxx' IN g_dir_name WITH p_logsys(3).
    TRANSLATE g_dir_name(10) TO LOWER CASE.
    TRANSLATE g_dir_name+17(8) TO LOWER CASE.

    CALL FUNCTION 'EPS_DELETE_FILE'
      EXPORTING
        file_name              = g_dfile
        dir_name               = g_dir_name
      EXCEPTIONS
        invalid_eps_subdir
        sapgparam_failed
        build_directory_failed
        no_authorization
        build_path_failed
        delete_failed.

    IF sy-subrc <> 0.
      WRITE: / text-e05, g_dfile.      "Could not delete file
    ENDIF.

  ENDLOOP.

ENDFORM.                    "delete_old_files

*&---------------------------------------------------------------------*
*&      Form  Delete_old_table_entries
*&---------------------------------------------------------------------*
*       Delete entries from former runs in custom table YSE_ACC_FILES
*----------------------------------------------------------------------*
FORM delete_old_table_entries USING p_vkorg.

  g_date = sy-datum - 7.
  CONCATENATE g_date c_0000 INTO g_ersda.

  REFRESH i_delfiles.

  SELECT * FROM yse_acc_files
      WHERE msgtyp EQ p_mess
        AND vkorg EQ p_vkorg
        AND ersda LT g_ersda.
    MOVE yse_acc_files TO i_delfiles.
    APPEND i_delfiles.
    CLEAR i_delfiles.
  ENDSELECT.

  IF sy-subrc = 0.
    DELETE FROM yse_acc_files WHERE msgtyp EQ p_mess
                                AND vkorg EQ p_vkorg
                                AND ersda LT g_ersda.

    IF sy-subrc <> 0.
* Could not delete entrie(s) in table YSE_ACC_FILES
      WRITE: / text-e06, p_vkorg.
    ENDIF.

  ENDIF.

ENDFORM.                    "delete_old_table_entries

*&---------------------------------------------------------------------*
*&      Form  create_idocs_cust
*&---------------------------------------------------------------------*
*      -->MESSAGE_TYPE
*----------------------------------------------------------------------*
FORM create_idocs_cust  USING    message_type.

  CLEAR: created_idocs.
* FIND RECEIVING PARTNER
  SELECT SINGLE rcvprn INTO wa_edidc-rcvprn
         FROM edp13
         WHERE mestyp = c_mestyp_sup.
* Polulate Control Record
  wa_edidc-mestyp =  c_mestyp_sup.
  wa_edidc-idoctp =  c_idoc_type_sup.
  wa_edidc-rcvprt =  c_ls.

  LOOP AT it_acc_delta.
    IF NOT it_acc_delta-matwa IS INITIAL.
      CLEAR i_edidd_data[].

      i_edidd_data-segnam  = c_segment_sup.

      SELECT SINGLE famback famback INTO (it_acc-vkorg,
it_acc_delta-data+0(4) )
           FROM yse_sd_acc_fam
           WHERE vkorg = it_acc_delta-vkorg
             AND vtweg = it_acc_delta-vtweg.

      i_edidd_data-sdata   = it_acc_delta-data.
      APPEND i_edidd_data.
      CLEAR i_edidd_data.

      gv_pos = 44.
      WHILE gv_pos < 480.
        IF NOT it_acc_delta-data+gv_pos(18) IS INITIAL.
          i_edidd_data-segnam  = c_segment_sup_nmat.
          i_edidd_data-sdata   = it_acc_delta-data+gv_pos(18).
          APPEND i_edidd_data.
          CLEAR i_edidd_data.
        ENDIF.
        gv_pos = gv_pos + 18.
      ENDWHILE.

      IF NOT i_edidd_data[] IS INITIAL.
        CALL FUNCTION 'MASTER_IDOC_DISTRIBUTE'
          EXPORTING
            master_idoc_control            = wa_edidc
          TABLES
            communication_idoc_control     = i_edidc_control_comm
            master_idoc_data               = i_edidd_data
          EXCEPTIONS
            error_in_idoc_control          = 1
            error_writing_idoc_status      = 2
            error_in_idoc_data             = 3
            sending_logical_system_unknown = 4
            OTHERS                         = 5.

        IF sy-subrc <> 0.

        ELSE.
          created_idocs = created_idocs + 1.
        ENDIF.

        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.

        CALL FUNCTION 'EDI_DOCUMENT_DEQUEUE_LATER'
          EXPORTING
            docnum                 = i_edidc_control_comm-docnum
          EXCEPTIONS
            idoc_is_not_to_dequeue = 1
            OTHERS                 = 2.

      ENDIF.
    ENDIF.
  ENDLOOP.

ENDFORM.                    "create_idocs_cust

*Text symbol text£º
*001:Initial load parameters
*E01:No data found for the selected Sales Organization
*E02:No filename of previous run for Sales Organization
*E03:Could not open input file
*E04:Could not open output file
*E05:Could not delete file
*E06:Could not delete entries in table YSE_ACC_FILES
*E07:Give a valid selection date
*E08:Give only 1 date (from - to)

*E09:Give only 1 date
*Selection text£º
*P_DATE:        Valid on
*P_INIT:        Initial load
*P_INORG:        Sales Org.to be initial loaded
*P_MESS:        Message type
*S_MATWA:        Superseded material number
*S_SUGRD:        Proposed Reason
*S_VKORG:        Sales Organization
*S_VTWEG:        Distribution Channel
