***********************************************************************************
* Program ID        : yrd_upload_po
* Author            : Yang Lei
* Date              : 10/05/2017
***********************************************************************************
* Description       :
***********************************************************************************

REPORT  yrd_upload_po NO STANDARD PAGE HEADING LINE-SIZE 255.


TYPES: BEGIN OF ty_upload,
         v_text(250)  TYPE c,            " FILE UPLOAD TEXT
       END OF ty_upload.
DATA: gt_upload TYPE TABLE OF ty_upload.

TYPES: BEGIN OF ty_item,
          serno TYPE char5,
          bukrs TYPE ekko-bukrs,
          ekorg TYPE ekko-ekorg,
          ekgrp TYPE ekko-ekgrp,
          bsart TYPE ekko-bsart,
          lifnr TYPE ekko-lifnr,
          waers TYPE ekko-waers,
          matnr TYPE ekpo-matnr,
          menge TYPE ekpo-menge,
          netpr TYPE ekpo-netpr,
          werks TYPE ekpo-werks,
          ref1  TYPE char10,
          ref2  TYPE char18,
       END OF ty_item,

       BEGIN OF ty_down2,
         ebeln TYPE ekko-ebeln,
         ebelp TYPE ekpo-ebelp,
         ref1  TYPE char10,
         ref2  TYPE char18,
       END OF ty_down2,

       BEGIN OF ty_err,
         serno TYPE char5,
         msg   TYPE char100,
       END OF ty_err.
DATA: gt_item_list TYPE TABLE OF ty_item.



DATA: gv_filename TYPE string.
CONSTANTS: c_filetype(10)   TYPE c VALUE 'ASC',     " FILE TYPE
           c_blanc          TYPE c VALUE ' ',
    con_tab  TYPE c VALUE cl_abap_char_utilities=>horizontal_tab.


PARAMETERS: p_file TYPE ibipparms-path LOWER CASE.

*.................. Selection screen validations...................... *

AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_file.
  CALL FUNCTION 'F4_FILENAME'
    IMPORTING
      file_name = p_file.


START-OF-SELECTION.
  PERFORM get_item_list.
  PERFORM create_po.

TOP-OF-PAGE.
  WRITE:
    /001 'Serial No.',
     015 'Error message'.

*&---------------------------------------------------------------------*
*&      Form  get_item_list
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM get_item_list .
  DATA: ls_item TYPE ty_item,
        lv_string TYPE string,
        lv_qty(13) TYPE c,
        LV_AMT(11) TYPE C.
  "Upload the item list.
  gv_filename = p_file.
  PERFORM get_from_pres IN PROGRAM yam_common_routines
                                    TABLES  gt_upload
                                    USING   gv_filename
                                            c_filetype
                                            c_blanc.

  LOOP AT gt_upload INTO lv_string.
    SPLIT lv_string AT con_tab
     INTO ls_item-serno
          ls_item-bukrs
          ls_item-ekorg
          ls_item-ekgrp
          ls_item-bsart
          ls_item-lifnr
          ls_item-waers
          ls_item-matnr
          lv_qty
          LV_AMT
          ls_item-werks
          ls_item-ref1
          ls_item-ref2.
    ls_item-menge = lv_qty.
    ls_item-netpr = LV_AMT.

    CALL FUNCTION 'CONVERSION_EXIT_MATN1_INPUT'
      EXPORTING
        input        = ls_item-matnr
      IMPORTING
        output       = ls_item-matnr
      EXCEPTIONS
        length_error = 1
        OTHERS       = 2.

    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        INPUT         = ls_item-lifnr
      IMPORTING
        OUTPUT        = ls_item-lifnr.

    APPEND ls_item TO gt_item_list.
  ENDLOOP.
ENDFORM.                    " get_item_list
*&---------------------------------------------------------------------*
*&      Form  create_po
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM create_po .
  DATA: ls_item  TYPE ty_item,
        ls_temp  TYPE ty_item,
        lv_ebelp TYPE ekpo-ebelp,
        lv_msg   TYPE char100,
        lv_qty(13) TYPE c,
        LV_AMT(11) TYPE C.
  DATA:
    lt_down TYPE STANDARD TABLE OF ty_item,
    lt_down2 TYPE STANDARD TABLE OF ty_down2,
    lt_item2 TYPE STANDARD TABLE OF ty_down2,
    ls_item2 TYPE ty_down2,
    lt_item TYPE STANDARD TABLE OF ty_item,
    ls_poheader  TYPE BAPIMEPOHEADER,
    ls_poheaderx TYPE BAPIMEPOHEADERX,
    lv_po_number TYPE BAPIMEPOHEADER-PO_NUMBER,
    lt_return    TYPE STANDARD TABLE OF BAPIRET2 WITH HEADER LINE,
    lt_poitem    TYPE STANDARD TABLE OF BAPIMEPOITEM WITH HEADER LINE,
    lt_poitemx   TYPE STANDARD TABLE OF BAPIMEPOITEMX WITH HEADER LINE,
    lv_len    TYPE i,
    lv_path   TYPE string,
    lt_string TYPE stringtab,
    ls_string LIKE LINE OF lt_string.

  LOOP AT gt_item_list INTO ls_temp.
    ls_item = ls_temp.
    APPEND ls_item TO lt_item.

    lv_ebelp = lv_ebelp + 1.
    lt_poitem-PO_ITEM   = lv_ebelp.
    lt_poitem-MATERIAL  = ls_item-matnr.
    lt_poitem-PLANT     = ls_item-werks.
    lt_poitem-QUANTITY  = ls_item-menge.
    lt_poitem-NET_PRICE = ls_item-netpr.
    SELECT SINGLE webre
      FROM lfm1
      INTO lt_poitem-GR_BASEDIV
     WHERE lifnr = ls_item-lifnr
       AND ekorg = ls_item-ekorg.
    APPEND lt_poitem.
    lt_poitemx-PO_ITEM   = lv_ebelp.
    lt_poitemx-MATERIAL  = 'X'.
    lt_poitemx-PLANT     = 'X'.
    lt_poitemx-QUANTITY  = 'X'.
    lt_poitemx-NET_PRICE = 'X'.
    lt_poitemx-GR_BASEDIV = 'X'.
    APPEND lt_poitemx.

    ls_item2-ebelp = lv_ebelp.
    ls_item2-ref1 = ls_item-ref1.
    ls_item2-ref2 = ls_item-ref2.
    APPEND ls_item2 TO lt_item2.

    AT END OF serno.
      ls_poheader-COMP_CODE  = ls_item-bukrs.
      ls_poheaderx-COMP_CODE = 'X'.
      ls_poheader-DOC_TYPE   = ls_item-bsart.
      ls_poheaderx-DOC_TYPE  = 'X'.
      ls_poheader-VENDOR     = ls_item-lifnr.
      ls_poheaderx-VENDOR    = 'X'.
      ls_poheader-PURCH_ORG  = ls_item-ekorg.
      ls_poheaderx-PURCH_ORG = 'X'.
      ls_poheader-PUR_GROUP  = ls_item-ekgrp.
      ls_poheaderx-PUR_GROUP = 'X'.
      ls_poheader-CURRENCY   = ls_item-waers.
      ls_poheaderx-CURRENCY  = 'X'.

      CALL FUNCTION 'BAPI_PO_CREATE1'
        EXPORTING
          POHEADER                     = ls_poheader
          POHEADERX                    = ls_poheaderx
        IMPORTING
          EXPPURCHASEORDER             = lv_po_number
        TABLES
          RETURN                       = lt_return[]
          POITEM                       = lt_poitem[]
          POITEMX                      = lt_poitemx[]
                .

      LOOP AT lt_return WHERE TYPE = 'E'.
        MESSAGE ID lt_return-ID
              TYPE lt_return-TYPE
            NUMBER lt_return-NUMBER
              WITH lt_return-MESSAGE_V1
                   lt_return-MESSAGE_V2
                   lt_return-MESSAGE_V3
                   lt_return-MESSAGE_V4
              INTO lv_msg.

        WRITE:
          /001 ls_item-serno,
           015 lv_msg.
      ENDLOOP.
      IF sy-subrc <> 0.
        READ TABLE lt_return WITH KEY TYPE = 'S'.
        MESSAGE ID lt_return-ID
              TYPE lt_return-TYPE
            NUMBER lt_return-NUMBER
              WITH lt_return-MESSAGE_V1
                   lt_return-MESSAGE_V2
                   lt_return-MESSAGE_V3
                   lt_return-MESSAGE_V4
              INTO lv_msg.

        WRITE:
          /001 ls_item-serno,
           015 lv_msg.

        LOOP AT lt_item2 INTO ls_item2.
          ls_item2-ebeln = lv_po_number.
          APPEND ls_item2 TO lt_down2.
        ENDLOOP.

        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'.
      ELSE.
        CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
        APPEND LINES OF lt_item TO lt_down.
      ENDIF.

      CLEAR lt_poitem[].
      CLEAR lt_poitemX[].
      CLEAR lt_return[].
      lv_ebelp = 0.
      CLEAR lt_item.
      CLEAR lt_item2.
    ENDAT.
  ENDLOOP.

  IF lt_down2 IS NOT INITIAL.
    lv_len = strlen( p_file ).
    lv_len = lv_len - 4.
    lv_path = p_file+0(lv_len).

    CONCATENATE lv_path
                '_map.txt'
           INTO lv_path.

    LOOP AT lt_down2 INTO ls_item2.
      CONCATENATE ls_item2-ebeln
                  ls_item2-ebelp
                  ls_item2-ref1
                  ls_item2-ref2
             INTO ls_string
             SEPARATED BY con_tab.
      APPEND ls_string TO lt_string.
    ENDLOOP.

    CALL FUNCTION 'GUI_DOWNLOAD'
      EXPORTING
        filename                        = lv_path
      TABLES
        data_tab                        = lt_string
      EXCEPTIONS
        file_write_error                = 1
        no_batch                        = 2
        gui_refuse_filetransfer         = 3
        invalid_type                    = 4
        no_authority                    = 5
        unknown_error                   = 6
        header_not_allowed              = 7
        separator_not_allowed           = 8
        filesize_not_allowed            = 9
        header_too_long                 = 10
        dp_error_create                 = 11
        dp_error_send                   = 12
        dp_error_write                  = 13
        unknown_dp_error                = 14
        access_denied                   = 15
        dp_out_of_memory                = 16
        disk_full                       = 17
        dp_timeout                      = 18
        file_not_found                  = 19
        dataprovider_exception          = 20
        control_flush_error             = 21
        OTHERS                          = 22
              .
    IF sy-subrc <> 0.
       MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH SY-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.
  ENDIF.

  CHECK lt_down IS NOT INITIAL.

  lv_len = strlen( p_file ).
  lv_len = lv_len - 4.
  lv_path = p_file+0(lv_len).

  CONCATENATE lv_path
              '_err.txt'
         INTO lv_path.

  CLEAR lt_string.
  LOOP AT lt_down INTO ls_item.
    WRITE ls_item-menge TO lv_qty NO-GAP NO-GROUPING NO-ZERO.
    CONDENSE lv_qty.
    WRITE ls_item-netpr TO LV_AMT NO-GAP NO-GROUPING NO-ZERO.
    CONDENSE LV_AMT.
    CONCATENATE ls_item-serno
                ls_item-bukrs
                ls_item-ekorg
                ls_item-ekgrp
                ls_item-bsart
                ls_item-lifnr
                ls_item-waers
                ls_item-matnr
                lv_qty
                LV_AMT
                ls_item-werks
           INTO ls_string
           SEPARATED BY con_tab.
    APPEND ls_string TO lt_string.
  ENDLOOP.

  CALL FUNCTION 'GUI_DOWNLOAD'
    EXPORTING
      filename                        = lv_path
    TABLES
      data_tab                        = lt_string
    EXCEPTIONS
      file_write_error                = 1
      no_batch                        = 2
      gui_refuse_filetransfer         = 3
      invalid_type                    = 4
      no_authority                    = 5
      unknown_error                   = 6
      header_not_allowed              = 7
      separator_not_allowed           = 8
      filesize_not_allowed            = 9
      header_too_long                 = 10
      dp_error_create                 = 11
      dp_error_send                   = 12
      dp_error_write                  = 13
      unknown_dp_error                = 14
      access_denied                   = 15
      dp_out_of_memory                = 16
      disk_full                       = 17
      dp_timeout                      = 18
      file_not_found                  = 19
      dataprovider_exception          = 20
      control_flush_error             = 21
      OTHERS                          = 22
            .
  IF sy-subrc <> 0.
     MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
             WITH SY-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " create_po

*Text symbol text£º
*BK1:Header Information
*E00:Error opening dataset, return code:
*I01:Session name
*I02:Open session
*I03:Insert transaction
*I04:Close Session
*I05:Return code =
*I06:Error session created
*S01:Session name
*S02:User
*S03:Keep session
*S04:Lock date
*S05:Processing Mode
*S06:Update Mode
*S07:Generate session
*S08:Call transaction
*S09:Error sessn
*S10:Nodata indicator

*S11:Short log
*Selection text£º
*P_BSTKD:D       .
*P_FILE:        Upload file
*P_KUNNR1:D       .
*P_KUNNR2:D       .
*P_LINES:        Item lines
*P_SPART:D       .
*P_VKBUR:D       .
*P_VKGRP:D       .
*P_VKORG:D       .
*P_VTWEG:D       .
