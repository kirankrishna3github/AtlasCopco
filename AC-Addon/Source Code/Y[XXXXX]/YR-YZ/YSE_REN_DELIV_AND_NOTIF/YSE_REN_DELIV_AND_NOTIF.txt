***********************************************************************
* Program ID           : YSE_REN_DELIV_AND_NOTIF                      *
* Program Title        : Physical utilization report                  *
* Author               : Erik Walravens                               *
* Date                 : 26.12.2006                                   *
* Development Number   : D010                                         *
* Change Request Number: CD1K908962                                   *
* Description          : This report displays an overview of delivery *
*                        documents and associated notification created*
*                        at creation of the delivery document.        *
***********************************************************************
* Notes:                                                              *
* The serial number and equipment number are fetched from the delivery*
* document. If this equipment does not exist anymore, it is displayed *
* nontheless. The equipment description is fetched while checking the *
* validity date, so for deleted equipment it will not be displayed.   *
* To speed up refresh if necessary, function should be created to     *
* update only the selected lines.                                     *
***********************************************************************
* Change History Log                                                  *
*---------------------------------------------------------------------*
* Mod. no.|  Date    | Name           | Correction Number | Change Ref*
*---------------------------------------------------------------------*
* MOD-001 |06/03/2007| Erik Walravens | CD1K911546        | 001       *
* Description: Change material description to service item material.  *
*---------------------------------------------------------------------*
* MOD-002 |07/03/2007| Erik Walravens | CD1K911815        | 002       *
* Description: Include contract messages into notification text and   *
* all languages.                                                      *
*---------------------------------------------------------------------*
* MOD-003 |08/03/2007| Erik Walravens | CD1K911843        | 003       *
* Description: Fix refresh and particularly notification status.      *
*---------------------------------------------------------------------*
* MOD-004 |08/03/2007| Erik Walravens | CD1K911915        | 004       *
* Description: Fuel Level button added.                               *
*              Customer name added in output.                         *
*              Contract number added in output.                       *
*---------------------------------------------------------------------*
* MOD-005 |09/03/2007| Erik Walravens | CD1K911972        | 005       *
* Description: Fix refresh notification status.                       *
*              Hotspot to contract added.                             *
*---------------------------------------------------------------------*
* MOD-006 |12/03/2007| Erik Walravens | CD1K912100        | 006       *
* Description: Added selection parameters in header.                  *
*              Set item category default values to deliveries.        *
*---------------------------------------------------------------------*
* MOD-007 |19/03/2007| Erik Walravens | CD1K912586        | 007       *
* Description: Optimizations of main selects.                         *
*              Fix selected row bug.                                  *
*---------------------------------------------------------------------*
* MOD-008 |22/03/2007| Erik Walravens | CD1K912852        | 008       *
* Description: Extend item category defaults to ZTOR and ZMIL.        *
*---------------------------------------------------------------------*
* MOD-009 |26/03/2007| Erik Walravens | CD1K913130        | 009       *
* Description: Add column with latest running hours measuring points. *
*---------------------------------------------------------------------*
* MOD-010 |13/04/2007| Pieter Jespers | CD1K913810        | 017       *
* Description: Authorisation check                                    *
*---------------------------------------------------------------------*
* MOD-011 |17/04/2008| Christophe VM  | CD2K940739        | 018       *
* Description: Equnr not in correct loop                              *
***********************************************************************

REPORT yse_ren_deliv_and_notif.

***********************************************************************
* CLASSES                                                             *
***********************************************************************
***********************************************************************
* CLASS lcl_event_handler DEFINITION                                  *
***********************************************************************
CLASS lcl_event_handler DEFINITION.
  PUBLIC SECTION.
    METHODS:
*   Hotspot click control
    handle_hotspot_click
          FOR EVENT hotspot_click OF cl_gui_alv_grid
          IMPORTING e_row_id e_column_id es_row_no.
ENDCLASS.                    "lcl_event_handler DEFINITION

***********************************************************************
* CLASS lcl_event_handler IMPLEMENTATION                              *
***********************************************************************
CLASS lcl_event_handler IMPLEMENTATION.
* Handle hotspot click
  METHOD handle_hotspot_click.
    PERFORM handle_hotspot_click USING e_row_id e_column_id es_row_no.
  ENDMETHOD.                    "handle_hotspot_click
ENDCLASS.                    "lcl_event_handler IMPLEMENTATION

***********************************************************************
* FIELD-SYMBOLS                                                       *
***********************************************************************
FIELD-SYMBOLS: <fieldcat> TYPE lvc_s_fcat.

***********************************************************************
* OBJECTS                                                             *
***********************************************************************
DATA : obj_container     TYPE REF TO cl_gui_custom_container.
DATA : obj_alv           TYPE REF TO cl_gui_alv_grid .
DATA : obj_event_handler TYPE REF TO lcl_event_handler.

***********************************************************************
* TABLES                                                              *
***********************************************************************
TABLES: likp, lips, vbup, qmel, equi, eqkt, equz, iloa, imptt.

***********************************************************************
* TYPE-POOLS                                                          *
***********************************************************************
TYPE-POOLS slis.


***********************************************************************
* INTERNAL TYPES                                                      *
***********************************************************************
TYPES: BEGIN OF str_runhrs,
         mpobj    TYPE imrc_mpobj,
         idate    TYPE imrc_idate,
         recdv    TYPE imrc_recdv,
       END OF str_runhrs.

***********************************************************************
* INTERNAL TABLES                                                     *
***********************************************************************
DATA: BEGIN OF it_likp OCCURS 0,
        vbeln   LIKE likp-vbeln,
        posnr   LIKE lips-posnr,
        pstyv   LIKE lips-pstyv,
        wadat   LIKE likp-wadat,
        zzsernr LIKE lips-zzsernr,
        zzequnr LIKE lips-zzequnr,
        kosta   LIKE vbup-kosta,
        wbsta   LIKE vbup-wbsta,
      END OF it_likp.

DATA: BEGIN OF it_likp_un OCCURS 0,
        vbeln   LIKE likp-vbeln,
      END OF it_likp_un.

DATA: BEGIN OF it_equi_un OCCURS 0,
        zzequnr LIKE lips-zzequnr,
      END OF it_equi_un.

DATA: BEGIN OF it_delinot OCCURS 0.
        INCLUDE STRUCTURE yse_rent_alv_delinot.
DATA:   bstnk   TYPE      bstkd,
      END OF it_delinot.

DATA:  BEGIN OF it_cont OCCURS 0,
         vbeln TYPE vbfa-vbeln,
         vbelv TYPE vbfa-vbelv,
         vbtyp_v LIKE vbfa-vbtyp_v,
       END OF it_cont.

DATA: BEGIN OF it_funcloc OCCURS 0,
        sernr   TYPE equi-sernr,
        equnr   TYPE equi-equnr,
        objnr   TYPE equi-objnr,
        eqktx   TYPE eqkt-eqktx,
        iloan   TYPE equz-iloan,
        tplnr   TYPE iloa-tplnr,
      END OF it_funcloc.

DATA:  BEGIN OF it_qmel OCCURS 0,
         qmnum TYPE qmnum,
         objnr TYPE j_objnr,
         bstnk TYPE bstkd,
       END OF it_qmel.

DATA:  BEGIN OF it_cust OCCURS 0,
         vbeln TYPE vbpa-vbeln,
         kunnr TYPE vbpa-kunnr,
         name1 TYPE kna1-name1,
       END OF it_cust.

* ALV grid
DATA:  it_fieldcat TYPE lvc_t_fcat,
       it_rows     TYPE lvc_t_row,    " table selected rows alv grid
       it_runhrs   TYPE TABLE OF str_runhrs WITH HEADER LINE,
       it_cells    TYPE lvc_t_cell,
       wa_cells    TYPE LINE OF lvc_t_cell,
       it_scol     TYPE lvc_s_col,    " Scroll column
       it_srow     TYPE lvc_s_roid,   " Scroll row
       it_fcol     TYPE lvc_s_col,    " Focus colomn
       it_frow     TYPE lvc_s_row.    " Focus row

***********************************************************************
* CONSTANTS                                                           *
***********************************************************************
CONSTANTS:
  lc_true        TYPE char1       VALUE 'X',    " true
  lc_engl        LIKE tvagt-spras VALUE 'E',    " english
  lc_lfart_deli  TYPE likp-lfart  VALUE 'LR',   " inbound delivery
  lc_lfart_delo  TYPE likp-lfart  VALUE 'ZLF',  " outbound delivery
  lc_pstyv_deli  TYPE lips-pstyv  VALUE 'ZMVN', " Inbound item cat
  lc_pstyv_delo  TYPE lips-pstyv  VALUE 'ZRVN', " Outbound item cat
  lc_pstyv_down  TYPE lips-pstyv  VALUE 'ZTOR', " Down payment item cat
  lc_pstyv_mile  TYPE lips-pstyv  VALUE 'ZMIL', " Milestone item cat
  lc_shipto      TYPE vbpa-parvw  VALUE 'WE',  " ship-to party
  lc_vbtyp_cont  TYPE vbfa-vbtyp_n VALUE 'C'.   " Order

***********************************************************************
* VARIABLES                                                           *
***********************************************************************
* Own data
DATA:
  lv_werks        LIKE lips-werks,     " Plant
  lv_vstel        LIKE likp-vstel,     " Shipping point
  lv_lfart        LIKE likp-lfart.     " Delivery type
* Output
DATA:
  gv_repid        LIKE sy-repid,
  okcode          LIKE sy-ucomm,       " return param screen 100
  gs_layout       TYPE lvc_s_layo ,
  g_behaviour_alv TYPE REF TO cl_dragdrop,
  o_werks(4)       TYPE c,             " Output field Plant
  o_vstel(4)       TYPE c.             " Output field Sales Org.
* Process
DATA:
  lv_records      TYPE f,              " records counter
  lv_tmptxt1(20)  TYPE c,              " temporary text field
  lv_tmptxt2(20)  TYPE c,              " temporary text field
  gv_mpobj   TYPE imrc_mpobj,          " Object nr Meas. Pnt Obj.
  lv_index        TYPE lvc_index.      " index alv grid

***********************************************************************
* SELECTION SCREEN                                                    *
***********************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-s01.

PARAMETERS:
*MOD-010: changed so-werks with p_werks for authorisation
  p_werks  LIKE lips-werks.               " receiving plant
SELECT-OPTIONS:
  so_vstel FOR likp-vstel,                " shipping point
  so_wadat FOR likp-wadat,                " Planned GI date
  so_kosta FOR vbup-kosta, " DEFAULT space, " Deliv status picking
  so_wbsta FOR vbup-wbsta, " DEFAULT 'A',   " Deliv status GI
  so_lfart FOR likp-lfart, " DEFAULT 'ZLF', " Delivery type
  so_pstyv FOR lips-pstyv.
SELECTION-SCREEN END OF BLOCK b1.

***********************************************************************
* INITIALIZATION                                                      *
***********************************************************************
INITIALIZATION.
  gv_repid = sy-repid.

* Load user's parameters
* Plant
  GET PARAMETER ID 'WRK' FIELD lv_werks.
  IF NOT lv_werks IS INITIAL.
    p_werks = lv_werks.
*    so_werks-sign = 'I'.
*    so_werks-option = 'EQ'.
*    so_werks-low = lv_werks.
*    APPEND so_werks.
  ENDIF.

* Shipping Point
  GET PARAMETER ID 'VST' FIELD lv_vstel.
  IF NOT lv_vstel IS INITIAL.
    so_vstel-sign = 'I'.
    so_vstel-option = 'EQ'.
    so_vstel-low = lv_vstel.
    APPEND so_vstel.
  ENDIF.

* Item category defaults
  so_pstyv-sign = 'I'.
  so_pstyv-option = 'EQ'.
  so_pstyv-low = lc_pstyv_deli.
  APPEND so_pstyv.
  so_pstyv-low = lc_pstyv_delo.
  APPEND so_pstyv.
  so_pstyv-low = lc_pstyv_down.
  APPEND so_pstyv.
  so_pstyv-low = lc_pstyv_mile.
  APPEND so_pstyv.

** Delivery Type
*  GET PARAMETER ID 'ALT' FIELD lv_lfart.
*  IF NOT lv_lfart IS INITIAL.
*    so_lfart-sign = 'I'.
*    so_lfart-option = 'EQ'.
*    so_lfart-low = lv_werks.
*    APPEND so_lfart.
*  ENDIF.

** Build delivery type range
*  so_lfart-sign = 'I'.
*  so_lfart-option = 'EQ'.
*  so_lfart-low = lc_lfart_deli.
*  APPEND so_lfart.
*  so_lfart-low = lc_lfart_delo.
*  APPEND so_lfart.

***********************************************************************
* AT SELECTION SCREEN                                                 *
***********************************************************************
AT SELECTION-SCREEN.

  PERFORM  check_authorization .


***********************************************************************
* START OF SELECTION                                                  *
***********************************************************************
START-OF-SELECTION.

  PERFORM load_data.

* Prepare header data
  PERFORM build_header.
* Display
  CALL SCREEN 100.


***********************************************************************
** FORM   LOAD_DATA                                                   *
***********************************************************************
* All data is loaded and copied into the output table. This is put in *
* a separate form because a refesh function exists on the ALV grid.   *
***********************************************************************
FORM load_data.

  CLEAR:
    it_delinot,
    it_delinot[].
* Load data
  PERFORM load_deliveries.
  PERFORM load_running_hours.
  PERFORM load_contracts.
  PERFORM load_location.
  PERFORM load_partners.

  PERFORM copy_data.

ENDFORM.    " load_data

***********************************************************************
* Module STATUS_0100 OUTPUT                                           *
***********************************************************************
MODULE status_0100 OUTPUT.
  SET TITLEBAR '100' .
  SET PF-STATUS 'STATUS100'.
ENDMODULE.                 " STATUS_0100  OUTPUT

***********************************************************************
* Module USER_COMMAND_0100 INPUT                                      *
***********************************************************************
MODULE user_command_0100 INPUT.

  CLEAR it_delinot.
  CLEAR it_rows.

  CASE okcode.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN 'EXIT'.
      LEAVE TO SCREEN 0.
    WHEN 'OVL01N'.
      PERFORM refresh_init.
      READ TABLE it_delinot INDEX lv_index.
      PERFORM edit_delivery USING it_delinot-vbeln.
    WHEN 'OIW52'.
      PERFORM refresh_init.
      READ TABLE it_delinot INDEX lv_index.
      PERFORM edit_notification USING it_delinot-qmnum.
    WHEN 'OIE02'.
      PERFORM refresh_init.
      READ TABLE it_delinot INDEX lv_index.
      PERFORM edit_equipment USING it_delinot-zzequnr.
    WHEN 'OIK02'.
      PERFORM refresh_init.
      CLEAR it_funcloc.
      READ TABLE it_delinot INDEX lv_index.
      READ TABLE it_funcloc WITH KEY equnr = it_delinot-zzequnr.
      PERFORM edit_meas_point USING it_funcloc-objnr.  " equi-objnr.
    WHEN 'OIK11'.
      PERFORM refresh_init.
      CLEAR it_funcloc.
      READ TABLE it_delinot INDEX lv_index.
      READ TABLE it_funcloc WITH KEY equnr = it_delinot-zzequnr.
      PERFORM create_meas_doc USING it_funcloc-objnr.  " equi-objnr.
    WHEN 'OIK11_2'.
      PERFORM refresh_init.
      CLEAR it_funcloc.
      READ TABLE it_delinot INDEX lv_index.
      READ TABLE it_funcloc WITH KEY equnr = it_delinot-zzequnr.
      PERFORM create_fuel_doc USING it_funcloc-objnr.  " equi-objnr.
    WHEN 'REFRESH'.
      PERFORM refresh_init.
      PERFORM refresh_exec.
  ENDCASE.
ENDMODULE.                 " USER_COMMAND_0100 INPUT

***********************************************************************
* Module PREPARE_SCREEN OUTPUT                                        *
***********************************************************************
MODULE prepare_screen OUTPUT.

  IF obj_container IS INITIAL .
    CREATE OBJECT obj_container
      EXPORTING
        container_name = 'OVERVIEW'.

    CREATE OBJECT obj_alv
      EXPORTING
        i_parent = obj_container.

    CREATE OBJECT obj_event_handler.

    PERFORM build_alv.

*   Enable line selection and hotspot clicking
    SET HANDLER obj_event_handler->handle_hotspot_click FOR obj_alv.
  ENDIF.
ENDMODULE.                 " PREPARE_SCREEN  OUTPUT

***********************************************************************
* Form BUILD_ALV                                                      *
***********************************************************************
FORM build_alv.

  REFRESH: it_fieldcat.

  CALL FUNCTION 'LVC_FIELDCATALOG_MERGE'
    EXPORTING
      i_buffer_active  = ' '
      i_structure_name = 'YSE_RENT_ALV_DELINOT'
    CHANGING
      ct_fieldcat      = it_fieldcat.

  LOOP AT it_fieldcat ASSIGNING <fieldcat>.
    CASE <fieldcat>-fieldname.
      WHEN  'VBELV'.
        <fieldcat>-hotspot   = lc_true.
        <fieldcat>-tooltip = 'Contract'(001).
        <fieldcat>-coltext = 'Contract'(002).
        <fieldcat>-outputlen = 12.
      WHEN  'VBELN'.
        <fieldcat>-hotspot   = lc_true.
        <fieldcat>-tooltip = 'Delivery Nr'(003).
        <fieldcat>-coltext = 'Delivery'(004).
        <fieldcat>-outputlen = 10.
      WHEN  'POSNR'.
        <fieldcat>-tooltip = 'Delivery item'(005).
        <fieldcat>-coltext = 'Deliv itm'(006).
        <fieldcat>-outputlen = 3.
      WHEN  'PSTYV'.
        <fieldcat>-tooltip   = 'Item category'(007).
        <fieldcat>-coltext   = 'Itm cat'(008).
        <fieldcat>-outputlen = 5.
      WHEN  'NAME1'.
        <fieldcat>-tooltip = 'Customer Name'(009).
        <fieldcat>-coltext = 'Customer'(010).
      WHEN  'WADAT'.
        <fieldcat>-tooltip   = 'Planned GI date'(011).
        <fieldcat>-coltext   = 'Pl. GI date'(012).
        <fieldcat>-outputlen = 9.
      WHEN  'KOSTA'.
        <fieldcat>-tooltip   = 'Picking status'(013).
        <fieldcat>-coltext   = 'Pck status'(014).
        <fieldcat>-outputlen = 2.
      WHEN  'WBSTA'.
        <fieldcat>-tooltip   = 'Good issue status'(015).
        <fieldcat>-coltext   = 'GI status'(016).
        <fieldcat>-outputlen = 2.
      WHEN  'ZZSERNR'.
        <fieldcat>-tooltip   = 'Serial Nr'(017).
        <fieldcat>-coltext   = 'Serial'(018).
        <fieldcat>-outputlen = 10.
      WHEN  'QMNUM'.
        <fieldcat>-hotspot   = lc_true.
        <fieldcat>-tooltip   = 'Notification number'(019).
        <fieldcat>-coltext   = 'Notif Nr'(020).
        <fieldcat>-outputlen = 10.
      WHEN  'NOTSTAT'.
        <fieldcat>-tooltip   = 'Status notification'(021).
        <fieldcat>-coltext   = 'Status'(022).
        <fieldcat>-outputlen = 10.
      WHEN  'ZZEQUNR'.
        <fieldcat>-hotspot   = lc_true.
        <fieldcat>-tooltip   = 'Equipment number'(023).
        <fieldcat>-coltext   = 'Equip nr'(024).
        <fieldcat>-outputlen = 9.
      WHEN  'EQKTX'.
        <fieldcat>-tooltip   = 'Equipment description'(025).
        <fieldcat>-coltext   = 'Equip desc'(026).
        <fieldcat>-outputlen = 16.
      WHEN  'TPLNR'.
        <fieldcat>-tooltip   = 'Functional location'(027).
        <fieldcat>-coltext   = 'Func loc'(028).
        <fieldcat>-outputlen = 19.
      WHEN  'RECDV'.
        <fieldcat>-tooltip   = 'Running Hours'(029).
        <fieldcat>-coltext   = 'Run Hrs'(030).
        <fieldcat>-outputlen = 6.
    ENDCASE.
  ENDLOOP.

*  gs_layout-no_toolbar = lc_true.
  gs_layout-cwidth_opt = lc_true.
  gs_layout-sel_mode   = 'D'.

  CALL METHOD obj_alv->set_table_for_first_display
    EXPORTING
      i_structure_name              = 'YSE_RENT_ALV_DELINOT'
      is_layout                     = gs_layout
    CHANGING
      it_outtab                     = it_delinot[]
      it_fieldcatalog               = it_fieldcat
    EXCEPTIONS
      invalid_parameter_combination = 1
      program_error                 = 2
      too_many_lines                = 3
      OTHERS                        = 4.

  IF sy-subrc <> 0.
*     MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*     WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.
ENDFORM.                    "build_alv

***********************************************************************
* Form HANDLE_HOTSPOT_CLICK                                           *
***********************************************************************
*      -->P_E_ROW_ID     text                                         *
*      -->P_E_COLUMN_ID  text                                         *
*      -->P_ES_ROW_NO    text                                         *
***********************************************************************
FORM handle_hotspot_click  USING    p_e_row_id
                                    p_e_column_id
                                    p_es_row_no STRUCTURE lvc_s_roid.

  PERFORM refresh_init.

  CLEAR it_delinot.
  READ TABLE it_delinot INDEX p_e_row_id.

* If Contract Number clicked
  IF p_e_column_id EQ 'VBELV'.
    PERFORM disp_contract USING it_delinot-vbelv.
  ENDIF.

* If Delivery Number clicked
  IF p_e_column_id EQ 'VBELN'.
    PERFORM edit_delivery USING it_delinot-vbeln.
  ENDIF.

* If Service Notification clicked
  IF p_e_column_id EQ 'QMNUM'.
    PERFORM edit_notification USING it_delinot-qmnum.
  ENDIF.

* If Equipment Number clicked
  IF p_e_column_id EQ 'ZZEQUNR'.
    PERFORM edit_equipment USING it_delinot-zzequnr.
  ENDIF.

ENDFORM.                    " handle_hotspot_click


***********************************************************************
* Form DISPLAY_CONTRACT                                               *
***********************************************************************
*   argument: arg_vbeln of type vbeln                                 *
***********************************************************************
* Opens transaction VA43 (Display contract) with as parameter the     *
* contract number.                                                    *
***********************************************************************
FORM disp_contract USING arg_vbeln LIKE it_delinot-vbelv.
  IF NOT arg_vbeln IS INITIAL.
*     Display contract
    SET PARAMETER ID 'KTN' FIELD arg_vbeln.
    CALL TRANSACTION 'VA43'.  " AND SKIP FIRST SCREEN.
*     Refresh ALV grid
    PERFORM refresh_exec.
  ENDIF.
ENDFORM.                    "disp_contract



***********************************************************************
* Form EDIT_DELIVERY                                                  *
***********************************************************************
*   argument: arg_vbeln of type vbeln                                 *
***********************************************************************
* Opens transaction VL02N (Change delivery) with as parameter the     *
* delivery number.                                                    *
***********************************************************************
FORM edit_delivery USING arg_vbeln LIKE it_delinot-vbeln.
  IF NOT arg_vbeln IS INITIAL.
*     Edit delivery
    SET PARAMETER ID 'VL' FIELD arg_vbeln.
    CALL TRANSACTION 'VL02N' AND SKIP FIRST SCREEN.
*     Refresh ALV grid
    PERFORM refresh_exec.
  ENDIF.
ENDFORM.                    "edit_delivery

***********************************************************************
* Form EDIT_NOTIFICATION                                              *
***********************************************************************
*   argument: arg_qmnum of type qmnum                                 *
***********************************************************************
* Opens transaction IW52 (Change notification) with as parameter the  *
* notification number. If there is no notification for the delivery,  *
* nothing happens.                                                    *
* Warning: it is possible that a notification number was attached to  *
* a delivery, but that the document does no longer exist. In that case*
* the transaction is launched, and issues an error message.           *
***********************************************************************
FORM edit_notification USING arg_qmnum LIKE it_delinot-qmnum.
  IF NOT arg_qmnum IS INITIAL.
*     Edit notification
    SET PARAMETER ID 'IQM' FIELD arg_qmnum.
    CALL TRANSACTION 'IW52' AND SKIP FIRST SCREEN.
*     Refresh ALV grid
    PERFORM refresh_exec.
  ENDIF.
ENDFORM.                    "edit_notification

***********************************************************************
* Form EDIT_EQUIPMENT                                                 *
***********************************************************************
*   argument: arg_equnr of type equi-equnr                            *
***********************************************************************
* Opens transaction IE02 (Change equipment) with as parameter the     *
* equipment number. If this equipment does exist, nothing happens.    *
***********************************************************************
FORM edit_equipment USING arg_equnr LIKE it_delinot-zzequnr.

  IF NOT arg_equnr IS INITIAL.
*   Edit equipment
    SET PARAMETER ID 'EQN' FIELD arg_equnr.
    CALL TRANSACTION 'IE02' AND SKIP FIRST SCREEN.
*   Refresh ALV grid
    PERFORM refresh_exec.
  ENDIF.
ENDFORM.                    "edit_equipment

***********************************************************************
* Form EDIT_MEAS_POINT                                                *
***********************************************************************
*   argument: arg_objnr of type equi-objnr                            *
***********************************************************************
* Open transaction IK02 (Change measuring point) with as parameter the*
* measuring point. If this point does not exist, nothing happens.     *
***********************************************************************
FORM edit_meas_point USING arg_objnr.

  DATA       lv_atinn TYPE cabn-atinn.
  CONSTANTS  lc_atnam TYPE cabn-atnam VALUE 'ZAM_RHRSTOTAL'.

* Convert characteristic to atinn
  CALL FUNCTION 'CONVERSION_EXIT_ATINN_INPUT'
    EXPORTING
      input  = lc_atnam          " 'ZAM_RHRSTOTAL'
    IMPORTING
      output = lv_atinn.         " '0000000816'

  IF NOT arg_objnr IS INITIAL.
*   Select measuring point
    CLEAR imptt.
    SELECT SINGLE *
        FROM imptt
       WHERE mpobj EQ arg_objnr
         AND atinn EQ lv_atinn.    " char ZAM_RHRSTOTAL.
*   If measuring point available
    IF NOT imptt-point IS INITIAL.
      SET PARAMETER ID 'IPT' FIELD imptt-point.
      CALL TRANSACTION 'IK02' AND SKIP FIRST SCREEN.
*     Refresh ALV grid
      PERFORM refresh_exec.
    ENDIF.
  ENDIF.
ENDFORM.                    "edit_meas_point

***********************************************************************
* Form CREATE_MEAS_DOC                                                *
***********************************************************************
*   argument: arg_objnr of type equi-objnr                            *
***********************************************************************
* Open transaction IK11 (Create measuring document) with as parameter *
* the measuring point. If this point does not exist, nothing happens. *
***********************************************************************
FORM create_meas_doc USING arg_objnr. " LIKE it_delinot-zzequnr.

  DATA       lv_atinn TYPE cabn-atinn.
  CONSTANTS  lc_atnam TYPE cabn-atnam VALUE 'ZAM_RHRSTOTAL_ACT'.

* Convert characteristic to atinn
  CALL FUNCTION 'CONVERSION_EXIT_ATINN_INPUT'
    EXPORTING
      input  = lc_atnam          " 'ZAM_RHRSTOTAL_ACT'
    IMPORTING
      output = lv_atinn.         " '0000000849'

  IF NOT arg_objnr IS INITIAL.
    CLEAR imptt.
    SELECT SINGLE *
        FROM imptt
       WHERE mpobj EQ arg_objnr
         AND atinn EQ lv_atinn.    " char ZAM_RHRSTOTAL_ACT.
    IF NOT imptt-point IS INITIAL.
      SET PARAMETER ID 'IPT' FIELD imptt-point.
      CALL TRANSACTION 'IK11' AND SKIP FIRST SCREEN.
*       Refresh ALV grid
      PERFORM refresh_exec.
    ENDIF.
  ENDIF.
ENDFORM.                    "create_meas_doc



***********************************************************************
* Form CREATE_FUEL_DOC                                                *
***********************************************************************
*   argument: arg_objnr of type equi-objnr                            *
***********************************************************************
* Open transaction IK11 (Create measuring document) with as parameter *
* the measuring point. If this point does not exist, nothing happens. *
***********************************************************************
FORM create_fuel_doc USING arg_objnr. " LIKE it_delinot-zzequnr.

  DATA       lv_atinn TYPE cabn-atinn.
  CONSTANTS  lc_atnam TYPE cabn-atnam VALUE 'ZAM_FUELLEVEL'.

* Convert characteristic to atinn
  CALL FUNCTION 'CONVERSION_EXIT_ATINN_INPUT'
    EXPORTING
      input  = lc_atnam          " 'ZAM_FUELLEVEL'
    IMPORTING
      output = lv_atinn.

  IF NOT arg_objnr IS INITIAL.
    CLEAR imptt.
    SELECT SINGLE *
        FROM imptt
       WHERE mpobj EQ arg_objnr
         AND atinn EQ lv_atinn.    " char ZAM_FUELLEVEL.
    IF NOT imptt-point IS INITIAL.
      SET PARAMETER ID 'IPT' FIELD imptt-point.
      CALL TRANSACTION 'IK11' AND SKIP FIRST SCREEN.
*       Refresh ALV grid
      PERFORM refresh_exec.
    ENDIF.
  ENDIF.
ENDFORM.                    "create_fuel_doc


*&---------------------------------------------------------------------*
*&      Form  refresh_init
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM refresh_init.

* Get current alv cursor and scroll settings
  CALL METHOD obj_alv->get_selected_cells
    IMPORTING
      et_cell = it_cells.

  CALL METHOD obj_alv->get_scroll_info_via_id
    IMPORTING
      es_col_info = it_scol
      es_row_no   = it_srow.

* Obtain cell index for transaction
  CALL METHOD obj_alv->get_selected_rows
    IMPORTING
      et_index_rows = it_rows.
  READ TABLE it_rows INDEX 1 INTO lv_index.
  IF NOT sy-subrc = 0.
    READ TABLE it_cells INDEX 1 INTO wa_cells.
    IF sy-subrc = 0.
      lv_index = wa_cells-row_id-index.
    ENDIF.
  ENDIF.

ENDFORM.    " refresh_init



*&---------------------------------------------------------------------*
*&      Form  refresh_exec
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM refresh_exec.

* Reload data
  PERFORM load_data.

* Refresh display
  CALL METHOD obj_alv->refresh_table_display.

* Restore scroll position
  CALL METHOD obj_alv->set_scroll_info_via_id
    EXPORTING
      is_col_info = it_scol
      is_row_no   = it_srow.

* Restore curson position
  READ TABLE it_cells INDEX 1 INTO wa_cells.
  IF sy-subrc = 0.
    it_frow-index = wa_cells-row_id-index.
    it_fcol-fieldname = wa_cells-col_id-fieldname.

    CALL METHOD obj_alv->set_current_cell_via_id
      EXPORTING
        is_row_id    = it_frow
        is_column_id = it_fcol.
  ELSE.

    CALL METHOD obj_alv->set_selected_rows
      EXPORTING
        it_index_rows = it_rows.

  ENDIF.

ENDFORM.    " refresh_exec

***********************************************************************
* Form LOAD_DATA                                                      *
***********************************************************************
*   argument: None                                                    *
***********************************************************************
* Selects data to be displayed.                                       *
***********************************************************************
FORM copy_data.

* Empty notifications table (for refresh passings)
  CLEAR it_qmel.
  REFRESH it_qmel.

* First loop in it_delinot and fill it_qmel
  LOOP AT it_likp.
    CLEAR it_delinot.
    MOVE-CORRESPONDING it_likp TO it_delinot.

*   Remove leading zeros from document number
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
      EXPORTING
        input  = it_delinot-vbeln
      IMPORTING
        output = lv_tmptxt1.
    CONCATENATE lv_tmptxt1
              '/'
         INTO lv_tmptxt2.
*   Remove leading zeros from item number
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
      EXPORTING
        input  = it_delinot-posnr
      IMPORTING
        output = lv_tmptxt1.
*   Build delivery/item code
    CONCATENATE lv_tmptxt2
                lv_tmptxt1
           INTO lv_tmptxt2.

*   Keep BSTNK to find Notifications later on
    it_qmel-bstnk = lv_tmptxt2.
    it_delinot-bstnk = lv_tmptxt2.

*   Copy contract
    READ TABLE it_cont WITH KEY vbeln = it_delinot-vbeln.
    IF sy-subrc = 0.
      it_delinot-vbelv = it_cont-vbelv.
    ENDIF.

*   Copy customer name
    READ TABLE it_cust WITH KEY vbeln = it_delinot-vbeln.
    IF sy-subrc = 0.
      it_delinot-name1 = it_cust-name1.
    ENDIF.

*   Copy Running Hours Measuring Points
    IF NOT it_delinot-zzequnr IS INITIAL.
      gv_mpobj+2 = it_delinot-zzequnr.
*     CLEAR it_runhrs.
      READ TABLE it_runhrs WITH KEY mpobj = gv_mpobj.
*     If a Measuring Point was found
      IF sy-subrc = 0.
*       insert it into the output table
        it_delinot-recdv = it_runhrs-recdv.
*       MODIFY it_delinot.
      ENDIF.
    ENDIF.  " it_delinot-zzequnr exists

    APPEND it_delinot.
    APPEND it_qmel.

  ENDLOOP.  "it_delinot

* Count selected notification records
  DESCRIBE TABLE it_qmel LINES lv_records.
  IF lv_records > 0.

*   Fetch the notifications releated to delivery items
    SELECT qmnum objnr bstnk
        FROM qmel
        INTO TABLE it_qmel
         FOR ALL ENTRIES IN it_qmel
       WHERE bstnk EQ it_qmel-bstnk.

*   Second loop through it_delinot
    LOOP AT it_delinot.

      READ TABLE it_qmel WITH KEY bstnk = it_delinot-bstnk.

      IF sy-subrc = 0.

        it_delinot-qmnum = it_qmel-qmnum.

*       Note: Set bypass_buffer to get new status when coming
*             back from transaction IW53.
        CALL FUNCTION 'STATUS_TEXT_EDIT'
          EXPORTING
            client           = sy-mandt
            objnr            = it_qmel-objnr
            spras            = lc_engl
            bypass_buffer    = lc_true
          IMPORTING
            line             = it_delinot-notstat
          EXCEPTIONS
            object_not_found = 1
            OTHERS           = 2.

      ENDIF.  " found notification

*     Add Functional location
      READ TABLE it_funcloc
        WITH KEY sernr = it_delinot-zzsernr.

      IF sy-subrc = 0.
        it_delinot-tplnr = it_funcloc-tplnr.
        it_delinot-eqktx = it_funcloc-eqktx.
      ENDIF.

      MODIFY it_delinot FROM it_delinot.
    ENDLOOP.  "it_delinot
  ENDIF.  " records in it_qmel

ENDFORM.  " Copy_data


***********************************************************************
** FORM  LOAD_DELIVERIES                                              *
***********************************************************************
* Loads the delivery documents and all relevant data to the output    *
* table.                                                              *
***********************************************************************
FORM load_deliveries.

* Select delivery documents - header and item data
  SELECT likp~vbeln lips~posnr lips~pstyv likp~wadat
         lips~zzsernr lips~zzequnr vbup~kosta vbup~wbsta
      INTO TABLE it_likp
      FROM likp
     INNER JOIN lips
        ON likp~vbeln EQ lips~vbeln
     INNER JOIN vbup
        ON vbup~vbeln EQ likp~vbeln
       AND vbup~posnr EQ lips~posnr
     WHERE likp~vstel IN so_vstel
       AND likp~wadat IN so_wadat
       AND likp~lfart IN so_lfart
       AND lips~werks =   p_werks
       AND lips~pstyv IN so_pstyv
       AND vbup~kosta IN so_kosta
       AND vbup~wbsta IN so_wbsta.

* Build
*   - list of unique Delivery numbers
*   - list of unique equipment numbers
  LOOP AT it_likp.
*   Copy delv number
    it_likp_un-vbeln = it_likp-vbeln.
    APPEND it_likp_un.
*   If eq. filled, copy it
    IF it_likp-zzequnr NE space.
      it_equi_un-zzequnr = it_likp-zzequnr.
      APPEND it_equi_un.
    ENDIF.
  ENDLOOP.
  SORT it_likp_un BY vbeln.
  DELETE ADJACENT DUPLICATES FROM it_likp_un.
  SORT it_equi_un BY zzequnr.
  DELETE ADJACENT DUPLICATES FROM it_equi_un.

ENDFORM.         " load_deliveries


***********************************************************************
** FORM  LOAD_RUNNING_HOURS                                           *
***********************************************************************
* Loads the latest running hours for the selected equipments.         *
***********************************************************************
FORM load_running_hours.

  CONSTANTS  lc_atnam TYPE cabn-atnam VALUE 'ZAM_RHRSTOTAL_ACT'.

  DATA:
    lv_atinn   TYPE cabn-atinn,   " Atinn number Running Hours
    lv_point   TYPE imrc_point.   " Measuring Point

  IF NOT it_equi_un[] IS INITIAL.

*   Obtain atinn number
    CALL FUNCTION 'CONVERSION_EXIT_ATINN_INPUT'
      EXPORTING
        input  = lc_atnam        " 'ZAM_RHRSTOTAL_ACT'
      IMPORTING
        output = lv_atinn.

*   Build measuring point objects
    gv_mpobj(2) = 'IE'.
    LOOP AT it_equi_un.
      gv_mpobj+2 = it_equi_un-zzequnr.
      it_runhrs-mpobj = gv_mpobj.
      APPEND it_runhrs.
    ENDLOOP.

    SORT it_runhrs.
    DELETE ADJACENT DUPLICATES FROM it_runhrs.

*   Load measuring points for selected objects
    SELECT mpobj idate recdv
        INTO TABLE it_runhrs
        FROM imrg
       INNER JOIN imptt
          ON imrg~point = imptt~point
         FOR ALL entries IN it_runhrs
       WHERE mpobj EQ it_runhrs-mpobj
*         AND IDATE GE LV_QMDAT
         AND imrg~idate LE sy-datum
         AND imrg~cancl EQ space
         AND imptt~lvorm = space
         AND imptt~atinn = lv_atinn.

*   Sort the points by date so that the most recent object
*   will be read first.
    SORT it_runhrs DESCENDING BY idate.

  ENDIF.    " it_LIKP NOT INITIAL

ENDFORM.    " load_running_hours


***********************************************************************
** FORM  LOAD_CONTRACTS                                               *
***********************************************************************
* Loads originating cotract numbers based on flow table and stores    *
* the data in an internal table.                                      *
***********************************************************************
FORM load_contracts.

  IF NOT it_likp_un[] IS INITIAL.
*   Prepare contract numbers
    SELECT vbeln vbelv vbtyp_v                          "#EC CI_NOFIRST
        FROM vbfa
        INTO TABLE it_cont
         FOR ALL ENTRIES IN it_likp_un
       WHERE vbeln   EQ it_likp_un-vbeln.
*        AND vbtyp_v EQ lc_vbtyp_cont.  " no index on the field
  ENDIF.

* Eliminate records from the document flow which don't have preceding
* document type "Contract" (or sales doc).
  LOOP AT it_cont WHERE vbtyp_v NE lc_vbtyp_cont.
    DELETE it_cont.
  ENDLOOP.

ENDFORM.    " load_contracts


***********************************************************************
** FORM  LOAD_LOCATION                                                *
***********************************************************************
* Loads the functional location and description from the assets table *
***********************************************************************
FORM load_location.

  IF NOT it_equi_un[] IS INITIAL.
*   Prepare functional location table
    SELECT equi~sernr equi~equnr equi~objnr
           eqkt~eqktx equz~iloan iloa~tplnr
        INTO TABLE it_funcloc
        FROM equi
       INNER JOIN eqkt
          ON equi~equnr EQ eqkt~equnr
       INNER JOIN equz
          ON equi~equnr EQ equz~equnr
       INNER JOIN iloa
          ON equz~iloan EQ iloa~iloan
         FOR ALL entries IN it_equi_un
       WHERE equi~equnr EQ it_equi_un-zzequnr
         AND equz~datab LE sy-datum
         AND equz~datbi GE sy-datum.
  ENDIF.

ENDFORM.      " load_func_loc


***********************************************************************
** FORM  BUILD_HEADER                                                 *
***********************************************************************
* Loads the partners in internal table it_cust.                       *
***********************************************************************
FORM load_partners.

  IF NOT it_likp_un[] IS INITIAL.
*   Prepare partners
    SELECT vbpa~vbeln vbpa~kunnr kna1~name1
        INTO TABLE it_cust
        FROM vbpa
       INNER JOIN kna1
          ON vbpa~kunnr EQ kna1~kunnr
         FOR ALL entries IN it_likp_un
       WHERE vbpa~vbeln EQ it_likp_un-vbeln
         AND vbpa~parvw EQ lc_shipto.
  ENDIF.

ENDFORM.                    "load_partners


***********************************************************************
** FORM  BUILD_HEADER                                                 *
***********************************************************************
* Prepares selection parameters to be displayed in header banner.     *
***********************************************************************
FORM build_header.

*  o_werks = so_werks-low.
  o_werks = p_werks.
  o_vstel = so_vstel-low.

ENDFORM.  " build_header
*&---------------------------------------------------------------------*
*&      Form  check_authorization
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM check_authorization .


  AUTHORITY-CHECK OBJECT 'I_SWERK'
           ID 'TCD' DUMMY
           ID 'SWERK' FIELD p_werks.

  IF sy-subrc = 4.
*   No authorisation to display data from Sales Organisation p_vkorg
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH p_werks.
  ELSEIF sy-subrc <> 0.
*   Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
  ENDIF.


ENDFORM.                    " check_authorization

*Text symbol text£º
*001:Contract
*002:Contract
*003:Delivery Nr
*004:Delivery
*005:Delivery item
*006:Deliv itm
*007:Item category
*008:Itm cat
*009:Customer Name
*010:Customer
*011:Planned GI date
*012:Pl. GI date
*013:Picking status
*014:Pck status
*015:Good issue status
*016:GI status
*017:Serial Nr
*018:Serial
*019:Notification number
*020:Notif Nr
*021:Status notification
*022:Status
*023:Equipment number
*024:Equip nr
*025:Equipment description
*026:Equip desc
*027:Functional location
*028:Func loc
*029:Running Hours
*030:Run Hrs

*S01:Selection parameters
*Selection text£º
*P_WERKS:D       Plant
*SO_KOSTA:        Delivery Status picking
*SO_LFART:        Delivery type
*SO_PSTYV:        Item category
*SO_VSTEL:        Shipping point
*SO_WADAT:        Planned GI date
*SO_WBSTA:        Delivery status GI
