*----------------------------------------------------------------------*
* Author  : Anda Wu                                                    *
* Created : 09.16.2015                                                 *
* Dev.nbr: CR3696- supersession information                            *
*----------------------------------------------------------------------*
* This report  is  new report for returns  of Russia                   *
************************************************************************
* Mod-xxx | xxxx.xx.xx | xxxx xxxx     | CD1Kxxxxxxxxxx  |   CRXXXX    *
************************************************************************
************************************************************************
REPORT yse_icos_history NO STANDARD PAGE HEADING LINE-SIZE 255.

************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES:
  vbak,
  vbap.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:

  BEGIN OF ty_alv_output,
    vbeln     TYPE vbak-vbeln,
    posnr     TYPE vbap-posnr,
    erdat     TYPE vbak-erdat,
    matnr     TYPE char18,       " Material Number
    kwmeng    TYPE vbap-kwmeng,  "quantity
    netwr     TYPE vbap-netwr,   "item net value
    kunnr     TYPE vbak-kunnr,
    name1     TYPE kna1-name1,
    land1     TYPE kna1-land1,
    glob_i    TYPE char1,
    bstkd     TYPE vbkd-bstkd,
  END OF ty_alv_output,

  BEGIN OF ty_vbap,
    vbeln     TYPE vbak-vbeln,    "Sales Order
    auart     TYPE vbak-auart,    "order type
    erdat     TYPE vbak-erdat,    "Order Creation Date
    vkorg     TYPE vbak-vkorg,    "Sales Organization
    vtweg     TYPE vbak-vtweg,    "Sales Channel
    kunnr     TYPE vbak-kunnr,
    posnr     TYPE vbap-posnr,    "Sales Item
    matnr     TYPE vbap-matnr,    "Material No.
    abgru     TYPE vbap-abgru,    "rejection reason code
    kwmeng    TYPE vbap-kwmeng,  "quantity
    netwr     TYPE vbap-netwr,   "item net value
  END OF ty_vbap,

  BEGIN OF ty_kna1,
    kunnr     TYPE kna1-kunnr,
    name1     TYPE kna1-name1,
    land1     TYPE kna1-land1,
  END OF ty_kna1,

  BEGIN OF ty_vbkd,
    vbeln     TYPE vbkd-vbeln,
    posnr     TYPE vbkd-posnr,
    bstkd     TYPE vbkd-bstkd,
  END OF ty_vbkd,
  BEGIN OF ty_mseg,
    mblnr   TYPE mseg-mblnr,    "Material Document Number
    mjahr   TYPE mseg-mjahr,    "Material Document Year
    zeile   TYPE mseg-zeile,    "Material Document Item
    bwart   TYPE mseg-bwart,    "Movement Type
    matnr   TYPE mseg-matnr,    "Material Number
    werks   TYPE mseg-werks,    "Plant
    kunnr   TYPE mseg-kunnr,    "Account Number of Customer
    bwtar   TYPE mseg-bwtar,    "Valuation Type
    kdauf   TYPE mseg-kdauf,    "Sales Order Number
    kdpos   TYPE mseg-kdpos,    "Item Number in Sales Order
    aufnr   TYPE mseg-aufnr,    "Order Number
  END OF ty_mseg,

  BEGIN OF ty_tvgrt,
    vkgrp    TYPE tvgrt-vkgrp,  "Sales Group
    bezei    TYPE tvgrt-bezei,  "Sales Group Name
  END OF ty_tvgrt,

  BEGIN OF ty_vbep,
    vbeln    TYPE vbep-vbeln,   "Sales Document
    posnr    TYPE vbep-posnr,   "Sales Document Item
    etenr    TYPE vbep-etenr,   "Delivery Schedule Line Number
    ettyp    TYPE vbep-ettyp,   "Schedule line category
    edatu    TYPE vbep-edatu,   "Requested delivery date
    wadat    TYPE vbep-wadat,   "Planned GI date
    bmeng    TYPE vbep-bmeng,   "Confirmed Quantity
  END OF ty_vbep,

  BEGIN OF ty_comp,
    rsnum     TYPE resb-rsnum,  "Number
    rspos     TYPE resb-rspos,  "Item Number
    vbeln_adv TYPE vbap-vbeln,  "Sales Document
    posnr_adv TYPE vbap-posnr,  "Sales Document Item
    sobkz     TYPE resb-sobkz,  "Special Stock Indicator
  END OF ty_comp.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_vbap     TYPE STANDARD TABLE OF ty_vbap,
  gt_kna1     TYPE STANDARD TABLE OF ty_kna1,
  gt_vbkd     TYPE STANDARD TABLE OF ty_vbkd,
  gt_alvdata  TYPE STANDARD TABLE OF ty_alv_output,
  gt_fieldcat TYPE slis_t_fieldcat_alv.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_alvdata  TYPE ty_alv_output,
  gs_disvar   TYPE disvariant,
  gs_layout   TYPE slis_layout_alv.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_col(10)  TYPE n,
  gv_text     TYPE string,
  gv_save     TYPE c,
  gv_callback TYPE slis_formname.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x                TYPE c VALUE 'X',
  gc_type_e           TYPE c VALUE 'E',
  gc_global           TYPE vbak-kunnr VALUE '0102000157',
  gc_callback_routine TYPE slis_formname VALUE 'USER_COMMAND'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-t02 .
SELECT-OPTIONS:
  s_vbeln      FOR vbak-vbeln,             "Material ID
  s_erdat      FOR vbak-erdat,
  s_matnr      FOR vbap-matnr,
  s_auart      FOR vbak-auart,
  s_vkorg      FOR vbak-vkorg,
  s_vtweg      FOR vbak-vtweg,
  s_abgru      FOR vbap-abgru,
  s_werks      FOR vbap-werks.
SELECTION-SCREEN END OF BLOCK b2.

* Alv variants
SELECTION-SCREEN BEGIN OF BLOCK b3 WITH FRAME TITLE text-t03.

PARAMETERS: p_vari TYPE disvariant-variant,       "Dispaly Variant
*** upgrade to backup data to server
  p_backup      TYPE char1 AS CHECKBOX DEFAULT 'X',
  p_path        TYPE rlgrap-filename
    NO-DISPLAY DEFAULT '/var/load/CP1/UK/original'.

SELECTION-SCREEN END OF BLOCK b3.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initilization
  PERFORM frm_init.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN                                                 *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.
* selection screen check
  PERFORM check_parameter.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN ON VALUE-REQUEST                                *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
  PERFORM f4_for_variant.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* progress indicator
  PERFORM process_info.
* get data
  PERFORM get_alv_data.
* alv property set
  PERFORM alv_prop_set.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
* ALV display
  PERFORM alv_display.
*** upgrade to backup data to server
  IF p_backup IS NOT INITIAL.
    IF sy-batch IS NOT INITIAL.
      PERFORM frm_backup2server.
    ENDIF.
  ENDIF.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do initilization
*----------------------------------------------------------------------*
FORM frm_init .

* initialize the internal table,workareas and variables
  REFRESH:
    gt_alvdata,
    gt_fieldcat.

  CLEAR:
    gs_alvdata,
    gs_disvar,
    gs_layout,
    gv_col,
    gv_text,
    gv_save,
    gv_callback.

  gv_save = 'A'.
* Set default alv layout
  gs_disvar-report = sy-repid.
  CALL FUNCTION 'REUSE_ALV_VARIANT_DEFAULT_GET'
    EXPORTING
      i_save        = gv_save
    CHANGING
      cs_variant    = gs_disvar
    EXCEPTIONS
      wrong_input   = 1
      not_found     = 2
      program_error = 3
      OTHERS        = 4.
  IF sy-subrc = 0.
    p_vari = gs_disvar-variant.
  ENDIF.

ENDFORM.                    " FRM_INIT

*&---------------------------------------------------------------------*
*&      Form  CHECK_PARAMETER
*&---------------------------------------------------------------------*
*       selection screen check
*----------------------------------------------------------------------*
FORM check_parameter .

  DATA: ls_disvar TYPE disvariant.

  IF p_vari IS NOT INITIAL.
    ls_disvar = gs_disvar.
    ls_disvar-variant = p_vari.
    CALL FUNCTION 'REUSE_ALV_VARIANT_EXISTENCE'
      EXPORTING
        i_save        = gv_save
      CHANGING
        cs_variant    = ls_disvar
      EXCEPTIONS
        wrong_input   = 1
        not_found     = 2
        program_error = 3
        OTHERS        = 4.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ELSE.
      gs_disvar = ls_disvar.
    ENDIF.
  ENDIF.

ENDFORM.                    " CHECK_PARAMETER
*&---------------------------------------------------------------------*
*&      Form  PROCESS_INFO
*&---------------------------------------------------------------------*
*       progress indicator
*----------------------------------------------------------------------*
FORM process_info .

  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    CONCATENATE 'Start report:'(t52) sy-repid
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t52.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 1
        text       = gv_text.
  ENDIF.

ENDFORM.                    " PROCESS_INFO
*&---------------------------------------------------------------------*
*&      Form  GET_ALV_DATA
*&---------------------------------------------------------------------*
*       get data
*----------------------------------------------------------------------*
FORM get_alv_data .
* Gain data from table VBAP- Header: Material Document
  PERFORM get_data_vbap.
  PERFORM get_data_vbkd_kna1.
* Generate ALV data for output
  PERFORM generate_alv_data.
* Edit all available
*  PERFORM edit_alv_data.
ENDFORM.                    " GET_ALV_DATA
*&---------------------------------------------------------------------*
*&      Form  ALV_PROP_SET
*&---------------------------------------------------------------------*
*       alv property set
*----------------------------------------------------------------------*
FORM alv_prop_set .

* Display ALV process indicator
  PERFORM alv_progress_indicator.

* FIELDCAT set
  PERFORM alv_fieldcat_set.

* LAYOUT set
  PERFORM alv_layout_set.

* Set Others
  PERFORM alv_others_set.

ENDFORM.                    " ALV_PROP_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_lines(10)  TYPE c.

  IF gt_alvdata IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-t61.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  IF sy-batch IS INITIAL.
    CLEAR: gv_text.
    DESCRIBE TABLE gt_alvdata LINES lv_lines.
    CONCATENATE 'Totally'(t62) lv_lines 'entries are generated'(t63)
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ENDIF.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'FRM_MY_STATUS'
      i_callback_user_command  = gv_callback
      is_layout                = gs_layout
      it_fieldcat              = gt_fieldcat
      i_save                   = gv_save
      is_variant               = gs_disvar
    TABLES
      t_outtab                 = gt_alvdata
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  FRM_MY_STATUS
*&---------------------------------------------------------------------*
*       Status Set
*----------------------------------------------------------------------*
*  --> I_EXTAB    STATUS
*----------------------------------------------------------------------*
FORM frm_my_status
  USING i_extab TYPE slis_t_extab.              "#EC CALLED "#EC NEEDED

  SET PF-STATUS '9100'.

ENDFORM.        "FRM_MY_STATUS

*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM alv_fieldcat_set .

** Create Fieldcatalogue from internal table

  CLEAR gv_col.

* Field definitions
  PERFORM add_field USING:
    'VBELN'    10  'Sales Doc.'(s01) 'Sales Document'(l01) 'X',
    'POSNR'    6   'Item'(s02)       'SO Item'(l02) 'X',
    'ERDAT'    8   'Create.On'(s03)  'SO creation date'(l03)   ' ',
    'MATNR'    18  'Material'(s04)   'Material Number'(l04)   ' ',
    'KWMENG'   19  'Quantity'(s05)   'Order Quantity'(l05)   ' ',
    'NETWR'    21  'Net Value'(s06)  'Item Net Value'(l06) ' ',
    'KUNNR'    10  'Customer'(s07)   'Customer Code'(l07) ' ',
    'NAME1'    35  'Cstm.Name'(s08)  'Customer Name'(l08) ' ',
    'LAND1'    3   'Country'(s09)    'Customer Country'(l09) ' ',
    'GLOB_I'   1 'Global.Dis'(s10) 'Sold Via Global Dispatch'(l10) ' ',
    'BSTKD'    35  'PO number'(s11)  'Customer PO number'(l11) ' '.

ENDFORM.                    " ALV_FIELDCAT_SET

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr_s
                         p_descr_l
                         p_hotsp.
  DATA:
     ls_fieldcat TYPE slis_fieldcat_alv.

  gv_col = gv_col + 1.

  CLEAR ls_fieldcat.
  ls_fieldcat-col_pos    = gv_col.
  ls_fieldcat-fieldname  = p_field.
  ls_fieldcat-outputlen  = p_len.
  ls_fieldcat-seltext_l  = p_descr_l.
  ls_fieldcat-seltext_m  = p_descr_l.
  ls_fieldcat-seltext_s  = p_descr_s.
  ls_fieldcat-fix_column = 'X'.
  ls_fieldcat-emphasize  = 'X'.
  ls_fieldcat-hotspot    = p_hotsp.

* Suppress leading zeros for the material number
  IF p_field = 'MATNR'.
    ls_fieldcat-edit_mask = '==MATN1'.
  ENDIF.

  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   "Auto optimize column width
  gs_layout-detail_popup = gc_x.        "Show detail screen

ENDFORM.                    " ALV_LAYOUT_SET

*&---------------------------------------------------------------------*
*&      Form  GENERATE_ALV_DATA
*&---------------------------------------------------------------------*
*       Generate ALV data for output
*----------------------------------------------------------------------*
FORM generate_alv_data .

  DATA:
    ls_kna1    TYPE ty_kna1,
    ls_vbkd    TYPE ty_vbkd,
    ls_vbap    TYPE ty_vbap.

  FIELD-SYMBOLS:
    <lfs_alvdata> TYPE ty_alv_output.

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to generate ALV data'(t64).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t64.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 90
        text       = gv_text.
  ENDIF.

  LOOP AT gt_vbap INTO ls_vbap.
    gs_alvdata-vbeln  =   ls_vbap-vbeln.
    gs_alvdata-posnr  =   ls_vbap-posnr.
    gs_alvdata-erdat  =   ls_vbap-erdat.
    gs_alvdata-matnr  =   ls_vbap-matnr.  " Material Number
    gs_alvdata-kwmeng =   ls_vbap-kwmeng.  "quantity
    gs_alvdata-netwr  =   ls_vbap-netwr.   "item net value
    gs_alvdata-kunnr  =   ls_vbap-kunnr.
    READ TABLE gt_kna1 INTO ls_kna1
      WITH KEY kunnr = ls_vbap-kunnr.
    IF sy-subrc = 0.
      gs_alvdata-name1 = ls_kna1-name1.
      gs_alvdata-land1 = ls_kna1-land1.
    ENDIF.
    IF ls_vbap-kunnr = gc_global.
      gs_alvdata-glob_i = 'Y'.
    ENDIF.
    READ TABLE gt_vbkd INTO ls_vbkd
      WITH KEY  vbeln = ls_vbap-vbeln
                posnr = ls_vbap-posnr.
    gs_alvdata-bstkd    = ls_vbkd-bstkd.
    APPEND  gs_alvdata TO gt_alvdata.
  ENDLOOP.
  LOOP AT gt_alvdata ASSIGNING <lfs_alvdata>.
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
      EXPORTING
        input  = <lfs_alvdata>-vbeln
      IMPORTING
        output = <lfs_alvdata>-vbeln.
    CALL FUNCTION 'CONVERSION_EXIT_MATN1_OUTPUT'
      EXPORTING
        input  = <lfs_alvdata>-matnr
      IMPORTING
        output = <lfs_alvdata>-matnr.
  ENDLOOP.

ENDFORM.                    " GENERATE_ALV_DATA

*&---------------------------------------------------------------------*
*&      Form  ALV_PROGRESS_INDICATOR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM alv_progress_indicator .

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to set ALV property'(t60).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t60.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 85
        text       = gv_text.
  ENDIF.

ENDFORM.                    " ALV_PROGRESS_INDICATOR

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       Set Others
*----------------------------------------------------------------------*
FORM alv_others_set .

  gv_callback = gc_callback_routine.

ENDFORM.                    " ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*&      Form  F4_FOR_VARIANT
*&---------------------------------------------------------------------*
*       F4 help for display variant
*----------------------------------------------------------------------*
FORM f4_for_variant .

  DATA: ls_disvar TYPE disvariant,
        lv_exit   TYPE c.

  ls_disvar-report = sy-repid.

  CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
    EXPORTING
      is_variant    = ls_disvar
      i_save        = gv_save
    IMPORTING
      e_exit        = lv_exit
      es_variant    = gs_disvar
    EXCEPTIONS
      not_found     = 1
      program_error = 2
      OTHERS        = 3.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    IF lv_exit = space.
      p_vari = gs_disvar-variant.
    ENDIF.
  ENDIF.

ENDFORM.                    " F4_FOR_VARIANT

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       USER_COMMAND
*----------------------------------------------------------------------*
FORM user_command USING u_ucomm     LIKE sy-ucomm           "#EC CALLED
                        p_selfield  TYPE slis_selfield.

  CASE u_ucomm.
    WHEN '&IC1'.        " SAP standard code for double-clicking
      IF p_selfield-fieldname = 'VBELN'.
        IF p_selfield-value IS NOT INITIAL.
          SET PARAMETER ID 'AUN' FIELD p_selfield-value.
          CALL TRANSACTION 'VA03'.
        ENDIF.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.

ENDFORM.                    " USER_COMMAND
*&---------------------------------------------------------------------*
*&      Form  EDIT_ALV_DATA
*&---------------------------------------------------------------------*
*       Edit all available
*----------------------------------------------------------------------*
*FORM edit_alv_data .
*
*ENDFORM.                    " EDIT_ALV_DATA
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_VBAP
*&---------------------------------------------------------------------*
*       Gain data from table MKPF - Header: Material Document
*----------------------------------------------------------------------*
FORM get_data_vbap.

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to extract data from table VBAP'(t81).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t81.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 50
        text       = gv_text.
  ENDIF.

  SELECT  vbak~vbeln     "Sales Order
          vbak~auart     "order type
          vbak~erdat     "Order Creation Date
          vbak~vkorg     "Sales Organization
          vbak~vtweg     "Sales Channel
          vbak~kunnr     "Customer
          vbap~posnr     "Sales Item
          vbap~matnr     "Material No.
          vbap~abgru     "rejection reason code
          vbap~kwmeng   "quantity
          vbap~netwr    "item net value
    FROM vbak
    INNER JOIN vbap
    ON vbak~vbeln = vbap~vbeln
    INTO TABLE gt_vbap
    WHERE vbak~vbeln IN s_vbeln
      AND vbak~auart IN s_auart
      AND vbak~erdat IN s_erdat
      AND vbak~vkorg IN s_vkorg
      AND vbak~vtweg IN s_vtweg
      AND vbap~matnr IN s_matnr
      AND vbap~abgru IN s_abgru
      AND vbap~werks IN s_werks.
  IF sy-subrc <> 0.
    IF sy-batch IS NOT INITIAL.
      CLEAR: gv_text.
      gv_text ='No data exist in table VBAP'(t55).
      MESSAGE i000(yse_sales_log) WITH gv_text.
    ELSE.
      MESSAGE s000(yse_sales_log) WITH text-t55 DISPLAY LIKE 'E'.
      LEAVE LIST-PROCESSING.
    ENDIF.
  ENDIF.
ENDFORM.                    " GET_DATA_VBAP
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_VBKD_KNA1
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_data_vbkd_kna1 .

  CHECK gt_vbap IS NOT INITIAL.
* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to extract data from table KNA1'(t93).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t93.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 70
        text       = gv_text.
  ENDIF.

  SELECT  kunnr
          name1
          land1
    FROM kna1
    INTO TABLE gt_kna1
    FOR ALL ENTRIES IN gt_vbap
    WHERE kunnr = gt_vbap-kunnr.

  SELECT  vbeln
          posnr
          bstkd
    FROM vbkd
    INTO TABLE gt_vbkd
    FOR ALL ENTRIES IN gt_vbap
    WHERE vbeln = gt_vbap-vbeln
      AND posnr = gt_vbap-posnr.

ENDFORM.                    " GET_DATA_VBKD_KNA1

*** upgrade to backup data to server
*&---------------------------------------------------------------------*
*&      Form  FRM_BACKUP2SERVER
*&---------------------------------------------------------------------*
*       Send the data to server
*----------------------------------------------------------------------*
FORM frm_backup2server .

  DATA: lv_path       TYPE rlgrap-filename,
        lv_line(4096) TYPE c,
        ls_output     TYPE ty_alv_output,
        lv_kwmeng      TYPE string,
        lv_netwr      TYPE string,
        lv_plifz      TYPE string,
        lv_tab        TYPE char1
          VALUE cl_abap_char_utilities=>horizontal_tab.
  CLEAR: lv_line.

* Build filename
  CONCATENATE p_path
              '/YSE_ICOS_Hisitory'
*              sy-uname '_' sy-datum '_' sy-uzeit
              '.xls'
         INTO lv_path.
  REPLACE FIRST OCCURRENCE OF 'CP1' IN lv_path WITH sy-sysid.

* Open file
  OPEN DATASET lv_path FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: 'Error opening file:'(026) , lv_path.
    EXIT.
  ENDIF.

  CONCATENATE text-l01    text-l02
              text-l03    text-l04
              text-l05    text-l06
              text-l07    text-l08
              text-l09    text-l10
              text-l11
       INTO   lv_line SEPARATED BY lv_tab.
  TRANSFER lv_line TO lv_path.

  LOOP AT gt_alvdata INTO ls_output.
    CLEAR: lv_line,lv_kwmeng,lv_netwr.
    lv_netwr  = ls_output-netwr.
    lv_kwmeng = ls_output-kwmeng.
    CONCATENATE   ls_output-vbeln  ls_output-posnr
                  ls_output-erdat  ls_output-matnr
                  lv_kwmeng  lv_netwr
                  ls_output-kunnr   ls_output-name1
                  ls_output-land1  ls_output-glob_i
                  ls_output-bstkd
         INTO   lv_line SEPARATED BY lv_tab.
    TRANSFER lv_line TO lv_path.
  ENDLOOP.

  CLOSE DATASET lv_path.
  WRITE: 'Report output written to file'(027),
         AT 35 lv_path.

ENDFORM.                    " FRM_BACKUP2SERVER

*Text symbol text£º
*026:Error opening file:
*027:Report output written to file
*L01:Sales Document
*L02:SO Item
*L03:SO creation date
*L04:Material Number
*L05:Order Quantity
*L06:Item Net Value
*L07:Customer Code
*L08:Customer Name
*L09:Customer Country
*L10:Sold Via Global Dispatch
*L11:Customer PO number
*S01:Sales Doc.
*S02:Item
*S03:Create.On
*S04:Material
*S05:Quantity
*S06:Net Value
*S07:Customer
*S08:Cstm.Name
*S09:Country
*S10:Global.Dis
*S11:PO number
*T02:Selection Screen
*T03:Layout
*T52:Start report:
*T55:No data exist in table VBAP
*T60:Begin to set ALV property
*T61:No data exist
*T62:Totally
*T63:entries are generated
*T64:Begin to generate ALV data
*T81:Begin to extract data from table VBAP

*T93:Begin to extract data from table KNA1
*Selection text£º
*P_BACKUP:        Backup data to Server
*P_VARI:D       .
*S_ABGRU:D       .
*S_AUART:D       .
*S_ERDAT:D       .
*S_MATNR:D       .
*S_VBELN:D       .
*S_VKORG:D       .
*S_VTWEG:D       .
*S_WERKS:D       .
