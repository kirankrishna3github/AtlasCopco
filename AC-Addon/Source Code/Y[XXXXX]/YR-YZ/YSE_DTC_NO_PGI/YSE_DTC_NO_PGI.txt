*----------------------------------------------------------------------*
* PROGRAM ID           : YSE_DTC_NO_PGI                                *
* PROGRAM TITLE        : Reporting of CC DN without PGI document       *
* AUTHOR               : Anda Wu                                       *
* DATE                 : 23/05/2014                                    *
* DEVELOPMENT ID       :  CD1K981658                                   *
* CHANGE REQUEST NUMBER:   CR3262                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG
*----------------------------------------------------------------------*
* MOD. NO.|  DATE      | NAME               | CORR. NR.  | CHANGE REF. *
* MOD-001  2014.05.27    Anda Wu             CD1K981711    CR3262
************************************************************************
REPORT  yse_dtc_no_pgi.

************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES:   likp,
          eban.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:

  BEGIN OF ty_likp,
    vbeln         TYPE likp-vbeln,
    erdat         TYPE likp-erdat,
    vkorg         TYPE likp-vkorg,
  END OF ty_likp,

  BEGIN OF ty_lips,
    vbeln         TYPE lips-vbeln,
    posnr         TYPE lips-posnr,
    pstyv         TYPE lips-pstyv,
    werks         TYPE lips-werks,
  END OF ty_lips,

  BEGIN OF ty_vbfa,
    vbelv         TYPE vbfa-vbelv,
    posnv         TYPE vbfa-posnv,
    vbeln         TYPE vbfa-vbeln,
    posnn         TYPE vbfa-posnn,
    vbtyp_n       TYPE vbfa-vbtyp_n,
    auart         TYPE vbak-auart,
  END OF ty_vbfa,

  BEGIN OF ty_vbep,
    vbeln         TYPE vbep-vbeln,
    posnr         TYPE vbep-posnr,
    banfn         TYPE vbep-banfn,
    bnfpo         TYPE vbep-bnfpo,
    flief         TYPE eban-flief,
  END OF ty_vbep,

  BEGIN OF ty_vbkd,
    vbeln         TYPE vbkd-vbeln,
    posnr         TYPE vbkd-posnr,
    ihrez         TYPE vbkd-ihrez,
  END OF ty_vbkd,

  BEGIN OF  ty_alv_output,
    sel           TYPE char1,
    vkorg         TYPE likp-vkorg,
    vbeln_1       TYPE vbap-vbeln,
    posnr_1       TYPE vbap-posnr,
    ihrez         TYPE vbkd-ihrez,
    vbeln         TYPE lips-vbeln,
    posnr         TYPE lips-posnr,
    pstyv         TYPE lips-pstyv,
    erdat         TYPE likp-erdat,
    vbeln_f       TYPE vbfa-vbeln,
    vbtyp_n       TYPE vbfa-vbtyp_n,
  END OF ty_alv_output.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_likp         TYPE STANDARD TABLE OF ty_likp,
  gt_pro_log      TYPE STANDARD TABLE OF string,
  gt_lips         TYPE STANDARD TABLE OF ty_lips,
  gt_vbkd         TYPE STANDARD TABLE OF ty_vbkd,
  gt_vbfa_j       TYPE STANDARD TABLE OF ty_vbfa,
  gt_vbfa_r       TYPE STANDARD TABLE OF ty_vbfa,
  gt_vbep         TYPE STANDARD TABLE OF ty_vbep,
  gt_fieldcat     TYPE slis_t_fieldcat_alv,
  gt_del          TYPE STANDARD TABLE OF ty_alv_output,
  gt_alv_output   TYPE STANDARD TABLE OF ty_alv_output.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_disvar           TYPE disvariant,
  gs_layout           TYPE slis_layout_alv.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_text             TYPE string,
  gv_save             TYPE char1,
  gv_pf_status        TYPE slis_formname,
  gv_callback         TYPE slis_formname.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x                TYPE char1 VALUE 'X',
  gc_pstyv_zdtc       TYPE lips-pstyv   VALUE 'ZDTC',
  gc_pstyv_zas2       TYPE lips-pstyv   VALUE 'ZAS2',
  gc_werks_cn90       TYPE lips-werks   VALUE 'CN90',
  gc_auart_zor        TYPE vbak-auart   VALUE 'ZOR',
  gc_auart_zo03       TYPE vbak-auart   VALUE 'ZO03',
  gc_vbtyp_j          TYPE char1 VALUE 'J',
  gc_vbtyp_r          TYPE char1 VALUE 'R',
  gc_type_e           TYPE char1 VALUE 'E',
  gc_type_s           TYPE char1 VALUE 'S',
  gc_type_hifn        TYPE char1 VALUE '-'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.

PARAMETERS:
  p_vari    TYPE disvariant-variant
            MODIF ID m1.         "Dispaly Variant

SELECT-OPTIONS:
  s_vkorg     FOR   likp-vkorg OBLIGATORY,     "Sales Organization
  s_flief     FOR   eban-flief OBLIGATORY,     "Desired Vendor
  s_erdat     FOR   likp-erdat.                "Delivery Creation Date

PARAMETERS:
  p_del       TYPE char1 AS CHECKBOX.           "Delete DN after ALV

SELECTION-SCREEN END OF BLOCK b1.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initilization
  PERFORM frm_init.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN OUTPUT                                          *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.
* screen initial set
  PERFORM frm_screen_set.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN                                                 *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.
* selection screen check
*  PERFORM check_parameter.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN ON VALUE-REQUEST                                *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
* F4 help of ALV layout
  PERFORM f4_for_variant.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* progress indicator
  PERFORM process_info.
* Extract data from table and generate AVL output Data
  PERFORM extract_data.
* alv property set
  PERFORM alv_prop_set.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
* ALV display
  PERFORM alv_display.
* Delete delivery notes
  IF p_del IS NOT INITIAL.
    PERFORM del_nopgi_dn.
  ENDIF.
* Display process log
  PERFORM show_proc_log.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do initilization
*----------------------------------------------------------------------*
FORM frm_init .

  REFRESH:  gt_likp,
            gt_lips,
            gt_pro_log,
            gt_vbkd,
            gt_vbfa_j,
            gt_vbfa_r,
            gt_vbep,
            gt_fieldcat,
            gt_alv_output.

  CLEAR:    gs_disvar,
            gs_layout,
            gv_text,
            gv_save,
            gv_callback.

  s_vkorg-sign    = 'I'.
  s_vkorg-option  = 'EQ'.
  s_vkorg-low     = 'CN01'.
  APPEND s_vkorg.

  s_vkorg-sign    = 'I'.
  s_vkorg-option  = 'EQ'.
  s_vkorg-low     = 'CN05'.
  APPEND s_vkorg.

ENDFORM.                    " FRM_INIT

*&---------------------------------------------------------------------*
*&      Form  FRM_SCREEN_SET
*&---------------------------------------------------------------------*
*       screen initial set
*----------------------------------------------------------------------*
FORM frm_screen_set .

  LOOP AT SCREEN.
    IF screen-group1 = 'M1'.
      screen-invisible = '1'.
      screen-input   = '0'.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " FRM_SCREEN_SET

*&---------------------------------------------------------------------*
*&      Form  F4_FOR_VARIANT
*&---------------------------------------------------------------------*
*       F4 help of ALV layout
*----------------------------------------------------------------------*
FORM f4_for_variant .

  DATA: ls_disvar TYPE disvariant,
        lv_exit   TYPE char1.

  ls_disvar-report = sy-repid.

  CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
    EXPORTING
      is_variant    = ls_disvar
      i_save        = gv_save
    IMPORTING
      e_exit        = lv_exit
      es_variant    = gs_disvar
    EXCEPTIONS
      not_found     = 1
      program_error = 2
      OTHERS        = 3.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    IF lv_exit = space.
      p_vari = gs_disvar-variant.
    ENDIF.
  ENDIF.

ENDFORM.                    " F4_FOR_VARIANT

*&---------------------------------------------------------------------*
*&      Form  PROCESS_INFO
*&---------------------------------------------------------------------*
*       progress indicator
*----------------------------------------------------------------------*
FORM process_info .

  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    CONCATENATE 'Start report:'(004) sy-repid
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-005.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 1
        text       = gv_text.
  ENDIF.

ENDFORM.                    " PROCESS_INFO
*&---------------------------------------------------------------------*
*&      Form  GET_MASTER_DATA
*&---------------------------------------------------------------------*
*       get data
*----------------------------------------------------------------------*
FORM get_master_data .

  SELECT   vbeln
           erdat
           vkorg
    FROM   likp
    INTO TABLE gt_likp
    WHERE vkorg IN s_vkorg
      AND erdat IN s_erdat.
  IF sy-subrc <> 0.
    CLEAR: gv_text.
    gv_text = text-051.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  SELECT    vbeln
            posnr
            pstyv
            werks
    FROM    lips
    INTO TABLE gt_lips
    FOR ALL ENTRIES IN gt_likp
    WHERE vbeln  = gt_likp-vbeln
      AND (   pstyv  = gc_pstyv_zdtc
           OR pstyv  = gc_pstyv_zas2 )
      AND werks  = gc_werks_cn90.
  IF sy-subrc <> 0.
    CLEAR: gv_text.
    gv_text = text-053.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  SELECT  vbfa~vbelv
          vbfa~posnv
          vbfa~vbeln
          vbfa~posnn
          vbfa~vbtyp_n
          vbak~auart
    FROM vbfa
      INNER JOIN vbak
      ON vbfa~vbelv = vbak~vbeln
    INTO TABLE gt_vbfa_j
    FOR ALL ENTRIES IN gt_lips
    WHERE vbfa~vbeln = gt_lips-vbeln
      AND vbfa~posnn = gt_lips-posnr
      AND vbfa~vbtyp_n = gc_vbtyp_j
      AND (     vbak~auart = gc_auart_zor
            OR  vbak~auart = gc_auart_zo03 ).
  IF gt_vbfa_j IS NOT INITIAL.
    SELECT   vbep~vbeln
             vbep~posnr
             vbep~banfn
             vbep~bnfpo
             eban~flief
      FROM vbep
        INNER JOIN eban
        ON  vbep~banfn = eban~banfn
        AND vbep~bnfpo = eban~bnfpo
      INTO TABLE gt_vbep
      FOR ALL ENTRIES IN gt_vbfa_j
      WHERE vbep~vbeln = gt_vbfa_j-vbelv
        AND vbep~posnr = gt_vbfa_j-posnv
        AND eban~flief IN s_flief.
    IF sy-subrc <> 0.
      CLEAR: gv_text.
      gv_text = text-055.
      MESSAGE s000(yse_sales_log) WITH gv_text
        DISPLAY LIKE gc_type_e.
      LEAVE LIST-PROCESSING.
    ENDIF.
    SELECT  vbeln
            posnr
            ihrez
      FROM  vbkd
      INTO TABLE gt_vbkd
      FOR ALL ENTRIES IN gt_vbfa_j
      WHERE vbeln = gt_vbfa_j-vbelv
        AND posnr = gt_vbfa_j-posnv.
  ENDIF.

  SELECT  vbelv
          posnv
          vbeln
          posnn
          vbtyp_n
    FROM vbfa
    INTO TABLE gt_vbfa_r
    FOR ALL ENTRIES IN gt_lips
    WHERE vbelv = gt_lips-vbeln
      AND posnv = gt_lips-posnr
      AND vbtyp_n = gc_vbtyp_r.

ENDFORM.                    " GET_MASTER_DATA
*&---------------------------------------------------------------------*
*&      Form  ALV_PROP_SET
*&---------------------------------------------------------------------*
*       alv property set
*----------------------------------------------------------------------*
FORM alv_prop_set .

* Display ALV process indicator
  PERFORM alv_progress_indicator.

* FIELDCAT set
  PERFORM alv_fieldcat_set.

* LAYOUT set
  PERFORM alv_layout_set.

** Set Others
  PERFORM alv_others_set.

ENDFORM.                    " ALV_PROP_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_lines(10)  TYPE c.

  IF gt_alv_output IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-060.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  IF sy-batch IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-010.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 80
        text       = gv_text.
  ELSE.
    CLEAR: gv_text.
    DESCRIBE TABLE gt_alv_output LINES lv_lines.
    CONCATENATE 'Totally'(015) lv_lines 'entries are generated'(020)
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ENDIF.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = gv_pf_status
      i_callback_user_command  = gv_callback
      is_layout                = gs_layout
      it_fieldcat              = gt_fieldcat
      i_save                   = gv_save
      is_variant               = gs_disvar
    TABLES
      t_outtab                 = gt_alv_output
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY
*&---------------------------------------------------------------------*
*&      Form  ALV_PROGRESS_INDICATOR
*&---------------------------------------------------------------------*
*       Display ALV process indicator
*----------------------------------------------------------------------*
FORM alv_progress_indicator .

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to set ALV property'(011).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-011.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 75
        text       = gv_text.
  ENDIF.

ENDFORM.                    " ALV_PROGRESS_INDICATOR
*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM alv_fieldcat_set .

  DATA:
     lv_linecnt  TYPE i,
     ls_fieldcat TYPE slis_fieldcat_alv.

  CLEAR lv_linecnt.
* Sales Org
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'VKORG'.
  ls_fieldcat-ref_fieldname = 'VKORG'.
  ls_fieldcat-ref_tabname = 'LIKP'.
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* Sales Order
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'VBELN_1'.
  ls_fieldcat-ref_fieldname = 'VBELN'.
  ls_fieldcat-ref_tabname = 'VBAP'.
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* SO item
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'POSNR_1'.
  ls_fieldcat-ref_fieldname = 'POSNR'.
  ls_fieldcat-ref_tabname = 'VBAP'.
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* Service Order
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'IHREZ'.
  ls_fieldcat-seltext_s = 'Serv.Doc'(033).
  ls_fieldcat-seltext_m = 'Service Order'(034).
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* DN Number
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'VBELN'.
  ls_fieldcat-hotspot   = gc_x.
  ls_fieldcat-ref_fieldname = 'VBELN'.
  ls_fieldcat-ref_tabname = 'LIPS'.
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* DN Item Number
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'POSNR'.
  ls_fieldcat-ref_fieldname = 'POSNR'.
  ls_fieldcat-ref_tabname = 'LIPS'.
  APPEND ls_fieldcat TO gt_fieldcat.

* DN item Category
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'PSTYV'.
  ls_fieldcat-ref_fieldname = 'PSTYV'.
  ls_fieldcat-ref_tabname = 'LIPS'.
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* DN Creation Date
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'ERDAT'.
  ls_fieldcat-ref_fieldname = 'ERDAT'.
  ls_fieldcat-ref_tabname = 'LIKP'.
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* Following document number
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'VBELN_F'.
  ls_fieldcat-seltext_s = 'Foll.Doc'(025).
  ls_fieldcat-seltext_m = 'Follow Document'(026).
  ls_fieldcat-seltext_l = 'Following Document Number'(027).
  APPEND ls_fieldcat TO gt_fieldcat.

* Following document category
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'VBTYP_N'.
  ls_fieldcat-seltext_s = 'Foll.Cate'(028).
  ls_fieldcat-seltext_m = 'Follow Category'(030).
  ls_fieldcat-seltext_l = 'Following Document Category'(031).
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   " Auto optimize column width
  gs_layout-detail_popup = gc_x.        " Show detail screen
  gs_layout-box_fieldname  = 'SEL'.

ENDFORM.                    " ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*&      Form  GEN_ALV_DATA
*&---------------------------------------------------------------------*
*       generate alv data
*----------------------------------------------------------------------*
FORM gen_alv_data .

  DATA: ls_likp     TYPE  ty_likp,
        ls_lips     TYPE  ty_lips,
        ls_vbfa_j   TYPE  ty_vbfa,
        ls_vbfa_r   TYPE  ty_vbfa,
        ls_vbep     TYPE  ty_vbep,
        ls_vbkd     TYPE  ty_vbkd,
        ls_alv_out  TYPE  ty_alv_output.

  LOOP AT gt_lips INTO ls_lips.
    CLEAR: ls_alv_out.
    ls_alv_out-vbeln      = ls_lips-vbeln.
    ls_alv_out-posnr      = ls_lips-posnr.
    ls_alv_out-pstyv      = ls_lips-pstyv.
    CLEAR: ls_likp.
    READ TABLE gt_likp INTO ls_likp
        WITH KEY vbeln = ls_lips-vbeln.
    IF sy-subrc = 0.
      ls_alv_out-erdat = ls_likp-erdat.
      ls_alv_out-vkorg = ls_likp-vkorg.
    ENDIF.
    CLEAR: ls_vbfa_j.
    READ TABLE  gt_vbfa_j INTO ls_vbfa_j
        WITH KEY  vbeln   = ls_lips-vbeln
                  posnn   = ls_lips-posnr.
    IF sy-subrc <> 0.
      CONTINUE.
    ELSE.
      ls_alv_out-vbeln_1 = ls_vbfa_j-vbelv.
      ls_alv_out-posnr_1 = ls_vbfa_j-posnv.
    ENDIF.
    CLEAR:  ls_vbkd.
    READ TABLE gt_vbkd  INTO ls_vbkd
        WITH KEY vbeln = ls_vbfa_j-vbelv
                 posnr = ls_vbfa_j-posnv.
    IF sy-subrc = 0.
      ls_alv_out-ihrez = ls_vbkd-ihrez.
    ENDIF.
    READ TABLE gt_vbep  INTO ls_vbep
        WITH KEY vbeln = ls_vbfa_j-vbelv
                 posnr = ls_vbfa_j-posnv.
    IF sy-subrc <> 0.
      CONTINUE.
    ENDIF.
    CLEAR: ls_vbfa_r.
    READ TABLE  gt_vbfa_r INTO ls_vbfa_r
        WITH KEY   vbelv = ls_lips-vbeln
                   posnv = ls_lips-posnr.
    ls_alv_out-vbeln_f  = ls_vbfa_r-vbeln.
    ls_alv_out-vbtyp_n  = ls_vbfa_r-vbtyp_n.
    APPEND ls_alv_out TO gt_alv_output.
    IF  ls_alv_out-vbeln_f IS INITIAL
      AND p_del IS NOT INITIAL.
      APPEND ls_alv_out TO gt_del.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " GEN_ALV_DATA
*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       SET CALLBACK_USER_COMMAND EVENTS
*----------------------------------------------------------------------*
FORM alv_others_set .

  gv_pf_status = 'SET_PF_STATS'.
  gv_callback  = 'CALLBACK_USERCOMMAND'.

ENDFORM.                    " ALV_OTHERS_SET

*&---------------------------------------------------------------------*
*&      Form  SET_PF_STATS
*&---------------------------------------------------------------------*
*       SET PF Status
*----------------------------------------------------------------------*
FORM set_pf_stats                                           "#EC CALLED
    USING rt_extab TYPE slis_t_extab.                       "#EC NEEDED

  SET PF-STATUS 'PGI_SCREEN'.

ENDFORM.                    " SET_PF_STATS

*&---------------------------------------------------------------------*
*&      Form  CALLBACK_USERCOMMAND
*&---------------------------------------------------------------------*
*       Handle events
*----------------------------------------------------------------------*
FORM callback_usercommand USING r_ucomm LIKE sy-ucomm       "#EC CALLED
                                rs_selfield TYPE slis_selfield.

  DATA: ls_alv_out  TYPE ty_alv_output.
  CASE r_ucomm.
    WHEN 'DEL'. " Delete select entries
      REFRESH: gt_del.
      LOOP AT gt_alv_output INTO ls_alv_out
          WHERE sel IS NOT INITIAL.
        APPEND ls_alv_out TO gt_del.
      ENDLOOP.
      PERFORM frm_del_delivery.
    WHEN '&IC1'. " SAP standard code for double-clicking
      IF rs_selfield-fieldname = 'VBELN'.
        IF rs_selfield-value IS NOT INITIAL.
          SET PARAMETER ID 'VL' FIELD rs_selfield-value.
          CALL TRANSACTION 'VL02N' AND SKIP FIRST SCREEN.
        ENDIF.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.


ENDFORM.                    " CALLBACK_USERCOMMAND
*&---------------------------------------------------------------------*
*&      Form  EXTRACT_DATA
*&---------------------------------------------------------------------*
*       Extract data from table and generate AVL output Data
*----------------------------------------------------------------------*
FORM extract_data .

* get data
  PERFORM get_master_data.
* generate alv data
  PERFORM gen_alv_data.

ENDFORM.                    " EXTRACT_DATA
*&---------------------------------------------------------------------*
*&      Form  DEL_NOPGI_DN
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM del_nopgi_dn .

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to delete the DNs which hava no PGI'(045).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-045.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 95
        text       = gv_text.
  ENDIF.
* Delete no PGI DN
  PERFORM frm_del_delivery.

ENDFORM.                    " DEL_NOPGI_DN
*&---------------------------------------------------------------------*
*&      Form  FRM_DEL_DELIVERY
*&---------------------------------------------------------------------*
*       Delete no PGI DN
*----------------------------------------------------------------------*
FORM frm_del_delivery .

  DATA: ls_del  TYPE ty_alv_output,
        ls_header_data  TYPE bapiobdlvhdrchg,
        ls_control      TYPE bapiobdlvhdrctrlchg,
        ls_return       TYPE bapiret2,
        lt_return       TYPE STANDARD TABLE OF bapiret2.
  IF gt_del IS INITIAL.
    CLEAR: gv_text.
    gv_text ='No entries to be deleted'(046).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    SORT gt_del BY vbeln posnr.
    DELETE ADJACENT DUPLICATES FROM gt_del
      COMPARING vbeln.
    LOOP AT gt_del INTO ls_del.
      REFRESH: lt_return.
      ls_header_data-deliv_numb = ls_del-vbeln.
      ls_control-deliv_numb     = ls_del-vbeln.
      ls_control-dlv_del        = gc_x.

      CALL FUNCTION 'BAPI_OUTB_DELIVERY_CHANGE'
        EXPORTING
          header_data    = ls_header_data
          header_control = ls_control
          delivery       = ls_del-vbeln
        TABLES
          return         = lt_return.
      LOOP AT lt_return INTO ls_return
                      WHERE type = gc_type_e.
        CLEAR: gv_text.
        MESSAGE ID ls_return-id TYPE ls_return-type
            NUMBER ls_return-number
            WITH ls_return-message_v1
                 ls_return-message_v2
                 ls_return-message_v3
                 ls_return-message_v4
            INTO gv_text.
        CONCATENATE gc_type_e gc_type_hifn ls_del-vbeln
                    gc_type_hifn gv_text
                    INTO gv_text
          SEPARATED BY space.
        APPEND gv_text TO gt_pro_log.
      ENDLOOP.
      IF sy-subrc <> 0.
        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
          EXPORTING
            wait = 'X'.
        CONCATENATE gc_type_s gc_type_hifn ls_del-vbeln
          gc_type_hifn 'Successfully deleted!'(088)
          INTO gv_text SEPARATED BY space.
        APPEND gv_text TO gt_pro_log.
      ENDIF.
    ENDLOOP.
  ENDIF.
ENDFORM.                    " FRM_DEL_DELIVERY
*&---------------------------------------------------------------------*
*&      Form  SHOW_PROC_LOG
*&---------------------------------------------------------------------*
*       Display process log
*----------------------------------------------------------------------*
FORM show_proc_log .

  IF sy-batch IS NOT INITIAL.
    LOOP AT gt_pro_log INTO gv_text.
      MESSAGE i000(yse_sales_log) WITH gv_text.
      CLEAR: gv_text.
    ENDLOOP.
  ELSE.
    IF gt_pro_log IS NOT INITIAL.
      LOOP AT gt_pro_log INTO gv_text.
        WRITE:/ gv_text.
        CLEAR: gv_text.
      ENDLOOP.
    ENDIF.
  ENDIF.

ENDFORM.                    " SHOW_PROC_LOG

*Text symbol text£º
*004:Start report:
*005:Start Processing...
*010:Start to display ALV
*011:Begin to set ALV property
*015:Totally
*020:entries are generated
*025:Foll.Doc
*026:Follow Document
*027:Following Document Number
*028:Foll.Cate
*030:Follow Category
*031:Following Document Category
*033:Serv.Doc
*034:Service Order
*045:Begin to delete the DNs which hava no PGI
*046:No entries to be deleted
*051:No delivery exist (LIKP) !
*053:No delivery item exist (LIPS)!
*055:No  Schedule Line Data exist for the fix vendor (VBEP)!
*060:No data extracted by selection screen
*088:Successfully deleted!

*T01:Selection Conditions
*Selection text£º
*P_DEL:        Delete No PGI Delivery?
*P_VARI:        ALV Layout
*S_ERDAT:D       .
*S_FLIEF:D       .
*S_VKORG:D       .
