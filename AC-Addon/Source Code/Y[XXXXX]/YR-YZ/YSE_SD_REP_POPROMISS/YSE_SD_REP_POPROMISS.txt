
************************************************************************
* Program ID           : YSE_SD_REP_POPROMISS                          *
* Program Title        : PO Request vs Effective Date                  *
* Author               : Inge Bruyndonckx                              *
* Date                 : 15.03.2007                                    *
* Change Request Number: CD1K912466       AIR22207
*                   15032007-SE-OTH-D081-PO by 1st promised date
* Description          :                                               *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME         |CORRECTION Nr| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |2007.08.26| UZZAWAL V    |CD1K942864   |      CR340         *
* Description : Exclude deleted PO lines                               *
*----------------------------------------------------------------------*
* MOD-002 |2010.09.11| Satyabrata Basu |CD1K959178   | T#11259         *
* Description : add field urgency code and transportation mode from PO *
*----------------------------------------------------------------------*
* MOD-003 |2010.09.22| Satyabrata Basu |CD1K959524   | T#11431         *
* Description : add Requisitioner (EKPO-AFNAM) from PO                 *
*----------------------------------------------------------------------*
* MOD-004 |2014.02.25| Anda Wu         |CD1K980198   | CR3089          *
* Description : selection parameter addition _Inc_34155                *
*----------------------------------------------------------------------*
REPORT  yse_sd_rep_popromiss.

INCLUDE yse_sd_rep_include.
*----------------------------------------------------------------------*
* Data declarations                                                    *
*----------------------------------------------------------------------*

*--- Tables
TABLES: vbak, vbep, ekko, ekpo, eket, ekes, ekbe, yse_po_confirm.

*--- Type pools
TYPE-POOLS slis.

*--- Structures
DATA: gs_layout         TYPE slis_layout_alv.
DATA: g_user_command          TYPE slis_formname VALUE 'USER_COMMAND'.
DATA: gs_sd_alv-variant LIKE disvariant.

*--- Internal tables
DATA: it_fieldcat       TYPE slis_t_fieldcat_alv.
DATA: wa_fieldcat TYPE slis_fieldcat_alv.

DATA: BEGIN OF i_ekko OCCURS 0,
      ebeln LIKE ekko-ebeln,
      bsart LIKE ekko-bsart,
      ekorg LIKE ekko-ekorg,
      aedat LIKE ekko-aedat,
      lifnr LIKE ekko-lifnr,
      END OF i_ekko.

DATA: BEGIN OF i_ekpo OCCURS 0,
        ebeln LIKE ekpo-ebeln,
      ebelp LIKE ekpo-ebelp,
      matnr LIKE ekpo-matnr,
      werks LIKE ekpo-werks,
      menge LIKE ekpo-menge,
      meins LIKE ekpo-meins,
      zzurgcode like ekpo-zzurgcode,           "+Mod-002 (Urgency code)
      zztranspmode like ekpo-zztranspmode,     "+Mod-002 (Transp. mode)
      afnam LIKE ekpo-afnam,                   "+Mod-003 (Requisitioner)
      END OF i_ekpo.

DATA: BEGIN OF i_eket OCCURS 0,
      ebeln LIKE eket-ebeln,
      ebelp LIKE eket-ebelp,
      eindt LIKE eket-eindt,
      END OF i_eket.

DATA: BEGIN OF i_ekes OCCURS 0,
      ebeln LIKE ekes-ebeln,
      ebelp LIKE ekes-ebelp,
      etens LIKE ekes-etens,
      ebtyp LIKE ekes-ebtyp,
      eindt LIKE ekes-eindt,
      END OF i_ekes.

DATA: BEGIN OF i_yse_po_confirm OCCURS 0.
        INCLUDE STRUCTURE yse_po_confirm.
DATA: END OF i_yse_po_confirm.

DATA: BEGIN OF i_ekbe OCCURS 0,
      ebeln LIKE ekbe-ebeln,
      ebelp LIKE ekbe-ebelp,
      zekkn LIKE ekbe-zekkn,
      vgabe LIKE ekbe-vgabe,
      budat LIKE ekbe-budat,
      END OF i_ekbe.

DATA: BEGIN OF i_ekkn OCCURS 0,
      ebeln LIKE ekkn-ebeln,
      ebelp LIKE ekkn-ebelp,
      zekkn LIKE ekkn-zekkn,
      vbeln LIKE ekkn-vbeln,
      vbelp LIKE ekkn-vbelp,
      END OF i_ekkn.

DATA: BEGIN OF i_vbfa OCCURS 0,
      vbelv LIKE vbfa-vbelv,
      posnv LIKE vbfa-posnv,
      vbtyp_n LIKE vbfa-vbtyp_n,
      vbeln LIKE vbfa-vbeln,
      END OF i_vbfa.

DATA: BEGIN OF i_likp OCCURS 0,
      vbeln LIKE likp-vbeln,
      wadat_ist LIKE likp-wadat_ist,
      END OF i_likp.


DATA: BEGIN OF i_vbep OCCURS 0,
      vbeln LIKE vbep-vbeln,
      posnr LIKE vbep-posnr,
      etenr LIKE vbep-etenr,
      edatu LIKE vbep-edatu,
      END OF i_vbep.
DATA: wa_vbep LIKE i_vbep.

DATA: BEGIN OF i_output OCCURS 0.
        INCLUDE STRUCTURE yse_sd_popromiss.
DATA: END OF i_output.


*--- Variables
DATA: enddatum LIKE sy-datum.
DATA: lv_first(1).
DATA: g_variant_flag    TYPE c.

*----------------------------------------------------------------------*
* Selection screen                                                     *
*----------------------------------------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-s01.
SELECT-OPTIONS: s_aedat FOR ekko-aedat.
PARAMETERS    : p_ekorg LIKE ekko-ekorg OBLIGATORY.
SELECT-OPTIONS: s_lifnr FOR ekko-lifnr.
SELECT-OPTIONS: s_bsart FOR ekko-bsart.
SELECT-OPTIONS: s_ebeln FOR ekko-ebeln.
***********Begin of MOD-004*******ADD*******************
SELECT-OPTIONS: s_eindt FOR eket-eindt.
***********End  of MOD-004 *******ADD*******************
SELECTION-SCREEN END OF BLOCK b1.
SELECTION-SCREEN: SKIP.
SELECTION-SCREEN BEGIN OF BLOCK b02 WITH FRAME TITLE text-s02.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT (25) text-s07.
SELECTION-SCREEN POSITION POS_LOW.
PARAMETERS: p_var   LIKE disvariant-variant.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK b02.



INITIALIZATION.

  REFRESH: s_aedat.
  s_aedat-low = sy-datum.
  s_aedat-low+6(2) = 01.

  CLEAR enddatum.
  CALL FUNCTION 'RP_LAST_DAY_OF_MONTHS'
    EXPORTING
      day_in                  = sy-datum
   IMPORTING
     last_day_of_month       = enddatum
* EXCEPTIONS
*   DAY_IN_NO_DATE          = 1
*   OTHERS                  = 2
            .
  s_aedat-high = enddatum.
  APPEND s_aedat.


AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_var.
  PERFORM variant_inputhelp USING p_var.

AT SELECTION-SCREEN.

  PERFORM check_authorization.
  PERFORM existence_variant USING p_var.

AT SELECTION-SCREEN OUTPUT.

  PERFORM variant_init.
  IF p_var IS INITIAL AND g_variant_flag IS INITIAL.
    PERFORM get_default_variant USING p_var.
    g_variant_flag = 'X'.
  ENDIF.

************************************************************************
* START MAIN PROGRAM                                                   *
************************************************************************
START-OF-SELECTION.

*  PERFORM initialize_data.
*
  PERFORM select_data.
  PERFORM process_data.
  PERFORM display_data.

*&---------------------------------------------------------------------*
*&      Form  select_data
*&---------------------------------------------------------------------*
FORM select_data .

* Select all relevant purchase orders
  SELECT ebeln ekorg aedat bsart lifnr
  FROM ekko
  INTO CORRESPONDING FIELDS OF TABLE i_ekko
  WHERE ebeln IN s_ebeln
  AND aedat IN s_aedat
  AND ekorg = p_ekorg
  AND bsart IN s_bsart
  AND lifnr IN s_lifnr.

  DESCRIBE TABLE i_ekko LINES sy-tfill.
  IF sy-tfill NE 0.
    SORT i_ekko.
* Select all relevant po items
    SELECT ebeln ebelp matnr werks menge meins
           zzurgcode zztranspmode                 "+Mod-002 (Satya)
           afnam                                  "+Mod-003 (Satya)
    FROM ekpo
    INTO CORRESPONDING FIELDS OF TABLE i_ekpo
    FOR ALL ENTRIES IN i_ekko
    WHERE ebeln EQ i_ekko-ebeln
     AND loekz NE 'L'.         " 20080826 MOD-001
*    AND loekz NE 'X'.         " 20080826 MOD-001

    DESCRIBE TABLE i_ekpo LINES sy-tfill.
    IF sy-tfill NE 0.
* select all schedule lines
      SELECT ebeln ebelp eindt
      FROM eket
      INTO CORRESPONDING FIELDS OF TABLE i_eket
      FOR ALL ENTRIES IN i_ekpo
      WHERE ebeln = i_ekpo-ebeln
      AND ebelp = i_ekpo-ebelp
      AND etenr = '1'
***********Begin of MOD-004*******ADD*******************
      AND eindt IN s_eindt.
***********End  of MOD-004 *******ADD*******************

* select all confirmation lines
      SELECT ebeln ebelp etens ebtyp eindt
      FROM ekes
      INTO CORRESPONDING FIELDS OF TABLE i_ekes
      FOR ALL ENTRIES IN i_ekpo
      WHERE ebeln = i_ekpo-ebeln
      AND ebelp = i_ekpo-ebelp.


* Select all first confirmation lines
      SELECT *
      FROM yse_po_confirm
      INTO CORRESPONDING FIELDS OF TABLE i_yse_po_confirm
      FOR ALL ENTRIES IN i_ekpo
      WHERE ebeln = i_ekpo-ebeln
      AND ebelp = i_ekpo-ebelp.

* Select po history
      SELECT ebeln ebelp zekkn vgabe budat
      FROM ekbe
      INTO CORRESPONDING FIELDS OF TABLE i_ekbe
      FOR ALL ENTRIES IN i_ekpo
            WHERE ebeln = i_ekpo-ebeln
            AND ebelp = i_ekpo-ebelp
            AND vgabe = '1'.
    ENDIF.

* select related sales order
    SELECT ebeln ebelp zekkn vbeln vbelp
    FROM ekkn
    INTO CORRESPONDING FIELDS OF TABLE i_ekkn
    FOR ALL ENTRIES IN i_ekpo
    WHERE ebeln = i_ekpo-ebeln
    AND ebelp = i_ekpo-ebelp
    AND zekkn = '01'.

    DESCRIBE TABLE i_ekkn LINES sy-tfill.
    IF sy-tfill NE 0.

* For this sales order => select schedule lines
      SELECT vbeln posnr etenr edatu
      FROM vbep
      INTO CORRESPONDING FIELDS OF TABLE i_vbep
      FOR ALL ENTRIES IN i_ekkn
      WHERE vbeln = i_ekkn-vbeln
      AND posnr = i_ekkn-vbelp.

* For this sales order => select deliveries from
* Sales document flow
      SELECT vbelv posnv vbtyp_n vbeln
      FROM vbfa
      INTO CORRESPONDING FIELDS OF TABLE i_vbfa
      FOR ALL ENTRIES IN i_ekkn
      WHERE vbelv EQ i_ekkn-vbeln
      AND posnv EQ i_ekkn-vbelp
      AND vbtyp_n EQ 'J'.

      DESCRIBE TABLE i_vbfa LINES sy-tfill.
      IF sy-tfill NE 0.
        SORT i_vbfa BY vbeln.
* Select outbound delivery
        SELECT vbeln wadat_ist
        FROM likp
        INTO CORRESPONDING FIELDS OF TABLE i_likp
        FOR ALL ENTRIES IN i_vbfa
        WHERE vbeln = i_vbfa-vbeln.
      ENDIF.
    ENDIF.
  ENDIF.
ENDFORM.                    " select_data

*&---------------------------------------------------------------------*
*&      Form  process_data
*&---------------------------------------------------------------------*

FORM process_data .
  SORT: i_ekko, i_ekpo, i_ekes, i_eket, i_yse_po_confirm, i_ekbe,
        i_ekkn, i_vbfa, i_likp.
  SORT: i_vbep BY vbeln posnr edatu.
  LOOP AT i_ekpo.
    CLEAR i_output.
    MOVE-CORRESPONDING i_ekpo TO i_output.
    CLEAR i_ekko.
    READ TABLE i_ekko WITH KEY ebeln = i_ekpo-ebeln
                               BINARY SEARCH.
    IF sy-subrc EQ 0.
      MOVE-CORRESPONDING i_ekko TO i_output.
    ENDIF.
    CLEAR i_eket.
    READ TABLE i_eket WITH KEY ebeln = i_ekpo-ebeln
                               ebelp = i_ekpo-ebelp
                               BINARY SEARCH.
    IF sy-subrc EQ 0.
      i_output-eindt = i_eket-eindt.
***********Begin of MOD-004*******ADD*******************
    ELSE.
      CONTINUE.
***********End  of MOD-004 *******ADD*******************
    ENDIF.
    CLEAR i_ekes.
    READ TABLE i_ekes WITH KEY ebeln = i_ekpo-ebeln
                               ebelp = i_ekpo-ebelp
                               ebtyp = 'AB'
                               BINARY SEARCH.
    IF sy-subrc EQ 0.
      i_output-eindt2 = i_ekes-eindt.
    ENDIF.
    CLEAR i_ekbe.
    READ TABLE i_ekbe WITH KEY ebeln = i_ekpo-ebeln
                               ebelp = i_ekpo-ebelp
                               BINARY SEARCH.
    IF sy-subrc EQ 0.
      i_output-budat = i_ekbe-budat.
    ENDIF.

    CLEAR i_yse_po_confirm.
    READ TABLE i_yse_po_confirm WITH KEY ebeln = i_ekpo-ebeln
                               ebelp = i_ekpo-ebelp
                               BINARY SEARCH.
    IF sy-subrc EQ 0.
      i_output-zzeindt = i_yse_po_confirm-zzeindt.
    ENDIF.

* Sales Order history
    CLEAR i_ekkn.
    READ TABLE i_ekkn WITH KEY ebeln = i_ekpo-ebeln
                               ebelp = i_ekpo-ebelp
                               BINARY SEARCH.
    IF sy-subrc EQ 0.
* Select document flow sales order
      CLEAR i_vbfa.
      READ TABLE i_vbfa WITH KEY vbelv = i_ekkn-vbeln
                                 posnv = i_ekkn-vbelp
                                 BINARY SEARCH.
      IF sy-subrc EQ 0.
        CLEAR i_likp.
        READ TABLE i_likp WITH KEY vbeln = i_vbfa-vbeln
                                 BINARY SEARCH.
        IF sy-subrc EQ 0.
          i_output-wadat_ist = i_likp-wadat_ist.
        ENDIF.
      ENDIF.
* Schedule lines
      CLEAR: lv_first.
      LOOP AT i_vbep WHERE vbeln = i_ekkn-vbeln
                       AND posnr = i_ekkn-vbelp.
        IF lv_first IS INITIAL.
          i_output-edatu1 = i_vbep-edatu.
          lv_first = 'X'.
        ENDIF.
        wa_vbep = i_vbep.
      ENDLOOP.
      IF sy-subrc EQ 0.
        i_output-edatu2 = wa_vbep-edatu.
      ENDIF.
    ENDIF.
    APPEND i_output.
  ENDLOOP.
ENDFORM.                    " process_data

*&---------------------------------------------------------------------*
*&      Form  display_data
*&---------------------------------------------------------------------*

FORM display_data .

  PERFORM fill_field_catalog.
  PERFORM alv_output.

ENDFORM.                    " display_data


*----------------------------------------------------------------------*
*       Form  FILL_FIELD_CATALOG                                       *
*----------------------------------------------------------------------*
*       text                                                           *
*----------------------------------------------------------------------*
FORM fill_field_catalog.

  x_repid = sy-repid.
  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
       EXPORTING
            i_program_name         = x_repid
*            i_internal_tabname     = 'I_OUTPUT'
            i_structure_name       = 'YSE_SD_POPROMISS'
*           i_client_never_display = 'X'
            i_inclname             = x_repid
*           i_bypassing_buffer     =
*           i_buffer_active        =
       CHANGING
            ct_fieldcat            = it_fieldcat
       EXCEPTIONS
            inconsistent_interface = 1
            program_error          = 2
            OTHERS                 = 3.
  IF sy-subrc <> 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  gs_layout-colwidth_optimize   = 'X'.
  gs_layout-confirmation_prompt = 'X'.
  gs_layout-key_hotspot         = 'X'.


* Change column headers
  LOOP AT it_fieldcat INTO wa_fieldcat.
    CASE wa_fieldcat-fieldname.
      WHEN 'EDATU1'.
        wa_fieldcat-seltext_l = text-f01.
        MODIFY it_fieldcat FROM wa_fieldcat.
      WHEN 'EDATU2'.
        wa_fieldcat-seltext_l = text-f02.
        MODIFY it_fieldcat FROM wa_fieldcat.
      WHEN 'EINDT'.
        wa_fieldcat-seltext_l = text-f03.
        MODIFY it_fieldcat FROM wa_fieldcat.
      WHEN 'ZZEINDT'.
        wa_fieldcat-seltext_l = text-f04.
        MODIFY it_fieldcat FROM wa_fieldcat.
      WHEN 'BUDAT'.
        wa_fieldcat-seltext_l = text-f05.
        MODIFY it_fieldcat FROM wa_fieldcat.
      WHEN 'EINDT2'.
        wa_fieldcat-seltext_l = text-f06.
        MODIFY it_fieldcat FROM wa_fieldcat.
      WHEN 'WADAT_IST'.
        wa_fieldcat-seltext_l = text-f07.
        MODIFY it_fieldcat FROM wa_fieldcat.
    ENDCASE.
  ENDLOOP.
ENDFORM.                    "fill_field_catalog


*----------------------------------------------------------------------*
*       Form  ALV_OUTPUT                                               *
*----------------------------------------------------------------------*
*       text                                                           *
*----------------------------------------------------------------------*
FORM alv_output.


  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
       EXPORTING
*           i_interface_check           = ' '
*           i_bypassing_buffer          =
*           i_buffer_active             = ' '
            i_callback_program          = 'YSE_SD_REP_POPROMISS'
*           i_callback_pf_status_set    = ' '
            i_callback_user_command     = g_user_command
*           i_callback_top_of_page      = ' '
*           i_callback_html_top_of_page = ' '
*           i_callback_html_end_of_list = ' '
*           i_structure_name            =
*           i_background_id             = ' '
            i_grid_title                = text-001
*           i_grid_settings             =
            is_layout                   = gs_layout
            it_fieldcat                 = it_fieldcat
*           it_excluding                =
*           it_special_groups           =
*            it_sort                     = it_sort
*           it_filter                   =
*           is_sel_hide                 =
            i_default                   = 'X'
            i_save                      = 'A'
            is_variant                  = gs_sd_alv-variant
*           it_events                   =
*           it_event_exit               =
*           is_print                    =
*           is_reprep_id                =
            i_screen_start_column       = 0
            i_screen_start_line         = 0
            i_screen_end_column         = 0
            i_screen_end_line           = 0
*           it_alv_graphics             =
*           it_add_fieldcat             =
*           it_hyperlink                =
*      importing
*           e_exit_caused_by_caller     =
*           es_exit_caused_by_user      =
       TABLES
            t_outtab                    = i_output
       EXCEPTIONS
            program_error               = 1
            OTHERS                      = 2.
  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_OUTPUT

* ----------------------------------------------------------------------
*   FORM
*   USER COMMAND
*
*   User commands management
*
* ----------------------------------------------------------------------
FORM user_command USING pi_ucomm       LIKE sy-ucomm
                        pi_rs_selfield TYPE slis_selfield.  "#EC CALLED

  CASE pi_ucomm.

    WHEN '&IC1'.
      CASE  pi_rs_selfield-fieldname.
        WHEN 'EBELN'.
          SET PARAMETER ID 'BES' FIELD pi_rs_selfield-value.
          CALL TRANSACTION 'ME23N' AND SKIP FIRST SCREEN.
      ENDCASE.

  ENDCASE.

ENDFORM.                                                   "user_comman
*&---------------------------------------------------------------------*
*&      Form  Check_Authorization
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM check_authorization .

  AUTHORITY-CHECK OBJECT 'V_KONH_EKO'
           ID 'EKORG' FIELD p_ekorg
           ID 'ACTVT' DUMMY.

  IF sy-subrc = 4.
*   No authorisation to display the data
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '002' WITH p_ekorg.
  ELSEIF sy-subrc <> 0.
*   Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '001'.
  ENDIF.

ENDFORM.                    " Check_Authorization

*Text symbol text£º
*001:Purchase - Requested vs effective
*F01:Customer Requested delivery date
*F02:Scheduled delivery date Sales Order
*F03:Scheduled date Purchase Order
*F04:First promised date
*F05:Effective delivery date GR
*F06:Effective delivery date DTC
*F07:Effective delivery date to Customer
*S01:Purchase Order Selections
*S02:Output Selections

*S07:ALV Output Variant
*Selection text£º
*P_EKORG:D       .
*P_VAR:        Layout
*S_AEDAT:D       .
*S_BSART:D       .
*S_EBELN:D       .
*S_EINDT:        Requested  Delivery Date
*S_LIFNR:D       .
