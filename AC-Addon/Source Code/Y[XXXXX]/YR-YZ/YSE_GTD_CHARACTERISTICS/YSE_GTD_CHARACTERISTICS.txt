*&---------------------------------------------------------------------*
*& Report  YSE_GTD_CHARACTERISTICS
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& LCR Russia : Report with GTD characteristics                        *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 10.03.2010
*  Change Request Number : CR1168
*  Transport request Nr. : CD1K955213
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME        | CORRECTION NR. | CHANGE REF. #  *
*----------------------------------------------------------------------*
* MOD-001 | 05.08.2010 | Jules Smets | CD1K958444     | CR1229         *
*         | Add characteristics ZRU_BLOCK_GM & ZRU_FOC (+ parameter)   *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_gtd_characteristics.

TABLES: mcha,
        mchb,
        mska,
        makt,
        mbew,
        ausp,
        cabnt.

* ALV grid
TYPE-POOLS: slis.

* Internal table with plants
DATA: BEGIN OF t_wrk  OCCURS 0,
        werks     TYPE werks_d,
      END OF t_wrk.

* Internal table with batches
DATA: BEGIN OF t_mcha  OCCURS 0,
        matnr     TYPE matnr,
        werks     TYPE werks_d,
        charg     TYPE charg_d,
        gtdnr     TYPE atwrt,
      END OF t_mcha.
DATA: h_mcha  LIKE LINE OF t_mcha.
* Batch stocks
DATA: BEGIN OF t_mchb  OCCURS 0,
        matnr     TYPE matnr,
        werks     TYPE werks_d,
        charg     TYPE charg_d,
        clabs     TYPE labst,
        cumlm     TYPE umlmd,
        cinsm     TYPE insme,
        ceinm     TYPE einme,
        cspem     TYPE speme,
        cretm     TYPE retme,
      END OF t_mchb.
DATA: h_mchb  LIKE LINE OF t_mchb.
* Sales order stock
DATA: BEGIN OF t_mska  OCCURS 0,
        matnr     TYPE matnr,
        werks     TYPE werks_d,
        charg     TYPE charg_d,
        kalab     TYPE labst,
      END OF t_mska.
DATA: h_mska  LIKE LINE OF t_mska.
* Material valuation
DATA: BEGIN OF t_mbew  OCCURS 0,
        matnr     TYPE matnr,
        bwkey     TYPE bwkey,
        bwtar     TYPE bwtar_d,
        verpr     TYPE verpr,
      END OF t_mbew.
DATA: h_mbew  LIKE LINE OF t_mbew.

* Material descriptions
DATA: BEGIN OF t_makt  OCCURS 0,
        matnr     TYPE matnr,
        maktx     TYPE maktx,
      END OF t_makt.

* Internal table with output lines
DATA: BEGIN OF t_out  OCCURS 0,
        matnr     TYPE matnr,
        maktx     TYPE maktx,
        werks     TYPE werks_d,
        charg     TYPE charg_d,
        gtdnr     TYPE atwrt,
        grdate    TYPE atwrt,
        country   TYPE atwrt,
        last      TYPE atwrt,
*** MOD-001 * begin ***
        blockgm   TYPE atwrt,
        foc       TYPE atwrt,
*** MOD-001 * end ***
        stock     TYPE gsbest,
        price     TYPE verpr,
      END OF t_out.
DATA: h_out  LIKE LINE OF t_out.

* Internal table with classificaton data
DATA: BEGIN OF t_clobjdat OCCURS 0.
        INCLUDE STRUCTURE clobjdat.
DATA: END OF t_clobjdat.

* Internal table with classes
DATA: BEGIN OF t_class OCCURS 0.
        INCLUDE STRUCTURE sclass.
DATA: END OF t_class.

DATA: xv_variant         LIKE disvariant,
      xv_variant_flag    TYPE c,
      xv_sd_alv_variant  LIKE disvariant,
      xt_fcat            TYPE slis_t_fieldcat_alv,
      ls_fcat            LIKE LINE OF xt_fcat,
      xt_alv_sort        TYPE slis_t_sortinfo_alv,
      xv_user_command    TYPE slis_formname  VALUE 'USER_COMMAND',
      xv_variant_save    TYPE c              VALUE 'U',
      xv_layout          TYPE slis_layout_alv.

DATA: h_atinn        TYPE atinn,
      h_atinn_gtdnr  TYPE atinn,
      h_object       TYPE objnum,
      h_atbez_gtdnr  TYPE atbez,
      h_atbez_grdat  TYPE atbez,
      h_atbez_cntry  TYPE atbez,
      h_atbez_lastd  TYPE atbez,
*** MOD-001 * begin ***
      h_atbez_blockgm  TYPE atbez,
      h_atbez_foc    TYPE atbez,
*** MOD-001 * end ***
      h_atflv        TYPE atflv,
      h_gsbest       TYPE gsbest,
      h_tsost        TYPE gsbest,
      h_totst        TYPE gsbest,
      h_verpr        TYPE verpr,
      h_col(10)      TYPE n,
      h_repid        LIKE sy-repid.

CONSTANTS: c_atnam_gtdnr    TYPE atnam       VALUE 'ZRU_GDTNUMBER',
           c_atnam_grdat    TYPE atnam       VALUE 'ZRU_GR_DATE',
           c_atnam_cntry    TYPE atnam       VALUE 'ZRU_COUNTRY1',
           c_atnam_lastd    TYPE atnam       VALUE 'ZRU_GTDLAST',
*** MOD-001 * begin ***
           c_atnam_blockgm  TYPE atnam       VALUE 'ZRU_BLOCK_GM',
           c_atnam_foc      TYPE atnam       VALUE 'ZRU_FOC',
*** MOD-001 * end ***
           c_cltype_022     TYPE klassenart  VALUE '022',
           c_objtab_mcha    TYPE tabelle     VALUE 'MCHA',
           c_e              TYPE xfeld       VALUE 'E',
           c_x              TYPE xfeld       VALUE 'X'.

* Selections
SELECTION-SCREEN  BEGIN OF BLOCK sel  WITH FRAME  TITLE text-s01.
SELECT-OPTIONS: so_matnr  FOR mcha-matnr,                  "Material
                so_werks  FOR mcha-werks  OBLIGATORY       "Plant
                              MEMORY ID wrk,
                so_charg  FOR mcha-charg,                  "Batch
                so_gtdnr  FOR ausp-atwrt.                  "GTD-nr.
SELECTION-SCREEN SKIP.
PARAMETERS: p_stock       AS CHECKBOX  DEFAULT 'X',
            p_foc         AS CHECKBOX  DEFAULT ' '.         "MOD-001
SELECTION-SCREEN  END OF BLOCK sel.


*----------------------------------------------------------------------*
* On the selection screen                                              *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

  PERFORM  check_authorization.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

* Statistics for transaction / program
  CALL METHOD ycl_statistics=>record_transaction.

* Convert batch numbers (selection)
  PERFORM convert_batch_sel.

* Build & display list
  PERFORM select_data.
  PERFORM alv_fcat.
  PERFORM alv_layout.
  PERFORM alv_display.


*&---------------------------------------------------------------------*
*&  Forms                                                              *
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  SELECT werks FROM t001w
               INTO TABLE t_wrk
               WHERE werks IN so_werks.

  LOOP AT t_wrk.
    AUTHORITY-CHECK OBJECT 'M_MATE_WRK'
             ID 'ACTVT' DUMMY
             ID 'WERKS' FIELD t_wrk-werks.
    IF sy-subrc = 4.
*     No authorisation to display data
      MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '010' WITH t_wrk-werks.
      EXIT.
    ELSEIF sy-subrc <> 0.
*     Error checking authorization.
      MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
      EXIT.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  CONVERT_BATCH_SEL
*&---------------------------------------------------------------------*
*       Convert batch numbers (selection)
*----------------------------------------------------------------------*
FORM convert_batch_sel .

  LOOP AT so_charg.
    IF NOT so_charg-low IS INITIAL.
      PERFORM conversion_alpha USING so_charg-low.
    ENDIF.
    IF NOT so_charg-high IS INITIAL.
      PERFORM conversion_alpha USING so_charg-high.
    ENDIF.
    MODIFY so_charg.
  ENDLOOP.

ENDFORM.                    " CONVERT_BATCH_SEL

*&---------------------------------------------------------------------*
*&      Form  CONVERSION_ALPHA
*&---------------------------------------------------------------------*
*       Prefill field with zeros (when numeric)
*----------------------------------------------------------------------*
FORM conversion_alpha  USING    p_field TYPE char10.

  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
    EXPORTING
      input  = p_field
    IMPORTING
      output = p_field.

ENDFORM.                    " CONVERSION_ALPHA

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

  REFRESH: t_mcha, t_mchb, t_mska, t_mbew, t_makt, t_out.

* Get atinn-code for characteristic 'ZRU_GDTNUMBER'
  PERFORM convert_charc USING c_atnam_gtdnr
                              h_atinn_gtdnr.

* Select batches (and GTD numbers)
  SELECT b~matnr b~werks b~charg c~atwrt
         INTO TABLE t_mcha
         FROM mcha AS b
              INNER JOIN ausp AS c
                         ON c~objek = b~cuobj_bm
         WHERE b~matnr IN so_matnr
           AND b~werks IN so_werks
           AND b~charg IN so_charg
           AND c~atwrt IN so_gtdnr
           AND c~atinn = h_atinn_gtdnr.
  SORT t_mcha.

  CHECK NOT t_mcha[] IS INITIAL.

* Batch stock (quantities)
  SELECT matnr werks charg clabs cumlm cinsm ceinm cspem cretm
         INTO TABLE t_mchb
         FROM mchb
         FOR ALL ENTRIES IN t_mcha
         WHERE matnr = t_mcha-matnr
           AND werks = t_mcha-werks
           AND charg = t_mcha-charg.
  SORT t_mchb.

* Sales order stock (quantities)
  SELECT matnr werks charg kalab
         INTO TABLE t_mska
         FROM mska
         FOR ALL ENTRIES IN t_mcha
         WHERE matnr = t_mcha-matnr
           AND werks = t_mcha-werks
           AND charg = t_mcha-charg.
  SORT t_mska.

* Moving price (per batch)
  SELECT matnr bwkey bwtar verpr
         INTO TABLE t_mbew
         FROM mbew
         FOR ALL ENTRIES IN t_mcha
         WHERE matnr = t_mcha-matnr
           AND bwkey = t_mcha-werks
           AND bwtar = t_mcha-charg.
  SORT t_mchb.

* Get material descriptions
  SELECT matnr maktx
         INTO TABLE t_makt
         FROM makt
         FOR ALL ENTRIES IN t_mcha
             WHERE matnr = t_mcha-matnr
               AND spras = c_e.
  SORT t_makt.
  DELETE ADJACENT DUPLICATES FROM t_makt.

* Fill output table
  LOOP AT t_mcha.

*   Material description
    AT NEW matnr.
      CLEAR t_makt.
      READ TABLE t_makt WITH KEY matnr = t_mcha-matnr
                        BINARY SEARCH.
    ENDAT.

*   Get characteristics
    PERFORM get_characteristics.
*   Get stock
    PERFORM get_stock.
*   Get price
    PERFORM get_price.
*   Fill output line
    PERFORM fill_line.

  ENDLOOP.

  SORT t_out.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  CONVERT_CHARC
*&---------------------------------------------------------------------*
*       Get atinn code for characteristic
*----------------------------------------------------------------------*
*      -->P_ATNAM : Characteristic name
*      -->P_ATINN : Characteristic code
*----------------------------------------------------------------------*
FORM convert_charc  USING    p_atnam
                             p_atinn.

  CALL FUNCTION 'CONVERSION_EXIT_ATINN_INPUT'
    EXPORTING
      input  = p_atnam
    IMPORTING
      output = p_atinn.

ENDFORM.                    " CONVERT_CHARC

*&---------------------------------------------------------------------*
*&      Form  GET_CHARACTERISTICS
*&---------------------------------------------------------------------*
*       Get characteristics
*----------------------------------------------------------------------*
FORM get_characteristics .

  CLEAR t_clobjdat[].

**  CONCATENATE t_mcha-matnr t_mcha-werks t_mcha-charg
**              INTO h_object.
  h_object+0(18)  = t_mcha-matnr.
  h_object+18(4)  = t_mcha-werks.
  h_object+22(10) = t_mcha-charg.

  CALL FUNCTION 'CLAF_CLASSIFICATION_OF_OBJECTS'
    EXPORTING
      classtype          = c_cltype_022
      features           = c_x
      object             = h_object
      objecttable        = c_objtab_mcha
    TABLES
      t_class            = t_class
      t_objectdata       = t_clobjdat
    EXCEPTIONS
      no_classification  = 1
      no_classtypes      = 2
      invalid_class_type = 3
      OTHERS             = 4.

  IF sy-subrc <> 0.
*    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.

ENDFORM.                    " GET_CHARACTERISTICS

*&---------------------------------------------------------------------*
*&      Form  GET_STOCK
*&---------------------------------------------------------------------*
*       Get batch stock
*----------------------------------------------------------------------*
FORM get_stock .

  CLEAR: h_gsbest, h_tsost, h_totst.

* Batch stock
  LOOP AT t_mchb WHERE matnr = t_mcha-matnr
                   AND werks = t_mcha-werks
                   AND charg = t_mcha-charg.
    h_gsbest = h_gsbest + t_mchb-clabs + t_mchb-cumlm + t_mchb-cinsm
                        + t_mchb-ceinm + t_mchb-cspem + t_mchb-cretm.
  ENDLOOP.

* Sales order stock
  LOOP AT t_mska WHERE matnr = t_mcha-matnr
                   AND werks = t_mcha-werks
                   AND charg = t_mcha-charg.
    h_tsost = h_tsost + t_mska-kalab.
  ENDLOOP.

* Total stock
  h_totst = h_gsbest + h_tsost.

ENDFORM.                    " GET_STOCK

*&---------------------------------------------------------------------*
*&      Form  GET_PRICE
*&---------------------------------------------------------------------*
*       Get valuation (per batch)
*----------------------------------------------------------------------*
FORM get_price .

  CLEAR h_verpr.

  READ TABLE t_mbew WITH KEY matnr = t_mcha-matnr
                             bwkey = t_mcha-werks
                             bwtar = t_mcha-charg.
  IF sy-subrc = 0.
    h_verpr = t_mbew-verpr.
  ENDIF.

ENDFORM.                    " GET_PRICE

*&---------------------------------------------------------------------*
*&      Form  FILL_LINE
*&---------------------------------------------------------------------*
*       Fill output line
*----------------------------------------------------------------------*
FORM fill_line .

* Only batches with stock ?
  IF NOT p_stock IS INITIAL.
    CHECK h_totst NE 0.
  ENDIF.

  CLEAR t_out.

* Batch data
  MOVE-CORRESPONDING t_mcha TO t_out.

* Material description
  t_out-maktx = t_makt-maktx.

* Characteristics
  LOOP AT t_clobjdat.
    CASE t_clobjdat-atnam.
      WHEN c_atnam_gtdnr.
*       t_out-gtdnr already filled
      WHEN c_atnam_grdat.
        t_out-grdate  = t_clobjdat-ausp1.
      WHEN c_atnam_cntry.
        t_out-country = t_clobjdat-ausp1.
      WHEN c_atnam_lastd.
        t_out-last    = t_clobjdat-ausp1.
*** MOD-001 * begin ***
      WHEN c_atnam_blockgm.
        t_out-blockgm = t_clobjdat-ausp1.
      WHEN c_atnam_foc.
        t_out-foc     = t_clobjdat-ausp1.
*** MOD-001 * end ***
    ENDCASE.
  ENDLOOP.

*** MOD-001 * begin ***
* Only batches with 'FOC' ?
  IF NOT p_foc IS INITIAL.
    CHECK t_out-foc = 'X'.
  ENDIF.
*** MOD-001 * end ***

* Stock
  t_out-stock = h_totst.

* Price
  t_out-price = h_verpr.

  APPEND t_out.

ENDFORM.                    " FILL_LINE

*&---------------------------------------------------------------------*
*&      Form  ALV_FCAT
*&---------------------------------------------------------------------*
*       ALV field catalog
*----------------------------------------------------------------------*
FORM alv_fcat .
*
** Create Fieldcatalogue from internal table
** CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE' ...

  CLEAR h_col.

* Get characteristic descriptions
  PERFORM get_char_descr USING c_atnam_gtdnr h_atbez_gtdnr.
  PERFORM get_char_descr USING c_atnam_grdat h_atbez_grdat.
  PERFORM get_char_descr USING c_atnam_cntry h_atbez_cntry.
  PERFORM get_char_descr USING c_atnam_lastd h_atbez_lastd.
*** MOD-001 * begin ***
  PERFORM get_char_descr USING c_atnam_blockgm h_atbez_blockgm.
  PERFORM get_char_descr USING c_atnam_foc     h_atbez_foc.
*** MOD-001 * end ***

* Field definitions
  PERFORM add_field USING 'MATNR'   18 'Material'(t01)    ' '  ' '.
  PERFORM add_field USING 'MAKTX'   40 'Description'(t02) ' '  ' '.
  PERFORM add_field USING 'WERKS'    6 'Plant'(t03)       ' '  ' '.
  PERFORM add_field USING 'CHARG'   10 'Batch'(t04)       'X'  ' '.
  PERFORM add_field USING 'GTDNR'   30 h_atbez_gtdnr      ' '  ' '.
  PERFORM add_field USING 'GRDATE'  20 h_atbez_grdat      ' '  ' '.
  PERFORM add_field USING 'COUNTRY' 30 h_atbez_cntry      ' '  ' '.
  PERFORM add_field USING 'LAST'    10 h_atbez_lastd      ' '  ' '.
*** MOD-001 * begin ***
  PERFORM add_field USING 'BLOCKGM' 20 h_atbez_blockgm    ' '  'X'.
  PERFORM add_field USING 'FOC'     15 h_atbez_foc        ' '  'X'.
*** MOD-001 * end ***
  PERFORM add_field USING 'STOCK'   15 'Stock'(t05)       ' '  ' '.
  PERFORM add_field USING 'PRICE'   15 'Price'(t06)       ' '  ' '.

ENDFORM.                    " ALV_FCAT

*&---------------------------------------------------------------------*
*&      Form  GET_CHAR_DESCR
*&---------------------------------------------------------------------*
*       Get characteristic description
*----------------------------------------------------------------------*
*      -->P_ATNAM : Characteristic name
*      -->P_ATBEZ : Characteristic description
*----------------------------------------------------------------------*
FORM get_char_descr  USING    p_atnam
                              p_atbez.

  CLEAR: p_atbez,
         h_atinn.

* Get atinn-code for characteristic
  PERFORM convert_charc USING p_atnam
                              h_atinn.

* Get characteristic description
  SELECT SINGLE atbez INTO p_atbez
         FROM cabnt
         WHERE atinn = h_atinn
           AND spras = c_e.

ENDFORM.                    " GET_CHAR_DESCR

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr
                         p_hotsp
                         p_check.                           "MOD-001

  h_col = h_col + 1.

  CLEAR ls_fcat.
  ls_fcat-col_pos    = h_col.
  ls_fcat-fieldname  = p_field.
  ls_fcat-outputlen  = p_len.
  ls_fcat-seltext_l  = p_descr.
  ls_fcat-seltext_m  = p_descr.
  ls_fcat-seltext_s  = p_descr.
  ls_fcat-fix_column = 'X'.
  ls_fcat-emphasize  = 'X'.
  ls_fcat-hotspot    = p_hotsp.
  ls_fcat-checkbox   = p_check.                             "MOD-001
* Suppress leading zeros for the material number
  IF p_field = 'MATNR'.
    ls_fcat-edit_mask = '==MATN1'.
  ENDIF.

  APPEND ls_fcat TO xt_fcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT
*&---------------------------------------------------------------------*
*       Modify ALV layout
*----------------------------------------------------------------------*
FORM alv_layout .

  xv_layout-zebra = c_x.
*  xv_layout-colwidth_optimize = c_x.
*  xv_layout-smalltitle = 'X'.
*  xv_layout-grid_title = 'title'.

ENDFORM.                    " ALV_LAYOUT

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       Display ALV grid
*----------------------------------------------------------------------*
FORM alv_display .

*  xv_sd_alv_variant = xv_variant.
  h_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = h_repid
      i_callback_user_command = xv_user_command
*      i_callback_top_of_page  = 'ALV_TOP'
      i_grid_title            = 'GTD characteristics'(h01)
      is_layout               = xv_layout
      it_fieldcat             = xt_fcat
      it_sort                 = xt_alv_sort
      i_default               = 'X'
      i_save                  = 'A'
*      is_variant              = xv_sd_alv_variant
      i_screen_start_column   = 0
      i_screen_start_line     = 0
      i_screen_end_column     = 0
      i_screen_end_line       = 0
    TABLES
      t_outtab                = t_out
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.

  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&   Form  USER_COMMAND
*&---------------------------------------------------------------------*
*    Process user command
*----------------------------------------------------------------------*
FORM user_command USING ucomm    LIKE sy-ucomm
                        selfield TYPE slis_selfield.

*-- Check function code
  CASE ucomm.
    WHEN '&IC1'.
*     Select hotspot
      IF NOT selfield-value IS INITIAL.
        CASE selfield-fieldname.
**          WHEN 'MATNR'.
**            SET PARAMETER ID 'MAT' FIELD selfield-value.
**            CALL TRANSACTION 'MM03' AND SKIP FIRST SCREEN.
          WHEN 'CHARG'.
*           Show batch
            CLEAR h_out.
            READ TABLE t_out INDEX selfield-tabindex INTO h_out.
            SET PARAMETER ID 'MAT' FIELD h_out-matnr.
            SET PARAMETER ID 'WRK' FIELD h_out-werks.
            SET PARAMETER ID 'CHA' FIELD selfield-value.
            CALL TRANSACTION 'MSC3N' AND SKIP FIRST SCREEN.
        ENDCASE.
      ENDIF.
  ENDCASE.

ENDFORM.                    " USER_COMMAND

*Text symbol text£º
*H01:GTD characteristics
*S01:Selection
*T01:Material
*T02:Description
*T03:Plant
*T04:Batch
*T05:Stock

*T06:Price
*Selection text£º
*P_FOC:        Only batches 'Free Of Charge'
*P_STOCK:        Only batches  with stock
*SO_CHARG:D       .
*SO_GTDNR:        GTD number
*SO_MATNR:D       .
*SO_WERKS:D       .
