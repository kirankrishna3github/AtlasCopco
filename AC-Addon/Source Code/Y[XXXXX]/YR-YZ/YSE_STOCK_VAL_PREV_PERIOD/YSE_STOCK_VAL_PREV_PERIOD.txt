*----------------------------------------------------------------------*
* PROGRAM ID           : YSE_STOCK_VAL_PREV_PERIOD                     *
* PROGRAM TITLE        : Stock valuation report for previous period    *
* AUTHOR               : Marc Jacobs                                   *
* DATE                 : 18/10/2007                                    *
* CHANGE REQUEST NUMBER: CD1K922279                                    *
* PROGRAM DESCRIPTION  : A report to calculate the stock valuation     *
*                        for previous period                           *
*                        Possibility to update workfile for downloads  *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |03/04/2008|M.Jacobs  |CD2K940408| auth check on MBEW        *
*                                            extra parameter           *
* MOD-002 |28/04/2008|L.Mertens |CD2K941041| extend outputlist         *
* MOD-003 |07/05/2008|M.Jacobs  |CD1K940165| auth.check also in batch  *
* MOD-004 |22/07/2008|M.Jacobs  |CD1K942242| Optimisation              *
* MOD-005 |07/06/2010|Wouter D. |Issue 9802| Double stock overview due *
*         |                    to use of batches in Russia (MBEW-BWTAR *
* MOD-006 |23/11/2010|L.Mertens |CD1K961208| Add segment               *
* MOD-007 |23/07/2013|Anda Wu   |CD1K977041| Add 2 columns "Vendor"    *
*                                           and "Account group" (CN)   *
*MOD-008 |26/06/2015|Prakash   |CD1K977041| undo the 3274 changes      *
*                                                                     *
*----------------------------------------------------------------------*
REPORT yse_stock_val_prev_period NO STANDARD PAGE HEADING
                                           LINE-COUNT 65
* begin of change MOD-002
*                                          LINE-SIZE 120.
                                           LINE-SIZE 255.
* end of change MOD-002
TYPE-POOLS: slis.

TABLES: mbew,                         "material valuation
        mara,                         "material master
        ebew.                         "Sales Order Stock Valuation
* begin of insertion MOD-007
TYPES: BEGIN OF ty_eord,
        matnr  TYPE eord-matnr,       "Material Number
        werks  TYPE eord-werks,       "Plant
        lifnr  TYPE eord-lifnr,       "Vendor Account Number
        flifn  TYPE eord-flifn,       "Indicator: Fixed vendor
       END OF ty_eord,
       BEGIN OF ty_lfa1,
         lifnr TYPE lfa1-lifnr,       "Account Number of Vendor
         ktokk TYPE lfa1-ktokk,       "Vendor account group
       END OF ty_lfa1.
* end of insertion MOD-007
DATA: BEGIN OF gt_mbew OCCURS 0.
        INCLUDE STRUCTURE mbew.
DATA :END OF gt_mbew.

DATA: BEGIN OF gt_mbewt OCCURS 0.
        INCLUDE STRUCTURE mbew.
DATA :END OF gt_mbewt.

DATA: BEGIN OF gt_ebew OCCURS 0.
        INCLUDE STRUCTURE ebew.
DATA :END OF gt_ebew.

DATA: BEGIN OF gt_ebewt OCCURS 0.
        INCLUDE STRUCTURE ebew.
DATA :END OF gt_ebewt.

DATA: BEGIN OF gt_out OCCURS 0,
        bwkey LIKE mbew-bwkey,
        matnr LIKE mbew-matnr,
        maktx LIKE makt-maktx,
        segment type yse_prctr_bl-segment,       "mod-006
        zprctr LIKE marc-prctr,
        zgac(4) TYPE c,
        zpgc(4) TYPE c,
        lbkum LIKE mbew-lbkum,
        salk3 LIKE mbew-salk3,
        vmkum LIKE mbew-vmkum,
        vmsal LIKE mbew-vmsal,
        stprs LIKE mbew-stprs,
        bwprs LIKE mbew-bwprs,
        zkprs LIKE mbew-zkprs,
        abwkz LIKE mbew-abwkz,
        lfgja LIKE mbew-lfgja,
        lfmon LIKE mbew-lfmon,
        bklas LIKE mbew-bklas,
* begin of insertion MOD-007
        lifnr TYPE eord-lifnr,
        ktokk TYPE lfa1-ktokk,
* end of insertion MOD-007
        selected,
      END OF gt_out.

DATA : wa_out LIKE gt_out.
DATA : wa_stock_report LIKE yse_stock_report.

DATA: g_mtart         LIKE mara-mtart,
      g_prdha         LIKE mara-prdha,
      g_maktx         LIKE makt-maktx,
* begin of insertion MOD-007
      g_flg_cn        TYPE char1,
* end of insertion MOD-007
      g_spras         LIKE makt-spras,
      g_prctr         LIKE marc-prctr,
      gt_fieldcat     TYPE slis_t_fieldcat_alv,
      g_events_tab    TYPE slis_t_event,
      g_form_user_command TYPE slis_formname VALUE 'USER_COMMAND_L',
      g_ic1           LIKE sy-ucomm VALUE '&IC1',
      g_repid         LIKE sy-repid,
      l_index         TYPE sy-tabix.

* begin of insertion MOD-004
FIELD-SYMBOLS: <mbew> TYPE any table.
* end of insertion MOD-004

CONSTANTS: c_sc1(3)             VALUE 'SC1',
           c_s(1) TYPE c        VALUE 'S',
           c_x(1) TYPE c        VALUE 'X',
           c_spras(1) TYPE c    VALUE 'E',
* begin of insertion MOD-004
           c_mbew(35) TYPE c    VALUE '(SAPLMG27)IMBEWH[]'.
* end of insertion MOD-004
* begin of insertion MOD-003
CONSTANTS : c_tcode LIKE sy-tcode VALUE 'YSE_FI_REP_STOCKS'.
* end of insertion MOD-003
* begin of insertion MOD-007
CONSTANTS:  c_cn01 TYPE werks VALUE 'CN01',
            c_cn41 TYPE werks VALUE 'CN41'.
* end of insertion MOD-007

SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-b01.
SELECT-OPTIONS: s_bwkey FOR mbew-bwkey,
                s_matnr FOR mbew-matnr,
                s_bklas FOR mbew-bklas,
                s_mtart FOR mara-mtart OBLIGATORY DEFAULT 'ZMAT'.
SELECTION-SCREEN END   OF BLOCK b1.

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-b02.
PARAMETERS: p_dtc AS CHECKBOX,
            p_dtconl AS CHECKBOX.
SELECT-OPTIONS: s_dtc FOR ebew-bwkey. "OBLIGATORY.
SELECTION-SCREEN END   OF BLOCK b2.

SELECTION-SCREEN BEGIN OF BLOCK b3 WITH FRAME TITLE text-b03.
PARAMETERS: p_updf AS CHECKBOX.
SELECTION-SCREEN END OF BLOCK b3.

*.................. Selection screen validations...................... *
* at selection screen on.

AT SELECTION-SCREEN ON BLOCK b2.
  IF p_dtc IS NOT INITIAL AND s_dtc IS INITIAL.
    MESSAGE e001(00) WITH text-e01.
  ENDIF.

  IF p_dtconl IS NOT INITIAL AND s_dtc IS INITIAL.
    MESSAGE e001(00) WITH text-e01.
  ENDIF.

  IF p_dtc IS NOT INITIAL AND p_dtconl IS NOT INITIAL.
    MESSAGE e001(00) WITH text-e02.
  ENDIF.

* begin of insertion MOD-003
  IF sy-batch IS INITIAL.
* end of insertion MOD-003
    LOOP AT s_bwkey.
      AUTHORITY-CHECK OBJECT 'I_SWERK'
                 ID 'TCD'   FIELD sy-tcode
                 ID 'SWERK' FIELD s_bwkey-low.
      IF sy-subrc NE 0.
        MESSAGE e001(00) WITH text-e04 s_bwkey-low.
      ELSEIF NOT s_bwkey-high IS INITIAL.
        AUTHORITY-CHECK OBJECT 'I_SWERK'
                 ID 'TCD'   FIELD sy-tcode
                 ID 'SWERK' FIELD s_bwkey-high.
        IF sy-subrc NE 0.
          MESSAGE e001(00) WITH text-e04 s_bwkey-high.
        ENDIF.
      ENDIF.
    ENDLOOP.

    LOOP AT s_dtc.
      AUTHORITY-CHECK OBJECT 'I_SWERK'
                   ID 'TCD'   FIELD sy-tcode
                   ID 'SWERK' FIELD s_dtc-low.
      IF sy-subrc NE 0.
        MESSAGE e001(00) WITH text-e04 s_dtc-low.
      ELSEIF NOT s_dtc-high IS INITIAL.
        AUTHORITY-CHECK OBJECT 'I_SWERK'
                 ID 'TCD'   FIELD sy-tcode
                 ID 'SWERK' FIELD s_dtc-high.
        IF sy-subrc NE 0.
          MESSAGE e001(00) WITH text-e04 s_dtc-high.
        ENDIF.
      ENDIF.
    ENDLOOP.
* begin of insertion MOD-003
  ELSE.
    LOOP AT s_bwkey.
      AUTHORITY-CHECK OBJECT 'I_SWERK'
                 ID 'TCD'   FIELD c_tcode
                 ID 'SWERK' FIELD s_bwkey-low.
      IF sy-subrc NE 0.
        MESSAGE e001(00) WITH text-e04 s_bwkey-low.
      ELSEIF NOT s_bwkey-high IS INITIAL.
        AUTHORITY-CHECK OBJECT 'I_SWERK'
                 ID 'TCD'   FIELD c_tcode
                 ID 'SWERK' FIELD s_bwkey-high.
        IF sy-subrc NE 0.
          MESSAGE e001(00) WITH text-e04 s_bwkey-high.
        ENDIF.
      ENDIF.
    ENDLOOP.

    LOOP AT s_dtc.
      AUTHORITY-CHECK OBJECT 'I_SWERK'
                   ID 'TCD'   FIELD c_tcode
                   ID 'SWERK' FIELD s_dtc-low.
      IF sy-subrc NE 0.
        MESSAGE e001(00) WITH text-e04 s_dtc-low.
      ELSEIF NOT s_dtc-high IS INITIAL.
        AUTHORITY-CHECK OBJECT 'I_SWERK'
                 ID 'TCD'   FIELD c_tcode
                 ID 'SWERK' FIELD s_dtc-high.
        IF sy-subrc NE 0.
          MESSAGE e001(00) WITH text-e04 s_dtc-high.
        ENDIF.
      ENDIF.
    ENDLOOP.
  ENDIF.
* end of insertion MOD-003
*-----------------------------------------------------------------------
START-OF-SELECTION.


* begin of change MOD-001
* PERFORM authority_begin(rsaqexce).
* PERFORM authority(rsaqexce) USING 'MBEW'.
* PERFORM authority_end(rsaqexce).
PERFORM authority_begin(rsaqexce) USING 'CL_QUERY_TAB_ACCESS_AUTHORITY'.
  PERFORM authority(rsaqexce) USING 'MBEW'
                                       'CL_QUERY_TAB_ACCESS_AUTHORITY'.
  PERFORM authority_end(rsaqexce) USING 'CL_QUERY_TAB_ACCESS_AUTHORITY'.
* end of change MOD-001
* begin of insertion MOD-007
  PERFORM check_werks_cn.
* end of insertion MOD-007
  IF p_dtconl IS INITIAL.

* selection of normal sales stock

    SELECT * FROM mbew INTO TABLE gt_mbew
     WHERE bwkey IN s_bwkey
     AND   bklas IN s_bklas
     AND   matnr IN s_matnr
* begin of insertion MOD-005
     AND   BWTAR eq SPACE.
* MBEW contains one line per batch and one 'total' line. Since currently
* only Russia uses batches, they are the first to stumble across the
* problem.  See example below.
* Material Valuation Area Valuation Type   Total Stock
*
* 409         PL09                               0,000    <- OK
* 409         PL10                               0,000    <- OK
* 409         RU01                              15,000    <- OK
* 409         RU01           0000000034          0,000
* 409         RU01           0000000042          1,000
* 409         RU01           0000000045          1,000
* 409         RU01           0000000049          1,000 etc ....
* end of insertion MOD-005

* sort table
    SORT gt_mbew BY matnr.

* select only the records with a correct material type
    LOOP AT gt_mbew.
      AT NEW matnr.
        CLEAR g_mtart.
        SELECT SINGLE mtart INTO g_mtart FROM mara
         WHERE matnr = gt_mbew-matnr.
      ENDAT.
      IF g_mtart NOT IN s_mtart.
        DELETE gt_mbew.
      ENDIF.
    ENDLOOP.

* call function mbew_extend record by record

    LOOP AT gt_mbew.
      REFRESH gt_mbewt.
      MOVE-CORRESPONDING gt_mbew TO gt_mbewt.
      APPEND gt_mbewt.
* begin of insertion MOD-004
      ASSIGN (c_mbew) TO <mbew>.
      IF <mbew> IS ASSIGNED.
        CLEAR <mbew>.
      ENDIF.
* end of insertion MOD-004
      CALL FUNCTION 'MBEW_EXTEND'
        EXPORTING
          xvper    = 'X'
        TABLES
          mbew_tab = gt_mbewt.
* update gt_mbew
      LOOP AT gt_mbewt.
        MOVE-CORRESPONDING gt_mbewt TO gt_mbew.
        MODIFY gt_mbew.
      ENDLOOP.
* delete entry in table gt_mbew when no values
      IF gt_mbew-vmkum IS INITIAL AND
         gt_mbew-vmsal IS INITIAL AND
         gt_mbew-lbkum IS INITIAL AND
         gt_mbew-salk3 IS INITIAL.
        DELETE gt_mbew.
      ENDIF.
    ENDLOOP.

  ENDIF.

* extra selection +DTC or DTC only

  IF p_dtc = 'X' OR p_dtconl = 'X'.

* selection of DTC stock
    SELECT * FROM ebew INTO TABLE gt_ebew
       WHERE bwkey IN s_dtc
       AND   matnr IN s_matnr.

* sort table
    SORT gt_ebew BY matnr.

* select only the records with a correct material type
    LOOP AT gt_ebew.
      AT NEW matnr.
        CLEAR g_mtart.
        SELECT SINGLE mtart INTO g_mtart FROM mara
         WHERE matnr = gt_ebew-matnr.
      ENDAT.
      IF g_mtart NOT IN s_mtart.
        DELETE gt_ebew.
      ENDIF.
    ENDLOOP.

* call function ebew_extend record by record

    LOOP AT gt_ebew.
      REFRESH gt_ebewt.
      MOVE-CORRESPONDING gt_ebew TO gt_ebewt.
      APPEND gt_ebewt.
      CALL FUNCTION 'EBEW_EXTEND'
        EXPORTING
          xvper    = 'X'
        TABLES
          ebew_tab = gt_ebewt.
* update gt_mbew
      LOOP AT gt_ebewt.
        MOVE-CORRESPONDING gt_ebewt TO gt_ebew.
        MODIFY gt_ebew.
      ENDLOOP.
* delete entry in table gt_ebew when no values
      IF gt_ebew-vmkum IS INITIAL AND
         gt_ebew-vmsal IS INITIAL AND
         gt_ebew-lbkum IS INITIAL AND
         gt_ebew-salk3 IS INITIAL.
        DELETE gt_ebew.
      ENDIF.
    ENDLOOP.
* flag p_dtc = on
  ENDIF.

* add table gt_mbew to table gt_out

  IF NOT gt_mbew[] IS INITIAL.
    LOOP AT gt_mbew.
      CLEAR gt_out.
      MOVE-CORRESPONDING gt_mbew TO gt_out.
* profit center
      SELECT SINGLE prctr INTO gt_out-zprctr FROM marc
       WHERE matnr = gt_mbew-matnr
       AND   werks = gt_mbew-bwkey.
* begin of insert MOD-006
* segment
      if sy-subrc = 0.
        SELECT SINGLE segment INTO gt_out-segment
          FROM yse_prctr_bl WHERE prctr = gt_out-zprctr.
      endif.
* end of insert MOD-006
* values current period
      gt_out-lbkum = gt_mbew-lbkum.
      gt_out-salk3 = gt_mbew-salk3.
* values previous period
      gt_out-vmkum = gt_mbew-vmkum.
      gt_out-vmsal = gt_mbew-vmsal.
* save in gt_out
      APPEND gt_out.
    ENDLOOP.
  ENDIF.

* add table gt_ebew to table gt_out

  IF NOT gt_ebew[] IS INITIAL.
    LOOP AT gt_ebew.
      READ TABLE gt_out WITH KEY bwkey = gt_ebew-bwkey
                                 matnr = gt_ebew-matnr.
      IF sy-subrc = 0.
        wa_out = gt_out.
* values current period
        wa_out-lbkum = wa_out-lbkum + gt_ebew-lbkum.
        wa_out-salk3 = wa_out-salk3 + gt_ebew-salk3.
* values previous period
        wa_out-vmkum = wa_out-vmkum + gt_ebew-vmkum.
        wa_out-vmsal = wa_out-vmsal + gt_ebew-vmsal.
        MODIFY gt_out FROM wa_out INDEX sy-tabix.
      ELSE.
        CLEAR gt_out.
        MOVE-CORRESPONDING gt_ebew TO gt_out.
* profit center
        SELECT SINGLE prctr INTO gt_out-zprctr FROM marc
         WHERE matnr = gt_ebew-matnr
         AND   werks = gt_ebew-bwkey.
* begin of insert MOD-006
* segment
        if sy-subrc = 0.
          SELECT SINGLE segment INTO gt_out-segment
            FROM yse_prctr_bl WHERE prctr = gt_out-zprctr.
        endif.
* end of insert MOD-006
* values current period
        gt_out-lbkum = gt_ebew-lbkum.
        gt_out-salk3 = gt_ebew-salk3.
* values previous period
        gt_out-vmkum = gt_ebew-vmkum.
        gt_out-vmsal = gt_ebew-vmsal.
* save in gt_out
        APPEND gt_out.
      ENDIF.
    ENDLOOP.
  ENDIF.

* following things only once for a new material number

  SORT gt_out BY matnr.

  IF NOT gt_out[] IS INITIAL.
    LOOP AT gt_out.
      AT NEW matnr.
* gac and pgc
        CLEAR g_prdha.
        SELECT SINGLE prdha INTO g_prdha FROM mara
         WHERE matnr = gt_out-matnr.
* material description
        CLEAR g_maktx.
        SELECT SINGLE maktx INTO g_maktx FROM makt
         WHERE matnr = gt_out-matnr AND spras = c_spras.
      ENDAT.
      gt_out-zgac = g_prdha(4).
      gt_out-zpgc = g_prdha+4(4).
      gt_out-maktx = g_maktx.
      MODIFY gt_out.
    ENDLOOP.
  ENDIF.

* begin of insert MOD-007
  IF g_flg_cn IS NOT INITIAL.
    DATA: lt_out LIKE STANDARD TABLE OF gt_out,
          lt_eord TYPE STANDARD TABLE OF ty_eord,
          lt_lfa1 TYPE STANDARD TABLE OF ty_lfa1,
          ls_eord TYPE ty_eord,
          ls_lfa1 TYPE ty_lfa1.
    lt_out = gt_out[].
    SORT   lt_out by matnr bwkey.
    delete ADJACENT DUPLICATES FROM lt_out COMPARING matnr bwkey.
    IF lt_out IS NOT INITIAL.
      SELECT  matnr                   "Material Number
              werks                   "Plant
              lifnr                   "Vendor Account Number
              flifn                   "Indicator: Fixed vendor
        FROM  eord
        INTO TABLE lt_eord
        FOR ALL ENTRIES IN lt_out
        WHERE matnr = lt_out-matnr
          AND werks = lt_out-bwkey
          AND flifn = 'X'.
      SELECT  matnr                   "Material Number
              werks                   "Plant
              lifnr                   "Vendor Account Number
              flifn                   "Indicator: Fixed vendor
        FROM  eord
        APPENDING TABLE lt_eord
        FOR ALL ENTRIES IN lt_out
        WHERE matnr = lt_out-matnr
          AND ( werks = c_cn01 OR werks = c_cn41 )
          AND flifn = 'X'.
      IF lt_eord IS NOT INITIAL.
        SELECT  lifnr                 "Account Number of Vendor
                ktokk                 "Vendor account group
          FROM lfa1
          INTO TABLE lt_lfa1
          FOR ALL ENTRIES IN lt_eord
          WHERE lifnr = lt_eord-lifnr.
      ENDIF.
      LOOP AT gt_out.
        CLEAR: ls_eord.
        READ TABLE lt_eord INTO ls_eord
          with key matnr = gt_out-matnr
                   werks = gt_out-bwkey.
        IF sy-subrc = 0.
          gt_out-lifnr = ls_eord-lifnr.
        ELSE.
          CLEAR: ls_eord.
          READ TABLE lt_eord INTO ls_eord
            with key matnr = gt_out-matnr
                     werks = c_cn01.
          IF sy-subrc = 0.
            gt_out-lifnr = ls_eord-lifnr.
          ELSE.
            CLEAR: ls_eord.
            READ TABLE lt_eord INTO ls_eord
              with key matnr = gt_out-matnr
                       werks = c_cn41.
            IF sy-subrc = 0.
              gt_out-lifnr = ls_eord-lifnr.
            ENDIF.
          ENDIF.
        ENDIF.
        CLEAR ls_lfa1.
        READ TABLE lt_lfa1 INTO ls_lfa1
          WITH KEY lifnr = ls_eord-lifnr.
        IF sy-subrc = 0.
          gt_out-ktokk = ls_lfa1-ktokk.
        ENDIF.
        MODIFY gt_out.
      ENDLOOP.
    ENDIF.
    REFRESH: lt_out.
  ENDIF.
* begin of insert MOD-007
*-----------------------------------------------------------------------
END-OF-SELECTION.

  IF gt_out[] IS INITIAL.
    SKIP 3.
    WRITE: /15 'No result in this selection'(015).
    STOP.
  ENDIF.

  SORT gt_out BY bwkey matnr.

* create ALV-GRID
  PERFORM build_field_catlog CHANGING gt_fieldcat.
  PERFORM fill_events_f14.
  PERFORM alv_display.
* update file YSE_STOCK_REPORT
  IF p_updf = 'X'.
    PERFORM update_file.
  ENDIF.

*-----------------------------------------------------------------------
TOP-OF-PAGE.

*&---------------------------------------------------------------------*
*&      Form  build_field_catlog
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      <--P_GT_FIELDCAT  text
*----------------------------------------------------------------------*
FORM build_field_catlog  CHANGING pt_fieldcat TYPE slis_t_fieldcat_alv.

  DATA : ls_fcat TYPE slis_fieldcat_alv.
*---------------------Plant-----------------------*
  ls_fcat-fieldname = 'BWKEY'.
  ls_fcat-seltext_l = 'Plant'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------------Material--------------------*
  ls_fcat-fieldname = 'MATNR'.
  ls_fcat-rollname = 'MATNR'.
  ls_fcat-outputlen = '18'.
  ls_fcat-no_convext = 'X'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*------------------Material description-----------*
  ls_fcat-fieldname = 'MAKTX'.
  ls_fcat-seltext_l = 'Material description'.
* begin of change MOD-004
*  ls_fcat-outputlen = '35'.
  ls_fcat-outputlen = '40'.
* end of changde MOD-004
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
* begin of insert MOD-006
*------------------Profit Center------------------*
  ls_fcat-fieldname = 'SEGMENT'.
  ls_fcat-seltext_l = 'Segment'.
  ls_fcat-outputlen = '10'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
* end of insert MOD-006
*------------------Profit Center------------------*
  ls_fcat-fieldname = 'ZPRCTR'.
  ls_fcat-seltext_l = 'Prof. Cntr'.
  ls_fcat-outputlen = '10'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------GAC--------------------*
  ls_fcat-fieldname = 'ZGAC'.
  ls_fcat-seltext_l = 'GAC'.
  ls_fcat-outputlen = '4'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------PGC--------------------*
  ls_fcat-fieldname = 'ZPGC'.
  ls_fcat-seltext_l = 'PGC'.
  ls_fcat-outputlen = '4'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------total stock current period-------------*
  ls_fcat-fieldname = 'LBKUM'.
  ls_fcat-seltext_l = 'Stock curr.period'.
  ls_fcat-outputlen = '17'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------value stock current period-------------*
  ls_fcat-fieldname = 'SALK3'.
  ls_fcat-seltext_l = 'Value curr.period'.
  ls_fcat-outputlen = '17'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------total stock prev period-------------*
  ls_fcat-fieldname = 'VMKUM'.
  ls_fcat-seltext_l = 'Stock prev.period'.
  ls_fcat-outputlen = '17'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------value stock prev period-------------*
  ls_fcat-fieldname = 'VMSAL'.
  ls_fcat-seltext_l = 'Value prev.period'.
  ls_fcat-outputlen = '17'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------standard price-------------*
  ls_fcat-fieldname = 'STPRS'.
  ls_fcat-seltext_l = 'Standard Price'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------Tax price 1 -------------*
  ls_fcat-fieldname = 'BWPRS'.
  ls_fcat-seltext_l = 'Tax Price 1'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------Future Price -------------*
  ls_fcat-fieldname = 'ZKPRS'.
  ls_fcat-seltext_l = 'Future Price'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------Devaluation indicator-------------*
  ls_fcat-fieldname = 'ABWKZ'.
  ls_fcat-seltext_l = 'Dev.Ind'.
  ls_fcat-outputlen = '07'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------- year-------------*
  ls_fcat-fieldname = 'LFGJA'.
  ls_fcat-seltext_l = 'Last Yr.Mvt'.
  ls_fcat-outputlen = '11'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------- period------------*
  ls_fcat-fieldname = 'LFMON'.
  ls_fcat-seltext_l = 'Last Per.Mvt'.
  ls_fcat-outputlen = '12'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*----------Valuation Class-----------*
  ls_fcat-fieldname = 'BKLAS'.
  ls_fcat-seltext_l = 'Val.Cl'.
  ls_fcat-outputlen = '06'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
* begin of insertion MOD-007
  IF g_flg_cn IS NOT INITIAL.
*  ----------Vendor number  -----------*
    ls_fcat-fieldname = 'LIFNR'.
    ls_fcat-seltext_l = 'Vendor Number'.
    ls_fcat-outputlen = '10'.
    APPEND ls_fcat TO pt_fieldcat.
    CLEAR ls_fcat.
*  --Vendor account assignment group--*
    ls_fcat-fieldname = 'KTOKK'.
    ls_fcat-seltext_l = 'Vendor Account Group'.
    ls_fcat-outputlen = '4'.
    APPEND ls_fcat TO pt_fieldcat.
    CLEAR ls_fcat.
  ENDIF.
* end of insertion MOD-007
ENDFORM.                    " build_field_catlog

*&---------------------------------------------------------------------*
*&      Form  fill_events_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM fill_events_f14 .

  DATA h_event       TYPE slis_alv_event.

  CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
    EXPORTING
      i_list_type = 0
    IMPORTING
      et_events   = g_events_tab.

*--- allocate form for user-command ---------------------------------*
  READ TABLE g_events_tab WITH KEY name = slis_ev_user_command
                        INTO h_event.
  IF sy-subrc = 0.
    MOVE g_form_user_command TO h_event-form.
    MODIFY g_events_tab FROM h_event INDEX sy-tabix.
  ENDIF.

ENDFORM.                    " fill_events_f14

*&--------------------------------------------------------------------*
*&      Form  user_command_l
*&--------------------------------------------------------------------*
*       text
*---------------------------------------------------------------------*
*      -->P_UCOMM    text
*      -->P_SELFIELD text
*---------------------------------------------------------------------*
FORM user_command_l USING p_ucomm LIKE sy-ucomm
                          p_selfield TYPE slis_selfield.

  p_selfield-refresh = c_s.
  PERFORM check_pf2_with_object_f16 USING p_ucomm.
  PERFORM set_p_selfield_general_f16 USING p_selfield.

  CASE p_ucomm.
    WHEN 'ISEL'.
      p_ucomm = 'DISP'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
  ENDCASE.

ENDFORM.                    "user_command_l

*&---------------------------------------------------------------------*
*&      Form  check_pf2_with_object_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*----------------------------------------------------------------------*
FORM check_pf2_with_object_f16  USING    p_ucomm.

  CHECK p_ucomm = g_ic1.
  p_ucomm = 'ISEL'.

ENDFORM.                    " check_pf2_with_object_f16

*&---------------------------------------------------------------------*
*&      Form  set_p_selfield_general_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM set_p_selfield_general_f16  USING f_selfield TYPE slis_selfield.

  f_selfield-col_stable = c_x.
  f_selfield-row_stable = c_x.

ENDFORM.                    " set_p_selfield_general_f16

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*      -->P_ENDCASE  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_f16  USING p_ucomm LIKE sy-ucomm
                                p_selfield TYPE slis_selfield.

  PERFORM check_object_tab_marked_f14 USING p_ucomm p_selfield.

  LOOP AT gt_out WHERE selected = c_x .
    l_index = sy-tabix.
    PERFORM fcodes_with_mark_l USING p_ucomm p_selfield.
    gt_out-selected = ' '.
    MODIFY gt_out INDEX l_index.
  ENDLOOP.

  CLEAR p_ucomm.

ENDFORM.                    " fcodes_with_mark_f16

*&---------------------------------------------------------------------*
*&      Form  check_object_tab_marked_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM check_object_tab_marked_f14  USING    p_ucomm LIKE sy-ucomm
                                        p_selfield TYPE slis_selfield.

  READ TABLE gt_out WITH KEY selected = c_x.

  IF NOT sy-subrc IS INITIAL.
    IF NOT p_selfield-tabindex IS INITIAL.
      READ TABLE gt_out  INDEX p_selfield-tabindex.
      gt_out-selected = c_x.
      MODIFY gt_out  INDEX p_selfield-tabindex.
    ENDIF.
  ELSE.
*--- Checkbox markiert -----------------------------------------------*
    p_selfield-sel_tab_field = 'G_MARK'.
  ENDIF.

ENDFORM.                    " check_object_tab_marked_f14

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_l
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_F_UCOMM  text
*      -->P_F_SELFIELD  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_l  USING   p_ucomm LIKE sy-ucomm
                              p_selfield TYPE slis_selfield.

  DATA: h_ucomm LIKE sy-ucomm.

  CASE p_ucomm.
*   Display material number
    WHEN 'DISP'.
      SET PARAMETER ID 'MAT' FIELD  gt_out-matnr.
      CALL TRANSACTION 'MM03' AND SKIP FIRST SCREEN.
  ENDCASE.

ENDFORM.                    " fcodes_with_mark_l

*&---------------------------------------------------------------------*
*&      Form  alv_display
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM alv_display .

  g_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
     EXPORTING
       i_callback_program                =  g_repid
       i_save                            = 'A'
       it_events                         =  g_events_tab[]
*      I_GRID_TITLE                      =
*      I_GRID_SETTINGS                   =
*      is_layout                         =  g_layout
       it_fieldcat                       =  gt_fieldcat[]
     TABLES
        t_outtab                         =  gt_out.

ENDFORM.                    " alv_display
*&---------------------------------------------------------------------*
*&      Form  update_file
*&---------------------------------------------------------------------*
*       update file YSE_STOCK_REP
*----------------------------------------------------------------------*
FORM update_file .

* delete existing records for the selected valuation keys
  DELETE FROM yse_stock_report CLIENT SPECIFIED
      WHERE mandt EQ sy-mandt
        AND bwkey IN s_bwkey.
  COMMIT WORK AND WAIT.
* insert records
  IF NOT gt_out[] IS INITIAL.
    LOOP AT gt_out.
      wa_stock_report-mandt = sy-mandt.
      MOVE-CORRESPONDING gt_out TO wa_stock_report.
      INSERT yse_stock_report FROM wa_stock_report.
    ENDLOOP.
    COMMIT WORK AND WAIT.
    MESSAGE i001(00) WITH text-i01.

  ENDIF.


ENDFORM.                    "updat
* begin of insertion MOD-007
*&---------------------------------------------------------------------*
*&      Form  CHECK_WERKS_CN
*&---------------------------------------------------------------------*
*       Check if the plant belong to china(including HK)
*----------------------------------------------------------------------*
form CHECK_WERKS_CN .
  DATA: lt_t001w TYPE STANDARD TABLE OF t001w,
        ls_t001w TYPE t001w.
  SELECT *
    FROM t001w
    INTO TABLE lt_t001w
    WHERE werks in s_bwkey.
  LOOP AT lt_t001w INTO ls_t001w.
    IF    ls_t001w-werks(2) = 'CN'
      or  ls_t001w-werks(2) = 'HK'.
      g_flg_cn = 'X'.
      EXIT.
    ENDIF.
  ENDLOOP.
endform.                    " CHECK_WERKS_CN

* end of insertion MOD-007
*Text symbol text£º
*001:created
*015:No result in this selection
*B01:Selection
*B02:DTC Stock
*B03:Update file
*E01:Fill in plants for DTC
*E02:Select only 1 option for DTC-stock
*E03:Fill in plants please
*E04:Not authorised for plant:

*I01:File YSE_STOCK_REPORT updated.
*Selection text£º
*P_DTC:        Include DTC-Stock
*P_DTCONL:        DTC-Stock only
*P_UPDF:        Update file YSE_STOCK_REPORT
*S_BKLAS:        Valuation Class
*S_BWKEY:        Plant
*S_DTC:        DTC Plants
*S_MATNR:        Material Number
*S_MTART:        Material Type
