*&---------------------------------------------------------------------*
* PROGRAM ID           : YSE_NEWGAC_ADAPT_NE                           *
* PROGRAM TITLE        :                                               *
* AUTHOR               : Anda Wu                                       *
* DATE                 : 03/11/2016                                    *
* DEVELOPMENT ID       : CD1K990054                                    *
* CHANGE REQUEST NUMBER: CR4052                                        *
* PROGRAM DESCRIPTION  : Upate the Material master data  to adapt      *
*                        new GAC (New version)                         *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
REPORT  yse_newgac_adapt_new.
************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS: slis,truxs.

************************************************************************
* TABLES DECLARATIONS
************************************************************************
TABLES: marc,mvke.

************************************************************************
* TYPES
************************************************************************
TYPES: BEGIN OF ty_marc,
        matnr     TYPE marc-matnr,
        werks     TYPE marc-werks,
        prctr     TYPE marc-prctr,
       END OF ty_marc,

       BEGIN OF ty_mvke,
         matnr    TYPE mvke-matnr,
         vkorg    TYPE mvke-vkorg,
         vtweg    TYPE mvke-vtweg,
         prodh    TYPE mvke-prodh,
       END OF ty_mvke,

       BEGIN OF ty_file_in,
         matnr    TYPE marc-matnr,
         gac_new  TYPE prctr,
         pgc_new  TYPE char10,
       END OF ty_file_in,

       BEGIN OF ty_object,
         matnr    TYPE  marc-matnr,
       END OF ty_object,

       BEGIN OF ty_errlog,
        matnr   TYPE marc-matnr,
        werks   TYPE marc-werks,
        vkorg   TYPE mvke-vkorg,
        vtweg   TYPE mvke-vtweg,
        errmsg  TYPE char128,
       END OF ty_errlog.

************************************************************************
* VARIABLES DECLARATIONS
************************************************************************
DATA:   gv_ucomm          TYPE sy-ucomm,
        gt_fieldcat       TYPE slis_t_fieldcat_alv,
        gv_noalvlog       TYPE char1,
        gv_suc_pc       TYPE char10,
        gv_suc_ph       TYPE char10.

************************************************************************
* WORKAERA
************************************************************************
DATA: gs_layout       TYPE slis_layout_alv.

************************************************************************
* CONSTANTS DECLARATIONS
************************************************************************
DATA: gc_x    TYPE char1 VALUE 'X',
      gc_pkgs TYPE i     VALUE 100000,
      gc_prc_line TYPE i     VALUE 200000,
      gc_callback_routine TYPE slis_formname VALUE 'USER_COMMAND'.

************************************************************************
* INTERNAL TABLE DECLARATIONS
************************************************************************
DATA: gt_errlog_ph    TYPE STANDARD TABLE OF ty_errlog,
      gt_errlog_pc    TYPE STANDARD TABLE OF ty_errlog,
      gt_ngac_log_pc  TYPE STANDARD TABLE OF yse_mat_ngac_log,
      gt_ngac_log_ph  TYPE STANDARD TABLE OF yse_mat_ngac_log.

************************************************************************
* SELECTION SCREEN
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b0 WITH FRAME TITLE text-003.
PARAMETER: rb_upl RADIOBUTTON GROUP gp1 DEFAULT 'X'
                  USER-COMMAND fun MODIF ID m0,
           rb_upd RADIOBUTTON GROUP gp1,
           rb_reset RADIOBUTTON GROUP gp1.
SELECTION-SCREEN END OF BLOCK b0.

SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-002.
PARAMETER: cb_ph AS CHECKBOX USER-COMMAND ph MODIF ID m1,
           cb_pc AS CHECKBOX USER-COMMAND pc MODIF ID m1,
           cb_st AS CHECKBOX USER-COMMAND pc MODIF ID m1.
SELECTION-SCREEN END OF BLOCK b1.

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-001.
SELECT-OPTIONS: s_matnr FOR mvke-matnr MODIF ID m2,
                s_vkorg FOR mvke-vkorg MODIF ID m3,
                s_vtweg FOR mvke-vtweg MODIF ID m3,
                s_werks FOR marc-werks MODIF ID m4.
PARAMETER:  p_file TYPE char128 MODIF ID m5.
SELECTION-SCREEN END OF BLOCK b2.

AT SELECTION-SCREEN OUTPUT.
  PERFORM frm_screen_set.

AT SELECTION-SCREEN.
  gv_ucomm = sy-ucomm.
  IF sy-ucomm  IS INITIAL OR sy-ucomm = 'ONLI'.
    PERFORM frm_screen_check.
  ENDIF.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR  p_file.
  PERFORM file_f4_help.

************************************************************************
* INITIALIZATION.
************************************************************************
INITIALIZATION.
* Do initialization
  PERFORM do_init.

************************************************************************
* START-OF-SELECTION
************************************************************************
START-OF-SELECTION.
  CASE 'X'.
    WHEN rb_upl.
*     Upload the file to SAP customerizing table
      PERFORM do_upload.
    WHEN rb_upd.
*     Update the materail master date
      PERFORM do_update.
    WHEN rb_reset.
*     Reset the customerizing table YSE_MAT_NGAC_LOG
      PERFORM do_reset.
    WHEN OTHERS.
  ENDCASE.

END-OF-SELECTION.
* Show the procee error log
  PERFORM show_log.

*&---------------------------------------------------------------------*
*&      Form  DO_INIT
*&---------------------------------------------------------------------*
*       Do initialization
*----------------------------------------------------------------------*
FORM do_init .

  REFRESH:  gt_errlog_ph,
            gt_errlog_pc,
            gt_ngac_log_pc,
            gt_ngac_log_ph,
            gt_fieldcat.

  gv_noalvlog = gc_x.
  CLEAR: gs_layout.
  gv_suc_ph = 0.
  gv_suc_pc = 0.
ENDFORM.                    " DO_INIT
*&---------------------------------------------------------------------*
*&      Form  GET_MATERIALS_DATA
*&---------------------------------------------------------------------*
*       Get the material data per selection screen
*----------------------------------------------------------------------*
FORM get_materials_data .

  IF cb_pc IS NOT INITIAL.
    SELECT *
      FROM yse_mat_ngac_log
      INTO TABLE gt_ngac_log_pc
      WHERE matnr IN s_matnr
        AND werks IN s_werks
        AND upd_ind = 'Y'
        AND ( flag_pc = 'E' OR flag_pc = space )
        AND werks <> space.
*    IF sy-subrc <> 0.
*      IF  cb_ph IS INITIAL .
*        MESSAGE s001(00) WITH text-m02 DISPLAY LIKE 'E'.
*        LEAVE LIST-PROCESSING.
*      ENDIF.
*    ENDIF.

  ENDIF.

  IF cb_ph IS NOT INITIAL.
    SELECT *
      FROM yse_mat_ngac_log
      INTO TABLE gt_ngac_log_ph
      WHERE matnr IN s_matnr
        AND vkorg IN s_vkorg
        AND vtweg IN s_vtweg
        AND upd_ind = 'Y'
        AND ( flag_ph = 'E' OR flag_ph = space ).
    DELETE gt_ngac_log_ph WHERE vkorg = space
                            AND vtweg = space.
*    IF sy-subrc <> 0.
*      IF cb_pc IS INITIAL.
*        MESSAGE s001(00) WITH text-m92 DISPLAY LIKE 'E'.
*        LEAVE LIST-PROCESSING.
*      ENDIF.
*    ENDIF.
  ENDIF.

  IF cb_ph IS NOT INITIAL OR cb_pc IS NOT INITIAL.
    IF    gt_ngac_log_pc IS INITIAL
      AND gt_ngac_log_ph IS INITIAL.
      MESSAGE s001(00) WITH text-m82 DISPLAY LIKE 'E'.
      LEAVE LIST-PROCESSING.
    ENDIF.
  ENDIF.


ENDFORM.                    " GET_MATERIALS_DATA
*&---------------------------------------------------------------------*
*&      Form  UPDATE_MATERIALS
*&---------------------------------------------------------------------*
*       Update the materials per the mapping
*----------------------------------------------------------------------*
FORM update_materials .

  DATA:
        lflg_update   TYPE char1,
        ls_errlog     TYPE ty_errlog,
        lv_tabix      TYPE sy-tabix,
        lv_count      TYPE sy-tabix,
        lt_log_delt   TYPE STANDARD TABLE OF yse_mat_ngac_log,
        ls_ngc_log    TYPE yse_mat_ngac_log.

  DATA : ls_headdata      TYPE bapimathead,
         ls_plantdata     TYPE bapi_marc,
         ls_plantdatax    TYPE bapi_marcx,
         ls_salesdata     TYPE bapi_mvke,
         ls_salesdatax    TYPE bapi_mvkex,
         ls_return        TYPE bapiret2.

  IF cb_ph IS NOT INITIAL.
    LOOP AT gt_ngac_log_ph INTO ls_ngc_log.
      lv_tabix = sy-tabix.
      lv_count = lv_count + 1.
      CLEAR:    ls_headdata,
                ls_salesdata,
                ls_salesdatax,
                lflg_update   ,
                ls_errlog     .

*     Material head data
      ls_headdata-material = ls_ngc_log-matnr.
      ls_headdata-sales_view   =  gc_x.
*      ls_headdata-cost_view    = gc_x.

      IF ls_ngc_log-nprodh <> ls_ngc_log-oprodh.
        ls_salesdata-sales_org = ls_ngc_log-vkorg.
        ls_salesdata-distr_chan = ls_ngc_log-vtweg.
        ls_salesdata-prod_hier  = ls_ngc_log-nprodh.
        ls_salesdatax-sales_org = ls_ngc_log-vkorg.
        ls_salesdatax-distr_chan = ls_ngc_log-vtweg.
        ls_salesdatax-prod_hier = gc_x.
      ELSE.
        CONTINUE.
      ENDIF.

      CALL FUNCTION 'BAPI_MATERIAL_SAVEDATA'
        EXPORTING
          headdata   = ls_headdata
          salesdata  = ls_salesdata
          salesdatax = ls_salesdatax
        IMPORTING
          return     = ls_return.
*     Check if the update was successful.
      IF ls_return-type = 'S'.
*       save the record into the database.
        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
          EXPORTING
            wait = gc_x.
        gv_suc_ph = gv_suc_ph + 1.
        ls_ngc_log-flag_ph = 'U'.
        CLEAR: ls_ngc_log-txt_ph.
*        ls_ngc_log-upd_ind = 'N'.
*        ls_ngc_log-txt_ph  = ls_return-message.
      ELSE.
        CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
        CLEAR ls_errlog.
        ls_errlog-matnr  = ls_ngc_log-matnr.
        ls_errlog-vkorg  = ls_ngc_log-vkorg.
        ls_errlog-vtweg  = ls_ngc_log-vtweg.
        ls_errlog-errmsg = ls_return-message.
        APPEND ls_errlog TO gt_errlog_ph.
        ls_ngc_log-flag_ph = 'E'.
        ls_ngc_log-txt_ph  = ls_return-message.
      ENDIF.
      ls_ngc_log-aenam  = sy-uname.
      ls_ngc_log-aedat  = sy-datum.
      ls_ngc_log-aezet  = sy-uzeit.
      APPEND ls_ngc_log TO lt_log_delt.
      DELETE gt_ngac_log_ph INDEX lv_tabix.
      IF lv_count = 10000.
        MODIFY yse_mat_ngac_log FROM TABLE lt_log_delt.
        lv_count = 0.
        REFRESH: lt_log_delt.
      ENDIF.
    ENDLOOP.
  ENDIF.

  REFRESH: gt_ngac_log_ph.

  IF cb_pc IS NOT INITIAL.
    LOOP AT gt_ngac_log_pc INTO ls_ngc_log .
      lv_tabix = sy-tabix.
      lv_count = lv_count + 1.
      CLEAR:    ls_headdata   ,
                ls_plantdata  ,
                ls_plantdatax ,
                lflg_update   ,
                ls_errlog     .
*     Material head data
      ls_headdata-material = ls_ngc_log-matnr.
*      ls_headdata-basic_view  = gc_x.
      ls_headdata-sales_view   =  gc_x.
*      ls_headdata-cost_view    = gc_x.

      IF ls_ngc_log-nprctr <> ls_ngc_log-oprctr.
        ls_plantdata-plant = ls_ngc_log-werks.
        ls_plantdata-profit_ctr = ls_ngc_log-nprctr.
        ls_plantdatax-plant = ls_ngc_log-werks.
        ls_plantdatax-profit_ctr = gc_x.
      ELSE.
        CONTINUE.
      ENDIF.

      CALL FUNCTION 'BAPI_MATERIAL_SAVEDATA'
        EXPORTING
          headdata   = ls_headdata
          plantdata  = ls_plantdata
          plantdatax = ls_plantdatax
        IMPORTING
          return     = ls_return.
*     Check if the update was successful.
      IF ls_return-type = 'S'.
*       save the record into the database.
        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
          EXPORTING
            wait = gc_x.
        gv_suc_pc = gv_suc_pc + 1.
        ls_ngc_log-flag_pc = 'U'.
*        ls_ngc_log-upd_ind = 'N'.
        CLEAR: ls_ngc_log-txt_pc.
      ELSE.
        CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
        CLEAR ls_errlog.
        ls_errlog-matnr  = ls_ngc_log-matnr.
        ls_errlog-werks  = ls_ngc_log-werks.
        ls_errlog-errmsg = ls_return-message.
        APPEND ls_errlog TO gt_errlog_pc.
        ls_ngc_log-flag_pc = 'E'.
        ls_ngc_log-txt_pc  = ls_return-message.
      ENDIF.
      ls_ngc_log-aenam  = sy-uname.
      ls_ngc_log-aedat  = sy-datum.
      ls_ngc_log-aezet  = sy-uzeit.
      APPEND ls_ngc_log TO lt_log_delt.
      DELETE gt_ngac_log_pc INDEX lv_tabix.
      IF lv_count = 10000.
        MODIFY yse_mat_ngac_log FROM TABLE lt_log_delt.
        lv_count = 0.
        REFRESH: lt_log_delt.
      ENDIF.
    ENDLOOP.
  ENDIF.
  MODIFY yse_mat_ngac_log FROM TABLE lt_log_delt.
  COMMIT WORK AND WAIT.

ENDFORM.                    " UPDATE_MATERIALS
*&---------------------------------------------------------------------*
*&      Form  SHOW_LOG
*&---------------------------------------------------------------------*
*       Show the procee error log
*----------------------------------------------------------------------*
FORM show_log .

  DATA: lv_err_no_gac TYPE char10,
        lv_err_no_ph  TYPE char10,
        lv_message1 TYPE string,
        lv_message2 TYPE string,
        lv_message3 TYPE string,
        lv_message4 TYPE string.

  IF rb_upd IS NOT INITIAL.
    DESCRIBE TABLE gt_errlog_pc LINES lv_err_no_gac.
    DESCRIBE TABLE gt_errlog_ph LINES lv_err_no_ph.

    CONDENSE: gv_suc_pc,gv_suc_ph,lv_err_no_ph,lv_err_no_gac,
              lv_message1,lv_message2,lv_message3,lv_message4.

    IF cb_pc IS NOT INITIAL.
      lv_message1 = text-i10.
      lv_message2 = text-i11.
      REPLACE '&1' IN lv_message1 WITH gv_suc_pc.
      REPLACE '&1' IN lv_message2 WITH lv_err_no_gac.
    ENDIF.

    IF cb_ph IS NOT INITIAL.
      CLEAR: lv_message3, lv_message4.
      lv_message3 = text-i10.
      lv_message4 = text-i21.
      REPLACE '&1' IN lv_message3 WITH gv_suc_ph.
      REPLACE '&1' IN lv_message4 WITH lv_err_no_ph.
      IF lv_message1 IS NOT INITIAL.
        CONCATENATE cl_abap_char_utilities=>vertical_tab
          lv_message3 INTO lv_message3.
      ENDIF.
    ENDIF.

    IF cb_st IS NOT INITIAL.
      IF lv_message4 IS INITIAL.
        lv_message4 = text-t31.
      ELSE.
        CONCATENATE lv_message4 '|' text-t31 INTO lv_message4.
      ENDIF.
    ENDIF.


    MESSAGE i001(00) WITH lv_message1 lv_message2
                          lv_message3 lv_message4.
    IF gv_noalvlog IS INITIAL.
*     FIELDCAT set
      PERFORM alv_fieldcat_set.
*     LAYOUT set
      PERFORM alv_layout_set.
*     ALV display
      PERFORM alv_display.
    ENDIF.
  ENDIF.


ENDFORM.                    " SHOW_LOG

*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM alv_fieldcat_set .

  DATA:
     lv_linecnt  TYPE i,
     ls_fieldcat TYPE slis_fieldcat_alv.

  CLEAR lv_linecnt.
* Material Number
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'MATNR'.
  ls_fieldcat-seltext_s = 'Mat.No'(030).
  ls_fieldcat-seltext_m = 'Material Number'(031).
  ls_fieldcat-hotspot = gc_x.
  ls_fieldcat-ref_fieldname =  'MATNR'.
  ls_fieldcat-ref_tabname   = 'MARA'.
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* Sales org
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'VKORG'.
  ls_fieldcat-seltext_s = 'Sales Org'(052).
  ls_fieldcat-seltext_m = 'Sales Organization'(053).
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* Distribute channel
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'VTWEG'.
  ls_fieldcat-seltext_s = 'Distr. Chl'(054).
  ls_fieldcat-seltext_m = 'Distribution Channel'(055).
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* Plant
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'WERKS'.
  ls_fieldcat-seltext_s = 'Plant'(032).
  ls_fieldcat-seltext_m = 'Plant'(032).
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* Error message
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'ERRMSG'.
  ls_fieldcat-seltext_s = 'Err.Msg'(033).
  ls_fieldcat-seltext_m = 'Error Message'(034).
  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   " Auto optimize column width
  gs_layout-detail_popup = gc_x.        " Show detail screen

ENDFORM.                    " ALV_LAYOUT_SET

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .
  DATA: lv_callback TYPE slis_formname,
        lt_errlog   TYPE STANDARD TABLE OF ty_errlog.
  APPEND LINES OF gt_errlog_pc TO lt_errlog.
  APPEND LINES OF gt_errlog_ph TO lt_errlog.
  CHECK lt_errlog IS NOT INITIAL.
  lv_callback = gc_callback_routine.
  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = sy-repid
      is_layout               = gs_layout
      i_callback_user_command = lv_callback
      it_fieldcat             = gt_fieldcat
    TABLES
      t_outtab                = lt_errlog
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       USER_COMMAND
*----------------------------------------------------------------------*
FORM user_command USING u_ucomm     LIKE sy-ucomm           "#EC CALLED
                        p_selfield  TYPE slis_selfield.

  CASE u_ucomm.
    WHEN '&IC1'.        " SAP standard code for double-clicking
      IF p_selfield-fieldname = 'MATNR'.
        IF p_selfield-value IS NOT INITIAL.
          SET PARAMETER ID 'MAT' FIELD p_selfield-value.
          CALL TRANSACTION 'MM03' AND SKIP FIRST SCREEN.
        ENDIF.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.

ENDFORM.                    " USER_COMMAND
*&---------------------------------------------------------------------*
*&      Form  FRM_SCREEN_SET
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM frm_screen_set .
  IF rb_upl  IS NOT INITIAL.
    LOOP AT SCREEN.
      IF    screen-group1 = 'M1'
        OR  screen-group1 = 'M2'.
        screen-input = '0'.
        screen-invisible = '1'.
        MODIFY SCREEN .
      ENDIF.
    ENDLOOP.
  ELSEIF rb_upd IS NOT INITIAL.
    LOOP AT SCREEN.
      IF screen-group1 = 'M5'.
        screen-input = '0'.
        screen-invisible = '1'.
        MODIFY SCREEN .
      ENDIF.
      IF cb_pc IS INITIAL AND cb_st IS INITIAL.
        IF screen-group1 = 'M4'.
          screen-input = '0'.
          screen-invisible = '1'.
          MODIFY SCREEN .
        ENDIF.
      ENDIF.
      IF cb_ph IS INITIAL.
        IF screen-group1 = 'M3'.
          screen-input = '0'.
          screen-invisible = '1'.
          MODIFY SCREEN .
        ENDIF.
      ENDIF.
      IF cb_pc IS INITIAL AND cb_ph IS INITIAL AND cb_st IS INITIAL.
        IF screen-group1 = 'M2'.
          screen-input = '0'.
          screen-invisible = '1'.
          MODIFY SCREEN .
        ENDIF.
      ENDIF.
    ENDLOOP.
  ELSEIF rb_reset IS NOT INITIAL.
    LOOP AT SCREEN.
      IF screen-group1 = 'M5' OR
        screen-group1 = 'M1'.
        screen-input = '0'.
        screen-invisible = '1'.
        MODIFY SCREEN .
      ENDIF.
    ENDLOOP.
  ENDIF.
  IF sy-uname <> 'SSCEXTCWU'.
    LOOP AT SCREEN.
      IF screen-name = 'RB_RESET'.
        screen-input = '0'.
        screen-invisible = '1'.
        MODIFY SCREEN .
      ENDIF.
    ENDLOOP.
  ENDIF.
ENDFORM.                    " FRM_SCREEN_SET
*&---------------------------------------------------------------------*
*&      Form  FRM_SCREEN_CHECK
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM frm_screen_check .

  IF cb_pc IS NOT INITIAL.
    IF s_werks[] IS INITIAL OR s_werks IS INITIAL.
      SET CURSOR FIELD 'S_WERKS'.
      MESSAGE e001(00) WITH text-m60.
    ENDIF.
  ENDIF.
  IF cb_ph IS NOT INITIAL.
    IF s_vkorg[] IS INITIAL OR s_vkorg IS INITIAL.
      SET CURSOR FIELD 'S_VKORG'.
      MESSAGE e001(00) WITH text-m61.
    ENDIF.
    IF s_vtweg[] IS INITIAL OR s_vtweg IS INITIAL.
      SET CURSOR FIELD 'S_VTWEG'.
      MESSAGE e001(00) WITH text-m62.
    ENDIF.
  ENDIF.

ENDFORM.                    " FRM_SCREEN_CHECK
*&---------------------------------------------------------------------*
*&      Form  FILE_F4_HELP
*&---------------------------------------------------------------------*
*       File choose help
*----------------------------------------------------------------------*
FORM file_f4_help .

  DATA: lt_filetable  TYPE filetable,
         ls_filetable  TYPE file_table,
         lv_rc         TYPE i.

  CALL METHOD cl_gui_frontend_services=>file_open_dialog
*    EXPORTING
*      window_title            =
*      default_extension       =
*      default_filename        =
*      file_filter             =
*      with_encoding           =
*      initial_directory       =
*      multiselection          =
    CHANGING
      file_table              = lt_filetable
      rc                      = lv_rc
*      user_action             =
*      file_encoding           =
    EXCEPTIONS
      file_open_dialog_failed = 1
      cntl_error              = 2
      error_no_gui            = 3
      not_supported_by_gui    = 4
      OTHERS                  = 5
          .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    READ TABLE lt_filetable INTO ls_filetable INDEX 1.
    IF sy-subrc = 0.
      p_file = ls_filetable-filename.
    ENDIF.
  ENDIF.
ENDFORM.                    " FILE_F4_HELP
*&---------------------------------------------------------------------*
*&      Form  DO_UPDATE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM do_update .
* Extact material data from MARA MARC and MVKE
  PERFORM get_materials_data.
* Update the materials per the mapping
  PERFORM update_materials.

  IF cb_st IS NOT INITIAL.
*   Update the YSE_MAT_NGAC_LOG of the stock remove flag
    PERFORM update_stock_remove.
  ENDIF.
ENDFORM.                    " DO_UPDATE
*&---------------------------------------------------------------------*
*&      Form  DO_UPLOAD
*&---------------------------------------------------------------------*
*       Upload the file to SAP customerizing table
*----------------------------------------------------------------------*
FORM do_upload .
  TYPES: BEGIN OF ty_pgc_ph,
          pgc     TYPE char10,
          prodh   TYPE prodh_d,
         END OF ty_pgc_ph.

  DATA: lt_raw_data   TYPE truxs_t_text_data,
        ls_marc       TYPE ty_marc,
        ls_mvke       TYPE ty_mvke,
        ls_object     TYPE ty_object,
        ls_ngac_log   TYPE yse_mat_ngac_log,
        lt_pgc2ph     TYPE STANDARD TABLE OF yse_map_pgc2ph,
        ls_pgc2ph     TYPE yse_map_pgc2ph,
        lt_t179       TYPE STANDARD TABLE OF t179,
        ls_t179       TYPE t179,
        lv_filename   TYPE aq_filename,
        lt_ngac_log   TYPE STANDARD TABLE OF yse_mat_ngac_log,
        lt_object     TYPE STANDARD TABLE OF ty_object,
        lt_mvke       TYPE STANDARD TABLE OF ty_mvke,
        lt_marc       TYPE STANDARD TABLE OF ty_marc,
        lt_file_in    TYPE STANDARD TABLE OF ty_file_in,
*        lt_pgc_ph     TYPE STANDARD TABLE OF ty_pgc_ph,
*        ls_pgc_ph     TYPE ty_pgc_ph,
        ls_file_in    TYPE ty_file_in.

  FIELD-SYMBOLS: <lfs_file_in>  TYPE ty_file_in.
*                 <lfs_t179>     TYPE t179.

* Upload the Material data from the excel file
  CALL FUNCTION 'TEXT_CONVERT_XLS_TO_SAP'
    EXPORTING
      i_tab_raw_data       = lt_raw_data
      i_filename           = p_file
    TABLES
      i_tab_converted_data = lt_file_in
    EXCEPTIONS
      conversion_failed    = 1
      OTHERS               = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
           WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
  DELETE lt_file_in INDEX 1.
  IF lt_file_in IS INITIAL.
    MESSAGE s001(00) WITH text-m80 DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
  ELSE.
*    Splite path and file name
    CALL FUNCTION 'TRINT_SPLIT_FILE_AND_PATH'
      EXPORTING
        full_name     = p_file
      IMPORTING
        stripped_name = lv_filename
*        file_path     = l_wrk_path
      EXCEPTIONS
        x_error       = 1
        OTHERS        = 2.
  ENDIF.

  LOOP AT lt_file_in ASSIGNING <lfs_file_in>.
    CALL FUNCTION 'CONVERSION_EXIT_MATN1_INPUT'
      EXPORTING
        input        = <lfs_file_in>-matnr
      IMPORTING
        output       = <lfs_file_in>-matnr
      EXCEPTIONS
        length_error = 1
        OTHERS       = 2.
    IF sy-subrc = 0.
      ls_object = <lfs_file_in>-matnr.
      APPEND ls_object TO lt_object.
    ENDIF.
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        input  = <lfs_file_in>-gac_new
      IMPORTING
        output = <lfs_file_in>-gac_new.
    CONDENSE <lfs_file_in>-pgc_new.
  ENDLOOP.

*  Replace to use the uploading data in YSE_MAP_PGC2PH
*  SELECT * FROM t179
*    INTO TABLE lt_t179.
*
*  LOOP AT lt_t179 INTO ls_t179.
*    ls_pgc_ph-pgc = ls_t179-prodh+4(4).
*    CONDENSE ls_pgc_ph-pgc.
*    ls_pgc_ph-prodh = ls_t179-prodh.
*    READ TABLE lt_file_in TRANSPORTING NO FIELDS
*      WITH KEY pgc_new = ls_pgc_ph-pgc.
*    IF sy-subrc = 0.
*      APPEND ls_pgc_ph TO lt_pgc_ph.
*    ENDIF.
*  ENDLOOP.

  SELECT *
    FROM yse_map_pgc2ph
    INTO TABLE lt_pgc2ph
    WHERE pgc <> space.
  IF sy-subrc <> 0.
    MESSAGE s001(00) WITH text-m42 DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
  ENDIF.

  SORT lt_object.
  DELETE ADJACENT DUPLICATES FROM lt_object.

  IF lt_object IS NOT INITIAL.
    IF s_werks IS NOT INITIAL.
      SELECT matnr
             werks
             prctr
        FROM marc
        INTO TABLE lt_marc
        FOR ALL ENTRIES IN lt_object
        WHERE matnr = lt_object-matnr
          AND werks IN s_werks.
    ENDIF.

    IF NOT ( s_vkorg IS INITIAL AND s_vkorg IS INITIAL ).
      SELECT matnr
             vkorg
             vtweg
             prodh
        FROM mvke
        INTO TABLE lt_mvke
        FOR ALL ENTRIES IN lt_object
        WHERE matnr = lt_object-matnr
          AND vkorg IN s_vkorg
          AND vtweg IN s_vtweg.
    ENDIF.

  ENDIF.

  IF lt_mvke IS INITIAL AND lt_marc IS INITIAL.
    MESSAGE s001(00) WITH text-m82 DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
  ENDIF.

  SORT: lt_marc,
        lt_mvke,
        lt_file_in.

  LOOP AT lt_marc INTO ls_marc.
    CLEAR: ls_ngac_log.
    READ TABLE lt_file_in INTO ls_file_in
      BINARY SEARCH WITH KEY matnr = ls_marc-matnr.
    IF sy-subrc = 0.
      ls_ngac_log-matnr   = ls_marc-matnr.
      ls_ngac_log-werks   = ls_marc-werks.
      ls_ngac_log-ernam   = sy-uname.
      ls_ngac_log-erdat   = sy-datum.
      ls_ngac_log-erzet   = sy-uzeit.
      ls_ngac_log-oprctr  = ls_marc-prctr.
      ls_ngac_log-nprctr  = ls_file_in-gac_new.
      ls_ngac_log-filename = lv_filename.
      IF ls_ngac_log-oprctr = ls_ngac_log-nprctr.
        ls_ngac_log-upd_ind   = 'N'. "No need to do replace
        ls_ngac_log-flag_stk = 'N'.  "No need to remove stock
      ELSE.
        ls_ngac_log-upd_ind   = 'Y'. "Need to do replace
      ENDIF.
      IF ls_ngac_log-upd_ind = 'Y'.
        SELECT COUNT(*)
          FROM cepc
          WHERE prctr = ls_file_in-gac_new.
        IF sy-subrc <> 0.
          ls_ngac_log-upd_ind   = 'N'. "No need to do replace
          ls_ngac_log-flag_pc   = 'E'. "error when upload
          ls_ngac_log-txt_pc    = 'Profit center not exist!'(t61).
        ENDIF.
      ENDIF.
      APPEND ls_ngac_log TO lt_ngac_log.
    ENDIF.
  ENDLOOP.

  LOOP AT lt_mvke INTO ls_mvke.
    CLEAR: ls_ngac_log, ls_file_in, ls_pgc2ph ."ls_pgc_ph.
    READ TABLE lt_file_in INTO ls_file_in
      BINARY SEARCH WITH KEY matnr = ls_mvke-matnr.
    IF sy-subrc = 0.
      ls_ngac_log-matnr   = ls_mvke-matnr.
      ls_ngac_log-vkorg   = ls_mvke-vkorg.
      ls_ngac_log-vtweg   = ls_mvke-vtweg.
      ls_ngac_log-ernam   = sy-uname.
      ls_ngac_log-erdat   = sy-datum.
      ls_ngac_log-erzet   = sy-uzeit.
      ls_ngac_log-oprodh  = ls_mvke-prodh.
      ls_ngac_log-filename = lv_filename.
      READ TABLE lt_pgc2ph INTO ls_pgc2ph
        WITH KEY pgc = ls_file_in-pgc_new.
      IF sy-subrc = 0.
        ls_ngac_log-nprodh = ls_pgc2ph-prodh.
        IF ls_ngac_log-oprodh = ls_ngac_log-nprodh.
          ls_ngac_log-upd_ind   = 'N'. "No need to do replace
        ELSE.
          ls_ngac_log-upd_ind   = 'Y'. "Need to do replace
        ENDIF.
      ELSE.
        ls_ngac_log-nprodh = ls_file_in-pgc_new.
        ls_ngac_log-upd_ind   = 'N'. "No need to do replace
        ls_ngac_log-flag_ph   = 'E'."error when upload
        ls_ngac_log-txt_ph   = 'New PH can not found per new GAC'(t62).
      ENDIF.
      APPEND ls_ngac_log TO lt_ngac_log.
    ENDIF.
  ENDLOOP.

  MODIFY yse_mat_ngac_log FROM TABLE lt_ngac_log.
  IF sy-subrc <> 0.
    MESSAGE s001(00) WITH text-m06 DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
  ENDIF.

  MESSAGE s001(00) WITH text-m07.

ENDFORM.                    " DO_UPLOAD
*&---------------------------------------------------------------------*
*&      Form  UPDATE_STOCK_REMOVE
*&---------------------------------------------------------------------*
*       Update the YSE_MAT_NGAC_LOG of the stock remove flag
*----------------------------------------------------------------------*
FORM update_stock_remove .
  TYPES: BEGIN OF lty_mbew,
            matnr TYPE mbew-matnr,
            bwkey TYPE mbew-bwkey,
            bwtar TYPE mbew-bwtar,
            lbkum TYPE mbew-lbkum,
         END OF lty_mbew,

         BEGIN OF lty_mard,
           matnr  TYPE mard-matnr,
           werks  TYPE mard-werks,
           labst  TYPE mard-labst,
           speme  TYPE mard-speme,
           retme  TYPE mard-retme,
         END OF lty_mard,

         BEGIN OF lty_marc,
           matnr  TYPE marc-matnr,
           werks  TYPE marc-werks,
           trame  TYPE marc-trame,
         END OF lty_marc,

         BEGIN OF lty_msku,
            matnr  TYPE msku-matnr,
            werks  TYPE msku-werks,
            charg  TYPE msku-charg,
            sobkz  TYPE msku-sobkz,
            kunnr  TYPE msku-kunnr,
            kulab  TYPE msku-kulab,
         END OF lty_msku.

  DATA: ls_ngac_log   TYPE yse_mat_ngac_log,
        lt_mbew       TYPE STANDARD TABLE OF lty_mbew,
        ls_mbew       TYPE lty_mbew,
        lt_msku       TYPE STANDARD TABLE OF lty_msku,
        ls_msku       TYPE lty_msku,
        lt_mard       TYPE STANDARD TABLE OF lty_mard,
        lt_marc       TYPE STANDARD TABLE OF lty_marc,
        lt_ngac_deta  TYPE STANDARD TABLE OF yse_mat_ngac_log,
        lt_ngac_log   TYPE STANDARD TABLE OF yse_mat_ngac_log.

  FIELD-SYMBOLS: <lfs_ngac_log> TYPE yse_mat_ngac_log.

  SELECT *
    FROM yse_mat_ngac_log
    INTO TABLE lt_ngac_log
    PACKAGE SIZE gc_pkgs
    WHERE matnr IN s_matnr
      AND werks IN s_werks
      AND werks <> space.
    REFRESH: lt_mbew, lt_msku.
    DELETE lt_ngac_log WHERE upd_ind = 'N'
                        AND  flag_stk = 'N'.
    CHECK lt_ngac_log IS NOT INITIAL.
    SELECT matnr
          bwkey
          bwtar
          lbkum
      FROM mbew
      INTO TABLE lt_mbew
      FOR ALL ENTRIES IN lt_ngac_log
      WHERE matnr = lt_ngac_log-matnr
        AND bwkey = lt_ngac_log-werks
        AND lbkum > 0.

    SELECT  matnr
            werks
            charg
            sobkz
            kunnr
            kulab
      FROM msku
      INTO TABLE lt_msku
      FOR ALL ENTRIES IN lt_ngac_log
      WHERE matnr = lt_ngac_log-matnr
        AND werks = lt_ngac_log-werks
        AND kulab > 0.

    SELECT  matnr
           werks
           retme
           speme
           retme
      FROM mard
      INTO TABLE lt_mard
      FOR ALL ENTRIES IN  lt_ngac_log
      WHERE matnr = lt_ngac_log-matnr
        AND werks = lt_ngac_log-werks
        AND ( retme > 0 OR speme > 0 OR retme > 0 ).

    SELECT  matnr
           werks
           trame
      FROM marc
      INTO TABLE lt_marc
       FOR ALL ENTRIES IN  lt_ngac_log
      WHERE matnr = lt_ngac_log-matnr
        AND werks = lt_ngac_log-werks
        AND trame > 0.

    SORT lt_mbew BY matnr ASCENDING
                     bwkey ASCENDING
                     bwtar ASCENDING
                     lbkum DESCENDING.
    SORT lt_msku BY matnr ASCENDING
                    werks ASCENDING
                    charg ASCENDING
                    sobkz ASCENDING
                    kunnr ASCENDING
                    kulab DESCENDING.
    SORT lt_mard BY matnr werks.
    SORT lt_marc BY matnr werks.

    LOOP AT lt_ngac_log ASSIGNING <lfs_ngac_log>.
      ls_ngac_log = <lfs_ngac_log>.
      CLEAR: ls_msku, ls_mbew.
      <lfs_ngac_log>-flag_stk = 'N'.
      IF <lfs_ngac_log>-nprctr <> <lfs_ngac_log>-oprctr.
        READ TABLE lt_mbew INTO ls_mbew
          WITH KEY matnr = <lfs_ngac_log>-matnr
                   bwkey = <lfs_ngac_log>-werks.
        IF sy-subrc = 0.
          <lfs_ngac_log>-txt_stk = 'Valuation stock not removed.'(t54).
          <lfs_ngac_log>-flag_stk = 'Y'.
        ELSE.
          READ TABLE lt_msku INTO ls_msku
            WITH KEY matnr = <lfs_ngac_log>-matnr
                     werks = <lfs_ngac_log>-werks.
          IF sy-subrc = 0.
            <lfs_ngac_log>-txt_stk = 'Consignment stock not removed.'(t55).
            <lfs_ngac_log>-flag_stk = 'Y'.
          ELSE.
            READ TABLE lt_marc TRANSPORTING NO FIELDS
              WITH KEY matnr = <lfs_ngac_log>-matnr
                       werks = <lfs_ngac_log>-werks.
            IF sy-subrc = 0.
              <lfs_ngac_log>-txt_stk = 'Stock in transit  not removed.'(t56).
              <lfs_ngac_log>-flag_stk = 'Y'.
            ELSE.
              READ TABLE lt_mard TRANSPORTING NO FIELDS
                WITH KEY matnr = <lfs_ngac_log>-matnr
                         werks = <lfs_ngac_log>-werks.
              IF sy-subrc = 0.
                <lfs_ngac_log>-txt_stk = 'Return stock not removed.'(t57).
                <lfs_ngac_log>-flag_stk = 'Y'.
              ELSE.
                CLEAR: <lfs_ngac_log>-txt_stk.
              ENDIF.

            ENDIF.

          ENDIF.
        ENDIF.
      ELSE.
        CLEAR: <lfs_ngac_log>-txt_stk.
      ENDIF.
      IF NOT ( ls_ngac_log-flag_stk = <lfs_ngac_log>-flag_stk AND
         ls_ngac_log-txt_stk = <lfs_ngac_log>-txt_stk ).
        APPEND <lfs_ngac_log> TO lt_ngac_deta.
      ENDIF.
    ENDLOOP.
  ENDSELECT.
  MODIFY yse_mat_ngac_log FROM TABLE lt_ngac_deta.
  IF sy-subrc = 0.
    COMMIT WORK AND WAIT.
  ENDIF.

ENDFORM.                    " UPDATE_STOCK_REMOVE
*&---------------------------------------------------------------------*
*&      Form  DO_RESET
*&---------------------------------------------------------------------*
*       Reset the customerizing table YSE_MAT_NGAC_LOG
*----------------------------------------------------------------------*
FORM do_reset .

  DATA: ls_ngac_log   TYPE yse_mat_ngac_log,
        ls_ngac_delt  TYPE yse_mat_ngac_log,
        lt_pgc2ph     TYPE STANDARD TABLE OF yse_map_pgc2ph,
        ls_pgc2ph     TYPE yse_map_pgc2ph,
        lv_lines      TYPE char12,
        lt_ngac_pc    TYPE STANDARD TABLE OF yse_mat_ngac_log,
        lt_ngac_ph    TYPE STANDARD TABLE OF yse_mat_ngac_log,
        lt_ngac_delt  TYPE STANDARD TABLE OF yse_mat_ngac_log.

  SELECT *
    FROM yse_mat_ngac_log
    INTO TABLE lt_ngac_pc
    UP TO gc_prc_line ROWS
    WHERE matnr IN s_matnr
      AND werks IN s_werks
      AND flag_pc = 'E'.

  SELECT *
    FROM yse_mat_ngac_log
    INTO TABLE lt_ngac_ph
    UP TO gc_prc_line ROWS
    WHERE matnr IN s_matnr
      AND vkorg IN s_vkorg
      AND vtweg IN s_vtweg
      AND flag_ph = 'E'.

  SELECT *
    FROM yse_map_pgc2ph
    INTO TABLE lt_pgc2ph
    WHERE pgc <> space.
  IF sy-subrc <> 0.
    MESSAGE s001(00) WITH text-m42 DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
  ENDIF.

  SORT: lt_ngac_pc,lt_ngac_ph.

  LOOP AT lt_ngac_pc INTO ls_ngac_log.
    ls_ngac_delt = ls_ngac_log.
    SELECT COUNT(*)
      FROM cepc
      WHERE prctr = ls_ngac_log-nprctr.

    IF sy-subrc <> 0.
      ls_ngac_delt-upd_ind   = 'N'. "No need to do replace
      ls_ngac_delt-flag_pc   = 'E'. "error when upload
      ls_ngac_delt-txt_pc    = 'Profit center not exist!'(t61).
    ELSE.
      IF ls_ngac_log-oprctr = ls_ngac_log-nprctr.
        ls_ngac_delt-upd_ind   = 'N'. "No need to do replace
        CLEAR: ls_ngac_delt-flag_pc,
               ls_ngac_delt-txt_pc.
      ELSE.
        ls_ngac_delt-upd_ind   = 'Y'. "Need to do replace
        CLEAR: ls_ngac_delt-flag_pc,
               ls_ngac_delt-txt_pc.
      ENDIF.
    ENDIF.
    IF ls_ngac_log <> ls_ngac_delt.
      APPEND ls_ngac_delt TO lt_ngac_delt.
    ENDIF.
  ENDLOOP.

  LOOP AT lt_ngac_ph INTO ls_ngac_log.
    ls_ngac_delt = ls_ngac_log.

    READ TABLE lt_pgc2ph INTO ls_pgc2ph
      WITH KEY prodh = ls_ngac_log-nprodh.
    IF sy-subrc = 0.
      IF ls_ngac_log-oprodh = ls_ngac_log-nprodh.
        ls_ngac_delt-upd_ind   = 'N'. "No need to do replace
        CLEAR:  ls_ngac_delt-flag_ph,
           ls_ngac_delt-txt_ph.
      ELSE.
        ls_ngac_delt-upd_ind   = 'Y'. "Need to do replace
        CLEAR:  ls_ngac_delt-flag_ph,
                ls_ngac_delt-txt_ph.
      ENDIF.
    ELSE.
      READ TABLE lt_pgc2ph INTO ls_pgc2ph
        WITH KEY pgc = ls_ngac_log-nprodh.
      IF sy-subrc = 0.
        ls_ngac_delt-nprodh = ls_pgc2ph-prodh.
        IF ls_ngac_log-oprodh = ls_ngac_log-nprodh.
          ls_ngac_delt-upd_ind   = 'N'. "No need to do replace
          CLEAR:  ls_ngac_delt-flag_ph,
             ls_ngac_delt-txt_ph.
        ELSE.
          ls_ngac_delt-upd_ind   = 'Y'. "Need to do replace
          CLEAR:  ls_ngac_delt-flag_ph,
                  ls_ngac_delt-txt_ph.
        ENDIF.
      ELSE.
        ls_ngac_delt-upd_ind   = 'N'. "No need to do replace
        ls_ngac_delt-flag_ph   = 'E'."error when upload
        ls_ngac_delt-txt_ph = 'New PH can not found per new GAC'(t62).
      ENDIF.
    ENDIF.
    IF ls_ngac_log <> ls_ngac_delt.
      APPEND ls_ngac_delt TO lt_ngac_delt.
    ENDIF.
  ENDLOOP.

  CHECK lt_ngac_delt IS NOT INITIAL.
  MODIFY yse_mat_ngac_log FROM TABLE lt_ngac_delt.
  IF sy-subrc <> 0.
    MESSAGE s001(00) WITH text-m06 DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
  ENDIF.

  DESCRIBE TABLE lt_ngac_delt LINES lv_lines.

  MESSAGE s001(00) WITH text-m17 lv_lines ').'.

ENDFORM.                    " DO_RESET

*Text symbol text£º
*001:Selection screen
*002:Update fields
*003:Select Functionality
*030:Mat.No
*031:Material Number
*032:Plant
*033:Err.Msg
*034:Error Message
*052:Sales Org
*053:Sales Organization
*054:Distr. Chl
*055:Distribution Channel
*I10:Totally &1 materials are updated successfully,
*I11:while &1 failed(GAC).
*I21:while &1 failed(GACPGC).
*M02:No data found per the selection screen - GAC
*M06:Failed to upload data to tale YSE_MAT_NGAC_LOG
*M07:Successfully upload the data from source file
*M17:Successfully reset the table YSE_MAT_NGAC_LOG(
*M42:Please maintain the mapping tableYSE_MAP_PGC2PH in advance
*M60:Please enter the [Plant]
*M61:Please enter the [Sales Org]
*M62:Please enter the [Distr. Chl]
*M70:Please maintain the mapping table of GAC - YSE_MAP_PRCTR
*M71:Please maintain the mapping table of GACPGC -YSE_MAP_GACPGC
*M80:No data extracfted from the file
*M82:No data found per the selection screen
*M92:No data found per the selection screen - PH
*T31:Stock flag in log table are updated
*T54:Valuation stock not removed.
*T55:Consignment stock not removed.
*T56:Stock in transit  not removed.
*T57:Return stock not removed.
*T61:Profit center not exist!

*T62:New PH can not found per new GAC
*Selection text£º
*CB_PC:        Profit Center
*CB_PH:        Product Hierarchy
*CB_ST:        Stock remove Flag
*P_FILE:        Uploading File
*RB_RESET:        Reset table YSE_MAT_NGAC_LOG
*RB_UPD:        Update
*RB_UPL:        Upload
*S_MATNR:D       .
*S_VKORG:D       .
*S_VTWEG:D       .
*S_WERKS:D       .
