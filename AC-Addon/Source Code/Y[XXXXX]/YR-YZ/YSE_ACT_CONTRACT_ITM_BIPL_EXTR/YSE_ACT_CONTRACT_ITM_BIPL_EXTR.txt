*&---------------------------------------------------------------------*
*& Report  YSE_ACT_CONTRACT_ITM_BIPL_EXTR
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& ACT! : Contract Item Billing Plans extraction                       *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 16.10.2012
*  Change Request Number :
*  Transport request Nr. : CD1K973505
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NR.|   DATE     | NAME            | CORRECTION NR.| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                 | CD1K9.....    |             *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_act_contract_itm_bipl_extr.

TABLES: vbak,
        vbap,
        veda,
        vbup,
        vbkd,
        fpla,
        fplt.

* Types
TYPES: BEGIN OF ty_vbap,
         vbeln      TYPE vbeln_va,
         posnr      TYPE posnr_va,
         fplnr      TYPE fplnr,
         vkuegru    TYPE vkgru_veda,
         veindat    TYPE vedat_veda,
      END OF ty_vbap.

TYPES: BEGIN OF ty_fplt,
         fplnr      TYPE fplnr,
         fkdat      TYPE bfdat,
         nfdat      TYPE nfdat,
         afdat      TYPE fkdat,
         fakwr      TYPE fakwr,
         waers      TYPE waers,
         fksaf      TYPE fksaf,
         autte      TYPE autte,
      END OF ty_fplt.

* Internal tables
DATA: gt_vbap  TYPE STANDARD TABLE OF ty_vbap
                    WITH HEADER LINE.

DATA: gt_fplt  TYPE SORTED TABLE OF ty_fplt
                    WITH NON-UNIQUE KEY fplnr
                    WITH HEADER LINE.

DATA: BEGIN OF gt_out  OCCURS 0,
        vbeln       TYPE vbeln_va,
        tab01(1)    TYPE c,
        posnr       TYPE posnr_va,
        tab02(1)    TYPE c,
        fkdat       TYPE bfdat,
        tab03(1)    TYPE c,
        nfdat       TYPE nfdat,
        tab04(1)    TYPE c,
        afdat       TYPE fkdat,
        tab05(1)    TYPE c,
        fakwr       TYPE char21,
        tab06(1)    TYPE c,
        waers       TYPE waers,
        tab07(1)    TYPE c,
        fksaf       TYPE fksaf,
        tab08(1)    TYPE c,
        autte       TYPE autte,
        tab09(1)    TYPE c,
      END OF gt_out.

DATA: BEGIN OF gt_outh  OCCURS 0,
        vbeln       TYPE fieldname,
        tab01(1)    TYPE c,
        posnr       TYPE fieldname,
        tab02(1)    TYPE c,
        fkdat       TYPE fieldname,
        tab03(1)    TYPE c,
        nfdat       TYPE fieldname,
        tab04(1)    TYPE c,
        afdat       TYPE fieldname,
        tab05(1)    TYPE c,
        fakwr       TYPE fieldname,
        tab06(1)    TYPE c,
        waers       TYPE fieldname,
        tab07(1)    TYPE c,
        fksaf       TYPE fieldname,
        tab08(1)    TYPE c,
        autte       TYPE fieldname,
        tab09(1)    TYPE c,
      END OF gt_outh.

DATA: gv_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      gv_ofile         LIKE /sapdmc/lsoinp-filename,
      gv_logsys        LIKE tbdlst-logsys,
      gv_ersda(14)     TYPE c,                      "YYYYMMDDHHMMSS
      gv_fkdat_s       TYPE bfdat,
      gv_error         TYPE xfeld.

*CONSTANTS: gc_stat_dl  TYPE j_status  VALUE 'I0076'.

SELECTION-SCREEN  BEGIN OF BLOCK input  WITH FRAME  TITLE text-s01.
*PARAMETERS: p_vkorg  LIKE vbak-vkorg  OBLIGATORY  MEMORY ID vko.
PARAMETERS: p_bukrs  TYPE aufk-bukrs OBLIGATORY MEMORY ID buk,
            p_vkorg  LIKE vbak-vkorg MEMORY ID vko.
SELECTION-SCREEN SKIP.
SELECT-OPTIONS: s_vkbur  FOR vbak-vkbur,
                s_vkgrp  FOR vbak-vkgrp,
                s_vbdat  FOR veda-vbegdat,
                s_vndat  FOR veda-venddat,
                s_auart  FOR vbak-auart,
                s_vbeln  FOR vbak-vbeln,
                s_abgru  FOR vbap-abgru,
                s_gbsta  FOR vbup-gbsta,
                s_fksaa  FOR vbup-fksaa.
SELECTION-SCREEN SKIP.
PARAMETERS: p_canc_r   AS CHECKBOX,
            p_canc_d   AS CHECKBOX.
SELECTION-SCREEN  END OF BLOCK input.


************************************************************************
*       I N I T I A L I Z A T I O N    E V E N T                       *
************************************************************************
INITIALIZATION.

* Timestamp
  CONCATENATE sy-datum sy-uzeit INTO gv_ersda.

* Logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = gv_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

* Select data
  PERFORM select_data.

* Check anything selected
  IF gt_vbap[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No contracts selected'(e01).
    RETURN.
  ENDIF.

* Build output
  PERFORM build_output.

* Save output.
  PERFORM save_output.

  IF gv_error IS INITIAL.
    WRITE: /01 'Program ended succesfully'(i01).
  ENDIF.


************************************************************************
*       S U B R O U T I N E S                                          *
************************************************************************

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

  DATA: lv_where TYPE string.
  lv_where = 'tvko~bukrs EQ p_bukrs'.
  IF p_vkorg IS NOT INITIAL.
    CONCATENATE lv_where 'AND h~vkorg = p_vkorg' INTO lv_where SEPARATED BY space.
  ENDIF.
  CONCATENATE lv_where 'AND h~vkbur IN s_vkbur'
                       'AND h~vkgrp IN s_vkgrp'
                       'AND c~vbegdat IN s_vbdat'
                       'AND c~venddat IN s_vndat'
                       'AND h~auart IN s_auart'
                       'AND h~vbeln IN s_vbeln'
                       'AND i~abgru IN s_abgru'
                       'AND s~gbsta IN s_gbsta'
                       'AND s~fksaa IN s_fksaa'
                       'AND h~vbtyp =  ''G'''
                       'AND c~vposn = 0'
    INTO lv_where SEPARATED BY space.

* Contract items
*  SELECT i~vbeln i~posnr b~fplnr c~vkuegru c~veindat
*         INTO TABLE gt_vbap
*         FROM vbap AS i
*         INNER JOIN vbak AS h
*                    ON h~vbeln = i~vbeln
*         INNER JOIN veda AS c
*                    ON c~vbeln = i~vbeln
*         INNER JOIN vbup AS s
*                    ON s~vbeln = i~vbeln  AND
*                       s~posnr = i~posnr
*         INNER JOIN vbkd AS b
*                    ON b~vbeln = i~vbeln  AND
*                       b~posnr = i~posnr
*         WHERE h~vkorg   =  p_vkorg
*           AND h~vkbur   IN s_vkbur
*           AND h~vkgrp   IN s_vkgrp
*           AND c~vbegdat IN s_vbdat
*           AND c~venddat IN s_vndat
*           AND h~auart   IN s_auart
*           AND h~vbeln   IN s_vbeln
*           AND i~abgru   IN s_abgru
*           AND s~gbsta   IN s_gbsta
*           AND s~fksaa   IN s_fksaa
*           AND h~vbtyp   =  'G'
*           AND c~vposn   = 0.
  SELECT i~vbeln i~posnr b~fplnr c~vkuegru c~veindat
         INTO TABLE gt_vbap
         FROM vbap AS i
         INNER JOIN vbak AS h
                    ON h~vbeln = i~vbeln
         INNER JOIN veda AS c
                    ON c~vbeln = i~vbeln
         INNER JOIN vbup AS s
                    ON s~vbeln = i~vbeln  AND
                       s~posnr = i~posnr
         INNER JOIN vbkd AS b
                    ON b~vbeln = i~vbeln  AND
                       b~posnr = i~posnr
         INNER JOIN tvko AS tvko
           ON tvko~vkorg EQ h~vkorg
         WHERE (lv_where).
  CHECK NOT gt_vbap[] IS INITIAL.

* Check cancellation
  IF NOT p_canc_r IS INITIAL.
    DELETE gt_vbap WHERE NOT vkuegru IS INITIAL.
  ENDIF.
  IF NOT p_canc_d IS INITIAL.
    DELETE gt_vbap WHERE NOT veindat IS INITIAL.
  ENDIF.

  SORT gt_vbap BY vbeln posnr.

* Item billing plan data
  SELECT bl~fplnr bl~fkdat bl~nfdat bl~afdat
         bl~fakwr bl~waers bl~fksaf bh~autte
         INTO TABLE gt_fplt
         FROM fplt AS bl
         INNER JOIN fpla AS bh
                    ON bh~fplnr = bl~fplnr
         FOR ALL ENTRIES IN gt_vbap
         WHERE bl~fplnr  = gt_vbap-fplnr.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  BUILD_OUTPUT
*&---------------------------------------------------------------------*
*       Build table fot output file
*----------------------------------------------------------------------*
FORM build_output .

  LOOP AT gt_vbap.

*   Billing plan
    LOOP AT gt_fplt WHERE fplnr = gt_vbap-fplnr.

      CLEAR: gt_out.
      MOVE '|' TO: gt_out-tab01, gt_out-tab02, gt_out-tab03,
                   gt_out-tab04, gt_out-tab05, gt_out-tab06,
                   gt_out-tab07, gt_out-tab08, gt_out-tab09.

      gt_out-vbeln = gt_vbap-vbeln.
      gt_out-posnr = gt_vbap-posnr.

      MOVE-CORRESPONDING gt_fplt TO gt_out.

*     Check from/to dates
      IF gt_out-fkdat > gt_out-nfdat.
        gv_fkdat_s   = gt_out-fkdat.
        gt_out-fkdat = gt_out-nfdat.
        gt_out-nfdat = gv_fkdat_s.
      ENDIF.

*     Packed fields
      WRITE gt_fplt-fakwr TO gt_out-fakwr CURRENCY gt_fplt-waers.

      APPEND gt_out.

    ENDLOOP.

  ENDLOOP.

ENDFORM.                    " BUILD_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  SAVE_OUTPUT
*&---------------------------------------------------------------------*
*       Save output file
*----------------------------------------------------------------------*
FORM save_output .

* Open file
*  CONCATENATE 'CONTRACT_ITM_BIPL' p_vkorg gv_ersda
  CONCATENATE 'CONTRACT_ITM_BIPL' p_bukrs
             INTO gv_ofile SEPARATED BY '_'.
  IF p_vkorg IS NOT INITIAL.
    CONCATENATE gv_ofile p_vkorg
               INTO gv_ofile SEPARATED BY '_'.
  ENDIF.
  CONCATENATE gv_ofile gv_ersda
             INTO gv_ofile SEPARATED BY '_'.

  REPLACE 'xxx' IN gv_directory WITH gv_logsys(3).
  CONCATENATE gv_directory gv_ofile INTO gv_ofile.

  OPEN DATASET gv_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-e02, gv_ofile.
    gv_error = 'X'.
    EXIT.
  ENDIF.

* Fill header
  PERFORM fill_header.
  LOOP AT gt_outh.
    TRANSFER gt_outh TO gv_ofile.
  ENDLOOP.

* Fill output
  LOOP AT gt_out.
    TRANSFER gt_out TO gv_ofile.
  ENDLOOP.

ENDFORM.                    " SAVE_OUTPUT

*&---------------------------------------------------------------------*
*&      Form  FILL_HEADER
*&---------------------------------------------------------------------*
*       Fill header
*----------------------------------------------------------------------*
FORM fill_header .

  CLEAR: gt_outh.
  MOVE '|' TO: gt_outh-tab01, gt_outh-tab02, gt_outh-tab03,
               gt_outh-tab04, gt_outh-tab05, gt_outh-tab06,
               gt_outh-tab07, gt_outh-tab08, gt_outh-tab09.

  gt_outh-vbeln  = 'VBELN'.
  gt_outh-posnr  = 'POSNR'.
  gt_outh-fkdat  = 'FKDAT'.
  gt_outh-nfdat  = 'NFDAT'.
  gt_outh-afdat  = 'AFDAT'.
  gt_outh-fakwr  = 'FAKWR'.
  gt_outh-waers  = 'WAERS'.
  gt_outh-fksaf  = 'FKSAF'.
  gt_outh-autte  = 'AUTTE'.
  APPEND gt_outh.

  gt_outh-vbeln  = 'Contract'.
  gt_outh-posnr  = 'Item'.
  gt_outh-fkdat  = 'Settlem. From'.
  gt_outh-nfdat  = 'Setlement To'.
  gt_outh-afdat  = 'Billing Date'.
  gt_outh-fakwr  = 'Billing Value'.
  gt_outh-waers  = 'Curr.'.
  gt_outh-fksaf  = 'Bill. Stat.'.
  gt_outh-autte  = 'Adv. Bill.'.
  APPEND gt_outh.

ENDFORM.                    " FILL_HEADER

*Text symbol text£º
*E01:No contracts selected
*I01:Program ended succesfully

*S01:Selection
*Selection text£º
*P_BUKRS:D       .
*P_CANC_D:        Check cancellation receipt
*P_CANC_R:        Check cancellation reason
*P_VKORG:D       .
*S_ABGRU:D       .
*S_AUART:D       .
*S_FKSAA:D       .
*S_GBSTA:D       .
*S_VBDAT:D       .
*S_VBELN:D       .
*S_VKBUR:D       .
*S_VKGRP:D       .
*S_VNDAT:D       .
