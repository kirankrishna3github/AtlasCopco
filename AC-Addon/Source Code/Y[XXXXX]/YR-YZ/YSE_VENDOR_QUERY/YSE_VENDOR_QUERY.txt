*----------------------------------------------------------------------*
* Author  : Yang Lei                                                   *
* Created : 15.05.2017                                                 *
* Dev.nbr:                                                     *
*----------------------------------------------------------------------*
*                                                                      *
************************************************************************
* Mod-xxx | xxxx.xx.xx | xxxx xxxx     | CD1Kxxxxxxxxxx  |   CRXXXX    *
************************************************************************
************************************************************************

REPORT  YSE_VENDOR_QUERY.

************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS SLIS.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES:
  lfa1,
  lfb1,
  lfm1.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:
  BEGIN OF TY_OUT,
    lifnr    TYPE lfa1-lifnr,
    bukrs    TYPE lfb1-bukrs,
    ekorg    TYPE lfm1-ekorg,
    ktokk    TYPE lfa1-ktokk,
    name1    TYPE lfa1-name1,
    name2    TYPE lfa1-name2,
    name3    TYPE lfa1-name3,
    name4    TYPE lfa1-name4,
    mcod1    TYPE lfa1-mcod1,
    stras    TYPE lfa1-stras,
    pstlz    TYPE lfa1-pstlz,
    ort01    TYPE lfa1-ort01,
    land1    TYPE lfa1-land1,
    regio    TYPE lfa1-regio,
    spras    TYPE lfa1-spras,
    telf1    TYPE lfa1-telf1,
    telf2    TYPE lfa1-telf2,
    teltx    TYPE lfa1-teltx,
    kunnr    TYPE lfa1-kunnr,
    vbund    TYPE lfa1-vbund,
    stcd1    TYPE lfa1-stcd1,
    stceg    TYPE lfa1-stceg,
    banks    TYPE lfbk-banks,
    bankl    TYPE lfbk-bankl,
    bankn    TYPE lfbk-bankn,
    bkref    TYPE lfbk-bkref,
    banka    TYPE bnka-banka,
    akont    TYPE lfb1-akont,
    zterm    TYPE lfb1-zterm,
    zwels    TYPE lfb1-zwels,
    reprf    TYPE lfb1-reprf,
    waers    TYPE lfm1-waers,
    zter2    TYPE lfm1-zterm,
    inco1    TYPE lfm1-inco1,
    inco2    TYPE lfm1-inco2,
    webre    TYPE lfm1-webre,
    kzaut    TYPE lfm1-kzaut,
    kzret    TYPE lfm1-kzret,
    bstae    TYPE lfm1-bstae,
  END OF TY_OUT,

  GTY_OUT   TYPE STANDARD TABLE OF TY_OUT.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  GT_OUT TYPE GTY_OUT,
  gt_lfa1     TYPE STANDARD TABLE OF lfa1,
  gt_lfb1     TYPE STANDARD TABLE OF lfb1,
  gt_lfm1     TYPE STANDARD TABLE OF lfm1,
  gt_lfbk     TYPE STANDARD TABLE OF lfbk,
  gt_bnka     TYPE STANDARD TABLE OF bnka,
  gt_fieldcat TYPE slis_t_fieldcat_alv.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  GS_OUT TYPE TY_OUT,
  gs_layout TYPE slis_layout_alv,
  gs_disvar TYPE disvariant.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_col(10) TYPE n,
  gv_save    TYPE c.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x TYPE c LENGTH 1 VALUE 'X'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK B1 WITH FRAME TITLE TEXT-T01 .
  SELECT-OPTIONS:
    s_lifnr FOR lfa1-lifnr,
    s_bukrs FOR lfb1-bukrs NO-EXTENSION NO INTERVALS OBLIGATORY,
    s_ekorg FOR lfm1-ekorg NO-EXTENSION NO INTERVALS OBLIGATORY.
SELECTION-SCREEN END OF BLOCK B1.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* Get data
  PERFORM GET_DATA.
* Display ALV
  PERFORM DISPLAY_DATA.

*&---------------------------------------------------------------------*
*&      Form  GET_DATA
*&---------------------------------------------------------------------*
*       Get data
*----------------------------------------------------------------------*
FORM GET_DATA .
  DATA:
    lt_lfbk  TYPE STANDARD TABLE OF lfbk,
    ls_lfa1  TYPE lfa1,
    ls_lfb1  TYPE lfb1,
    ls_lfm1  TYPE lfm1,
    ls_lfbk  TYPE lfbk,
    ls_bnka  TYPE bnka,
    lv_tabix TYPE sy-tabix.

  SELECT *
    FROM lfa1
    INTO TABLE gt_lfa1
   WHERE lifnr IN s_lifnr.
  IF sy-subrc <> 0.
    MESSAGE 'No target data!' TYPE 'S' DISPLAY LIKE 'E'.
    LEAVE LIST-PROCESSING.
  ENDIF.

  SELECT *
    FROM lfb1
    INTO TABLE gt_lfb1
   WHERE lifnr IN s_lifnr
     AND bukrs IN s_bukrs.
  SORT gt_lfb1 BY lifnr ASCENDING.

  SELECT *
    FROM lfm1
    INTO TABLE gt_lfm1
   WHERE lifnr IN s_lifnr
     AND ekorg IN s_ekorg.
  SORT gt_lfm1 BY lifnr ASCENDING.

  SELECT *
    FROM lfbk
    INTO TABLE gt_lfbk
   WHERE lifnr IN s_lifnr.
  SORT gt_lfbk BY lifnr ASCENDING.

  IF gt_lfbk IS NOT INITIAL.
    lt_lfbk = gt_lfbk.
    SORT lt_lfbk
      BY banks ASCENDING
         bankl ASCENDING.
    DELETE ADJACENT DUPLICATES FROM lt_lfbk COMPARING banks bankl.

    SELECT *
      FROM bnka
      INTO TABLE gt_bnka
       FOR ALL ENTRIES IN lt_lfbk
     WHERE banks = lt_lfbk-banks
       AND bankl = lt_lfbk-bankl.
    SORT gt_bnka
      BY banks ASCENDING
         bankl ASCENDING.
  ENDIF.

* Mapping
  CLEAR gt_out.

  LOOP AT gt_lfa1 INTO ls_lfa1.
    CLEAR:
      ls_lfb1,
      ls_lfm1.

    READ TABLE gt_lfb1 INTO ls_lfb1
          WITH KEY lifnr = ls_lfa1-lifnr BINARY SEARCH.
    READ TABLE gt_lfm1 INTO ls_lfm1
          WITH KEY lifnr = ls_lfa1-lifnr BINARY SEARCH.

    gs_out-lifnr = ls_lfa1-lifnr.
    gs_out-bukrs = ls_lfb1-bukrs.
    gs_out-ekorg = ls_lfm1-ekorg.
    gs_out-ktokk = ls_lfa1-ktokk.
    gs_out-name1 = ls_lfa1-name1.
    gs_out-name2 = ls_lfa1-name2.
    gs_out-name3 = ls_lfa1-name3.
    gs_out-name4 = ls_lfa1-name4.
    gs_out-mcod1 = ls_lfa1-mcod1.
    gs_out-stras = ls_lfa1-stras.
    gs_out-pstlz = ls_lfa1-pstlz.
    gs_out-ort01 = ls_lfa1-ort01.
    gs_out-land1 = ls_lfa1-land1.
    gs_out-regio = ls_lfa1-regio.
    gs_out-spras = ls_lfa1-spras.
    gs_out-telf1 = ls_lfa1-telf1.
    gs_out-telf2 = ls_lfa1-telf2.
    gs_out-teltx = ls_lfa1-teltx.
    gs_out-kunnr = ls_lfa1-kunnr.
    gs_out-vbund = ls_lfa1-vbund.
    gs_out-stcd1 = ls_lfa1-stcd1.
    gs_out-stceg = ls_lfa1-stceg.
    gs_out-akont = ls_lfb1-akont.
    gs_out-zterm = ls_lfb1-zterm.
    gs_out-zwels = ls_lfb1-zwels.
    gs_out-reprf = ls_lfb1-reprf.
    gs_out-waers = ls_lfm1-waers.
    gs_out-zter2 = ls_lfm1-zterm.
    gs_out-inco1 = ls_lfm1-inco1.
    gs_out-inco2 = ls_lfm1-inco2.
    gs_out-webre = ls_lfm1-webre.
    gs_out-kzaut = ls_lfm1-kzaut.
    gs_out-kzret = ls_lfm1-kzret.
    gs_out-bstae = ls_lfm1-bstae.

    READ TABLE gt_lfbk TRANSPORTING NO FIELDS
          WITH KEY lifnr = ls_lfa1-lifnr
          BINARY SEARCH.
    IF sy-subrc = 0.
      lv_tabix = sy-tabix.
      LOOP AT gt_lfbk INTO ls_lfbk
                      FROM lv_tabix.
        IF ls_lfbk-lifnr <> ls_lfa1-lifnr.
          EXIT.
        ENDIF.

        CLEAR ls_bnka.
        READ TABLE gt_bnka INTO ls_bnka
              WITH KEY banks = ls_lfbk-banks
                       bankl = ls_lfbk-bankl
              BINARY SEARCH.
        gs_out-banks = ls_lfbk-banks.
        gs_out-bankl = ls_lfbk-bankl.
        gs_out-bankn = ls_lfbk-bankn.
        gs_out-bkref = ls_lfbk-bkref.
        gs_out-banka = ls_bnka-banka.
        APPEND gs_out TO gt_out.
      ENDLOOP.
    ELSE.
      APPEND gs_out TO gt_out.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " GET_DATA
*&---------------------------------------------------------------------*
*&      Form  DISPLAY_DATA
*&---------------------------------------------------------------------*
*       Display ALV
*----------------------------------------------------------------------*
FORM DISPLAY_DATA .

  CLEAR:
    gv_col,
    gt_fieldcat,
    gs_layout.

  PERFORM add_field USING:
     'LIFNR' 10  text-003 text-003,
     'BUKRS' 4   text-002 text-002,
     'EKORG' 4   text-004 text-004,
     'KTOKK' 4   text-005 text-005,
     'NAME1' 35  text-006 text-006,
     'NAME2' 35  text-007 text-007,
     'NAME3' 35  text-008 text-008,
     'NAME4' 35  text-009 text-009,
     'MCOD1' 35  text-010 text-010,
     'STRAS' 25  text-011 text-011,
     'PSTLZ' 10  text-012 text-012,
     'ORT01' 35  text-013 text-013,
     'LAND1' 3   text-014 text-014,
     'REGIO' 3   text-015 text-015,
     'SPRAS' 1   text-016 text-016,
     'TELF1' 16  text-017 text-017,
     'TELF2' 16  text-018 text-018,
     'TELTX' 30  text-019 text-019,
     'KUNNR' 10  text-020 text-020,
     'VBUND' 6   text-021 text-021,
     'STCD1' 16  text-022 text-022,
     'STCEG' 20  text-023 text-023,
     'BANKS' 3   text-024 text-024,
     'BANKL' 15  text-025 text-025,
     'BANKN' 18  text-026 text-026,
     'BKREF' 20  text-027 text-027,
     'BANKA' 60  text-028 text-028,
     'AKONT' 10  text-029 text-029,
     'ZTERM' 4   text-030 text-030,
     'ZWELS' 10  text-031 text-031,
     'REPRF' 1   text-032 text-032,
     'WAERS' 5   text-033 text-033,
     'ZTER2' 4   text-030 text-030,
     'INCO1' 3   text-034 text-034,
     'INCO2' 28  text-035 text-035,
     'WEBRE' 1   text-036 text-036,
     'KZAUT' 1   text-037 text-037,
     'KZRET' 1   text-038 text-038,
     'BSTAE' 4   text-039 text-039.

  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   "Auto optimize column width
  gs_layout-detail_popup = gc_x.        "Show detail screen

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'SET_PF_STATUS'
      is_layout                = gs_layout
      it_fieldcat              = gt_fieldcat
      i_save                   = gv_save
      is_variant               = gs_disvar
    TABLES
      t_outtab                 = gt_out
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    IF sy-batch = gc_x.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ELSE.
      MESSAGE ID sy-msgid TYPE 'S' NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
            DISPLAY LIKE 'E'.
      LEAVE LIST-PROCESSING.
    ENDIF.
  ENDIF.

ENDFORM.                    " DISPLAY_DATA
*&-------------------------------------------------------------------
*& Form set_pf_status
*&-------------------------------------------------------------------
FORM SET_PF_STATUS USING rt_extab TYPE slis_t_extab.
 SET PF-STATUS 'ALV_PF_STATUS'.
ENDFORM. "Set_pf_status
*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr_s
                         p_descr_l.
  DATA:
     ls_fieldcat TYPE slis_fieldcat_alv.

  gv_col = gv_col + 1.

  CLEAR ls_fieldcat.
  ls_fieldcat-col_pos    = gv_col.
  ls_fieldcat-fieldname  = p_field.
  ls_fieldcat-outputlen  = p_len.
  ls_fieldcat-seltext_l  = p_descr_l.
  ls_fieldcat-seltext_m  = p_descr_l.
  ls_fieldcat-seltext_s  = p_descr_s.
  ls_fieldcat-fix_column = gc_x.
  ls_fieldcat-emphasize  = gc_x.

  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ADD_FIELD

*Text symbol text£º
*002:Company
*003:Vendor
*004:Purch.Org.
*005:Acc. Group
*006:Name1
*007:Name2
*008:Name3
*009:Name4
*010:SearchTerm
*011:Street
*012:PostalCode
*013:City
*014:Country
*015:Region
*016:Language
*017:Telephone
*018:Mobile
*019:Fax
*020:Customer
*021:Tr.Partner
*022:Tax Number
*023:VAT Reg.No
*024:Bank Ctry.
*025:Bank Key
*026:Bank acct
*027:Reference
*028:Bank Name
*029:Recon Acct
*030:Pay.Terms
*031:Pay.Method
*032:Doubl Ent.
*033:Currency
*034:Incoterms
*035:Incoterms1
*036:GR Based
*037:Auto. PO
*038:ReturnVend
*039:Conf.Ctrol

*T01:Database Selections
*Selection text£º
*S_BUKRS:        Company code
*S_EKORG:        Purchasing Org
*S_LIFNR:        Vendor
