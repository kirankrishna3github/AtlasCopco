*----------------------------------------------------------------------*
* Author  : Anda Wu                                                    *
* Created : 04.27.2015                                                 *
* Dev.nbr: CR3609-New report for Russia SAP0000042520                  *
*----------------------------------------------------------------------*
* This report  is  new report for returns  of Russia                   *
************************************************************************
* Mod-xxx | xxxx.xx.xx | xxxx xxxx     | CD1Kxxxxxxxxxx  |   CRXXXX    *
************************************************************************
************************************************************************
REPORT yse_sd_return_rep NO STANDARD PAGE HEADING LINE-SIZE 255.

************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES:
  aufk,
  vbap,
  mkpf,
  vbak,
  vbkd.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:

  BEGIN OF ty_alv_output,
    mblnr    TYPE char10,       "Material document number
    aufnr    TYPE char12,       "Service order
    comment  TYPE char120,      "Comment
    vaplz    TYPE aufk-vaplz,   "Mn.wk.ctr
    bemot    TYPE aufk-bemot,   "Accounting Indicator
    verpr    TYPE mbew-verpr,   "moving av.price
    matnr    TYPE char18,       "Material Number
    kunnr    TYPE vbak-kunnr,   "Customer
    name1    TYPE kna1-name1,   "Customer Name
    kdauf    TYPE aufk-kdauf,   "Sales order
    vkbur    TYPE vkbur,        "Sales Office
    vkgrp    TYPE vkgrp,        "Sales Group
    bezei    TYPE tvgrt-bezei,  "Sales Group Description
    vbeln  TYPE vbfa-vbeln,   "ASSO Number
    isdat    TYPE erdat,        "issue Date
    budat    TYPE budat,        "Return Date
    bstkd_e  TYPE vbkd-bstkd_e, "Reason Code
    ktext    TYPE crtx-ktext,   "Service technicians
  END OF ty_alv_output,

  BEGIN OF ty_mkpf,
    mblnr   TYPE mkpf-mblnr,    "Material Document Number
    mjahr   TYPE mkpf-mjahr,    "Material Document Year
    budat   TYPE mkpf-budat,    "Posting Date in the Document
  END OF ty_mkpf,

  BEGIN OF ty_mseg,
    mblnr   TYPE mseg-mblnr,    "Material Document Number
    mjahr   TYPE mseg-mjahr,    "Material Document Year
    zeile   TYPE mseg-zeile,    "Material Document Item
    bwart   TYPE mseg-bwart,    "Movement Type
    matnr   TYPE mseg-matnr,    "Material Number
    werks   TYPE mseg-werks,    "Plant
    kunnr   TYPE mseg-kunnr,    "Account Number of Customer
    bwtar   TYPE mseg-bwtar,    "Valuation Type
    kdauf   TYPE mseg-kdauf,    "Sales Order Number
    kdpos   TYPE mseg-kdpos,    "Item Number in Sales Order
    aufnr   TYPE mseg-aufnr,    "Order Number
  END OF ty_mseg,

  BEGIN OF ty_vbak,
    vbeln    TYPE  vbak-vbeln,  "Sales Document
    auart    TYPE  vbak-auart,  "Sales order type
    vkorg    TYPE  vbak-vkorg,  "Sales Org.
    vtweg    TYPE  vbak-vtweg,  "Distribution channel
    vkbur    TYPE  vbak-vkbur,  "Sales Office
    vkgrp    TYPE  vbak-vkgrp,  "Sales Group
    vsbed    TYPE  vbak-vsbed,  "Shipping condition
    kunnr    TYPE  vbak-kunnr,  "Sold-to party
  END OF ty_vbak,

  BEGIN OF ty_makt,
    matnr    TYPE makt-matnr,   "Material Number
    spras    TYPE makt-spras,   "Language Key
    maktx    TYPE makt-maktx,   "Material Description
  END OF ty_makt,

  BEGIN OF ty_mbew,
    matnr	   TYPE mbew-matnr,   "Material Number
    bwkey	   TYPE mbew-bwkey,   "Valuation Area
    bwtar	   TYPE mbew-bwtar,   "Valuation Type
    verpr    TYPE mbew-verpr,   "Moving Average Price/Periodic Unit Price
    stprs    TYPE mbew-stprs,   "Standard price
  END OF ty_mbew,

  BEGIN OF ty_mard,
    matnr    TYPE mard-matnr,   "Material Number
    werks    TYPE mard-werks,   "Plant
    lgort    TYPE mard-lgort,   "Storage Locatio
    labst    TYPE mard-labst,   "UU Stock
  END OF ty_mard,

  BEGIN OF ty_mvke,
    matnr	   TYPE mvke-matnr,   "Material Number
    vkorg	   TYPE mvke-vkorg,   "Sales Organization
    vtweg    TYPE mvke-vtweg,   "Distribution Channel
    prodh    TYPE mvke-prodh,   "Product hierarchy
  END OF ty_mvke,

  BEGIN OF ty_tvgrt,
    vkgrp    TYPE tvgrt-vkgrp,  "Sales Group
    bezei    TYPE tvgrt-bezei,  "Sales Group Name
  END OF ty_tvgrt,

  BEGIN OF ty_vbep,
    vbeln    TYPE vbep-vbeln,   "Sales Document
    posnr    TYPE vbep-posnr,   "Sales Document Item
    etenr    TYPE vbep-etenr,   "Delivery Schedule Line Number
    ettyp    TYPE vbep-ettyp,   "Schedule line category
    edatu    TYPE vbep-edatu,   "Requested delivery date
    wadat    TYPE vbep-wadat,   "Planned GI date
    bmeng    TYPE vbep-bmeng,   "Confirmed Quantity
  END OF ty_vbep,

  BEGIN OF ty_comp,
    rsnum     TYPE resb-rsnum,  "Number
    rspos     TYPE resb-rspos,  "Item Number
    vbeln_adv TYPE vbap-vbeln,  "Sales Document
    posnr_adv TYPE vbap-posnr,  "Sales Document Item
    sobkz     TYPE resb-sobkz,  "Special Stock Indicator
  END OF ty_comp,

  BEGIN OF ty_vbkd,
    vbeln     TYPE vbkd-vbeln,  "Sales Document
    posnr     TYPE vbkd-posnr,  "Sales Document Item
    zterm     TYPE vbkd-zterm,  "Terms of Payment Key
    bstkd     TYPE vbkd-bstkd,  "Customer PO number
    ihrez     TYPE vbkd-ihrez,  "Service Order
  END OF ty_vbkd,

  BEGIN OF ty_vbfa,
    vbelv     TYPE vbfa-vbelv,  "Preceding sales doc
    posnv     TYPE vbfa-posnv,  "Preceding item
    vbeln     TYPE vbfa-vbeln,  "Subsequent sale doc
    posnn     TYPE vbfa-posnn,  "Subsequent item
    vbtyp_n   TYPE vbfa-vbtyp_n,
    fkimg     TYPE vbrp-fkimg,  "Invoiced Quantity
    aubel     TYPE vbrp-aubel,  "SO
    aupos     TYPE vbrp-aupos,  "Item
  END OF ty_vbfa.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_vbak     TYPE STANDARD TABLE OF ty_vbak,
  gt_mkpf     TYPE STANDARD TABLE OF ty_mkpf,
  gt_mseg     TYPE STANDARD TABLE OF ty_mseg,
  gt_vbfa     TYPE STANDARD TABLE OF vbfa,
*  gt_viqmel   TYPE STANDARD TABLE OF viqmel,
  gt_crhd     TYPE STANDARD TABLE OF crhd,
  gt_aufk     TYPE STANDARD TABLE OF aufk,
  gt_mbew     TYPE STANDARD TABLE OF ty_mbew,
  gt_alvdata  TYPE STANDARD TABLE OF ty_alv_output,
  gt_fieldcat TYPE slis_t_fieldcat_alv.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_alvdata  TYPE ty_alv_output,
  gs_disvar   TYPE disvariant,
  gs_layout   TYPE slis_layout_alv.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_col(10)  TYPE n,
  gv_text     TYPE string,
  gv_save     TYPE c,
  gv_callback TYPE slis_formname.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x                TYPE c VALUE 'X',
  gc_type_e           TYPE c VALUE 'E',
  gc_callback_routine TYPE slis_formname VALUE 'USER_COMMAND'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-t02 .
SELECT-OPTIONS:
  s_budat      FOR mkpf-budat OBLIGATORY,  "Return date
  s_matnr      FOR vbap-matnr,             "Material ID
  s_aufnr      FOR aufk-aufnr,             "Service order number
  s_vaplz      FOR aufk-vaplz,             "Mn.wk.ctr
  s_kunnr      FOR vbak-kunnr,             "Sold-to party
  s_vkorg      FOR vbak-vkorg,             "Sales org
  s_bstkd      FOR vbkd-bstkd_e.           "Reason code
SELECTION-SCREEN END OF BLOCK b2.

* Alv variants
SELECTION-SCREEN BEGIN OF BLOCK b3 WITH FRAME TITLE text-t03.

PARAMETERS: p_vari TYPE disvariant-variant.       "Dispaly Variant

SELECTION-SCREEN END OF BLOCK b3.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initilization
  PERFORM frm_init.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN                                                 *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.
* selection screen check
  PERFORM check_parameter.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN ON VALUE-REQUEST                                *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
  PERFORM f4_for_variant.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* progress indicator
  PERFORM process_info.
* get data
  PERFORM get_alv_data.
* alv property set
  PERFORM alv_prop_set.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
* ALV display
  PERFORM alv_display.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do initilization
*----------------------------------------------------------------------*
FORM frm_init .

* initialize the internal table,workareas and variables
  REFRESH:
    gt_vbak,
    gt_mbew,
    gt_alvdata,
    gt_fieldcat.

  CLEAR:
    gs_alvdata,
    gs_disvar,
    gs_layout,
    gv_col,
    gv_text,
    gv_save,
    gv_callback.

  gv_save = 'A'.
* Set default alv layout
  gs_disvar-report = sy-repid.
  CALL FUNCTION 'REUSE_ALV_VARIANT_DEFAULT_GET'
    EXPORTING
      i_save        = gv_save
    CHANGING
      cs_variant    = gs_disvar
    EXCEPTIONS
      wrong_input   = 1
      not_found     = 2
      program_error = 3
      OTHERS        = 4.
  IF sy-subrc = 0.
    p_vari = gs_disvar-variant.
  ENDIF.

ENDFORM.                    " FRM_INIT

*&---------------------------------------------------------------------*
*&      Form  CHECK_PARAMETER
*&---------------------------------------------------------------------*
*       selection screen check
*----------------------------------------------------------------------*
FORM check_parameter .

  DATA: ls_disvar TYPE disvariant.

  IF p_vari IS NOT INITIAL.
    ls_disvar = gs_disvar.
    ls_disvar-variant = p_vari.
    CALL FUNCTION 'REUSE_ALV_VARIANT_EXISTENCE'
      EXPORTING
        i_save        = gv_save
      CHANGING
        cs_variant    = ls_disvar
      EXCEPTIONS
        wrong_input   = 1
        not_found     = 2
        program_error = 3
        OTHERS        = 4.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ELSE.
      gs_disvar = ls_disvar.
    ENDIF.
  ENDIF.

ENDFORM.                    " CHECK_PARAMETER
*&---------------------------------------------------------------------*
*&      Form  PROCESS_INFO
*&---------------------------------------------------------------------*
*       progress indicator
*----------------------------------------------------------------------*
FORM process_info .

  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    CONCATENATE 'Start report:'(t52) sy-repid
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t53.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 1
        text       = gv_text.
  ENDIF.

ENDFORM.                    " PROCESS_INFO
*&---------------------------------------------------------------------*
*&      Form  GET_ALV_DATA
*&---------------------------------------------------------------------*
*       get data
*----------------------------------------------------------------------*
FORM get_alv_data .
* Gain data from table MKPF - Header: Material Document
  PERFORM get_data_mkpf.
* Gain data from table MSEG - Sales Document: Header Data
  PERFORM get_data_mseg.
* Gain data from table AUFK - Order master data
  PERFORM get_data_aufk.
* Gain data from table MBEW - Material Valuation
  PERFORM get_data_mbew.
* Gain data from table VBAK - Sales Document: Header Data
  PERFORM get_data_vbak.
* Gain data from table VBFA - Document flow
  PERFORM get_data_vbfa.
* Gain data from table CRHD - Work Center Header
  PERFORM get_data_crhd.
* Generate ALV data for output
  PERFORM generate_alv_data.
* Edit all available
*  PERFORM edit_alv_data.
ENDFORM.                    " GET_ALV_DATA
*&---------------------------------------------------------------------*
*&      Form  ALV_PROP_SET
*&---------------------------------------------------------------------*
*       alv property set
*----------------------------------------------------------------------*
FORM alv_prop_set .

* Display ALV process indicator
  PERFORM alv_progress_indicator.

* FIELDCAT set
  PERFORM alv_fieldcat_set.

* LAYOUT set
  PERFORM alv_layout_set.

* Set Others
  PERFORM alv_others_set.

ENDFORM.                    " ALV_PROP_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_lines(10)  TYPE c.

  IF gt_alvdata IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-t61.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  IF sy-batch IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-t63.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 85
        text       = gv_text.
  ELSE.
    CLEAR: gv_text.
    DESCRIBE TABLE gt_alvdata LINES lv_lines.
    CONCATENATE 'Totally'(t62) lv_lines 'entries are generated'(t63)
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ENDIF.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'FRM_MY_STATUS'
      i_callback_user_command  = gv_callback
      is_layout                = gs_layout
      it_fieldcat              = gt_fieldcat
      i_save                   = gv_save
      is_variant               = gs_disvar
    TABLES
      t_outtab                 = gt_alvdata
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  FRM_MY_STATUS
*&---------------------------------------------------------------------*
*       Status Set
*----------------------------------------------------------------------*
*  --> I_EXTAB    STATUS
*----------------------------------------------------------------------*
FORM frm_my_status
  USING i_extab TYPE slis_t_extab.              "#EC CALLED "#EC NEEDED

  SET PF-STATUS '9100'.

ENDFORM.        "FRM_MY_STATUS

*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM alv_fieldcat_set .

** Create Fieldcatalogue from internal table

  CLEAR gv_col.

* Field definitions
  PERFORM add_field USING:
     'MBLNR'    10  'Mat Doc'(s01) 'Material Document Number'(l01) 'X',
     'AUFNR'    12  'SEO'(s02)     'Service Order'(l02)       'X',
     'COMMENT'  120 'Comment'(s03)   'Long Text In SEO'(l03)   ' ',
     'VAPLZ'    8 'Mn.wk.ctr'(s04)   'Main Work Center'(l04) ' ',
     'BEMOT'    2 'Acc.Ind'(s05)    'Accounting Indicator'(l05)' ',
     'VERPR'    11 'Avg.Prc'(s06)   'Moving Average Price'(l06) ' ',
     'MATNR'    18 'Material'(s07)   'Material Number'(l07) ' ',
     'KUNNR'    10 'Customer'(s08)  'Customer Number'(l08)    ' ',
     'NAME1'    35 'Cust.Name'(s20)  'Customer Name'(l20) ' ',
     'KDAUF'    10 'Sev.SO'(s09)    'Service Sales Order'(l09) ' ',
     'VKBUR'    4 'Sales Off.'(s10)   'Sales Office'(l10)     ' ',
     'VKGRP'    3 'Sales.Grp'(s11)   'Sales Group'(l11)  ' ',
     'BEZEI'    3 'SalGrp.Nm'(s25)   'Sales Group Name'(l25)  ' ',
     'VBELN'    10 'Asso No.'(s12)  'ASSO Number'(l12) ' ',
     'ISDAT'    8  'Dat.Issue'(s13)    'Date Of Issue'(l13)   ' ',
     'BUDAT'    8  'Dat.Retn'(s14)     'Date Of Return'(l14)      ' ',
     'BSTKD_E'  35  'Reas.Cod'(s15)   'Reason Code'(l15)  ' ',
     'KTEXT'    40 'Serv.Tech'(s16)   'Service Technicians'(l16) ' '.

ENDFORM.                    " ALV_FIELDCAT_SET

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr_s
                         p_descr_l
                         p_hotsp.
  DATA:
     ls_fieldcat TYPE slis_fieldcat_alv.

  gv_col = gv_col + 1.

  CLEAR ls_fieldcat.
  ls_fieldcat-col_pos    = gv_col.
  ls_fieldcat-fieldname  = p_field.
  ls_fieldcat-outputlen  = p_len.
  ls_fieldcat-seltext_l  = p_descr_l.
  ls_fieldcat-seltext_m  = p_descr_l.
  ls_fieldcat-seltext_s  = p_descr_s.
  ls_fieldcat-fix_column = 'X'.
  ls_fieldcat-emphasize  = 'X'.
  ls_fieldcat-hotspot    = p_hotsp.

* Suppress leading zeros for the material number
  IF p_field = 'MATNR'.
    ls_fieldcat-edit_mask = '==MATN1'.
  ENDIF.

  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   "Auto optimize column width
  gs_layout-detail_popup = gc_x.        "Show detail screen

ENDFORM.                    " ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_VBAK
*&---------------------------------------------------------------------*
*       Gain data from table VBAK - Sales Document: Header Data
*       Gain data from table VBAP - Sales Document: Item Data
*----------------------------------------------------------------------*
FORM get_data_vbak .

  CHECK gt_aufk IS NOT INITIAL.
* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to retrieve table VBAK'(t54).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t54.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 55
        text       = gv_text.
  ENDIF.

  SELECT   vbeln     "Sales Document
           auart     "Sales order type
           vkorg     "Sales Org.
           vtweg     "Distribution channel
           vkbur     "Sales Office
           vkgrp     "Sales Group
           vsbed     "Shipping condition
           kunnr     "Sold-to party
    FROM  vbak
    INTO TABLE gt_vbak
    FOR ALL ENTRIES IN gt_aufk
    WHERE vbeln = gt_aufk-kdauf
      AND vkorg IN s_vkorg
      AND kunnr  in s_kunnr.
  IF sy-subrc <> 0.
    IF sy-batch IS NOT INITIAL.
      CLEAR: gv_text.
      gv_text ='No data exist in table VBAK'(t59).
      MESSAGE i000(yse_sales_log) WITH gv_text.
    ELSE.
      MESSAGE e000(yse_sales_log) WITH text-t59.
    ENDIF.
  ENDIF.
ENDFORM.                    " GET_DATA_VBAK

*&---------------------------------------------------------------------*
*&      Form  GENERATE_ALV_DATA
*&---------------------------------------------------------------------*
*       Generate ALV data for output
*----------------------------------------------------------------------*
FORM generate_alv_data .

  DATA:
    ls_vbak    TYPE ty_vbak,
    ls_mseg    TYPE ty_mseg,
    ls_mkpf    TYPE ty_mkpf,
    ls_aufk    TYPE aufk,
    ls_crhd    TYPE crhd,
    ls_vbfa    TYPE vbfa,
    lv_posnn   TYPE vbfa-posnn,
    lv_vbelv   TYPE vbfa-vbelv,
    lv_posnv   TYPE vbfa-posnv,
    ls_mbew    TYPE ty_mbew.

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to generate ALV data'(t64).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t64.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 90
        text       = gv_text.
  ENDIF.

  LOOP AT gt_mseg INTO ls_mseg.
    CLEAR gs_alvdata.
    gs_alvdata-mblnr = ls_mseg-mblnr.
    gs_alvdata-aufnr = ls_mseg-aufnr.
    gs_alvdata-matnr = ls_mseg-matnr.
    CLEAR: ls_mkpf.
    READ TABLE gt_mkpf INTO ls_mkpf
     WITH KEY mblnr = ls_mseg-mblnr
              mjahr = ls_mseg-mjahr.
    gs_alvdata-budat = ls_mkpf-budat.
    CLEAR: ls_aufk.
    READ TABLE gt_aufk INTO ls_aufk
      WITH KEY aufnr = ls_mseg-aufnr.
    gs_alvdata-comment = ls_aufk-zzcom.
    gs_alvdata-vaplz = ls_aufk-vaplz.
    IF ls_aufk-vaplz NOT IN s_vaplz.
      CONTINUE.
    ENDIF.
    gs_alvdata-bemot = ls_aufk-bemot.
    gs_alvdata-kdauf = ls_aufk-kdauf.
    CLEAR: ls_mbew.
    READ TABLE gt_mbew INTO ls_mbew
      WITH KEY  matnr = ls_mseg-matnr
                bwkey = ls_mseg-werks
                bwtar = ls_mseg-bwtar.
    gs_alvdata-verpr = ls_mbew-verpr.
    CLEAR ls_vbak.
    READ TABLE gt_vbak INTO ls_vbak
      WITH KEY vbeln = ls_aufk-kdauf.
    IF sy-subrc <> 0.
      CONTINUE.
    ENDIF.
    gs_alvdata-kunnr = ls_vbak-kunnr.
    gs_alvdata-vkbur = ls_vbak-vkbur.
    gs_alvdata-vkgrp = ls_vbak-vkgrp.
    CLEAR: ls_crhd.
    READ TABLE gt_crhd INTO ls_crhd
      WITH KEY arbpl = ls_aufk-vaplz
               werks = ls_aufk-werks.
    SELECT SINGLE ktext
      FROM crtx
      INTO gs_alvdata-ktext
      WHERE objty = ls_crhd-objty
        AND objid = ls_crhd-objid
        AND spras = sy-langu.
    IF sy-subrc <> 0.
      SELECT SINGLE ktext
        FROM crtx
        INTO gs_alvdata-ktext
        WHERE objty = ls_crhd-objty
          AND objid = ls_crhd-objid
          AND spras = 'E'.
    ENDIF.

    SELECT vbeln posnn
      FROM vbfa
      INTO (gs_alvdata-vbeln,lv_posnn)
      UP TO 1 ROWS
      WHERE vbelv = ls_aufk-kdauf
        AND posnv = ls_aufk-kdpos
        AND vbtyp_n = 'C'.
    ENDSELECT.
    CLEAR: lv_vbelv, lv_posnv.
    READ TABLE gt_vbfa INTO ls_vbfa
      WITH KEY vbeln = ls_mseg-mblnr
               posnn = ls_mseg-zeile.
    IF sy-subrc = 0.
      lv_vbelv = ls_vbfa-vbelv.
      lv_posnv = ls_vbfa-posnv.
    ELSE.
      lv_vbelv = gs_alvdata-vbeln.
      lv_posnv = lv_posnn.
    ENDIF.
*   Get the date of issue
    PERFORM get_issue_date USING lv_vbelv
                                  lv_posnv
                           CHANGING gs_alvdata-isdat.
    SELECT bstkd_e
      FROM vbkd
      INTO gs_alvdata-bstkd_e
      UP TO 1 ROWS
      WHERE vbeln = lv_vbelv
        AND posnr = lv_posnv.
    ENDSELECT.
    IF s_bstkd IS NOT INITIAL.
      IF gs_alvdata-bstkd_e NOT IN s_bstkd.
        CONTINUE.
      ENDIF.
    ENDIF.
    SELECT SINGLE name1
      FROM kna1
      INTO gs_alvdata-name1
      WHERE kunnr = gs_alvdata-kunnr.
    SELECT SINGLE bezei
      FROM tvgrt
      INTO gs_alvdata-bezei
      WHERE spras = sy-langu
        AND vkgrp = gs_alvdata-vkgrp.
    IF sy-subrc <> 0.
      SELECT SINGLE bezei
        FROM tvgrt
        INTO gs_alvdata-bezei
        WHERE spras = 'E'
          AND vkgrp = gs_alvdata-vkgrp.
    ENDIF.
    APPEND gs_alvdata TO gt_alvdata.
  ENDLOOP.

ENDFORM.                    " GENERATE_ALV_DATA

*&---------------------------------------------------------------------*
*&      Form  ALV_PROGRESS_INDICATOR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM alv_progress_indicator .

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to set ALV property'(t60).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t60.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 85
        text       = gv_text.
  ENDIF.

ENDFORM.                    " ALV_PROGRESS_INDICATOR

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       Set Others
*----------------------------------------------------------------------*
FORM alv_others_set .

  gv_callback = gc_callback_routine.

ENDFORM.                    " ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*&      Form  F4_FOR_VARIANT
*&---------------------------------------------------------------------*
*       F4 help for display variant
*----------------------------------------------------------------------*
FORM f4_for_variant .

  DATA: ls_disvar TYPE disvariant,
        lv_exit   TYPE c.

  ls_disvar-report = sy-repid.

  CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
    EXPORTING
      is_variant    = ls_disvar
      i_save        = gv_save
    IMPORTING
      e_exit        = lv_exit
      es_variant    = gs_disvar
    EXCEPTIONS
      not_found     = 1
      program_error = 2
      OTHERS        = 3.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    IF lv_exit = space.
      p_vari = gs_disvar-variant.
    ENDIF.
  ENDIF.

ENDFORM.                    " F4_FOR_VARIANT

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       USER_COMMAND
*----------------------------------------------------------------------*
FORM user_command USING u_ucomm     LIKE sy-ucomm           "#EC CALLED
                        p_selfield  TYPE slis_selfield.

  CASE u_ucomm.
    WHEN '&IC1'.        " SAP standard code for double-clicking
      IF p_selfield-fieldname = 'AUFNR'.
        IF p_selfield-value IS NOT INITIAL.
          SET PARAMETER ID 'ANR' FIELD p_selfield-value.
          CALL TRANSACTION 'IW33'.
        ENDIF.
      ENDIF.
      IF p_selfield-fieldname = 'MATNR'.
        IF p_selfield-value IS NOT INITIAL.
          SET PARAMETER ID 'MAT' FIELD p_selfield-value.
          CALL TRANSACTION 'MM03'.
        ENDIF.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.

ENDFORM.                    " USER_COMMAND
*&---------------------------------------------------------------------*
*&      Form  EDIT_ALV_DATA
*&---------------------------------------------------------------------*
*       Edit all available
*----------------------------------------------------------------------*
*FORM edit_alv_data .
*
*ENDFORM.                    " EDIT_ALV_DATA
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_MKPF
*&---------------------------------------------------------------------*
*       Gain data from table MKPF - Header: Material Document
*----------------------------------------------------------------------*
FORM get_data_mkpf.

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to extract data from table MKPF'(t81).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t81.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 5
        text       = gv_text.
  ENDIF.
  SELECT  mblnr
          mjahr
          budat
    FROM  mkpf
    INTO TABLE gt_mkpf
    WHERE budat IN s_budat.
  IF sy-subrc <> 0.
    IF sy-batch IS NOT INITIAL.
      CLEAR: gv_text.
      gv_text ='No data exist in table MKPF'(t55).
      MESSAGE i000(yse_sales_log) WITH gv_text.
    ELSE.
      MESSAGE e000(yse_sales_log) WITH text-t55.
    ENDIF.
  ENDIF.
ENDFORM.                    " GET_DATA_MKPF
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_MSEG
*&---------------------------------------------------------------------*
*       Gain data from table MSEG - Sales Document: Header Data
*----------------------------------------------------------------------*
FORM get_data_mseg .
* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to extract data from table MSEG'(t82).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t82.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 15
        text       = gv_text.
  ENDIF.
  SELECT   mblnr      "Material Document Number
           mjahr      "Material Document Year
           zeile      "Material Document Item
           bwart      "Movement Type
           matnr      "Material Number
           werks      "Plant
           kunnr      "Account Number of Customer
           bwtar      "Valuation Type
           kdauf      "Sales Order Number
           kdpos      "Item Number in Sales Order
           aufnr      "Order Number
    FROM  mseg
    INTO TABLE gt_mseg
    FOR ALL ENTRIES IN gt_mkpf
    WHERE mblnr = gt_mkpf-mblnr
      AND mjahr = gt_mkpf-mjahr
      AND bwart = '262'
      AND matnr IN s_matnr
      AND aufnr IN s_aufnr.
  IF sy-subrc <> 0.
    IF sy-batch IS NOT INITIAL.
      CLEAR: gv_text.
      gv_text ='No data exist in table MSEG'(t56).
      MESSAGE i000(yse_sales_log) WITH gv_text.
    ELSE.
      MESSAGE e000(yse_sales_log) WITH text-t56.
    ENDIF.
  ENDIF.
ENDFORM.                    " GET_DATA_MSEG
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_VBFA
*&---------------------------------------------------------------------*
*       Gain data from table VBFA - Notification Header
*----------------------------------------------------------------------*
FORM get_data_vbfa .

  TYPES: BEGIN OF lty_mseg_temp,
          mblnr TYPE mseg-mblnr,
          mosnn TYPE vbfa-posnn,
         END OF lty_mseg_temp.
  DATA: lt_mseg_temp  TYPE STANDARD TABLE OF lty_mseg_temp,
        ls_mseg_temp  TYPE lty_mseg_temp,
        ls_mseg   TYPE ty_mseg.

  CHECK gt_mseg IS NOT INITIAL.
* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to extract data from table VBFA'(t83).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t83.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 60
        text       = gv_text.
  ENDIF.

  LOOP AT gt_mseg INTO ls_mseg.
    ls_mseg_temp-mblnr  = ls_mseg-mblnr.
    ls_mseg_temp-mosnn  = ls_mseg-zeile.
    APPEND ls_mseg_temp TO lt_mseg_temp.
  ENDLOOP.

  SELECT *
    FROM vbfa
    INTO TABLE gt_vbfa
    FOR ALL ENTRIES IN lt_mseg_temp
    WHERE vbeln = lt_mseg_temp-mblnr
      AND posnn = lt_mseg_temp-mosnn
      AND vbtyp_v = 'C'.

ENDFORM.                    " GET_DATA_vbfa
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_AUFK
*&---------------------------------------------------------------------*
*       Gain data from table AUFK - Order master data
*----------------------------------------------------------------------*
FORM get_data_aufk .

  CHECK gt_mseg IS NOT INITIAL.
* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to extract data from table AUFK'(t84).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t84.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 25
        text       = gv_text.
  ENDIF.

  SELECT *
    FROM aufk
    INTO TABLE gt_aufk
    FOR ALL ENTRIES IN gt_mseg
    WHERE aufnr = gt_mseg-aufnr.

ENDFORM.                    " GET_DATA_AUFK
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_MBEW
*&---------------------------------------------------------------------*
*       Gain data from table MBEW - Material Valuation
*----------------------------------------------------------------------*
FORM get_data_mbew .
  CHECK gt_mseg IS NOT INITIAL.
* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to extract data from table MBEW'(t85).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t85.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 45
        text       = gv_text.
  ENDIF.
  SELECT   matnr
           bwkey       "Valuation Area
           bwtar       "Valuation Type
           verpr       "Moving Average Price/Periodic Unit Price
           stprs       "Standard price
    FROM mbew
    INTO TABLE gt_mbew
    FOR ALL ENTRIES IN gt_mseg
    WHERE matnr = gt_mseg-matnr
      AND bwkey = gt_mseg-werks
      AND bwtar = gt_mseg-bwtar.
ENDFORM.                    " GET_DATA_MBEW
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_CRHD
*&---------------------------------------------------------------------*
*       tGain data from table CRHD - Work Center Header
*----------------------------------------------------------------------*
FORM get_data_crhd .
  CHECK gt_aufk IS NOT INITIAL.
* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to extract data from table CRHD'(t88).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t88.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 70
        text       = gv_text.
  ENDIF.
  SELECT   *
    FROM crhd
    INTO TABLE gt_crhd
    FOR ALL ENTRIES IN gt_aufk
    WHERE objty = 'A'
      AND arbpl = gt_aufk-vaplz
      AND werks = gt_aufk-werks.
ENDFORM.                    " GET_DATA_CRHD
*&---------------------------------------------------------------------*
*&      Form  GET_ISSUE_DATE
*&---------------------------------------------------------------------*
*       Get the date of issue
*----------------------------------------------------------------------*
*      -->P_VBELN  text
*      <--P_ISDAT  text
*----------------------------------------------------------------------*
FORM get_issue_date  USING    p_vbeln
                              p_posnn
                     CHANGING p_isdat.
  DATA: lt_vbfa TYPE STANDARD TABLE OF vbfa,
        ls_vbfa TYPE vbfa.
  SELECT *
    FROM vbfa
    INTO TABLE lt_vbfa
    WHERE vbelv = p_vbeln
*      AND posnn <> 0
      AND posnv = p_posnn
      AND vbtyp_n = 'R'
      AND bwart = '261'.
  IF sy-subrc = 0.
    CLEAR ls_vbfa.
    READ TABLE lt_vbfa INTO ls_vbfa
      INDEX 1.
    SELECT SINGLE budat
      FROM mkpf
      INTO p_isdat
      WHERE mblnr = ls_vbfa-vbeln
        AND mjahr = ls_vbfa-mjahr.
  ENDIF.
ENDFORM.                    " GET_ISSUE_DATE

*Text symbol text£º
*L01:Material Document Number
*L02:Service Order
*L03:Long Text In SEO
*L04:Main Work Center
*L05:Accounting Indicator
*L06:Moving Average Price
*L07:Material Number
*L08:Customer Number
*L09:Service Sales Order
*L10:Sales Office
*L11:Sales Group
*L12:ASSO Number
*L13:Date Of Issue
*L14:Date Of Return
*L15:Reason Code
*L16:Service Technicians
*L20:Customer Name
*L25:Sales Group Name
*S01:Mat Doc
*S02:SEO
*S03:Comment
*S04:Mn.wk.ctr
*S05:Acc.Ind
*S06:Avg.Prc
*S07:Material
*S08:Customer
*S09:Sev.SO
*S10:Sales Off.
*S11:Sales.Grp
*S12:Asso No.
*S13:Dat.Issue
*S14:Dat.Retn
*S15:Reas.Cod
*S16:Serv.Tech
*S20:Cust.Name
*S25:SalGrp.Nm
*T02:Selection Input
*T03:ALV Layout
*T52:Start report:
*T53:Start report: YSE_SD_OOH
*T54:Begin to retrieve table VBAK
*T55:No data exist in table MKPF
*T56:No data exist in table MSEG
*T59:No data exist in table VBAK
*T60:Begin to set ALV property
*T61:No data exist
*T62:Totally
*T63:entries are generated
*T64:Begin to generate ALV data
*T81:Begin to extract data from table MKPF
*T82:Begin to extract data from table MSEG
*T83:Begin to extract data from table VBFA
*T84:Begin to extract data from table AUFK
*T85:Begin to extract data from table MBEW

*T88:Begin to extract data from table CRHD
*Selection text£º
*P_VARI:D       .
*S_AUFNR:        Service Order No.
*S_BSTKD:        Reason Code
*S_BUDAT:        Retun Date
*S_KUNNR:        Customer
*S_MATNR:D       .
*S_VAPLZ:D       .
*S_VKORG:D       .
