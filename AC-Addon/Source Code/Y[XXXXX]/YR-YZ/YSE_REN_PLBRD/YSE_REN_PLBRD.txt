REPORT YSE_REN_PLBRD MESSAGE-ID YSE_RENTAL.
**********************************************************************
* Program ID        : YSE_REN_PLBRD                                   *
* Program Title     : AC Rental Planning Board                        *
* Author            : Tom Van Oevelen                                 *
* Date              : 23/11/2006                                      *
* Functional spec   : D002.Rental Planning Board.doc                  *
*=====================================================================*
* Change History Log                                                  *
*---------------------------------------------------------------------*
* Mod. no.|  Date    | Name           | Correction Number | Change Ref*
*---------------------------------------------------------------------*
* MOD-001 |27/10/2008| M.Jacobs       | CD1K944234        | CR037     *
* Descr: change BUKRS GMIX into value of chararterstic RE_PGC_RERENT  *
*---------------------------------------------------------------------*
* MOD-002 |dd/mm/yyyy| xxxxxxxxxxxxxx | XXXXxxxxxx                    *
* Description:                                                        *
***********************************************************************
***********************************************************************
* Comments, improvements, etc.
* ----------------------------
* - Issue xls.45
* - Issue xls.74
* - Issue xls.73
* - Issue xls.75: equipment in repair shows as "On rent" in RPB
* - How is service shown on planning board and what wants changing?
* - Use in document sometimes doesn't work...(serial not copied)
*   With regards to the use of VBKD-BSTKD_E as temporary field: this could cause the message
*   "Business Header data doesn't apply to item ..."
*   With regards to the sometimes failing: could have to do with plant not filled. Check defualting functionality
* - Pl Brd: "current status available" on selection screen
* - "Available 1-/1 -> should not occur, is it due to the fact that we only
*   recently stopped creating overlapping contracts on the same equipment?
*   E.g. client 160 on 15/Jan/07 model 12 shows -7/2. This is due to 9 deliveries registered
*        to happen for the equipments on that day. Only 1 delivery still exists in the system.
*        Is related to the update terminated error.
* - The quotation doesn't change status when contract was created via PlBrd
*   (that is the status in document flow... also for inquiry converted to quotation)
* - SelScr: field Model should also work with materials
* - Possible to check if S/N can be selected if you 'transfer to rental basket'
*   or can this check only be executed at the 'create document'?
* - The selection mechanism can perhaps be improved in case one or more models
*   are entered together with characteristics
* - When creating Quotation, allow to create with quantity zero
* - Possibility to sort? E.g. via product hierarchy (material master: basic data 1)
* ********************************************************************* *
* Information ********************************************************* *
* ********************************************************************* *
* - Additional information on Serial numbers in SD (VA42):
*  + When clicking "Technical objects", the form FCODE_POTO (incl MV45AF0F_FCODE_POTO) is triggered
*  + The form FCODE_POTO in turn lets the actual work happen by calling FM SERIALPROFILE_CHECK (makes the popup)
*  + After the Function call, an internal table SERNR_CHG is updated with for the current item a NEW line, saying how
*    many serial numbers are assigned to the item in field ANZSN_NEW
*    * the field ANZSN_OLD holds the number of serial numbers before the change
*    * the field ANZSN is also available on VBAP
*  + Also the field "SERIAL_COMMIT" is used to fill SERIAL_DATALOSS...
* - All internal tables etc still have S502 in the name (a structure generated by LIS,
*   however, that DDIC table is no longer used, it's now YSE_RENT_SME
* ********************************************************************* *
* Solved issues ******************************************************* *
* ********************************************************************* *
* - Preceding zeroes in the rental basket can go
* - Shopping basket should become "Rental Basket"
* - Use Serial number everywhere, not equipment number (confirm this)
* - In the "Doc" chart: don't show purchase order but doc number
* - Sel Scr: show the chosen model's description (in the models block)
* - Sel Scr: show the last item's POSNR
* - Sel Scr: Select options: you have to enter a range, otherwise no result
*   should be more flexible
* - From contracts, the search help for materials should include an option to
*   search using the planning board
* - Block saving of a contract if used eqpt already used in other contract
*   This should be covered from the MV45A program (UE SAVE_DOC_PREPARE)
* - Add button for "section overview"
* - If a quotation is selected with a model -> fill in the model
* - Change "owned fleet"/"other rental comp fleet" selection logic (see Fleet selection mechanism.doc)
* - Sales items from a quotation are copied to the contract (even if not mapped/requested by user
* - From quotation to rental contract -> serial number not visible in the description field
* - When item transferred to shopping basket, no msg comes up. This should
*   be added together with error handling of this function. (e.g. "no model selected")
* - The serial numbers are created with OBJVW = 'K' -> this should be 'S'!
* - Should try to find a way to retrieve the last used Quot/Enq (parameter id)
* - Use In Doc functionality: if currently FLoc is one of the yards (use YSE_RENT_DEPOTS to
*   find out) default the plant with the corresponding plant from YS..DEPOTS. Otherwise
*   do nothing. -> should also happen when creating a contract from the planning board
* - The refresh button doesn't first save the Rental Basket
* - Allow for a "legend" popup so that colors etc are explained
* - Doubleclicking is OK for Contract, Del, ServOrd, Equip, Material
* - Planning board "snapshot list output"
* - Can you include the 'model description' in the list display of the planning board.
* - By default: show start/end date of of item line in selection screen and overview
*   planning board --> at this moment only the first weeks are selected and the user
*   has to press on the button 'section / overview'.
* - When new document is created and shown in change to the user, when the user then clicks
*   back, is it possible to go straight to the planning board selection screen?
* - If a quantity > 1 is inserted in the inquiry, it is not copied to the quoation. While
*   this is copied in the standard 'copy with reference'. Can you have a look at it?
*   -> Haven't changed anything but appears to be working in my example (10000114 >
*   20000426 > 40000511 (client 160)
* - From Enqu -> Quot, copy the non-replaced lines to the quotation as text (without any
*   replacement or mapping). For Quot -> Contr: unmapped items should be reported in appl log
* - When using PlBrd as search help to change a serial number on a contract, the number
*   doesn't get saved. Appears to work now (serial number treatment has changed in the meanwhile)
*   Tested in CD1 Cl 160 with contract 40000519
*        10 8162037066  XAS77DD AB D(B) JW RS CH / AIP33195 1 PC  516 5 D/Wk, 16 H/D      ZMVN  13
*      item 10 has been replaced by
*        10 8162057916  XAS77DD AB D(B) JW RS CH / AIP543543  1 PC  516 5 D/Wk, 16 H/D      ZMVN  10
* - Use planning board as search help from enquiry as well!
* - Details (quantity, owning CC, etc) have to be added!:
*     a.o. to table IT_EQUNR fields should be added for Owning CC, Current Loc,
*          Current Sta which will be filled at "LOAD_EQUI" and then used in LOAD_AVLY to
*          fill IT_MATEQ for LEVEL = 'EQ'
* - Search help function YSE_RPB_SH4MAT should be aborted if doc type is not rental. use memory to
*   find out about the doc type
* - Contract/quotation saved and opened by planning board, can we force incompletion check at
*   time of save (even if user himself hasn't changed anything)?
* ********************************************************************* *

*&---------------------------------------------------------------------*
*& INCLUDES -- Data types, data variables, constants ... --------------*
*&---------------------------------------------------------------------*
INCLUDE: SGRCCNST,              " General Graphics Constants
         LBARCCON,              " Constants for Network/Hierarchy (barchart) Graphics
         BARCDATA,              " General Data Declaration for the Bar Chart
         <ctldef>,              " General Definitions For Controls
         <symbol>,              " Assign Symbol Font Characters for Lists to ASCII Codes
         <icon>,                " Assign Icon Font Characters for Lists to ASCII Codes
         YSE_REN_PLBRD_GT1,     " Globally defined types
         YSE_REN_PLBRD_GD1,     " Global data variables
         YSE_REN_PLBRD_GC1.     " Global constants

*&---------------------------------------------------------------------*
*& SELECTION SCREEN ---------------------------------------------------*
*&---------------------------------------------------------------------*
* Document / Items
SELECTION-SCREEN BEGIN OF BLOCK B60 WITH FRAME TITLE TEXT-924.
  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 1(20) TEXT-924 FOR FIELD P_VBELN.
    SELECTION-SCREEN POSITION 21.
    PARAMETER P_VBELN TYPE VBELN             MODIF ID SEH.          " Doc Nr
    SELECTION-SCREEN COMMENT 33(8) TEXT-927 FOR FIELD P_DOCENQ.
    PARAMETER P_DOCENQ RADIOBUTTON GROUP R20  USER-COMMAND DOC MODIF ID SEH. "Radio Button Enquiry
    SELECTION-SCREEN COMMENT 45(8) TEXT-928 FOR FIELD P_DOCQUO.
    PARAMETER P_DOCQUO RADIOBUTTON GROUP R20 MODIF ID SEH. "Radio Button Quotation
    SELECTION-SCREEN PUSHBUTTON 59(10) PB_FETCH USER-COMMAND FETCH.
    SELECTION-SCREEN COMMENT 79(10) TEXT-960 FOR FIELD P_POSHI.     " Last item
    PARAMETER P_POSHI TYPE POSNR         MODIF ID POS.              " Last item
  SELECTION-SCREEN END OF LINE.
  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 3(18) TEXT-926 FOR FIELD P_POSNR.
    SELECTION-SCREEN POSITION 21.
    PARAMETER P_POSNR TYPE POSNR         MODIF ID SEH. " Position
    PARAMETER P_PMATNR(18) TYPE C        MODIF ID POS. " Pos Material
    PARAMETER P_PKWMEN     TYPE KWMENG
                      VISIBLE LENGTH 7   MODIF ID POS. " Pos Quant
    PARAMETER P_PVRKME     TYPE VRKME    MODIF ID POS. " Pos SU
    PARAMETER P_PGRPOS     TYPE GRPOS    MODIF ID POS. " Pos Alt Item
    PARAMETER P_PARKTX     TYPE ARKTX    MODIF ID POS. " Description
  SELECTION-SCREEN END OF LINE.
  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 5(15) TEXT-901 FOR FIELD P_BEGDAT.
    PARAMETER P_BEGDAT LIKE SY-DATUM.                  " Start date
    SELECTION-SCREEN COMMENT 32(15) TEXT-902 FOR FIELD P_ENDDAT.
    PARAMETER P_ENDDAT LIKE SY-DATUM.                  " End date
    SELECTION-SCREEN PUSHBUTTON 59(10) PB_RESET USER-COMMAND RESET.
  SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK B60.
* Models / Equipments
SELECTION-SCREEN BEGIN OF BLOCK B10 WITH FRAME TITLE TEXT-915.
* General characteristics
* Model  /  SOrg
  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 1(9) TEXT-925 FOR FIELD P_VKORG.
    PARAMETER P_VKORG TYPE VKORG OBLIGATORY. " MEMORY ID VKO.           " SalesOrg
    SELECTION-SCREEN COMMENT 19(4) TEXT-934 FOR FIELD P_VTWEG.
    PARAMETER P_VTWEG TYPE VTWEG. " MEMORY ID VTW.                      " DCh
    SELECTION-SCREEN COMMENT 30(12) TEXT-917 FOR FIELD P_VKBUR.
    PARAMETER P_VKBUR TYPE VKBUR. " MEMORY ID VKB.                      " Sales Office
  SELECTION-SCREEN END OF LINE.
  SELECTION-SCREEN BEGIN OF LINE.
    SELECTION-SCREEN COMMENT 1(6) TEXT-916 FOR FIELD SO_MODEL.
    SELECT-OPTIONS SO_MODEL FOR GV_MODEL NO INTERVALS.               " Model
    SELECTION-SCREEN POSITION 34.
    PARAMETER P_MMAKTX     TYPE MAKTX    MODIF ID POS.               " Model description
  SELECTION-SCREEN END OF LINE.
  SELECTION-SCREEN SKIP.
* Compressor characteristics
* **************************
* Toggling checkbox to show/hide Compressor Characteristics
  SELECTION-SCREEN BEGIN OF LINE.
    PARAMETER P_CLCOMP TYPE C AS CHECKBOX USER-COMMAND RUSR DEFAULT GC_CHAR_X.
    SELECTION-SCREEN COMMENT 23(30) TEXT-914 FOR FIELD P_CLCOMP.     " Check box
  SELECTION-SCREEN END OF LINE.
* Start compressor characteristics
  SELECTION-SCREEN BEGIN OF BLOCK B30 WITH FRAME TITLE TEXT-914.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 1(20) TEXT-922 FOR FIELD SO_PRESS MODIF ID CMP.
      SELECT-OPTIONS SO_PRESS FOR GV_PRESS MODIF ID CMP NO-EXTENSION.    " Pressure (bar)
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 1(20) TEXT-923 FOR FIELD SO_CAPAC MODIF ID CMP.
      SELECT-OPTIONS SO_CAPAC FOR GV_CAPAC MODIF ID CMP NO-EXTENSION.    " Capacity (m3/min)
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(17) TEXT-920 FOR FIELD P_CPOILF MODIF ID CMP.
      PARAMETER P_CPOILF(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID CMP. " Oilfree/inject.
      SELECTION-SCREEN COMMENT 44(17) TEXT-921 FOR FIELD P_CPDIEL MODIF ID CMP.
      PARAMETER P_CPDIEL(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID CMP. " Diesel/Electric
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(17) TEXT-935 FOR FIELD P_CPMOBI MODIF ID CMP.
      PARAMETER P_CPMOBI(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID CMP. " Mobile/Stationary
      SELECTION-SCREEN COMMENT 44(17) TEXT-929 FOR FIELD P_CPAFCL MODIF ID CMP.
      PARAMETER P_CPAFCL(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID CMP. " Aftercooler
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(17) TEXT-936 FOR FIELD P_CPREFI MODIF ID CMP.
      PARAMETER P_CPREFI(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID CMP. " Refinery Equipment
      SELECTION-SCREEN COMMENT 44(17) TEXT-930 FOR FIELD P_CPGENR MODIF ID CMP.
      PARAMETER P_CPGENR(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID CMP. " Generator built in
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(17) TEXT-937 FOR FIELD P_CPEXFU MODIF ID CMP.
      PARAMETER P_CPEXFU(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID CMP. " External Fuel Connection
      SELECTION-SCREEN COMMENT 44(17) TEXT-951 FOR FIELD P_CPDRYR MODIF ID CMP.
      PARAMETER P_CPDRYR(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID CMP. " Dryer built in
    SELECTION-SCREEN END OF LINE.
  SELECTION-SCREEN END OF BLOCK B30.
* Generator characteristics
* *************************
* Toggling checkbox to show/hide Generator Characteristics
  SELECTION-SCREEN BEGIN OF LINE.
    PARAMETER P_CLGENR TYPE C AS CHECKBOX USER-COMMAND RUSR DEFAULT GC_CHAR_X.
    SELECTION-SCREEN COMMENT 23(30) TEXT-938 FOR FIELD P_CLGENR.     " Check box
  SELECTION-SCREEN END OF LINE.
* Start Generator characteristics
  SELECTION-SCREEN BEGIN OF BLOCK B40 WITH FRAME TITLE TEXT-938.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(20) TEXT-940 FOR FIELD SO_VOLT MODIF ID GNR.
      SELECT-OPTIONS SO_VOLT FOR GV_VOLT MODIF ID GNR NO-EXTENSION.          " Voltage
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(20) TEXT-941 FOR FIELD SO_FREQ MODIF ID GNR.
      SELECT-OPTIONS SO_FREQ FOR GV_FREQ MODIF ID GNR NO-EXTENSION.            " Frequency
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(20) TEXT-942 FOR FIELD SO_POWR MODIF ID GNR.
      SELECT-OPTIONS SO_POWR FOR GV_POWR MODIF ID GNR NO-EXTENSION.            " Power
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(20) TEXT-943 FOR FIELD SO_AMPS MODIF ID GNR.
      SELECT-OPTIONS SO_AMPS FOR GV_AMPS MODIF ID GNR NO-EXTENSION.            " Power (AMPS)
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(17) TEXT-939 FOR FIELD P_GNSILE MODIF ID GNR.
      PARAMETER P_GNSILE(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID GNR. " Silenced/Open Frame
      SELECTION-SCREEN COMMENT 44(17) TEXT-935 FOR FIELD P_GNMOBI MODIF ID GNR.
      PARAMETER P_GNMOBI(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID GNR. " Mobile/Stationary
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(17) TEXT-937 FOR FIELD P_GNEXFU MODIF ID GNR.
      PARAMETER P_GNEXFU(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID GNR. " External Fuel Connection
      SELECTION-SCREEN COMMENT 44(17) TEXT-944 FOR FIELD P_GNSKID MODIF ID GNR.
      PARAMETER P_GNSKID(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID GNR. " Skid Fuel Tank
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(17) TEXT-945 FOR FIELD P_GNPARA MODIF ID GNR.
      PARAMETER P_GNPARA(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID GNR. " Parallel Operation
      SELECTION-SCREEN COMMENT 44(17) TEXT-946 FOR FIELD P_GNASTA MODIF ID GNR.
      PARAMETER P_GNASTA(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID GNR. " Auto Start
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(17) TEXT-947 FOR FIELD P_GNRSTA MODIF ID GNR.
      PARAMETER P_GNRSTA(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID GNR. " Remote Start
      SELECTION-SCREEN COMMENT 44(17) TEXT-948 FOR FIELD P_GNRMON MODIF ID GNR.
      PARAMETER P_GNRMON(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID GNR. " Remote Monitoring
    SELECTION-SCREEN END OF LINE.
    SELECTION-SCREEN BEGIN OF LINE.
      SELECTION-SCREEN COMMENT 01(17) TEXT-949 FOR FIELD P_GNPCON MODIF ID GNR.
      PARAMETER P_GNPCON(20) TYPE C AS LISTBOX VISIBLE LENGTH 20 MODIF ID GNR. " Power Connection
    SELECTION-SCREEN END OF LINE.
  SELECTION-SCREEN END OF BLOCK B40.
SELECTION-SCREEN END OF BLOCK B10.

*&---------------------------------------------------------------------*
*& INCLUDES -- Supporting Forms, Screen Logic ... ---------------------*
*&---------------------------------------------------------------------*
INCLUDE: YSE_REN_PLBRD_F01,    " Supporting Forms
         YSE_REN_PLBRD_F02,    " Supporting Forms Barchart Data Preparation
         YSE_REN_PLBRD_F03,    " Supporting Forms Barchart Processing Logic
         YSE_REN_PLBRD_F04,    " Supporting Forms List Output
         YSE_REN_PLBRD_SCR0300," Screen0300 processing (PBO, PAI)
         YSE_REN_PLBRD_SCR0301," Screen0301 processing (PBO, PAI).
         YSE_REN_PLBRD_SCR0200," Screen0200 processing (PBO, PAI)
         YSE_REN_PLBRD_SCR0201," Screen0201 processing (PBO, PAI)
         YSE_REN_PLBRD_SCR0202," Screen0202 processing (PBO, PAI)
         YSE_REN_PLBRD_SCR0203." Screen0203 processing (PBO, PAI)

*&---------------------------------------------------------------------*
*& INITIALIZATION -----------------------------------------------------*
*&---------------------------------------------------------------------*
INITIALIZATION.

* Enable the button "List output" by calling custom status
  SET PF-STATUS 'SELSCRN'.

* CHECK WHETHER PLANNING BOARD IS USED AS SEARCH HELP ***
* *************************************************** ***
  PERFORM CHECK_TRIGGER.

** READ PARAMETER-ID'S HOLDING THE VIEW MODE FOR TOP AND BOTTOM ***
** ************************************************************ ***
*  GET PARAMETER ID 'YSE_RPB_ST' FIELD GV_ST.
*  IF SY-SUBRC <> 0. GV_ST = GC_ST_MD. ENDIF.
*  GET PARAMETER ID 'YSE_RPB_SB' FIELD GV_SB.
*  IF SY-SUBRC <> 0. GV_SB = GC_SB_DO. ENDIF.

* READ THE MEMORY ID VALUES FOR STANDARD FIELDS ***
* ********************************************* ***
* The addition "MEMORY ID ..." leaves the parameter variable (e.g. P_VKORG) empty until user presses "Enter"
  GET PARAMETER ID 'VKO' FIELD P_VKORG.
  GET PARAMETER ID 'VTW' FIELD P_VTWEG.
  GET PARAMETER ID 'VKB' FIELD P_VKBUR.

* INITIALIZE THE TIME FRAME TO BE SHOWN ON PLANNING BOARD ***
* ******************************************************* ***
  PERFORM SELSCR_RESET.

* INITIALIZE THE DROP DOWN VALUES ***
* ******************************* ***
  PERFORM SELSCR_PREPARE_COMBO USING 'P_CPOILF' GC_CLS_COM_OILFRE. " Fill control P_CPOILFwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_CPDIEL' GC_CLS_COM_DIESEL. " Fill control P_CPDIELwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_CPMOBI' GC_CLS_COM_MOBILE. " Fill control P_CPMOBIwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_CPAFCL' GC_CLS_COM_AFTRCL. " Fill control P_CPAFCLwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_CPREFI' GC_CLS_COM_REFINE. " Fill control P_CPREFIwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_CPGENR' GC_CLS_COM_GENERT. " Fill control P_CPGENRwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_CPEXFU' GC_CLS_COM_EXFUEL. " Fill control P_CPEXFUwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_CPDRYR' GC_CLS_COM_DRYER.  " Fill control P_CPDRYRwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_GNSILE' GC_CLS_GEN_SILOFR. " Fill control P_GNSILEwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_GNMOBI' GC_CLS_GEN_MOBILE. " Fill control P_GNMOBIwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_GNEXFU' GC_CLS_GEN_EXFUEL. " Fill control P_GNEXFUwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_GNSKID' GC_CLS_GEN_SKIDFT. " Fill control P_GNSKIDwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_GNPARA' GC_CLS_GEN_PAROPR. " Fill control P_GNPARAwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_GNASTA' GC_CLS_GEN_ASTART. " Fill control P_GNASTAwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_GNRSTA' GC_CLS_GEN_RSTART. " Fill control P_GNRSTAwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_GNRMON' GC_CLS_GEN_RMONIT. " Fill control P_GNRMONwith char. values
  PERFORM SELSCR_PREPARE_COMBO USING 'P_GNPCON' GC_CLS_GEN_POWCON. " Fill control P_GNPCONwith char. values

* INITIALIZE THE BUTTONS ***
* ********************** ***
  MOVE 'Fetch'(801) TO PB_FETCH.
  MOVE 'Reset'(802) TO PB_RESET.

*&---------------------------------------------------------------------*
*& AT SELECTION SCREEN OUTPUT -----------------------------------------*
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.

  DATA: LV_CLCOMP   TYPE I,
        LV_CLGENR   TYPE I.

* Set flag for Compressor characteristics
  IF P_CLCOMP = GC_CHAR_X.
    LV_CLCOMP = 1.
  ELSE.
    LV_CLCOMP = 0.
  ENDIF.

* Set flag for Generator characteristics
  IF P_CLGENR = GC_CHAR_X.
    LV_CLGENR = 1.
  ELSE.
    LV_CLGENR = 0.
  ENDIF.

* Loop at screen and (de)activate blocks according to flags
  LOOP AT SCREEN.
    CASE SCREEN-GROUP1.
      WHEN GC_CLCOMP.
        SCREEN-ACTIVE = LV_CLCOMP.
      WHEN GC_CLGENR.
        SCREEN-ACTIVE = LV_CLGENR.
      WHEN GC_DIS.
        SCREEN-INPUT = 0.
      WHEN GC_POS.
        SCREEN-INPUT = 0.
      WHEN GC_SEH.
        IF NOT GV_SH IS INITIAL.
          SCREEN-INPUT = 0.
        ENDIF.
    ENDCASE.
    MODIFY SCREEN.
  ENDLOOP.

*&---------------------------------------------------------------------*
*& AT SELECTION SCREEN ON VALUE-REQUEST FOR P_POSNR -------------------*
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_POSNR.

  CALL SCREEN 200 STARTING AT 40 5 ENDING AT 135 17.

*&---------------------------------------------------------------------*
*& AT SELECTION SCREEN ON VALUE-REQUEST FOR P_VBELN -------------------*
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_VBELN.

  DATA: IT_RETURN TYPE DDSHRETVAL OCCURS 0 WITH HEADER LINE.

  CALL FUNCTION 'F4IF_FIELD_VALUE_REQUEST'
    EXPORTING
      TABNAME                   = 'VBAK'
      FIELDNAME                 = 'VBELN'
      SEARCHHELP                = 'VMVA'
    TABLES
      RETURN_TAB                = IT_RETURN
    EXCEPTIONS
      FIELD_NOT_FOUND
      NO_HELP_FOR_FIELD
      INCONSISTENT_HELP
      NO_VALUES_FOUND.

  IF SY-SUBRC = 0.
    READ TABLE IT_RETURN INDEX 1.
    P_VBELN = IT_RETURN-FIELDVAL.
  ENDIF.

*&---------------------------------------------------------------------*
*& AT SELECTION SCREEN ON VALUE-REQUEST FOR P_VKBUR -------------------*
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_VKBUR.

  DATA: IT_RETURN TYPE DDSHRETVAL OCCURS 0 WITH HEADER LINE,
        IT_DSELC  TYPE DSELC      OCCURS 0,
        WA_DSELC  TYPE DSELC.

* Pass the already chosen Sales org and distribution channel to the FM
  IF NOT P_VKORG IS INITIAL.
    WA_DSELC-FLDNAME   = 'VKORG'.
    WA_DSELC-FLDINH    = P_VKORG.
    WA_DSELC-DYFLDNAME = 'VKORG'.
    APPEND WA_DSELC TO IT_DSELC.
    IF NOT P_VTWEG IS INITIAL.
      WA_DSELC-FLDNAME   = 'VTWEG'.
      WA_DSELC-FLDINH    = P_VTWEG.
      WA_DSELC-DYFLDNAME = 'VTWEG'.
      APPEND WA_DSELC TO IT_DSELC.
    ENDIF. " NOT P_VTWEG IS INITIAL
  ENDIF. " NOT P_VKORG IS INITIAL

* Call drop down list
  CALL FUNCTION 'F4TOOL_CHECKTABLE_HELP'
    EXPORTING
      CHECKTABLE             = 'TVKBZ'
      RETFIELD               = 'VKBUR'
    TABLES
      DYNPFLD_MAPPING        = IT_DSELC
      RETURN_TAB             = IT_RETURN
   EXCEPTIONS
     TABL_NOT_EXISTS        = 1
     FIELD_NOT_EXISTS       = 2
     ILLEGAL_CALL           = 3
     OTHERS                 = 4.

  IF SY-SUBRC = 0.
    READ TABLE IT_RETURN WITH KEY FIELDNAME = 'VKBUR'.
    IF SY-SUBRC = 0.
      P_VKBUR = IT_RETURN-FIELDVAL.
    ENDIF.
  ENDIF.

*&---------------------------------------------------------------------*
*& AT SELECTION SCREEN ------------------------------------------------*
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.

  DATA: LV_AUART    TYPE AUART,
        LV_LIFSK    TYPE LIFSK,
        LV_LINES    TYPE I,
        LV_GET_VEDA TYPE C,
        BEGIN OF IT_VBFA OCCURS 0,
          VBELV     TYPE VBELV,
        END OF IT_VBFA,
        LV_VBELN    TYPE VBELN.

* Set authorization mode
  AUTHORITY-CHECK OBJECT 'V_VBAK_VKO'
           ID 'VKORG' FIELD P_VKORG
           ID 'VTWEG' FIELD P_VTWEG
           ID 'SPART' FIELD '01'
           ID 'ACTVT' FIELD '01'.    " Create/Generate
  IF SY-SUBRC EQ 4.                  " No full authorization
    CLEAR GV_AUTHF.
  ELSEIF SY-SUBRC NE 0.              " Auth check failed (tehnical err)
    MESSAGE E048 WITH SY-SUBRC.
  ELSE.                              " Full authorization
    GV_AUTHF = GC_CHAR_X.
  ENDIF.

  CASE SY-UCOMM.
* If radiobutton was clicked, reset doc number ***
* ******************************************** ***
    WHEN 'DOC'.
      IF NOT P_VBELN IS INITIAL.
        SELECT SINGLE AUART FROM VBAK INTO LV_AUART WHERE VBELN = P_VBELN.
        IF LV_AUART = 'ZQT' AND NOT P_DOCQUO IS INITIAL.
        ELSEIF LV_AUART = 'ZIN' AND NOT P_DOCENQ IS INITIAL.
        ELSE.
          PERFORM SELSCR_RESET.
        ENDIF.
      ENDIF.
    WHEN 'RESET'.
      PERFORM SELSCR_RESET.

* If fetch was clicked, fetch doc number ***
* ************************************** ***
    WHEN 'FETCH'.
*     Doc number that should be retrieved depends on which radiobutton was clicked
      IF     P_DOCENQ NE SPACE.
        GET PARAMETER ID 'AFN' FIELD LV_VBELN. " See include FV45KF0V_VBAK-VBELN_GET_PARAME
      ELSEIF P_DOCQUO NE SPACE.
        GET PARAMETER ID 'AGN' FIELD LV_VBELN. " See include FV45KF0V_VBAK-VBELN_GET_PARAME
      ENDIF.
*     Check if OK
      IF SY-SUBRC = 0.
        P_VBELN = LV_VBELN.
      ENDIF.

* If BACK was chosen, do a leave program - is required for when the planning board is used as search help ***
* ******************************************************************************************************* ***
    WHEN 'BACK'.
      LEAVE PROGRAM.
  ENDCASE.

* Reset flag which says whether VEDA should be re-read
  LV_GET_VEDA = SPACE.
* If P_VBELN has changed ***
* ********************** ***
  IF GV_VBELN_V NE P_VBELN.
*   Clear the old posnr to force a read/refresh of the position
    GV_POSNR_V = SPACE.

*   Empty the internal tables of the document items
    CLEAR: IT_VBAP, IT_VBAP[].

*   Determine the document type of the order number entered
    SELECT SINGLE AUART LIFSK
             INTO (LV_AUART, LV_LIFSK)
             FROM VBAK
            WHERE VBELN = P_VBELN.

*   Set correct radio button which indicates the document type
*   Also set the succeeding document type (for further logic)
    IF SY-SUBRC = 0.
      CASE LV_AUART.
        WHEN GC_AUART_ENQU.
          P_DOCQUO = SPACE.
          P_DOCENQ = GC_CHAR_X.
          GV_VBTYP_V = 'A'.   " Preceeding docuemnt in the flow is of type Enquiry
          GV_VBTYP_N = 'B'.   " Succeeding document in the flow is of type Quotation
        WHEN GC_AUART_QUOT.
          P_DOCENQ = SPACE.
          P_DOCQUO = GC_CHAR_X.
          GV_VBTYP_V = 'B'.   " Preceeding docuemnt in the flow is of type Quotation
          GV_VBTYP_N = 'C'.   " Succeeding document in the flow is of type Order (Contract)
        WHEN OTHERS.
          P_DOCQUO = SPACE.
          P_DOCENQ = SPACE.
          MESSAGE E007 WITH P_VBELN.
      ENDCASE.
    ELSE.
      CLEAR P_POSHI.
*      P_DOCQUO = SPACE.
*      P_DOCENQ = SPACE.
      IF NOT P_VBELN IS INITIAL.
        MESSAGE E006 WITH P_VBELN.
      ENDIF.
    ENDIF.

*   Check if there is already a subsequent document for the chosen doc. (warn the user)
    SELECT VBELV FROM VBFA
                 INTO TABLE IT_VBFA
                WHERE VBELV   = P_VBELN
                  AND VBTYP_V = GV_VBTYP_V
                  AND VBTYP_N = GV_VBTYP_N.
    IF SY-SUBRC = 0.
      MESSAGE W008 WITH P_VBELN.
    ENDIF.

*   When in quotation mode (creating contract), check delivery block of quot
    IF P_DOCQUO NE SPACE AND LV_LIFSK NE SPACE.
      MESSAGE W002(YSE_AUT_BLOCK) WITH P_VBELN LV_LIFSK.
    ENDIF.

*   Select the items from VBAP
    SELECT POSNR MATNR KWMENG VRKME GRPOS ARKTX KDMAT PSTYV ABGRU
             FROM VBAP
             INTO CORRESPONDING FIELDS OF TABLE IT_VBAP
            WHERE VBELN = P_VBELN.
*   Display the last item number
    DESCRIBE TABLE IT_VBAP LINES LV_LINES.
    IF LV_LINES > 0.
      READ TABLE IT_VBAP INDEX LV_LINES.
      IF SY-SUBRC = 0.
        P_POSHI = IT_VBAP-POSNR.
      ENDIF.
    ELSE.
      CLEAR P_POSHI.
    ENDIF.
*   Read the item chosen by user, if it fails, just read the first item
    READ TABLE IT_VBAP WITH KEY POSNR = P_POSNR.
    IF SY-SUBRC <> 0.
      READ TABLE IT_VBAP INDEX 1.
    ENDIF.
*   Set the chosen item number with what results from the above step
    P_POSNR = IT_VBAP-POSNR.
    PERFORM SELSCR_SET_ITEM_PROP.

*   Make sure start- and enddate are searched for again
    LV_GET_VEDA = 'X'.

*   Save the new VBELN as processed
    GV_VBELN_V = P_VBELN.
  ENDIF.

* If P_POSNR has changed
  IF GV_POSNR_V NE P_POSNR.

*   Read the item chosen by user, if it fails, just read the first item
    READ TABLE IT_VBAP WITH KEY POSNR = P_POSNR.
    IF SY-SUBRC <> 0.
      READ TABLE IT_VBAP INDEX 1.
    ENDIF.
*   Set the chosen item number with what results from the above step
    P_POSNR = IT_VBAP-POSNR.
*   Show the chosen item details
    PERFORM SELSCR_SET_ITEM_PROP.

*   Clear the characteristics from the selection screen
    PERFORM SELSCR_CLEAR_CHARS.

*   Stop here if item has Reason for rejection
    IF NOT IT_VBAP-ABGRU IS INITIAL.
      MESSAGE E055 WITH IT_VBAP-POSNR.
    ENDIF.

*   Make sure start- and enddate are searched for again
    LV_GET_VEDA = 'X'.

*   Save the position as processed
    GV_POSNR_V = P_POSNR.

  ENDIF.

* Find the start- and enddate for the item
  IF NOT LV_GET_VEDA IS INITIAL.
    SELECT SINGLE VBEGDAT VENDDAT
             INTO (P_BEGDAT, P_ENDDAT)
             FROM VEDA
            WHERE VBELN = P_VBELN
              AND VPOSN = P_POSNR.
    IF SY-SUBRC <> 0.
      SELECT SINGLE VBEGDAT VENDDAT
           INTO (P_BEGDAT, P_ENDDAT)
           FROM VEDA
          WHERE VBELN = P_VBELN
            AND VPOSN = SPACE.
    ENDIF.
  ENDIF.

* Update the description of the model chosen
  IF NOT SO_MODEL IS INITIAL.
    SELECT SINGLE MAKTX
           INTO P_MMAKTX
           FROM MAKT
          WHERE SPRAS = 'EN'
            AND MATNR = SO_MODEL-LOW.
    IF SY-SUBRC <> 0.
      CLEAR P_MMAKTX.
    ENDIF.
  ELSE.
    CLEAR P_MMAKTX.
  ENDIF.

* Check daterange
  IF P_BEGDAT > P_ENDDAT.
    MESSAGE E047.
  ENDIF.

* Check that depot has been maintained
  PERFORM LOAD_DEPT.

* Check authorization for updated selection screen values
  PERFORM CHECK_AUTH.         " Check authorization

  CASE SY-UCOMM.
* If List output was requested ***
* **************************** ***
    WHEN 'LSOUT'.
      PERFORM START-OF-SELECTION_LS.
  ENDCASE.

*&---------------------------------------------------------------------*
*& AT SELECTION SCREEN ON P_BEGDAT-------------------------------------*
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON P_BEGDAT.
  IF P_BEGDAT IS INITIAL.
    MESSAGE E056.
  ENDIF.
*&---------------------------------------------------------------------*
*& AT SELECTION SCREEN ON P_ENDDAT-------------------------------------*
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON P_ENDDAT.
  IF P_ENDDAT IS INITIAL.
    MESSAGE E056.
  ENDIF.

*&---------------------------------------------------------------------*
*& AT SELECTION SCREEN ON EXIT-COMMAND --------------------------------*
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON EXIT-COMMAND.

  DATA: LV_OKCODE TYPE SYUCOMM.

* When plbrd is used as search help, export "OK_CODE" (here it's UCOMM)
  IF GV_SH EQ GC_CHAR_X.
*   First convert the code to a more meaningfull code
    CASE SY-UCOMM.
      WHEN 'E'.
        LV_OKCODE = 'BACK'.
      WHEN 'ENDE'.
        LV_OKCODE = 'EXIT'.
      WHEN 'ECAN'.
        LV_OKCODE = 'QUIT'.
    ENDCASE.
    EXPORT OK_CODE FROM LV_OKCODE TO MEMORY ID GC_MID_OKCOD.
  ENDIF.

*&---------------------------------------------------------------------*
*& START OF SELECTION -------------------------------------------------*
*&---------------------------------------------------------------------*
START-OF-SELECTION.

  PERFORM SELSCR_SET_PARAM.   " Set the corresponding parameter ID for VBELN
  PERFORM CHECK_WIN32.        " Check that the client is on a win32 platform
  PERFORM CHECK_TRIGGER.      " Check whether planning board is triggered as search help or not
  PERFORM DELETE_SOURCE_DATA.
  PERFORM LOAD_DATA.          " Load the data (documents, models, ...)

  PERFORM CREATE_CHARTCONTROL." Prepare a barchart control
  PERFORM PREPARE_CHART_DATA. " Prepare the data to be passed to the barchart control

  CALL FUNCTION 'BARC_GRAPHIC_PBO'
    EXPORTING
      ABAP_CMD       = 'SEL'
      CONFIRM        = 'X'
      PROFILE        = GRAPH_PROFILE
      STAT           = STAT
      STATUS_TEXT    = STATUS_TEXT
      CONTROL_HANDLE = GV_BARC_OCX_HNDL  "F#R OCX-UMSTELLUNG
    TABLES
      BOXES          = BOXES
      BOX_VALS       = BOX_VALS
      DELETIONS      = DELETIONS
      LINKS          = LINKS
      LINK_VALS      = LINK_VALS
      NODES          = NODES
      NODE_VALS      = NODE_VALS
      POSITIONS      = POSITIONS
    EXCEPTIONS
      ERR_IN_PROFILE = 1
      INV_PROFILE    = 2
      INV_WINID      = 3.

* Start default with the whole period visible
  CALL FUNCTION 'BARCHART_SET_FUNCTION_CODE'
    EXPORTING
      HANDLE        = GV_BARC_OCX_HNDL
      FUNCTION_CODE = 'DZIO'.
*    IMPORTING
*      RETURN        = RETVAL.

  CALL SCREEN 300.

*&---------------------------------------------------------------------*
*& START OF SELECTION - LIST OUTPUT -----------------------------------*
*&---------------------------------------------------------------------*
FORM START-OF-SELECTION_LS .

  PERFORM SELSCR_SET_PARAM.   " Set the corresponding parameter ID for VBELN
  PERFORM CHECK_WIN32.        " Check that the client is on a win32 platform
  PERFORM CHECK_TRIGGER.      " Check whether planning board is triggered as search help or not
  PERFORM DELETE_SOURCE_DATA.
  PERFORM LOAD_DATA.          " Load the data (documents, models, ...)

  IF GV_ST = GC_ST_MD.
    PERFORM LS_CREATE_DYN_TAB_MD.
    PERFORM LS_FILL_TAB_MD.
  ELSE.
    PERFORM LS_CREATE_DYN_TAB_EQ.
    PERFORM LS_FILL_TAB_EQ.
  ENDIF.

  CALL SCREEN 301.

ENDFORM.                    " START-OF-SELECTION_LS

*Text symbol text£º
*001:Graphics Profile
*002:End of loading process
*003:Box/node duplicated
*004:Box already exists in the line
*005:Box inserted
*006:Node inserted
*007:Cannot change plant
*008:Fld content changed
*012:Layer inserted
*013:No free layer type available
*014:No free text index available
*016:Box
*025:No Object Selected
*026:Not possible to move boxes between charts
*201:Models
*203:Documents
*801:Fetch
*802:Reset
*803:Models
*804:Equipments
*901:Startdate
*902:Enddate
*903:   Available
*904:   Soft Reservation
*905:View Top
*906:View Bottom
*907::
*908:(Soft Reserved)
*909:(Available)
*910:Quantity
*911:A Quotation
*912:Quotation already exists
*913:already exists (possibly more than one), create new document?
*914:Compressor Characteristics
*915:Models
*916:Model
*917:Sales Office
*918:General
*920:Oil Free/Injected
*921:Diesel/Elect.
*922:Pressure (bar)
*923:Capacity (m#/min)
*924:Document
*925:Sales Org.
*926:Item
*927:Enquiry
*928:Quotation
*929:Aftercooler Built-In
*930:Generator Built-In
*931:Owning CC
*932:Current Location
*933:Current Status
*934:DCh
*935:Mobile/Stationary
*936:Refinery Equipment
*937:External Fuel Connection
*938:Generator Characteristics
*939:Silenced/Open Frame
*940:Voltage
*941:Frequency
*942:Power
*943:Power (AMPS)
*944:Skid Fuel Tank
*945:Parallel Operation
*946:Auto Start
*947:Remote Start
*948:Remote Monitoring
*949:Power Connection
*950:Valid-to date set to
*951:Dryer Built-In
*960:Last Item
*961:Failed to assign sernr
*962:to item
*963:Failed to save serial numbers for document
*964:Failed to check serial numbers for document
*965:Item & isn't mapped in rental basket, hence is not copied to the contract
