*----------------------------------------------------------------------*
***INCLUDE YSE_REN_PLBRD_F04 .
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  LS_CREATE_DYN_TAB_MD
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM LS_CREATE_DYN_TAB_MD.

  DATA: LS_FIELDCAT   TYPE LVC_S_FCAT.
  DATA: LV_DATUM      TYPE SY-DATUM,
        LV_DATUMC(10) TYPE C.

* Clear the Field Catalog
  CLEAR: GT_FIELDCAT, GT_FIELDCAT[].

* Set the number of key columns
  GV_LS_KEYCOLS = 4.

* Define first column: Model
  LS_FIELDCAT-FIELDNAME  = 'MODEL'.
  LS_FIELDCAT-REF_TABLE  = 'YSE_RPB_LSOUT'.
  LS_FIELDCAT-REF_FIELD  = 'ZMODEL'.
  LS_FIELDCAT-KEY        = 'X'.
  LS_FIELDCAT-COL_OPT    = 'X'.
  APPEND LS_FIELDCAT TO GT_FIELDCAT.

* Define first column: Model description
  LS_FIELDCAT-FIELDNAME  = 'MAKTX'.
  LS_FIELDCAT-REF_TABLE  = 'MAKT'.
  LS_FIELDCAT-REF_FIELD  = 'MAKTX'.
  LS_FIELDCAT-KEY        = 'X'.
  LS_FIELDCAT-COL_OPT    = 'X'.
  APPEND LS_FIELDCAT TO GT_FIELDCAT.

* Define second column: Soft Reservation / Available
  LS_FIELDCAT-FIELDNAME  = 'LEVEL'.
  LS_FIELDCAT-REF_TABLE  = 'YSE_RPB_LSOUT'.
  LS_FIELDCAT-REF_FIELD  = 'ZSRAV'.
  LS_FIELDCAT-KEY        = 'X'.
  LS_FIELDCAT-COL_OPT    = 'X'.
  APPEND LS_FIELDCAT TO GT_FIELDCAT.

* Define third column: Quantity
  LS_FIELDCAT-FIELDNAME  = 'QUANT'.
  LS_FIELDCAT-REF_TABLE  = 'YSE_RPB_LSOUT'.
  LS_FIELDCAT-REF_FIELD  = 'ZQUANT'.
  LS_FIELDCAT-KEY        = 'X'.
  LS_FIELDCAT-COL_OPT    = 'X'.
  APPEND LS_FIELDCAT TO GT_FIELDCAT.

* Determine the number of columns that we want to add
  GV_LS_COLUMNS = P_ENDDAT - P_BEGDAT + 1.

* Add the extra columns (one for each day in the range)
  MOVE P_BEGDAT TO LV_DATUM.
  DO GV_LS_COLUMNS TIMES.
    CLEAR LS_FIELDCAT.
    LS_FIELDCAT-FIELDNAME  = LV_DATUM.
    LS_FIELDCAT-INTTYPE    = 'C'.
    LS_FIELDCAT-INTLEN     = 8.
    LS_FIELDCAT-COLTEXT    = LV_DATUM.
    LS_FIELDCAT-OUTPUTLEN  = 8.
    LS_FIELDCAT-KEY        = ' '.
*   LS_FIELDCAT-COL_OPT    = 'X'.
    ADD 1 TO LV_DATUM.
    APPEND LS_FIELDCAT TO GT_FIELDCAT.
  ENDDO.

* If next method call dumps, check the generated code by putting a break point at
* LSKBHF06, form fb_table_create, line 547: "generate subroutine pool lt_source..."
  CALL METHOD CL_ALV_TABLE_CREATE=>CREATE_DYNAMIC_TABLE
    EXPORTING
      IT_FIELDCATALOG = GT_FIELDCAT
    IMPORTING
      EP_TABLE        = GP_TABLE.

  ASSIGN GP_TABLE->* TO <GT_TABLE>.

ENDFORM.                    " LS_CREATE_DYN_TAB_MD

*---------------------------------------------------------------------*
*       FORM LS_FILL_TAB_MD                                           *
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
FORM LS_FILL_TAB_MD.

* Local variables
  DATA:          LV_COUNTER(3) TYPE N,
                 LV_INDEX      LIKE SY-INDEX,
                 LV_TEXT(40)   TYPE C.
  FIELD-SYMBOLS: <LS_LINE_SR>,               " A working area for the number of Soft reservations
                 <LS_LINE_AV>,               " A working area for the number of available machines
                 <LS_FIELD>.

* Get 2 working areas which will be filled for every model-day
  ASSIGN LOCAL COPY OF INITIAL LINE OF <GT_TABLE> TO <LS_LINE_SR>.
  ASSIGN LOCAL COPY OF INITIAL LINE OF <GT_TABLE> TO <LS_LINE_AV>.

* Loop over the MATEQ table for the models
  LOOP AT IT_MATEQ WHERE LEVEL = 'MD'.
*   Copy the model to the MATNR field of the Soft Reservations line
    ASSIGN COMPONENT 1 OF STRUCTURE <LS_LINE_SR> TO <LS_FIELD>.
    <LS_FIELD> = IT_MATEQ-MATNR.
*   Copy the model to the MATNR field of the Available line
    ASSIGN COMPONENT 1 OF STRUCTURE <LS_LINE_AV> TO <LS_FIELD>.
    <LS_FIELD> = IT_MATEQ-MATNR.

*   Copy the model to the MAKTX field of the Soft Reservations line
    ASSIGN COMPONENT 2 OF STRUCTURE <LS_LINE_SR> TO <LS_FIELD>.
    <LS_FIELD> = IT_MATEQ-MAKTX.
*   Copy the model to the MAKTX field of the Available line
    ASSIGN COMPONENT 2 OF STRUCTURE <LS_LINE_AV> TO <LS_FIELD>.
    <LS_FIELD> = IT_MATEQ-MAKTX.

*   Indicate the line type for the soft reservations
    ASSIGN COMPONENT 3 OF STRUCTURE <LS_LINE_SR> TO <LS_FIELD>.
    <LS_FIELD> = 'SR'.
*   Indicate the line type for the availability
    ASSIGN COMPONENT 3 OF STRUCTURE <LS_LINE_AV> TO <LS_FIELD>.
    <LS_FIELD> = 'AV'.

*   Copy the quantity to the quant field of the Soft Reservations line
    ASSIGN COMPONENT 4 OF STRUCTURE <LS_LINE_SR> TO <LS_FIELD>.
    <LS_FIELD> = IT_MATEQ-QUANT.
*   Copy the quantity to the quant field of the Available line
    ASSIGN COMPONENT 4 OF STRUCTURE <LS_LINE_AV> TO <LS_FIELD>.
    <LS_FIELD> = IT_MATEQ-QUANT.

*   Reset the column index so that it seams the 3rd header column was written
    LV_INDEX = GV_LS_KEYCOLS.

*   Loop over the softreservation/availability lines for the model
    LOOP AT IT_AVAIL WHERE MATNR = IT_MATEQ-MATNR.
      LV_INDEX = LV_INDEX + 1.
*     Copy the number of soft reservations to the correct column of the WA for soft reservations
      ASSIGN COMPONENT LV_INDEX OF STRUCTURE <LS_LINE_SR> TO <LS_FIELD>.
      <LS_FIELD> = IT_AVAIL-SOFTR.
*     Copy the number of available machines to the correct column of the WA for availability
      ASSIGN COMPONENT LV_INDEX OF STRUCTURE <LS_LINE_AV> TO <LS_FIELD>.
      <LS_FIELD> = IT_AVAIL-AVAIL.

    ENDLOOP.
    APPEND <LS_LINE_AV> TO <GT_TABLE>.
    APPEND <LS_LINE_SR> TO <GT_TABLE>.
  ENDLOOP.  "   AT IT_MATEQ WHERE LEVEL = 'MD'.

ENDFORM.                    " LS_FILL_TAB_MD

*&---------------------------------------------------------------------*
*&      Form  LS_CREATE_DYN_TAB_EQ
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM LS_CREATE_DYN_TAB_EQ.

  DATA: LS_FIELDCAT   TYPE LVC_S_FCAT.
  DATA: LV_DATUM      TYPE SY-DATUM,
        LV_DATUMC(10) TYPE C.

* Clear the Field Catalog
  CLEAR: GT_FIELDCAT, GT_FIELDCAT[].

* Set the number of key columns
  GV_LS_KEYCOLS = 5.

* Define first column: Model
  LS_FIELDCAT-FIELDNAME  = 'MODEL'.
  LS_FIELDCAT-REF_TABLE  = 'YSE_RPB_LSOUT'.
  LS_FIELDCAT-REF_FIELD  = 'ZMODEL'.
  LS_FIELDCAT-KEY        = 'X'.
  LS_FIELDCAT-COL_OPT    = 'X'.
  APPEND LS_FIELDCAT TO GT_FIELDCAT.

* Define first column: Model description
  LS_FIELDCAT-FIELDNAME  = 'MAKTX'.
  LS_FIELDCAT-REF_TABLE  = 'MAKT'.
  LS_FIELDCAT-REF_FIELD  = 'MAKTX'.
  LS_FIELDCAT-KEY        = 'X'.
  LS_FIELDCAT-COL_OPT    = 'X'.
  APPEND LS_FIELDCAT TO GT_FIELDCAT.

* Define second column: Material
  LS_FIELDCAT-FIELDNAME  = 'MATNR'.
  LS_FIELDCAT-REF_TABLE  = 'MARA'.
  LS_FIELDCAT-REF_FIELD  = 'MATNR'.
  LS_FIELDCAT-KEY        = 'X'.
  LS_FIELDCAT-COL_OPT    = 'X'.
  APPEND LS_FIELDCAT TO GT_FIELDCAT.

* Define third column: Serial Number
  LS_FIELDCAT-FIELDNAME  = 'SERNR'.
  LS_FIELDCAT-REF_TABLE  = 'YSE_RPB_LSOUT'.
  LS_FIELDCAT-REF_FIELD  = 'ZSERNR'.
  LS_FIELDCAT-KEY        = 'X'.
  LS_FIELDCAT-COL_OPT    = 'X'.
  APPEND LS_FIELDCAT TO GT_FIELDCAT.

* Define foruth column: Current Status
  LS_FIELDCAT-FIELDNAME  = 'STATUS'.
  LS_FIELDCAT-REF_TABLE  = 'YSE_RPB_LSOUT'.
  LS_FIELDCAT-REF_FIELD  = 'ZSTAT'.
  LS_FIELDCAT-KEY        = 'X'.
  LS_FIELDCAT-COL_OPT    = 'X'.
  APPEND LS_FIELDCAT TO GT_FIELDCAT.

* Determine the number of columns that we want to add
  GV_LS_COLUMNS = P_ENDDAT - P_BEGDAT + 1.

* Add the extra columns (one for each day in the range)
  MOVE P_BEGDAT TO LV_DATUM.
  DO GV_LS_COLUMNS TIMES.
    CLEAR LS_FIELDCAT.
    LS_FIELDCAT-FIELDNAME  = LV_DATUM.
    LS_FIELDCAT-INTTYPE    = 'C'.
    LS_FIELDCAT-INTLEN     = 2.
    LS_FIELDCAT-COLTEXT    = LV_DATUM.
    LS_FIELDCAT-OUTPUTLEN  = 8.
    LS_FIELDCAT-KEY        = ' '.
*   LS_FIELDCAT-COL_OPT    = 'X'.
    ADD 1 TO LV_DATUM.
    APPEND LS_FIELDCAT TO GT_FIELDCAT.
  ENDDO.

* If next method call dumps, check the generated code by putting a break point at
* LSKBHF06, form fb_table_create, line 547: "generate subroutine pool lt_source..."
  CALL METHOD CL_ALV_TABLE_CREATE=>CREATE_DYNAMIC_TABLE
    EXPORTING
      IT_FIELDCATALOG = GT_FIELDCAT
    IMPORTING
      EP_TABLE        = GP_TABLE.

  ASSIGN GP_TABLE->* TO <GT_TABLE>.

ENDFORM.                    " LS_CREATE_DYN_TAB_EQ

*---------------------------------------------------------------------*
*       FORM LS_FILL_TAB_EQ
*---------------------------------------------------------------------*
*       ........                                                      *
*---------------------------------------------------------------------*
FORM LS_FILL_TAB_EQ.

* Local variables
  DATA:          LV_COUNTER(3) TYPE N,
                 LV_OFFSET     LIKE SY-INDEX,
                 LV_INDEX      LIKE SY-INDEX,
                 LV_DAYS       TYPE I,
                 LV_TEXT(40)   TYPE C.
  FIELD-SYMBOLS: <LS_LINE>,               " A working area
                 <LS_FIELD>.

* Get working area which will be filled for every equipment
  ASSIGN LOCAL COPY OF INITIAL LINE OF <GT_TABLE> TO <LS_LINE>.

* Loop over the MATEQ table for the equipments
  LOOP AT IT_MATEQ WHERE LEVEL = 'EQ'.

    CLEAR <LS_LINE>.

*   Find corresponding model
    READ TABLE IT_MATNR WITH KEY MATNR = IT_MATEQ-MATNR.
*   If model found
    IF SY-SUBRC EQ 0.
*     Copy the Model to the MODEL field
      ASSIGN COMPONENT 1 OF STRUCTURE <LS_LINE> TO <LS_FIELD>.
      <LS_FIELD> = IT_MATNR-MODEL.

*     Find the models description
      READ TABLE IT_MODEL WITH KEY MODEL = IT_MATNR-MODEL.
*     If model description found
      IF SY-SUBRC EQ 0.
*       Copy the model description to the MAKTX field
        ASSIGN COMPONENT 2 OF STRUCTURE <LS_LINE> TO <LS_FIELD>.
        <LS_FIELD> = IT_MODEL-MAKTX.
*     If model description not found
      ELSE.
*       Clear the MAKTX field
        ASSIGN COMPONENT 2 OF STRUCTURE <LS_LINE> TO <LS_FIELD>.
        <LS_FIELD> = SPACE.
      ENDIF.
*   If not model found
    ELSE.
*     Clear the MODEL field
      ASSIGN COMPONENT 1 OF STRUCTURE <LS_LINE> TO <LS_FIELD>.
      <LS_FIELD> = SPACE.
    ENDIF.

*   Copy the MATNR to the MATNR field
    ASSIGN COMPONENT 3 OF STRUCTURE <LS_LINE> TO <LS_FIELD>.
    <LS_FIELD> = IT_MATEQ-MATNR.

*   Copy the equipment number
    ASSIGN COMPONENT 4 OF STRUCTURE <LS_LINE> TO <LS_FIELD>.
    <LS_FIELD> = IT_MATEQ-SERNR.

*   Copy the current status
    ASSIGN COMPONENT 5 OF STRUCTURE <LS_LINE> TO <LS_FIELD>.
    <LS_FIELD> = IT_MATEQ-STAT.

*   Loop over the softreservation/availability lines for the model
    LOOP AT IT_AVAIL WHERE EQUNR = IT_MATEQ-EQUNR.

*     Determine the offset and duration (number of days)
      IF IT_AVAIL-BEGDAT > P_BEGDAT.
        LV_OFFSET = IT_AVAIL-BEGDAT - P_BEGDAT.
        IF IT_AVAIL-ENDDAT < P_ENDDAT.
          LV_DAYS = IT_AVAIL-ENDDAT - IT_AVAIL-BEGDAT + 1.
        ELSE.
          LV_DAYS = P_ENDDAT - IT_AVAIL-BEGDAT + 1.
        ENDIF.
      ELSE.
        LV_OFFSET = 0.
        IF IT_AVAIL-ENDDAT < P_ENDDAT.
          LV_DAYS = IT_AVAIL-ENDDAT - P_BEGDAT + 1.
        ELSE.
          LV_DAYS = P_ENDDAT - P_BEGDAT + 1.
        ENDIF.
      ENDIF.

*     Determine column index
      LV_INDEX = GV_LS_KEYCOLS + LV_OFFSET.

      DO LV_DAYS TIMES.
        LV_INDEX = LV_INDEX + 1.
*       Copy the status
        ASSIGN COMPONENT LV_INDEX OF STRUCTURE <LS_LINE> TO <LS_FIELD>.
        <LS_FIELD> = IT_AVAIL-STAT.
      ENDDO.

    ENDLOOP.
    APPEND <LS_LINE> TO <GT_TABLE>.
  ENDLOOP.  "   AT IT_MATEQ WHERE LEVEL = 'EQ'.

ENDFORM.                    " LS_FILL_TAB_EQ
