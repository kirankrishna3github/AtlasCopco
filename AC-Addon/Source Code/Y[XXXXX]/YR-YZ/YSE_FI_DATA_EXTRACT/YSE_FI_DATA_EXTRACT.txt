*&---------------------------------------------------------------------*
*& Report  YSE_DATA_EXTRACT                                            *
* AUTHOR        : Anda Wu                                              *
* DATE          : 17/10/2013                                           *
* DEVELOPMENT ID:                                                      *
*                                                                      *
* CHANGE REQUEST NUMBER:                                               *
*                                                                      *
* Program Description:          Extract data related to SAS 99         *
*&---------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
*  DATE    | NAME     |TRANSPORT  | CR# and short Description          *
*----------------------------------------------------------------------*
*17/10/2013|Anda Wu   |CD1K977879 |CRxxxx: Initial version             *
*----------------------------------------------------------------------*

REPORT  yse_fi_data_extract.

************************************************************************
*
*                   SAS 99 Data Extract Program
*   Purpose:  Extract data related to SAS 99 analysis from tables SKAT,
*             BSEG AND BKPF
*
*   How to Use ?
*   This program is dealing with very large tables BSEG and BKPF .
*   If it is run online with selection of more than 3 to 4 month data
*   will be taking much time in execution .It may time out
*        So it is advisable to run in  background . If it is run in
*   background system will not be able to  download the file
*   on presentation server.
*
*   This program has 3 options.
*   1. EXPORT: option will fatch the data from the database tables and
*       put them into database memory . if you are running the program
*       in backgaround select this option .
*   2. IMPORT : After running the program in background run the program
*       online with this selection . system will read data from database
*       memory and down load to presentation server
*   3. ONLINE : This option will fatch data from the table and download
*      to presenation server online . If you want to fatch data for
*      less than three months use this option .
*
*   How can I run this program efficiently?
*       let's say if you want to down load data for one year .
*       run program in background for 3 month each and export radio
*       button selected . Use CPU Date for the 3 month selection .
*       After job completed run program online with IMPORT radio button
*       selected and download each table in different file.
*       Performance of the program will be very good if it is run for
*       one company code and one fiscal year .
************************************************************************


************************************************************************
*
*                  DATA DEFINITIONS
*
************************************************************************
TABLES:

   skat,
   bkpf,
   bseg.

TABLES indx.

DATA:

   BEGIN OF tempbseg OCCURS 0,
     mandt LIKE bseg-mandt ,
     bukrs LIKE bseg-bukrs ,
     belnr LIKE bseg-belnr,
     gjahr LIKE bseg-gjahr,
     buzei LIKE bseg-buzei,
     shkzg LIKE bseg-shkzg,
     dmbtr LIKE bseg-dmbtr,
     sgtxt LIKE bseg-sgtxt,
     kostl LIKE bseg-kostl,
     hkont LIKE bseg-hkont,
   END OF tempbseg.

DATA:
    BEGIN OF tempskat OCCURS 0,
      spras LIKE skat-spras,
      ktopl LIKE skat-ktopl,
      saknr LIKE skat-saknr,
      txt50 LIKE skat-txt50,
    END OF tempskat.

DATA:
    BEGIN OF tempbkpf OCCURS 0,
        mandt LIKE bkpf-mandt,
        bukrs LIKE bkpf-bukrs,
        belnr LIKE bkpf-belnr,
        gjahr LIKE bkpf-gjahr,
        blart LIKE bkpf-blart,
        bldat LIKE bkpf-bldat,
        budat LIKE bkpf-budat,
        monat LIKE bkpf-monat,
        usnam LIKE bkpf-usnam,
        bktxt LIKE bkpf-bktxt,
        waers LIKE bkpf-waers,
        kursf LIKE bkpf-kursf,
   END OF tempbkpf.

DATA:
    BEGIN OF join OCCURS 0,
        mandt LIKE bkpf-mandt,
        bukrs LIKE bkpf-bukrs,
        belnr LIKE bkpf-belnr,
        gjahr LIKE bkpf-gjahr,
        blart LIKE bkpf-blart,
        bldat LIKE bkpf-bldat,
        budat LIKE bkpf-budat,
        monat LIKE bkpf-monat,
        usnam LIKE bkpf-usnam,
        bktxt LIKE bkpf-bktxt,
        waers LIKE bkpf-waers,
        kursf LIKE bkpf-kursf,
        buzei LIKE bseg-buzei,
        shkzg LIKE bseg-shkzg,
        dmbtr LIKE bseg-dmbtr,
        sgtxt LIKE bseg-sgtxt,
        kostl LIKE bseg-kostl,
        hkont LIKE bseg-hkont,
END OF join.

*local fields .

DATA  i_index LIKE sy-tabix .
DATA  l_bkpf TYPE i .

DATA: skatkey LIKE indx-srtfd VALUE 'TEMPSKAT'.
DATA: bkpfkey LIKE indx-srtfd VALUE 'TEMPBKPF'.
DATA: bsegkey LIKE indx-srtfd VALUE 'TEMPBSEG'.
DATA: joinkey LIKE indx-srtfd VALUE 'JOIN'.

************************************************************************
*
*                SELECTION SCREEN
*
************************************************************************

SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.
SELECT-OPTIONS client FOR bseg-mandt DEFAULT sy-mandt OBLIGATORY.
SELECT-OPTIONS co_code FOR bseg-bukrs OBLIGATORY.
SELECT-OPTIONS date FOR bkpf-bldat.
SELECT-OPTIONS lang FOR skat-spras DEFAULT sy-langu.
SELECT-OPTIONS ch_accts FOR skat-ktopl.
SELECT-OPTIONS doc_type FOR bkpf-blart.
SELECT-OPTIONS user FOR bkpf-usnam.
SELECT-OPTIONS post_dt FOR bkpf-budat.
SELECT-OPTIONS doc_no FOR bkpf-belnr.
SELECT-OPTIONS fisc_yr FOR bkpf-gjahr OBLIGATORY.
SELECT-OPTIONS cpu_dt FOR bkpf-cpudt .
SELECTION-SCREEN END OF BLOCK b1 .

SELECTION-SCREEN SKIP 2.

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-t02.
*Select data from tables and export to database memory
PARAMETERS : export RADIOBUTTON GROUP rad1 .
* Import priviously selected from  database memory
PARAMETERS : import RADIOBUTTON GROUP rad1 .
*Select data and download to excel
PARAMETERS : online RADIOBUTTON GROUP rad1 .

SELECTION-SCREEN END OF BLOCK b2 .

SELECTION-SCREEN BEGIN OF BLOCK b3 WITH FRAME TITLE text-t03.

PARAMETERS : skatfile LIKE  rlgrap-filename
                           DEFAULT 'c:\temp\tempskat.dat'.
PARAMETERS : bkpffile LIKE  rlgrap-filename
                           DEFAULT 'c:\temp\tempbkpf.dat'.
PARAMETERS : bsegfile LIKE  rlgrap-filename
                           DEFAULT 'c:\temp\tempbseg.dat'.
PARAMETERS : joinfile LIKE  rlgrap-filename
                           DEFAULT 'c:\temp\tempjoin.dat'.

SELECTION-SCREEN END OF BLOCK b3 .

SELECTION-SCREEN BEGIN OF BLOCK b4 WITH FRAME TITLE text-t04.
*display Join data on sreen ? if this check box selected data from
*internal table will be displayed on the screen
PARAMETER display AS CHECKBOX .
SELECTION-SCREEN END OF BLOCK b4 .
************************************************************************
*                    DATA SELECTION
*
************************************************************************
START-OF-SELECTION .
  IF online = 'X' .
    IF sy-batch = 'X' .
      MESSAGE e999(fh) WITH 'With ONLINE option'(005) 'you cannot this run program'(010)
                            'in background'(015) .
    ENDIF.
  ENDIF.

  IF export = 'X' OR online = 'X' .
*selection from skat .
    PERFORM select_skat .

*selection from bkpf .
    PERFORM select_bkpf .

*check whether data was populated in table bkpf or not .
    DESCRIBE TABLE tempbkpf LINES l_bkpf .

    IF l_bkpf NE 0 .
*selection from bseg .
      PERFORM select_bseg .
*populate internal table join with bkpf and bseg data.
      PERFORM poulate_join .
    ENDIF.
  ENDIF.
  IF export = 'X' .
*export internal tables to data base memory
    PERFORM export_data .
  ELSEIF import = 'X' .
*import internal tables to data base memory
    PERFORM import_data .
  ELSEIF online = 'X' .
*run online and download to excel file .
    PERFORM online_data .

  ENDIF.
*write data on the screen .

  IF display = 'X' .
    PERFORM write_data.
  ENDIF.
************************************************************************
*
*                  SUBROUTINES
*
************************************************************************
*&---------------------------------------------------------------------*
*&      Form  SELECT_SKAT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM select_skat.
************************************************************************
*
*SELECT SUBSET OF DATA FROM TABLE SPRAS FOR DOWNLOAD AND OUTSIDE REVIEW
*
************************************************************************

  SELECT spras ktopl saknr txt50 FROM skat INTO CORRESPONDING FIELDS OF
  TABLE tempskat
      WHERE spras IN lang
      AND ktopl IN ch_accts.


ENDFORM.                    " SELECT_SKAT

*&---------------------------------------------------------------------*
*&      Form  SELECT_BKPF
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM select_bkpf.

************************************************************************
*
* SELECT SUBSET OF DATA FROM TABLE BKPF FOR DOWNLOAD AND OUTSIDE REVIEW
*
************************************************************************

  SELECT mandt bukrs belnr gjahr blart bldat budat monat usnam bktxt
   waers kursf
 FROM bkpf CLIENT SPECIFIED INTO CORRESPONDING FIELDS OF TABLE tempbkpf
   WHERE mandt IN client
   AND blart IN doc_type
   AND bldat IN date
   AND bukrs IN co_code
   AND gjahr IN fisc_yr
   AND belnr IN doc_no
   AND budat IN post_dt
   AND cpudt IN cpu_dt
   AND usnam IN user .

* sort table
  SORT tempbkpf BY mandt bukrs belnr gjahr .

ENDFORM.                    " SELECT_BKPF

*&---------------------------------------------------------------------*
*&      Form  SELECT_BSEG
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM select_bseg.
************************************************************************
*
*  SELECT SUBSET OF DATA FROM TABLE BSEG FOR DOWNLOAD AND OUTSIDE REVIEW
*
************************************************************************

  SELECT mandt buzei shkzg dmbtr sgtxt kostl hkont belnr bukrs gjahr
          FROM bseg
   CLIENT SPECIFIED INTO CORRESPONDING FIELDS OF TABLE tempbseg
   FOR ALL ENTRIES IN tempbkpf

   WHERE mandt IN client
    AND bukrs = tempbkpf-bukrs
    AND gjahr = tempbkpf-gjahr
    AND belnr = tempbkpf-belnr .

  SORT tempbseg .

ENDFORM.                    " SELECT_BSEG

*&---------------------------------------------------------------------*
*&      Form  POULATE_JOIN
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM poulate_join.
  LOOP AT tempbseg .


    READ TABLE tempbkpf WITH KEY mandt = tempbseg-mandt
                                 bukrs = tempbseg-bukrs
                                 belnr = tempbseg-belnr
                                 gjahr = tempbseg-gjahr BINARY SEARCH .
    IF sy-subrc = 0 .

      MOVE-CORRESPONDING tempbkpf TO join.

      join-buzei = tempbseg-buzei.
      join-shkzg = tempbseg-shkzg.
      join-dmbtr = tempbseg-dmbtr.
      join-sgtxt = tempbseg-sgtxt.
      join-kostl = tempbseg-kostl.
      join-hkont = tempbseg-hkont.

      APPEND join .
    ENDIF.                                                              .
  ENDLOOP.

ENDFORM.                    " POULATE_JOIN

*&---------------------------------------------------------------------*
*&      Form  WRITE_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM write_data.
************************************************************************
*
*     WRITE CONTENTS OF TABLE JOIN TO THE SCREEN
*
************************************************************************

  WRITE: 'Join Results'(006).

  LOOP AT join.
    WRITE: / join-mandt, join-bukrs, join-belnr,
             join-dmbtr CURRENCY join-waers,
             join-blart, join-bldat,
             join-budat, join-monat, join-usnam,
             join-bktxt, join-waers, join-kursf,
             join-buzei, join-shkzg, join-gjahr,
             join-sgtxt, join-kostl, join-hkont.
  ENDLOOP.


ENDFORM.                    " WRITE_DATA

*&---------------------------------------------------------------------*
*&      Form  EXPORT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM export_data.
*send internal tables to the database memory .

  EXPORT tempskat TO  DATABASE indx(st) ID skatkey.
  IF sy-subrc = 0 .
    WRITE :/ 'SKAT data sucessfully exported'(016) .
  ENDIF.

  EXPORT tempbkpf TO  DATABASE indx(st) ID bkpfkey.
  IF sy-subrc = 0 .
    WRITE :/ 'BKPF data sucessfully exported'(026) .
  ENDIF.
  EXPORT tempbseg TO  DATABASE indx(st) ID bsegkey.
  IF sy-subrc = 0 .
    WRITE :/ 'BSEG data sucessfully exported'(036) .
  ENDIF.
  EXPORT join TO  DATABASE indx(st) ID joinkey.
  IF sy-subrc = 0 .
    WRITE :/ 'JOIN data sucessfully exported'(046) .
  ENDIF.



ENDFORM.                    " EXPORT_DATA

*&---------------------------------------------------------------------*
*&      Form  ONLINE_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM online_data.
  PERFORM skat_excel_download .
  PERFORM bkpf_excel_download .
  PERFORM bseg_excel_download .
  PERFORM join_excel_download .

ENDFORM.                    " ONLINE_DATA

*&---------------------------------------------------------------------*
*&      Form  IMPORT_DATA
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM import_data.
*import tables from the database  and down load to excel .

  IMPORT  tempskat FROM  DATABASE indx(st) ID skatkey.
  IF sy-subrc = 0 .
    DELETE FROM DATABASE indx(st) ID skatkey.
  ENDIF.

  IMPORT  tempbkpf FROM  DATABASE indx(st) ID bkpfkey.
  IF sy-subrc = 0 .
    DELETE FROM DATABASE indx(st) ID bkpfkey.
  ENDIF.

  IMPORT  tempbseg FROM  DATABASE indx(st) ID bsegkey.
  IF sy-subrc = 0 .
    DELETE FROM DATABASE indx(st) ID bsegkey.
  ENDIF.

  IMPORT  join FROM  DATABASE indx(st) ID joinkey.
  IF sy-subrc = 0 .
    DELETE FROM DATABASE indx(st) ID joinkey.
  ENDIF.

  PERFORM skat_excel_download .
  PERFORM bkpf_excel_download .
  PERFORM bseg_excel_download .
  PERFORM join_excel_download .





ENDFORM.                    " IMPORT_DATA
*&---------------------------------------------------------------------*
*&      Form  SKAT_EXCEL_DOWNLOAD
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_ENDFORM  text
*----------------------------------------------------------------------*
FORM skat_excel_download  .
***********************************
**************************************
*
*  DOWNLOAD RECORDS FROM INTERNAL TABLES INTO A FLAT FILE.
*
***********************************
**************************************

  CALL FUNCTION 'WS_DOWNLOAD'
    EXPORTING
      filename            = skatfile
      filetype            = 'DAT'
    TABLES
      data_tab            = tempskat
    EXCEPTIONS
      file_open_error     = 1
      file_write_error    = 2
      invalid_filesize    = 3
      invalid_table_width = 4
      invalid_type        = 5
      no_batch            = 6
      unknown_error       = 7
      OTHERS              = 8.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
**********************************************


ENDFORM.                    " SKAT_EXCEL_DOWNLOAD

*&---------------------------------------------------------------------*
*&      Form  BKPF_EXCEL_DOWNLOAD
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM bkpf_excel_download.
*********************************************
  CALL FUNCTION 'WS_DOWNLOAD'
    EXPORTING
      filename            = bkpffile
      filetype            = 'DAT'
    TABLES
      data_tab            = tempbkpf
    EXCEPTIONS
      file_open_error     = 1
      file_write_error    = 2
      invalid_filesize    = 3
      invalid_table_width = 4
      invalid_type        = 5
      no_batch            = 6
      unknown_error       = 7
      OTHERS              = 8.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

***********************************************************************

ENDFORM.                    " BKPF_EXCEL_DOWNLOAD

*&---------------------------------------------------------------------*
*&      Form  BSEG_EXCEL_DOWNLOAD
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM bseg_excel_download.
********************************************
  CALL FUNCTION 'WS_DOWNLOAD'
    EXPORTING
      filename            = bsegfile
      filetype            = 'DAT'
    TABLES
      data_tab            = tempbseg
    EXCEPTIONS
      file_open_error     = 1
      file_write_error    = 2
      invalid_filesize    = 3
      invalid_table_width = 4
      invalid_type        = 5
      no_batch            = 6
      unknown_error       = 7
      OTHERS              = 8.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
***********************************************************************

ENDFORM.                    " BSEG_EXCEL_DOWNLOAD

*&---------------------------------------------------------------------*
*&      Form  JOIN_EXCEL_DOWNLOAD
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM join_excel_download.
*********************************************

  CALL FUNCTION 'WS_DOWNLOAD'
    EXPORTING
      filename            = joinfile
      filetype            = 'DAT'
    TABLES
      data_tab            = join
    EXCEPTIONS
      file_open_error     = 1
      file_write_error    = 2
      invalid_filesize    = 3
      invalid_table_width = 4
      invalid_type        = 5
      no_batch            = 6
      unknown_error       = 7
      OTHERS              = 8.

  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.
ENDFORM.                    " JOIN_EXCEL_DOWNLOAD
************************************************************************
*
*                   END PROGRAM
*

************************************************************************
*Text symbol text£º
*005:With ONLINE option
*006:Join Results
*010:you cannot this run program
*015:in background
*016:SKAT data sucessfully exported
*026:BKPF data sucessfully exported
*036:BSEG data sucessfully exported
*046:JOIN data sucessfully exported
*T01:Selection Condition
*T02:Functoins
*T03:Filename

*T04:Flag
*Selection text£º
*BKPFFILE:D       .
*BSEGFILE:D       .
*CH_ACCTS:D       .
*CLIENT:D       .
*CO_CODE:D       .
*CPU_DT:D       .
*DATE:D       .
*DOC_NO:D       .
*DOC_TYPE:D       .
*FISC_YR:D       .
*JOINFILE:D       .
*LANG:D       .
*POST_DT:D       .
*SKATFILE:D       .
*USER:D       .
