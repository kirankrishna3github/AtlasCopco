*&---------------------------------------------------------------------*
*& Report  YSE_CHANGE_ORDERS_COPA
*&
*&---------------------------------------------------------------------*
*&
*& Change orders / contracts to rectify COPA (sign)
*&
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 05.11.2010
*  Change Request Number : CR1393
*  Transport request Nr. : CD1K960802
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. |   DATE     | NAME               | CORRECT. NR| CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 | 01.01.9999 |                    | CD1K9..... | CR....      *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_change_orders_copa.

TABLES: vbak,
        ce11000.

* ALV grid
TYPE-POOLS: slis.

* Internal table for document category descriptions
DATA: BEGIN OF it_typd OCCURS 0,
        vbtyp    TYPE vbtyp,
        dtext    TYPE val_text,
      END OF it_typd.

* Internal table for Sales Order Items (from input file)
DATA: BEGIN OF it_inso OCCURS 0,
        vbeln    TYPE vbeln_va,
        posnr    TYPE posnr_va,
      END OF it_inso.

* Internal table (for Sales Order update)
DATA: BEGIN OF it_vbak OCCURS 0,
        vbeln    TYPE vbeln_va,
        vbtyp    TYPE vbtyp,
        trvog    TYPE trvog,
        vsnmr_v  TYPE vsnmr_v,
      END OF it_vbak.

* Internal table (for Error output)
DATA: BEGIN OF it_err OCCURS 0,
        vbeln      TYPE vbeln_va,
        typtx(20)  TYPE c,
        mtext      TYPE string,
        trvog      TYPE trvog,
      END OF it_err.
DATA: ws_err  LIKE LINE OF it_err.

DATA:bdcdata     LIKE bdcdata  OCCURS 0
                              WITH HEADER LINE.

DATA: xv_variant         LIKE disvariant,
      xv_variant_flag    TYPE c,
      xv_sd_alv_variant  LIKE disvariant,
      xt_fcat            TYPE slis_t_fieldcat_alv,
      ls_fcat            LIKE LINE OF xt_fcat,
      xt_alv_sort        TYPE slis_t_sortinfo_alv,
      xv_user_command    TYPE slis_formname  VALUE 'USER_COMMAND',
      xv_variant_save    TYPE c              VALUE 'U',
      xv_layout          TYPE slis_layout_alv.

DATA: filename     TYPE string,
      gv_mode      TYPE ctu_mode  VALUE 'N',
      gv_mtext     TYPE string,
      gv_tcode     TYPE sytcode,
      gv_belnr     TYPE rke_belnr,
      gv_col(3)    TYPE n,
      gv_repid     LIKE sy-repid,
      bdc_options  TYPE ctu_params,
      it_messages  TYPE TABLE OF bdcmsgcoll,
      wa_messages  LIKE bdcmsgcoll.

*&---------------------------------------------------------------------*
*&  Selection screen                                                   *
*&---------------------------------------------------------------------*
* Input file
PARAMETERS: p_infile(132) LOWER CASE.
*                 DEFAULT 'C:\COPA.REPAIR.txt'.


AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_infile.

* Get the filename
  PERFORM get_filename USING p_infile.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

* Fill internal table for document category descriptions
  SELECT domvalue_l ddtext INTO TABLE it_typd
         FROM dd07t
         WHERE domname    = 'VBTYP'
           AND ddlanguage = 'E'.

* Read input file
  PERFORM read_file.

* Process input
  PERFORM process_data.

* Display errors
  IF it_err[] IS INITIAL.
    MESSAGE i001(00) WITH 'No errors found'(i04).
  ELSE.
    PERFORM alv_fcat.
    PERFORM alv_layout.
    PERFORM alv_display.
  ENDIF.

*&---------------------------------------------------------------------*
*&  Forms                                                              *
*&---------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      Form  GET_FILENAME
*&---------------------------------------------------------------------*
*       Get the filename
*----------------------------------------------------------------------*
*      -->P_INFILE : Input file
*----------------------------------------------------------------------*
FORM get_filename  USING    p_infile.

  CALL FUNCTION 'WS_FILENAME_GET'
    EXPORTING
      def_path         = 'C:\'
      mask             = ',*.TXT.'
    IMPORTING
      filename         = p_infile
    EXCEPTIONS
      inv_winsys       = 1
      no_batch         = 2
      selection_cancel = 3
      selection_error  = 4
      OTHERS           = 5.

  IF sy-subrc <> 0.
  ENDIF.

ENDFORM.                    " GET_FILENAME

*&---------------------------------------------------------------------*
*&      Form  READ_FILE
*&---------------------------------------------------------------------*
*       Read input file
*----------------------------------------------------------------------*
FORM read_file .

  filename = p_infile.

  CALL FUNCTION 'GUI_UPLOAD'
    EXPORTING
      filename            = filename
      filetype            = 'ASC'
*      has_field_separator = 'X'
      has_field_separator = ' '
    TABLES
      data_tab            = it_inso.

  IF sy-subrc <> 0.
    WRITE: / 'Program can not read the file:' , filename.
    RETURN.
  ENDIF.

ENDFORM.                    " READ_FILE

*&---------------------------------------------------------------------*
*&      Form  PROCESS_DATA
*&---------------------------------------------------------------------*
*       Process input data
*----------------------------------------------------------------------*
FORM process_data .

* Add leading zeros to Sales Order number
  LOOP AT it_inso.
    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        input  = it_inso-vbeln
      IMPORTING
        output = it_inso-vbeln.
    MODIFY it_inso.
  ENDLOOP.

* Sort input
  SORT it_inso.

* Get versions (for update)
  SELECT vbeln vbtyp trvog vsnmr_v INTO TABLE it_vbak
         FROM vbak
         FOR ALL ENTRIES IN it_inso
         WHERE vbeln = it_inso-vbeln.

  SORT it_vbak.

  DELETE ADJACENT DUPLICATES FROM it_vbak COMPARING vbeln.

* Process input
  LOOP AT it_inso.

*   New Sales Order
    AT NEW vbeln.

      CLEAR it_vbak.
      READ TABLE it_vbak WITH KEY vbeln = it_inso-vbeln
                         BINARY SEARCH.

*     Update SO via transaction VA02 (order) or VA42 (contract)
*     -> triggers COPA update
      PERFORM call_vax2.

    ENDAT.

  ENDLOOP.

ENDFORM.                    " PROCESS_DATA

*&---------------------------------------------------------------------*
*&      Form  CALL_VAX2
*&---------------------------------------------------------------------*
*       Update SO via transaction VA02 / contract via VA42
*      (triggers COPA update)
*----------------------------------------------------------------------*
FORM call_vax2 .

* Clear structures and tables for the BAPI
  CLEAR: bdcdata, it_messages.
  REFRESH: bdcdata, it_messages.

* Select Sales Order
  PERFORM bdc_dynpro USING 'SAPMV45A'   '0102'.
  PERFORM bdc_field  USING 'BDC_OKCODE' '/00'.
  PERFORM bdc_field  USING 'VBAK-VBELN' it_inso-vbeln.

* Go to header
  PERFORM bdc_dynpro USING 'SAPMV45A'   '4001'.
  PERFORM bdc_field  USING 'BDC_OKCODE' '=HEAD'.

* Update field 'Version' (with the same value) -> triggers COPA update
  PERFORM bdc_dynpro USING 'SAPMV45A'   '4002'.
  PERFORM bdc_field  USING 'BDC_OKCODE' '=SICH'.
  PERFORM bdc_field  USING 'VBAK-VSNMR_V' it_vbak-vsnmr_v.

  bdc_options-dismode = gv_mode.
  bdc_options-updmode = 'S'.

  CASE it_vbak-trvog.
    WHEN '0'.
*     Sales Order
      CALL TRANSACTION 'VA02' USING bdcdata
                              OPTIONS FROM bdc_options
                              MESSAGES INTO it_messages.
    WHEN '4'.
*     Contract
      CALL TRANSACTION 'VA42' USING bdcdata
                              OPTIONS FROM bdc_options
                              MESSAGES INTO it_messages.
  ENDCASE.

* Get errors
  LOOP AT it_messages INTO wa_messages
                      WHERE msgtyp = 'A'  OR
                            msgtyp = 'E'.
    MESSAGE ID wa_messages-msgid
            TYPE wa_messages-msgtyp
            NUMBER wa_messages-msgnr
            INTO gv_mtext
            WITH wa_messages-msgv1 wa_messages-msgv2
                 wa_messages-msgv3 wa_messages-msgv4.
    PERFORM add_error USING gv_mtext.
    EXIT.
  ENDLOOP.
* Check if A record is created
  IF sy-subrc NE 0.
    PERFORM check_a_record.
  ENDIF.

ENDFORM.                                                    " CALL_VAX2

*&---------------------------------------------------------------------*
*&      Form  BDC_DYNPRO
*&---------------------------------------------------------------------*
*       Start new screen
*----------------------------------------------------------------------*
*      -->PROGRAM : Program name
*      -->DYNPRO  : Screen number
*----------------------------------------------------------------------*
FORM bdc_dynpro USING  program
                       dynpro.

  CLEAR bdcdata.
  bdcdata-program  = program.
  bdcdata-dynpro   = dynpro.
  bdcdata-dynbegin = 'X'.
  APPEND bdcdata.

ENDFORM.                    " BDC_DYNPRO

*&---------------------------------------------------------------------*
*&      Form  BDC_FIELD
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->FNAM : Field name
*      -->FVAL : Field value
*----------------------------------------------------------------------*
FORM bdc_field USING  fnam
                      fval.

  CLEAR bdcdata.
  bdcdata-fnam = fnam.
  bdcdata-fval = fval.
  APPEND bdcdata.

ENDFORM.                    " BDC_FIELD

*&---------------------------------------------------------------------*
*&      Form  ADD_ERROR
*&---------------------------------------------------------------------*
*       Add error
*----------------------------------------------------------------------*
*      -->P_MTEXT : Error text
*----------------------------------------------------------------------*
FORM add_error  USING    p_mtext.

  CLEAR it_err.

  it_err-vbeln = it_inso-vbeln.
  CLEAR it_typd.
  READ TABLE it_typd WITH KEY vbtyp = it_vbak-vbtyp.
  it_err-typtx = it_typd-dtext.
  it_err-mtext = p_mtext.
  it_err-trvog = it_vbak-trvog.
  APPEND it_err.

ENDFORM.                    " ADD_ERROR

*&---------------------------------------------------------------------*
*&      Form  CHECK_A_RECORD
*&---------------------------------------------------------------------*
*       Check if A record is created
*----------------------------------------------------------------------*
FORM check_a_record .

  SELECT SINGLE belnr INTO gv_belnr
         FROM ce11000
         WHERE vrgar = 'A'
           AND kaufn = it_inso-vbeln
           AND hzdat = sy-datum.

* If no A record, display error
  IF sy-subrc NE 0.
    PERFORM add_error USING 'No A record found'(i01).
  ENDIF.

ENDFORM.                    " CHECK_A_RECORD

*&---------------------------------------------------------------------*
*&      Form  ALV_FCAT
*&---------------------------------------------------------------------*
*       ALV field catalog
*----------------------------------------------------------------------*
FORM alv_fcat .

  PERFORM add_field USING 'VBELN'  10  'Document'(t01)  'X'.
  PERFORM add_field USING 'TYPTX'  20  'Type'(t02)      ' '.
  PERFORM add_field USING 'MTEXT'  80  'Message'(t03)   ' '.

ENDFORM.                    " ALV_FCAT

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr
                         p_hotsp.

  gv_col = gv_col + 1.

  CLEAR ls_fcat.
  ls_fcat-col_pos    = gv_col.
  ls_fcat-fieldname  = p_field.
  ls_fcat-outputlen  = p_len.
  ls_fcat-seltext_l  = p_descr.
  ls_fcat-seltext_m  = p_descr.
  ls_fcat-seltext_s  = p_descr.
  ls_fcat-fix_column = 'X'.
  ls_fcat-emphasize  = 'X'.
  ls_fcat-hotspot    = p_hotsp.
  ls_fcat-key        = p_hotsp.

* Suppress leading zeros for the material number
  IF p_field = 'VBELN'.
    ls_fcat-edit_mask = '==ALPHA'.
  ENDIF.

  APPEND ls_fcat TO xt_fcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT
*&---------------------------------------------------------------------*
*       Modify ALV layout
*----------------------------------------------------------------------*
FORM alv_layout .

  xv_layout-zebra = 'X'.
*  xv_layout-colwidth_optimize = 'X'.
*  xv_layout-smalltitle = 'X'.
*  xv_layout-grid_title = 'title'.

ENDFORM.                    " ALV_LAYOUT

*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       Display ALV grid
*----------------------------------------------------------------------*
FORM alv_display .

*  xv_sd_alv_variant = xv_variant.
  gv_repid = sy-repid.

  SORT it_err.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program      = gv_repid
      i_callback_user_command = xv_user_command
*      i_callback_top_of_page  = 'ALV_TOP'
      i_grid_title            = 'Rectify COPA'(h01)
      is_layout               = xv_layout
      it_fieldcat             = xt_fcat
      it_sort                 = xt_alv_sort
      i_default               = 'X'
      i_save                  = 'A'
*      is_variant              = xv_sd_alv_variant
      i_screen_start_column   = 0
      i_screen_start_line     = 0
      i_screen_end_column     = 0
      i_screen_end_line       = 0
    TABLES
      t_outtab                = it_err
    EXCEPTIONS
      program_error           = 1
      OTHERS                  = 2.

  IF sy-subrc NE 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&   Form  USER_COMMAND
*&---------------------------------------------------------------------*
*    Process user command
*----------------------------------------------------------------------*
FORM user_command USING ucomm    LIKE sy-ucomm
                        selfield TYPE slis_selfield.

* Check function code
  CASE ucomm.
    WHEN '&IC1'.
*     Select hotspot
      IF NOT selfield-value IS INITIAL.
        CASE selfield-fieldname.
          WHEN 'VBELN'.
            IF NOT selfield-value IS INITIAL.
              CLEAR ws_err.
              READ TABLE it_err INDEX selfield-tabindex INTO ws_err.
              CASE ws_err-trvog.
                WHEN '0'.
*                 Sales Order
                  SET PARAMETER ID 'AUN' FIELD selfield-value.
                  CALL TRANSACTION 'VA03' AND SKIP FIRST SCREEN.
                WHEN '4'.
*                 Contract
                  SET PARAMETER ID 'KTN' FIELD selfield-value.
                  CALL TRANSACTION 'VA43' AND SKIP FIRST SCREEN.
              ENDCASE.
            ENDIF.
        ENDCASE.
      ENDIF.
  ENDCASE.

ENDFORM.                    " USER_COMMAND

*Text symbol text£º
*H01:Rectify COPA
*I01:No A record found
*I02:Sales Order
*I03:Contract

*I04:No errors found
*Selection text£º
*P_INFILE:        Input File
