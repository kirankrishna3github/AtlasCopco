*----------------------------------------------------------------------*
* Author  : Yang Lei                                                   *
* Created : 01.04.2017                                                 *
* Dev.nbr: CR4173                                                      *
*----------------------------------------------------------------------*
*                                                                      *
************************************************************************
* Mod-xxx | xxxx.xx.xx | xxxx xxxx     | CD1Kxxxxxxxxxx  |   CRXXXX    *
************************************************************************
************************************************************************

REPORT  YSE_SPLIT_ORDFLW_BY_DIVI.

************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES:
  mara,
  t001w,
  t001,
  cepc,
  fagl_segm.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:
  BEGIN OF ty_mska,
    matnr TYPE mska-matnr,          "Material Number
    werks TYPE mska-werks,          "Plant
    lgort TYPE mska-lgort,          "Storage location
    sobkz TYPE mska-sobkz,          "Special stock ind.
    vbeln TYPE mska-vbeln,          "SO number
    posnr TYPE mska-posnr,          "SO item
    kalab TYPE mska-kalab,          "Valuated Unrestricted-Use Stock
    kaspe TYPE mska-kaspe,          "Blocked Stock
  END OF ty_mska,

  BEGIN OF ty_mbew,
    matnr TYPE mbew-matnr,          "Material Number
    bwkey TYPE mbew-bwkey,          "Valuation Area
    bwtar TYPE mbew-bwtar,          "Valuation Type
    stprs TYPE mbew-stprs,          "Standard price
    bklas TYPE mbew-bklas,          "Valuation Class
  END OF ty_mbew,

  BEGIN OF ty_makt,
    matnr TYPE makt-matnr,          "Material Number
    spras TYPE makt-spras,          "Language Key
    maktx TYPE makt-maktx,          "Short description
  END OF ty_makt,

  BEGIN OF ty_vbak,
    vbeln TYPE vbak-vbeln,          "SO number
    kunnr TYPE vbak-kunnr,          "Customer code
  END OF ty_vbak,

  BEGIN OF ty_marc,
    matnr TYPE marc-matnr,          "Material Number
    werks TYPE marc-werks,          "Plant
    prctr TYPE marc-prctr,          "Profit center
  END OF ty_marc,

  BEGIN OF ty_div,
    vkorg TYPE yse_it_cust_div-vkorg,  "Sales org.
    kunnr TYPE yse_it_cust_div-kunnr,  "Customer
    divis TYPE yse_it_cust_div-divis,  "Division
  END OF ty_div,

  BEGIN OF ty_mard,
    matnr TYPE mard-matnr,             "Material
    werks TYPE mard-werks,             "Plant
    labst TYPE mard-labst,             "Valuated Unrestricted-Use Stock
    speme TYPE mard-speme,             "Blocked Stock
  END OF ty_mard,

  BEGIN OF ty_mbew_free,
    matnr TYPE mbew-matnr,             "Material
    bwkey TYPE mbew-bwkey,             "Valuation Area
    stprs TYPE mbew-stprs,             "Standard price
    bklas TYPE mbew-bklas,             "Valuation Class
  END OF ty_mbew_free,

  BEGIN OF ty_bsis,
    bukrs   TYPE bsis-bukrs,           "Company code
    hkont   TYPE bsis-hkont,           "G/L account
    gjahr   TYPE bsis-gjahr,           "Fiscal Year
    belnr   TYPE bsis-belnr,           "Accounting Document Number
    buzei   TYPE bsis-buzei,           "Line number
    dmbtr   TYPE bsis-dmbtr,           "Amount in Local Currency
    shkzg   TYPE bsis-shkzg,           "Debit/Credit
    prctr   TYPE bsis-prctr,           "Profit center
    segment TYPE bsis-segment,         "Segment
  END OF ty_bsis,

  BEGIN OF ty_bseg,
    bukrs TYPE bseg-bukrs,             "Company code
    gjahr TYPE bseg-gjahr,             "Fiscal Year
    belnr TYPE bseg-belnr,             "Accounting Document Number
    buzei TYPE bseg-buzei,             "Line number
    matnr TYPE bseg-matnr,             "Material
    werks TYPE bseg-werks,             "Plant
    vbel2 TYPE bseg-vbel2,             "Sales Document
  END OF ty_bseg,

  BEGIN OF ty_sum,
    matnr TYPE bseg-matnr,             "Material
    werks TYPE bseg-werks,             "Plant
    vbel2 TYPE char10,                 "SO
    hkont TYPE bsis-hkont,             "GIT account
    prctr TYPE bsis-prctr,             "Profit center
    segment TYPE bsis-segment,         "Segment
    dmbtr TYPE bsis-dmbtr,             "Amount
    kunnr TYPE char10,                 "Customer code
    divis TYPE yse_it_cust_div-divis,  "ITBA division
  END OF ty_sum,

  BEGIN OF ty_alv,
    matnr TYPE mska-matnr,             "Material
    werks TYPE mska-werks,             "Plant
    lgort TYPE mska-lgort,             "Storage location
    sobkz TYPE mska-sobkz,             "Special stock ind.
    vbeln TYPE char10,                 "SO number
    posnr TYPE mska-posnr,             "SO item
    ssqty TYPE mska-kalab,             "sales stock qty
    ssttl TYPE mska-kalab,             "Total stock value
    stprs TYPE mbew-stprs,             "standard price
    bklas TYPE mbew-bklas,             "Valuation Class
    maktx TYPE makt-maktx,             "short desription
    kunnr TYPE char10,                 "Customer code
    prctr TYPE marc-prctr,             "Profit center
    divis TYPE yse_it_cust_div-divis,  "ITBA division
    hkont TYPE bseg-hkont,             "GL account
  END OF ty_alv,

  gty_mska TYPE STANDARD TABLE OF ty_mska,
  gty_mbew TYPE STANDARD TABLE OF ty_mbew,
  gty_makt TYPE STANDARD TABLE OF ty_makt,
  gty_vbak TYPE STANDARD TABLE OF ty_vbak,
  gty_marc TYPE STANDARD TABLE OF ty_marc,
  gty_div  TYPE STANDARD TABLE OF ty_div,
  gty_prctr TYPE STANDARD TABLE OF yse_prctr_bl,
  gty_mard TYPE STANDARD TABLE OF ty_mard,
  gty_mbew_free TYPE STANDARD TABLE OF ty_mbew_free,
  gty_makt_free TYPE STANDARD TABLE OF ty_makt,
  gty_marc_free TYPE STANDARD TABLE OF ty_marc,
  gty_bsis TYPE STANDARD TABLE OF ty_bsis,
  gty_bseg TYPE STANDARD TABLE OF ty_bseg,
  gty_sum  TYPE STANDARD TABLE OF ty_sum,
  gty_alv  TYPE STANDARD TABLE OF ty_alv.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_mska TYPE gty_mska,
  gt_mbew TYPE gty_mbew,
  gt_makt TYPE gty_makt,
  gt_vbak TYPE gty_vbak,
  gt_marc TYPE gty_marc,
  gt_div  TYPE gty_div,
  gt_prctr TYPE gty_prctr,
  gt_mard TYPE gty_mard,
  gt_mbew_free TYPE gty_mbew_free,
  gt_makt_free TYPE gty_makt_free,
  gt_marc_free TYPE gty_marc_free,
  gt_bsis TYPE gty_bsis,
  gt_bseg TYPE gty_bseg,
  gt_vbak_r2 TYPE gty_vbak,
  gt_alv  TYPE gty_alv,
  gt_alv2 TYPE gty_sum,
  gt_fieldcat TYPE slis_t_fieldcat_alv.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_mska TYPE ty_mska,
  gs_mbew TYPE ty_mbew,
  gs_makt TYPE ty_makt,
  gs_vbak TYPE ty_vbak,
  gs_marc TYPE ty_marc,
  gs_div  TYPE ty_div,
  gs_mard TYPE ty_mard,
  gs_mbew_free TYPE ty_mbew_free,
  gs_bsis TYPE ty_bsis,
  gs_bseg TYPE ty_bseg,
  gs_alv  TYPE ty_alv,
  gs_layout TYPE slis_layout_alv,
  gs_disvar TYPE disvariant.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_col(10) TYPE n,
  gv_text    TYPE string,
  gv_save    TYPE c.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x TYPE c LENGTH 1 VALUE 'X',
  gc_e TYPE c LENGTH 1 VALUE 'E',
  gc_s TYPE c LENGTH 1 VALUE 'S'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01 .
  SELECT-OPTIONS:
    s_matnr FOR mara-matnr,                "Material
    s_werks FOR t001w-werks,               "Plant
    s_bukrs FOR t001-bukrs NO-EXTENSION NO INTERVALS,  "Company
    s_prctr FOR cepc-prctr,                "SCE  GAC
    s_segmt FOR fagl_segm-segment.         "IT Segment
SELECTION-SCREEN END OF BLOCK b1.

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-t02 .
  PARAMETERS:
    p_chk TYPE c AS CHECKBOX.              "No zero stock lines
SELECTION-SCREEN END OF BLOCK b2.

SELECTION-SCREEN BEGIN OF BLOCK b3 WITH FRAME TITLE text-t05 .
  PARAMETERS:
    p_rept1 TYPE c RADIOBUTTON GROUP g1 DEFAULT 'X',
    p_rept2 TYPE c RADIOBUTTON GROUP g1.
SELECTION-SCREEN END OF BLOCK b3.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initilization
  PERFORM frm_init.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* Get data
  PERFORM get_data.
* Mapping data
  PERFORM edit_data.
* FIELDCAT set
  PERFORM alv_fieldcat_set.
* LAYOUT set
  PERFORM alv_layout_set.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
* ALV display
  PERFORM alv_display.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do initilization
*----------------------------------------------------------------------*
FORM FRM_INIT .

  CLEAR:
    gt_mska,
    gt_mbew,
    gt_makt,
    gt_vbak,
    gt_marc,
    gt_div,
    gt_alv,
    gt_fieldcat,
    gt_prctr,
    gt_mard,
    gt_mbew_free,
    gt_makt_free,
    gt_marc_free,
    gt_bsis,
    gt_bseg,
    gt_vbak_r2,
    gt_alv2,
    gs_mska,
    gs_mbew,
    gs_makt,
    gs_vbak,
    gs_marc,
    gs_div,
    gs_alv,
    gs_layout,
    gs_disvar,
    gs_mard,
    gs_mbew_free,
    gs_bsis,
    gs_bseg,
    gv_col,
    gv_text,
    gv_save.

ENDFORM.                    " FRM_INIT
*&---------------------------------------------------------------------*
*&      Form  GET_DATA
*&---------------------------------------------------------------------*
*       Get data
*----------------------------------------------------------------------*
FORM GET_DATA .
  DATA:
    lt_mska  TYPE gty_mska,
    lt_vbak  TYPE gty_vbak,
    lt_mard  TYPE gty_mard,
    lt_hkont TYPE RANGE OF bsis-hkont,
    ls_hkont LIKE LINE OF lt_hkont,
    lt_bsis  TYPE gty_bsis,
    lt_bseg  TYPE gty_bseg.

  IF p_rept1 = gc_x.
*   Get MSKA
    SELECT matnr                        "Material Number
           werks                        "Plant
           lgort                        "Storage location
           sobkz                        "Special stock ind.
           vbeln                        "SO number
           posnr                        "SO item
           kalab                        "Valuated Unrestricted-Use Stock
           kaspe                        "Blocked Stock
      FROM mska
      INTO TABLE gt_mska
     WHERE matnr IN s_matnr
       AND werks IN s_werks.

*   Get MBEW
    IF gt_mska IS NOT INITIAL.
      lt_mska = gt_mska.
      SORT lt_mska BY matnr ASCENDING werks ASCENDING.
      DELETE ADJACENT DUPLICATES FROM lt_mska COMPARING matnr werks.

      SELECT matnr                        "Material Number
             bwkey                        "Valuation Area
             bwtar                        "Valuation Type
             stprs                        "Standard price
             bklas                        "Valuation Class
        FROM mbew
        INTO TABLE gt_mbew
         FOR ALL ENTRIES IN lt_mska
       WHERE matnr = lt_mska-matnr
         AND bwkey = lt_mska-werks.
      SORT gt_mbew
        BY matnr ASCENDING
           bwkey ASCENDING.
    ENDIF.

*   Get MAKT
    IF gt_mska IS NOT INITIAL.
      lt_mska = gt_mska.
      SORT lt_mska BY matnr ASCENDING.
      DELETE ADJACENT DUPLICATES FROM lt_mska COMPARING matnr.

      SELECT matnr                        "Material Number
             spras                        "Language Key
             maktx                        "Short description
        FROM makt
        INTO TABLE gt_makt
         FOR ALL ENTRIES IN lt_mska
       WHERE matnr = lt_mska-matnr
         AND spras = gc_e.
      SORT gt_makt
        BY matnr ASCENDING.
    ENDIF.

*   Get VBAK
    IF gt_mska IS NOT INITIAL.
      lt_mska = gt_mska.
      SORT lt_mska BY vbeln ASCENDING.
      DELETE ADJACENT DUPLICATES FROM lt_mska COMPARING vbeln.

      SELECT vbeln                        "SO number
             kunnr                        "Customer code
        FROM vbak
        INTO TABLE gt_vbak
         FOR ALL ENTRIES IN lt_mska
       WHERE vbeln = lt_mska-vbeln.
      SORT gt_vbak
        BY vbeln ASCENDING.
    ENDIF.

*   Get MARC
    IF gt_mska IS NOT INITIAL.
      lt_mska = gt_mska.
      SORT lt_mska BY matnr ASCENDING werks ASCENDING.
      DELETE ADJACENT DUPLICATES FROM lt_mska COMPARING matnr werks.

      SELECT matnr                        "Material Number
             werks                        "Plant
             prctr                        "Profit Center
        FROM marc
        INTO TABLE gt_marc
         FOR ALL ENTRIES IN lt_mska
       WHERE matnr = lt_mska-matnr
         AND werks = lt_mska-werks.
      SORT gt_marc
        BY matnr ASCENDING
           werks ASCENDING.
    ENDIF.

*   Get divition
    IF gt_vbak IS NOT INITIAL.
      lt_vbak = gt_vbak.
      SORT lt_vbak BY kunnr ASCENDING.
      DELETE ADJACENT DUPLICATES FROM lt_vbak COMPARING kunnr.

      SELECT vkorg                      "Sales org.
             kunnr                      "Customer
             divis                      "Division
        FROM yse_it_cust_div
        INTO TABLE gt_div
         FOR ALL ENTRIES IN lt_vbak
       WHERE kunnr = lt_vbak-kunnr.
      SORT gt_div
        BY kunnr ASCENDING.
    ENDIF.

*   Get Profit center
    SELECT *
      FROM yse_prctr_bl
      INTO TABLE gt_prctr
     WHERE segment IN s_segmt.
    SORT gt_prctr
      BY prctr ASCENDING.

*   Branch 2: Free stock
    SELECT matnr                        "Material
           werks                        "Plant
           labst                        "Valuated Unrestricted-Use Stock
           speme                        "Blocked Stock
      FROM mard
      INTO TABLE gt_mard
     WHERE matnr IN s_matnr
       AND werks IN s_werks.

*   Get MBEW
    IF gt_mard IS NOT INITIAL.
      lt_mard = gt_mard.
      SORT lt_mard BY matnr ASCENDING werks ASCENDING.
      DELETE ADJACENT DUPLICATES FROM lt_mard COMPARING matnr werks.

      SELECT matnr                        "Material
             bwkey                        "Valuation Area
             stprs                        "Standard price
             bklas                        "Valuation Class
        FROM mbew
        INTO TABLE gt_mbew_free
         FOR ALL ENTRIES IN lt_mard
       WHERE matnr = lt_mard-matnr
         AND bwkey = lt_mard-werks.
      SORT gt_mbew_free
        BY matnr ASCENDING
           bwkey ASCENDING.
    ENDIF.

*   Get MAKT
    IF gt_mard IS NOT INITIAL.
      lt_mard = gt_mard.
      SORT lt_mard BY matnr ASCENDING.
      DELETE ADJACENT DUPLICATES FROM lt_mard COMPARING matnr.

      SELECT matnr                        "Material Number
             spras                        "Language Key
             maktx                        "Short description
        FROM makt
        INTO TABLE gt_makt_free
         FOR ALL ENTRIES IN lt_mard
       WHERE matnr = lt_mard-matnr
         AND spras = gc_e.
      SORT gt_makt_free
        BY matnr ASCENDING.
    ENDIF.

*   Get MARC
    IF gt_mard IS NOT INITIAL.
      lt_mard = gt_mard.
      SORT lt_mard BY matnr ASCENDING werks ASCENDING.
      DELETE ADJACENT DUPLICATES FROM lt_mard COMPARING matnr werks.

      SELECT matnr                        "Material Number
             werks                        "Plant
             prctr                        "Profit Center
        FROM marc
        INTO TABLE gt_marc_free
         FOR ALL ENTRIES IN lt_mard
       WHERE matnr = lt_mard-matnr
         AND werks = lt_mard-werks.
      SORT gt_marc_free
        BY matnr ASCENDING
           werks ASCENDING.
    ENDIF.

  ELSE.
    ls_hkont-sign   = 'I'.
    ls_hkont-option = 'EQ'.
    ls_hkont-low    = '0001461901'.
    APPEND ls_hkont TO lt_hkont.
    ls_hkont-sign   = 'I'.
    ls_hkont-option = 'EQ'.
    ls_hkont-low    = '0001402900'.
    APPEND ls_hkont TO lt_hkont.
    ls_hkont-sign   = 'I'.
    ls_hkont-option = 'EQ'.
    ls_hkont-low    = '0001402901'.
    APPEND ls_hkont TO lt_hkont.

*   Get BSIS
    SELECT bukrs                        "Company code
           hkont                        "G/L account
           gjahr                        "Fiscal Year
           belnr                        "Accounting Document Number
           buzei                        "Line number
           dmbtr                        "Amount in Local Currency
           shkzg                        "Debit/Credit
           prctr                        "Profit center
           segment                      "Segment
      FROM bsis
      INTO TABLE gt_bsis
     WHERE bukrs   IN s_bukrs
       AND hkont   IN lt_hkont
       AND segment IN s_segmt.

*   Get BSEG
    IF gt_bsis IS NOT INITIAL.
      lt_bsis = gt_bsis.
      SORT lt_bsis
        BY bukrs ASCENDING
           gjahr ASCENDING
           belnr ASCENDING
           buzei ASCENDING.
      DELETE ADJACENT DUPLICATES FROM lt_bsis
                            COMPARING bukrs gjahr belnr buzei.

      SELECT bukrs                        "Company code
             gjahr                        "Fiscal Year
             belnr                        "Accounting Document Number
             buzei                        "Line number
             matnr                        "Material
             werks                        "Plant
             vbel2                        "Sales Document
        FROM bseg
        INTO TABLE gt_bseg
         FOR ALL ENTRIES IN lt_bsis
       WHERE bukrs = lt_bsis-bukrs
         AND gjahr = lt_bsis-gjahr
         AND belnr = lt_bsis-belnr
         AND buzei = lt_bsis-buzei.
      SORT gt_bseg
        BY bukrs ASCENDING
           gjahr ASCENDING
           belnr ASCENDING
           buzei ASCENDING.
    ENDIF.

*   Get VBAK
    IF gt_bseg IS NOT INITIAL.
      lt_bseg = gt_bseg.
      SORT lt_bseg
        BY vbel2 ASCENDING.
      DELETE ADJACENT DUPLICATES FROM lt_bseg
                            COMPARING vbel2.

      SELECT vbeln                        "SO number
             kunnr                        "Customer code
        FROM vbak
        INTO TABLE gt_vbak_r2
         FOR ALL ENTRIES IN lt_bseg
       WHERE vbeln = lt_bseg-vbel2.
      SORT gt_vbak_r2
        BY vbeln ASCENDING.
    ENDIF.

*   Get divition
    IF gt_vbak_r2 IS NOT INITIAL.
      lt_vbak = gt_vbak_r2.
      SORT lt_vbak BY kunnr ASCENDING.
      DELETE ADJACENT DUPLICATES FROM lt_vbak COMPARING kunnr.

      SELECT vkorg                      "Sales org.
             kunnr                      "Customer
             divis                      "Division
        FROM yse_it_cust_div
        INTO TABLE gt_div
         FOR ALL ENTRIES IN lt_vbak
       WHERE kunnr = lt_vbak-kunnr.
      SORT gt_div
        BY kunnr ASCENDING.
    ENDIF.
  ENDIF.

ENDFORM.                    " GET_DATA
*&---------------------------------------------------------------------*
*&      Form  EDIT_DATA
*&---------------------------------------------------------------------*
*       Mapping data
*----------------------------------------------------------------------*
FORM EDIT_DATA .
  DATA:
    lr_cn6   TYPE RANGE OF t001w-werks,
    lr_cn9   TYPE RANGE OF t001w-werks,
    ls_werk  LIKE LINE OF lr_cn6,
    lt_sum   TYPE gty_sum,
    ls_sum   TYPE ty_sum,
    ls_sum2  TYPE ty_sum,
    lv_dmbtr TYPE bsis-dmbtr.

  CLEAR:
    gt_alv,
    gt_alv2.

  IF p_rept1 = gc_x.
    ls_werk-sign   = 'I'.
    ls_werk-option = 'CP'.
    ls_werk-low    = 'CN6*'.
    APPEND ls_werk TO lr_cn6.
    ls_werk-sign   = 'I'.
    ls_werk-option = 'CP'.
    ls_werk-low    = 'CN9*'.
    APPEND ls_werk TO lr_cn9.

*   Sales order stock
    LOOP AT gt_mska INTO gs_mska.
      CLEAR:
        gs_alv,
        gs_mbew,
        gs_makt,
        gs_vbak,
        gs_marc,
        gs_div.

      READ TABLE gt_mbew INTO gs_mbew
            WITH KEY matnr = gs_mska-matnr
                     bwkey = gs_mska-werks
            BINARY SEARCH.
      READ TABLE gt_makt INTO gs_makt
            WITH KEY matnr = gs_mska-matnr
            BINARY SEARCH.
      READ TABLE gt_vbak INTO gs_vbak
            WITH KEY vbeln = gs_mska-vbeln
            BINARY SEARCH.
      READ TABLE gt_marc INTO gs_marc
            WITH KEY matnr = gs_mska-matnr
                     werks = gs_mska-werks
            BINARY SEARCH.
      READ TABLE gt_div INTO gs_div
            WITH KEY kunnr = gs_vbak-kunnr
            BINARY SEARCH.

*     Determine target data
      READ TABLE gt_prctr TRANSPORTING NO FIELDS
            WITH KEY prctr = gs_marc-prctr
            BINARY SEARCH.
      IF sy-subrc <> 0.
        CONTINUE.
      ENDIF.

*     Material
      gs_alv-matnr = gs_mska-matnr.
*     Plant
      gs_alv-werks = gs_mska-werks.
*     Storage location
      gs_alv-lgort = gs_mska-lgort.
*     Special stock ind.
      gs_alv-sobkz = gs_mska-sobkz.
*     SO number
      gs_alv-vbeln = gs_mska-vbeln.
*     SO item
      gs_alv-posnr = gs_mska-posnr.
*     sales stock qty
      gs_alv-ssqty = gs_mska-kalab + gs_mska-kaspe.
*     Total stock value
      gs_alv-ssttl = ( gs_mska-kalab + gs_mska-kaspe )
                     * gs_mbew-stprs.
*     standard price
      gs_alv-stprs = gs_mbew-stprs.
*     Valuation Class
      gs_alv-bklas = gs_mbew-bklas.
*     short desription
      gs_alv-maktx = gs_makt-maktx.
*     Customer code
      gs_alv-kunnr = gs_vbak-kunnr.
*     Profit center
      gs_alv-prctr = gs_marc-prctr.
*     ITBA division
      gs_alv-divis = gs_div-divis.

      IF  p_chk = gc_x
      AND gs_alv-ssqty = 0.
        CONTINUE.
      ENDIF.

*     Process division(still blank)
      IF gs_alv-divis IS INITIAL.
        IF gs_alv-prctr IN s_prctr.
          gs_alv-divis = 'SCE'.
        ELSE.
          gs_alv-divis = 'GIN'.
        ENDIF.
      ENDIF.

*     G/L account
      IF gs_alv-bklas = '3060'.
        gs_alv-hkont = '1462901'.
      ELSEIF gs_alv-bklas = '3040'.
        IF gs_alv-werks IN lr_cn6.
          gs_alv-hkont = '1461900'.
        ENDIF.
        IF gs_alv-werks IN lr_cn9.
          gs_alv-hkont = '1402904'.
        ENDIF.
      ENDIF.

      APPEND gs_alv TO gt_alv.
    ENDLOOP.

*   Free stock
    LOOP AT gt_mard INTO gs_mard.
      CLEAR:
        gs_alv,
        gs_mbew_free,
        gs_makt,
        gs_marc.

      READ TABLE gt_mbew_free INTO gs_mbew_free
            WITH KEY matnr = gs_mard-matnr
                     bwkey = gs_mard-werks
            BINARY SEARCH.
      READ TABLE gt_makt_free INTO gs_makt
            WITH KEY matnr = gs_mard-matnr
            BINARY SEARCH.
      READ TABLE gt_marc_free INTO gs_marc
            WITH KEY matnr = gs_mard-matnr
                     werks = gs_mard-werks
            BINARY SEARCH.

*     Determine target data
      READ TABLE gt_prctr TRANSPORTING NO FIELDS
            WITH KEY prctr = gs_marc-prctr
            BINARY SEARCH.
      IF sy-subrc <> 0.
        CONTINUE.
      ENDIF.

*     Material
      gs_alv-matnr = gs_mard-matnr.
*     Plant
      gs_alv-werks = gs_mard-werks.
*     sales stock qty
      gs_alv-ssqty = gs_mard-labst + gs_mard-speme.
*     Total stock value
      gs_alv-ssttl = ( gs_mard-labst + gs_mard-speme )
                     * gs_mbew_free-stprs.
*     standard price
      gs_alv-stprs = gs_mbew_free-stprs.
*     Valuation Class
      gs_alv-bklas = gs_mbew_free-bklas.
*     short desription
      gs_alv-maktx = gs_makt-maktx.
*     Profit center
      gs_alv-prctr = gs_marc-prctr.

      IF  p_chk = gc_x
      AND gs_alv-ssqty = 0.
        CONTINUE.
      ENDIF.

*     ITBA division
      IF gs_alv-prctr IN s_prctr.
        gs_alv-divis = 'SCE'.
      ELSE.
        gs_alv-divis = 'GIN'.
      ENDIF.

*     G/L account
      IF gs_alv-bklas = '3060'.
        gs_alv-hkont = '1462901'.
      ELSEIF gs_alv-bklas = '3040'.
        IF gs_alv-werks IN lr_cn6.
          gs_alv-hkont = '1461900'.
        ENDIF.
        IF gs_alv-werks IN lr_cn9.
          gs_alv-hkont = '1402904'.
        ENDIF.
      ENDIF.

      APPEND gs_alv TO gt_alv.
    ENDLOOP.

    IF gt_alv[] IS INITIAL.
      IF sy-batch = gc_x.
        MESSAGE E001(00) WITH text-001.
      ELSE.
        MESSAGE s001(00) WITH text-001 DISPLAY LIKE gc_e.
        LEAVE LIST-PROCESSING.
      ENDIF.
    ENDIF.
  ELSE.
    LOOP AT gt_bsis INTO gs_bsis.
      CLEAR:
        ls_sum,
        gs_bseg,
        gs_vbak,
        gs_div.

      READ TABLE gt_bseg INTO gs_bseg
            WITH KEY bukrs = gs_bsis-bukrs
                     gjahr = gs_bsis-gjahr
                     belnr = gs_bsis-belnr
                     buzei = gs_bsis-buzei
            BINARY SEARCH.
      IF gs_bseg-vbel2 IS NOT INITIAL.
        READ TABLE gt_vbak_r2 INTO gs_vbak
              WITH KEY vbeln = gs_bseg-vbel2
              BINARY SEARCH.
      ENDIF.
      READ TABLE gt_div INTO gs_div
            WITH KEY kunnr = gs_vbak-kunnr
            BINARY SEARCH.

*     Material
      ls_sum-matnr = gs_bseg-matnr.
*     Plant
      ls_sum-werks = gs_bseg-werks.
*     SO
      ls_sum-vbel2 = gs_bseg-vbel2.
*     GIT account
      ls_sum-hkont = gs_bsis-hkont.
*     Profit center
      ls_sum-prctr = gs_bsis-prctr.
*     Segment
      ls_sum-segment = gs_bsis-segment.
*     Amount
      ls_sum-dmbtr = gs_bsis-dmbtr.
      IF gs_bsis-shkzg = 'H'.
        ls_sum-dmbtr = ls_sum-dmbtr * -1.
      ENDIF.
*     Customer code
      ls_sum-kunnr = gs_vbak-kunnr.
*     ITBA division
      ls_sum-divis = gs_div-divis.

      IF ls_sum-divis IS INITIAL.
        IF ls_sum-prctr IN s_prctr.
          ls_sum-divis = 'SCE'.
        ELSE.
          ls_sum-divis = 'GIN'.
        ENDIF.
      ENDIF.

      APPEND ls_sum TO lt_sum.
    ENDLOOP.

    SORT lt_sum
      BY matnr   ASCENDING
         werks   ASCENDING
         vbel2   ASCENDING
         hkont   ASCENDING
         prctr   ASCENDING
         segment ASCENDING.

    LOOP AT lt_sum INTO ls_sum.
      ls_sum2 = ls_sum.
      lv_dmbtr = lv_dmbtr + ls_sum2-dmbtr.

      AT END OF segment.
        IF  p_chk = gc_x
        AND lv_dmbtr = 0.
          CONTINUE.
        ENDIF.

        ls_sum2-dmbtr = lv_dmbtr.
        APPEND ls_sum2 TO gt_alv2.

        CLEAR:
          lv_dmbtr.
      ENDAT.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " EDIT_DATA
*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM ALV_FIELDCAT_SET .

** Create Fieldcatalogue from internal table

  CLEAR:
    gv_col,
    gt_fieldcat.

  IF p_rept1 = gc_x.
*   Field definitions
    PERFORM add_field USING:
       'MATNR' 10 text-002 text-002,
       'WERKS' 4  text-003 text-003,
       'LGORT' 4  text-004 text-005,
       'SOBKZ' 1  text-006 text-007,
       'VBELN' 10 text-008 text-008,
       'POSNR' 6  text-009 text-009,
       'SSQTY' 13 text-010 text-011,
       'SSTTL' 13 text-012 text-013,
       'STPRS' 11 text-014 text-015,
       'BKLAS' 4  text-024 text-025,
       'MAKTX' 40 text-016 text-017,
       'KUNNR' 10 text-018 text-019,
       'PRCTR' 10 text-020 text-021,
       'DIVIS' 4  text-022 text-023,
       'HKONT' 10 text-026 text-027.
  ELSE.
*   Field definitions
    PERFORM add_field USING:
       'MATNR' 10 text-002 text-002,
       'WERKS' 4  text-003 text-003,
       'VBEL2' 10 text-028 text-029,
       'HKONT' 10 text-026 text-027,
       'PRCTR' 10 text-020 text-021,
       'SEGMENT' 10 text-030 text-030,
       'DMBTR' 13 text-031 text-032,
       'KUNNR' 10 text-018 text-019,
       'DIVIS' 4  text-022 text-023.
  ENDIF.

ENDFORM.                    " ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr_s
                         p_descr_l.
  DATA:
     ls_fieldcat TYPE slis_fieldcat_alv.

  gv_col = gv_col + 1.

  CLEAR ls_fieldcat.
  ls_fieldcat-col_pos    = gv_col.
  ls_fieldcat-fieldname  = p_field.
  ls_fieldcat-outputlen  = p_len.
  ls_fieldcat-seltext_l  = p_descr_l.
  ls_fieldcat-seltext_m  = p_descr_l.
  ls_fieldcat-seltext_s  = p_descr_s.
  ls_fieldcat-fix_column = gc_x.
  ls_fieldcat-emphasize  = gc_x.

* Suppress leading zeros for the material number
  IF p_field = 'MATNR'.
    ls_fieldcat-edit_mask = '==MATN1'.
  ENDIF.

  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ADD_FIELD
*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   "Auto optimize column width
  gs_layout-detail_popup = gc_x.        "Show detail screen

ENDFORM.                    " ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM ALV_DISPLAY .

  DATA: lv_lines(10)  TYPE c.

  IF p_rept1 = gc_x.
    CLEAR: gv_text.
    DESCRIBE TABLE gt_alv LINES lv_lines.
    CONCATENATE text-t03 lv_lines text-t04
           INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.

    CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
      EXPORTING
        i_callback_program       = sy-repid
        is_layout                = gs_layout
        it_fieldcat              = gt_fieldcat
        i_save                   = gv_save
        is_variant               = gs_disvar
      TABLES
        t_outtab                 = gt_alv
      EXCEPTIONS
        program_error            = 1
        OTHERS                   = 2.
    IF sy-subrc <> 0.
      IF sy-batch = gc_x.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      ELSE.
        MESSAGE ID sy-msgid TYPE gc_s NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
              DISPLAY LIKE gc_e.
        LEAVE LIST-PROCESSING.
      ENDIF.
    ENDIF.
  ELSE.
    CLEAR: gv_text.
    DESCRIBE TABLE gt_alv2 LINES lv_lines.
    CONCATENATE text-t03 lv_lines text-t04
           INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.

    CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
      EXPORTING
        i_callback_program       = sy-repid
        is_layout                = gs_layout
        it_fieldcat              = gt_fieldcat
        i_save                   = gv_save
        is_variant               = gs_disvar
      TABLES
        t_outtab                 = gt_alv2
      EXCEPTIONS
        program_error            = 1
        OTHERS                   = 2.
    IF sy-subrc <> 0.
      IF sy-batch = gc_x.
        MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
      ELSE.
        MESSAGE ID sy-msgid TYPE gc_s NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4
              DISPLAY LIKE gc_e.
        LEAVE LIST-PROCESSING.
      ENDIF.
    ENDIF.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*Text symbol text£º
*001:No process data
*002:Material
*003:Plant
*004:Stor. Loc
*005:Storage location
*006:Spec.Stock
*007:Special stock ind.
*008:SO number
*009:SO item
*010:Sales stk.
*011:Sales stock qty
*012:Total stk.
*013:Total stock value
*014:Std price
*015:Standard price
*016:Descr.
*017:Short desription
*018:Customer
*019:Customer code
*020:Profit Ctr
*021:Profit center
*022:Division
*023:ITBA division
*024:Valuation
*025:Valuation Class
*026:G/L acc.
*027:G/L account
*028:SO num.
*029:Sales Document
*030:Segment
*031:Subtotal
*032:Subtotal amount
*T01:Database Selections
*T02:Settings
*T03:Totally
*T04:entries are process

*T05:Report Selections
*Selection text£º
*P_CHK:        No zero stock lines
*P_REPT1:        Stock report
*P_REPT2:        GIT report
*S_BUKRS:        Company code
*S_MATNR:        Material
*S_PRCTR:        SCE GAC
*S_SEGMT:        IT Segment
*S_WERKS:        Plant
