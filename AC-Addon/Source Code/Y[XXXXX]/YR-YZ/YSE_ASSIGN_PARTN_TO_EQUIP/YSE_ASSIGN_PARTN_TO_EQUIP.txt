*----------------------------------------------------------------------
* PROGRAM ID           : YSE_ASSIGN_PARTN_TO_EQUIP                     *
* PROGRAM TITLE        : UPUpdate Partner function in equip. master    *
* AUTHOR               : LUC MERTENS                                   *
* DATE                 : 09/01/2007                                    *
* DEVELOPMENT ID       :                                               *
* CHANGE REQUEST NUMBER: CD1K909120                                    *
* PROGRAM DESCRIPTION  : Posting program for equipment partner details *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE #
*----------------------------------------------------------------------*
*
************************************************************************

REPORT YSE_ASSIGN_PARTN_TO_EQUIP
       NO STANDARD PAGE HEADING
       LINE-SIZE 270
       MESSAGE-ID yam_dc.

*---------------------------------------------------------------------*
* TABLE DECLARATION
*---------------------------------------------------------------------*
Tables  /sapdmc/lsmemory.     " DEFAULT VALUES FOR PROJ,SUBPROJ,OBJECT

*---------------------------------------------------------------------*
* TYPE DEFINITIONS
*---------------------------------------------------------------------*
* - DECLARATION FOR STRUCTURE TO READ THE FLAT FILE STRING PER STRING
TYPES: BEGIN OF ty_upload,
         v_text(500)   TYPE c,            " FILE UPLOAD TEXT
       END OF ty_upload.

* - DECLARATION FOR THE CORRESPONDING TARGET FIELDS
TYPES : BEGIN OF ty_final_record,
            matnr      type matnr,      " material number
            sernr      type gernr,      " serial number
            kunnr      type kunnr,      " customer number
            parvw      type parvw,      " partner function
            vkorg      type vkorg,      " sales organization
            vtweg      type vtweg,      " distr. channel
            spart      type spart,      " division
        END OF ty_final_record.

* - DECLARATION FOR THE HEADER DATA
TYPES : BEGIN OF ty_header,
            matnr      type matnr,      " material number
            sernr      type gernr,      " serial number
        END OF ty_header.

* - DECLARATION FOR THE ITEM DATA
TYPES : BEGIN OF ty_detail,
            matnr      type matnr,      " material number
            sernr      type gernr,      " serial number
            kunnr      type kunnr,      " customer number
            parvw      type parvw,      " partner function
            vkorg      type vkorg,      " sales organization
            vtweg      type vtweg,      " distr. channel
            spart      type spart,      " division
        END OF ty_detail.

* - DECLARATION FOR THE PARTNERS DATA
TYPES : BEGIN OF ty_partners,
            equnr      type equnr,      " equipment number
            parvw      type parvw,      " partner function
            kunn2      type kunn2,      " partner number
        END OF ty_partners.

*---------------------------------------------------------------------*
* INTERNAL TABLE DECLARATIONS                                         *
*---------------------------------------------------------------------*
* - INTERNAL TABLE FOR THE FILE OF LEGACY DATA UPLOAD FOR CONTRACTS
DATA : i_upload TYPE STANDARD TABLE OF ty_upload
                       INITIAL SIZE 0 WITH HEADER LINE.

* - INTERNAL TABLE FOR THE LEGACY DATA
DATA : i_final_record TYPE STANDARD TABLE OF ty_final_record
                       INITIAL SIZE 0 WITH HEADER LINE.

* - INTERNAL TABLE FOR THE HEADER DATA
DATA : i_header TYPE STANDARD TABLE OF ty_header
                      INITIAL SIZE 0 WITH HEADER LINE.

* - INTERNAL TABLE FOR THE DETAIL DATA
DATA : i_detail TYPE STANDARD TABLE OF ty_detail
                      INITIAL SIZE 0 WITH HEADER LINE.

* - INTERNAL TABLE FOR THE PARTNER DATA
DATA : i_partners TYPE STANDARD TABLE OF ty_partners
                      INITIAL SIZE 0 WITH HEADER LINE.

*---------------------------------------------------------------------*
* INTERNAL TABLE FOR BDC AND CONTRACT DETAILS                         *
*---------------------------------------------------------------------*
DATA : i_bdcdata LIKE bdcdata OCCURS 0 WITH HEADER LINE,
       struct_bdcdata TYPE bdcdata.

*---------------------------------------------------------------------*
* VARIABLE DECLARATIONS                                               *
*---------------------------------------------------------------------*
DATA :  g_count        TYPE sy-tabix,           " COUNT
        gv_equnr       type equnr,              " equipment number
        gv_kunn2       type kunn2,              " partner number
        gv_idx(2)      type n,                  " index
        gv_scr_fld(20) TYPE c,
        p_infile  LIKE /sapdmc/lsoinp-filename. " FILE

*---------------------------------------------------------------------*
* CONSTANT DECLARATIONS                                               *
*---------------------------------------------------------------------*
CONSTANTS : c_rb_pre         TYPE c VALUE  ' ',      " SPACE
            c_filetype(10)   TYPE c VALUE 'ASC',     " FILE TYPE
            c_9999999999     type kunnr value '9999999999',
            c_x              TYPE c VALUE 'X'.       " CHECKED

*---------------------------------------------------------------------*
* CONSTANT DECLARATIONS FOR BDC
*---------------------------------------------------------------------*
CONSTANTS : c_group     LIKE apqi-groupid VALUE 'Y_EQUI_PARTN',
            c_trans     LIKE tstc-tcode   VALUE 'IE02'.

*--------- S T A R T   O F   M A I N   P R O C E S S I N G -----------*

*---------------------------------------------------------------------*
* START-OF-SELECTION             - START OF DATABASE ACCESS           *
*---------------------------------------------------------------------*
START-OF-SELECTION.

* - GET THE FILENAME OF THE CONVERT FILE AND
* - IMPORT /SAPDMC/LSMEMORY FROM MEMORY ID '/SAPDMC/LSMW'.

  IMPORT /sapdmc/lsmemory FROM MEMORY ID '/SAPDMC/LSMW'.

  PERFORM get_lsmw IN PROGRAM yam_common_routines
                    USING  /sapdmc/lsmemory-project
                           /sapdmc/lsmemory-subproj
                           /sapdmc/lsmemory-object
                           c_x
                   CHANGING p_infile.

  IF sy-subrc <> 0.
    MESSAGE e007.
  ENDIF.


* - READ INPUT FILE FROM PRES/APPL SERVER INTO INTERNAL TABLE
  PERFORM get_input_file.

* - SPILT INTERNAL TABLE RECORDS INTO HEADER AND DETAIL RECORDS
  PERFORM split_file.


*---------------------------------------------------------------------*
* END-OF-SELECTION               - END OF DATABASE ACCESS             *
*---------------------------------------------------------------------*
END-OF-SELECTION.

*&---------------------------------------------------------------------*
*&      FORM  GET_INPUT_FILE
*&---------------------------------------------------------------------*
*       TEXT
*----------------------------------------------------------------------*
*  -->  P1        TEXT
*  <--  P2        TEXT
*----------------------------------------------------------------------*
FORM get_input_file .

  IF c_rb_pre = c_x.
* - FILE READ FROM PRESENTATION SERVER
    PERFORM get_from_pres IN PROGRAM yam_common_routines
                                     TABLES  i_upload
                                     USING   p_infile
                                             c_filetype
                                             c_x.

  ELSE.
* - FILE READ FROM APPLICATION SERVER
    PERFORM get_from_appl IN PROGRAM yam_common_routines
                                     TABLES i_upload
                                     USING  p_infile.

  ENDIF.

ENDFORM.                    " GET_INPUT_FILE

*&---------------------------------------------------------------------*
*&      FORM  SPLIT_FILE
*&---------------------------------------------------------------------*
*   SPLIT DATA FILE INTO HEADER AND DETAIL LEVEL FILES
*   HEADER FILE STORES THE TOTAL NUMBER OF LINE ITEMS WITHIN
*   THE RECORD.
*----------------------------------------------------------------------*
*  -->  P1        TEXT
*  <--  P2        TEXT
*----------------------------------------------------------------------*
FORM split_file .

* - PASS THE RECORDS FROM FILE TO A COMMON INTERNAL TABLE
  LOOP AT i_upload.
    MOVE i_upload-v_text+00(18)    TO i_final_record-matnr.
    move i_upload-v_text+18(18)    to i_final_record-sernr.
    move i_upload-v_text+36(10)    to i_final_record-kunnr.
    move i_upload-v_text+46(02)    to i_final_record-parvw.
    move i_upload-v_text+48(04)    to i_final_record-vkorg.
    move i_upload-v_text+52(02)    to i_final_record-vtweg.
    move i_upload-v_text+54(02)    to i_final_record-spart.
    APPEND i_final_record.

* - PASS THE HEADER DETAILS TO THE HEADER INTERNAL TABLE
    MOVE-CORRESPONDING i_final_record TO i_header.
    APPEND i_header.
    CLEAR  i_header.

* - PASS THE ITEM DETAILS TO THE DETAIL INTERNAL TABLE
    MOVE-CORRESPONDING i_final_record TO i_detail.
    APPEND i_detail.
    CLEAR  i_detail.
  ENDLOOP.

* - SORT THE HEADER INTERNAL TABLE AND DELETE THE DUPLICATES
  SORT i_header BY matnr sernr.
  DELETE ADJACENT DUPLICATES FROM i_header COMPARING matnr sernr.


* - CHECK IF HEADER IS NOT BLANK
  IF NOT i_header[] IS INITIAL.

    perform open_group.

    LOOP AT i_header.

* -   CHECK FOR THE EQUIPMENTS EXISTANCE
      PERFORM EQUIP_UPDATE_PARTNERS.

    ENDLOOP.

    perform close_group.

  ENDIF.

ENDFORM.                    " SPLIT_FILE

*&---------------------------------------------------------------------*
*&   FORM EQUIP_UPDATE_PARTNERS
*&---------------------------------------------------------------------*
*       TEXT
*----------------------------------------------------------------------*
*  -->  P1        TEXT
*  <--  P2        TEXT
*----------------------------------------------------------------------*
FORM EQUIP_UPDATE_PARTNERS.

  CLEAR: g_count.

* CHECK FOR THE MULTIPLE SALES DOCUMENT VERSION
  SELECT COUNT( * ) FROM equi
          INTO (g_count) WHERE sernr EQ i_header-sernr
                           AND matnr EQ i_header-matnr
                         GROUP BY sernr matnr.
  ENDSELECT.

* CHECK IF 1 ENTRY EXISTS
  IF g_count = 1.

*.. Select Equipment Number
    SELECT SINGLE equnr INTO  gv_equnr
                        FROM  equi
                        WHERE matnr EQ i_header-matnr
                          AND sernr EQ i_header-sernr.

    PERFORM prep_data.
    PERFORM bdc_filldata.
    perform bdc_transaction using c_trans.
    refresh i_bdcdata.

* IF NO ENTRY EXISTS THEN EXIT PROCESSING THIS ENTRY
  ELSEIF g_count < 1.

    write: / text-023, i_header-matnr, i_header-sernr.

* IF MULTIPLE ENTRIES FOUND THEN DO NOT PROCESS
  ELSEIF g_count > 1.

    WRITE : / text-024, i_header-matnr, i_header-sernr.

  ENDIF.

ENDFORM.                    "EQUIP_UPDATE_PARTNERS

*&---------------------------------------------------------------------*
*&      FORM  PREP_DATA
*&---------------------------------------------------------------------*
*       TEXT
*----------------------------------------------------------------------*
*  -->  P1        TEXT
*  <--  P2        TEXT
*----------------------------------------------------------------------*
FORM prep_data.

* Select the appropriate partners
  refresh i_partners.

  loop at i_detail where sernr = i_header-sernr
                     and matnr = i_header-matnr.

    if i_detail-parvw = 'WE'.
*.... look for default ship-to partner
      select kunn2 into gv_kunn2
                   up to 1 rows
                   from knvp
                   where kunnr = i_detail-kunnr
                     and vkorg = i_detail-vkorg
                     and vtweg = i_detail-vtweg
                     and spart = i_detail-spart
                     and parvw = i_detail-parvw
                     and defpa = c_x.
      endselect.
      if sy-subrc <> 0.
        select kunn2 into gv_kunn2
                     up to 1 rows
                     from knvp
                     where kunnr = i_detail-kunnr
                       and vkorg = i_detail-vkorg
                       and vtweg = i_detail-vtweg
                       and spart = i_detail-spart
                       and parvw = i_detail-parvw.
        endselect.
      endif.
    else.
      select kunn2 into gv_kunn2
                   up to 1 rows
                   from knvp
                   where kunnr = i_detail-kunnr
                     and vkorg = i_detail-vkorg
                     and vtweg = i_detail-vtweg
                     and spart = i_detail-spart
                     and parvw = i_detail-parvw.
      endselect.
    endif.

    if sy-subrc = 0.
      move gv_equnr       to i_partners-equnr.
      move i_detail-parvw to i_partners-parvw.
      move gv_kunn2       to i_partners-kunn2.
      append i_partners.
      clear i_partners.
    else.
*.... Partner function was not found --> fill with 9999999999
*.... which will block B-I session
      move gv_equnr       to i_partners-equnr.
      move i_detail-parvw to i_partners-parvw.
      move c_9999999999   to i_partners-kunn2.
      append i_partners.
      clear i_partners.
    endif.

  endloop.

ENDFORM.                    " PREP_DATA

*&---------------------------------------------------------------------*
*&      Form  bdc_filldata
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM bdc_filldata .

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPMIEQ0' '0100' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: '' '' '' 'RM63E-EQUNR' gv_equnr
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '/00'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

* Overview
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPMIEQ0' '0101' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=PART'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

* Partners
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPLIPAR' '0200' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  clear gv_idx.
  loop at i_partners.
    add 1 to gv_idx.
    clear gv_scr_fld.
    concatenate 'IHPA-PARVW(' gv_idx ')' into gv_scr_fld.
    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                       USING: '' '' '' gv_scr_fld i_partners-parvw
                       CHANGING struct_bdcdata.

    APPEND struct_bdcdata TO i_bdcdata.
    CLEAR  struct_bdcdata.

    clear gv_scr_fld.
    concatenate 'IHPA-PARNR(' gv_idx ')' into gv_scr_fld.
    PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                       USING: '' '' '' gv_scr_fld i_partners-kunn2
                       CHANGING struct_bdcdata.

    APPEND struct_bdcdata TO i_bdcdata.
    CLEAR  struct_bdcdata.
  endloop.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=BACK'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

* Save
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPMIEQ0' '0101' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=BU'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

ENDFORM.        "bdc_filldata

*&---------------------------------------------------------------------*
*&      Form  BDC_TRANSACTION
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_C_TRANS  text
*----------------------------------------------------------------------*
FORM bdc_transaction USING p_c_trans.

  CALL FUNCTION 'BDC_INSERT'
    EXPORTING
      tcode            = p_c_trans
    TABLES
      dynprotab        = i_bdcdata
    EXCEPTIONS
      internal_error   = 1
      not_open         = 2
      queue_error      = 3
      tcode_invalid    = 4
      printing_invalid = 5
      posting_invalid  = 6
      OTHERS           = 7.
  .
  IF sy-subrc <> 0.
    MESSAGE e014.
  ENDIF.

ENDFORM.                    " BDC_TRANSACTION

*&---------------------------------------------------------------------*
*&      Form  open_group
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM open_group .

  CALL FUNCTION 'BDC_OPEN_GROUP'
    EXPORTING
      client = sy-mandt
      group  = c_group
      user   = sy-uname
      keep   = ' '.
  .
  IF sy-subrc <> 0.
    MESSAGE e013. " OPEN SESSION FAILED
  ENDIF.

ENDFORM.                    " open_group

*&---------------------------------------------------------------------*
*&      Form  close_group
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM close_group .

  CALL FUNCTION 'BDC_CLOSE_GROUP'.

ENDFORM.                    " close_group

*Text symbol text£º
*023:Error !!! Equipment does not exist for this entry

*024:Error !!! Multiple equipments found for this entry
*Selection text£º
*P_FILE:D       File name
