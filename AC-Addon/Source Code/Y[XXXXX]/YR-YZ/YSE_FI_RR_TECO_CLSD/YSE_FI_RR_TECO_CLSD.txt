*&---------------------------------------------------------------------*
*& Report  YSE_FI_RR_TECO_CLSD
*&
*&---------------------------------------------------------------------*
*&                                                                     *
*& Change contract status from TECO to CLSD (on item level)            *
*&                                                                     *
*&---------------------------------------------------------------------*
*  Author                : Jules Smets
*  Date                  : 04.05.2012
*  Change Request Number : CR2012
*  Transport request Nr. : CD1K971626
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD.NR. | DATE       | NAME           | CORRECTION NR. | CHANGE REF. *
*----------------------------------------------------------------------*
* MOD-001 |            |                | CD1K9xxxxx     | CRxxxx      *
*         |                                                            *
*----------------------------------------------------------------------*

************************************************************************

REPORT  yse_fi_rr_teco_clsd.

TABLES: jest.

TYPES: BEGIN OF ty_vbap,
         vbeln     TYPE vbeln,
         posnr     TYPE posnr,
         objnr     TYPE j_objnr,
       END OF ty_vbap.

TYPES: BEGIN OF ty_jest,
         objnr     TYPE j_objnr,
         stat      TYPE j_estat,
       END OF ty_jest.

* Internal table with contracts
DATA: gt_vbap TYPE STANDARD TABLE OF ty_vbap
                   WITH HEADER LINE.

* Internal table with processed contracts
DATA: gt_vbaph TYPE HASHED TABLE OF ty_vbap
                    WITH UNIQUE KEY vbeln posnr
                    WITH HEADER LINE.

* Internal table with status
DATA: gt_jest TYPE HASHED TABLE OF ty_jest
                   WITH UNIQUE  KEY objnr
                   WITH HEADER LINE.

DATA: gv_mode(1)   TYPE c   VALUE 'N',
      gv_msgtxt    LIKE t100-text,
      gv_file      TYPE string.

DATA: i_bdcdata LIKE bdcdata  OCCURS 0  WITH HEADER LINE,
      struct_bdcdata TYPE bdcdata,
      gt_err    LIKE bdcmsgcoll  OCCURS 0  WITH HEADER LINE.

CONSTANTS: gc_g(1)    TYPE c         VALUE 'G',
           gc_clsd    TYPE j_status  VALUE 'I0046',
           gc_teco    TYPE j_status  VALUE 'I0045'.

* Selections
SELECTION-SCREEN  BEGIN OF BLOCK sel  WITH FRAME  TITLE text-s01.
PARAMETERS:     p_bukrs    TYPE bukrs  OBLIGATORY
                                       MEMORY ID buk.
SELECTION-SCREEN SKIP.
PARAMETERS:     p_test     AS CHECKBOX  DEFAULT 'X'.
SELECTION-SCREEN  END OF BLOCK sel.
SELECTION-SCREEN SKIP.
SELECTION-SCREEN  BEGIN OF BLOCK file  WITH FRAME  TITLE text-s02.
PARAMETERS: p_ifile  LIKE rlgrap-filename
                          OBLIGATORY
                          DEFAULT 'C:\SAP\CLSD\TECO_XXXX.txt'.
SELECTION-SCREEN  END OF BLOCK file.


*----------------------------------------------------------------------*
* On the selection screen                                              *
*----------------------------------------------------------------------*

*------------------  F4-Help for filename -----------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_ifile.

  CALL FUNCTION 'WS_FILENAME_GET'
   EXPORTING
     def_filename           = p_ifile
     def_path               = 'C:\SAP\CLSD\'
     mask                   = ',*,*.txt.'
     mode                   = 'O'
     title                  = text-s03
   IMPORTING
     filename               = p_ifile
*     RC                     =
   EXCEPTIONS
     inv_winsys             = 1
     no_batch               = 2
     selection_cancel       = 3
     selection_error        = 4
     OTHERS                 = 5
            .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON p_bukrs.

  PERFORM  check_authorization.


*&---------------------------------------------------------------------*
*&  Main program                                                       *
*&---------------------------------------------------------------------*

INITIALIZATION.


*----------------------------------------------------------------------*
START-OF-SELECTION.

* Select data
  PERFORM select_data.
* Check anything selected
  IF gt_vbap[] IS INITIAL.
    MESSAGE ID 'YSE_GENERAL' TYPE 'S' NUMBER '000'
            WITH 'No contracts selected'(e01).
    RETURN.
  ENDIF.

* Process data
  PERFORM process_data.

  ULINE.


*----------------------------------------------------------------------*
*       FORMS
*----------------------------------------------------------------------*

*&---------------------------------------------------------------------*
*&      FORM  CHECK_AUTHORIZATION
*&---------------------------------------------------------------------*
*       Check authorizations
*----------------------------------------------------------------------*
FORM check_authorization .

  AUTHORITY-CHECK OBJECT 'F_BKPF_BUK'
           ID 'ACTVT' DUMMY
           ID 'BUKRS' FIELD p_bukrs.

  IF sy-subrc = 4.
*     No authorisation to display data
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '008' WITH p_bukrs.
    EXIT.
  ELSEIF sy-subrc <> 0.
*     Error checking authorization.
    MESSAGE ID 'YSE_GENERAL' TYPE 'E' NUMBER '004'.
    EXIT.
  ENDIF.

ENDFORM.                    " CHECK_AUTHORIZATION

*&---------------------------------------------------------------------*
*&      Form  SELECT_DATA
*&---------------------------------------------------------------------*
*       Select data
*----------------------------------------------------------------------*
FORM select_data .

* Input file
  PERFORM upload_data.
  CHECK NOT gt_vbap[] IS INITIAL.
  SORT gt_vbap BY vbeln posnr.
  DELETE ADJACENT DUPLICATES FROM gt_vbap COMPARING vbeln posnr.

* Contracts
  SELECT h~vbeln i~posnr i~objnr
         INTO TABLE gt_vbaph
         FROM vbap AS i
         INNER JOIN vbak AS h
                    ON i~vbeln = h~vbeln
         FOR ALL ENTRIES IN gt_vbap
         WHERE i~vbeln    = gt_vbap-vbeln
           AND i~posnr    = gt_vbap-posnr
           AND h~bukrs_vf =  p_bukrs
           AND h~vbtyp    =  gc_g.

* Status
  SORT gt_vbap BY objnr.

  SELECT objnr stat
         INTO TABLE gt_jest
         FROM jest
         FOR ALL ENTRIES IN gt_vbap
         WHERE objnr = gt_vbap-objnr
           AND stat  =  gc_teco
           AND inact =  '  '.

  SORT gt_vbap BY vbeln posnr.

ENDFORM.                    " SELECT_DATA

*&---------------------------------------------------------------------*
*&      Form  UPLOAD_DATA
*&---------------------------------------------------------------------*
*       Upload input file
*----------------------------------------------------------------------*
FORM upload_data .

  REPLACE 'XXXX' IN p_ifile WITH p_bukrs.
  gv_file = p_ifile.

  CALL FUNCTION 'GUI_UPLOAD'
    EXPORTING
      filename                = gv_file
      filetype                = 'ASC'
    TABLES
      data_tab                = gt_vbap
    EXCEPTIONS
      file_open_error         = 1
      file_read_error         = 2
      no_batch                = 3
      gui_refuse_filetransfer = 4
      invalid_type            = 5
      no_authority            = 6
      unknown_error           = 7
      bad_data_format         = 8
      header_not_allowed      = 9
      separator_not_allowed   = 10
      header_too_long         = 11
      unknown_dp_error        = 12
      access_denied           = 13
      dp_out_of_memory        = 14
      disk_full               = 15
      dp_timeout              = 16
      OTHERS                  = 17.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " UPLOAD_DATA

*&---------------------------------------------------------------------*
*&      Form  PROCESS_DATA
*&---------------------------------------------------------------------*
*       Proces data
*----------------------------------------------------------------------*
FORM process_data .

  LOOP AT gt_vbap.

*   Check valid contract
    READ TABLE gt_vbaph WITH TABLE KEY vbeln = gt_vbap-vbeln
                                       posnr = gt_vbap-posnr.
    IF sy-subrc NE 0.
      gv_msgtxt = 'NO valid contract'(e02).
      PERFORM list_msg.
      CONTINUE.
    ENDIF.

*   Check status
    READ TABLE gt_jest WITH TABLE KEY objnr = gt_vbap-objnr.
    IF sy-subrc NE 0.
      gv_msgtxt = 'NO status TECO'(e03).
      PERFORM list_msg.
      CONTINUE.
    ENDIF.

*   Change status
    IF p_test IS INITIAL.
*     Prepare transaction VA42
      PERFORM prepare_va42.
*     Change contract (via transaction VA42)
      PERFORM call_va42.
    ELSE.
      gv_msgtxt = 'Can be processed'(i01).
      PERFORM list_msg.
    ENDIF.

  ENDLOOP.

ENDFORM.                    " PROCESS_DATA

*&---------------------------------------------------------------------*
*&      Form  PREPARE_VA42
*&---------------------------------------------------------------------*
*       Prepare transaction VA42
*----------------------------------------------------------------------*
FORM prepare_va42 .

  REFRESH: i_bdcdata, gt_err.

* Header
  PERFORM bdc_dynpro      USING 'SAPMV45A' '0102'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '/00'.
  PERFORM bdc_field       USING 'VBAK-VBELN' gt_vbap-vbeln.

* Items
  PERFORM bdc_dynpro      USING 'SAPMV45A' '4001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=POPO'.
* Select item (on top)
  PERFORM bdc_dynpro      USING 'SAPMV45A' '0251'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=POSI'.
  PERFORM bdc_field       USING 'RV45A-POSNR' gt_vbap-posnr.
* Go to item status
  PERFORM bdc_dynpro      USING 'SAPMV45A' '4001'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=PSTA'.
  PERFORM bdc_field       USING 'RV45A-VBAP_SELKZ(01)' 'X'.
* Item system status
  PERFORM bdc_dynpro      USING 'SAPMV45A' '4003'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=STAE'.
* Reset status 'Technically completed'
  PERFORM bdc_dynpro      USING 'SAPMV45A' '0258'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=BUTA'.
* Item system status
  PERFORM bdc_dynpro      USING 'SAPMV45A' '4003'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=STAE'.
* Set status 'Completed'
  PERFORM bdc_dynpro      USING 'SAPMV45A' '0258'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=STAB'.
* Save
  PERFORM bdc_dynpro      USING 'SAPMV45A' '4003'.
  PERFORM bdc_field       USING 'BDC_OKCODE' '=SICH'.

ENDFORM.                    " PREPARE_VA42

*----------------------------------------------------------------------*
*        Start new screen                                              *
*----------------------------------------------------------------------*
FORM bdc_dynpro USING program dynpro.

  CLEAR i_bdcdata.
  i_bdcdata-program  = program.
  i_bdcdata-dynpro   = dynpro.
  i_bdcdata-dynbegin = 'X'.
  APPEND i_bdcdata.

ENDFORM.                    "BDC_DYNPRO

*----------------------------------------------------------------------*
*        Insert field                                                  *
*----------------------------------------------------------------------*
FORM bdc_field USING fnam fval.

  CLEAR i_bdcdata.
  i_bdcdata-fnam = fnam.
  i_bdcdata-fval = fval.
  APPEND i_bdcdata.

ENDFORM.                    "bdc_field

*&---------------------------------------------------------------------*
*&      Form  CALL_VA42
*&---------------------------------------------------------------------*
*       Call transaction VA42 (Change contract)
*----------------------------------------------------------------------*
FORM call_va42 .

  CALL TRANSACTION 'VA42' USING i_bdcdata
                          MODE gv_mode
                          UPDATE 'S'
                          MESSAGES INTO gt_err.

* Check for errors
  LOOP AT gt_err WHERE msgtyp = 'E'
                    OR msgtyp = 'A'.
*   List
    CLEAR gv_msgtxt.
    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*        SPRSL                   = SY-LANGU
        arbgb                   = gt_err-msgid
        msgnr                   = gt_err-msgnr
        msgv1                   = gt_err-msgv1
        msgv2                   = gt_err-msgv2
        msgv3                   = gt_err-msgv3
        msgv4                   = gt_err-msgv4
      IMPORTING
        msgtext                 = gv_msgtxt
      EXCEPTIONS
        message_not_found       = 1
        OTHERS                  = 2.
    PERFORM list_msg.
  ENDLOOP.

* No errors
  IF sy-subrc NE 0.
*   List
    gv_msgtxt = 'Status changed from TECO to TCLSD'(i02).
    PERFORM list_msg.
  ENDIF.

ENDFORM.                    " CALL_VA42

*&---------------------------------------------------------------------*
*&      Form  LIST_MSG
*&---------------------------------------------------------------------*
*       List message
*----------------------------------------------------------------------*
FORM list_msg .

  WRITE: /01 gt_vbap-vbeln,
          12 gt_vbap-posnr,
          20 gv_msgtxt.

ENDFORM.                    " LIST_MSG

*List header: Column headings£º

*001:Contract   Item    Message
*Text symbol text£º
*E01:No contracts selected
*E02:NO valid contract
*E03:NO status TECO
*I01:Can be processed
*I02:Status changed from TECO to TCLSD
*S01:Selections
*S02:File

*S03:Select File
*Selection text£º
*P_BUKRS:D       .
*P_IFILE:        Input File
*P_TEST:        Test Run
