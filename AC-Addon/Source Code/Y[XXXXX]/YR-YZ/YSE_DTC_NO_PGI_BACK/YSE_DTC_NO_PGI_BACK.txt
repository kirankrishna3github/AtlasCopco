*----------------------------------------------------------------------*
* PROGRAM ID           : YSE_DTC_NO_PGI                                *
* PROGRAM TITLE        : Reporting of CC DN without PGI document       *
* AUTHOR               : Anda Wu                                       *
* DATE                 : 23/05/2014                                    *
* DEVELOPMENT ID       :  CD1K981658                                   *
* CHANGE REQUEST NUMBER:   CR3262                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG
*----------------------------------------------------------------------*
* MOD. NO.|  DATE      | NAME               | CORR. NR.  | CHANGE REF. *
************************************************************************
REPORT  yse_dtc_no_pgi_back.

************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES:   likp,
          eban.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:

  BEGIN OF ty_likp,
    vbeln         TYPE likp-vbeln,
    erdat         TYPE likp-erdat,
    vkorg         TYPE likp-vkorg,
  END OF ty_likp,

  BEGIN OF ty_lips,
    vbeln         TYPE lips-vbeln,
    posnr         TYPE lips-posnr,
    pstyv         TYPE lips-pstyv,
    werks         TYPE lips-werks,
  END OF ty_lips,

  BEGIN OF ty_vbfa,
    vbelv         TYPE vbfa-vbelv,
    posnv         TYPE vbfa-posnv,
    vbeln         TYPE vbfa-vbeln,
    posnn         TYPE vbfa-posnn,
    vbtyp_n       TYPE vbfa-vbtyp_n,
  END OF ty_vbfa,

  BEGIN OF ty_vbep,
    vbeln         TYPE vbep-vbeln,
    posnr         TYPE vbep-posnr,
    banfn         TYPE vbep-banfn,
    bnfpo         TYPE vbep-bnfpo,
    flief         TYPE eban-flief,
  END OF ty_vbep,

  BEGIN OF  ty_alv_output,
    sel           TYPE char1,
    vbeln         TYPE lips-vbeln,
    posnr         TYPE lips-posnr,
    vbeln_f       TYPE vbfa-vbeln,
    vbtyp_n       TYPE vbfa-vbtyp_n,
  END OF ty_alv_output.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_likp         TYPE STANDARD TABLE OF ty_likp,
  gt_lips         TYPE STANDARD TABLE OF ty_lips,
  gt_vbfa_j       TYPE STANDARD TABLE OF ty_vbfa,
  gt_vbfa_r       TYPE STANDARD TABLE OF ty_vbfa,
  gt_vbep         TYPE STANDARD TABLE OF ty_vbep,
  gt_fieldcat     TYPE slis_t_fieldcat_alv,
  gt_alv_output   TYPE STANDARD TABLE OF ty_alv_output.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_disvar           TYPE disvariant,
  gs_layout           TYPE slis_layout_alv.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_text             TYPE string,
  gv_save             TYPE char1,
  gv_pf_status        TYPE slis_formname,
  gv_callback         TYPE slis_formname.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x                TYPE char1 VALUE 'X',
  gc_pstyv_zdtc       TYPE lips-pstyv   VALUE 'ZDTC',
  gc_pstyv_zas2       TYPE lips-pstyv   VALUE 'ZAS2',
  gc_werks_cn90       TYPE lips-werks   VALUE 'CN90',
  gc_vbtyp_j          TYPE char1 VALUE 'J',
  gc_vbtyp_r          TYPE char1 VALUE 'R',
  gc_type_e           TYPE char1 VALUE 'E'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-t01.

PARAMETERS:
  p_vari    TYPE disvariant-variant
            MODIF ID m1.         "Dispaly Variant

SELECT-OPTIONS:
  s_vkorg     FOR   likp-vkorg,             "Sales Organization
  s_flief     FOR   eban-flief,             "Desired Vendor
  s_erdat     FOR   likp-erdat.             "Delivery Creation Date

SELECTION-SCREEN END OF BLOCK b1.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initilization
  PERFORM frm_init.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN OUTPUT                                          *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.
* screen initial set
  PERFORM frm_screen_set.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN                                                 *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.
* selection screen check
*  PERFORM check_parameter.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN ON VALUE-REQUEST                                *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
* F4 help of ALV layout
  PERFORM f4_for_variant.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.

* progress indicator
  PERFORM process_info.
* Extract data from table and generate AVL output Data
  PERFORM extract_data.

* alv property set
  PERFORM alv_prop_set.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
* ALV display
  PERFORM alv_display.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do initilization
*----------------------------------------------------------------------*
FORM frm_init .

  REFRESH:
            gt_alv_output.

  CLEAR:    gs_disvar,
            gs_layout,
            gv_text,
            gv_save,
            gv_callback.

  s_vkorg-sign    = 'I'.
  s_vkorg-option  = 'EQ'.
  s_vkorg-low     = 'CN01'.
  APPEND s_vkorg.

  s_vkorg-sign    = 'I'.
  s_vkorg-option  = 'EQ'.
  s_vkorg-low     = 'CN05'.
  APPEND s_vkorg.

  s_flief-sign    = 'I'.
  s_flief-option  = 'EQ'.
  s_flief-low     = '0102000722'.
  APPEND s_flief.

ENDFORM.                    " FRM_INIT

*&---------------------------------------------------------------------*
*&      Form  FRM_SCREEN_SET
*&---------------------------------------------------------------------*
*       screen initial set
*----------------------------------------------------------------------*
FORM frm_screen_set .

  LOOP AT SCREEN.
    IF screen-group1 = 'M1'.
      screen-invisible = '1'.
      screen-input   = '0'.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " FRM_SCREEN_SET

*&---------------------------------------------------------------------*
*&      Form  F4_FOR_VARIANT
*&---------------------------------------------------------------------*
*       F4 help of ALV layout
*----------------------------------------------------------------------*
FORM f4_for_variant .

  DATA: ls_disvar TYPE disvariant,
        lv_exit   TYPE char1.

  ls_disvar-report = sy-repid.

  CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
    EXPORTING
      is_variant    = ls_disvar
      i_save        = gv_save
    IMPORTING
      e_exit        = lv_exit
      es_variant    = gs_disvar
    EXCEPTIONS
      not_found     = 1
      program_error = 2
      OTHERS        = 3.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    IF lv_exit = space.
      p_vari = gs_disvar-variant.
    ENDIF.
  ENDIF.

ENDFORM.                    " F4_FOR_VARIANT

*&---------------------------------------------------------------------*
*&      Form  PROCESS_INFO
*&---------------------------------------------------------------------*
*       progress indicator
*----------------------------------------------------------------------*
FORM process_info .

  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    CONCATENATE 'Start report:'(004) sy-repid
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-005.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 1
        text       = gv_text.
  ENDIF.

ENDFORM.                    " PROCESS_INFO
*&---------------------------------------------------------------------*
*&      Form  GET_MASTER_DATA
*&---------------------------------------------------------------------*
*       get data
*----------------------------------------------------------------------*
FORM get_master_data .

  SELECT   vbeln
           erdat
           vkorg
    FROM   likp
    INTO TABLE gt_likp
    WHERE vkorg IN s_vkorg
      AND erdat IN s_erdat.
  IF sy-subrc <> 0.
    CLEAR: gv_text.
    gv_text = text-051.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  SELECT    vbeln
            posnr
            pstyv
            werks
    FROM    lips
    INTO TABLE gt_lips
    FOR ALL ENTRIES IN gt_likp
    WHERE vbeln  = gt_likp-vbeln
      AND (   pstyv  = gc_pstyv_zdtc
           OR pstyv  = gc_pstyv_zas2 )
      AND werks  = gc_werks_cn90.
  IF sy-subrc <> 0.
    CLEAR: gv_text.
    gv_text = text-053.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  SELECT  vbelv
          posnv
          vbeln
          posnn
          vbtyp_n
    FROM vbfa
    INTO TABLE gt_vbfa_j
    FOR ALL ENTRIES IN gt_lips
    WHERE vbeln = gt_lips-vbeln
      AND posnn = gt_lips-posnr
      AND vbtyp_n = gc_vbtyp_j.
  IF gt_vbfa_j IS NOT INITIAL.
    SELECT   vbep~vbeln
             vbep~posnr
             vbep~banfn
             vbep~bnfpo
             eban~flief
      FROM vbep
        INNER JOIN eban
        ON  vbep~banfn = eban~banfn
        AND vbep~bnfpo = eban~bnfpo
      INTO TABLE gt_vbep
      FOR ALL ENTRIES IN gt_vbfa_j
      WHERE vbep~vbeln = gt_vbfa_j-vbelv
        AND vbep~posnr = gt_vbfa_j-posnv
        AND eban~flief IN s_flief.
    IF sy-subrc <> 0.
      CLEAR: gv_text.
      gv_text = text-055.
      MESSAGE s000(yse_sales_log) WITH gv_text
        DISPLAY LIKE gc_type_e.
      LEAVE LIST-PROCESSING.
    ENDIF.
  ENDIF.

  SELECT  vbelv
          posnv
          vbeln
          posnn
          vbtyp_n
    FROM vbfa
    INTO TABLE gt_vbfa_r
    FOR ALL ENTRIES IN gt_lips
    WHERE vbeln = gt_lips-vbeln
      AND posnn = gt_lips-posnr
      AND vbtyp_n = gc_vbtyp_r.

ENDFORM.                    " GET_MASTER_DATA
*&---------------------------------------------------------------------*
*&      Form  ALV_PROP_SET
*&---------------------------------------------------------------------*
*       alv property set
*----------------------------------------------------------------------*
FORM alv_prop_set .

* Display ALV process indicator
  PERFORM alv_progress_indicator.

* FIELDCAT set
  PERFORM alv_fieldcat_set.

* LAYOUT set
  PERFORM alv_layout_set.

** Set Others
  PERFORM alv_others_set.

ENDFORM.                    " ALV_PROP_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_lines(10)  TYPE c.

  IF gt_alv_output IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-060.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  IF sy-batch IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-010.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 80
        text       = gv_text.
  ELSE.
    CLEAR: gv_text.
    DESCRIBE TABLE gt_alv_output LINES lv_lines.
    CONCATENATE 'Totally'(015) lv_lines 'entries are generated'(020)
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ENDIF.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = gv_pf_status
      i_callback_user_command  = gv_callback
      is_layout                = gs_layout
      it_fieldcat              = gt_fieldcat
      i_save                   = gv_save
      is_variant               = gs_disvar
    TABLES
      t_outtab                 = gt_alv_output
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY
*&---------------------------------------------------------------------*
*&      Form  ALV_PROGRESS_INDICATOR
*&---------------------------------------------------------------------*
*       Display ALV process indicator
*----------------------------------------------------------------------*
FORM alv_progress_indicator .

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to set ALV property'(011).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-011.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 75
        text       = gv_text.
  ENDIF.

ENDFORM.                    " ALV_PROGRESS_INDICATOR
*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM alv_fieldcat_set .

  DATA:
     lv_linecnt  TYPE i,
     ls_fieldcat TYPE slis_fieldcat_alv.

  CLEAR lv_linecnt.
* DN Number
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'VBELN'.
  ls_fieldcat-ref_fieldname = 'VBELN'.
  ls_fieldcat-ref_tabname = 'LIPS'.
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

* DN Item Number
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'POSNR'.
  ls_fieldcat-ref_fieldname = 'POSNR'.
  ls_fieldcat-ref_tabname = 'LIPS'.
  APPEND ls_fieldcat TO gt_fieldcat.

* Following document number
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'VBELN_F'.
  ls_fieldcat-seltext_s = 'Foll.Doc'(025).
  ls_fieldcat-seltext_m = 'Follow Document'(026).
  ls_fieldcat-seltext_l = 'Following Document Number'(027).
  APPEND ls_fieldcat TO gt_fieldcat.

* Following document category
  CLEAR ls_fieldcat.
  lv_linecnt = lv_linecnt + 1.
  ls_fieldcat-col_pos = lv_linecnt.
  ls_fieldcat-fieldname = 'VBTYP_N'.
  ls_fieldcat-seltext_s = 'Foll.Cate'(028).
  ls_fieldcat-seltext_m = 'Follow Category'(030).
  ls_fieldcat-seltext_l = 'Following document category'(031).
  ls_fieldcat-fix_column = gc_x.
  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   " Auto optimize column width
  gs_layout-detail_popup = gc_x.        " Show detail screen
  gs_layout-box_fieldname  = 'SEL'.

ENDFORM.                    " ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*&      Form  GEN_ALV_DATA
*&---------------------------------------------------------------------*
*       generate alv data
*----------------------------------------------------------------------*
FORM gen_alv_data .

  DATA: ls_lips     TYPE  ty_lips,
        ls_vbfa_j   TYPE  ty_vbfa,
        ls_vbfa_r   TYPE  ty_vbfa,
        ls_vbep     TYPE  ty_vbep,
        ls_alv_out  TYPE  ty_alv_output.

  LOOP AT gt_lips INTO ls_lips.
    CLEAR: ls_alv_out.
    ls_alv_out-vbeln      = ls_lips-vbeln.
    ls_alv_out-posnr      = ls_lips-posnr.
    READ TABLE  gt_vbfa_j INTO ls_vbfa_j
        WITH KEY  vbeln   = ls_lips-vbeln
                  posnn   = ls_lips-posnr.
    IF sy-subrc <> 0.
      CONTINUE.
    ENDIF.
    READ TABLE gt_vbep  INTO ls_vbep
        WITH KEY vbeln = ls_vbfa_j-vbelv
                 posnr = ls_vbfa_j-posnv.
    IF sy-subrc <> 0.
      CONTINUE.
    ENDIF.
    READ TABLE  gt_vbfa_r INTO ls_vbfa_r
        WITH KEY   vbelv = ls_lips-vbeln
                   posnv = ls_lips-posnr.
    ls_alv_out-vbeln_f  = ls_vbfa_r-vbeln.
    ls_alv_out-vbtyp_n  = ls_vbfa_r-vbtyp_n.
    APPEND ls_alv_out TO gt_alv_output.
  ENDLOOP.

ENDFORM.                    " GEN_ALV_DATA
*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       SET CALLBACK_USER_COMMAND EVENTS
*----------------------------------------------------------------------*
FORM alv_others_set .

*  gv_pf_status = 'SET_PF_STATS'.
*  gv_callback  = 'CALLBACK_USERCOMMAND'.

ENDFORM.                    " ALV_OTHERS_SET

*&---------------------------------------------------------------------*
*&      Form  SET_PF_STATS
*&---------------------------------------------------------------------*
*       SET PF Status
*----------------------------------------------------------------------*
FORM set_pf_stats  USING rt_extab TYPE slis_t_extab.  "#EC CALLED

  DATA: lt_extab TYPE slis_t_extab,
        ls_extab TYPE slis_extab.

  ls_extab-fcode = 'REFR'.
  APPEND ls_extab TO lt_extab.
  SET PF-STATUS 'STATS_PGI' EXCLUDING lt_extab.

ENDFORM.                    " SET_PF_STATS

*&---------------------------------------------------------------------*
*&      Form  CALLBACK_USERCOMMAND
*&---------------------------------------------------------------------*
*       Handle events
*----------------------------------------------------------------------*
FORM callback_usercommand USING r_ucomm LIKE sy-ucomm "#EC CALLED
                                rs_selfield TYPE slis_selfield.

  DATA: ls_alv_out  TYPE ty_alv_output.
  CASE r_ucomm.
    WHEN 'DEL'.
      READ TABLE gt_alv_output INTO ls_alv_out
        INDEX rs_selfield-tabindex.
    WHEN 'BACK'.
      LEAVE TO SCREEN 0.
    WHEN 'CANC'.
      LEAVE PROGRAM.
    WHEN 'EXIT'.
      LEAVE PROGRAM.
    WHEN: 'REFR'.
      LEAVE TO SCREEN 0.
      REFRESH:  gt_likp,
                gt_lips,
                gt_vbfa_j,
                gt_vbfa_r,
                gt_vbep,
                gt_alv_output.
      PERFORM EXTRACT_DATA.
      PERFORM alv_display.
    WHEN OTHERS.
  ENDCASE.

ENDFORM.                    " CALLBACK_USERCOMMAND
*&---------------------------------------------------------------------*
*&      Form  EXTRACT_DATA
*&---------------------------------------------------------------------*
*       Extract data from table and generate AVL output Data
*----------------------------------------------------------------------*
form EXTRACT_DATA .

* get data
  PERFORM get_master_data.
* generate alv data
  PERFORM gen_alv_data.

endform.                    " EXTRACT_DATA
*&---------------------------------------------------------------------*
*&      Module  STATUS_0100  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
module STATUS_0100 output.

  SET PF-STATUS 'STATS_PGI'.

endmodule.                 " STATUS_0100  OUTPUT
*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_0100  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
module USER_COMMAND_0100 input.

endmodule.                 " USER_COMMAND_0100  INPUT

*Text symbol text£º
*004:Start report:
*005:Start Processing...
*010:Start to display ALV
*011:Begin to set ALV property
*015:Totally
*020:entries are generated
*025:Foll.Doc
*026:Follow Document
*027:Following document number
*028:Foll.Cate
*030:Follow Category
*031:Following document category
*051:No delivery exist (LIKP) !
*053:No delivery item exist (LIPS)!
*055:No  Schedule Line Data exist for vendor 102000722 (VBEP)!
*060:No data extracted by selection screen

*T01:Selection Conditions
*Selection text£º
*P_VARI:        ALV Layout
*S_ERDAT:D       .
*S_FLIEF:D       .
*S_VKORG:D       .
