************************************************************************
* Program ID        : YSE_SALES_PICKS              *
* Program Title     : Program title                                    *
* Author            : Anson Sun                                        *
* Date              : 12/08/2011                               	     *
* Change Request Number:2174                                           *
* Description       : The purpose of this program is Sales Picks Report*
*======================================================================*
* Copied From       : (Cloned Program)                               *
* Title             : (Program Title)                          	     *
* Other Related obj : (Object names)                               *
*======================================================================*
* Change History Log                                             	     *
*----------------------------------------------------------------------*
* Mod. no.|  Date    | Name           | Correction Number  | Change    *
*----------------------------------------------------------------------*
* MOD-001 |15/03/2013| Anda Wu        | CD1K975288         | CR2567    *
*                                                                      *
* Description: Add filed sales group behind sales office               *
*----------------------------------------------------------------------*

REPORT  yse_sales_picks MESSAGE-ID yse_cs_sd_msg.
*----------------------------------------------------------------------*
*       TABLES
*----------------------------------------------------------------------*
TABLES:
  vbrk,                      "Billing Document: Header Data
  vbrp,                      "Billing Document: Item Data
  vbak,                      "Sales Document: Header Data
  vbpa,                      "Sales Document: Partner
  kna1,                      "General Data in Customer Master
  adrc.                      "Addresses
*----------------------------------------------------------------------*
*       TYPE-POOLS
*----------------------------------------------------------------------*
TYPE-POOLS slis.
*----------------------------------------------------------------------*
*       TYPE
*----------------------------------------------------------------------*
TYPES:BEGIN OF ty_output.                                     "MOD-001
        INCLUDE TYPE yse_sales_picks.                         "MOD-001
TYPES: vkgrp TYPE vbak-vkgrp.                                 "MOD-001
TYPES END OF ty_output.                                       "MOD-001
*----------------------------------------------------------------------*
*       DATA
*----------------------------------------------------------------------*
DATA:
  i_output      TYPE TABLE OF ty_output, "yse_sales_picks,    MOD-001
  i_fieldcat    TYPE slis_t_fieldcat_alv.
*----------------------------------------------------------------------*
*       SELECTION-SCREEN
*----------------------------------------------------------------------*
SELECT-OPTIONS:
  s_vkorg FOR vbrk-vkorg NO-EXTENSION NO INTERVALS OBLIGATORY,
  s_vtweg FOR vbrk-vtweg NO-EXTENSION NO INTERVALS DEFAULT '01',
  s_fkdat FOR vbrk-fkdat NO-EXTENSION OBLIGATORY.
*----------------------------------------------------------------------*
*       INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

* initial process
  PERFORM ini_process.
*----------------------------------------------------------------------*
*       AT SELECTION-SCREEN OUTPUT
*----------------------------------------------------------------------*
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-name = 'S_VTWEG-LOW'.
      screen-input = 0.
      MODIFY SCREEN.
    ENDIF.
  ENDLOOP.
*----------------------------------------------------------------------*
*       AT SELECTION-SCREEN
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

* Authority check for Sales Organization
  PERFORM authority_check.
*----------------------------------------------------------------------*
*       START-OF-SELECTION
*----------------------------------------------------------------------*
START-OF-SELECTION.

* main process
  PERFORM main_process.
*&---------------------------------------------------------------------*
*&      Form  ini_process
*&---------------------------------------------------------------------*
*       initial process
*----------------------------------------------------------------------*
FORM ini_process .

  DATA: lrh_fkdat LIKE LINE  OF s_fkdat.
  REFRESH:
    i_output,
    i_fieldcat.
* Set Default Date-to
  IF s_fkdat-high IS INITIAL.
    lrh_fkdat-sign   = 'I'.
    lrh_fkdat-option = 'BT'.
    lrh_fkdat-high   = sy-datum.
    APPEND lrh_fkdat TO s_fkdat.
  ENDIF.

ENDFORM.                    " ini_process
*&---------------------------------------------------------------------*
*&      Form  authority_check
*&---------------------------------------------------------------------*
*       Authority check for Sales Organization
*----------------------------------------------------------------------*
FORM authority_check .

* Authority check for Sales Organization
  AUTHORITY-CHECK OBJECT 'V_VBRK_VKO'
           ID 'VKORG' FIELD s_vkorg-low
           ID 'ACTVT' FIELD '03'.
  IF sy-subrc <> 0.
    MESSAGE e002.
  ENDIF.

ENDFORM.                    " authority_check
*&---------------------------------------------------------------------*
*&      Form  main_process
*&---------------------------------------------------------------------*
*       main process
*----------------------------------------------------------------------*
FORM main_process .

* get business data
  PERFORM get_main_data.
* display ALV
  PERFORM display_alv.

ENDFORM.                    " main_process
*&---------------------------------------------------------------------*
*&      Form  get_main_data
*&---------------------------------------------------------------------*
*       get business data
*----------------------------------------------------------------------*
FORM get_main_data.

* Get business data
  DATA: li_data       TYPE TABLE OF ty_output,"yse_sales_picks,MOD-001
        lrec_data       TYPE ty_output,"yse_sales_picks, MOD-001
        lrec_output     TYPE ty_output,"yse_sales_picks, MOD-001
        lrec_output_bak TYPE ty_output."yse_sales_picks. MOD-001
  DATA: l_gac           TYPE prctr.

  SELECT *
    INTO CORRESPONDING FIELDS OF TABLE li_data
    FROM vbrk INNER JOIN vbrp
      ON ( vbrk~vbeln = vbrp~vbeln )
   WHERE vbrk~vkorg IN s_vkorg         "Sales organisation
     AND vbrk~vtweg IN s_vtweg         "Distribution Channel
     AND vbrk~fkdat IN s_fkdat.        "Billing Date

  SORT li_data BY vbeln posnr aubel aupos matnr.
  LOOP AT li_data INTO lrec_data.
    MOVE-CORRESPONDING lrec_data TO lrec_output.

*   Get GAC
    PERFORM conversion_exit_alpha_output USING lrec_output-prctr
                                      CHANGING lrec_output-gac.
    PERFORM conversion_exit_alpha_input USING lrec_output-gac
                                     CHANGING l_gac.
*   Get PLC
    PERFORM get_plc USING l_gac
                 CHANGING lrec_output-target1.

*   Get Material Group Description
    PERFORM get_wgbez USING lrec_output-matkl
                   CHANGING lrec_output-wgbez.

*   Get Partner: Bill-to
    PERFORM get_partner USING lrec_output-aubel
                              'RE'
                     CHANGING lrec_output-kunre
                              lrec_output-name_re.
*   Get Partner: Sold-to
    PERFORM get_partner USING lrec_output-aubel
                              'AG'
                     CHANGING lrec_output-kunag
                              lrec_output-name_ag.
*   Get Customer
    PERFORM get_customer USING lrec_output-kunag
                      CHANGING lrec_output-bran1
                               lrec_output-vtext
                               lrec_output-land1
                               lrec_output-regio
                               lrec_output-bezei
                               lrec_output-brsch
                               lrec_output-brtxt.
*   Get Partner: Ship-to
    PERFORM get_partner USING lrec_output-aubel
                              'WE'
                     CHANGING lrec_output-kunwe
                              lrec_output-name_we.

*   Set Year&Month
    lrec_output-yyyy_vf = lrec_output-fkdat+0(4).
    lrec_output-mm_vf   = lrec_output-fkdat+4(2).

*   Set Qty&Net Value
    IF lrec_output-shkzg IS NOT INITIAL.
      lrec_output-fkimg = lrec_output-fkimg * -1.
      lrec_output-netwr = lrec_output-netwr * -1.
    ENDIF.

*   Set Picks
    IF lrec_output-shkzg IS NOT INITIAL.
      CLEAR lrec_output-picks.
    ELSE.
      READ TABLE i_output INTO lrec_output_bak
                          WITH KEY aubel  = lrec_output-aubel
                                   matnr  = lrec_output-matnr
                                   shkzg  = space.
      IF sy-subrc = 0.
        CLEAR lrec_output-picks.
      ELSE.
        lrec_output-picks = 1.
      ENDIF.
    ENDIF.

    APPEND lrec_output TO i_output.
    CLEAR  lrec_output.
  ENDLOOP.

* no data process
  IF i_output IS INITIAL.
*   no data process.
    MESSAGE s001.
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " get_main_data
*&---------------------------------------------------------------------*
*&      Form  display_alv
*&---------------------------------------------------------------------*
*       display using ALV
*----------------------------------------------------------------------*
FORM display_alv .

  DATA: lrec_layout TYPE slis_layout_alv.

* edit alv fieldcat
  PERFORM edit_alv_fieldcat.

  lrec_layout-colwidth_optimize = 'X'.
  lrec_layout-zebra             = 'X'.
  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'SET_STATUS_10001'
      i_callback_user_command  = 'USER_COMMAND'
      is_layout                = lrec_layout
      it_fieldcat              = i_fieldcat
    TABLES
      t_outtab                 = i_output
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    EXIT.
  ELSE.
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " display_alv
*&---------------------------------------------------------------------*
*&      Form  edit_alv_fieldcat
*&---------------------------------------------------------------------*
*       edit alv fieldcat
*----------------------------------------------------------------------*
FORM edit_alv_fieldcat.

  DATA: lrec_fieldcat TYPE slis_fieldcat_alv,
        ls_fieldcat   TYPE slis_fieldcat_alv, "MOD-001
        lflg_found    TYPE c,                 "MOD-001
        lv_index      TYPE c.                 "MOD-001
  CLEAR: lflg_found.                          "MOD-001
  REFRESH: i_fieldcat.

  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
    EXPORTING
      i_structure_name       = 'YSE_SALES_PICKS'
      i_bypassing_buffer     = 'X'
    CHANGING
      ct_fieldcat            = i_fieldcat
    EXCEPTIONS
      inconsistent_interface = 1
      program_error          = 2
      OTHERS                 = 3.
  IF sy-subrc <> 0.
    EXIT.
  ENDIF.
  LOOP AT i_fieldcat INTO lrec_fieldcat.
    lv_index = sy-tabix + 1.                                  "MOD-001
    CASE lrec_fieldcat-fieldname.
      WHEN 'PRCTR'.
        lrec_fieldcat-no_out = 'X'.
      WHEN 'VKBUR'.                                           "MOD-001
        lflg_found = 'X'.                                     "MOD-001
        ls_fieldcat-col_pos     = lrec_fieldcat-col_pos + 1.  "MOD-001
        ls_fieldcat-fieldname   = 'VKGRP'.                    "MOD-001
        ls_fieldcat-ref_fieldname = 'VKGRP'.                  "MOD-001
        ls_fieldcat-ref_tabname   =  'VBAK'.                  "MOD-001
        INSERT ls_fieldcat INTO i_fieldcat INDEX lv_index.    "MOD-001
        CONTINUE.                                             "MOD-001
      WHEN 'VKGRP'.                                           "MOD-001
        CONTINUE.                                             "MOD-001
    ENDCASE.
    IF lflg_found IS NOT INITIAL.                             "MOD-001
      lrec_fieldcat-col_pos = lrec_fieldcat-col_pos + 1.      "MOD-001
    ENDIF.                                                    "MOD-001
    MODIFY i_fieldcat FROM lrec_fieldcat.
    CLEAR  lrec_fieldcat.
  ENDLOOP.

ENDFORM.                    " edit_alv_fieldcat
*&---------------------------------------------------------------------*
*&      Form  set_status_10001
*&---------------------------------------------------------------------*
*       set screen status
*----------------------------------------------------------------------*
FORM set_status_10001 USING i_extab TYPE slis_t_extab.

  SET PF-STATUS '10001'.
  SET TITLEBAR  '10001'.

ENDFORM.                               " set_status_10001
*&---------------------------------------------------------------------*
*&      Form  user_command
*&---------------------------------------------------------------------*
*       user command
*----------------------------------------------------------------------*
FORM user_command  USING    i_ucomm    LIKE sy-ucomm
                            i_selfield TYPE slis_selfield.

  CASE i_ucomm.
    WHEN '&IC1'.
      PERFORM display_process USING i_selfield.
  ENDCASE.

ENDFORM.                               " user_command
*&---------------------------------------------------------------------*
*&      Form  display_process
*&---------------------------------------------------------------------*
*       display process
*----------------------------------------------------------------------*
FORM display_process  USING    selfield TYPE slis_selfield.

  DATA lrec_output TYPE ty_output. "yse_sales_picks. MOD-001
  READ TABLE i_output INTO lrec_output INDEX selfield-tabindex.
  IF sy-subrc = 0.
    CASE selfield-fieldname.
      WHEN 'AUBEL'.
        SET PARAMETER ID 'AUN' FIELD lrec_output-aubel.
        CALL TRANSACTION 'VA03' AND SKIP FIRST SCREEN.
      WHEN 'VBELN'.
        SET PARAMETER ID 'VF' FIELD lrec_output-vbeln.
        CALL TRANSACTION 'VF03' AND SKIP FIRST SCREEN.
    ENDCASE.
  ENDIF.

ENDFORM.                    " display_process
*&---------------------------------------------------------------------*
*&      Form  get_partner
*&---------------------------------------------------------------------*
*       Get Partner
*----------------------------------------------------------------------*
FORM get_partner  USING vbeln parvw
               CHANGING kunnr name.
  CLEAR: kunnr, name.
  SELECT SINGLE kunnr INTO kunnr
           FROM vbpa WHERE vbeln = vbeln
                       AND posnr = '000000'
                       AND parvw = parvw.
  IF sy-subrc = 0.
*   Get Partner Name
    PERFORM get_name1 USING kunnr
                   CHANGING name.
  ENDIF.

ENDFORM.                    " get_partner
*&---------------------------------------------------------------------*
*&      Form  get_name1
*&---------------------------------------------------------------------*
*       Get Partner Name
*----------------------------------------------------------------------*
FORM get_name1  USING  kunnr
             CHANGING  name1.

  CLEAR: name1.
* get Partner Address
  DATA l_adrnr TYPE kna1-adrnr.
  SELECT SINGLE adrnr INTO (l_adrnr)
    FROM kna1 WHERE kunnr = kunnr.
* get Partner Name
  SELECT name1 INTO name1
    FROM adrc  UP TO 1 ROWS
   WHERE addrnumber = l_adrnr.
  ENDSELECT.

ENDFORM.                                                    " get_name1
*&---------------------------------------------------------------------*
*&      Form  get_customer
*&---------------------------------------------------------------------*
*       Get Customer
*----------------------------------------------------------------------*
FORM get_customer  USING    kunag
                   CHANGING bran1 vtext
                            land1 regio bezei
                            brsch brtxt.
  CLEAR: bran1, vtext,
         land1, regio, bezei,
         brsch, brtxt.
* Get Customer
  SELECT SINGLE bran1                  "Industry code 1
                land1                  "Country Key
                regio                  "Region (State, Province, County)
                brsch                  "Industry key
    INTO (bran1, land1, regio, brsch)
    FROM kna1 WHERE kunnr = kunag.
  IF sy-subrc = 0.
*   Get Industry code description
    SELECT SINGLE vtext INTO vtext
      FROM tbrct  WHERE spras = sy-langu AND braco = bran1.
*   Get Region description
    SELECT SINGLE bezei INTO bezei
      FROM t005u  WHERE spras = sy-langu
                    AND land1 = land1
                    AND bland = regio.
*   Industry key description
    SELECT SINGLE brtxt INTO brtxt
      FROM t016t  WHERE spras = sy-langu AND brsch = brsch.
  ENDIF.

ENDFORM.                    " get_customer
*&---------------------------------------------------------------------*
*&      Form  get_maktx
*&---------------------------------------------------------------------*
*       material description
*----------------------------------------------------------------------*
FORM get_maktx  USING    matnr
                CHANGING maktx.
  CLEAR maktx.
  SELECT SINGLE maktx
    INTO maktx FROM makt
   WHERE matnr = matnr AND spras = sy-langu.

ENDFORM.                    " get_maktx
*&---------------------------------------------------------------------*
*&      Form  get_wgbez
*&---------------------------------------------------------------------*
*       material group description
*----------------------------------------------------------------------*
FORM get_wgbez  USING    matkl
                CHANGING wgbez.
  CLEAR wgbez.
  SELECT SINGLE wgbez
    INTO wgbez FROM t023t
   WHERE spras = sy-langu AND matkl = matkl.

ENDFORM.                    " get_wgbez
*&---------------------------------------------------------------------*
*&      Form  GET_PLC
*&---------------------------------------------------------------------*
*       Get PLC
*----------------------------------------------------------------------*
FORM get_plc  USING    prctr
              CHANGING target1.
  DATA: l_segment TYPE yse_prctr_bl-segment.
  CLEAR target1.
  SELECT SINGLE segment INTO l_segment FROM yse_prctr_bl
                       WHERE prctr = prctr.
  IF sy-subrc = 0.
    SELECT target1 INTO target1 FROM k9rcd11000010 UP TO 1 ROWS
                               WHERE sour1_from = l_segment
                                 AND sour1_to   = l_segment.
    ENDSELECT.
  ENDIF.

ENDFORM.                    " GET_PLC
*&---------------------------------------------------------------------*
*&      Form  CONVERSION_EXIT_ALPHA_OUTPUT
*&---------------------------------------------------------------------*
*       Conversion exit ALPHA, internal->external
*----------------------------------------------------------------------*
FORM conversion_exit_alpha_output  USING    input
                                   CHANGING output.
  CLEAR output.
  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_OUTPUT'
    EXPORTING
      input         = input
    IMPORTING
      output        = output.

ENDFORM.                    " CONVERSION_EXIT_ALPHA_OUTPUT
*&---------------------------------------------------------------------*
*&      Form  CONVERSION_EXIT_ALPHA_INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM conversion_exit_alpha_input  USING    input
                                  CHANGING output.
  CLEAR output.
  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
    EXPORTING
      input         = input
    IMPORTING
      output        = output.

ENDFORM.                    " CONVERSION_EXIT_ALPHA_INPUT

*Selection text£º
*S_FKDAT:        Billing date
*S_VKORG:        Sales Org.
*S_VTWEG:        Distribution channel
