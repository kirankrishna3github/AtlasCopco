*----------------------------------------------------------------------*
* Author  : Anda Wu                                                    *
* Created : 09.15.2015                                                 *
* Dev.nbr: CR3696- vendor information                                  *
*----------------------------------------------------------------------*
* This report  is  new report for returns  of Russia                   *
************************************************************************
* Mod-xxx | xxxx.xx.xx | xxxx xxxx     | CD1Kxxxxxxxxxx  |   CRXXXX    *
************************************************************************
************************************************************************
REPORT yse_icos_vendor NO STANDARD PAGE HEADING LINE-SIZE 255.

************************************************************************
* TYPE SPOOLS                                                          *
************************************************************************
TYPE-POOLS slis.

************************************************************************
* TABLES                                                               *
************************************************************************
TABLES:
  lfb1,
  lfm1.

************************************************************************
* TYPES                                                                *
************************************************************************
TYPES:

  BEGIN OF ty_alv_output,
    lifnr     TYPE lfa1-lifnr,
    name1     TYPE lfa1-name1,
    land1     TYPE lfa1-land1,
    ktokk     TYPE lfa1-ktokk,
    bukrs     TYPE lfb1-bukrs,
    txt30    TYPE t077x-txt30,
    ekorg     TYPE lfm1-ekorg,
  END OF ty_alv_output,

  BEGIN OF ty_lfa1,
    lifnr     TYPE lfa1-lifnr,
    name1     TYPE lfa1-name1,
    land1     TYPE lfa1-land1,
    ktokk     TYPE lfa1-ktokk,
    bukrs     TYPE lfb1-bukrs,
  END OF ty_lfa1,

  BEGIN OF ty_makt,
    matnr    TYPE makt-matnr,   "Material Number
    spras    TYPE makt-spras,   "Language Key
    maktx    TYPE makt-maktx,   "Material Description
  END OF ty_makt,

  BEGIN OF ty_mbew,
    matnr	   TYPE mbew-matnr,   "Material Number
    bwkey	   TYPE mbew-bwkey,   "Valuation Area
    bwtar	   TYPE mbew-bwtar,   "Valuation Type
    verpr    TYPE mbew-verpr,   "Moving Average Price/Periodic Unit Price
    stprs    TYPE mbew-stprs,   "Standard price
  END OF ty_mbew,

  BEGIN OF ty_mard,
    matnr    TYPE mard-matnr,   "Material Number
    werks    TYPE mard-werks,   "Plant
    lgort    TYPE mard-lgort,   "Storage Locatio
    labst    TYPE mard-labst,   "UU Stock
  END OF ty_mard,

  BEGIN OF ty_mvke,
    matnr	   TYPE mvke-matnr,   "Material Number
    vkorg	   TYPE mvke-vkorg,   "Sales Organization
    vtweg    TYPE mvke-vtweg,   "Distribution Channel
    prodh    TYPE mvke-prodh,   "Product hierarchy
  END OF ty_mvke,

  BEGIN OF ty_tvgrt,
    vkgrp    TYPE tvgrt-vkgrp,  "Sales Group
    bezei    TYPE tvgrt-bezei,  "Sales Group Name
  END OF ty_tvgrt,

  BEGIN OF ty_vbep,
    vbeln    TYPE vbep-vbeln,   "Sales Document
    posnr    TYPE vbep-posnr,   "Sales Document Item
    etenr    TYPE vbep-etenr,   "Delivery Schedule Line Number
    ettyp    TYPE vbep-ettyp,   "Schedule line category
    edatu    TYPE vbep-edatu,   "Requested delivery date
    wadat    TYPE vbep-wadat,   "Planned GI date
    bmeng    TYPE vbep-bmeng,   "Confirmed Quantity
  END OF ty_vbep,

  BEGIN OF ty_comp,
    rsnum     TYPE resb-rsnum,  "Number
    rspos     TYPE resb-rspos,  "Item Number
    vbeln_adv TYPE vbap-vbeln,  "Sales Document
    posnr_adv TYPE vbap-posnr,  "Sales Document Item
    sobkz     TYPE resb-sobkz,  "Special Stock Indicator
  END OF ty_comp,

  BEGIN OF ty_vbkd,
    vbeln     TYPE vbkd-vbeln,  "Sales Document
    posnr     TYPE vbkd-posnr,  "Sales Document Item
    zterm     TYPE vbkd-zterm,  "Terms of Payment Key
    bstkd     TYPE vbkd-bstkd,  "Customer PO number
    ihrez     TYPE vbkd-ihrez,  "Service Order
  END OF ty_vbkd,

  BEGIN OF ty_vbfa,
    vbelv     TYPE vbfa-vbelv,  "Preceding sales doc
    posnv     TYPE vbfa-posnv,  "Preceding item
    vbeln     TYPE vbfa-vbeln,  "Subsequent sale doc
    posnn     TYPE vbfa-posnn,  "Subsequent item
    vbtyp_n   TYPE vbfa-vbtyp_n,
    fkimg     TYPE vbrp-fkimg,  "Invoiced Quantity
    aubel     TYPE vbrp-aubel,  "SO
    aupos     TYPE vbrp-aupos,  "Item
  END OF ty_vbfa.

************************************************************************
* INTERNAL TABLES                                                      *
************************************************************************
DATA:
  gt_lfa1     TYPE STANDARD TABLE OF ty_lfa1,
  gt_t077x    TYPE STANDARD TABLE OF t077x,
  gt_lfm1     TYPE STANDARD TABLE OF lfm1,
  gt_alvdata  TYPE STANDARD TABLE OF ty_alv_output,
  gt_fieldcat TYPE slis_t_fieldcat_alv.

************************************************************************
* WORKAREAS                                                            *
************************************************************************
DATA:
  gs_alvdata  TYPE ty_alv_output,
  gs_disvar   TYPE disvariant,
  gs_layout   TYPE slis_layout_alv.

************************************************************************
* VARIABLES                                                            *
************************************************************************
DATA:
  gv_col(10)  TYPE n,
  gv_text     TYPE string,
  gv_save     TYPE c,
  gv_callback TYPE slis_formname.

************************************************************************
* CONSTANTS                                                            *
************************************************************************
CONSTANTS:
  gc_x                TYPE c VALUE 'X',
  gc_type_e           TYPE c VALUE 'E',
  gc_callback_routine TYPE slis_formname VALUE 'USER_COMMAND'.

************************************************************************
* SELECTION-SCREEN                                                     *
************************************************************************

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-t02 .
SELECT-OPTIONS:
*  s_budat      FOR mkpf-budat OBLIGATORY,  "Return date
  s_lifnr      FOR lfb1-lifnr,              "Vendor
  s_bukrs      FOR lfb1-bukrs,              "Company Code
  s_ekorg      FOR lfm1-ekorg.              "Purchase Org
SELECTION-SCREEN END OF BLOCK b2.

* Alv variants
SELECTION-SCREEN BEGIN OF BLOCK b3 WITH FRAME TITLE text-t03.

PARAMETERS: p_vari TYPE disvariant-variant,       "Dispaly Variant
*********** upgrade to backup data to server
            p_backup      TYPE char1 AS CHECKBOX DEFAULT 'X',
            p_path        TYPE rlgrap-filename
              NO-DISPLAY DEFAULT '/var/load/CP1/UK/original'.

SELECTION-SCREEN END OF BLOCK b3.

*&---------------------------------------------------------------------*
*& INITIALIZATION                                                      *
*&---------------------------------------------------------------------*
INITIALIZATION.
* Do initilization
  PERFORM frm_init.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN                                                 *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN.
* selection screen check
  PERFORM check_parameter.

*&---------------------------------------------------------------------*
*& AT SELECTION-SCREEN ON VALUE-REQUEST                                *
*&---------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_vari.
  PERFORM f4_for_variant.

*&---------------------------------------------------------------------*
*& START-OF-SELECTION                                                  *
*&---------------------------------------------------------------------*
START-OF-SELECTION.
* progress indicator
  PERFORM process_info.
* get data
  PERFORM get_alv_data.
* alv property set
  PERFORM alv_prop_set.

*&---------------------------------------------------------------------*
*& END-OF-SELECTION                                                    *
*&---------------------------------------------------------------------*
END-OF-SELECTION.
* ALV display
  PERFORM alv_display.
*** upgrade to backup data to server
  IF p_backup IS NOT INITIAL.
    IF sy-batch IS NOT INITIAL.
      PERFORM frm_backup2server.
    ENDIF.
  ENDIF.

*&---------------------------------------------------------------------*
*&      Form  FRM_INIT
*&---------------------------------------------------------------------*
*       Do initilization
*----------------------------------------------------------------------*
FORM frm_init .

* initialize the internal table,workareas and variables
  REFRESH:
    gt_alvdata,
    gt_fieldcat.

  CLEAR:
    gs_alvdata,
    gs_disvar,
    gs_layout,
    gv_col,
    gv_text,
    gv_save,
    gv_callback.

  gv_save = 'A'.
* Set default alv layout
  gs_disvar-report = sy-repid.
  CALL FUNCTION 'REUSE_ALV_VARIANT_DEFAULT_GET'
    EXPORTING
      i_save        = gv_save
    CHANGING
      cs_variant    = gs_disvar
    EXCEPTIONS
      wrong_input   = 1
      not_found     = 2
      program_error = 3
      OTHERS        = 4.
  IF sy-subrc = 0.
    p_vari = gs_disvar-variant.
  ENDIF.

ENDFORM.                    " FRM_INIT

*&---------------------------------------------------------------------*
*&      Form  CHECK_PARAMETER
*&---------------------------------------------------------------------*
*       selection screen check
*----------------------------------------------------------------------*
FORM check_parameter .

  DATA: ls_disvar TYPE disvariant.

  IF p_vari IS NOT INITIAL.
    ls_disvar = gs_disvar.
    ls_disvar-variant = p_vari.
    CALL FUNCTION 'REUSE_ALV_VARIANT_EXISTENCE'
      EXPORTING
        i_save        = gv_save
      CHANGING
        cs_variant    = ls_disvar
      EXCEPTIONS
        wrong_input   = 1
        not_found     = 2
        program_error = 3
        OTHERS        = 4.
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
              WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ELSE.
      gs_disvar = ls_disvar.
    ENDIF.
  ENDIF.

ENDFORM.                    " CHECK_PARAMETER
*&---------------------------------------------------------------------*
*&      Form  PROCESS_INFO
*&---------------------------------------------------------------------*
*       progress indicator
*----------------------------------------------------------------------*
FORM process_info .

  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    CONCATENATE 'Start report:'(t52) sy-repid
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t52.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 1
        text       = gv_text.
  ENDIF.

ENDFORM.                    " PROCESS_INFO
*&---------------------------------------------------------------------*
*&      Form  GET_ALV_DATA
*&---------------------------------------------------------------------*
*       get data
*----------------------------------------------------------------------*
FORM get_alv_data .
* Gain data from table LFA1
  PERFORM get_data_lfa1.

  PERFORM get_data_t077x.

  PERFORM get_data_lfm1.

* Generate ALV data for output
  PERFORM generate_alv_data.
* Edit all available
*  PERFORM edit_alv_data.
ENDFORM.                    " GET_ALV_DATA
*&---------------------------------------------------------------------*
*&      Form  ALV_PROP_SET
*&---------------------------------------------------------------------*
*       alv property set
*----------------------------------------------------------------------*
FORM alv_prop_set .

* Display ALV process indicator
  PERFORM alv_progress_indicator.

* FIELDCAT set
  PERFORM alv_fieldcat_set.

* LAYOUT set
  PERFORM alv_layout_set.

* Set Others
  PERFORM alv_others_set.

ENDFORM.                    " ALV_PROP_SET
*&---------------------------------------------------------------------*
*&      Form  ALV_DISPLAY
*&---------------------------------------------------------------------*
*       ALV display
*----------------------------------------------------------------------*
FORM alv_display .

  DATA: lv_lines(10)  TYPE c.

  IF gt_alvdata IS INITIAL.
    CLEAR: gv_text.
    gv_text = text-t61.
    MESSAGE s000(yse_sales_log) WITH gv_text
      DISPLAY LIKE gc_type_e.
    LEAVE LIST-PROCESSING.
  ENDIF.

  IF sy-batch IS INITIAL.
    CLEAR: gv_text.
    DESCRIBE TABLE gt_alvdata LINES lv_lines.
    CONCATENATE 'Totally'(t62) lv_lines 'entries are generated'(t63)
                  INTO gv_text SEPARATED BY space.
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ENDIF.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'FRM_MY_STATUS'
      i_callback_user_command  = gv_callback
      is_layout                = gs_layout
      it_fieldcat              = gt_fieldcat
      i_save                   = gv_save
      is_variant               = gs_disvar
    TABLES
      t_outtab                 = gt_alvdata
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " ALV_DISPLAY

*&---------------------------------------------------------------------*
*&      Form  FRM_MY_STATUS
*&---------------------------------------------------------------------*
*       Status Set
*----------------------------------------------------------------------*
*  --> I_EXTAB    STATUS
*----------------------------------------------------------------------*
FORM frm_my_status
  USING i_extab TYPE slis_t_extab.              "#EC CALLED "#EC NEEDED

  SET PF-STATUS '9100'.

ENDFORM.        "FRM_MY_STATUS

*&---------------------------------------------------------------------*
*&      Form  ALV_FIELDCAT_SET
*&---------------------------------------------------------------------*
*       FIELDCAT set
*----------------------------------------------------------------------*
FORM alv_fieldcat_set .

** Create Fieldcatalogue from internal table

  CLEAR gv_col.

* Field definitions
  PERFORM add_field USING:
     'LIFNR'    10  'Vendor'(s01) 'Vendor Number'(l01) 'X',
     'NAME1'    35  'Name'(s02)     'Vendor Name'(l02)       ' ',
     'LAND1'    3   'Country'(s03)   'Country'(l03)   ' ',
     'KTOKK'    4 'Acct Group'(s04)   'Account group'(l04) ' ',
     'TXT30'    30 'Reqmt'(s05) 'Description'(l05) ' '.

ENDFORM.                    " ALV_FIELDCAT_SET

*&---------------------------------------------------------------------*
*&      Form  ADD_FIELD
*&---------------------------------------------------------------------*
*       Add field to field catalog
*----------------------------------------------------------------------*
FORM add_field  USING    p_field
                         p_len
                         p_descr_s
                         p_descr_l
                         p_hotsp.
  DATA:
     ls_fieldcat TYPE slis_fieldcat_alv.

  gv_col = gv_col + 1.

  CLEAR ls_fieldcat.
  ls_fieldcat-col_pos    = gv_col.
  ls_fieldcat-fieldname  = p_field.
  ls_fieldcat-outputlen  = p_len.
  ls_fieldcat-seltext_l  = p_descr_l.
  ls_fieldcat-seltext_m  = p_descr_l.
  ls_fieldcat-seltext_s  = p_descr_s.
  ls_fieldcat-fix_column = 'X'.
  ls_fieldcat-emphasize  = 'X'.
  ls_fieldcat-hotspot    = p_hotsp.

* Suppress leading zeros for the material number
  IF p_field = 'MATNR'.
    ls_fieldcat-edit_mask = '==MATN1'.
  ENDIF.

  APPEND ls_fieldcat TO gt_fieldcat.

ENDFORM.                    " ADD_FIELD

*&---------------------------------------------------------------------*
*&      Form  ALV_LAYOUT_SET
*&---------------------------------------------------------------------*
*       LAYOUT set
*----------------------------------------------------------------------*
FORM alv_layout_set .

  CLEAR: gs_layout.
  gs_layout-zebra = gc_x.               "ALV lines cross-color display
  gs_layout-colwidth_optimize = gc_x.   "Auto optimize column width
  gs_layout-detail_popup = gc_x.        "Show detail screen

ENDFORM.                    " ALV_LAYOUT_SET

*&---------------------------------------------------------------------*
*&      Form  GENERATE_ALV_DATA
*&---------------------------------------------------------------------*
*       Generate ALV data for output
*----------------------------------------------------------------------*
FORM generate_alv_data .

  DATA:
    ls_lfm1    TYPE lfm1,
    ls_t077x   TYPE t077x,
    ls_lfa1    TYPE ty_lfa1.

  FIELD-SYMBOLS:
    <lfs_alvdata> TYPE ty_alv_output.

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to generate ALV data'(t64).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t64.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 90
        text       = gv_text.
  ENDIF.
  LOOP AT gt_lfa1 INTO ls_lfa1.
    gs_alvdata-lifnr =  ls_lfa1-lifnr.
    gs_alvdata-name1 =  ls_lfa1-name1.
    gs_alvdata-land1 = ls_lfa1-land1.
    gs_alvdata-ktokk =  ls_lfa1-ktokk.
    gs_alvdata-bukrs = ls_lfa1-bukrs.
    READ TABLE gt_lfm1 INTO ls_lfm1
      WITH KEY lifnr = ls_lfa1-lifnr.
    IF sy-subrc <> 0.
      CONTINUE.
    ELSE.
      gs_alvdata-ekorg = ls_lfm1-ekorg.
    ENDIF.
    READ TABLE gt_t077x INTO ls_t077x
      WITH KEY ktokd = ls_lfa1-ktokk.
    IF sy-subrc = 0.
      gs_alvdata-txt30 = ls_t077x-txt30.
    ENDIF.
    APPEND gs_alvdata TO gt_alvdata.
  ENDLOOP.

ENDFORM.                    " GENERATE_ALV_DATA

*&---------------------------------------------------------------------*
*&      Form  ALV_PROGRESS_INDICATOR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM alv_progress_indicator .

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to set ALV property'(t60).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t60.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 85
        text       = gv_text.
  ENDIF.

ENDFORM.                    " ALV_PROGRESS_INDICATOR

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       Set Others
*----------------------------------------------------------------------*
FORM alv_others_set .

  gv_callback = gc_callback_routine.

ENDFORM.                    " ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*&      Form  F4_FOR_VARIANT
*&---------------------------------------------------------------------*
*       F4 help for display variant
*----------------------------------------------------------------------*
FORM f4_for_variant .

  DATA: ls_disvar TYPE disvariant,
        lv_exit   TYPE c.

  ls_disvar-report = sy-repid.

  CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
    EXPORTING
      is_variant    = ls_disvar
      i_save        = gv_save
    IMPORTING
      e_exit        = lv_exit
      es_variant    = gs_disvar
    EXCEPTIONS
      not_found     = 1
      program_error = 2
      OTHERS        = 3.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ELSE.
    IF lv_exit = space.
      p_vari = gs_disvar-variant.
    ENDIF.
  ENDIF.

ENDFORM.                    " F4_FOR_VARIANT

*&---------------------------------------------------------------------*
*&      Form  ALV_OTHERS_SET
*&---------------------------------------------------------------------*
*       USER_COMMAND
*----------------------------------------------------------------------*
FORM user_command USING u_ucomm     LIKE sy-ucomm           "#EC CALLED
                        p_selfield  TYPE slis_selfield.
  DATA: ls_alvdata TYPE ty_alv_output.
  CASE u_ucomm.
    WHEN '&IC1'.        " SAP standard code for double-clicking
      IF p_selfield-fieldname = 'LIFNR'.
        IF p_selfield-value IS NOT INITIAL.
          SET PARAMETER ID 'LIF' FIELD p_selfield-value.
          READ TABLE gt_alvdata INDEX p_selfield-TABINDEX INTO ls_alvdata.
          IF sy-subrc = 0.
            SET PARAMETER ID 'BUK' FIELD ls_alvdata-bukrs.
            SET PARAMETER ID 'EKO' FIELD ls_alvdata-ekorg.
          ENDIF.
          CALL TRANSACTION 'XK03'.
        ENDIF.
      ENDIF.
    WHEN OTHERS.
  ENDCASE.

ENDFORM.                    " USER_COMMAND
*&---------------------------------------------------------------------*
*&      Form  EDIT_ALV_DATA
*&---------------------------------------------------------------------*
*       Edit all available
*----------------------------------------------------------------------*
*FORM edit_alv_data .
*
*ENDFORM.                    " EDIT_ALV_DATA
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_LFA1
*&---------------------------------------------------------------------*
*       Gain data from table MKPF - Header: Material Document
*----------------------------------------------------------------------*
FORM get_data_lfa1.

* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to extract data from table LFA1'(t81).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t81.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 50
        text       = gv_text.
  ENDIF.

  SELECT    lfa1~lifnr
            lfa1~name1
            lfa1~land1
            lfa1~ktokk
            lfb1~bukrs
    FROM    lfa1
    INNER JOIN lfb1
    ON lfa1~lifnr = lfb1~lifnr
    INTO TABLE gt_lfa1
    WHERE lfa1~lifnr IN s_lifnr
      AND bukrs IN s_bukrs.

  IF sy-subrc <> 0.
    IF sy-batch IS NOT INITIAL.
      CLEAR: gv_text.
      gv_text ='No data exist in table LFA1-LFB1'(t55).
      MESSAGE i000(yse_sales_log) WITH gv_text.
    ELSE.
      MESSAGE s000(yse_sales_log) WITH text-t55 DISPLAY LIKE 'E'.
      LEAVE LIST-PROCESSING.
    ENDIF.
  ENDIF.
ENDFORM.                    " GET_DATA_LFA1
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_T077X
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_data_t077x .

  CHECK gt_lfa1 IS NOT INITIAL.
* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to extract data from table T077X'(t85).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t85.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 50
        text       = gv_text.
  ENDIF.

  SELECT    *
    FROM    t077x
    INTO TABLE gt_t077x
    FOR ALL ENTRIES IN gt_lfa1
    WHERE spras = 'EN'
      AND ktokd = gt_lfa1-ktokk.
  IF sy-subrc <> 0.
    IF sy-batch IS NOT INITIAL.
      CLEAR: gv_text.
      gv_text ='No data exist in table T077X'(t69).
      MESSAGE i000(yse_sales_log) WITH gv_text.
    ENDIF.
  ENDIF.

ENDFORM.                    " GET_DATA_T077X
*&---------------------------------------------------------------------*
*&      Form  GET_DATA_LFM1
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_data_lfm1 .
  CHECK gt_lfa1 IS NOT INITIAL.
* Progress indicator/ Log info generate
  IF sy-batch IS NOT INITIAL.
    CLEAR: gv_text.
    gv_text ='Begin to extract data from table T077X'(t87).
    MESSAGE i000(yse_sales_log) WITH gv_text.
  ELSE.
    CLEAR: gv_text.
    gv_text = text-t87.
    CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
      EXPORTING
        percentage = 70
        text       = gv_text.
  ENDIF.

  SELECT    *
    FROM    lfm1
    INTO TABLE gt_lfm1
    FOR ALL ENTRIES IN gt_lfa1
    WHERE lifnr = gt_lfa1-lifnr
      AND ekorg IN s_ekorg.
  IF sy-subrc <> 0.
    IF sy-batch IS NOT INITIAL.
      CLEAR: gv_text.
      gv_text ='No data exist in table LFM1'(t90).
      MESSAGE i000(yse_sales_log) WITH gv_text.
    ENDIF.
  ENDIF.
ENDFORM.                    " GET_DATA_LFM1
*** upgrade to backup data to server
*&---------------------------------------------------------------------*
*&      Form  FRM_BACKUP2SERVER
*&---------------------------------------------------------------------*
*       Send the data to server
*----------------------------------------------------------------------*
FORM frm_backup2server .

  DATA: lv_path       TYPE rlgrap-filename,
        lv_line(4096) TYPE c,
        ls_output     TYPE ty_alv_output,
        lv_tab        TYPE char1
          VALUE cl_abap_char_utilities=>horizontal_tab.
  CLEAR: lv_line.

* Build filename
  CONCATENATE p_path
              '/YSE_ICOS_Vendor'
*              sy-uname '_' sy-datum '_' sy-uzeit
              '.xls'
         INTO lv_path.
  REPLACE FIRST OCCURRENCE OF 'CP1' IN lv_path WITH sy-sysid.

* Open file
  OPEN DATASET lv_path FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: 'Error opening file:'(026) , lv_path.
    EXIT.
  ENDIF.

  CONCATENATE text-l01    text-l02
              text-l03    text-l04
              text-l05
       INTO   lv_line SEPARATED BY lv_tab.
  TRANSFER lv_line TO lv_path.

  LOOP AT gt_alvdata INTO ls_output.
    CLEAR: lv_line.
    CONCATENATE   ls_output-lifnr  ls_output-name1
                  ls_output-land1  ls_output-ktokk
                  ls_output-txt30

         INTO   lv_line SEPARATED BY lv_tab.
    TRANSFER lv_line TO lv_path.
  ENDLOOP.

  CLOSE DATASET lv_path.
  WRITE: 'Report output written to file'(027),
         AT 35 lv_path.

ENDFORM.                    " FRM_BACKUP2SERVER

*Text symbol text£º
*026:Error opening file:
*027:Report output written to file
*L01:Vendor Number
*L02:Vendor Name
*L03:Country
*L04:Account group
*L05:Description
*S01:Vendor
*S02:Name
*S03:Country
*S04:Acct Group
*S05:Reqmt
*T02:Selection Screen
*T03:Layout
*T52:Start report:
*T55:No data exist in table LFA1-LFB1
*T60:Begin to set ALV property
*T61:No data exist!
*T62:Totally
*T63:entries are generated
*T64:Begin to generate ALV data
*T69:No data exist in table T077X
*T81:Begin to extract data from table LFA1
*T85:Begin to extract data from table T077X
*T87:Begin to extract data from table T077X

*T90:No data exist in table LFM1
*Selection text£º
*P_BACKUP:        Backup data to Server
*P_VARI:D       .
*S_BUKRS:D       .
*S_EKORG:D       .
*S_LIFNR:D       .
