*&---------------------------------------------------------------------*
*& Report  YSE_DOWN_UP_APPL_SERVER_FILE                                *
*&---------------------------------------------------------------------*
**********************************************************************
* Program ID        : YSE_DOWN_UP_APPL_SERVER_FILE                    *
* Program Title     : Tool for up- downloading files from Appl. Server*
* Author            : Tom Van Oevelen                                 *
* Date              : 23/11/2006                                      *
* Functional spec   : no spec involved                                *
* Description       : This report is to be used only for testing      *
*         purposes where you need a file sitting on the application   *
*         server for a specific functionality to be tested (e.g.      *
*         server file processing in background                        *
*=====================================================================*
* Change History Log                                                  *
*---------------------------------------------------------------------*
* Mod. no.|  Date    | Name           | Correction Number | Change Ref*
*---------------------------------------------------------------------*
* MOD-001 |dd/mm/yyyy| xxxxxxxxxxxxxx | XXXXxxxxxx        | XXXXxxxxxx*
* Description:                                                        *
*---------------------------------------------------------------------*
* MOD-002 |dd/mm/yyyy| xxxxxxxxxxxxxx | XXXXxxxxxx                    *
* Description:                                                        *
***********************************************************************
REPORT  YSE_DOWN_UP_APPL_SERVER_FILE.

*  DATA
*        GV_STR(100) TYPE C.

************************************************************************
* Upload/Download File to/from Application Server                      *
************************************************************************
PARAMETERS:
  P_FILE_A       TYPE LOCALFILE DEFAULT '/tmp/' OBLIGATORY,  " Filename on application server
  P_UPL          RADIOBUTTON GROUP r1,                       " Radiobutton "Upload"
  P_UPB          RADIOBUTTON GROUP r1,                       " Radiobutton "Upload binary"
  P_DWN          RADIOBUTTON GROUP r1,                       " Radiobutton "Download"
  P_FILE_P(1024) TYPE C OBLIGATORY.                          " Filename on presentation server
SELECTION-SCREEN BEGIN OF LINE.
  SELECTION-SCREEN COMMENT 1(33) TEXT-004 FOR FIELD P_CODEP. " 'Codepage'
  PARAMETERS:
    P_CODEP        TYPE CPCODEPAGE DEFAULT '4110'.           " Codepage
  SELECTION-SCREEN COMMENT 43(10) P_CPTEXT.                  " Codepage description (HTML name)
SELECTION-SCREEN END OF LINE.

DATA:
  LV_CODEPAGE        TYPE ABAP_ENCOD,                  " Codepage in format for FM
  LV_TITLE           TYPE STRING,                      " Title for dialogue window
  LV_RC              TYPE I,                           " Return code from file dialogue
  LV_FILE_P          TYPE STRING,                      " Filename on presentation server (string)
  IT_CHAR_4096(4096) TYPE C OCCURS 0,                  " Internal table of char strings 4096
  WA_CHAR_4096(4096) TYPE C,                           " Working area for IT_CHAR_4096
  IT_CNTBIN          TYPE STANDARD TABLE OF SDOKCNTBIN," Internal table (binary content)
  WA_CNTBIN          TYPE SDOKCNTBIN,                  " Working area for IT_CNTBIN
  IT_FILE            TYPE FILETABLE,                   " Internal table of filenames for dialog
  WA_FILE            TYPE FILE_TABLE.                  " Working area for IT_FILE

************************************************************************
INITIALIZATION.
************************************************************************
  SELECT SINGLE CPATTR
           INTO P_CPTEXT
           FROM TCP00A
          WHERE CPCODEPAGE EQ P_CODEP
            AND CPATTRKIND EQ 'H'.  " HTML

************************************************************************
AT SELECTION-SCREEN OUTPUT.
************************************************************************
  IF SY-TCODE EQ 'YSE_DOWNLOAD_FILE'.
    LOOP AT SCREEN.
      IF    SCREEN-NAME EQ 'P_UPL'
         OR SCREEN-NAME EQ 'P_UPB'.
        SCREEN-INPUT = SPACE.
        MODIFY SCREEN.
      ENDIF.
    ENDLOOP.
  ENDIF.

************************************************************************
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_FILE_P.
************************************************************************
  LV_TITLE = 'Select file to be up/downloaded'(002).
  CLEAR:
    IT_FILE[], WA_FILE.
  CALL METHOD CL_GUI_FRONTEND_SERVICES=>FILE_OPEN_DIALOG
    EXPORTING
      WINDOW_TITLE            = LV_TITLE
*     DEFAULT_FILENAME        = LV_FILE_P
*     FILE_FILTER             =
*     INITIAL_DIRECTORY       =
*     WITH_ENCODING           =
    CHANGING
      FILE_TABLE              = IT_FILE
      RC                      = LV_RC
*     USER_ACTION             =
*     FILE_ENCODING           =
    EXCEPTIONS
      FILE_OPEN_DIALOG_FAILED = 1
      CNTL_ERROR              = 2
      ERROR_NO_GUI            = 3
      NOT_SUPPORTED_BY_GUI    = 4
      others                  = 5.
  IF SY-SUBRC <> 0.
    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
              WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.
  IF LV_RC = 1.
    READ TABLE IT_FILE INDEX 1 INTO WA_FILE.
    IF SY-SUBRC EQ 0.
      P_FILE_P = WA_FILE-FILENAME.
    ENDIF.
  ENDIF.

************************************************************************
AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_CODEP.
************************************************************************

  DATA:
    LT_TCP00A TYPE TCP00A     OCCURS 0,
    LT_RETTAB TYPE DDSHRETVAL OCCURS 0,
    WA_RETTAB LIKE LINE OF LT_RETTAB.

  SELECT     *
        FROM TCP00A
        INTO TABLE LT_TCP00A.

  CALL FUNCTION 'F4IF_INT_TABLE_VALUE_REQUEST'
    EXPORTING
      RETFIELD               = 'CPCODEPAGE'
      WINDOW_TITLE           = 'Select codepage'(001)
      VALUE_ORG              = 'S'
*     MULTIPLE_CHOICE        = ' '
*     DISPLAY                = ' '
    TABLES
      VALUE_TAB              = LT_TCP00A
      RETURN_TAB             = LT_RETTAB
    EXCEPTIONS
      PARAMETER_ERROR        = 1
      NO_VALUES_FOUND        = 2
      OTHERS                 = 3.
  IF SY-SUBRC <> 0.
    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ELSE.
    READ TABLE LT_RETTAB INTO WA_RETTAB INDEX 1.
    IF SY-SUBRC EQ 0.
      P_CODEP = WA_RETTAB-FIELDVAL.
      SELECT SINGLE CPATTR
           INTO P_CPTEXT
           FROM TCP00A
          WHERE CPCODEPAGE EQ WA_RETTAB-FIELDVAL
            AND CPATTRKIND EQ 'H'.  " HTML
    ENDIF.
  ENDIF.

************************************************************************
START-OF-SELECTION.
************************************************************************
* Move filename on presentation server to a string variable
  LV_FILE_P = P_FILE_P.

* In case "Download" option was chosen
* ************************************
  IF P_DWN NE space.
*   Open file on application server
    OPEN DATASET P_FILE_A FOR INPUT IN TEXT MODE ENCODING DEFAULT.
*   If an error occured
    IF SY-SUBRC <> 0.
*     Report error and exit
      WRITE 'Error opening file. Action aborted.'(003).
      EXIT.
    ENDIF.
*   Repeat until explicit exit command
    DO.
*     Read line from application server into character string
      READ DATASET P_FILE_A INTO WA_CHAR_4096.
*     If failed, exit the loop
      IF sy-subrc <> 0.
        EXIT.
      ENDIF.
*     Append the character string to character string table
      APPEND WA_CHAR_4096 TO IT_CHAR_4096.
    ENDDO.
*   Close file on appl server
    CLOSE DATASET P_FILE_A.

*   Download contents of char string table to local file
    LV_CODEPAGE = P_CODEP.
    CALL METHOD CL_GUI_FRONTEND_SERVICES=>GUI_DOWNLOAD
      EXPORTING
        FILENAME                  = LV_FILE_P
        FILETYPE                  = 'ASC'
        CODEPAGE                  = LV_CODEPAGE  " '4110'
      CHANGING
        DATA_TAB                  = IT_CHAR_4096
      EXCEPTIONS
        FILE_WRITE_ERROR          = 1
        NO_BATCH                  = 2
        GUI_REFUSE_FILETRANSFER   = 3
        INVALID_TYPE              = 4
        NO_AUTHORITY              = 5
        UNKNOWN_ERROR             = 6
        HEADER_NOT_ALLOWED        = 7
        SEPARATOR_NOT_ALLOWED     = 8
        FILESIZE_NOT_ALLOWED      = 9
        HEADER_TOO_LONG           = 10
        DP_ERROR_CREATE           = 11
        DP_ERROR_SEND             = 12
        DP_ERROR_WRITE            = 13
        UNKNOWN_DP_ERROR          = 14
        ACCESS_DENIED             = 15
        DP_OUT_OF_MEMORY          = 16
        DISK_FULL                 = 17
        DP_TIMEOUT                = 18
        FILE_NOT_FOUND            = 19
        DATAPROVIDER_EXCEPTION    = 20
        CONTROL_FLUSH_ERROR       = 21
        NOT_SUPPORTED_BY_GUI      = 22
        ERROR_NO_GUI              = 23
        others                    = 24.
    IF SY-SUBRC <> 0.
     MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    ENDIF.

* In case "Upload" option was chosen
* **********************************
  ELSEIF P_UPL NE space.
*   Upload contents of local file to char string table
    LV_CODEPAGE = P_CODEP.
    CALL METHOD CL_GUI_FRONTEND_SERVICES=>GUI_UPLOAD
      EXPORTING
        FILENAME                = LV_FILE_P
        FILETYPE                = 'ASC'
        CODEPAGE                = LV_CODEPAGE
      CHANGING
        DATA_TAB                = IT_CHAR_4096
      EXCEPTIONS
        FILE_OPEN_ERROR         = 1
        FILE_READ_ERROR         = 2
        NO_BATCH                = 3
        GUI_REFUSE_FILETRANSFER = 4
        INVALID_TYPE            = 5
        NO_AUTHORITY            = 6
        UNKNOWN_ERROR           = 7
        BAD_DATA_FORMAT         = 8
        HEADER_NOT_ALLOWED      = 9
        SEPARATOR_NOT_ALLOWED   = 10
        HEADER_TOO_LONG         = 11
        UNKNOWN_DP_ERROR        = 12
        ACCESS_DENIED           = 13
        DP_OUT_OF_MEMORY        = 14
        DISK_FULL               = 15
        DP_TIMEOUT              = 16
        NOT_SUPPORTED_BY_GUI    = 17
        ERROR_NO_GUI            = 18
        others                  = 19.
    IF SY-SUBRC <> 0.
      MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    ENDIF.
*   Open file on application server
    OPEN DATASET P_FILE_A FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
*   If an error occured
    IF SY-SUBRC <> 0.
*     Report error and exit
      WRITE 'Error opening file. Action aborted.'(003).
      EXIT.
    ENDIF.
*   Process every line of the char string table
    LOOP AT IT_CHAR_4096 INTO WA_CHAR_4096.
*     Move current character string line to file on appl server
      TRANSFER WA_CHAR_4096 TO P_FILE_A.
    ENDLOOP.
*   Close file on appl server
    CLOSE DATASET P_FILE_A.

* In case "Upload binary" option was chosen
* *****************************************
  ELSEIF P_UPB NE SPACE.
*   Upload contents of local file to binary table
    CALL METHOD CL_GUI_FRONTEND_SERVICES=>GUI_UPLOAD
      EXPORTING
        FILENAME                = LV_FILE_P
        FILETYPE                = 'BIN'
      CHANGING
        DATA_TAB                = IT_CNTBIN
      EXCEPTIONS
        FILE_OPEN_ERROR         = 1
        FILE_READ_ERROR         = 2
        NO_BATCH                = 3
        GUI_REFUSE_FILETRANSFER = 4
        INVALID_TYPE            = 5
        NO_AUTHORITY            = 6
        UNKNOWN_ERROR           = 7
        BAD_DATA_FORMAT         = 8
        HEADER_NOT_ALLOWED      = 9
        SEPARATOR_NOT_ALLOWED   = 10
        HEADER_TOO_LONG         = 11
        UNKNOWN_DP_ERROR        = 12
        ACCESS_DENIED           = 13
        DP_OUT_OF_MEMORY        = 14
        DISK_FULL               = 15
        DP_TIMEOUT              = 16
        NOT_SUPPORTED_BY_GUI    = 17
        ERROR_NO_GUI            = 18
        OTHERS                  = 19.
    IF SY-SUBRC <> 0.
      MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
                WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
    ENDIF.
*   Open file on application server
    OPEN DATASET P_FILE_A FOR OUTPUT IN BINARY MODE.
*   If an error occured
    IF SY-SUBRC <> 0.
*     Report error and exit
      WRITE 'Error opening file. Action aborted.'(003).
      EXIT.
    ENDIF.
*   Process every line of the char string table
    LOOP AT IT_CNTBIN INTO WA_CNTBIN.
*     Move current binary line to file on appl server
      TRANSFER WA_CNTBIN TO P_FILE_A.
    ENDLOOP.
*   Close file on appl server
    CLOSE DATASET P_FILE_A.

  ENDIF.

*Text symbol text£º
*001:Select codepage
*002:Select file to be up/downloaded
*003:Error opening file. Action aborted.

*004:Codepage
*Selection text£º
*P_CODEP:        Codepage
*P_DWN:        Download
*P_FILE_A:        Path+file name on Appl Server
*P_FILE_P:        Path+file name on Local Drive
*P_UPB:        Upload Binary
*P_UPL:        Upload
