************************************************************************
* Program ID        : YSE_UPDATE_PARTNER					     *
* Program Title     : Program title                                    *
* Author            : Anson Sun                                        *
* Date              : 12/08/2011                               	     *
* Change Request Number:2110                                           *
* Description       : The purpose of this program is to Mass load      *
*                     partner role End-Customer" in Equipment          *
*                     Master for CT China                              *
*======================================================================*
* Copied From       : (Cloned Program)                        	     *
* Title             : (Program Title)                          	     *
* Other Related obj : (Object names)                 		           *
*======================================================================*
* Change History Log                                             	     *
*----------------------------------------------------------------------*
* Mod. no.|  Date    | Name           | Correction Number  | Change    *
*----------------------------------------------------------------------*
* MOD-001 |dd/mm/yyyy| xxxxxxxxxxxxxx | XXXXxxxxxx         |           *
*                                                                      *
* Description:                                                   	     *
*----------------------------------------------------------------------*

REPORT  yse_update_partner MESSAGE-ID yse_cs_sd_msg.
*----------------------------------------------------------------------*
*       TABLES
*----------------------------------------------------------------------*
TABLES:
  tvkwz,                     "Allowed Plants per Sales Organization
  equi,                      "Equipment master data
  equz,                      "Equipment time segment
  ihpa.                      "Plant Maintenance: Partners
*----------------------------------------------------------------------*
*       TYPE-POOLS
*----------------------------------------------------------------------*
TYPE-POOLS slis.
*----------------------------------------------------------------------*
*       TYPE
*----------------------------------------------------------------------*
TYPES:
  BEGIN OF ty_equnr,
    werks TYPE equz-iwerk,             "Plant
    equnr TYPE equz-equnr,             "Equipment number
    datbi TYPE equz-datbi,             "Valid To Date
    eqlfn TYPE equz-eqlfn,             "Consecutive numberin
  END   OF ty_equnr,

  BEGIN OF ty_ihpa_chk,
    vkorg   TYPE tvkwz-vkorg,          "Sales Organization
    vtweg   TYPE tvkwz-vtweg,          "Distribution Channel
    werks   TYPE tvkwz-werks,          "Plant
    equnr   TYPE equi-equnr,           "Equipment Number
    objnr   TYPE ihpa-objnr,           "Object number
    parvw   TYPE ihpa-parvw,           "Partner Function
    counter TYPE ihpa-counter,         "Counter
  END   OF ty_ihpa_chk.
*----------------------------------------------------------------------*
*       DATA
*----------------------------------------------------------------------*
DATA:
  i_log         TYPE TABLE OF yse_update_partner_log,
  i_fieldcat    TYPE slis_t_fieldcat_alv.
*----------------------------------------------------------------------*
*       SELECTION-SCREEN
*----------------------------------------------------------------------*
SELECTION-SCREEN: BEGIN OF BLOCK b_sel1 WITH FRAME TITLE text-b01.
SELECT-OPTIONS:
  s_vkorg FOR tvkwz-vkorg NO-EXTENSION NO INTERVALS OBLIGATORY,
  s_vtweg FOR tvkwz-vtweg NO-EXTENSION NO INTERVALS DEFAULT '11'.
SELECTION-SCREEN: END   OF BLOCK b_sel1.

SELECTION-SCREEN: BEGIN OF BLOCK b_sel2 WITH FRAME TITLE text-b02.
SELECT-OPTIONS:
  s_erdat FOR equi-erdat  NO-EXTENSION OBLIGATORY,
  s_equnr FOR equi-equnr.
SELECTION-SCREEN: END   OF BLOCK b_sel2.
*----------------------------------------------------------------------*
*       INITIALIZATION
*----------------------------------------------------------------------*
INITIALIZATION.

* initial process
  PERFORM ini_process.
*----------------------------------------------------------------------*
*       AT SELECTION-SCREEN
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

* Authority check for Sales Organization
  PERFORM authority_check.
*----------------------------------------------------------------------*
*       START-OF-SELECTION
*----------------------------------------------------------------------*
START-OF-SELECTION.

* main process
  PERFORM main_process.
*----------------------------------------------------------------------*
*       END-OF-SELECTION
*----------------------------------------------------------------------*
END-OF-SELECTION.

* log process
  PERFORM display_log.
*&---------------------------------------------------------------------*
*&      Form  ini_process
*&---------------------------------------------------------------------*
*       initial process
*----------------------------------------------------------------------*
FORM ini_process .
  REFRESH:
    i_log,
    i_fieldcat.
ENDFORM.                    " ini_process
*&---------------------------------------------------------------------*
*&      Form  authority_check
*&---------------------------------------------------------------------*
*       Authority check for Sales Organization
*----------------------------------------------------------------------*
FORM authority_check .

* Authority check for Sales Organization
  AUTHORITY-CHECK OBJECT 'V_VBRK_VKO'
           ID 'VKORG' FIELD s_vkorg-low
           ID 'ACTVT' FIELD '03'.
  IF sy-subrc <> 0.
    MESSAGE e002.
  ENDIF.

ENDFORM.                    " authority_check
*&---------------------------------------------------------------------*
*&      Form  main_process
*&---------------------------------------------------------------------*
*       main process
*----------------------------------------------------------------------*
FORM main_process .

  DATA: li_werks TYPE TABLE OF tvkwz,
        li_equnr TYPE TABLE OF ty_equnr.
* Get Plant
  PERFORM get_plant TABLES li_werks.
* Get Equipment No.
  PERFORM get_equipment TABLES li_werks li_equnr.
* Update End Customer
  PERFORM update_end_customer TABLES li_equnr.

ENDFORM.                    " main_process
*&---------------------------------------------------------------------*
*&      Form  get_plant
*&---------------------------------------------------------------------*
*       Get Plant per Sales Organization
*----------------------------------------------------------------------*
FORM get_plant TABLES i_werks.

  SELECT * INTO TABLE i_werks FROM tvkwz
   WHERE vkorg IN s_vkorg
     AND vtweg IN s_vtweg.

ENDFORM.                    " get_plant
*&---------------------------------------------------------------------*
*&      Form  get_equipment
*&---------------------------------------------------------------------*
*       Get Equipment No
*----------------------------------------------------------------------*
FORM get_equipment TABLES i_werks STRUCTURE tvkwz
                          i_equnr.

  IF i_werks[] IS NOT INITIAL.
    SELECT equz~iwerk AS werks         "Plant
           equz~equnr                  "Equipment number
           equz~datbi                  "Valid To Date
*           equz~eqlfn                  "Consecutive numbering
      INTO CORRESPONDING FIELDS OF TABLE i_equnr
      FROM equi INNER JOIN equz
        ON ( equi~equnr = equz~equnr ) "Equipment number
      FOR ALL entries IN i_werks
     WHERE equi~equnr IN s_equnr       "Equipment number
       AND equi~erdat IN s_erdat       "Date created
       AND equz~datbi = '99991231'     "Valid To Date
       AND equz~iwerk = i_werks-werks. "Maintenance Planning Plant
  ENDIF.

ENDFORM.                    " get_equipment
*&---------------------------------------------------------------------*
*&      Form  update_end_customer
*&---------------------------------------------------------------------*
*       Mass load partner role "End customer" in Equipment for CT China
*----------------------------------------------------------------------*
FORM update_end_customer  TABLES i_equnr.

  DATA: lrec_equnr TYPE ty_equnr.
  DATA: l_objnr    TYPE ihpa-objnr,
        l_counter  TYPE ihpa-counter,
        l_sold_to  TYPE ihpa-parnr,
        l_end_cust TYPE ihpa-parnr.
  DATA: lrec_xihpa TYPE ihpavb,
        li_xihpa   TYPE TABLE OF ihpavb,
        lrec_ihpa_chk TYPE ty_ihpa_chk,
        li_ihpa_chk   TYPE TABLE OF ty_ihpa_chk.

* no data process.
  IF i_equnr[] IS INITIAL.
    MESSAGE s001.
    LEAVE LIST-PROCESSING.
  ENDIF.

  LOOP AT i_equnr[] INTO lrec_equnr.
*   Set Object number
    CONCATENATE 'IE' lrec_equnr-equnr INTO l_objnr.

*   Get Partner: Sold-to
    PERFORM get_partner USING l_objnr
                              'AG'
                     CHANGING l_sold_to.
*   Get Partner: End Customer
    PERFORM get_partner USING l_objnr
                              'ZE'
                     CHANGING l_end_cust.

*   Error Log: Sold-to does not exist in Equipment
    IF l_sold_to IS INITIAL.
      PERFORM set_log USING s_vkorg-low s_vtweg-low
                            lrec_equnr-werks lrec_equnr-equnr
                            '003'.
      CONTINUE.
    ENDIF.
*   Error Log: End-Customer already exist in Equipment
    IF l_end_cust IS NOT INITIAL.
      PERFORM set_log USING s_vkorg-low s_vtweg-low
                            lrec_equnr-werks lrec_equnr-equnr
                            '004'.
      CONTINUE.
    ENDIF.
*   Build the End-Customer is equal to Sold-to
    IF l_sold_to IS NOT INITIAL AND l_end_cust IS INITIAL.
      lrec_xihpa-mandt   = sy-mandt.
      lrec_xihpa-objnr   = l_objnr.
      lrec_xihpa-obtyp   = 'IEQ'.
      lrec_xihpa-erdat   = sy-datum.
      lrec_xihpa-erzeit  = sy-uzeit.
      lrec_xihpa-ernam   = sy-uname.
      lrec_xihpa-aedat   = sy-datum.
      lrec_xihpa-aezeit  = sy-uzeit.
      lrec_xihpa-aenam   = sy-uname.
*     Insert new partner
      lrec_xihpa-updkz   = 'I'.
*     'ZE' is End-Customer
      lrec_xihpa-parvw   = 'ZE'.
*     Get Max counter
      PERFORM get_max_counter USING l_objnr
                           CHANGING l_counter.
      lrec_xihpa-counter = l_counter + 1.
      lrec_xihpa-parnr   = l_sold_to.
      APPEND lrec_xihpa TO li_xihpa.
      CLEAR  lrec_xihpa.

*     Build Check Partners table after Mass Update
      lrec_ihpa_chk-vkorg   = s_vkorg-low.
      lrec_ihpa_chk-vtweg   = s_vtweg-low.
      lrec_ihpa_chk-werks   = lrec_equnr-werks.
      lrec_ihpa_chk-equnr   = lrec_equnr-equnr.
      lrec_ihpa_chk-objnr   = l_objnr.
      lrec_ihpa_chk-parvw   = 'ZE'.
      lrec_ihpa_chk-counter = l_counter + 1.
      APPEND lrec_ihpa_chk TO li_ihpa_chk.
      CLEAR  lrec_ihpa_chk.
    ENDIF.
    CLEAR l_objnr.
  ENDLOOP.

  IF li_xihpa IS NOT INITIAL.
*   Partner Update
    CALL FUNCTION 'PM_PARTNER_UPDATE'
      TABLES
        fxihpa = li_xihpa.

*   Check Partner Update
    LOOP AT li_ihpa_chk INTO lrec_ihpa_chk.
      SELECT SINGLE objnr INTO l_objnr FROM ihpa
                         WHERE objnr   = lrec_ihpa_chk-objnr
                           AND parvw   = lrec_ihpa_chk-parvw
                           AND counter = lrec_ihpa_chk-counter.
      IF sy-subrc <> 0.
        PERFORM set_log USING lrec_ihpa_chk-vkorg lrec_ihpa_chk-vtweg
                              lrec_ihpa_chk-werks lrec_ihpa_chk-equnr
                              '005'.
      ELSE.
        PERFORM set_log USING lrec_ihpa_chk-vkorg lrec_ihpa_chk-vtweg
                              lrec_ihpa_chk-werks lrec_ihpa_chk-equnr
                              '006'.
      ENDIF.
    ENDLOOP.
  ENDIF.

* Sort Log
  SORT i_log BY vkorg vtweg werks equnr.

ENDFORM.                    " update_end_customer
*&---------------------------------------------------------------------*
*&      Form  get_partner
*&---------------------------------------------------------------------*
*       Get Partner
*----------------------------------------------------------------------*
FORM get_partner USING objnr parvw
              CHANGING parnr.

  CLEAR: parnr.
  SELECT parnr INTO parnr
    FROM ihpa UP TO 1 ROWS
   WHERE objnr    = objnr
     AND parvw    = parvw
     AND kzloesch = space.
  ENDSELECT.

ENDFORM.                    " get_partner
*&---------------------------------------------------------------------*
*&      Form  display_log
*&---------------------------------------------------------------------*
*       display log
*----------------------------------------------------------------------*
FORM display_log .
  DATA: lrec_layout TYPE slis_layout_alv.

* edit alv fieldcat
  PERFORM edit_alv_fieldcat.

  lrec_layout-colwidth_optimize = 'X'.
  lrec_layout-zebra             = 'X'.
  CALL FUNCTION 'REUSE_ALV_LIST_DISPLAY'
    EXPORTING
      i_callback_program       = sy-repid
      i_callback_pf_status_set = 'SET_STATUS_10001'
      i_callback_user_command  = 'USER_COMMAND'
      is_layout                = lrec_layout
      it_fieldcat              = i_fieldcat
    TABLES
      t_outtab                 = i_log
    EXCEPTIONS
      program_error            = 1
      OTHERS                   = 2.
  IF sy-subrc <> 0.
    EXIT.
  ELSE.
    LEAVE LIST-PROCESSING.
  ENDIF.

ENDFORM.                    " display_log
*&---------------------------------------------------------------------*
*&      Form  edit_alv_fieldcat
*&---------------------------------------------------------------------*
*       edit alv fieldcat
*----------------------------------------------------------------------*
FORM edit_alv_fieldcat.

  DATA: lrec_fieldcat TYPE slis_fieldcat_alv.
  REFRESH: i_fieldcat.

  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
    EXPORTING
      i_structure_name       = 'YSE_UPDATE_PARTNER_LOG'
      i_bypassing_buffer     = 'X'
    CHANGING
      ct_fieldcat            = i_fieldcat
    EXCEPTIONS
      inconsistent_interface = 1
      program_error          = 2
      OTHERS                 = 3.
  IF sy-subrc <> 0.
    EXIT.
  ENDIF.
*  LOOP AT i_fieldcat INTO lrec_fieldcat.
*    CASE lrec_fieldcat-fieldname.
*      WHEN ''.
*    ENDCASE.
*    MODIFY i_fieldcat FROM lrec_fieldcat.
*    CLEAR  lrec_fieldcat.
*  ENDLOOP.

ENDFORM.                    " edit_alv_fieldcat
*&---------------------------------------------------------------------*
*&      Form  set_status_10001
*&---------------------------------------------------------------------*
*       set screen status
*----------------------------------------------------------------------*
FORM set_status_10001 USING i_extab TYPE slis_t_extab.

  SET PF-STATUS '10001'.
  SET TITLEBAR  '10001'.

ENDFORM.                               " set_status_10001
*&---------------------------------------------------------------------*
*&      Form  user_command
*&---------------------------------------------------------------------*
*       user command
*----------------------------------------------------------------------*
FORM user_command  USING    i_ucomm    LIKE sy-ucomm
                            i_selfield TYPE slis_selfield.

  CASE i_ucomm.
    WHEN '&IC1'.
      PERFORM display_process USING i_selfield.
  ENDCASE.

ENDFORM.                               " user_command
*&---------------------------------------------------------------------*
*&      Form  display_process
*&---------------------------------------------------------------------*
*       display process
*----------------------------------------------------------------------*
FORM display_process  USING    selfield TYPE slis_selfield.

  DATA lrec_log TYPE yse_update_partner_log.
  READ TABLE i_log INTO lrec_log INDEX selfield-tabindex.
  IF sy-subrc = 0.
    CASE selfield-fieldname.
      WHEN 'EQUNR'.
        SET PARAMETER ID 'EQN' FIELD lrec_log-equnr.
        CALL TRANSACTION 'IE03' AND SKIP FIRST SCREEN.

    ENDCASE.
  ENDIF.

ENDFORM.                    " display_process
*&---------------------------------------------------------------------*
*&      Form  GET_MAX_COUNTER
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_max_counter  USING    objnr
                      CHANGING counter.
  CLEAR counter.
  SELECT MAX( counter ) INTO counter FROM ihpa WHERE objnr = objnr.

ENDFORM.                    " GET_MAX_COUNTER
*&---------------------------------------------------------------------*
*&      Form  SET_LOG
*&---------------------------------------------------------------------*
*       Set Log
*----------------------------------------------------------------------*
FORM set_log  USING    vkorg vtweg werks equnr msgno.

  DATA: lrec_log   TYPE yse_update_partner_log.
  lrec_log-vkorg = vkorg.
  lrec_log-vtweg = vtweg.
  lrec_log-werks = werks.
  lrec_log-equnr = equnr.
  CASE msgno.
    WHEN '003'.
      lrec_log-msgty = 'E'.
      MESSAGE s003 WITH equnr INTO lrec_log-msgtx.
    WHEN '004'.
      lrec_log-msgty = 'E'.
      MESSAGE s004 WITH equnr INTO lrec_log-msgtx.
    WHEN '005'.
      lrec_log-msgty = 'E'.
      MESSAGE s005 WITH equnr INTO lrec_log-msgtx.
    WHEN '006'.
      lrec_log-msgty = 'S'.
      MESSAGE s006 WITH equnr INTO lrec_log-msgtx.
  ENDCASE.
  APPEND lrec_log TO i_log.

ENDFORM.                    " SET_LOG

*Text symbol text£º
*B01:Sales Org. Data

*B02:Equipment Data
*Selection text£º
*S_EQUNR:        Equipment
*S_ERDAT:        Equipment creation
*S_VKORG:        Sales Org.
*S_VTWEG:        Distribution channel
