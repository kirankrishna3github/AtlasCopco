*----------------------------------------------------------------------*
* PROGRAM ID           : YAM_I001_INST_BASE                            *
* PROGRAM TITLE        : AM: I001 Installed Base CTP                   *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 19/05/2005                                    *
* DEVELOPMENT ID       : I001                                          *
*                                                                      *
* CHANGE REQUEST NUMBER: CD1K902323                                    *
*                                                                      *
* Program Description:  Outbound program to send Installed Base to CTP *
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |2007.08.08|L. Mertens       |CD1K918762|                    *
*           - take into account the deletion mark for selection of BP  *
************************************************************************
REPORT YAM_I001_INST_BASE MESSAGE-ID yam_inf.

************************************************************************
*                   T A B L E S                                        *
************************************************************************
TABLES: YAM_I001,          " Custom table Installed Base CTP
        V_EQUI.            " PM technical objects (EQUI, func.location)

************************************************************************
*                   C O N S T A N T S                                  *
************************************************************************
CONSTANTS :c_mestyp     LIKE edidc-mestyp VALUE 'YAM_I001' ,
           c_idoc_type  LIKE edidc-idoctp VALUE 'YAM_I001' ,
           c_segment(10) TYPE c    VALUE 'YAM_E1I001',
           c_equipment  type eqart value 'EQUIPMENT',
           c_99991231(8)           value '99991231',
           c_yam_i001(8) type c    value 'YAM_I001',
           c_bp         type parvw value 'RE',   " Bill-to-party
           c_k          type objvw value 'K',    " Maintenance contract
           c_c(1)       type c     value 'C',    " Change
           c_a(1)       type c     value 'A',    " Add
           c_x(1)       type c     value 'X',
           c_run_hours  type impt-atnam value 'ZAM_RHRSTOTAL_ACT',
           c_underscore(1) type c  value '_',    " Underscore
           c_ls(2)                 VALUE 'LS',
           c_sc1(3)                VALUE 'SC1',
           c_input                 VALUE '0',
           c_0000(4)    type c     value '0000',
           c_001(3)     type c     value '001',
           c_e          type spras value 'E',    " Language
           c_clnt(4)    TYPE c     VALUE 'CLNT' ,
           c_wbi(3)     TYPE c     VALUE 'WBI'   .

************************************************************************
*                   V A R I A B L E S                                  *
************************************************************************
DATA:  g_equnr  TYPE equi-equnr,                " Equip Number
       g_pfile  like      /sapdmc/lsoinp-filename,
       g_ofile  like      /sapdmc/lsoinp-filename,
       g_delsetupfile like rlgrap-filename,
       g_swerk  TYPE v_equi-swerk,              " Plant
       g_ansdt  like equi-ansdt,                " Acquisition date
       g_retcd  like sy-subrc,                  " return code
       g_subrc  like sy-subrc,                  " return code
       g_index  LIKE sy-tabix,
       g_stat   LIKE jest-stat,                 " System status
       g_directory(25) type c value '/var/load/xxx/UK/convert/',
       g_fm_atinn type ausp-atinn,
       g_point  like imptt-point,               " Measurement point
       g_ersda(12) type c,                      " YYYYMMDDHHMM
       g_date   like sy-datum,
       g_parnr  TYPE i_parnr.                   " Partner Number

************************************************************************
*                  I N T E R N A L   T A B L E S                       *
************************************************************************
DATA : BEGIN OF i_prev OCCURS 0.
        include structure yam_e1i001.
DATA : END OF i_prev.

DATA : BEGIN OF i_equi OCCURS 0.
        include structure yam_e1i001.
DATA :    objnr LIKE v_equi-objnr,         " Object Number
          adrnr LIKE v_equi-adrnr,         " Address Number
          gewrk like v_equi-gewrk,         " Work center ID
          pm_objty like v_equi-pm_objty.   " Work center object type
DATA : END OF i_equi.

DATA : BEGIN OF i_delfiles OCCURS 0,
         mandt like yam_i001-mandt,
         swerk like yam_i001-swerk,
         ersda like yam_i001-ersda,
       END OF i_delfiles.

DATA : BEGIN OF i_equi_delta OCCURS 0.
        include structure yam_e1i001.
DATA : END OF i_equi_delta.

DATA : begin of wa_yam_i001,
         mandt like yam_i001-mandt,
         swerk like yam_i001-swerk,
         ersda like yam_i001-ersda,
       end of wa_yam_i001.

DATA: i_edidc_control_comm LIKE edidc OCCURS 1 WITH HEADER LINE ,
      i_edidd_data LIKE edidd OCCURS 0 WITH HEADER LINE         ,
      wa_edidc LIKE edidc                                       ,
      i_wa_point like impt,
      i_wa_value like imrg,
      wa_equi_delta like i_equi_delta,
      p_logsys LIKE tbdlst-logsys                               .


************************************************************************
*       S E L E C T - O P T I O N S / P A R A M E T E R S              *
************************************************************************

SELECTION-SCREEN : BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.

SELECT-OPTIONS: s_equnr FOR g_equnr,                         "Equip.Nr
                s_swerk FOR g_swerk OBLIGATORY NO INTERVALS. "Plant
PARAMETERS: p_inwrk  like g_swerk,    "Plant to be initial loaded
            p_mestyp LIKE edmsg-msgtyp DEFAULT c_mestyp MODIF ID sc1.

SELECTION-SCREEN: END OF BLOCK b1.

************************************************************************
INITIALIZATION.

  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = p_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
* ELSE.
*   REPLACE FIRST OCCURRENCE OF c_clnt IN p_logsys WITH c_wbi  .
  ENDIF.

************************************************************************
*    Disable Message Type Screen Field                                 *
************************************************************************
AT SELECTION-SCREEN OUTPUT.

  LOOP AT SCREEN.
    IF screen-group1 = c_sc1.
      screen-input   = c_input.
      MODIFY SCREEN.
      CONTINUE.
    ENDIF.
  ENDLOOP.

************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

** Validate if the ALE Configuration is correctly setup
* PERFORM validate_ale_configuration .

** Get the information for selected plant(s)
  LOOP AT s_swerk.

    PERFORM get_detail using s_swerk-low.

*   Create delta with new creations/updates by comparing the selected
*   data with this from the previous run. When a new plant
*   has to be loaded, no comparison has to take place.
    if not i_equi[] is initial.

      if s_swerk-low ne p_inwrk.

        sort i_equi by swerk equnr.

        clear: g_retcd.
        perform get_previous_file using s_swerk-low
                               changing g_retcd.

        if g_retcd = 0.
          PERFORM create_delta.
        else.
          CONTINUE.                      " Pass to next plant
        endif.
      else.
        loop at i_equi.
          move-corresponding i_equi to i_equi_delta.
          move c_a to i_equi_delta-msgfn.
          append i_equi_delta.
          clear i_equi_delta.
        endloop.
      endif.

*     write outputfile to application server
      clear g_retcd.

      CONCATENATE sy-datum sy-uzeit(4) into g_ersda.
      CONCATENATE c_yam_i001 s_swerk-low g_ersda
                INTO g_ofile separated by c_underscore.
      REPLACE 'xxx' IN g_directory WITH p_logsys(3).
      CONCATENATE g_directory g_ofile INTO g_ofile.

      perform write_outputfile.

      if g_retcd is initial.
*       update custom table YAM_I001
        perform update_custom_table using s_swerk-low.

*       Delete older entries in custom table YAM_I001
        PERFORM delete_old_table_entries using s_swerk-low.

*       Delete older files on appl.server
        if NOT i_delfiles[] IS INITIAL.
          PERFORM delete_old_files.
        endif.

      endif.

    endif.

  ENDLOOP.


************************************************************************
*       E N D - O F - S E L E C T I O N    E V E N T                   *
************************************************************************
END-OF-SELECTION .

  IF i_equi_delta[] IS INITIAL .
    write: / 'No Idocs created'(i03).
  else.

** Generate Idoc's
    sort i_equi_delta by equnr.
    PERFORM create_idocs.

  ENDIF.

  write: / 'Job finished'(i02).

*--- S U B R O U T I N E S --------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  Validate_ALE_Configuration
*&---------------------------------------------------------------------*
FORM validate_ale_configuration .

  DATA: l_create_idoc     TYPE     c .

* CHECK IF IDOC CONFIGURATION IS READY AND IDOC CAN BE PROCESSED.
  CALL FUNCTION 'ALE_MODEL_DETERMINE_IF_TO_SEND'
    EXPORTING
      message_type           = p_mestyp
    IMPORTING
      idoc_must_be_sent      = l_create_idoc
    EXCEPTIONS
      own_system_not_defined = 1
      OTHERS                 = 2.

  IF sy-subrc <> 0.
    MESSAGE e029 WITH p_mestyp.
    EXIT.
  ENDIF.

ENDFORM.                    " Validate_ALE_Configuration
*&---------------------------------------------------------------------*
*&      Form  Get_Detail
*&---------------------------------------------------------------------*
*       To get the information for selected plant(s)
*----------------------------------------------------------------------*
FORM get_detail using p_swerk.

  clear i_equi.
  refresh i_equi.

** Select data based on Selection screen parameters
  SELECT equnr sernr matnr eqktx swerk objnr adrnr gewrk pm_objty
           INTO corresponding fields of table i_equi
           FROM v_equi
           WHERE  equnr in s_equnr
             AND  eqart = c_equipment
             AND  datbi = c_99991231
             AND  swerk = p_swerk.

** Process Error - No data found for the Selected Plant
  IF sy-subrc NE 0 .
    write: text-e01, p_swerk.
    EXIT.
  ENDIF .

** Get related info and populate data into Final Internal Table
  LOOP AT i_equi.

*   g_index = sy-tabix.

**  get acquisition date
**  this was not selected from v_equi because this field has
**  a different type than this from i_equi !!!
    select single ansdt into g_ansdt
         from equi
         where equnr = i_equi-equnr.
    move g_ansdt to i_equi-ansdt.

**  get main work center description
    select single ktext into i_equi-ktext
         from crtx
         where objid = i_equi-gewrk
           and objty = i_equi-pm_objty
           and spras = c_e.

**  get BPCS customer number and address of Bill-to-party
    clear g_parnr.
    select single parnr into g_parnr
         from ihpa
         where objnr = i_equi-objnr
* begin of insert MOD-001
           and kzloesch <> 'X'
* end of insert MOD-001
           and parvw = c_bp.

    if sy-subrc = 0.
      select single sortl name1 stras ort01 pstlz regio land1
           into corresponding fields of i_equi
           from kna1
           where kunnr = g_parnr.
    endif.

**  get address of functional location
   select name1 street city1 post_code1 region country tel_number name2
       into (i_equi-fl_name1, i_equi-street, i_equi-city1,
             i_equi-post_code1, i_equi-region, i_equi-country,
             i_equi-tel_number, i_equi-name2)
       from adrc
       where addrnumber = i_equi-adrnr.
    endselect.

**  get product hierarchy
    select single prdha into i_equi-prdha
         from mara
         where matnr = i_equi-matnr.

**  get service contract number
    select sdaufnr into i_equi-vbeln
         from viser02
         where equnr eq i_equi-equnr
           and objvw eq c_k
           and datum le sy-datum.
    endselect.

**  get running hours
*   convert ZAM_RHRSTOTAL into internal value
    clear g_fm_atinn .
    CALL FUNCTION 'CONVERSION_EXIT_ATINN_INPUT'
      EXPORTING
        INPUT  = c_run_hours
      IMPORTING
        OUTPUT = g_FM_ATINN.

*   get measurement point
    if sy-subrc = 0.
      clear g_point.
      select point from IMPTT into g_point
         where mpobj eq i_equi-objnr
           and atinn eq g_fm_atinn.
      endselect.

      if sy-subrc = 0.
*       read last counter
        CALL FUNCTION 'MEASUREM_POINT_LAST_VALUE'
          EXPORTING
            I_POINT           = g_point
          IMPORTING
            E_WA_POINT        = i_wa_POINT
            E_WA_VALUE        = i_wa_VALUE
          EXCEPTIONS
            POINTER_NOT_FOUND = 01.
*
        if sy-subrc <> 0.
          write:/ text-E04, g_POINT.    "No value found for measr.point
        else.
*         convert value into display format
          PERFORM UNIT_CONVERSION USING I_WA_POINT-MSEHI
                                        I_WA_VALUE-READG
                                        i_equi-run_hours
                                        I_WA_POINT-DECIM
                                        I_WA_POINT-EXPON.
        endif.
      endif.
    endif.

**  check deletion flag
    SELECT SINGLE stat into g_stat
       FROM jest
       WHERE objnr = i_equi-objnr
         AND stat EQ 'I0076'
         AND inact NE 'X' .
    IF sy-subrc EQ 0 .
      i_equi-delet = c_x.
    ENDIF.

*   MODIFY i_equi transporting ktext fl_name1 street city1
*            post_code1 region country tel_number name2
*            prdha vbeln run_hours delet.
    MODIFY i_equi.

  ENDLOOP.

ENDFORM.                    " Get_Detail
*---------------------------------------------------------------------
*  FORM UNIT_CONVERSION
*---------------------------------------------------------------------
FORM UNIT_CONVERSION USING P_EINHEIT
                           P_FLTP_WERT
                           P_CHAR_WERT
                           P_DECIMAL
                           P_EXPONENT.

  CLEAR P_CHAR_WERT.
  CHECK NOT ( P_FLTP_WERT IS INITIAL ).

  CALL FUNCTION 'FLTP_CHAR_CONVERSION_FROM_SI'
    EXPORTING
      CHAR_UNIT       = P_EINHEIT
      DECIMALS        = P_DECIMAL
      EXPONENT        = P_EXPONENT
      FLTP_VALUE_SI   = P_FLTP_WERT
      INDICATOR_VALUE = c_x
      MASC_SYMBOL     = ' '
    IMPORTING
      CHAR_VALUE      = P_CHAR_WERT.

ENDFORM.                    "UNIT_CONVERSION
*&---------------------------------------------------------------------*
*&      Form  Create_delta
*&---------------------------------------------------------------------*
*       Create internal table with creations/updates
*----------------------------------------------------------------------*
FORM create_delta.

  loop at i_equi.

    read table i_prev with key equnr = i_equi-equnr
                    binary search.

    if sy-subrc = 0.
***** MUST BE UPDATED when structure of segment YAM_E1I001 is extended *
      if i_equi(555) <> i_prev(555).
************************************************************************
        move-corresponding i_equi to i_equi_delta.
        move c_c to i_equi_delta-msgfn.
        append i_equi_delta.
        clear i_equi_delta.
      endif.
    else.
      move-corresponding i_equi to i_equi_delta.
      move c_a to i_equi_delta-msgfn.
      append i_equi_delta.
      clear i_equi_delta.
    endif.

  endloop.

ENDFORM.                    " Create_delta
*&---------------------------------------------------------------------*
*&      Form  Get_previous_file
*&---------------------------------------------------------------------*
*       Get file from previous run
*----------------------------------------------------------------------*
FORM get_previous_file using p_swerk
                    changing p_retcd.

* prepare filename of previous run
  clear g_ersda.
  select ersda into g_ersda
      from YAM_I001 where swerk = p_swerk.
  endselect.

  if sy-subrc <> 0.
    write: / text-e02, p_swerk.       "No filename of previous run
*                                   available in custom table YAM_I001
    p_retcd = 4.
    exit.
  endif.

  CONCATENATE c_yam_i001 p_swerk g_ersda
                      INTO g_pfile separated by c_underscore.
  REPLACE 'xxx' IN g_directory WITH p_logsys(3).
  CONCATENATE g_directory g_pfile into g_pfile.

* FILE READ FROM APPLICATION SERVER
  PERFORM get_from_appl TABLES  i_prev
                        USING   g_pfile
                                p_retcd.

ENDFORM.                    " Get_previous_file
*&---------------------------------------------------------------------*
*&      Form  Get_from_appl
*&---------------------------------------------------------------------*
*       Get the file from application server into internal table
*----------------------------------------------------------------------*
FORM get_from_appl TABLES i_infile STRUCTURE i_prev
                   USING p_infile p_subrc.

  OPEN DATASET p_infile FOR INPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    write: / text-e03, p_infile.
    p_subrc = 4.
    exit.
  ENDIF.

  refresh i_infile.

  DO.
    READ DATASET p_infile INTO i_infile.
    IF sy-subrc <> 0.
      EXIT.
    ENDIF.
    APPEND i_infile.
  ENDDO.
  CLOSE DATASET p_infile.

ENDFORM.                    " GET_FROM_APPL
*&---------------------------------------------------------------------*
*&      Form  Write_outputfile
*&---------------------------------------------------------------------*
*       Write outputfile
*----------------------------------------------------------------------*
FORM write_outputfile.

  open dataset g_ofile for output in text mode encoding default.
  if sy-subrc <> 0.
    write: / text-e03, g_ofile.
*   message e011(YAM_DC).                      "Open dataset failed
    g_retcd = 4.
    exit.
  endif.

  sort i_equi by equnr.

  loop at i_equi.
***** MUST BE UPDATED when structure of segment YAM_E1I001 is extended
    transfer i_equi(555) to g_ofile.
************************************************************************
  endloop.

ENDFORM.                    " Write_outputfile
*&---------------------------------------------------------------------*
*&      Form  Update_custom_table
*&---------------------------------------------------------------------*
*       Update custom table YAM_I001
*----------------------------------------------------------------------*
FORM update_custom_table using p_swerk.

  wa_yam_i001-mandt = sy-mandt.
  wa_yam_i001-swerk = p_swerk.
  wa_yam_i001-ersda = g_ersda.

  insert YAM_I001 from wa_yam_i001.

ENDFORM.                    " Update_custom_table
*&---------------------------------------------------------------------*
*&      Form  Create_IDocs
*&---------------------------------------------------------------------*
*       Create Idoc's
*----------------------------------------------------------------------*
FORM create_idocs .

  DATA: g_created_comm_idocs TYPE sy-tabix .
  CLEAR : i_edidc_control_comm ,
          wa_edidc             ,
          i_edidd_data         .

** Polulate Control Record
  wa_edidc-mestyp =  p_mestyp.
  wa_edidc-idoctp =  c_idoc_type.
  wa_edidc-rcvprt =  c_ls.
* wa_edidc-rcvprn =  p_logsys .

** Create Idoc's for every new Equipment Number
  LOOP AT i_equi_delta.

    wa_equi_delta = i_equi_delta.

    AT NEW equnr .
      CLEAR i_edidd_data[] .
      i_edidd_data-segnam  = c_segment  .
      i_edidd_data-sdata   = wa_equi_delta.
      APPEND i_edidd_data .
    ENDAT .

    AT NEW swerk.
      clear wa_edidc-rcvprn.
      select single parnum into wa_edidc-rcvprn
           from yam_sel_partner
           where bukrs  = i_equi_delta-swerk
             and msgtyp = p_mestyp.
    ENDAT.

    AT END OF equnr .
** Generate Idoc's

      CALL FUNCTION 'MASTER_IDOC_DISTRIBUTE'
        EXPORTING
          master_idoc_control            = wa_edidc
        TABLES
          communication_idoc_control     = i_edidc_control_comm
          master_idoc_data               = i_edidd_data
        EXCEPTIONS
          error_in_idoc_control          = 1
          error_writing_idoc_status      = 2
          error_in_idoc_data             = 3
          sending_logical_system_unknown = 4
          OTHERS                         = 5.

      IF sy-subrc <> 0.
        MESSAGE e025 .
      ELSE.
        DESCRIBE TABLE i_edidc_control_comm LINES sy-tfill.
        ADD sy-tfill TO g_created_comm_idocs.

        read table i_edidc_control_comm index 1.

        REFRESH i_edidc_control_comm.
      ENDIF.

      call function 'BAPI_TRANSACTION_COMMIT'.

      CALL FUNCTION 'EDI_DOCUMENT_DEQUEUE_LATER'
        EXPORTING
          DOCNUM                 = i_edidc_control_comm-docnum
        EXCEPTIONS
          IDOC_IS_NOT_TO_DEQUEUE = 1
          OTHERS                 = 2.

    ENDAT .

  ENDLOOP .

  write: / text-i01, g_created_comm_idocs .

ENDFORM.                    " Create_IDocs
*&---------------------------------------------------------------------*
*&      Form  Delete_old_files
*&---------------------------------------------------------------------*
*       Delete files from former runs on application server
*----------------------------------------------------------------------*
FORM delete_old_files.

  data: g_dir_name like EPSF-EPSDIRNAM,
        g_dfile    like EPSF-EPSFILNAM.

  loop at i_delfiles.

    CONCATENATE c_yam_i001 i_delfiles-swerk i_delfiles-ersda
                         INTO g_dfile separated by c_underscore.

    move g_directory to g_dir_name.
    REPLACE 'xxx' IN g_dir_name WITH p_logsys(3).
    TRANSLATE g_dir_name(10) to lower case.
    TRANSLATE g_dir_name+17(8) to lower case.

* change MJ 20090602
*    CALL FUNCTION 'EPS_DELETE_FILE'
     call function 'YSE_EPS_DELETE_FILE'
      EXPORTING
        FILE_NAME = g_dfile
        DIR_NAME  = g_dir_name.

    if sy-subrc <> 0.
      write: / text-e05, g_dfile.      "Could not delete file
    endif.

  endloop.

ENDFORM.                    "delete_old_files
*&---------------------------------------------------------------------*
*&      Form  Delete_old_table_entries
*&---------------------------------------------------------------------*
*       Delete entries from former runs in custom table YAM_I001
*----------------------------------------------------------------------*
FORM delete_old_table_entries using p_swerk.

  g_date = sy-datum - 7.
  concatenate g_date c_0000 into g_ersda.

  refresh i_delfiles.

  select * from yam_i001
      where swerk eq p_swerk
        and ersda lt g_ersda.
    move yam_i001 to i_delfiles.
    append i_delfiles.
    clear i_delfiles.
  endselect.

  if sy-subrc = 0.
    delete from yam_i001 where swerk eq p_swerk
                           and ersda lt g_ersda.

    if sy-subrc <> 0.
      write: / text-e06, p_swerk.        "Could not delete entrie(s) in
*                                           table YAM_I001
    endif.

  endif.

ENDFORM.                    "delete_old_table_entries

*Text symbol text£º
*001:Selection Screen Input
*E01:No data available for the Selected Plant :
*E02:No previous run entry found in custom table YAM_I001 for plant :
*E03:Open dataset failed for :
*E04:No counter found for measurement point :
*E05:Could not delete file :
*E06:Could not delete entrie(s) in table YAM_I001 for :
*I01:Number of Idocs created :
*I02:Job finished

*I03:No Idocs created
*Selection text£º
*P_INWRK:        Plant to be initial loaded
*P_MESTYP:        Message Type
*S_EQUNR:D       Equipment
*S_SWERK:D       Maintenance plant
