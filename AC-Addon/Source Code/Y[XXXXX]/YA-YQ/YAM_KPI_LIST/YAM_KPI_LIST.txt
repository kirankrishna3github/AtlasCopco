REPORT yam_kpi_list .

*----------------------------------------------------------------------*
* Author: Tim Volkaert                                                 *
*                                                                      *
* Description: This program will output the selected KPI resuls in a   *
*              tree column display.                                    *
*                                                                      *
*                                                                      *
*                                                                      *
*----------------------------------------------------------------------*
* Modification log.                                                    *
*                                                                      *
*----------------------------------------------------------------------*

*.................. Tables............................................ *
TABLES: t001,    " Company Codes
        tvko.    " Organizational Unit: Sales Organizations

*.................. Types............................................. *
TYPE-POOLS sscr.
TYPES: BEGIN OF ty_kpi_res,
         bukrs TYPE yam_kpi_res-bukrs,
         vkorg TYPE yam_kpi_res-vkorg,
         kpity TYPE yam_kpi_res-kpity,
         kpifr TYPE yam_kpi_res-kpifr,
         datum TYPE yam_kpi_res-datum,
         wert  TYPE yam_kpi_res-wert,
         waers TYPE yam_kpi_res-waers,
         menge TYPE yam_kpi_res-menge,
         kmein TYPE yam_kpi_res-kmein,
         numb  TYPE yam_kpi_res-numb,
       END   OF ty_kpi_res.

*.................. Global variables.................................. *
*.. Variables
DATA: ok_code TYPE sy-ucomm,
      v_restrict TYPE sscr_restrict.

*.. Internal tables
DATA: t_opt_list TYPE sscr_opt_list OCCURS 5 WITH HEADER LINE,
      t_ass      TYPE sscr_ass      OCCURS 5 WITH HEADER LINE,
      t_t001    TYPE STANDARD TABLE OF t001        INITIAL SIZE 0
                                                   WITH HEADER LINE,
      t_tvko    TYPE STANDARD TABLE OF tvko        INITIAL SIZE 0
                                                   WITH HEADER LINE,
      t_kpi_res TYPE STANDARD TABLE OF ty_kpi_res  INITIAL SIZE 0
           WITH HEADER LINE ,
      t_kpi_cus TYPE STANDARD TABLE OF yam_kpi_cus WITH HEADER LINE,
      t_t246    TYPE STANDARD TABLE OF t246        INITIAL SIZE 0
                                                   WITH HEADER LINE,
      t_nodes TYPE treev_ntab,
      t_items TYPE STANDARD TABLE OF mtreeitm,
      t_plvl TYPE lvc_t_plvl WITH HEADER LINE,
      t_fcat TYPE lvc_t_fcat WITH HEADER LINE.

*.. Internal tables for XXL viewer
DATA BEGIN OF t_xmplt_o OCCURS 0.    " For further explanation, please
        INCLUDE STRUCTURE gxxlt_o.   " \ refer to the report XXLFTEST.
DATA END OF   t_xmplt_o.             " \ This report here is similar in
" \ structure, but focuses on list
DATA BEGIN OF t_xmplt_p OCCURS 1.    " \ objects with many different
        INCLUDE STRUCTURE gxxlt_p.   " \ column types.
DATA END   OF t_xmplt_p.

DATA BEGIN OF t_xmplt_h OCCURS 1.
        INCLUDE STRUCTURE gxxlt_h.
DATA END OF   t_xmplt_h.

DATA BEGIN OF t_xmplt_v OCCURS 1.
        INCLUDE STRUCTURE gxxlt_v.
DATA END   OF t_xmplt_v.

DATA BEGIN OF t_xmplt_s OCCURS 1.
        INCLUDE STRUCTURE gxxlt_s.
DATA END   OF t_xmplt_s.

DATA: BEGIN OF t_xmplt_d OCCURS 60,
        vtext TYPE vtext,
        butxt TYPE butxt,
        frmtx TYPE bezei40,
        date  TYPE d,
        value(18) TYPE c,

      END   OF t_xmplt_d.

*.. Field symbols

*.................. Objects........................................... *
DATA: obj_container TYPE REF TO cl_gui_custom_container,
      obj_tree      TYPE REF TO cl_gui_column_tree.

*.................. Constants......................................... *
CONSTANTS: c_hierh TYPE tv_itmname VALUE '&Hierarchy'.  " 'KPI_Info'.

*.................. Selection screen.................................. *
SELECTION-SCREEN BEGIN OF BLOCK b01 WITH FRAME TITLE text-b01.
SELECT-OPTIONS: s_bukrs FOR t001-bukrs.
SELECTION-SCREEN END   OF BLOCK b01.
SELECTION-SCREEN BEGIN OF BLOCK b02 WITH FRAME TITLE text-b02.
SELECT-OPTIONS: s_datum FOR sy-datum OBLIGATORY NO-EXTENSION.
SELECTION-SCREEN END   OF BLOCK b02.

*...................Transaction counter...............................*

 CALL METHOD YCL_STATISTICS=>RECORD_TRANSACTION
  .

*.................. Includes.......................................... *
*$*$- Start of INCLUDE YSC_KPI_LIST_I01                        -(NOU)
*----------------------------------------------------------------------*
***INCLUDE YSC_KPI_LIST_I01 .
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Module  STATUS_0100  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE status_0100 OUTPUT.
  SET PF-STATUS '100'.
  SET TITLEBAR '100'.

ENDMODULE.                 " STATUS_0100  OUTPUT

*eject
*&---------------------------------------------------------------------*
*&      Module  output_tree  OUTPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE output_tree OUTPUT.
  IF obj_tree IS INITIAL.
    PERFORM create_tree.
  ENDIF.
ENDMODULE.                 " output_tree  OUTPUT

*&---------------------------------------------------------------------*
*&      Module  USER_COMMAND_0100  INPUT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
MODULE user_command_0100 INPUT.
  DATA: l_t_rsparams TYPE STANDARD TABLE OF rsparams INITIAL SIZE 0
          WITH HEADER LINE.

  CASE ok_code.
    WHEN 'BACK'. " Finish program
*...... Determine current selection parameters
      CALL FUNCTION 'RS_REFRESH_FROM_SELECTOPTIONS'
        EXPORTING
          curr_report           = sy-cprog
*       IMPORTING
*         SP                    =
        TABLES
          selection_table       = l_t_rsparams
*       EXCEPTIONS
*         NOT_FOUND             = 1
*         NO_REPORT             = 2
*         OTHERS                = 3
                .
      IF sy-subrc <> 0.
* MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*         WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
      ENDIF.

      IF NOT obj_container IS INITIAL.
        " destroy tree container (detroys contained tree control, too)
        CALL METHOD obj_container->free
          EXCEPTIONS
            cntl_system_error = 1
            cntl_error        = 2.
        CLEAR obj_container.
        CLEAR obj_tree.
        SUBMIT (sy-cprog) VIA SELECTION-SCREEN WITH SELECTION-TABLE
          l_t_rsparams.
      ENDIF.
    WHEN 'PRINT'.
      PERFORM print_tree.
    WHEN 'XXL'.
      PERFORM xxl_list_viewer.
  ENDCASE.

ENDMODULE.                 " USER_COMMAND_0100  INPUT
*$*$--- End of INCLUDE YSC_KPI_LIST_I01                        -(NOU)

*$*$- Start of INCLUDE YSC_KPI_LIST_F01                        -(NOU)
*----------------------------------------------------------------------*
***INCLUDE YSC_KPI_LIST_F01 .
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  create_tree
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM create_tree.
*.. Local variables
  DATA: l_hierarchy_header TYPE treev_hhdr,
        l_days             TYPE i,
        l_date             TYPE d,
        l_day_p            TYPE p,
        l_day_c            TYPE wotnr,
        l_nodekey          TYPE tv_nodekey,
        l_index(3)         TYPE n,
        l_name             TYPE tv_itmname,
        l_header           TYPE tv_heading.

*.. Create a screen container for the tree object
  CREATE OBJECT obj_container
    EXPORTING
*      PARENT                      =
      container_name              = 'CTRL_TREE_CONT'
*      STYLE                       =
*      LIFETIME                    = lifetime_default
*      REPID                       =
*      DYNNR                       =
*      NO_AUTODEF_PROGID_DYNNR     =
     EXCEPTIONS
       cntl_error                  = 1
       cntl_system_error           = 2
       create_error                = 3
       lifetime_error              = 4
       lifetime_dynpro_dynpro_link = 5
       OTHERS                      = 6
      .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

*.. Setup the hierarchy header
  l_hierarchy_header-heading = text-h01.
  l_hierarchy_header-width = 40.         " width: 30 characters

*.. Build the first column containing the KPI information (Comp/Country,
*.. KPI description, ...)
  CREATE OBJECT obj_tree
    EXPORTING
*      LIFETIME                    =
      parent                = obj_container
*      SHELLSTYLE                  =
      node_selection_mode   = cl_gui_column_tree=>node_sel_mode_single
*      HIDE_SELECTION              =
      item_selection        = 'X'
      hierarchy_column_name = c_hierh
      hierarchy_header      = l_hierarchy_header
*      NO_HIERARCHY_COLUMN         =
*      NAME                        =
     EXCEPTIONS
       lifetime_error              = 1
       cntl_system_error           = 2
       create_error                = 3
       illegal_node_selection_mode = 4
       failed                      = 5
       illegal_column_name         = 6
       OTHERS                      = 7
      .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

*.. Insert the additional columns depending on the number of days
*.. selected
  IF s_datum-high IS INITIAL.
    l_days = 1.
  ELSE.
    l_days = s_datum-high - s_datum-low + 1.
  ENDIF.

  DO l_days TIMES.
*.... Build column name and header text
    l_index = sy-index.
    CONCATENATE 'Day' l_index INTO l_name.
    l_date = s_datum-low + sy-index - 1.
    CALL FUNCTION 'DAY_IN_WEEK'
      EXPORTING
        datum = l_date
      IMPORTING
        wotnr = l_day_p.
    l_day_c = l_day_p.
*.... Get description for day of week
    READ TABLE t_t246 WITH KEY wotnr = l_day_c.

    WRITE l_date TO l_header DD/MM/YY.
    l_header = l_header+0(5).
    CONCATENATE t_t246-kurzt l_header INTO l_header SEPARATED BY space.

    CALL METHOD obj_tree->add_column
      EXPORTING
        name                         = l_name
*        HIDDEN                       =
*        DISABLED                     =
        alignment                    = cl_gui_column_tree=>align_right
        width                        = 18
*        WIDTH_PIX                    =
*        HEADER_IMAGE                 =
        header_text                  = l_header
*        HEADER_TOOLTIP               =
       EXCEPTIONS
         column_exists                = 1
         illegal_column_name          = 2
         too_many_columns             = 3
         illegal_alignment            = 4
         different_column_types       = 5
         cntl_system_error            = 6
         failed                       = 7
         predecessor_column_not_found = 8
         OTHERS                       = 9
            .
    IF sy-subrc <> 0.
      MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
                 WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    ENDIF.

*.... Add column to fieldcatalog
    t_fcat-h_col_key = l_name.
    t_fcat-outputlen = 15.
    t_fcat-coltext = l_header.
    APPEND t_fcat.

  ENDDO.

  PERFORM build_nodes_and_items.

*.. Add information to tree
  CALL METHOD obj_tree->add_nodes_and_items
    EXPORTING
      node_table                     = t_nodes
      item_table                     = t_items
      item_table_structure_name      = 'MTREEITM'
    EXCEPTIONS
      failed                         = 1
      cntl_system_error              = 2
      error_in_tables                = 3
      dp_error                       = 4
      table_structure_name_not_found = 5
      OTHERS                         = 6.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

*.. Expand root nodes
  CALL METHOD obj_tree->expand_root_nodes
     EXPORTING
*      LEVEL_COUNT         =
       expand_subtree      = 'X'
     EXCEPTIONS
       failed              = 1
       illegal_level_count = 2
       cntl_system_error   = 3
       OTHERS              = 4
          .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
               WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

ENDFORM.                    " create_tree

*eject
*&---------------------------------------------------------------------*
*&      Form  build_nodes_and_items
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM build_nodes_and_items .

*.. Local variables
  DATA: l_node TYPE treev_node,
        l_item TYPE mtreeitm,
        l_vtext     TYPE vtext,
        l_index(3)  TYPE n,
        l_treshold  TYPE treshold2,
        l_kpi_text  TYPE yam_kpi_frmt-bezei,
        l_org_level TYPE yam_kpi_org_level.

*.... Root's children are the KPI's for the organizational level
  LOOP AT t_kpi_res.

    AT NEW bukrs.
*........ New sub level for company code specific entries
      l_org_level-bukrs = t_kpi_res-bukrs.
      CLEAR l_org_level-vkorg.
*........ Select company code name
      READ TABLE t_t001 WITH KEY bukrs = t_kpi_res-bukrs BINARY SEARCH.

*........ Root node is highest level
      CLEAR l_node.
      l_node-node_key = l_org_level.  " Key of the node
      l_node-isfolder = 'X'.              " a folder
      APPEND l_node TO t_nodes.
*........ Update level table
      t_plvl-node_key = l_org_level.
      t_plvl-level = 1.
      APPEND t_plvl.

      CLEAR l_item.
      l_item-node_key = l_org_level.
      l_item-item_name = c_hierh.
      l_item-class = cl_gui_column_tree=>item_class_text. " Text Item
      l_item-text = t_t001-butxt.
      APPEND l_item TO t_items.
    ENDAT.

    AT NEW vkorg.
*...... Entries for Sales org specific
      IF NOT t_kpi_res-vkorg IS INITIAL.
        l_org_level-vkorg = t_kpi_res-vkorg.
        CLEAR l_org_level-bukrs.

*........ Select sales org description
        SELECT SINGLE vtext
        INTO l_vtext
        FROM tvkot
        WHERE spras EQ sy-langu
          AND vkorg EQ t_kpi_res-vkorg.

*........ Root node is company code
        CLEAR l_node.
        l_node-node_key = l_org_level. " Key of the node
        l_node-relatkey = t_kpi_res-bukrs.
        l_node-isfolder = 'X'.              " a folder
        APPEND l_node TO t_nodes.
*........ Update level table
        t_plvl-node_key = l_org_level.
        t_plvl-level = 2.
        APPEND t_plvl.

        CLEAR l_item.
        l_item-node_key = l_org_level.
        l_item-item_name = c_hierh.
        l_item-class = cl_gui_column_tree=>item_class_text. " Text Item
        l_item-text = l_vtext.
        APPEND l_item TO t_items.
      ENDIF.
    ENDAT.

    AT NEW kpifr.
      CLEAR: l_node.
*...... Add new node for every KPI
      CONCATENATE l_org_level t_kpi_res-kpity t_kpi_res-kpifr INTO
        l_node-node_key.
      l_node-relatkey = l_org_level.
      l_node-relatship = cl_gui_column_tree=>relat_last_child.
      APPEND l_node TO t_nodes.
*...... Update level table
      t_plvl-node_key = l_node-node_key.
      IF l_org_level-bukrs IS INITIAL.
        t_plvl-level = 3.
        t_plvl-last_child = 'X'.
      ELSE.
        t_plvl-level = 2.
      ENDIF.
      APPEND t_plvl.
      CLEAR t_plvl.
*...... First item is KPI info
      SELECT SINGLE bezei
      FROM yam_kpi_frmt
      INTO l_kpi_text
      WHERE kpity EQ t_kpi_res-kpity
        AND kpifr EQ t_kpi_res-kpifr.

      CLEAR l_item.
      l_item-node_key = l_node-node_key.
      l_item-item_name = c_hierh.
      l_item-class = cl_gui_column_tree=>item_class_text. " Text Item
      l_item-text = l_kpi_text.
      APPEND l_item TO t_items.

    ENDAT.

*.... Process actual results

*.... Additional node items are the values (every column is one date)
    CLEAR l_item.
    l_item-node_key = l_node-node_key.
    l_index = t_kpi_res-datum - s_datum-low + 1.
    CONCATENATE 'Day' l_index INTO l_item-item_name.
    l_item-class = cl_gui_column_tree=>item_class_text.
    CASE t_kpi_res-kpity.
      WHEN 'Q'.
        WRITE t_kpi_res-menge TO l_item-text UNIT t_kpi_res-kmein
          LEFT-JUSTIFIED.
        l_treshold = t_kpi_res-menge.
        CONCATENATE l_item-text t_kpi_res-kmein INTO l_item-text
          SEPARATED BY space.
      WHEN 'C'.
        WRITE t_kpi_res-wert  TO l_item-text CURRENCY t_kpi_res-waers
            LEFT-JUSTIFIED.
        l_treshold = t_kpi_res-wert.
        IF t_kpi_res-waers EQ '3'.  "  Percentage output
          CONCATENATE l_item-text '%' INTO l_item-text
            SEPARATED BY space.
        ELSE.
          CONCATENATE l_item-text t_kpi_res-waers INTO l_item-text
            SEPARATED BY space.
        ENDIF.
      WHEN 'N'.
        WRITE t_kpi_res-numb  TO l_item-text LEFT-JUSTIFIED.
        l_treshold = t_kpi_res-numb.
    ENDCASE.
    l_item-alignment = cl_gui_column_tree=>align_right.

    READ TABLE t_kpi_cus WITH KEY kpity = t_kpi_res-kpity
                                  kpifr = t_kpi_res-kpifr
                                  bukrs = t_kpi_res-bukrs
                                  vkorg = t_kpi_res-vkorg BINARY SEARCH.
*.... Format value according to treshold value in customizing table
    IF l_treshold >= t_kpi_cus-treshold3.
      l_item-style = cl_tree_control_base=>style_emphasized_negative.
    ELSEIF l_treshold >= t_kpi_cus-treshold2.
      l_item-style = cl_tree_control_base=>style_emphasized.
    ELSE.
      l_item-style = cl_tree_control_base=>style_emphasized_positive.
    ENDIF.

*.... Append column
    APPEND l_item TO t_items.
    t_xmplt_d-vtext = l_vtext.
    t_xmplt_d-butxt = t_t001-butxt.
    t_xmplt_d-frmtx = l_kpi_text.
    t_xmplt_d-date  = t_kpi_res-datum.
    t_xmplt_d-value = l_item-text.
    APPEND t_xmplt_d.
  ENDLOOP.
ENDFORM.                    " build_nodes_and_items

*eject
*&---------------------------------------------------------------------*
*&      Form  xxl_list_viewer
*&---------------------------------------------------------------------*
*       text: Use the XXL API to export result data to Excel
*----------------------------------------------------------------------*
FORM xxl_list_viewer.


*  free: t_xmplt_v,
*        t_xmplt_s,
*        t_xmplt_h.
*
*  t_xmplt_v-col_no = 1.
*  t_xmplt_v-col_name = 'Sales org'.
*  append t_xmplt_v.
*
*  t_xmplt_v-col_no = 2.
*  t_xmplt_v-col_name = 'Company Code'.
*  append t_xmplt_v.
*
*  t_xmplt_h-col_no = 1.
*  t_xmplt_h-row_no = 1.
*  t_xmplt_h-col_name = 'KPI'.
*  append t_xmplt_h.
*
*  t_xmplt_h-col_no = 2.
*  t_xmplt_h-row_no = 1.
*  t_xmplt_h-col_name = 'Datum'.
*  append t_xmplt_h.
*
*  t_xmplt_h-col_no = 2.
*  t_xmplt_h-row_no = 2.
*  t_xmplt_h-col_name = 'Value'.
*  append t_xmplt_h.
*
*  t_xmplt_s-col_no = 1.
*  t_xmplt_s-col_typ = 'STR'.
*  t_xmplt_s-col_ops = 'DFT'.
*  t_xmplt_s-col_src = 1.
*  append t_xmplt_s.
*
*  t_xmplt_s-col_no = 2.
*  t_xmplt_s-col_typ = 'STR'.
*  t_xmplt_s-col_src = 2.
*  append t_xmplt_s.
*
*  t_xmplt_s-col_no = 3.
*  t_xmplt_s-col_typ = 'STR'.
*  t_xmplt_s-col_src = 3.
*  append t_xmplt_s.
*
*  t_xmplt_s-col_no = 4.
*  t_xmplt_s-col_typ = 'STR'.
*  t_xmplt_s-col_src = 4.
*  append t_xmplt_s.
*
*  t_xmplt_s-col_no = 5.
*  t_xmplt_s-col_typ = 'STR'.
*  t_xmplt_s-col_src = 5.
*  append t_xmplt_s.
*
*  call function 'XXL_FULL_API'
*    exporting
**     DATA_ENDING_AT          = -1
**     DATA_STARTING_AT        = 1
**     FILENAME                = ' '
**     HEADER_1                = ' '
**     HEADER_2                = ' '
**     NO_DIALOG               = 'X'
**     NO_START                = ' '
*      n_att_cols              = 2
*      n_hrz_keys              = 3
*      n_vrt_keys              = 2
**     SEMA_TYPE               = ' '
**     SO_TITLE                = ' '
*    tables
*      data                    = t_xmplt_d
*      hkey                    = t_xmplt_h
*      online_text             = t_xmplt_o
*      print_text              = t_xmplt_p
*      sema                    = t_xmplt_s
*      vkey                    = t_xmplt_v
**   EXCEPTIONS
**     CANCELLED_BY_USER       = 1
**     DATA_TOO_BIG            = 2
**     DIM_MISMATCH_DATA       = 3
**     DIM_MISMATCH_SEMA       = 4
**     DIM_MISMATCH_VKEY       = 5
**     ERROR_IN_HKEY           = 6
**     ERROR_IN_SEMA           = 7
**     FILE_OPEN_ERROR         = 8
**     FILE_WRITE_ERROR        = 9
**     INV_DATA_RANGE          = 10
**     INV_WINSYS              = 11
**     INV_XXL                 = 12
**     OTHERS                  = 13
*            .
*  if sy-subrc <> 0.
** MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
**         WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
*  endif.
*
ENDFORM.                    " xxl_list_viewer

*eject
*&---------------------------------------------------------------------*
*&      Form  print_tree
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM print_tree.
  TYPE-POOLS: slis.

  DATA: l_t_chit TYPE lvc_t_chit,
        l_t_nkey TYPE lvc_t_nkey WITH HEADER LINE,
        l_t_lcom TYPE slis_t_listheader,
        l_t_items TYPE lvc_t_item,
        l_t_nodes TYPE lvc_t_node.

  DATA: l_obj_alv TYPE REF TO cl_alv_tree_base.

  APPEND: LINES OF t_items TO l_t_items,
          LINES OF t_nodes TO l_t_nodes.

  LOOP AT t_plvl WHERE level BETWEEN 1 AND 2.
    l_t_nkey = t_plvl-node_key.
    APPEND l_t_nkey.
  ENDLOOP.

  CALL FUNCTION 'ALV_TREE_PRINT_SERVER'
    EXPORTING
      i_hierarchy_header = text-h01
      i_hierarchy_width  = 40
      it_checked_items   = l_t_chit
      i_oo_alv           = l_obj_alv
      i_print_preview    = 'X'
    TABLES
      it_items           = l_t_items
      it_nodes           = l_t_nodes
      it_level           = t_plvl
      it_exp_nodes       = l_t_nkey
      it_fieldcatalog    = t_fcat
      it_list_commentary = l_t_lcom.


ENDFORM.                    " print_tree
*$*$--- End of INCLUDE YSC_KPI_LIST_F01                        -(NOU)

*.................. Events............................................ *
INITIALIZATION.
  PERFORM initialization.

*.................. Main program...................................... *
START-OF-SELECTION.
  PERFORM select_kpi_results.

  SET SCREEN 100.

*.................. Form routines.....................................
*&---------------------------------------------------------------------*
*&      Form  initialization
*&---------------------------------------------------------------------*
*       text: Restrict the select options
*----------------------------------------------------------------------*
FORM initialization.
*.. Restrict Date select option to range or single value
  t_opt_list-name = 'DATE'.
  t_opt_list-options-bt = 'X'.
  t_opt_list-options-eq = 'X'.
  APPEND t_opt_list TO v_restrict-opt_list_tab.

  t_ass-kind = 'S'.
  t_ass-name = 'S_DATUM'.
  t_ass-sg_main = 'I'.
  t_ass-op_main = 'DATE'.
  APPEND t_ass TO v_restrict-ass_tab.

  CALL FUNCTION 'SELECT_OPTIONS_RESTRICT'
    EXPORTING
*     PROGRAM                      =
      restriction                  = v_restrict
*     DB                           = ' '
*   EXCEPTIONS
*     TOO_LATE                     = 1
*     REPEATED                     = 2
*     SELOPT_WITHOUT_OPTIONS       = 3
*     SELOPT_WITHOUT_SIGNS         = 4
*     INVALID_SIGN                 = 5
*     EMPTY_OPTION_LIST            = 6
*     INVALID_KIND                 = 7
*     REPEATED_KIND_A              = 8
*     OTHERS                       = 9
            .
  IF sy-subrc <> 0.
* MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*         WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.


ENDFORM.                    " initialization

*eject
*&---------------------------------------------------------------------*
*&      Form  select_kpi_results
*&---------------------------------------------------------------------*
*       text: Select the KPI results stored in the database table for
*             the selected company code(s)/countries and analysis period
*----------------------------------------------------------------------*
FORM select_kpi_results.

*.. Get list of selected company codes
  SELECT *
  FROM t001
  INTO TABLE t_t001
  WHERE bukrs IN s_bukrs.

  SELECT *
  FROM tvko
  INTO TABLE t_tvko
  WHERE bukrs IN s_bukrs.

  SELECT *
  FROM yam_kpi_cus
  INTO TABLE t_kpi_cus.

*.. Sort company codes
  SORT t_t001 BY bukrs.

*.. Select KPI results
  IF NOT t_t001[] IS INITIAL.
    SELECT bukrs vkorg kpity kpifr datum wert waers menge kmein numb
    APPENDING TABLE t_kpi_res
    FROM yam_kpi_res
    FOR ALL ENTRIES IN t_t001
    WHERE bukrs EQ t_t001-bukrs
      AND datum IN s_datum.
  ENDIF.

  CHECK NOT t_kpi_res[] IS INITIAL.

  SORT t_kpi_res BY bukrs vkorg kpity kpifr datum.

*.. Get the days of the week
  CALL FUNCTION 'WEEKDAY_GET'
*   EXPORTING
*     LANGUAGE                = SY-LANGU
*   IMPORTING
*     RETURN_CODE             =
    TABLES
      weekday                 = t_t246
    EXCEPTIONS
      weekday_not_found       = 1
      OTHERS                  = 2
            .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


ENDFORM.                    " select_kpi_results

*Text symbol text£º

*H01:Company Code/KPI
*Selection text£º
*S_BUKRS:D       Company Code
*S_DATUM:        Reporting date
