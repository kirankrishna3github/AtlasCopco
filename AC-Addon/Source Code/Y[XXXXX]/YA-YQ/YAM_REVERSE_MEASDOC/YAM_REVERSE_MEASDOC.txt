*----------------------------------------------------------------------*
* PROGRAM ID           : YAM_REVERSE_MEASDOC                           *
* PROGRAM TITLE        : Reverse measuring documents                   *
* AUTHOR               : LUC MERTENS                                   *
* DATE                 : 22/03/2012                                    *
* DEVELOPMENT ID       :                                               *
* CHANGE REQUEST NUMBER: CD1K970899                                    *
* PROGRAM DESCRIPTION  : This is a program to reverse meas.documents   *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME               |CORRECTION| CHANGE REF.     *
*----------------------------------------------------------------------*
* MOD-001 |          |                    |CD1K      |                 *
*                                                                      *
*----------------------------------------------------------------------*
REPORT yam_reverse_measdoc MESSAGE-ID yam_re.

*---------------------------------------------------------------------*
* TABLES DECLARATION                                                  *
*---------------------------------------------------------------------*
TABLES: imrg.

*---------------------------------------------------------------------*
* INTERNAL TABLE DECLARATIONS                                         *
*---------------------------------------------------------------------*
DATA: i_bdcdata      LIKE bdcdata OCCURS 0 WITH HEADER LINE,
      lv_mode(1)     TYPE c VALUE 'N',
      struct_bdcdata TYPE bdcdata,
      gt_err         LIKE bdcmsgcoll OCCURS 0 WITH HEADER LINE.

*---------------------------------------------------------------------*
* VARIABLE DECLARATIONS                                               *
*---------------------------------------------------------------------*
DATA: gv_retcd           TYPE sy-subrc,
      gv_msg(120)        TYPE c,
      gv_cnt_ok(5)       TYPE p,
      gv_cnt_nok(5)      TYPE p,
      gv_cnt_tot(5)      TYPE p,
      gv_mestx           LIKE t100-text.

DATA: BEGIN OF i_logtab OCCURS 0,
        cod(1)     TYPE c,
        msg(119)   TYPE c,
      END OF i_logtab.

DATA: BEGIN OF lt_mdocm OCCURS 0,
        mdocm LIKE imrg-mdocm,
      END OF lt_mdocm.

*---------------------------------------------------------------------*
* CONSTANT DECLARATIONS                                               *
*---------------------------------------------------------------------*
*CONSTANTS:

*---------------------------------------------------------------------*
* SELECTION SCREEN                                                    *
*---------------------------------------------------------------------*
PARAMETERS:     p_ernam TYPE imrg-ernam.
SELECT-OPTIONS: s_mdocm FOR  imrg-mdocm OBLIGATORY.

*---------------------------------------------------------------------*
* START-OF-SELECTION                                                  *
*---------------------------------------------------------------------*
START-OF-SELECTION.

  REFRESH: lt_mdocm,
           i_logtab.
  CLEAR:   gv_retcd,
           gv_cnt_ok,
           gv_cnt_nok,
           gv_cnt_tot.

  IF p_ernam IS INITIAL.
    SELECT mdocm INTO CORRESPONDING FIELDS OF TABLE lt_mdocm
           FROM imrg
           WHERE mdocm IN s_mdocm
             AND cancl EQ ' '.
  ELSE.
    SELECT mdocm INTO CORRESPONDING FIELDS OF TABLE lt_mdocm
           FROM imrg
           WHERE mdocm IN s_mdocm
             AND ernam EQ p_ernam
             AND cancl EQ ' '.
  ENDIF.

  IF NOT lt_mdocm[] IS INITIAL.
     LOOP AT lt_mdocm.
      REFRESH: i_bdcdata,
               gt_err.

      PERFORM prep_rev_measdoc USING lt_mdocm-mdocm.

      CALL TRANSACTION 'IK12' USING i_bdcdata
                              MODE lv_mode
                              UPDATE 'S'
                              MESSAGES INTO gt_err.

      IF sy-subrc <> 0.
        MESSAGE i001(00) WITH lt_mdocm-mdocm '-->' text-e14
                         INTO gv_msg.
        PERFORM add_message_to_tab USING gv_msg ' '.
        PERFORM get_trans_messages TABLES gt_err.
        add 1 to gv_cnt_nok.
      ELSE.
*       OK.
        MESSAGE i001(00) WITH lt_mdocm-mdocm '-->' text-i17
                         INTO gv_msg.
        PERFORM add_message_to_tab USING gv_msg ' '.
        add 1 to gv_cnt_ok.
      ENDIF.
    ENDLOOP.
  ELSE.
    MESSAGE i001(00) WITH 'No measuring documents selected'.
    EXIT.
  ENDIF.

* Write log on result screen
  PERFORM write_results.


*--- S U B R O U T I N E S --------------------------------------------*
*&---------------------------------------------------------------------*
*&      Form  add_message_to_tab
*&---------------------------------------------------------------------*
*       Add messages to log
*----------------------------------------------------------------------*
FORM add_message_to_tab USING p_msg p_cod.

  i_logtab-msg = p_msg.
  i_logtab-cod = p_cod.
  APPEND i_logtab.
  CLEAR i_logtab.

ENDFORM.                    " add_message_to_tab

*&---------------------------------------------------------------------*
*&      Form  get_trans_messages
*&---------------------------------------------------------------------*
*       Get the errormessages from the performed transaction
*----------------------------------------------------------------------*
*  -->  p_errtab    Table with errormessages
*----------------------------------------------------------------------*
FORM get_trans_messages TABLES p_errtab STRUCTURE gt_err.

  LOOP AT p_errtab WHERE msgtyp = 'E'  OR
                         msgtyp = 'A'.

    CALL FUNCTION 'RH_MESSAGE_GET'
      EXPORTING
*        SPRSL                   = SY-LANGU
        arbgb                   = p_errtab-msgid
        msgnr                   = p_errtab-msgnr
        msgv1                   = p_errtab-msgv1
        msgv2                   = p_errtab-msgv2
        msgv3                   = p_errtab-msgv3
        msgv4                   = p_errtab-msgv4
      IMPORTING
        msgtext                 = gv_mestx
      EXCEPTIONS
        message_not_found       = 1
        OTHERS                  = 2.

    IF sy-subrc = 0.
      PERFORM add_message_to_tab USING gv_mestx 'E'.
    ENDIF.

  ENDLOOP.

ENDFORM.                    " get_trans_messages

*&---------------------------------------------------------------------*
*&      Form  prep_rev_measdoc
*&---------------------------------------------------------------------*
*       Prepare reversal of measuring documents
*----------------------------------------------------------------------*
*      -->R_MDOCM text
*----------------------------------------------------------------------*
FORM prep_rev_measdoc  USING  r_mdocm.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                       USING  'SAPLIMR0'  '1210'  'X'  ''   ''
                       CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                       USING  ''  ''  ''  'IMRG-MDOCM'  r_mdocm
                       CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                       USING    ''  ''  ''  'BDC_OKCODE'  '/00'
                       CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                       USING  'SAPLIMR0'  '5210'  'X'  ''   ''
                       CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                       USING  ''  ''  ''  'BDC_OKCODE'  '=CNC1'
                       CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                       USING  'SAPLIMR0'  '5210'  'X'  ''   ''
                       CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                       USING  ''  ''  ''  'BDC_OKCODE'  '=BU'
                       CHANGING struct_bdcdata.
  APPEND struct_bdcdata  TO i_bdcdata.
  CLEAR  struct_bdcdata.

ENDFORM.                    " prep_rev_measdoc

*&---------------------------------------------------------------------*
*&      Form  write_results
*&---------------------------------------------------------------------*
*       Write results
*----------------------------------------------------------------------*
FORM write_results.

  LEAVE TO LIST-PROCESSING.

  LOOP AT i_logtab.
    IF i_logtab-cod = ' '.
      WRITE: / i_logtab-msg.
    ELSE.
      FORMAT COLOR COL_NEGATIVE.
      WRITE: /13 i_logtab-msg.
      FORMAT RESET.
    ENDIF.
  ENDLOOP.

  skip.
  uline.
  gv_cnt_tot = gv_cnt_ok + gv_cnt_nok.
  WRITE: / 'Reversed measuring documents          ', gv_cnt_ok,
         / 'NOT reversed measuring documents      ', gv_cnt_nok,
         / 'Total of processed measuring documents', gv_cnt_tot.
  uline.
  WRITE / 'End of program'.

ENDFORM.                    "write_results

*Text symbol text£º
*E14:Reversal failed !

*I17:Measurement document reversed
*Selection text£º
*P_ERNAM:D       .
*S_MDOCM:D       .
