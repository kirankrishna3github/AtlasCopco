*----------------------------------------------------------------------
* PROGRAM ID           : YAM_LIST_OF_CONTRACTS_NRRR                    *
* PROGRAM TITLE        : List of contracts for controller - Non REV.REC*
*                        relevant                                      *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 08/11/2005                                    *
* DEVELOPMENT ID       : SE-CR027                                      *
* CHANGE REQUEST NUMBER: CD1K903778                                    *
* PROGRAM DESCRIPTION  : List all contracts with contract data, but    *
*                        also last invoicing date and amount.          *
*                        Calculation of deferred revenues can be done  *
*                        in excelfile                                  *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |2005.12.13|Luc Mertens     |CD1K904229|                     *
*            - add period of next revenue relevant billing             *
*----------------------------------------------------------------------*

REPORT YAM_LIST_OF_CONTRACTS_NRRR no standard page heading
                             line-size 500.

TABLES: VBAK,                          "Sales Document: Header Data
        VBAP,                          "Sales Document: Item Data
        VEDA,                          "Contract data
        KNA1,                          "General Data in Customer Master
        MARA,                          "General Material Data
        VBFA.                          "Sales Document Flow

SELECT-OPTIONS: S_VBELN     for vbak-vbeln,
                S_AUART     for vbak-auart,
                S_VKORG     for vbak-vkorg obligatory memory id VKO,
                S_VTWEG     for vbak-vtweg obligatory memory id VTW,
                S_SPART     for vbak-spart obligatory memory id SPA.

DATA: begin of gt_vbak occurs 0,
        vbeln like vbak-vbeln,
        vkbur like vbak-vkbur,
        guebg like vbak-guebg,
        gueen like vbak-gueen,
        auart like vbak-auart,
        kunnr like vbak-kunnr,
        ktext like vbak-ktext,
        knumv like vbak-knumv,
      end of gt_vbak.

DATA: begin of gt_vbap occurs 0,
        vbeln like vbap-vbeln,
        posnr like vbap-posnr,
        matnr like vbap-matnr,
        arktx like vbap-arktx,
        netwr like vbap-netwr,
        prctr like vbap-prctr,
      end of gt_vbap.

DATA: begin of gt_vbfa occurs 0,
        vbelv like vbfa-vbelv,
        posnv like vbfa-posnv,
        erdat like vbfa-erdat,
        erzet like vbfa-erzet,
        vbtyp_n like vbfa-vbtyp_n,
        rfwrt like vbfa-rfwrt,
        waers like vbfa-waers,
        vbeln like vbfa-vbeln,
        posnn like vbfa-posnn,
      end of gt_vbfa.

DATA: begin of gt_vbrp occurs 0,
        vbeln like vbrp-vbeln,
        posnr like vbrp-posnr,
      end of gt_vbrp.

DATA: begin of gt_fpla occurs 0,
        vbeln like fpla-vbeln,
        perio like fpla-perio,
        autte like fpla-autte,
        fplnr like fpla-fplnr,
      end of gt_fpla.

DATA: begin of gt_konv occurs 0,
        knumv  like konv-knumv,
        kposn  like konv-kposn,
        kschl  like konv-kschl,
        kbetr  like konv-kbetr,
      end of gt_konv.

DATA: begin of gt_vbkd occurs 0,
        vbeln  like vbkd-vbeln,
        posnr  like vbkd-posnr,
        fplnr  like vbkd-fplnr,
      end of gt_vbkd.

DATA: begin of gt_fplt occurs 0,
        fplnr  like fplt-fplnr,
        fpltr  like fplt-fpltr,
        fkdat  like fplt-fkdat,
        nfdat  like fplt-nfdat,
        fksaf  like fplt-fksaf,
      end of gt_fplt.

DATA: begin of gt_veda occurs 0,
        vbeln  like veda-vbeln,
        vlaufz like veda-vlaufz,
      end of gt_veda.

DATA: begin of gt_kna1 occurs 0,
        kunnr like kna1-kunnr,
        name1 like kna1-name1,
      end of gt_kna1.

DATA: begin of gt_mara occurs 0,
        matnr like mara-matnr,
        prdha like mara-prdha,
      end of gt_mara.

DATA: begin of gt_viser02 occurs 0,
        sdaufnr like viser02-sdaufnr,
        posnr   like viser02-posnr,
        equnr   like viser02-equnr,
        sernr   like viser02-sernr,
      end of gt_viser02.

* begin of insert MOD-001
DATA: begin of gt_vbreve occurs 0,
        vbeln    like vbreve-vbeln,
        posnr    like vbreve-posnr,
        sakrv    like vbreve-sakrv,
        bdjpoper like vbreve-bdjpoper,
      end of gt_vbreve.
* end of insert MOD-001

CONSTANTS: c_zero(6)  type n value '000000',
           c_99991231(8) type c value '99991231',
           c_e        type spras value 'E',
           c_zpn0(4)  type c value 'ZPN0',
           c_vprs(4)  type c value 'VPRS',
           c_compl(1) type c value 'C',
* begin of insert MOD-001
           c_9999999(4) type p decimals 3 value '9999.999',
* end of insert MOD-001
           c_g(1)     type c value 'G',
           c_m(1)     type c value 'M'.

DATA: g_perio(02) type c,
      g_equnr     like riwol-equnr,
      g_eqtxt     like riwol-eqtxt,
      g_sernr     like riwol-sernr,
      g_tplnr     like riwol-tplnr,
      g_fkdat     like fplt-fkdat,
      g_nfdat     like fplt-nfdat,
* begin of insert MOD-001
      g_date      like sy-datum,
      g_err(1)    type c,
* end of insert MOD-001
      g_price     like konv-kbetr,
      g_cost      like konv-kbetr,
      g_profit(4) type p decimals 3,
      g_pltxt     like riwol-pltxt.


*-----------------------------------------------------------------------
START-OF-SELECTION.

  SELECT a~vbeln a~vkbur a~guebg a~gueen a~auart
         a~kunnr
         a~ktext
         a~knumv
        INTO corresponding fields of table gt_vbak
        FROM VBAK as a inner join VBUK as b
        ON a~vbeln eq b~vbeln
        WHERE a~vbeln in s_vbeln
          AND auart in s_auart
          AND vkorg in s_vkorg
          AND vtweg in s_vtweg
          AND spart in s_spart
          AND a~vbtyp eq c_g
          AND b~gbstk ne c_compl.

  if gt_vbak[] is initial.
    message E001(38) with text-e02.     "No objects selected
  endif.

  SELECT vbeln posnr netwr prctr arktx matnr
        INTO corresponding fields of table gt_vbap
        FROM VBAP
        FOR all entries in gt_vbak
        WHERE vbeln eq gt_vbak-vbeln.

  SELECT vbeln perio
         autte
         fplnr
        INTO corresponding fields of table gt_fpla
        FROM FPLA
        FOR all entries in gt_vbak
        WHERE vbeln eq gt_vbak-vbeln.

  SELECT vbeln posnr fplnr
        INTO corresponding fields of table gt_vbkd
        FROM VBKD
        FOR all entries in gt_vbap
        WHERE vbeln eq gt_vbap-vbeln
          AND posnr eq gt_vbap-posnr.

  SELECT knumv kposn kschl kbetr
        INTO corresponding fields of table gt_konv
        FROM KONV
        FOR all entries in gt_vbak
        WHERE knumv eq gt_vbak-knumv.

  SELECT fplnr fpltr fkdat nfdat fksaf
        INTO corresponding fields of table gt_fplt
        FROM FPLT
        FOR all entries in gt_fpla
        WHERE fplnr eq gt_fpla-fplnr.

  SELECT vbeln vlaufz
        INTO corresponding fields of table gt_veda
        FROM VEDA
        FOR all entries in gt_vbak
        WHERE vbeln eq gt_vbak-vbeln.

  SELECT kunnr name1
        INTO corresponding fields of table gt_kna1
        FROM KNA1
        FOR all entries in gt_vbak
        WHERE kunnr eq gt_vbak-kunnr.

  sort gt_kna1 by kunnr.
  delete adjacent duplicates from gt_kna1 comparing kunnr.

  SELECT matnr prdha
        INTO corresponding fields of table gt_mara
        FROM MARA
        FOR all entries in gt_vbap
        WHERE matnr eq gt_vbap-matnr.

  sort gt_mara by matnr.
  delete adjacent duplicates from gt_mara comparing matnr.

  SELECT erdat rfwrt vbtyp_n vbelv posnv erzet waers vbeln posnn
        INTO corresponding fields of table gt_vbfa
        FROM VBFA
        FOR all entries in gt_vbap
        WHERE vbelv eq gt_vbap-vbeln
          AND posnv eq gt_vbap-posnr
          AND vbtyp_n eq c_m.

  SELECT vbeln posnr
        INTO corresponding fields of table gt_vbrp
        FROM VBRP
        FOR all entries in gt_vbfa
        WHERE vbeln eq gt_vbfa-vbeln
          AND posnr eq gt_vbfa-posnn
          AND rrrel eq ' '.

  SELECT equnr sernr sdaufnr posnr
        INTO corresponding fields of table gt_viser02
        FROM VISER02
        FOR all entries in gt_vbap
        WHERE sdaufnr eq gt_vbap-vbeln
          AND posnr   eq gt_vbap-posnr.

* begin of insert MOD-001
  SELECT vbeln posnr sakrv bdjpoper
        INTO corresponding fields of table gt_vbreve
        FROM VBREVE
        FOR all entries in gt_vbap
        WHERE vbeln eq gt_vbap-vbeln
          AND posnr eq gt_vbap-posnr
          AND sakrv eq 'VPRS'.
* end of insert MOD-001
*-----------------------------------------------------------------------
END-OF-SELECTION.

  sort gt_vbak by vbeln.
  sort gt_vbap by vbeln posnr.
  sort gt_fpla by fplnr.
  sort gt_veda by vbeln.
  sort gt_vbfa by vbelv posnv erdat descending erzet descending.
  sort gt_vbrp by vbeln posnr.
  sort gt_viser02 by sdaufnr posnr.
  sort gt_vbkd by vbeln posnr.
  sort gt_fplt by fplnr fpltr.
  sort gt_konv by knumv kposn kschl.
* begin of insert MOD-001
  sort gt_vbreve by vbeln posnr sakrv bdjpoper.
* end of insert MOD-001

  loop at gt_vbap.

    at new vbeln.
      clear gt_vbak.
      read table gt_vbak with key vbeln = gt_vbap-vbeln
                          binary search.

      clear gt_veda-vlaufz.
      read table gt_veda with key vbeln = gt_vbap-vbeln
                          binary search.

      clear gt_kna1-name1.
      read table gt_kna1 with key kunnr = gt_vbak-kunnr
                          binary search.
    endat.

    clear gt_mara-prdha.
    read table gt_mara with key matnr = gt_vbap-matnr
                        binary search.

    clear: g_equnr, g_eqtxt, g_sernr, g_pltxt.
    clear gt_viser02.

    read table gt_viser02 with key sdaufnr = gt_vbap-vbeln
                                   posnr   = gt_vbap-posnr.

    if sy-subrc = 0.

      g_equnr = gt_viser02-equnr.
      g_sernr = gt_viser02-sernr.

      select eqktx tplnr into (g_eqtxt, g_tplnr)
           from v_equi
           where equnr = g_equnr
             and datbi = c_99991231.
      endselect.

      if sy-subrc = 0.
        select single pltxt into g_pltxt
             from iflotx
             where tplnr = g_tplnr
               and spras = c_e.
      endif.

    endif.

    loop at gt_vbfa where vbelv = gt_vbap-vbeln
                      and posnv = gt_vbap-posnr.

*     skip contract items for which the last invoice relates to a
*     billing that is revenue recognition relevant
      read table gt_vbrp with key vbeln = gt_vbfa-vbeln
                                  posnr = gt_vbfa-posnn
                        binary search transporting no fields.

      if sy-subrc ne 0.
        exit.
      endif.

*     calculate profit margin
      clear: g_price, g_cost.

      loop at gt_konv where knumv = gt_vbak-knumv
                        and kposn = gt_vbap-posnr.
        if gt_konv-kschl = c_zpn0.
          g_price = gt_konv-kbetr.
        elseif gt_konv-kschl = c_vprs.
          g_cost = gt_konv-kbetr.
        endif.

      endloop.

* begin of insert MOD-001
      CATCH SYSTEM-EXCEPTIONS ARITHMETIC_ERRORS = 1.
* end of insert MOD-001
      if not ( g_price is initial or
               g_cost is initial ).
          g_profit = ( g_price - g_cost ) / g_price * 100.
      endif.
* begin of insert MOD-001
      ENDCATCH.
      if sy-subrc = 1.
        g_profit = c_9999999.
      endif.
* end of insert MOD-001

*     get invoice frequency and billing period
      clear: g_perio, g_fkdat, g_nfdat.

      read table gt_vbkd with key vbeln = gt_vbap-vbeln
                                  posnr = gt_vbap-posnr
                          binary search.

      if sy-subrc = 0.
        read table gt_fpla with key fplnr = gt_vbkd-fplnr
                            binary search.

        case gt_fpla-perio.
          when 'Z0'.
            g_perio = 1.
          when 'Z1'.
            g_perio = 4.
          when 'Z2'.
            g_perio = 6.
          when 'Z3'.
            g_perio = 12.
          when 'Z4'.
            g_perio = 3.
          when others.
            clear g_perio.
        endcase.

        loop at gt_fplt where fplnr = gt_fpla-fplnr.

          if gt_fplt-fksaf = c_compl.
            g_fkdat = gt_fplt-fkdat.
            g_nfdat = gt_fplt-nfdat.
          endif.

        endloop.
      endif.

* begin of insert MOD-001
* take next revenue relevant billing situated in billing period
      clear: g_date,
             g_err.

      loop at gt_vbreve where vbeln = gt_vbap-vbeln
                          and posnr = gt_vbap-posnr
                          and sakrv = 'VPRS'.

        CALL FUNCTION 'LAST_DAY_IN_PERIOD_GET'
          EXPORTING
            I_GJAHR              = gt_vbreve-bdjpoper(4)
*           I_MONMIT             = 00
            I_PERIV              = 'K4'
            I_POPER              = gt_vbreve-bdjpoper+4(3)
          IMPORTING
            E_DATE               = g_date
          EXCEPTIONS
            INPUT_FALSE          = 1
            T009_NOTFOUND        = 2
            T009B_NOTFOUND       = 3
            OTHERS               = 4.

        IF SY-SUBRC <> 0.
          EXIT.
        ENDIF.

        if g_date le g_nfdat.
          move 'X' to g_err.
        endif.

        exit.                        "only first entry

      endloop.

* end of insert MOD-001

      write: / gt_vbak-vkbur,
             8 gt_vbak-vbeln,
            20 gt_vbak-guebg,
            31 gt_vbak-gueen,
            42 gt_veda-vlaufz no-zero,
            51 g_perio,
            61 gt_vbak-auart,
            66 gt_vbak-kunnr,
            78 gt_kna1-name1,
           114 gt_vbap-arktx,
           155 gt_vbap-prctr,
           166 gt_mara-prdha,
           182 g_equnr,
           200 g_eqtxt,
           240 g_sernr,
           260 gt_vbap-netwr,
           283 g_pltxt,
           324(15) gt_vbfa-erdat,
           340 gt_vbfa-rfwrt,
           362 gt_vbfa-waers,
           371 gt_vbak-ktext(30),
           407 gt_fpla-autte,
           415 g_fkdat,
           428 g_nfdat,
* begin of change MOD-001
*          440 g_profit.
           440 g_profit,
           455 g_date no-zero,
           467 g_err.
* end of change MOD-001

      exit.                       "only first entry (= most recent one)

    endloop.

  endloop.

* begin of insert MOD-001
  skip 2.
  write: / 'Job Finished'(050).
* end of insert MOD-001

*-----------------------------------------------------------------------
TOP-OF-PAGE.

  write:    'Branch'(001),
          8 'Contract_No'(002),
         20 'Start_date'(003),
         31 'End_date'(004),
         42 'Duration'(005),
         51 'Inv.freq.'(006),
         61 'Type'(007),
         66 'Customer_No'(008),
         78 'Name'(009),
        114 'Service_Product'(010),
        155 'Prof.Cent.'(011),
        166 'Service_GAC-PGC'(012),
        182 'Equipment'(021),
        200 'Description'(022),
        240 'Serial_No'(023),
        260 'Tot.Contract_NIS'(013),
        283 'Funct.Loc.Description'(025),
        324 'Last_Inv_Date'(014),
        340 'Last_Inv_Amount'(015),
        362 'Currency'(016),
        371 'Ctr.description'(017),
        402 'In_advance'(018),
        420 'Billing_period'(019),
* begin of change MOD-001
*       439 'Profit_Margin_%'(020).
        455 'First_RR_billing'(026).
* end of change MOD-001

  uline.
  skip.

*Text symbol text£º
*001:Branch
*002:Contract_No
*003:Start_date
*004:End_date
*005:Duration
*006:Inv.freq.
*007:Type
*008:Customer_No
*009:Name
*010:Service_Product
*011:Prof.Cent.
*012:Service_GAC-PGC
*013:Tot.Contract_NIS
*014:Last_Inv_Date
*015:Last_Inv_Amount
*016:Currency
*017:Ctr.description
*018:In_advance
*019:Billing_period
*020:Profit_Margin_%
*021:Equipment
*022:Description
*023:Serial_No
*024:Funct.Location
*025:Funct.Loc.Description
*026:First_RR_billing
*050:Job Finished
*E01:EXECUTE IN BACKGROUND ONLY !

*E02:No objects selected !
*Selection text£º
*S_AUART:        Contract type
*S_SPART:D       Division
*S_VBELN:        Contract number
*S_VKORG:D       Sales Organization
*S_VTWEG:D       Distribution Channel
