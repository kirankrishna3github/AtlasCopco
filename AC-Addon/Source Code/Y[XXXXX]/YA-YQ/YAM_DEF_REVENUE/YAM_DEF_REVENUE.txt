*----------------------------------------------------------------------
* PROGRAM ID           : YAM_DEF_REVENUE                               *
* PROGRAM TITLE        : Deferred revenues                             *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 08/01/2008                                    *
* DEVELOPMENT ID       : FR-CR042                                      *
* CHANGE REQUEST NUMBER: CD1K924882                                    *
* PROGRAM DESCRIPTION  : List all contracts with contract data, item   *
*      and billing plan information to identify on                     *
*      which the deferred revenue is not correctly calculated.         *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*

REPORT YAM_DEF_REVENUE no standard page heading
                       line-size 255.

TABLES: VBAK,                          "Sales Document: Header Data
        VBAP,                          "Sales Document: Item Data
        MARA,                          "General Material Data
        VBFA.                          "Sales Document Flow

SELECT-OPTIONS: S_VBELN     for  vbak-vbeln,
                S_AUART     for  vbak-auart,
                S_VKORG     for  vbak-vkorg obligatory memory id VKO.

PARAMETERS:     P_VTWEG     type vbak-vtweg obligatory memory id VTW,
                P_SPART     type vbak-spart obligatory memory id SPA.

DATA: begin of gt_vbak occurs 0,
        vbeln like vbak-vbeln,
        vkbur like vbak-vkbur,
        auart like vbak-auart,
        kunnr like vbak-kunnr,
        waerk like vbak-waerk,
      end of gt_vbak.

DATA: begin of gt_vbap occurs 0,
        vbeln like vbap-vbeln,
        posnr like vbap-posnr,
        matnr like vbap-matnr,
        netwr like vbap-netwr,
        prctr like vbap-prctr,
      end of gt_vbap.

DATA: begin of gt_vbfa occurs 0,
        vbelv like vbfa-vbelv,
        posnv like vbfa-posnv,
        erdat like vbfa-erdat,
        erzet like vbfa-erzet,
        vbeln like vbfa-vbeln,
        posnn like vbfa-posnn,
        vbtyp_n like vbfa-vbtyp_n,
        rfwrt like vbfa-rfwrt,
      end of gt_vbfa.

DATA: begin of gt_fpla occurs 0,
        vbeln like fpla-vbeln,
        bedat like fpla-bedat,
        endat like fpla-endat,
        perio like fpla-perio,
        fplnr like fpla-fplnr,
      end of gt_fpla.

DATA: begin of gt_vbkd occurs 0,
        vbeln  like vbkd-vbeln,
        posnr  like vbkd-posnr,
        fplnr  like vbkd-fplnr,
      end of gt_vbkd.

DATA: begin of gt_mara occurs 0,
        matnr like mara-matnr,
        prdha like mara-prdha,
      end of gt_mara.

DATA: begin of gt_viser02 occurs 0,
        sdaufnr like viser02-sdaufnr,
        posnr   like viser02-posnr,
        equnr   like viser02-equnr,
        sernr   like viser02-sernr,
      end of gt_viser02.

CONSTANTS: c_compl(1)    type c            value 'C',
           c_g(1)        type c            value 'G'.

DATA: g_perio(2)      type c,
      g_vlaufz(3)     type p decimals 1,
      g_res           type BCA_US_TERM,
      g_tot_inv       type rfwrt,
      g_nr_accrued(3) type p,
      g_diff(3)       type p,
      g_inv_day(6)    type p decimals 2,
      g_calc_date     like sy-datum,
      g_def_rev(6)    type p decimals 2,
      g_equnr         like riwol-equnr,
      g_sernr         like riwol-sernr.

*-----------------------------------------------------------------------
AT SELECTION-SCREEN ON s_vkorg.

  loop at s_vkorg.
    AUTHORITY-CHECK OBJECT 'V_VBAK_VKO'
             ID 'VKORG' FIELD s_vkorg-low
             ID 'VTWEG' FIELD p_vtweg
             ID 'SPART' FIELD p_spart
             ID 'ACTVT' FIELD '03'.

    IF sy-subrc NE 0.
*.... No authorization for sales organisation: &1
      MESSAGE S001(00) WITH text-e03 s_vkorg-low.
      exit.
    ENDIF.
  endloop.

*-----------------------------------------------------------------------
START-OF-SELECTION.

*  SELECT a~vbeln a~vkbur a~auart a~kunnr a~waerk
*        INTO corresponding fields of table gt_vbak
*        FROM VBAK as a inner join VBUK as b
*        ON a~vbeln eq b~vbeln
*        WHERE a~vbeln in s_vbeln
*          AND auart in s_auart
*          AND vkorg in s_vkorg
*          AND vtweg eq p_vtweg
*          AND spart eq p_spart
*          AND a~vbtyp eq c_g
*          AND b~gbstk ne c_compl.
  SELECT vbeln vkbur auart kunnr waerk
        INTO corresponding fields of table gt_vbak
        FROM VBAK
        WHERE vbeln in s_vbeln
          AND auart in s_auart
          AND vkorg in s_vkorg
          AND vtweg eq p_vtweg
          AND spart eq p_spart
          AND vbtyp eq c_g
          and gueen >= sy-datum.

  if gt_vbak[] is initial.
    message E001(38) with text-e02.     "No objects selected
  endif.

  SELECT vbeln posnr netwr prctr matnr
        INTO corresponding fields of table gt_vbap
        FROM VBAP
        FOR all entries in gt_vbak
        WHERE vbeln eq gt_vbak-vbeln.

  SELECT vbeln perio fplnr bedat endat
        INTO corresponding fields of table gt_fpla
        FROM FPLA
        FOR all entries in gt_vbak
        WHERE vbeln eq gt_vbak-vbeln.

  SELECT vbeln fplnr posnr
        INTO corresponding fields of table gt_vbkd
        FROM VBKD
        FOR all entries in gt_vbap
        WHERE vbeln eq gt_vbap-vbeln
          AND posnr eq gt_vbap-posnr.

  SELECT matnr prdha
        INTO corresponding fields of table gt_mara
        FROM MARA
        FOR all entries in gt_vbap
        WHERE matnr eq gt_vbap-matnr.

  sort gt_mara by matnr.
  delete adjacent duplicates from gt_mara comparing matnr.

  SELECT erdat erzet rfwrt vbtyp_n vbelv posnv vbeln posnn
        INTO corresponding fields of table gt_vbfa
        FROM VBFA
        FOR all entries in gt_vbap
        WHERE vbelv eq gt_vbap-vbeln
          AND posnv eq gt_vbap-posnr
          AND vbtyp_n in ('M', 'N').

  SELECT equnr sernr sdaufnr posnr
        INTO corresponding fields of table gt_viser02
        FROM VISER02
        FOR all entries in gt_vbap
        WHERE sdaufnr eq gt_vbap-vbeln
          AND posnr   eq gt_vbap-posnr.

*-----------------------------------------------------------------------
END-OF-SELECTION.

  sort gt_vbak by vbeln.
  sort gt_vbap by vbeln posnr.
  sort gt_fpla by fplnr.
  sort gt_vbfa by vbelv posnv.
  sort gt_viser02 by sdaufnr posnr.
  sort gt_vbkd by vbeln posnr.

* set calculation date
  g_calc_date = sy-datum.

  loop at gt_vbap.

    at new vbeln.
      clear gt_vbak.
      read table gt_vbak with key vbeln = gt_vbap-vbeln
                          binary search.
    endat.

    clear gt_mara-prdha.
    read table gt_mara with key matnr = gt_vbap-matnr
                        binary search.

    clear: g_equnr,
           g_sernr,
           gt_viser02.

    read table gt_viser02 with key sdaufnr = gt_vbap-vbeln
                                   posnr   = gt_vbap-posnr.

    if sy-subrc = 0.
      g_equnr = gt_viser02-equnr.
      g_sernr = gt_viser02-sernr.
    endif.

    read table gt_vbkd with key vbeln = gt_vbap-vbeln
                                posnr = gt_vbap-posnr
                        binary search.

    if sy-subrc = 0.
      read table gt_fpla with key fplnr = gt_vbkd-fplnr
                          binary search.

*.... skip invalid billing dates
      if gt_fpla-bedat is initial or
         gt_fpla-endat is initial.
        continue.
      endif.

*.... duration in year
      clear: g_vlaufz,
             g_res.

      CALL FUNCTION 'BCA_US_DATES_CALC_UNIT'
        EXPORTING
          I_START_DATE     = gt_fpla-bedat
          I_END_DATE       = gt_fpla-endat
          I_PERIODUNIT     = '3'              "months
        IMPORTING
          E_RETURN         = g_res
        EXCEPTIONS
          NOT_WHOLE_NUMBER = 1
          OTHERS           = 2.

      if not g_res is initial.
        g_vlaufz = g_res / 12.
      endif.

*.... invoice frequency
      clear: g_perio.

      case gt_fpla-perio.
        when 'Z0'.
          g_perio = 12.
        when 'Z1'.
          g_perio = 3.
        when 'Z2'.
          g_perio = 2.
        when 'Z3'.
          g_perio = 1.
        when 'Z4'.
          g_perio = 4.
        when others.
          clear g_perio.
      endcase.
    endif.

*.. get totalinvoiced amount (incl. credit notes) at calculation date
    clear g_tot_inv.
    loop at gt_vbfa where vbelv =  gt_vbap-vbeln
                      and posnv =  gt_vbap-posnr
                      and erdat <= g_calc_date.

      if gt_vbfa-vbtyp_n = 'N'.
        g_tot_inv = g_tot_inv - gt_vbfa-rfwrt.
      else.
        g_tot_inv = g_tot_inv + gt_vbfa-rfwrt.
      endif.
    endloop.

*.. calculate number of accrued days
    g_nr_accrued = g_calc_date - gt_fpla-bedat.

*.. calculate invoice amount per day
    g_diff = gt_fpla-endat - gt_fpla-bedat.
    if g_diff > 0.
      g_inv_day = gt_vbap-netwr / g_diff.
    endif.

*.. calculate deferred revenue
    if g_tot_inv <> 0 and g_inv_day <> 0 and g_nr_accrued <> 0.
      g_def_rev = ( g_inv_day * g_nr_accrued ) - g_tot_inv.
    endif.

    write: / gt_vbak-vkbur,
           8 gt_vbak-vbeln,
          20 gt_vbap-posnr,
          31 gt_fpla-bedat,
          42 gt_fpla-endat,
          53 g_vlaufz no-zero,
          65 g_perio,
          72 gt_vbak-auart,
          77 gt_vbak-kunnr,
          89 gt_vbap-prctr,
         100 gt_mara-prdha,
         116 g_equnr no-zero,
         130 g_sernr no-zero,
         141 gt_vbap-netwr currency gt_vbak-waerk,
         162 g_tot_inv currency gt_vbak-waerk,
         186 g_nr_accrued,
         197 g_inv_day,
         216 g_calc_date,
         230 g_def_rev.

  endloop.

*-----------------------------------------------------------------------
TOP-OF-PAGE.

  write:    'Branch'(001),
          8 'Contract_No'(002),
         20 'Item'(009),
         31 'Start_date'(003),
         42 'End_date'(004),
         53 'Duration'(005),
         62 'Inv.freq.'(006),
         72 'Type'(007),
         77 'Customer_No'(008),
         89 'Prof.Cent.'(011),
        100 'Service_GAC-PGC'(012),
        116 'Equipment'(021),
        130 'Serial_No'(023),
        145 'Tot.Contract_NIS'(014),
        168 'Tot.Inv.amount'(015),
        184 'Nr.accrued_days'(016),
        200 'Inv.amount_day'(017),
        216 'Calc.date'(018),
        230 'Deferred_Rev'(019).

  uline.
  skip.

*Text symbol text£º
*001:Branch
*002:Contract_No
*003:Start_date
*004:End_date
*005:Duration
*006:Inv.freq.
*007:Type
*008:Customer_No
*009:Item
*011:Prof.Cent.
*012:Service_GAC-PGC
*014:Tot.Contract_NIS
*015:Tot.Inv.amount
*016:Nr.accrued_days
*017:Inv.amount_day
*018:Calc.date
*019:Deferred_Rev
*020:Profit_Margin_%
*021:Equipment
*022:Description
*023:Serial_No
*E02:No objects selected !

*E03:No authorization for sales organisation: &1
*Selection text£º
*P_SPART:D       Division
*P_VTWEG:D       Distribution Channel
*S_AUART:        Contract type
*S_VBELN:        Contract number
*S_VKORG:D       Sales Organization
