*----------------------------------------------------------------------
* PROGRAM ID           : YAM_MAT_SPARE_PARTS_PRICES                    *
* PROGRAM TITLE        : AM: Listprice for used materials              *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 18/06/2008                                    *
* DEVELOPMENT ID       : CR-0241                                       *
* CHANGE REQUEST NUMBER: CD1K941396                                    *
* PROGRAM DESCRIPTION  : Create material listing with cost and list    *
*                        price + others.                               *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
REPORT YAM_MAT_SPARE_PARTS_PRICES no standard page heading
                                  line-size 255.

* Tables ----------------------------------------------------*
TABLES:
  MARA, MARC, MVKE, MBEW, MAKT,
  SSCRFIELDS.

* Type-pools ------------------------------------------------*
TYPE-POOLS icon.

* Type definition -------------------------------------------*
TYPES: BEGIN OF ty_upload,
         v_text(500)   TYPE c,            " FILE UPLOAD TEXT
       END OF ty_upload.

* Constants ------------------------------------------------*
CONSTANTS:
  C_X(1)    TYPE C     VALUE 'X',
  c_v(1)    type c     value 'V',
  c_01(2)   type c     value '01',
  c_rec(12) type c     value 'YAM_MAT_LIST',
  c_zpro    type kschl value 'ZPRO',
  c_pr00    type kschl value 'PR00'.

* Internal tables ------------------------------------------*
* Selected material data
DATA:  BEGIN OF gt_mat occurs 0,
        MATNR LIKE MARA-MATNR,
        MAKTX LIKE MAKT-MAKTX,
        WERKS LIKE MARC-WERKS,
        PRCTR LIKE MARC-PRCTR,
        stprs like mbew-stprs,
        vkorg like mvke-vkorg,
        vtweg like mvke-vtweg,
        spart like mara-spart,
        prodh LIKE Mvke-PRodh,
        mvgr2 like mvke-mvgr2,
      END OF gt_mat.

* Output
DATA:  BEGIN OF gt_OUTPUT occurs 0,
        MATNR LIKE MARA-MATNR,
        MAKT1 LIKE MAKT-MAKTX,
        MAKT2 LIKE MAKT-MAKTX,
        WERKS LIKE MARC-WERKS,
        PRCTR LIKE MARC-PRCTR,
        stprs like mbew-stprs,
        waers like T001-waers,
        vkorg like mvke-vkorg,
        vtweg like mvke-vtweg,
        spart like mara-spart,
        kbetr like konp-kbetr,
        konwa like konp-konwa,
        prgac(4) type c,
        prpgc(4) type c,
        mvgr2 like mvke-mvgr2,
      END OF gt_OUTPUT.

* All Char fields before writing to outputfile
DATA:  BEGIN OF gt_OUT2 occurs 0,
        MATNR LIKE MARA-MATNR,
        MAKT1 LIKE MAKT-MAKTX,
        MAKT2 LIKE MAKT-MAKTX,
        WERKS LIKE MARC-WERKS,
        PRCTR LIKE MARC-PRCTR,
        stprs(13) type c,
        waers like T001-waers,
        vkorg like mvke-vkorg,
        vtweg like mvke-vtweg,
        spart like mara-spart,
        kbetr(13) type c,
        konwa like konp-konwa,
        prgac(4) type c,
        prpgc(4) type c,
        mvgr2 like mvke-mvgr2,
      END OF gt_OUT2.

* Company table
DATA:  BEGIN OF gt_comp occurs 0,
        bwkey like t001k-bwkey,
        bukrs LIKE t001k-bukrs,
      END OF gt_comp.

* Currency table
DATA:  BEGIN OF gt_curr occurs 0,
        bukrs LIKE t001-bukrs,
        waers LIKE t001-waers,
        spras like t001-spras,
      END OF gt_curr.

* Last created file from application server
DATA: i_upload TYPE STANDARD TABLE OF ty_upload
                       INITIAL SIZE 0 WITH HEADER LINE.

* CTAM companies
DATA:  begin of gt_yam_ctam_ccodes occurs 10.
        include structure YAM_CTAM_CCODES.
DATA:  end of gt_yam_ctam_ccodes.

* Variables -------------------------------------------------------*
DATA: gv_knumh type knumh,
      gv_spras type spras.

DATA: begin of gv_selcr,
        credat  like sy-datum,
        werksfr type werks_d,
        werksto type werks_d,
        slash1(1) value '/',
        vkorgfr type vkorg,
        vkorgto type vkorg,
        slash2(1) value '/',
        vtwegfr type vtweg,
        vtwegto type vtweg,
        slash3(1) value '/',
        spartfr type spart,
        spartto type spart,
*        slash4(1) value '/',
*        mtart   type mtart,
        slash5(1) value '/',
        prctrfr type prctr,
        prctrto type prctr,
*        slash6(1) value '/',
*        matnrfr type matnr,
*        matnrto type matnr,
*        slash7(1) value '/',
*        matklfr type matkl,
*        matklto type matkl,
      end of gv_selcr.

DATA: g_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      g_ofile         LIKE /sapdmc/lsoinp-filename,
      g_ifile         LIKE /sapdmc/lsoinp-filename,
      gv_path         type string,
      p_logsys        LIKE tbdlst-logsys.

* Selection screen -------------------------------------------------*
SELECTION-SCREEN: BEGIN OF BLOCK B1 WITH FRAME TITLE TEXT-001.
SELECT-OPTIONS:
so_werks      FOR marc-werks obligatory,
so_vkorg      for mvke-vkorg obligatory,
so_vtweg      for mvke-vtweg obligatory,
so_spart      for mara-spart obligatory.
PARAMETERS:
p_mtart       type mtart obligatory default 'ZMAT'.
SELECT-OPTIONS:
so_PRCTR      FOR marc-prctr obligatory,
so_matnr      for mara-matnr,
so_matkl      for mara-matkl.
SELECTION-SCREEN skip 1.
parameters:
p_active radiobutton group 01 default 'X',
p_all    radiobutton group 01.
SELECTION-SCREEN END OF BLOCK B1.

SELECTION-SCREEN BEGIN OF BLOCK B3 WITH FRAME TITLE text-003.
parameters p_file(1)     TYPE c DEFAULT ' '.
SELECTION-SCREEN skip 1.
SELECTION-SCREEN: BEGIN OF LINE,
     PUSHBUTTON 2(10) push USER-COMMAND down,
     POSITION 12.
PARAMETERS: p_lastfi     TYPE c LENGTH 8,
            p_fentry(70) type c.
SELECTION-SCREEN: END OF LINE.
parameters p_tofile   type RLGRAP-FILENAME.
SELECTION-SCREEN END OF BLOCK B3.

* Initilization ----------------------------------------------------*
INITIALIZATION.

* Get logical system
  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = p_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
         WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

* Get filename from application server
* and show creation date and first entry
  REPLACE 'xxx' IN g_directory WITH p_logsys(3).
  CONCATENATE g_directory c_rec INTO g_ifile.

  PERFORM get_from_appl TABLES i_upload
                        USING  g_ifile.

  read table i_upload index 1.
  if sy-subrc = 0.
    move i_upload-v_text(8) to p_lastfi.
    move i_upload-v_text+8(70) to p_fentry.
  endif.


* F4 help for filename ------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_tofile.

  PERFORM get_filename CHANGING p_tofile.


* at selection-screen on plant ---------------------------------------*
AT SELECTION-SCREEN ON so_werks.

  loop at so_werks.
    AUTHORITY-CHECK OBJECT 'I_IWERK'
             ID 'TCD'   FIELD sy-tcode
             ID 'IWERK' FIELD so_werks-low.

    IF sy-subrc NE 0.
*.... No authorization for plant
      MESSAGE e001(00) WITH text-e10 so_werks-low.
    ENDIF.
  endloop.


* at selection-screen on sales organisation --------------------------*
AT SELECTION-SCREEN ON so_vkorg.

  loop at so_vkorg.
    AUTHORITY-CHECK OBJECT 'V_VBAK_VKO'
           ID 'VKORG' FIELD so_vkorg
           ID 'ACTVT' FIELD '03'.

    IF sy-subrc <> 0.
*.... No authorization for sales org.
      MESSAGE e001(00) WITH text-e11 so_vkorg-low.
    ENDIF.
  endloop.


* At selection-screen output----------------------------------------*
AT SELECTION-SCREEN OUTPUT.

  CALL FUNCTION 'ICON_CREATE'
    EXPORTING
      name   = 'ICON_EXPORT'
      text   = 'Download'
      info   = 'Down'
    IMPORTING
      RESULT = push
    EXCEPTIONS
      OTHERS = 1.


* At selection-screen ----------------------------------------------*
AT SELECTION-SCREEN.

  case sy-ucomm.
    when 'DOWN'.
      if p_lastfi is initial.
        message E001(00) with text-007.
      endif.

      if P_TOFILE is initial.
        message E001(00) with text-008.
      endif.

*.... Download file to local server
      gv_path = p_tofile.

      CALL FUNCTION 'GUI_DOWNLOAD'
        EXPORTING
          filename              = gv_path
          filetype              = 'ASC'
          write_field_separator = 'X'
        TABLES
          data_tab              = i_upload.
    when others.
  endcase.


* Start of selection -----------------------------------------------*
START-OF-SELECTION.

* Pre-select data
* Get company codes
  select bwkey bukrs into table gt_comp
    from T001K.

  sort gt_comp by bwkey.

* Get Company currencies
  select bukrs waers spras
    into table gt_curr
    from T001.

  sort gt_curr by bukrs.

* Get CTAM company codes
  select * from YAM_CTAM_CCODES
    into table gt_yam_ctam_ccodes.

  sort gt_yam_ctam_ccodes by bukrs.

* Select the materials
  if p_all = c_x.
    SELECT mara~matnr mara~spart
           makt~maktx
           mvke~vkorg mvke~vtweg mvke~prodh mvke~mvgr2
           marc~werks marc~prctr
           mbew~stprs
      INTO corresponding fields of table gt_mat
      FROM mara inner join marc
        on mara~matnr EQ marc~matnr
          left join makt
        on makt~matnr eq mara~matnr and
           makt~spras eq sy-langu
          inner join mvke
        on mvke~matnr eq mara~matnr
          inner join mbew
        on mbew~matnr eq mara~matnr and
           mbew~bwkey eq marc~werks
      WHERE mara~matnr in so_matnr
        and mara~matkl in so_matkl
        and mara~mtart eq p_mtart
        and mara~spart in so_spart
        and marc~werks in so_werks
        and marc~prctr in so_prctr
        and mvke~vkorg in so_vkorg
        and mvke~vtweg in so_vtweg.
  else.
    SELECT mara~matnr mara~spart
           makt~maktx
           mvke~vkorg mvke~vtweg mvke~prodh mvke~mvgr2
           marc~werks marc~prctr
           mbew~stprs
      INTO corresponding fields of table gt_mat
      FROM mara inner join marc
        on mara~matnr EQ marc~matnr
          left join makt
        on makt~matnr eq mara~matnr and
           makt~spras eq sy-langu
          inner join mvke
        on mvke~matnr eq mara~matnr
          inner join mbew
        on mbew~matnr eq mara~matnr and
           mbew~bwkey eq marc~werks
      WHERE mara~matnr in so_matnr
        and mara~matkl in so_matkl
        and mara~mtart eq p_mtart
        and mara~spart in so_spart
        and marc~werks in so_werks
        and marc~prctr in so_prctr
        and mvke~vkorg in so_vkorg
        and mvke~vtweg in so_vtweg
        and mbew~stprs ne 0.
  endif.


* End of selection -----------------------------------------------*
END-OF-SELECTION.

  if gt_mat[] is initial.
    message s001(00) with text-004.
  endif.

* Get additional data
  loop at gt_mat.
    clear gt_output.
    move-corresponding gt_mat to gt_output.
    move gt_mat-maktx to gt_output-makt1.

*.. Get company currency and language
    perform get_company_data.

*.. Get description in country language
    if not gv_spras is initial.
      perform get_local_descr.
    endif.

*.. Get listprices
    perform get_listprices.

*.. Split producthierarchy in GAC and PGC
    move gt_mat-prodh(4)   to gt_output-prgac.
    move gt_mat-prodh+4(4) to gt_output-prpgc.

    append gt_output.
  endloop.

  sort gt_output by matnr werks prctr.

* Create outputfile
  if p_file = 'X'.
    perform create_outputfile.
  else.
    loop at gt_output.
      write: / gt_output-MATNR,
               gt_output-MAKT1,
               gt_output-MAKT2,
               gt_output-WERKS,
               gt_output-PRCTR,
               gt_output-stprs,
               gt_output-waers,
               gt_output-vkorg,
               gt_output-vtweg,
               gt_output-spart,
               gt_output-kbetr,
               gt_output-konwa,
               gt_output-prgac,
               gt_output-prpgc,
               gt_output-mvgr2.
    endloop.
  endif.

  message s001(00) with 'Job finished'.

************************************************************************
*- S U B R O U T I N E S ----------------------------------------------*
************************************************************************
* Get listprices *
******************
FORM get_listprices.

  read table gt_yam_ctam_ccodes with key bukrs = gt_curr-bukrs
     binary search.

  if sy-subrc = 0.
    SELECT SINGLE knumh FROM a304 INTO gv_knumh
                 WHERE kappl =  c_v
                   AND kschl =  c_pr00
                   AND vkorg =  gt_mat-vkorg
                   AND vtweg =  gt_mat-vtweg
                   AND matnr =  gt_mat-matnr
*                  AND kfrst =  ' '
                   AND datab <= sy-datum
                   AND datbi >= sy-datum.

    IF sy-subrc = 0.
      SELECT SINGLE kbetr konwa
        FROM konp
        INTO (gt_output-kbetr, gt_output-konwa)
                WHERE knumh = gv_knumh
                  AND kopos = c_01.
    ENDIF.
  else.
    SELECT SINGLE knumh FROM a954 INTO gv_knumh
                 WHERE kappl =  c_v
                   AND kschl =  c_zpro
                   AND vkorg =  gt_mat-vkorg
                   AND spart =  gt_mat-spart
                   AND matnr =  gt_mat-matnr
                   AND datab <= sy-datum
                   AND datbi >= sy-datum.

    IF sy-subrc = 0.
      SELECT SINGLE kbetr konwa
        FROM konp
        INTO (gt_output-kbetr, gt_output-konwa)
                WHERE knumh = gv_knumh
                  AND kopos = c_01.
    ENDIF.
  endif.

ENDFORM.                    "get_listprices

* Get listprices *
******************
FORM get_local_descr.

  select single maktx into gt_output-makt2
    from MAKT where matnr = gt_mat-matnr
                and spras = gv_spras.

ENDFORM.                    "get_local_descr

* Get company info *
********************
FORM get_company_data.

  read table gt_comp with key bwkey = gt_mat-werks
     binary search.

  if sy-subrc = 0.
    clear gv_spras.

    read table gt_curr with key bukrs = gt_comp-bukrs
       binary search.

    if sy-subrc = 0.
      gv_spras = gt_curr-spras.
      gt_output-waers = gt_curr-waers.
    endif.
  endif.

ENDFORM.                    "get_company_data

* Create outputfile on the application server *
***********************************************
FORM create_outputfile.

* Create outputfile
  REPLACE 'xxx' IN g_directory WITH p_logsys(3).
  CONCATENATE g_directory c_rec INTO g_ofile.

  OPEN DATASET g_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
  IF sy-subrc <> 0.
    WRITE: / text-005, g_ofile.
    EXIT.
  ENDIF.

* Write 1st entry with creation date and selection criteria
  gv_selcr-credat  = sy-datum.
  gv_selcr-werksfr = so_werks-low.
  gv_selcr-werksto = so_werks-high.
  gv_selcr-vkorgfr = so_vkorg-low.
  gv_selcr-vkorgto = so_vkorg-high.
  gv_selcr-vtwegfr = so_vtweg-low.
  gv_selcr-vtwegto = so_vtweg-high.
  gv_selcr-spartfr = so_spart-low.
  gv_selcr-spartto = so_spart-high.
* gv_selcr-mtart   = p_mtart.
  gv_selcr-prctrfr = so_prctr-low.
  gv_selcr-prctrto = so_prctr-high.
*  gv_selcr-matnrfr = so_matnr-low.
*  gv_selcr-matnrto = so_matnr-high.
*  gv_selcr-matklfr = so_matkl-low.
*  gv_selcr-matklto = so_matkl-high.
  transfer gv_selcr to g_ofile.

* In case the structure contains 'non-character' fields (C,N,D,T)
* this needs to be converted before writing them to the outputfile
  loop at gt_output.
    move-corresponding gt_output to gt_out2.
    append gt_out2.
    clear gt_out2.
  endloop.

  loop at gt_out2.
    transfer gt_out2 to g_ofile.
  endloop.

  CLOSE DATASET g_ofile.
  IF sy-subrc <> 0.
    WRITE: / text-006, g_ofile.
    EXIT.
  ENDIF.

ENDFORM.                    "create_outputfil

* Get filename *
****************
FORM get_filename CHANGING p_fname TYPE rlgrap-filename.

* File table and number of selected files
  DATA: t_tabfich TYPE filetable,
        l_rc TYPE i.

  DATA: l_extension TYPE string,
        l_nombre TYPE string,
        l_directorio TYPE string.

* Initialization
  REFRESH t_tabfich.
  CLEAR t_tabfich.

* Open a dialog to choose a file
  CALL METHOD cl_gui_frontend_services=>file_open_dialog
    EXPORTING
*     window_title      = p_window_title
      default_extension = l_extension
      default_filename  = l_nombre
      initial_directory = l_directorio
      multiselection    = space
    CHANGING
      file_table        = t_tabfich
      rc                = l_rc.
*     file_encoding     = l_encoding.

  IF sy-subrc <> 0.
    MESSAGE w003(YAM_AFRU).
  ELSE.
*   Multiple selection is not allowed so only one
*   file could be selected
    READ TABLE t_tabfich INDEX l_rc INTO p_fname.
    gv_path = p_fname.
  ENDIF.

ENDFORM.

* Get file from application server *
************************************
FORM get_from_appl TABLES i_infile STRUCTURE i_upload
                   USING p_infile.

  OPEN DATASET p_infile FOR INPUT IN TEXT MODE ENCODING DEFAULT.

  check sy-subrc = 0.

  DO.
    READ DATASET p_infile INTO i_infile-v_text.
    IF sy-subrc <> 0.
      EXIT.
    ENDIF..
    APPEND i_infile.
  ENDDO.

  CLOSE DATASET p_infile.

ENDFORM.                    " GET_FROM_APPL

*Text symbol text£º
*001:Selection of materials
*002:Which materials?
*003:Output specifications
*004:No materials selected
*005:Open dataset failed for :
*006:Close dataset failed for :
*007:File not found to download !
*008:Please enter the directory path and filename
*E10:No authorisation for plant :

*E11:No authorisation for sales organisation :
*Selection text£º
*P_ACTIVE:        Only active materials(cost #0)
*P_ALL:        All materials
*P_FENTRY:        Selection criteria
*P_FILE:        Create new file?
*P_LASTFI:        Last file: Creation date
*P_MTART:D       .
*P_TOFILE:        Directory path - filename
*SO_MATKL:D       .
*SO_MATNR:D       .
*SO_PRCTR:D       .
*SO_SPART:D       .
*SO_VKORG:D       .
*SO_VTWEG:D       .
*SO_WERKS:D       .
