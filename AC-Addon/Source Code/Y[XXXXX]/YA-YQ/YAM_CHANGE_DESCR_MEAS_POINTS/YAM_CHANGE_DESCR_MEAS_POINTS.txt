REPORT yam_change_descr_meas_points.
*----------------------------------------------------------------------*
* PROGRAM ID           : YAM_CHANGE_DESCR_MEAS_POINTS                  *
* PROGRAM TITLE        : AM: Change description measurement points     *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 13/07/2006                                    *
* DEVELOPMENT ID       :                                               *
*                                                                      *
* CHANGE REQUEST NUMBER: ES-TPR008                                     *
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 | xxxx.xx.xx | xxxxxxxxxxxxxxxxx |xxxxxxxxxx |xxxxxxxxxxxxx| *
************************************************************************
* Program Description:  Change description of measurement points       *
*                       for the selected equipments                    *
************************************************************************
* MOD-001 | 2010.09.27 | Lakshmi R         |CD1K959627 |             | *
* Replace Hard code value for Mode with a variable                     *
************************************************************************


*---------------------------------------------------------------------*
* TABLES                                                              *
*---------------------------------------------------------------------*
TABLES: v_equi.           " PM technical objects (EQUI, func.location)

*---------------------------------------------------------------------*
* CONSTANTS                                                           *
*---------------------------------------------------------------------*
CONSTANTS: c_99991231 TYPE d     VALUE '99991231',
           c_equip    TYPE eqart VALUE 'EQUIPMENT'.

*---------------------------------------------------------------------*
* INTERNAL TABLES                                                     *
*---------------------------------------------------------------------*
DATA: BEGIN OF gt_equi OCCURS 0,
         equnr TYPE equnr,
         objnr TYPE imrc_mpobj,
      END OF gt_equi.

DATA: BEGIN OF i_impt_tab OCCURS 0.
        INCLUDE STRUCTURE impt.
DATA: END   OF i_impt_tab.

DATA: BEGIN OF i_bdcdata OCCURS 0.     "BatchInput screen Data
        INCLUDE STRUCTURE bdcdata.
DATA: END OF i_bdcdata.

*---------------------------------------------------------------------*
* VARIABLES                                                           *
*---------------------------------------------------------------------*
DATA: g_mpobj            LIKE imptt-mpobj,
      g_atbez            TYPE atbez,
      g_curr_dynpro      LIKE ibipbdcd-dynpro,   " current dynpro
      g_curr_program     LIKE syst-repid,        " current program name
*Begin of insert MOD-001
      g_mode             TYPE c LENGTH 1,
*End of insert MOD-001
      g_curr_okcode(5).                          " last issued okcode

*---------------------------------------------------------------------*
* SELECT-OPTIONS/PARAMETERS                                           *
*---------------------------------------------------------------------*
SELECT-OPTIONS: s_equnr FOR v_equi-equnr.

PARAMETERS: p_bukrs TYPE v_equi-bukrs OBLIGATORY,
            p_spras TYPE spras OBLIGATORY.


*- START_OF_SELECTION   -----------------------------------------------*
START-OF-SELECTION.

* Select equipments
  SELECT equnr objnr
    INTO CORRESPONDING FIELDS OF TABLE gt_equi
    FROM v_equi WHERE equnr IN s_equnr
                  AND eqart EQ c_equip
                  AND bukrs EQ p_bukrs
                  AND datbi EQ c_99991231.

  IF gt_equi[] IS INITIAL.
    MESSAGE s001(00) WITH text-s99.
    EXIT.
  ENDIF.

* Retrieve the list of measuring points
  LOOP AT gt_equi.

    CONCATENATE 'IE' gt_equi-equnr INTO g_mpobj.
    REFRESH: i_impt_tab.

    CALL FUNCTION 'MEASUREM_POINTS_READ_TO_OBJECT'
      EXPORTING
        measurement_point_object = g_mpobj
      TABLES
        impt_tab                 = i_impt_tab
      EXCEPTIONS
        no_entry_found           = 1
        OTHERS                   = 2.

    IF i_impt_tab[] IS INITIAL.
      WRITE: / gt_equi-equnr, text-e03.
      CONTINUE.
    ENDIF.

*-- Change Measuring point description
    LOOP AT i_impt_tab.

*---- get descriptions from table CABNT
      SELECT SINGLE atbez INTO g_atbez
        FROM cabnt
        WHERE atinn = i_impt_tab-atinn
          AND spras = p_spras.

      IF sy-subrc NE 0.
        WRITE: / gt_equi-equnr, i_impt_tab-point, text-e01, p_spras.
        CONTINUE.
      ENDIF.

*---- initial screen
      PERFORM bdc_dynpro USING 'SAPLIMR0' '1110'.
      PERFORM bdc_field_no_blank USING 'IMPT-POINT' i_impt_tab-point.
      PERFORM bdc_goto_dynpro USING '/00' 'SAPLIMR0' '5110'.

*---- change measuring point
      PERFORM bdc_field_no_blank USING 'IMPT-PTTXT' g_atbez.
      PERFORM bdc_field  USING 'BDC_OKCODE'  '=BU'.

*---- Perform Call Transaction to change MPoint description
*Begin of comments MOD-001
*      CALL TRANSACTION 'IK02' USING i_bdcdata
*                              MODE 'N' UPDATE 'S'.
*End of comments MOD-001

*Begin of insert MOD-001
      g_mode = 'N'.
      CALL TRANSACTION 'IK02' USING i_bdcdata
                              MODE g_mode
                              UPDATE 'S'.
*End of insert MOD-001

      IF sy-subrc NE 0.
        WRITE: / gt_equi-equnr, i_impt_tab-point, text-e02.
      ENDIF.

      REFRESH i_bdcdata.

    ENDLOOP.

  ENDLOOP.

*-SUBROUTINES *********************************************************
*---------------------------------------------------------------------*
*       FORM BDC_GOTO_DYNPRO                                          *
*---------------------------------------------------------------------*
*  -->  OK_CODE                                                       *
*  -->  PROGRAM                                                       *
*  -->  DYNPRO                                                        *
*---------------------------------------------------------------------*
FORM bdc_goto_dynpro USING ok_code program dynpro.

* using Fcode OK_CODE in current dynp goto next dynpro PROG DYNP.
  PERFORM bdc_okcode USING ok_code.
  PERFORM bdc_dynpro USING program dynpro.

ENDFORM.                    "bdc_goto_dynpro

*----------------------------------------------------------------------*
*       FORM BDC_DYNPRO                                                *
*----------------------------------------------------------------------*
*       Add a dynpro to BDCDATA. INTERNAL Table with all dynpros.      *
*       for a transaction.   See DDIC STRUCTURE BDCDATA                *
*----------------------------------------------------------------------*
*       PROGRAM    Program name
*       DYNPRO     Current Dynpro
*----------------------------------------------------------------------*
FORM bdc_dynpro USING program TYPE ibipbdcd-program
                      dynpro  TYPE clike.

  CLEAR i_bdcdata.                     "Clear header record
  i_bdcdata-program  = program.
  i_bdcdata-dynpro   = dynpro.
  i_bdcdata-dynbegin = 'X'.            "New dynpro flag (X/_)
  APPEND i_bdcdata.
* curr_dynpro_index = syst-tabix.      " note index to bdcdata
  g_curr_program = program.            " record the current screen
  g_curr_dynpro  = dynpro.

ENDFORM.                    "bdc_dynpro

*----------------------------------------------------------------------*
*       FORM BDC_FIELD                                                 *
*----------------------------------------------------------------------*
*       Build the DYNPRO Table BDCDATA, with the field information.    *
*       Here we a defining a field and its contents for the currently
*       Active dynpro.  (Maintain BDCDATA)
*----------------------------------------------------------------------*
*  -->  FIELD_NAME    Name of field on the DYNPRO                      *
*       FIELD_VALUE   Contents of the field                            *
*----------------------------------------------------------------------*
FORM bdc_field USING field_name  TYPE clike
                     field_value TYPE simple. "any type could be passed

  DATA: fieldtype.
  CLEAR i_bdcdata.
  DESCRIBE FIELD field_value TYPE fieldtype.
  i_bdcdata-fnam = field_name.
  IF fieldtype = 'D'.                  "is it a date field
    WRITE field_value TO i_bdcdata-fval.
  ELSE.
    i_bdcdata-fval = field_value.
  ENDIF.
  APPEND i_bdcdata.

ENDFORM.                    "bdc_field

*----------------------------------------------------------------------*
*       FORM BDC_FIELD_NO_BLANK                                        *
*----------------------------------------------------------------------*
*       Build the DYNPRO Table BDCDATA, with the field information.    *
*       Here we a defining a field and its contents for the currently  *
*       Active dynpro.  (Maintain BDCDATA)                             *
*       DO NOT MOVE / SET the field when the source field is BLANK     *
*----------------------------------------------------------------------*
*  -->  FIELD_NAME    Name of field on the DYNPRO                      *
*       FIELD_VALUE   Contents of the field                            *
*----------------------------------------------------------------------*
FORM bdc_field_no_blank USING field_name  TYPE clike
                              field_value TYPE simple.

  CLEAR i_bdcdata.
  IF NOT field_value IS INITIAL
  AND NOT field_value = space.
    PERFORM bdc_field USING field_name field_value.
  ENDIF.

ENDFORM.                    "bdc_field_no_blank

*---------------------------------------------------------------------*
*  set the okcode                                                     *
*---------------------------------------------------------------------*
FORM bdc_okcode  USING field_value TYPE clike. "OK-CODE

* set OK-code with FIELD_VALUE.
  PERFORM bdc_field USING 'BDC_OKCODE'   field_value.
  g_curr_okcode = field_value.

ENDFORM.                    "bdc_okcode

*Text symbol text£º
*E00:Error opening dataset, return code:
*E01:No description found for language :
*E02:Transaction IK02 failed !!!
*E03:Equipment has no measuring points
*I01:Session name
*I02:Open session
*I03:Insert transaction
*I04:Close Session
*I05:Return code =
*I06:Error session created
*S01:Session name
*S02:User
*S03:Keep session
*S04:Lock date
*S05:Processing Mode
*S06:Update Mode
*S07:Generate session
*S08:Call transaction
*S09:Error sessn
*S10:Nodata indicator

*S99:No objects were selected
*Selection text£º
*P_BUKRS:D       Company Code
*P_SPRAS:D       Language Key
*S_EQUNR:D       Equipment
