*&-------------------------------------------------------------------*
*& Report  YAM_HR_IMPORT_INFO_TYPES
*&
*&---------------------------------------------------------------------*
* Program ID        : YAM_HR_IMPORT_INFO_TYPES
* Program Title        : Program to import flat-files into info-types PA2001, PA2002 & PA0003
* Author               : Miguel de Francisco Fernandez
* Date                 : 23/10/2007
* Change Request Number:CD1K923182
* Description         : The purpose of this program is to import the flat-file generated by report YAM_HR_DOWNLOADAFRU and
*                       YAM_HR_REFORMAT to enter data into info-types:
*                       absences (PA2001)
*                       attendances (PA2002)
*                       payroll status (PA0003)
*================================================================*
* Copied From         : (Cloned Program)
* Title               : (Program Title)
* Other Related obj   : (Object names)
*==================================================================*
* Change History Log                                             	*
*------------------------------------------------------------------*
* Mod. no.|  Date    | Name           | Correction Number  | Change
* Reference #
*------------------------------------------------------------------*

REPORT  YAM_HR_IMPORT_INFO_TYPES NO STANDARD PAGE HEADING
         line-size 164
         line-count 65(2)
         message-ID YAM_AFRU.

*&---------------------------------------------------------------------*
*&      TABLES
*&---------------------------------------------------------------------*
tables: PA2001, PA2002, PA0003,AUFK ,T554S.

*-------------------------------------------------------------------*
*  DATA DECLARATIONS
*-------------------------------------------------------------------*
*Types
types: begin of ty_info,
         pernr TYPE pa2002-pernr,       " personnel number
         SUBTY type pa2002-SUBTY,       " Subtype
         OBJPS type pa2002-OBJPS,       "Object type
         SPRPS type pa2002-SPRPS,       "lock indicator
         ENDDA type pa2002-ENDDA,       "end date
         begda type pa2002-begda,       "strat date
         SEQNR type pa2002-SEQNR.       "number of register
types: end of ty_info.

types: begin of ty_error,
         pernr TYPE pa2002-pernr,       " personnel number
         SUBTY type pa2002-SUBTY,       " Subtype
         OBJPS type pa2002-OBJPS,       "Object type
         SPRPS type pa2002-SPRPS,       "lock indicator
         ENDDA type pa2002-ENDDA,       "end date
         begda type pa2002-begda,       "strat date
         SEQNR type pa2002-SEQNR,      "number of register
         txt(220).
types: end of ty_error.

* Internal tables

data: y_info type standard table of ty_info with header line.

data: y_error type standard table of ty_error with header line.

data: begin of y_input occurs 0,
         pernr like pa0105-pernr,       " personnel number
         isdd like afru-isdd,           " execution date
         aufnr like afru-aufnr,         " order
         subty1 like pa2001-subty,      " absence-type
         subty2 like pa2002-subty,      " attendance-type
         start_time like PA2002-beguz,  " start time
         end_time like pa2002-enduz,    " end time
         duration like PA2002-STDAZ, " duration
 END OF y_input.

data: begin of  y_p2001 occurs 0.
        include structure p2001.
data: end of y_p2001.

data: begin of  y_p2002 occurs 0.
        include structure p2002.
data: end of y_p2002.

data: begin of  y_p0003 occurs 0.
        include structure p0003.
data: end of y_p0003.


data: begin of y_bapireturn1 occurs 0.
        include structure bapireturn1.
data: end of y_bapireturn1.

data: begin of y_return occurs 0.
        include structure bapireturn1.
data: end of y_return.

DATA: BEGIN OF y_batch OCCURS 0.
        INCLUDE STRUCTURE BDCDATA.
DATA: END OF y_batch.

DATA: BEGIN OF y_errorbatch OCCURS 0.
        INCLUDE STRUCTURE bdcmsgcoll.
DATA: END OF y_errorbatch.

* Variables
DATA: v_inp_file(80),
      v_pa2001 like p2001-infty value '2001',
      v_pa2002 like p2001-infty value '2002',
      v_pa0003 like p2001-infty value '0003',
      v_path TYPE string,
      v_mode type c value 0,
      mode(1) VALUE 'N' ,
      v_burks like aufk-bukrs.

DATA: g_directory(40) TYPE c VALUE '/var/load/xxx/UK/read/YAM_HR_REFORMAT',
      p_logsys        LIKE tbdlst-logsys,
      v_line type string,
      v_date(10),   "execution date
      v_time(5),    "Total time
      v_time_s(4),  "start time
      v_time_e(4),  "end time
      v_error,      "Error in the proccess
      v_screen(4).  "Screen number
*----------------------------------------------------------------------*
*  SELECTION SCREEN                                                    *
*----------------------------------------------------------------------*
selection-screen begin of block A with frame title text-010.
selection-screen skip 1.
parameters: P_PERNR type pa0105-pernr.  " personnel number
selection-screen skip 1.
parameters: P_R_DATE type dats."To delete data from Absen&Atten
">= refresh date from the file
selection-screen skip 1.
parameters: P_FILE TYPE rlgrap-filename." path + file
selection-screen skip 1.
selection-screen end of block A.

*----------------------------------------------------------------------*
* AT SELECTION-SCREEN                                                  *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR p_file.

  PERFORM get_file_name.
*-------------------------------------------------------------------*
* INITIALIZATION
*-------------------------------------------------------------------*
INITIALIZATION.
*-Clear internal tables
  refresh: y_input,
           y_info,
           y_error,
           y_p2001,
           y_p2002,
           y_p0003,
           y_return,
           y_bapireturn1.

*----------------------------------------------------------------------*
* EVENT     AT SELECTION-SCREEN
*
*----------------------------------------------------------------------
AT SELECTION-SCREEN.

  if sy-batch is initial.
    if sy-ucomm eq 'ONLI'.
      if P_FILE is initial.
        set CURSOR FIELD 'P_FILE'.
        MESSAGE e004.
      ENDIf.
    endif.
  endif.
*&---------------------------------------------------------------------*
*&      START-OF-SELECTION
*&---------------------------------------------------------------------*
START-OF-SELECTION.

* Getting the file that will be imported on the INFO-TYPES
  if sy-batch is initial.
* Importing file from local PC
    perform get_file_local.
  else.
* Importing file from SAP server
    perform get_file.
  endif.

* Dealing with the imported data to load it on the corresponding info-types
  perform load_info_types.

* Display error log
  perform error_log.

*&---------------------------------------------------------------------*
*&      END-OF-SELECTION
*&---------------------------------------------------------------------*
END-OF-SELECTION.
*&---------------------------------------------------------------------*
*&      Form  GET_FILE
*&---------------------------------------------------------------------*
* To import the file that from a SAP server
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM GET_FILE .

  CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
    IMPORTING
      own_logical_system             = p_logsys
    EXCEPTIONS
      own_logical_system_not_defined = 1
      OTHERS                         = 2.

  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
        WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

  REPLACE 'xxx' IN g_directory WITH p_logsys(3).

  v_inp_file = g_directory.

  OPEN DATASET v_inp_file FOR INPUT IN TEXT MODE ENCODING DEFAULT.

  IF SY-SUBRC NE 0.
    MESSAGE e002.
    EXIT.
  ENDIF.

  DO.
    read DATASET v_inp_file INTO v_line.
    IF SY-SUBRC NE 0.
      EXIT.
    ENDIF.
    y_input-pernr = v_line(8).
    y_input-isdd = v_line+8(8).
    y_input-aufnr = v_line+16(12).
    y_input-subty1 = v_line+28(4).
    y_input-SUBTY2 = v_line+32(4).
    y_input-start_time = v_line+36(6).
    y_input-end_time = v_line+42(6).
    y_input-duration = v_line+48.
    APPEND y_input.
    clear y_input.
  ENDDO.

  CLOSE DATASET v_inp_file.

  IF sy-subrc NE 0.
    MESSAGE e001 WITH v_inp_file.
    EXIT.
  ENDIF.

ENDFORM.                    " GET_FILE
*&---------------------------------------------------------------------*
*&      Form  LOAD_INFO_TYPES
*&---------------------------------------------------------------------*
*       Dealing with imported data to load it on the corresponding
*       INFO-TYPE
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM LOAD_INFO_TYPES .

  sort y_input by PERNR ISDD AUFNR SUBTY1 SUBTY2 START_TIME END_TIME
  DURATION.

* Dealing with the data
  if not P_R_DATE is initial.
    IF not P_PERNR is initial.
* Select from PA2001 to delete the data that fullfills the requirements
      select PERNR SUBTY OBJPS SPRPS ENDDA begda SEQNR into table
                                          y_info from PA2001 where
                                            pernr = p_pernr and
                                            begda >= p_r_date.
* Deletion for PA2001
      loop at y_info.

        move-corresponding y_info to y_p2001.
        move-CORRESPONDING y_info to y_error.

        y_p2001-infty = V_PA2001.

        perform deletion_info_type_2001.

        clear Y_info.
        clear y_error.

      endloop.
* Select from PA2002 to delete the data that fullfills the requirements
      clear y_info.
      refresh y_info.

      select PERNR SUBTY OBJPS SPRPS ENDDA begda SEQNR into table
                                         y_info from PA2002 where
                                              pernr = p_pernr and
                                              begda >= p_r_date.

* Deletion for PA2002
      loop at y_info.

        move-corresponding y_info to y_p2002.
        move-CORRESPONDING y_info to y_error.

        y_p2002-infty = V_PA2002.

        perform deletion_info_type_2002.

        clear Y_info.
        clear y_error.

      endloop.

      clear y_info.
      refresh y_info.
* load data from the file into PA2001 and PA2002
      loop at y_input where pernr = p_pernr.

        move-CORRESPONDING y_input to y_error.

        if not y_input-subty1 is initial.
* PA2001
          perform update_info_type_2001.

        endif.

        if not y_input-subty2 is initial.
* PA2002
          perform update_info_type_2002.

        endif.

      endloop.

    else. "p_pernr initial

      loop at y_input.

* Select from PA2001 to delete the data that fullfills the requirements
        clear y_info.
        refresh y_info.

        select PERNR SUBTY OBJPS SPRPS ENDDA begda SEQNR into table
                                           y_info from PA2001 where
                                              pernr = y_input-pernr and
                                              begda = y_input-isdd.
* Deletion for PA2001
        loop at y_info.

          move-corresponding y_info to y_p2001.

          y_p2001-infty = V_PA2001.

          perform deletion_info_type_2001.

        endloop.

* Select from PA2002 to delete the data that fullfills the requirements
        clear y_info.
        refresh y_info.

        select PERNR SUBTY OBJPS SPRPS ENDDA begda SEQNR into table
                                           y_info from PA2002 where
                                                 pernr = y_input-pernr and
                                                 begda = y_input-isdd.

* Deletion for PA2002
        loop at y_info.

          move-corresponding y_info to y_p2002.

          y_p2001-infty = V_PA2002.

          perform deletion_info_type_2002.

        endloop.

      endloop.
    endif.

  else. "refresh of date is initial

    loop at y_input.

* Data to update on the PA0003
*      y_p0003-prdat = y_input-isdd.  "Earliest personal retroactive accounting date
*      y_p0003-bderr = y_input-isdd.  "Recalculation for time evaluation

*      at new pernr.

*        if p_pernr eq space or p_pernr = y_input-pernr.

* Update info type 0003

*          select PERNR SUBTY OBJPS SPRPS ENDDA begda SEQNR into table
*                                             y_info from PA0003 where
*                                             pernr = y_input-pernr.

*          if sy-subrc = 0.

*            read table y_info with KEY pernr = y_input-pernr.

*            move-CORRESPONDING y_info to y_p0003.

*            y_p0003-infty = V_PA0003.      "info type number

*            perform update_info_type_0003.

*          else.
* Error: info type PA0003
*            move-CORRESPONDING y_p0003 to y_error.
*            y_error-txt = text-001.
*            APPEND y_error.
*            clear y_error.
*          endif.
*        endif.
*    endat.

      at new isdd. "execution date

        if p_pernr eq space or p_pernr = y_input-pernr.

* Select from PA2001 to delete the data that fullfills the requirements
          clear y_info.
          refresh y_info.

          select PERNR SUBTY OBJPS SPRPS ENDDA begda SEQNR into table
                                             y_info from PA2001 where
                                                   pernr = y_input-pernr and
                                                   begda = y_input-isdd.

* delete info-type PA2001
          loop at y_info.

            move-corresponding y_info to y_p2001.

            y_p2001-infty = V_PA2001.

            perform deletion_info_type_2001.

          endloop.

* Select from PA2002 to delete the data that fullfills the requirements
          clear y_info.
          refresh y_info.

          select PERNR SUBTY OBJPS SPRPS ENDDA begda SEQNR into table
                                             y_info from PA2002 where
                                                   pernr = y_input-pernr and
                                                   begda = y_input-isdd.
* delete info-type PA2002
          loop at y_info.

            move-corresponding y_info to y_p2002.

            y_p2002-infty = V_PA2002.

            perform deletion_info_type_2002.

          endloop.

        endif.
      endat.

      if p_pernr is initial or p_pernr = y_input-pernr.
* load records on PA2001 and PA2002
        if not y_input-subty1 is initial.

          perform update_info_type_2001.

        endif.
        clear y_p2001.
        refresh y_p2001.
        if not y_input-subty2 is initial.

          perform update_info_type_2002.

        endif.
        clear y_p2002.
        refresh y_p2002.

      endif.
    endloop.
  endif.

ENDFORM.                    " LOAD_INFO_TYPES
*&---------------------------------------------------------------------*
*&      Form  DELETION_INFO_TYPE
*&---------------------------------------------------------------------*
*       Deletion of info-types PA2001
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM deletion_info_type_2001 .

  CALL FUNCTION 'BAPI_EMPLOYEE_ENQUEUE'
    EXPORTING
      number = y_p2001-pernr
    IMPORTING
      return = y_return.

* Dealing with possible error
  if y_return-type eq 'E'.
    move-corresponding y_p2001 to y_error.
    y_error-txt = y_return-message.
    APPEND y_error.
    v_error = 'X'.
  endif.


  if v_error is initial.

    CALL FUNCTION 'HR_INFOTYPE_OPERATION'
  EXPORTING
            INFTY                  = y_p2001-infty
            NUMBER                 = y_p2001-pernr
            SUBTYPE                = y_p2001-subty
            OBJECTID               = y_p2001-objps
            LOCKINDICATOR          = y_p2001-sprps
            VALIDITYEND            = y_p2001-endda
            VALIDITYBEGIN          = y_p2001-begda
            RECORDNUMBER           = y_p2001-SEQNR
            RECORD                 = y_p2001
            OPERATION              = 'DEL'
*                   TCLAS                  = 'A'
            DIALOG_MODE            = v_mode
*                   NOCOMMIT               =
*                   VIEW_IDENTIFIER        =
*                   SECONDARY_RECORD       =
        IMPORTING
          RETURN                 = y_bapireturn1.

* Dealing with possible error
    if y_bapireturn1-type eq 'E'.
      move-corresponding y_p2001 to y_error.
      y_error-txt = y_bapireturn1-message.
      APPEND y_error.
      clear y_error.
    endif.


    CLEAR y_bapireturn1.
    refresh y_bapireturn1.

    CALL FUNCTION 'BAPI_EMPLOYEE_DEQUEUE'
      EXPORTING
        number = y_p2001-pernr
      IMPORTING
        return = y_return.

* Dealing with possible error
    if y_return-type eq 'E'.
      y_error-txt = y_return-message.
      APPEND y_error.
      clear y_error.
    endif.
  endif.

  CLEAR y_return.
  refresh y_return.
  clear y_p2001.
  refresh y_p2001.

ENDFORM.                    " DELETION_INFO-TYPE
*&---------------------------------------------------------------------*
*&      Form  GET_FILE_LOCAL
*&---------------------------------------------------------------------*
*       Upload file from the local PC
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM GET_FILE_LOCAL .

  TRANSLATE P_FILE TO LOWER CASE.

  v_path = P_FILE.

  CALL FUNCTION 'GUI_UPLOAD'
    EXPORTING
   FILENAME                       = v_path
   FILETYPE                       = 'ASC'
   HAS_FIELD_SEPARATOR            = 'X'
*   HEADER_LENGTH                 = 0
*   READ_BY_LINE                  = 'X'
*   DAT_MODE                      = ' '
*   CODEPAGE                      = ' '
*   IGNORE_CERR                   = ABAP_TRUE
*   REPLACEMENT                   = '#'
*   CHECK_BOM                     = ' '
*   VIRUS_SCAN_PROFILE            =
*   NO_AUTH_CHECK                 = ' '
* IMPORTING
*   FILELENGTH                    =
*   HEADER                        =
TABLES
  DATA_TAB                        = y_input
EXCEPTIONS
 FILE_OPEN_ERROR               = 1
 FILE_READ_ERROR               = 2
 NO_BATCH                      = 3
 GUI_REFUSE_FILETRANSFER       = 4
 INVALID_TYPE                  = 5
 NO_AUTHORITY                  = 6
 UNKNOWN_ERROR                 = 7
 BAD_DATA_FORMAT               = 8
 HEADER_NOT_ALLOWED            = 9
 SEPARATOR_NOT_ALLOWED         = 10
 HEADER_TOO_LONG               = 11
 UNKNOWN_DP_ERROR              = 12
 ACCESS_DENIED                 = 13
 DP_OUT_OF_MEMORY              = 14
 DISK_FULL                     = 15
 DP_TIMEOUT                    = 16
 OTHERS                        = 17.

  IF SY-SUBRC <> 0.
    MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
            WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
  ENDIF.


ENDFORM.                    " GET_FILE_LOCAL
*&---------------------------------------------------------------------*
*&      Form  DELETION_INFO_TYPE_2002
*&---------------------------------------------------------------------*
*       Deletion of info-type PA2002
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM DELETION_INFO_TYPE_2002 .

  CALL FUNCTION 'BAPI_EMPLOYEE_ENQUEUE'
    EXPORTING
      number = y_p2002-pernr
    IMPORTING
      return = y_return.

* Dealing with possible error
  if y_return-type eq 'E'.
    move-corresponding y_p2002 to y_error.
    y_error-txt = y_return-message.
    APPEND y_error.
    clear y_error.
    v_error = 'X'.
  endif.

  CLEAR y_return.
  refresh y_return.

  if v_error is initial.

    CALL FUNCTION 'HR_INFOTYPE_OPERATION'
              EXPORTING
                        INFTY                  = y_p2002-infty
                        NUMBER                 = y_p2002-pernr
                        SUBTYPE                = y_p2002-subty
                        OBJECTID               = y_p2002-objps
                        LOCKINDICATOR          = y_p2002-sprps
                        VALIDITYEND            = y_p2002-endda
                        VALIDITYBEGIN          = y_p2002-begda
                        RECORDNUMBER           = y_p2002-SEQNR
                        RECORD                 = y_p2002
                        OPERATION              = 'DEL'
*                   TCLAS                  = 'A'
                        DIALOG_MODE            = v_mode
*                   NOCOMMIT               =
*                   VIEW_IDENTIFIER        =
*                   SECONDARY_RECORD       =
                    IMPORTING
                      RETURN                 = y_bapireturn1.

* Dealing with possible error
    if y_bapireturn1-type eq 'E'.
      move-corresponding y_p2002 to y_error.
      y_error-txt = y_bapireturn1-message.
      APPEND y_error.
      clear y_error.
    endif.

    CALL FUNCTION 'BAPI_EMPLOYEE_DEQUEUE'
      EXPORTING
        number = y_p2002-pernr
      IMPORTING
        return = y_return.

* Dealing with possible error
    if y_return-type eq 'E'.
      move-corresponding y_p2002 to y_error.
      y_error-txt = y_return-message.
      APPEND y_error.
      clear y_error.
    endif.

  endif.

  CLEAR y_return.
  refresh y_return.
  clear y_p2002.
  refresh y_p2002.

ENDFORM.                    " DELETION_INFO_TYPE_2002
*&---------------------------------------------------------------------*
*&      Form  UPDATE_INFO_TYPE_2001
*&---------------------------------------------------------------------*
*       Update information on info type PA2001
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM UPDATE_INFO_TYPE_2001 .
* Call transaction to upload data into info type PA2001.

  PERFORM bdc_dynpro using 'SAPMP50A' '1000'.

  PERFORM bdc_field USING 'BDC_OKCODE' '/00'.

  PERFORM bdc_field USING 'RP50G-PERNR' y_input-pernr.

  PERFORM bdc_field USING 'RP50G-TIMR6' 'X'.

  PERFORM bdc_field using 'RP50G-CHOIC' V_PA2001.

  PERFORM bdc_field using 'RP50G-SUBTY' y_input-subty1.
* Place correct format to the date fields
  perform format_date using y_input-isdd CHANGING v_date.

  PERFORM bdc_field using 'RP50G-ENDDA' v_date.

  PERFORM bdc_field using 'RP50G-BEGDA' v_date.


  PERFORM bdc_dynpro using 'SAPMP50A' '1000'.

  PERFORM bdc_field USING 'BDC_OKCODE' 'INS'.

  PERFORM bdc_field USING 'RP50G-PERNR' y_input-pernr.

  PERFORM bdc_field USING 'RP50G-TIMR6' 'X'.

  PERFORM bdc_field using 'RP50G-ENDDA' v_date.

  PERFORM bdc_field using 'RP50G-BEGDA' v_date.

  if y_input-SUBTY1 eq '0100'.
    v_screen = '2001'.
  elseif y_input-subty1 eq '0200'.
    v_screen = '2002'.
  ELSEIF y_input-subty1 eq '0900'.
    v_screen = '2000'.
  endif.

  PERFORM bdc_dynpro using 'MP200000' v_screen.

  PERFORM bdc_field using 'BDC_OKCODE' '/00'.

  PERFORM bdc_field using 'P2001-BEGDA' v_date.

  PERFORM bdc_field using 'P2001-ENDDA' v_date.

  v_time_s = y_input-start_time.

  PERFORM bdc_field using 'P2001-BEGUZ' v_time_s.

*  v_time_e = y_input-end_time.

*  PERFORM bdc_field using 'P2001-ENDUZ' v_time_e.

  v_time = y_input-duration.

  REPLACE '.' with ',' into v_time.

  PERFORM bdc_field using 'P2001-STDAZ' v_time.

  PERFORM bdc_dynpro using 'MP200000' v_screen.

  PERFORM bdc_field using 'BDC_OKCODE' '=UPD'.

  PERFORM bdc_field using 'P2001-BEGUZ' v_time_s.

*  v_time_e = y_input-end_time.

*  PERFORM bdc_field using 'P2001-ENDUZ' v_time_e.

  v_time = y_input-duration.

  REPLACE '.' with ',' into v_time.

  PERFORM bdc_field using 'P2001-STDAZ' v_time.

  CALL TRANSACTION 'PA30' USING y_batch
                   MODE mode
                   UPDATE 'S'
                   MESSAGES INTO y_errorbatch.

  clear y_batch.
  refresh y_batch.
  clear v_time.
  clear v_time_e.
  clear v_time_s.
  clear v_screen.

*   Dealing with possible error
  loop at y_errorbatch.
    if y_errorbatch-msgtyp eq 'E' or
       y_errorbatch-msgtyp eq  'S'.

* To eliminate the messages when the process goes ok
      check not y_errorbatch-MSGNR  eq '200'.
      check not y_errorbatch-MSGNR eq '102'.

      CALL FUNCTION 'MESSAGE_TEXT_BUILD'
        EXPORTING
          MSGID               = y_errorbatch-msgid
          MSGNR               = y_errorbatch-msgnr
          MSGV1               = y_errorbatch-msgv1
          MSGV2               = y_errorbatch-msgv2
          MSGV3               = y_errorbatch-msgv3
          MSGV4               = y_errorbatch-msgv4
        IMPORTING
          MESSAGE_TEXT_OUTPUT = y_error-txt.

      y_error-pernr = y_input-pernr.
      y_error-SUBTY = y_input-SUBTY1.
      y_error-ENDDA = y_input-isdd.
      y_error-begda = y_input-isdd.
      append y_error.
      clear y_error.
    endif.
  ENDLOOP.

  clear y_errorbatch.
  refresh y_errorbatch.

ENDFORM.                    " UPDATE_INFO_TYPE_2001
*&---------------------------------------------------------------------*
*&      Form  UPDATE_INFO_TYPE_2002
*&---------------------------------------------------------------------*
*       Update info type PA2002
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM UPDATE_INFO_TYPE_2002 .

** Searching for the Company code for the orders
*  if not y_input-aufnr is initial.
*    select single BUKRS into v_burks from aufk WHERE
*                                          aufnr eq y_input-aufnr.
*
*  endif.
*
*  if sy-subrc ne 0.
*    y_error-pernr = y_input-pernr.
*    y_error-SUBTY = y_input-SUBTY2.
*    y_error-ENDDA = y_input-isdd.
*    y_error-txt = text-013.
*    append y_error.
*    clear y_error.
*    clear y_input-aufnr.
*  endif.

* Call transaction to upload data into info type PA2002.

  PERFORM bdc_dynpro using 'SAPMP50A' '1000'.

  PERFORM bdc_field USING 'BDC_OKCODE' '/00'.

  PERFORM bdc_field USING 'RP50G-PERNR' y_input-pernr.

  PERFORM bdc_field USING 'RP50G-TIMR6' 'X'.

  PERFORM bdc_field using 'RP50G-CHOIC' V_PA2002.

  PERFORM bdc_field using 'RP50G-SUBTY' y_input-subty2.

* Place correct format to the date fields
  perform format_date using y_input-isdd CHANGING v_date.

  PERFORM bdc_field using 'RP50G-ENDDA' v_date.

  PERFORM bdc_field using 'RP50G-BEGDA' v_date.

  PERFORM bdc_dynpro using 'SAPMP50A' '1000'.

  PERFORM bdc_field USING 'BDC_OKCODE' 'INS'.

  PERFORM bdc_field USING 'RP50G-PERNR' y_input-pernr.

  PERFORM bdc_field USING 'RP50G-TIMR6' 'X'.

  PERFORM bdc_field using 'RP50G-ENDDA' v_date.

  PERFORM bdc_field using 'RP50G-BEGDA' v_date.

  PERFORM bdc_dynpro using 'MP200000' '2051'.

  if not y_input-aufnr is initial.
    PERFORM bdc_field using 'BDC_OKCODE' '=PRIM'.
  else.
    PERFORM bdc_field using 'BDC_OKCODE' '/00'.
  endif.

*  PERFORM bdc_field using 'BDC_OKCODE' '/00'.

  PERFORM bdc_field using 'P2002-BEGDA' v_date.

  PERFORM bdc_field using 'P2002-ENDDA' v_date.

  v_time_s = y_input-start_time.

  PERFORM bdc_field using 'P2002-BEGUZ' v_time_s.

  v_time_e = y_input-end_time.

  PERFORM bdc_field using 'P2002-ENDUZ' v_time_e.

  v_time = y_input-duration.

  REPLACE '.' with ',' into v_time.

  PERFORM bdc_field using 'P2002-STDAZ' v_time.

  if not y_input-aufnr is initial.

    PERFORM bdc_dynpro using 'SAPLHRTV' '0300'.

    PERFORM bdc_field using 'BDC_OKCODE' '=GOON'.

    PERFORM bdc_field using 'COBL-AUFNR' y_input-aufnr.

*      PERFORM bdc_field using 'COBL-BUKRS' v_burks.

  endif.

  PERFORM bdc_dynpro using 'MP200000' '2051'.

  PERFORM bdc_field using 'BDC_OKCODE' '=UPD'.

  PERFORM bdc_field using 'P2002-BEGDA' v_date.

  PERFORM bdc_field using 'P2002-ENDDA' v_date.

  PERFORM bdc_field using 'P2002-BEGUZ' v_time_s.

*  PERFORM bdc_field using 'P2002-ENDUZ' v_time_e.

  PERFORM bdc_field using 'P2002-STDAZ' v_time.

  CALL TRANSACTION 'PA30' USING y_batch
                   MODE mode
                   UPDATE 'S'
                   MESSAGES INTO y_errorbatch.

  clear y_batch.
  refresh y_batch.
  clear v_time.
  clear v_time_e.
  clear v_time_s.

*   Dealing with possible error
  loop at y_errorbatch.
    if y_errorbatch-msgtyp eq 'E' or
       y_errorbatch-msgtyp eq  'S'.
* To eliminate the messages when the process goes ok
      check not y_errorbatch-MSGNR  eq '200'.
      check not y_errorbatch-MSGNR eq '102'.

      CALL FUNCTION 'MESSAGE_TEXT_BUILD'
        EXPORTING
          MSGID               = y_errorbatch-msgid
          MSGNR               = y_errorbatch-msgnr
          MSGV1               = y_errorbatch-msgv1
          MSGV2               = y_errorbatch-msgv2
          MSGV3               = y_errorbatch-msgv3
          MSGV4               = y_errorbatch-msgv4
        IMPORTING
          MESSAGE_TEXT_OUTPUT = y_error-txt.

      y_error-pernr = y_input-pernr.
      y_error-SUBTY = y_input-SUBTY2.
      y_error-ENDDA = y_input-isdd.
      y_error-begda = y_input-isdd.
      append y_error.
      clear y_error.
    endif.

  ENDLOOP.

  clear y_errorbatch.
  refresh y_errorbatch.

ENDFORM.                    " UPDATE_INFO_TYPE_2002
*&---------------------------------------------------------------------*
*&      Form  UPDATE_INFO_TYPE_0003
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM UPDATE_INFO_TYPE_0003 .

  CALL FUNCTION 'BAPI_EMPLOYEE_ENQUEUE'
    EXPORTING
      number = y_p0003-pernr
    IMPORTING
      return = y_return.

* Dealing with possible error
  if y_return-type eq 'E'.
    move-corresponding y_p0003 to y_error.
    y_error-txt = y_return-message.
    APPEND y_error.
    clear y_error.
  endif.

  CLEAR y_return.
  refresh y_return.

  CALL FUNCTION 'HR_INFOTYPE_OPERATION'
              EXPORTING
                        INFTY                  = y_p0003-infty
                        NUMBER                 = y_p0003-pernr
                        SUBTYPE                = y_p0003-subty
                        OBJECTID               = y_p0003-objps
                        LOCKINDICATOR          = y_p0003-sprps
                        VALIDITYEND            = y_p0003-endda
                        VALIDITYBEGIN          = y_p0003-begda
                        RECORDNUMBER           = y_p0003-SEQNR
                        RECORD                 = y_p0003
                        OPERATION              = 'MOD'
*                   TCLAS                  = 'A'
                        DIALOG_MODE            = v_mode
*                   NOCOMMIT               =
*                   VIEW_IDENTIFIER        =
*                   SECONDARY_RECORD       =
                    IMPORTING
                      RETURN                 = y_bapireturn1.

* Dealing with possible error
  if y_bapireturn1-type eq 'E'.
    move-corresponding y_p0003 to y_error.
    y_error-txt = y_bapireturn1-message.
    APPEND y_error.
    clear y_error.
  endif.

  CLEAR y_bapireturn1.
  refresh y_bapireturn1.

  CALL FUNCTION 'BAPI_EMPLOYEE_DEQUEUE'
    EXPORTING
      number = y_p0003-pernr
    IMPORTING
      return = y_return.

* Dealing with possible error
  if y_return-type eq 'E'.
    move-corresponding y_p0003 to y_error.
    y_error-txt = y_return-message.
    APPEND y_error.
    clear y_error.
  endif.

  CLEAR y_return.
  refresh y_return.


ENDFORM.                    " UPDATE_INFO_TYPE_0003
*&---------------------------------------------------------------------*
*&      Form  GET_FILE_NAME
*&---------------------------------------------------------------------*
*       To get the file from the local PC
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM GET_FILE_NAME .

  DATA:
    t_tabfich TYPE filetable,
    l_rc TYPE i.

  DATA: l_extension TYPE string,
        l_nombre TYPE string,
        l_directorio TYPE string.

* Inicializar variables
* Reset the variables
  REFRESH t_tabfich.
  CLEAR t_tabfich.

* Open a dialog to choose a file
  CALL METHOD cl_gui_frontend_services=>file_open_dialog
    EXPORTING
*      window_title      =
      default_extension = l_extension
      default_filename  = l_nombre
      initial_directory = l_directorio
      multiselection    = space
    CHANGING
      file_table        = t_tabfich
      rc                = l_rc.
*      file_encoding     = l_encoding.

  IF sy-subrc <> 0.
    MESSAGE w208(00) WITH text-e01.
  ELSE.
* Multiple selection are not allowed so only one
* file could be selected
    READ TABLE t_tabfich INDEX l_rc INTO p_file.
    v_path = p_file.
  ENDIF.


ENDFORM.                    " GET_FILE_NAME
*&---------------------------------------------------------------------*
*&      Form  error_log
*&---------------------------------------------------------------------*
*       Show the log of error of this proccess
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM error_log .

  WRITE: / text-002,
        15 text-003,
        25 text-004,
        37 text-005,
        47 text-006,
        59 text-007,
        70 text-008,
        90 text-009.
  WRITE: / sy-uline(130).
  LOOP AT y_error.
    WRITE: / y_error-pernr under text-002,
             y_error-subty under text-003,
             y_error-OBJPS under text-004,
             y_error-SPRPS under text-005,
             y_error-ENDDA under text-006,
             y_error-begda under text-007,
             y_error-SEQNR under text-008 CENTERED,
             y_error-txt   under text-009.
  endloop.

ENDFORM.                    " error_log
*&---------------------------------------------------------------------*
*&      Form  bdc_dynpro
*&---------------------------------------------------------------------*
*       Start new screen
*----------------------------------------------------------------------*
*      -->P_1476   text
*      -->P_1477   text
*----------------------------------------------------------------------*
FORM bdc_dynpro  USING    program
                          dynpro.
  CLEAR y_batch.
  y_batch-program = program.
  y_batch-dynpro = dynpro.
  y_batch-dynbegin = 'X'.
  append y_batch.

ENDFORM.                    " bdc_dynpro
*&---------------------------------------------------------------------*
*&      Form  bdc_field
*&---------------------------------------------------------------------*
*       Insert field
*----------------------------------------------------------------------*
*      -->P_1481   text
*      -->P_1482   text
*----------------------------------------------------------------------*
FORM bdc_field  USING    fnam
                         fval.
  CLEAR y_batch.
  y_batch-fnam = fnam.
  y_batch-fval = fval.
  APPEND y_batch.

ENDFORM.                    " bdc_field
*&---------------------------------------------------------------------*
*&      Form  format_date
*&---------------------------------------------------------------------*
*       Format the date fields
*----------------------------------------------------------------------*
*      -->P_Y_INPUT_ISDD  text
*      <--P_DATE  text
*----------------------------------------------------------------------*
FORM format_date  USING    P_Y_INPUT_ISDD
                  CHANGING P_DATE.

  write P_Y_INPUT_ISDD to p_date DD/MM/YYYY.

*  CONCATENATE P_Y_INPUT_ISDD+6(2) '.' P_Y_INPUT_ISDD+4(2)'.' P_Y_INPUT_ISDD(4) into p_date.

ENDFORM.                    " format_date

*Text symbol text£º
*001:Info type PA0003 was not created yet
*002:Personal N#
*003:Subtype
*004:Object ID
*005:Lock ind.
*006:End Date
*007:Start Date
*008:Rec with same key
*009:Error message
*010:Insert absences, attendances and payroll status
*011:Error updating info type PA2002
*013:The order number does not exists on the system

*014:Attendace/Absence type not valid in selected period
*Selection text£º
*P_FILE:        File
*P_PERNR:        Personal N#
*P_R_DATE:        Refresh of date
