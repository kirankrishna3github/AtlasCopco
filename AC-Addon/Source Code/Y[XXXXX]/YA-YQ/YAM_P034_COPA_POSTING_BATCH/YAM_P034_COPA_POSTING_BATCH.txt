*----------------------------------------------------------------------*
* PROGRAM ID           : YAM_P034_COPA_POSTING_BATCH                   *
* PROGRAM TITLE        : COPA Posting Batch Program                    *
* AUTHOR               : Amit Mahajan                                  *
* DATE                 : 6/12/2004                                     *
* DEVELOPMENT ID       : XXXX                                          *
*
* CHANGE REQUEST NUMBER:                                               *
* PROGRAM DESCRIPTION  : THIS PROGRAM WILL READ ALL OPEN SERVICE ORDERS*
*                        AND CALL THE FUNCTION MODULE                  *
*                        YAM_YBUS2088_YPOSTCODOCUMENT,TO DO THE COPA   *
*                        POSTINGS.
* INPUT SCREEN: SERVICE ORDER RANGE AND TEST RUN(CHECKBOX) IS NEEDED ON*
* SELECTION SCREEN.                                                    *
* OUTPUT SCREEN : SERVICE ORDER,ORDER TYPE,ORDER CATEGORY,COMPANY CODE*
*SHORT TEXT,ACTIVE STATUS,SUCESS/FAILURE CODE AND MESSAGE             *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
* MOD-001 | 11.01.05 |Vikrant Sud|CD1K901314| Change the selection logic
*to select the Order List. According to the New Logic only those orders
*for which a confirmation has been created in the desried selection
*screens will be processed to update the COPA entries using FM -
*"YAM_YBUS2088_YPOSTCODOCUMENT".
* MOD-002 | 10.03.05 |Luc Mertens|CD1K901818| Change the outputlist;   *
*                                 ordernr. was not fully printed       *
*----------------------------------------------------------------------*

REPORT  yam_p034_copa_posting_batch NO STANDARD PAGE HEADING MESSAGE-ID
yam_copa.
************************************************************************
*                  D A T A B A S E   T A B L E S                       *
************************************************************************
TYPE-POOLS : slis.
TABLES:  aufk,             "Order master data
         yam_ord_conf_sta.
************************************************************************
*                  I N T E R N A L   T A B L E S                       *
************************************************************************
*     ORDER DATA TABLE                                                 *
************************************************************************
DATA : BEGIN OF i_aufk OCCURS 0,
        aufnr LIKE aufk-aufnr,
        auart LIKE aufk-auart,
        autyp LIKE aufk-autyp,
        objnr LIKE aufk-objnr,
        bukrs LIKE aufk-bukrs,
        ktext LIKE aufk-ktext,
        txt04 LIKE tj02t-txt04,
        flag,
        code(10),
        message TYPE string,
        selected,
        pm_selected TYPE pm_selected,
        color(4) TYPE c,
       END OF i_aufk.
* begin of MOD-002
DATA : BEGIN OF i_aufk2 OCCURS 0,
        aufnr(15),
        auart LIKE aufk-auart,
        autyp LIKE aufk-autyp,
        objnr LIKE aufk-objnr,
        bukrs LIKE aufk-bukrs,
        ktext LIKE aufk-ktext,
        txt04 LIKE tj02t-txt04,
        flag,
        code(10),
        message TYPE string,
        selected,
        pm_selected TYPE pm_selected,
        color(4) TYPE c,
       END OF i_aufk2.
* end of MOD-002
************************************************************************
* VARIABLE
************************************************************************
DATA : l_index TYPE sy-tabix,
       line LIKE  bsvx-sttxt,
       l_lines  TYPE i,
       gt_fieldcat TYPE slis_t_fieldcat_alv,". LVC_T_FCAT.
       g_layout           TYPE slis_layout_alv,
       g_listheader_tab   TYPE slis_t_listheader,
       g_events_tab       TYPE slis_t_event,
       g_print            TYPE slis_print_alv,
       g_repid            LIKE sy-repid,
       g_selmod,
       g_ic1 LIKE sy-ucomm VALUE '&IC1',
       g_event_exit_tab   TYPE slis_t_event_exit,
       selmod_0      VALUE '0',
      selmod_s      VALUE 'S',
      selmod_1      VALUE '1',
      selmod_x      VALUE 'X',
      selmod_d      VALUE 'D',
      g_form_user_command TYPE slis_formname VALUE 'USER_COMMAND_L',
      g_form_top_of_page  TYPE slis_formname VALUE 'TOP_OF_PAGE_F14',
 c_color_green(4)    TYPE c VALUE 'C500', "Green intense/inverse off
 c_color_red(4)  TYPE c VALUE 'C600', "Lt blue intens/inverse off
  c_color_gray(4)    TYPE c VALUE 'C100'. "Green intense/inverse off
************************************************************************
* CONSTANTS
************************************************************************
CONSTANTS : c_x VALUE 'X',
            c_s  VALUE 'S',
            c_fail(4) VALUE 'Fail',
            c_sucess(6) VALUE 'Sucess',
            c_status LIKE jstat-stat VALUE 'I0046',
            c_msgid TYPE sy-msgid VALUE 'YAM_COPA',
            c_ucomm TYPE sy-ucomm VALUE 'ONLI',
            c_msgty_e VALUE 'E',
            c_msg1(3) VALUE '040',    "CONSTANTS FOR MESSAGE NO'S
            c_msg2(3) VALUE '041',    "CONSTANTS FOR MESSAGE NO'S
            c_msg3(3) VALUE '042',    "CONSTANTS FOR MESSAGE NO'S
            c_msg4(3) VALUE '043',    "CONSTANTS FOR MESSAGE NO'S
            c_msg5(3) VALUE '044',    "CONSTANTS FOR MESSAGE NO'S
            c_msg6(3) VALUE '045'.    "CONSTANTS FOR MESSAGE NO'S
***********************************************************************
*       S E L E C T - O P T I O N S / P A R A M E T E R S              *
************************************************************************
SELECTION-SCREEN : BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.
SELECT-OPTIONS: s_aufnr FOR aufk-aufnr, " OBLIGATORY ,
                s_orstat FOR yam_ord_conf_sta-iphas OBLIGATORY,
                s_bukrs FOR yam_ord_conf_sta-bukrs.

SELECT-OPTIONS: s_ersda  FOR yam_ord_conf_sta-ersda.
PARAMETERS :    c_p  AS CHECKBOX.
SELECTION-SCREEN: END OF BLOCK b1.
*----------------------------------------------------------------------*
*   INITIALIZATION.
*----------------------------------------------------------------------*
INITIALIZATION.


  s_ersda-sign = 'I'.
  s_ersda-option = 'BT'.
  s_ersda-low = sy-datum - 1.
  s_ersda-high = sy-datum.
  APPEND s_ersda.

* Start of MOD-001
*AT SELECTION-SCREEN .
*  IF SY-UCOMM = C_UCOMM.
*  SELECT AUFNR AUART AUTYP OBJNR BUKRS KTEXT INTO  TABLE I_AUFK
*  FROM AUFK
*  WHERE AUFNR IN S_AUFNR.
*    IF SY-SUBRC NE 0.
*      MESSAGE E039.
*    ENDIF.
*  ENDIF.
* End of MOD-001

************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.
* Start of MOD-001
  PERFORM select_orders.
* End of MOD-001
  PERFORM copa_update.
  PERFORM build_field_catlog CHANGING gt_fieldcat.
  PERFORM fill_events_f14.
  PERFORM fill_event_exit_tab_f14.
  PERFORM build_layout.
  PERFORM comment_build USING g_listheader_tab.
*--------------------------------------------------------------------*
* CALL FUNCTION MODULE TO REUSE_ALV_GRID_DISPLAY TO DISPLAY THE GRID
*OUTPUT
*--------------------------------------------------------------------*
  PERFORM alv_display.
*&---------------------------------------------------------------------*
*&      Form  COPA_UPDATE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM copa_update .
************************************************************************
* CALL THE FUNCTION MODULE STATUS_CHECK TO SEE IF SERVICE ORDER IS     *
* CLOSED,IF SERVICE ORDER IS CLOSED THEN DELETE IT FROM INTERNAL TABLE
************************************************************************
  LOOP AT i_aufk.
*    l_index = sy-tabix.
    CLEAR i_aufk-message.
    PERFORM show_progress_indicator USING i_aufk-aufnr .
************************************************************************
*    CALL FUNCTION 'STATUS_CHECK'
*      EXPORTING
*        client            = sy-mandt
*        objnr             = i_aufk-objnr
*        status            = c_status
*      EXCEPTIONS
*        object_not_found  = 1
*        status_not_active = 2
*        OTHERS            = 3.
*    IF sy-subrc EQ  0.
*      PERFORM message_format USING i_aufk-aufnr sy-subrc  CHANGING
*      i_aufk-message.
*      i_aufk-flag = c_x.
*      i_aufk-code = c_fail.
*      i_aufk-color =  c_color_red.
*      PERFORM status_active.
*    ELSE.

************************************************************************
* CALL FUNCTION YAM_YBUS2088_YPOSTCODOCUMENT AND PASS THE ORDER NO AND
* TEST CHECKBOX AS EXPORTING PARAMETERS
************************************************************************
    PERFORM status_active.
    IF i_aufk-flag EQ space.
      CALL FUNCTION 'YAM_YBUS2088_YPOSTCODOCUMENT'
        EXPORTING
          i_order                  = i_aufk-aufnr
          testrun                  = c_p
        EXCEPTIONS
          service_order_lock_issue = 1
          application_log_error    = 2
          OTHERS                   = 3.
      IF sy-subrc NE 0.
        PERFORM message_format USING i_aufk-aufnr sy-subrc  CHANGING
        i_aufk-message.
        i_aufk-color =   c_color_red.
        i_aufk-code = c_fail.
      ELSE.
        i_aufk-code = c_sucess.
      ENDIF.
      MODIFY i_aufk.
    ENDIF.
*    ENDIF.
  ENDLOOP.
ENDFORM.                    " COPA_UPDATE
*&---------------------------------------------------------------------*
*&      Form  MESSAGE_FORMAT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_I_AUFK_AUFNR  text
*      -->P_SY_SUBRC  text
*      <--P_L_MESSAGE  text
*----------------------------------------------------------------------*
FORM message_format  USING    p_aufnr
                              p_subrc
                     CHANGING p_message.
  DATA : l_messageno TYPE sy-msgno.
  CASE p_subrc.
    WHEN '0'.
      l_messageno =  c_msg5.
    WHEN '1'.
      l_messageno = c_msg2.
    WHEN '2'.
      l_messageno = c_msg3.
    WHEN '3'.
      l_messageno = c_msg4.
  ENDCASE.
*-------------------------------------------------------------*
* USE FUNCTION FORMAT_MESSAGE TO FORMAT THE MESSAGES
*-------------------------------------------------------------*
  CALL FUNCTION 'FORMAT_MESSAGE'
    EXPORTING
      id   = c_msgid
      lang = sy-langu
      no   = l_messageno
      v1   = p_aufnr
    IMPORTING
      msg  = p_message.
ENDFORM.                    " MESSAGE_FORMAT
*&---------------------------------------------------------------------*
*&      Form  build_field_catlog
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      <--P_GT_FIELDCAT  text
*----------------------------------------------------------------------*
FORM build_field_catlog  CHANGING pt_fieldcat TYPE slis_t_fieldcat_alv.

  DATA : ls_fcat TYPE slis_fieldcat_alv.
*--------------------------ORDER----------------------------*
  ls_fcat-fieldname = 'AUFNR'.
  ls_fcat-rollname = 'AUFNR'.
  ls_fcat-outputlen = '12'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------ORDER TYPE----------------------------*
  ls_fcat-fieldname = 'AUART'.
  ls_fcat-rollname = 'AUFART'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------ORDER CATAGORY----------------------------*
  ls_fcat-fieldname = 'AUTYP'.
  ls_fcat-rollname = 'AUFTYP'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------COMPANY CODE----------------------------*
  ls_fcat-fieldname = 'BUKRS'.
  ls_fcat-rollname = 'BUKRS'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------SHORT TEXT----------------------------*
  ls_fcat-fieldname = 'KTEXT'.
  ls_fcat-rollname = 'AUFTEXT'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------STATUS----------------------------*
  ls_fcat-fieldname = 'TXT04'.
  ls_fcat-rollname = 'J_TXT04'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-------------------SUCESS/FAILURE CODE----------------------------*
  ls_fcat-fieldname = 'CODE'.
  ls_fcat-inttype = 'S'.
  ls_fcat-outputlen = '20'.
  ls_fcat-seltext_l = 'Sucess/Failure Code'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------MESSAGE----------------------------*
  ls_fcat-fieldname = 'MESSAGE'.
  ls_fcat-inttype = 'S'.
  ls_fcat-outputlen = '40'.
  ls_fcat-seltext_l = 'Message'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
ENDFORM.                    " build_field_catlog
*&---------------------------------------------------------------------*
*&      Form  build_layout
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM build_layout .
  g_layout-box_fieldname        = 'SELECTED'.  "field for checkbox
  g_layout-get_selinfos         = c_x. "show selection screen
  g_layout-detail_popup         = c_x. "show detail via popup
  g_layout-detail_initial_lines = c_x. "all fields in detail
  g_layout-zebra                = c_x. "striped pattern
  g_layout-info_fieldname       = 'COLOR'.

ENDFORM.                    " build_layout
*&---------------------------------------------------------------------*
*&      Form  comment_build
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_G_LISTHEADER_TAB  text
*----------------------------------------------------------------------*
FORM comment_build USING l_top_of_page TYPE slis_t_listheader.
  DATA: ls_line TYPE slis_listheader,
          ld_date(10) TYPE c.

***Header
  CLEAR ls_line.
  ls_line-typ  = 'H'.
  ls_line-info  = text-005.
  APPEND ls_line TO l_top_of_page.

***Selection
  IF c_p EQ  c_x.
    CLEAR ls_line.
    ls_line-typ  = 'S'.
    ls_line-key = text-013.
    APPEND ls_line TO l_top_of_page.
  ENDIF.
* ORDER
  CLEAR ls_line.
  ls_line-typ  = 'S'.
  ls_line-key  = text-006.
  ls_line-info  = s_aufnr+3(12).
  ls_line-info+15(27)  = s_aufnr+15(12).
  APPEND ls_line TO l_top_of_page.
  CLEAR ls_line.
ENDFORM.                    " comment_build
*&---------------------------------------------------------------------*
*&      Form  alv_display
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM alv_display .

* begin of MOD-002
  refresh i_aufk2.
  loop at i_aufk.
    move-corresponding i_aufk to i_aufk2.
    append i_aufk2.
    clear i_aufk2.
  endloop.
* end of MOD-002

  g_repid = sy-repid.
  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
     EXPORTING
       i_callback_program                 =  g_repid
       i_save                             = 'A'
     it_events                         =  g_events_tab[]
*   I_GRID_TITLE                      =
*   I_GRID_SETTINGS                   =
       is_layout                         =   g_layout
       it_fieldcat                        =  gt_fieldcat[]

      TABLES
* begin of MOD-002
*       t_outtab                          = i_aufk.
        t_outtab                          = i_aufk2.
* end of MOD-002

ENDFORM.                    " alv_display
*&---------------------------------------------------------------------*
*&      Form  fill_events_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM fill_events_f14 .
  DATA h_event       TYPE slis_alv_event.

  CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
    EXPORTING
      i_list_type = 0
    IMPORTING
      et_events   = g_events_tab.
*--- allocate form for top-of-page ----------------------------------*
  READ TABLE g_events_tab WITH KEY name = slis_ev_top_of_page
                        INTO h_event.
  IF sy-subrc = 0.
    MOVE g_form_top_of_page TO h_event-form.
    MODIFY g_events_tab FROM h_event INDEX sy-tabix.
  ENDIF.
*--- allocate form for user-command ---------------------------------*
  READ TABLE g_events_tab WITH KEY name = slis_ev_user_command
                        INTO h_event.
  IF sy-subrc = 0.
    MOVE g_form_user_command TO h_event-form.
    MODIFY g_events_tab FROM h_event INDEX sy-tabix.
  ENDIF.
ENDFORM.                    " fill_events_f14
*&---------------------------------------------------------------------*
*&      Form  STATUS_ACTIVE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM status_active .
************************************************************************
* CALL FUNCTION MODULE STATUS_TEXT_EDIT TO GET THE  CURRENT STATUS OF
* THE SERVICE ORDER
************************************************************************
  CALL FUNCTION 'STATUS_TEXT_EDIT'
    EXPORTING
      client           = sy-mandt
      objnr            = i_aufk-objnr
      only_active      = c_x
      spras            = sy-langu
    IMPORTING
      line             = line
    EXCEPTIONS
      object_not_found = 1
      OTHERS           = 2.

************************************************************************
* MOVE FIRST FOUR CHARACTERS OF THE LINE TO I_AUFK-STAT
* BECUASE SYSTEM STATUS TEXT IS OF 4 CHARACTERS ONLY
************************************************************************
  IF sy-subrc EQ 0.
    MOVE line+0(4) TO i_aufk-txt04.
  ELSE.
    PERFORM message_format_stat USING i_aufk-aufnr sy-subrc  CHANGING
    i_aufk-message.
    i_aufk-flag = c_x.
    i_aufk-code = c_fail.
    i_aufk-color =  c_color_red.
  ENDIF.
*  MODIFY i_aufk INDEX l_index.
  MODIFY i_aufk.

ENDFORM.                    " STATUS_ACTIVE
*&---------------------------------------------------------------------*
*&      Form  MESSAGE_FORMAT_STAT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_I_AUFK_AUFNR  text
*      -->P_SY_SUBRC  text
*      <--P_I_AUFK_MESSAGE  text
*----------------------------------------------------------------------*
FORM message_format_stat USING p_aufnr
                               p_subrc
                      CHANGING p_message.
  DATA : l_messageno1 TYPE sy-msgno.
  CASE p_subrc.
    WHEN '1'.
      l_messageno1 = c_msg6.
    WHEN '2'.
      l_messageno1 = c_msg4.
  ENDCASE.
*-------------------------------------------------------------*
* USE FUNCTION FORMAT_MESSAGE TO FORMAT THE MESSAGES
*-------------------------------------------------------------*
  CALL FUNCTION 'FORMAT_MESSAGE'
    EXPORTING
      id   = c_msgid
      lang = sy-langu
      no   = l_messageno1
      v1   = p_aufnr
    IMPORTING
      msg  = p_message.

ENDFORM.                    " MESSAGE_FORMAT_STAT
*&---------------------------------------------------------------------*
*&      Form  fill_event_exit_tab_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM fill_event_exit_tab_f14 .
  DATA h_event_exit_wa TYPE slis_event_exit.
*--- pf2 used by salv and also by application (dobble-click) --------*
  h_event_exit_wa-ucomm = g_ic1.
  h_event_exit_wa-after = c_x.
  APPEND h_event_exit_wa TO g_event_exit_tab.
ENDFORM.                    " fill_event_exit_tab_f14
*&--------------------------------------------------------------------*
*&      Form  user_command_l
*&--------------------------------------------------------------------*
*       text
*---------------------------------------------------------------------*
*      -->P_UCOMM    text
*      -->P_SELFIELD text
*---------------------------------------------------------------------*
FORM user_command_l USING p_ucomm LIKE sy-ucomm
                          p_selfield TYPE slis_selfield.
  p_selfield-refresh = c_s.
  PERFORM check_pf2_with_object_f16 USING p_ucomm.
  PERFORM set_p_selfield_general_f16 USING p_selfield.

  CASE p_ucomm.
    WHEN 'ISEL'.
      p_ucomm = 'DISP'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
  ENDCASE.
ENDFORM.                    "user_command_l
*&---------------------------------------------------------------------*
*&      Form  check_pf2_with_object_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*----------------------------------------------------------------------*
FORM check_pf2_with_object_f16  USING    p_ucomm.
  CHECK p_ucomm = g_ic1.
  p_ucomm = 'ISEL'.
ENDFORM.                    " check_pf2_with_object_f16
*&---------------------------------------------------------------------*
*&      Form  set_p_selfield_general_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM set_p_selfield_general_f16  USING f_selfield TYPE slis_selfield.

  f_selfield-col_stable = c_x.
  f_selfield-row_stable = c_x.
ENDFORM.                    " set_p_selfield_general_f16
*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*      -->P_ENDCASE  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_f16  USING p_ucomm LIKE sy-ucomm
                                p_selfield TYPE slis_selfield.

  PERFORM check_object_tab_marked_f14 USING p_ucomm
                                                 p_selfield.
  LOOP AT i_aufk WHERE selected = c_x .
    l_index = sy-tabix.
    PERFORM fcodes_with_mark_l USING p_ucomm
                                           p_selfield.
    i_aufk-selected = ' '.
    MODIFY i_aufk INDEX l_index.
  ENDLOOP.

  CLEAR p_ucomm.
ENDFORM.                    " fcodes_with_mark_f16
*&---------------------------------------------------------------------*
*&      Form  check_object_tab_marked_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM check_object_tab_marked_f14  USING    p_ucomm LIKE sy-ucomm
                                        p_selfield TYPE slis_selfield.



  READ TABLE i_aufk WITH KEY selected = c_x.
  IF NOT sy-subrc IS INITIAL.
    IF NOT p_selfield-tabindex IS INITIAL.
      READ TABLE i_aufk  INDEX p_selfield-tabindex.
      i_aufk-selected = c_x.
      MODIFY i_aufk  INDEX p_selfield-tabindex.
    ENDIF.
  ELSE.
*--- Checkbox markiert -----------------------------------------------*
    p_selfield-sel_tab_field = 'G_MARK'.
  ENDIF.

ENDFORM.                    " check_object_tab_marked_f14
*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_l
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_F_UCOMM  text
*      -->P_F_SELFIELD  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_l  USING   p_ucomm LIKE sy-ucomm
                              p_selfield TYPE slis_selfield.
  DATA: h_ucomm LIKE sy-ucomm.
  CASE p_ucomm.
*   Display service order
    WHEN 'DISP'.
      SET PARAMETER ID 'ANR' FIELD  i_aufk-aufnr.
      CALL TRANSACTION 'IW33' AND SKIP FIRST SCREEN.
  ENDCASE.


ENDFORM.                    " fcodes_with_mark_l
*&--------------------------------------------------------------------*
*&      Form  top_of_page_f14
*&--------------------------------------------------------------------*
*       text
*---------------------------------------------------------------------*
FORM top_of_page_f14.

*--- Display header lines above list display -------------------------
  IF NOT g_listheader_tab[] IS INITIAL.
    CALL FUNCTION 'REUSE_ALV_COMMENTARY_WRITE'
      EXPORTING
        it_list_commentary = g_listheader_tab.
  ENDIF.

ENDFORM.                    "top_of_page_f14

* Start of MOD-001
*&---------------------------------------------------------------------*
*&      Form  SELECT_ORDERS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM select_orders .
 SELECT DISTINCT aufnr auart autyp objnr bukrs ktext INTO  TABLE i_aufk
 FROM yam_ord_conf_sta
 WHERE aufnr IN s_aufnr AND
       iphas IN s_orstat AND
       bukrs IN s_bukrs AND
       ersda IN s_ersda.

  IF sy-subrc NE 0.
    MESSAGE e039.
  ELSE.
* Get Total Number of Lines for I_AUFK
    DESCRIBE TABLE i_aufk LINES l_lines.
  ENDIF.

ENDFORM.                    " SELECT_ORDERS
* End of MOD-001
*&---------------------------------------------------------------------*
*&      Form  show_progress_indicator
*&---------------------------------------------------------------------*
*      text: Show an indexation progress indicator
*----------------------------------------------------------------------*
FORM show_progress_indicator
      USING aufnr.
*.. Local variables
  DATA: ld_perc TYPE p,
        l_text(80) TYPE c.

  ld_perc = sy-tabix / l_lines * 100.

  l_text = text-t03.

  REPLACE '&1' WITH aufnr INTO l_text.

  CALL FUNCTION 'SAPGUI_PROGRESS_INDICATOR'
    EXPORTING
      percentage = ld_perc
      text       = l_text.

ENDFORM.                    " show_progress_indicator

*Text symbol text£º
*001:Selection Screen
*002:Report for the COPA Posting
*003:Service Order No
*004:Status
*005:COPA UPDATE
*006:Order No:
*007:No status available for Service order
*009:Order No
*010:Message Text
*013:Report  in Test Mode

*T03:Processing Service Order &1
*Selection text£º
*C_P:        Test run
*S_AUFNR:D       Order
*S_BUKRS:D       Company Code
*S_ERSDA:D       Entered on
*S_ORSTAT:D       Phase
