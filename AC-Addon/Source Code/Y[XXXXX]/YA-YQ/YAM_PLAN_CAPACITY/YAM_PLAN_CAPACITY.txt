*----------------------------------------------------------------------
* PROGRAM ID           : YAM_PLAN_CAPACITY                             *
* PROGRAM TITLE        : Plan capacity requirements                    *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 19/01/2007                                    *
* DEVELOPMENT ID       : All-CR313                                     *
* CHANGE REQUEST NUMBER: CD1K909541                                    *
* PROGRAM DESCRIPTION  : Create report to see plan capacity for the    *
*                        future                                        *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |2007.05.14|L. Mertens       |CD1K914988| add division       *
* MOD-002 |2008.02.27|L. Mertens       |CD1K926660|                    *
*        - calculate all future costs with cost price from curr.year   *
* MOD-003 |2008.03.17|L. Mertens       |CD1K927261|
**      - select only active maintenance plans                         *
*----------------------------------------------------------------------*

REPORT YAM_PLAN_CAPACITY no standard page heading
                                  line-size 200.

TYPE-POOLS: SLIS.

TABLES: vbak,                          "Sales Document: Header Data
        crhd,
        plpo,
        plas,
        mpla,
        mpos,
        mhis,
        rmipm,
        T024I.


*- VARIABLES----------------------------------------------------------
DATA: begin of gt_mplan occurs 0,
        warpl     like mpla-warpl,
        abnum     like mhis-abnum,
        zaehl     like mhis-zaehl,
        wapos     like mpos-wapos,
        equnr     like mpos-equnr,
        serialnr  like mpos-serialnr,
        plan_sort like mpla-plan_sort,
        abrho     like mpla-abrho,
        pstxt     like mpos-pstxt,
        plnty     like mpos-plnty,
        plnnr     like mpos-plnnr,
        plnal     like mpos-plnal,
        wpgrp     like mpos-wpgrp,
        gewrk     like mpos-gewrk,
        iwerk     like mpos-iwerk,
        kdauf     like mpos-kdauf,
        kdpos     like mpos-kdpos,
        nplda     like mhis-nplda,
        wppos     like mhio-wppos,
      end of gt_mplan.

DATA: begin of gt_final occurs 0,
        warpl        like mpla-warpl,
        abnum        like mhis-abnum,
        wapos        like mpos-wapos,
        plan_sort    like mpla-plan_sort,
        abrho        like mpla-abrho,
        pstxt        like mpos-pstxt,
        plnnr        like mpos-plnnr,
        plnal        like mpos-plnal,
        equnr        like mpos-equnr,
        serialnr     like mpos-serialnr,
        eqktx        like eqkt-eqktx,
        arbpl        like crhd-arbpl,
        parnr        like ihpa-parnr,
        parnr_name   like kna1-name1,
        billto       like ihpa-parnr,
        billto_name  like kna1-name1,
        kdauf        like mpos-kdauf,
        kdpos        like mpos-kdpos,
        nplda        like mhis-nplda,
        hrs          type arbeit,
*       hrs_unit     type arbeite,
        hrs_cost(7)  type p decimals 2,
        spp_cost(7)  type p decimals 2,
        dist         type arbeit,
*       dist_unit    type arbeite,
        dist_cost(7) type p decimals 2,
* begin of MOD-001
        plcid(4)     type c,
* end of MOD-001
        selected,
      end of gt_final.

DATA: begin of gt_crhd occurs 0.
        include structure crhd.
DATA: end of gt_crhd.

DATA: begin of gt_plas occurs 0,
        plnty     like plas-plnty,
        plnnr     like plas-plnnr,
        plnal     like plas-plnal,
        plnfl     like plas-plnfl,
        plnkn     like plas-plnkn,
        zaehl     like plas-zaehl,
      end of gt_plas.

DATA: begin of gt_plpo occurs 0,
        plnty     like plas-plnty,
        plnnr     like plas-plnnr,
        plnkn     like plas-plnkn,
        zaehl     like plas-zaehl,
        steus     like plpo-steus,
        larnt     like plpo-larnt,
        arbei     like plpo-arbei,
        arbeh     like plpo-arbeh,
        werks     like plpo-werks,
      end of gt_plpo.

DATA: begin of gt_plmz occurs 0,
        plnty     like plas-plnty,
        plnnr     like plas-plnnr,
        plnkn     like plas-plnkn,
        stlty     like plmz-stlty,
        stlnr     like plmz-stlnr,
        stlkn     like plmz-stlkn,
        imeng     like plmz-imeng,
        imein     like plmz-imein,
      end of gt_plmz.

DATA: begin of gt_stpo occurs 0,
        stlty     like stpo-stlty,
        stlnr     like stpo-stlnr,
        stlkn     like stpo-stlkn,
        idnrk     like stpo-idnrk,
      end of gt_stpo.

* work center
DATA: BEGIN OF arbid_tab_c OCCURS 0,
        arbid LIKE crhd-objid,
        arbpl LIKE crhd-arbpl.
DATA: END OF arbid_tab_c.

data: i_cosel    TYPE STANDARD TABLE OF
                 cosel   INITIAL SIZE 0,

      i_costa    TYPE STANDARD TABLE OF
                 costa   INITIAL SIZE 0.

DATA: gt_basic_data type cuvtab OCCURS 0  WITH HEADER LINE,
      gt_value_n    type CUVTAB_VALN OCCURS 0  WITH HEADER LINE,
      gt_value_c    type CUVTAB_VALC OCCURS 0  WITH HEADER LINE.

DATA: g_REPID             LIKE CEDDB-PROGRAM,
      gv_objnr_equi       like ihpa-objnr,
      gv_atinn_country    type atinn,
      gv_atinn_km         type atinn,
      gv_atinn_exlab      type atinn,
      gv_comp             type bukrs,
      gv_spp_cost         type stprs,
      gv_spp_unit         type peinh,
      gv_kostl            type kostl,
      gv_werks            type iwerk,
      gv_save_werks       type iwerk,
* begin of insert MOD-001
      gv_prctr            type prctr,
* end of insert MOD-001
      gv_parnr            like ihpa-parnr,
      gv_billto           like ihpa-parnr,
      gv_hrs_unit         type arbeh,
      gv_dist_unit        type arbeh,
      gv_kokrs            type kokrs,
      gv_jahr             LIKE bkpf-gjahr,
      gv_period           TYPE cest1-perio,
      gv_monat            TYPE bkpf-monat,
      gv_monat1           TYPE cest1-perde,
      gv_objnr            type cssl-objnr,
* begin of insert MOD-003
      gv_objnr_warpl      like jest-objnr,
* end of insert MOD-003
      gv_fldperiod        TYPE dd03l-fieldname,
      gv_leinh            LIKE cssl-leinh,
      wa_cosel            TYPE cosel,
      wa_costa            TYPE costa,
      l_index             TYPE sy-tabix,
      gv_ic1               LIKE sy-ucomm VALUE '&IC1',
      gt_fieldcat         TYPE slis_t_fieldcat_alv,
      g_events_tab        TYPE slis_t_event,
      g_form_user_command TYPE slis_formname VALUE 'USER_COMMAND_L'.

*- Constants------------------------------------------------------------
CONSTANTS: gc_x(1)        type c       value 'X',
           gc_ag          type parvw   value 'AG',
           gc_re          type parvw   value 'RE',
           gc_ie(2)       type c       value 'IE',
           gc_zero(8)     type c       value '00000000',
           gc_zam001      type lstar   value 'ZAM001',
           gc_zam010      type lstar   value 'ZAM010',
           gc_zco3        type steus   value 'ZCO3',
           gc_z_vc_table  type vtnam   value 'Z_VC_TABLE',
           gc_country(20) type c       value 'CH_EQUIPMENT_COUNTRY',
           gc_km(08)      type c       value 'CH_TL_KM',
           gc_exlab(17)   type c       value 'CH_TL_EXTRALABOUR',
           gc_gmix        type werks_d value 'GMIX',
           gc_kl(2)       type c       value 'KL',
           gc_tof0(4)     type c       value 'TOF0',
           gc_toe0(4)     type c       value 'TOE0'.

*----------------------------------------------------------------------*
* ranges                                                               *
*----------------------------------------------------------------------*
RANGES: arbid_c FOR afru-arbid.        " id of work center

*- SELECTION SCREEN---------------------------------------------------
SELECTION-SCREEN : BEGIN OF BLOCK B1 WITH FRAME TITLE TEXT-001.
PARAMETERS:     p_iwerk  like mpos-iwerk obligatory memory id WRK.
SELECT-OPTIONS: s_equnr  for  mpos-equnr,
                s_kdauf  for  vbak-vbeln matchcode object vmva,
                s_warpl  for  mpla-warpl.
PARAMETERS:     p_wpgrp  like mpos-wpgrp.
SELECT-OPTIONS: s_arbpl  for  RMIPM-GEWERK obligatory,
                s_nplda  for  mhis-nplda obligatory.

CALL METHOD YCL_STATISTICS=>RECORD_TRANSACTION
  .
SELECTION-SCREEN: END OF BLOCK B1.

*.................. Selection screen validations...................... *
AT SELECTION-SCREEN on p_iwerk.

* Check planning plant
  select single werks
       into gv_werks
       from T001W
       where werks = p_iwerk.

  if sy-subrc <> 0.
*.. Planning plant does not exist
    MESSAGE E001(00) WITH text-e01.
  endif.

  AUTHORITY-CHECK OBJECT 'I_IWERK'
           ID 'TCD'   FIELD sy-tcode
           ID 'IWERK' FIELD p_iwerk.

  IF sy-subrc NE 0.
*.. No authorization for plant: &1
    MESSAGE E001(00) WITH text-e02 p_iwerk.
  ENDIF.

*
AT SELECTION-SCREEN on p_wpgrp.

* Check planner group
  if not p_wpgrp is initial.
    SELECT SINGLE * FROM T024i
       WHERE iwerk = p_iwerk
         AND ingrp = p_wpgrp.

    IF sy-subrc <> 0.
*.... Planner group does not exist for planning plant &1
      MESSAGE e001(00) WITH text-e03 p_iwerk.
    ENDIF.
  endif.

*----------------------------------------------------------------------*
* at selection-screen                                                  *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

  REFRESH arbid_tab_c.
  REFRESH arbid_c.

*-START OF SELECTION----------------------------------------------------
START-OF-SELECTION.

  PERFORM get_arbid TABLES arbid_tab_c s_arbpl arbid_c
                    USING p_iwerk.

* Select maintenance plans
  if p_wpgrp is initial.
    SELECT
       a~warpl a~abrho a~plan_sort
       b~wapos b~pstxt b~plnty b~plnnr b~plnal b~wpgrp b~gewrk b~equnr
       b~kdauf b~kdpos b~iwerk b~serialnr
       c~abnum c~zaehl c~nplda
       d~wppos
          INTO CORRESPONDING FIELDS OF TABLE gt_mplan
          FROM mpla AS a INNER JOIN mpos AS b ON
                a~warpl = b~warpl
          INNER JOIN mhis AS c ON
                c~warpl = a~warpl
          INNER JOIN mhio AS d ON
                d~warpl = c~warpl and
                d~abnum = c~abnum and
                d~wppos = b~wapos
          WHERE  a~warpl IN s_warpl
            AND  b~equnr IN s_equnr
            AND  b~kdauf IN s_kdauf
            AND  b~iwerk EQ p_iwerk
            AND  b~gewrk IN arbid_c
            AND  c~nplda IN s_nplda
            AND  c~tstat NE gc_x
            AND  ( c~lrmdt EQ gc_zero
                   or c~lrmdt EQ space ).    "EXTUVE 6978 ( 20091231 )

  else.
    SELECT
       a~warpl a~abrho a~plan_sort
       b~wapos b~pstxt b~plnty b~plnnr b~plnal b~wpgrp b~gewrk b~equnr
       b~kdauf b~kdpos b~iwerk b~serialnr
       c~abnum c~zaehl c~nplda
       d~wppos
          INTO CORRESPONDING FIELDS OF TABLE gt_mplan
          FROM mpla AS a INNER JOIN mpos AS b ON
                a~warpl = b~warpl
          INNER JOIN mhis AS c ON
                c~warpl = a~warpl
          INNER JOIN mhio AS d ON
                d~warpl = c~warpl and
                d~abnum = c~abnum and
                d~wppos = b~wapos
          WHERE  a~warpl IN s_warpl
            AND  b~equnr IN s_equnr
            AND  b~kdauf IN s_kdauf
            AND  b~iwerk EQ p_iwerk
            AND  b~gewrk IN arbid_c
            AND  b~wpgrp EQ p_wpgrp
            AND  c~nplda IN s_nplda
            AND  c~tstat NE gc_x
            AND  ( c~lrmdt EQ gc_zero
                   or c~lrmdt EQ space ).    "EXTUVE 6978 ( 20091231 )
  endif.

* begin of insert MOD-003
*.. select only the active maintenance plans
*.. not deleted / not inactive
  loop at gt_mplan.
    CONCATENATE 'WO' gt_mplan-warpl INTO gv_objnr_warpl.

    CALL FUNCTION 'STATUS_CHECK'
      EXPORTING
        objnr             = gv_objnr_warpl
        status            = 'I0320'
      EXCEPTIONS
        object_not_found  = 1
        status_not_active = 2
        OTHERS            = 3.
    IF sy-subrc EQ 0.
      delete gt_mplan.
      continue.
    ELSE.
      CALL FUNCTION 'STATUS_CHECK'
        EXPORTING
          objnr             = gv_objnr_warpl
          status            = 'I0076'
        EXCEPTIONS
          object_not_found  = 1
          status_not_active = 2
          OTHERS            = 3.
      IF sy-subrc EQ 0.
        delete gt_mplan.
        continue.
      ENDIF.
    ENDIF.
  endloop.
* end of insert MOD-003

  if gt_mplan[] is initial.
    write: / text-i01.              "No MP's selected
    exit.
  endif.

* preselect variant table for central task lists
  CALL FUNCTION 'CUTX_INIT_STRUC_ENTRY_PLANNING'
    EXPORTING
      VAR_TAB                = gc_z_vc_table
    TABLES
      ET_BASIC_DATA          = gt_basic_data
*     ET_TABLE_TEXTS         =
*     ET_TABLE_FIELDS        =
*     ET_TABLE_INDICES       =
*     ET_TABLE_LINE          =
      ET_TABLE_VALUE_N       = gt_value_n
      ET_TABLE_VALUE_C       = gt_value_c
    EXCEPTIONS
      TABLE_NOT_FOUND        = 1
      OTHERS                 = 2.

  if sy-subrc <> 0.
    write: / text-i02.              "No variant table available
    exit.
  endif.

* get company code for the selected planning plant
  select single bukrs into gv_comp
     from T001k
     where bwkey = p_iwerk.

* preselect workcenters
  select * from crhd into table gt_crhd
    where werks = p_iwerk
      and objty = 'A'.

  sort gt_crhd by objid.

* get internal numbers for the caracteristics
  select single atinn into gv_atinn_country
      from CABN
      where atnam = gc_country.

  if sy-subrc <> 0.
    write: / text-i03, 'CH_EQUIPMENT_COUNTRY'.
    exit.
  endif.

  select single atinn into gv_atinn_km
      from CABN
      where atnam = gc_km.

  if sy-subrc <> 0.
    write: / text-i03, 'CH_TL_KM'.
    exit.
  endif.

  select single atinn into gv_atinn_exlab
      from CABN
      where atnam = gc_exlab.

  if sy-subrc <> 0.
    write: / text-i03, 'CH_TL_EXTRALABOUR'.
    exit.
  endif.


*-----------------------------------------------------------------------
END-OF-SELECTION.

  sort gt_mplan by warpl abnum wapos.
  refresh: gt_plas,
           gt_plpo.

  loop at gt_mplan.

    move-corresponding gt_mplan to gt_final.
    write gt_mplan-warpl to gt_final-warpl no-zero.
    write gt_mplan-wapos to gt_final-wapos no-zero.
    write gt_mplan-equnr to gt_final-equnr no-zero.
    write gt_mplan-serialnr to gt_final-serialnr no-zero.
    write gt_mplan-kdauf to gt_final-kdauf no-zero.

*.. Select tasklist info
    select plnkn plnty plnnr plnal plnfl zaehl
       into corresponding fields of table gt_plas
       from PLAS
       where plnty = gt_mplan-plnty
         and plnnr = gt_mplan-plnnr
         and plnal = gt_mplan-plnal
         and loekz = space.

    if not gt_plas[] is initial.
      select arbei arbeh larnt steus werks
             plnty plnnr plnkn zaehl
         into corresponding fields of table gt_plpo
         from PLPO
         for all entries in gt_plas
         where plnty = gt_plas-plnty
           and plnnr = gt_plas-plnnr
           and plnkn = gt_plas-plnkn
           and loekz = space
           and ( steus = gc_zco3
            or larnt = gc_zam010 ).
    endif.

    if not gt_plpo[] is initial.
      clear: gv_save_werks,
             gv_hrs_unit,
             gv_dist_unit.

      loop at gt_plpo.
        if gt_plpo-steus = gc_zco3.
          add gt_plpo-arbei to gt_final-hrs.
          move gt_plpo-arbeh to gv_hrs_unit.
        else.
          add gt_plpo-arbei to gt_final-dist.
          move gt_plpo-arbeh to gv_dist_unit.
        endif.
        move gt_plpo-werks to gv_save_werks.
      endloop.

*.... Select spare parts
      refresh: gt_plmz,
               gt_stpo.

      Select stlty stlnr stlkn imeng imein
             plnnr plnty plnkn
         into corresponding fields of table gt_plmz
         from PLMZ
         for all entries in gt_plpo
         where plnnr = gt_plpo-plnnr
           and plnty = gt_plpo-plnty
           and plnkn = gt_plpo-plnkn
           and loekz = space.

      if not gt_plmz[] is initial.
        select idnrk
               stlty stlnr stlkn
           into corresponding fields of table gt_stpo
           from STPO
           for all entries in gt_plmz
           where stlty = gt_plmz-stlty
             and stlnr = gt_plmz-stlnr
             and stlkn = gt_plmz-stlkn
             and lkenz = space.
      endif.

      if not gt_stpo[] is initial.
        sort gt_stpo by stlty stlnr stlkn.

*...... Get cost of spare parts; always read with planning plant
        loop at gt_plmz.

          read table gt_stpo with key stlty = gt_plmz-stlty
                                      stlnr = gt_plmz-stlnr
                                      stlkn = gt_plmz-stlkn
                             binary search.

          if sy-subrc = 0.
            select single stprs peinh
               into (gv_spp_cost, gv_spp_unit)
               from MBEW
               where matnr = gt_stpo-idnrk
                 and bwkey = gt_mplan-iwerk.

            if sy-subrc = 0.
              gv_spp_cost =
                    gv_spp_cost * gt_plmz-imeng / gv_spp_unit.
              add gv_spp_cost to gt_final-spp_cost.
            endif.
          endif.

        endloop.
      endif.

*.... Only for GMIX: read Variant table for Central task lists
      if gv_save_werks = gc_gmix.

        read table gt_basic_data with key mandt = sy-mandt
                                          vtnam = gc_z_vc_table.

        IF SY-SUBRC = 0.
          read table gt_value_c with key mandt = sy-mandt
                                         vtint = gt_basic_data-vtint
                                         atinn = gv_atinn_country
                                         valc  = gv_comp.
          if sy-subrc = 0.
*.......... get km
            read table gt_value_n with key mandt = sy-mandt
                                          vtint = gt_basic_data-vtint
                                           slnid = gt_value_c-slnid
                                           atinn = gv_atinn_km.
            if sy-subrc = 0.
              add gt_value_n-val_from to gt_final-dist.
            endif.
*.......... get extra labour
            read table gt_value_n with key mandt = sy-mandt
                                          vtint = gt_basic_data-vtint
                                           slnid = gt_value_c-slnid
                                           atinn = gv_atinn_exlab.
            if sy-subrc = 0.
              add gt_value_n-val_from to gt_final-hrs.
            endif.
          endif.
        ENDIF.

      endif.

*.... look for the cost price per hour/distance
      perform get_kostl using gt_mplan-gewrk gv_kostl gv_kokrs.

*.... Get financial period
      PERFORM get_fi_period USING gt_mplan-nplda
                                  gt_mplan-iwerk
                         CHANGING gv_monat
                                  gv_jahr.

*.... get cost price hour
      perform calc_cost using gv_kokrs
                              gv_kostl
                              gc_zam001
                              gv_jahr
                              gv_monat
                              gt_final-hrs
                              gv_hrs_unit
                     changing gt_final-hrs_cost.

*.... get cost price distance
      perform calc_cost using gv_kokrs
                              gv_kostl
                              gc_zam010
                              gv_jahr
                              gv_monat
                              gt_final-dist
                              gv_dist_unit
                     changing gt_final-dist_cost.

* begin of insert mod-002
*     calculate future cost with cost price of current period
      if gt_final-hrs_cost is initial.
*...... Get financial period
        PERFORM get_fi_period USING sy-datum
                                    gt_mplan-iwerk
                           CHANGING gv_monat
                                    gv_jahr.

*...... get cost price hour
        perform calc_cost using gv_kokrs
                                gv_kostl
                                gc_zam001
                                gv_jahr
                                gv_monat
                                gt_final-hrs
                                gv_hrs_unit
                       changing gt_final-hrs_cost.

*...... get cost price distance
        perform calc_cost using gv_kokrs
                                gv_kostl
                                gc_zam010
                                gv_jahr
                                gv_monat
                                gt_final-dist
                                gv_dist_unit
                       changing gt_final-dist_cost.
      endif.
* end of insert MOD-002
    endif.

*.. Select workcenters
    read table gt_crhd with key objid = gt_mplan-gewrk
             binary search.

    if sy-subrc = 0.
      move gt_crhd-arbpl to gt_final-arbpl.
    endif.

*.. Select customer (sold-to)
    concatenate gc_ie gt_mplan-equnr into gv_objnr_equi.
    select single parnr into gv_parnr
        from IHPA
        where objnr    = gv_objnr_equi
          and parvw    = gc_ag
          and kzloesch = space.

*.. Select customer name (sold-to)
    if sy-subrc = 0.
      write gv_parnr to gt_final-parnr no-zero.
      select single name1 into gt_final-parnr_name
          from KNA1
          where kunnr    = gv_parnr.
    endif.

*.. Select bill-to
    select single parnr into gv_billto
        from IHPA
        where objnr    = gv_objnr_equi
          and parvw    = gc_re
          and kzloesch = space.

*.. Select customer name (bill-to)
    if sy-subrc = 0.
      write gv_billto to gt_final-billto no-zero.
      select single name1 into gt_final-billto_name
          from KNA1
          where kunnr    = gv_billto.
    endif.

*.. Select the equipment description
    select single eqktx into gt_final-eqktx
        from EQKT
        where equnr    = gt_mplan-equnr
          and spras    = sy-langu.

* begin of insert MOD-001
*.. Select the profit center from the sales document
*.. in order to derive the division (PLCID)
    select single prctr into gv_prctr
      from vbap
      where vbeln eq gt_mplan-kdauf
        and posnr eq gt_mplan-kdpos.

    if sy-subrc = 0.
      case gv_prctr+1(1).
        when '1'.
          gt_final-plcid = 'AIF'.
        when '2'.
          gt_final-plcid = 'AII'.
        when '3'.
          gt_final-plcid = 'AIP'.
        when '4'.
          gt_final-plcid = 'GAP'.
      endcase.
    endif.
* end of insert MOD-001

    append gt_final.
    clear gt_final.

  endloop.

* create ALV-GRID
  PERFORM build_field_catlog CHANGING gt_fieldcat.
  PERFORM fill_events_f14.
  PERFORM alv_display.


*- SUBROUTINES---------------------------------------------------------
*&---------------------------------------------------------------------*
*&      Form  build_field_catlog
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      <--P_GT_FIELDCAT  text
*----------------------------------------------------------------------*
FORM build_field_catlog  CHANGING pt_fieldcat TYPE slis_t_fieldcat_alv.

  DATA : ls_fcat TYPE slis_fieldcat_alv.

*--------------------Maintenance plan-------------------------*
  ls_fcat-fieldname = 'WARPL'.
  ls_fcat-rollname = 'WARPL'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------Maintenance call-------------------------*
  ls_fcat-fieldname = 'ABNUM'.
  ls_fcat-rollname = 'ABNUM'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------Maintenance item-------------------------*
  ls_fcat-fieldname = 'WAPOS'.
  ls_fcat-rollname = 'WAPOS'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------Planned date-----------------------------*
  ls_fcat-fieldname = 'NPLDA'.
  ls_fcat-rollname = 'NPLDA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*----------------Sort field for maintenance plans----------------*
  ls_fcat-fieldname = 'PLAN_SORT'.
  ls_fcat-rollname = 'PLAN_SORT'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------Scheduling Period---------------------*
  ls_fcat-fieldname = 'ABRHO'.
  ls_fcat-rollname = 'ABRHO'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------------Item Short Text---------------------*
  ls_fcat-fieldname = 'PSTXT'.
  ls_fcat-rollname = 'POSTXT'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------------Equipment---------------------------*
  ls_fcat-fieldname = 'EQUNR'.
  ls_fcat-rollname = 'EQUNR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------------Equipment description---------------*
  ls_fcat-fieldname = 'EQKTX'.
  ls_fcat-outputlen = '40'.
  ls_fcat-seltext_l = 'Equipment description'(i10).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------------Serial number-----------------------*
  ls_fcat-fieldname = 'SERIALNR'.
  ls_fcat-rollname = 'GERNR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------------Division----------------------------*
  ls_fcat-fieldname = 'PLCID'.
  ls_fcat-outputlen = '8'.
  ls_fcat-seltext_l = 'Division'(i11).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-------------------Key for Task List Group--------------------*
  ls_fcat-fieldname = 'PLNNR'.
  ls_fcat-outputlen = '16'.
  ls_fcat-seltext_l = 'Task List Group'(i12).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Group Counter-----------------*
  ls_fcat-fieldname = 'PLNAL'.
  ls_fcat-rollname = 'PLNAL'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Work Counter-----------------*
  ls_fcat-fieldname = 'ARBPL'.
  ls_fcat-rollname = 'ARBPL'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Sold-to partner---------------------*
  ls_fcat-fieldname = 'PARNR'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Sold-to partner'(i13).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Sold-to name------------------------*
  ls_fcat-fieldname = 'PARNR_NAME'.
  ls_fcat-outputlen = '35'.
  ls_fcat-seltext_l = 'Name Sold-to'(i14).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Bill-to partner---------------------*
  ls_fcat-fieldname = 'BILLTO'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Bill-to partner'(i15).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Bill-to name------------------------*
  ls_fcat-fieldname = 'BILLTO_NAME'.
  ls_fcat-outputlen = '35'.
  ls_fcat-seltext_l = 'Name Bill-to'(i16).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Sales Document--------------------*
  ls_fcat-fieldname = 'KDAUF'.
  ls_fcat-rollname = 'VBELN_VA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Sales Document Item----------------*
  ls_fcat-fieldname = 'KDPOS'.
  ls_fcat-rollname = 'POSNR_VA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Hours-----------------------------*
  ls_fcat-fieldname = 'HRS'.
  ls_fcat-rollname = 'ARBEIT'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
**-----------------------Hours Unit-------------------------*
*  ls_fcat-fieldname = 'HRS_UNIT'.
*  ls_fcat-rollname = 'ARBEITE'.
*  APPEND ls_fcat TO pt_fieldcat.
*  CLEAR ls_fcat.
*-----------------------Hours Cost--------------------------*
  ls_fcat-fieldname = 'HRS_COST'.
  ls_fcat-outputlen = '16'.
  ls_fcat-seltext_l = 'Work Cost'(i17).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*----------------Spare parts Cost--------------------------*
  ls_fcat-fieldname = 'SPP_COST'.
  ls_fcat-outputlen = '16'.
  ls_fcat-seltext_l = 'Spare parts Cost'(i18).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Distance--------------------------*
  ls_fcat-fieldname = 'DIST'.
  ls_fcat-outputlen = '16'.
  ls_fcat-seltext_l = 'Distance'(i19).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
**--------------------Distance Unit-------------------------*
*  ls_fcat-fieldname = 'DIST_UNIT'.
*  ls_fcat-outputlen = '16'.
*  ls_fcat-seltext_l = 'Distance Unit'.
*  APPEND ls_fcat TO pt_fieldcat.
*  CLEAR ls_fcat.
*--------------------Distance Cost--------------------------*
  ls_fcat-fieldname = 'DIST_COST'.
  ls_fcat-outputlen = '16'.
  ls_fcat-seltext_l = 'Distance Cost'(i20).
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.

ENDFORM.                    " build_field_catlog

*&---------------------------------------------------------------------*
*&      Form  fill_events_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM fill_events_f14 .

  DATA h_event       TYPE slis_alv_event.

  CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
    EXPORTING
      i_list_type = 0
    IMPORTING
      et_events   = g_events_tab.

*--- allocate form for user-command ---------------------------------*
  READ TABLE g_events_tab WITH KEY name = slis_ev_user_command
                        INTO h_event.
  IF sy-subrc = 0.
    MOVE g_form_user_command TO h_event-form.
    MODIFY g_events_tab FROM h_event INDEX sy-tabix.
  ENDIF.

ENDFORM.                    " fill_events_f14

*&--------------------------------------------------------------------*
*&      Form  user_command_l
*&--------------------------------------------------------------------*
*       text
*---------------------------------------------------------------------*
*      -->P_UCOMM    text
*      -->P_SELFIELD text
*---------------------------------------------------------------------*
FORM user_command_l USING p_ucomm LIKE sy-ucomm
                          p_selfield TYPE slis_selfield.

  p_selfield-refresh = 'S'.
  PERFORM check_pf2_with_object_f16 USING p_ucomm.
  PERFORM set_p_selfield_general_f16 USING p_selfield.

  CASE p_ucomm.
    WHEN 'ISEL'.
      p_ucomm = 'DISP'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
  ENDCASE.

ENDFORM.                    "user_command_l

*&---------------------------------------------------------------------*
*&      Form  check_pf2_with_object_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*----------------------------------------------------------------------*
FORM check_pf2_with_object_f16  USING    p_ucomm.

  CHECK p_ucomm = gv_ic1.
  p_ucomm = 'ISEL'.

ENDFORM.                    " check_pf2_with_object_f16

*&---------------------------------------------------------------------*
*&      Form  set_p_selfield_general_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM set_p_selfield_general_f16  USING f_selfield TYPE slis_selfield.

  f_selfield-col_stable = gc_x.
  f_selfield-row_stable = gc_x.

ENDFORM.                    " set_p_selfield_general_f16

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*      -->P_ENDCASE  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_f16  USING p_ucomm LIKE sy-ucomm
                                p_selfield TYPE slis_selfield.

  PERFORM check_object_tab_marked_f14 USING p_ucomm p_selfield.

  LOOP AT gt_final WHERE selected = gc_x .
    l_index = sy-tabix.
    PERFORM fcodes_with_mark_l USING p_ucomm p_selfield.
    gt_final-selected = ' '.
    MODIFY gt_final INDEX l_index.
  ENDLOOP.

  CLEAR p_ucomm.

ENDFORM.                    " fcodes_with_mark_f16

*&---------------------------------------------------------------------*
*&      Form  check_object_tab_marked_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM check_object_tab_marked_f14  USING    p_ucomm LIKE sy-ucomm
                                        p_selfield TYPE slis_selfield.

  READ TABLE gt_final WITH KEY selected = gc_x.

  IF NOT sy-subrc IS INITIAL.
    IF NOT p_selfield-tabindex IS INITIAL.
      READ TABLE gt_final INDEX p_selfield-tabindex.
      gt_final-selected = gc_x.
      MODIFY gt_final INDEX p_selfield-tabindex.
    ENDIF.
  ELSE.
*--- Checkbox markiert -----------------------------------------------*
    p_selfield-sel_tab_field = 'G_MARK'.
  ENDIF.

ENDFORM.                    " check_object_tab_marked_f14

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_l
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_F_UCOMM  text
*      -->P_F_SELFIELD  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_l  USING   p_ucomm LIKE sy-ucomm
                              p_selfield TYPE slis_selfield.

  DATA: h_ucomm LIKE sy-ucomm.

  CASE p_ucomm.
*   Display MP
    WHEN 'DISP'.
      SET PARAMETER ID 'MPL' FIELD  gt_final-warpl.
      CALL TRANSACTION 'IP03' AND SKIP FIRST SCREEN.
  ENDCASE.

ENDFORM.                    " fcodes_with_mark_l

*&---------------------------------------------------------------------*
*&      Form  alv_display
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM alv_display .

  g_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
     EXPORTING
       i_callback_program                =  g_repid
       i_save                            = 'A'
       it_events                         =  g_events_tab[]
*      I_GRID_TITLE                      =
*      I_GRID_SETTINGS                   =
*      is_layout                         =  g_layout
       it_fieldcat                       =  gt_fieldcat[]
     TABLES
        t_outtab                         =  gt_final.

ENDFORM.                    " alv_display

*&---------------------------------------------------------------------*
*&      Form  GET_KOSTL
*&---------------------------------------------------------------------*
*       read cost center and controlling area
*----------------------------------------------------------------------*
*  -->  arbid
*  <--  kostl
*----------------------------------------------------------------------*
FORM get_kostl USING    arbid TYPE cr_objid
               CHANGING kostl TYPE kostl
                        kokrs type kokrs.

  DATA:
    i_rcrco_a like rcrco_a.

  CALL FUNCTION 'CR_WORKCENTER_READ_COSTING'
    EXPORTING
      ARBID                    = arbid
      DATE                     = sy-datum
*     ALL_SETS_FROM_DATE       = ' '
    IMPORTING
*     ECRHD                    =
      EDATA                    = i_rcrco_a
*   TABLES
*     ET_CRCO_A                =
   EXCEPTIONS
     NOT_FOUND                = 1
     OTHERS                   = 2
      .

  IF SY-SUBRC = 0 and not i_rcrco_a-kostl is initial.
    kostl = i_rcrco_a-kostl.
    kokrs = i_rcrco_a-kokrs.
  ENDIF.

ENDFORM.                               " GET_KOSTL

*&---------------------------------------------------------------------*
*&      Form  get_fi_period
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_fi_period USING    p_budat
                            p_bukrs
                   CHANGING p_monat
                            p_gjahr.

  CALL FUNCTION 'FI_PERIOD_DETERMINE'
    EXPORTING
      i_budat              = p_budat
      i_bukrs              = p_bukrs
*     I_PERIV              = ' '
*     I_GJAHR              = 0000
*     I_MONAT              = 00
*     X_XMO16              = ' '
    IMPORTING
      e_gjahr              = p_gjahr
      e_monat              = p_monat
*     E_POPER              =
    EXCEPTIONS
      fiscal_year          = 1
      period               = 2
      period_version       = 3
      posting_period       = 4
      special_period       = 5
      version              = 6
      posting_date         = 7
      OTHERS               = 8.

ENDFORM.                    " get_fi_period

*&---------------------------------------------------------------------*
*&      Form  calc_cost
*&---------------------------------------------------------------------*
*       Get costprice from the activity type/price planning maint. KP26
*----------------------------------------------------------------------*
FORM calc_cost USING p_kokrs
                     p_costc
                     p_acttype
                     p_jahr
                     p_monat
                     p_quant
                     p_unit
            CHANGING p_value.

  DATA: l_pricequant(7) TYPE p DECIMALS 2 VALUE 1,
        l_unit(10) TYPE c,
        l_messagetext(80) TYPE c.

  FIELD-SYMBOLS: <fs>  TYPE ANY,
                 <fs2> TYPE ANY.

  CLEAR: i_cosel[],
         i_costa[],
         wa_cosel.

  REFRESH: i_cosel[],
           i_costa[].

* Construct internal table for COSEL.
  wa_cosel-sign   = 'I'.
  wa_cosel-option = 'EQ'.

* Field Fiscal year
  wa_cosel-field  = 'GJAHR'.
  wa_cosel-low    = p_jahr.
  APPEND wa_cosel TO i_cosel.
  CLEAR: wa_cosel-field,
         wa_cosel-low.

* Field Ledger for Controlling objects
  wa_cosel-field  = 'LEDNR'.
  wa_cosel-low    = '00'.
  APPEND wa_cosel TO i_cosel.
  CLEAR: wa_cosel-field,
         wa_cosel-low.

* Field Object Number
* Construct object number
  CLEAR gv_objnr.

  CONCATENATE gc_kl
              p_kokrs
              p_costc
              p_acttype INTO gv_objnr.

  wa_cosel-field  = 'OBJNR'.
  wa_cosel-low    = gv_objnr.
  APPEND wa_cosel TO i_cosel.
  CLEAR: wa_cosel-field,
         wa_cosel-low.

* Field Version
  wa_cosel-field  = 'VERSN'.
  wa_cosel-low    = '000'.
  APPEND wa_cosel TO i_cosel.
  CLEAR: wa_cosel-field,
         wa_cosel-low.

* Field Value Type
  wa_cosel-field  = 'WRTTP'.
  wa_cosel-low    = '01'.
  APPEND wa_cosel TO i_cosel.
  CLEAR: wa_cosel-field,
         wa_cosel-low.

* Get totals table for prices of all periods
  CALL FUNCTION 'K_COSTA_READ_MULTI'
    EXPORTING
      von_periode       = '001'
      bis_periode       = '500'
      modus             = '1'
*     PROGNAME          =
*     FORMNAME          =
    TABLES
      t_cosel           = i_cosel
      t_costa           = i_costa
*     T_FGR             =
            .

  CONCATENATE gc_tof0 p_monat INTO gv_fldperiod.

* This table should only come back with one row
  LOOP AT i_costa INTO wa_costa.

*   Get Activity Unit
    CLEAR: gv_leinh.
    FREE: gv_leinh.

    SELECT SINGLE leinh FROM cssl INTO gv_leinh WHERE
                             kokrs = p_kokrs               AND
                             kostl = p_costc               AND
                             lstar = p_acttype             AND
                             gjahr = p_jahr.

    IF sy-subrc NE 0.
*     Activity Unit Not Found in Table CSSL For Key & & & &
    ELSE.

      ASSIGN COMPONENT gv_fldperiod
      OF STRUCTURE     wa_costa TO <fs>.

      ASSIGN <fs> TO <fs2>.

      IF p_unit NE gv_leinh and
         not p_unit is initial.

        PERFORM unit_conversion USING  gv_leinh
                                       p_unit
                                       p_quant
                              CHANGING l_pricequant.
      ELSE.

        l_pricequant = p_quant.

      ENDIF.

*     Part calculation of field value (has to be divided still by price
*     unit)
      p_value = <fs> * l_pricequant.

*     Get price unit out of TOE0xx and recalculate correct value
      CLEAR gv_fldperiod.
      CONCATENATE gc_toe0 p_monat INTO gv_fldperiod.

      ASSIGN COMPONENT gv_fldperiod
      OF STRUCTURE     wa_costa TO <fs>.

      p_value = p_value / <fs>.

    ENDIF.

  ENDLOOP.

ENDFORM.                    " calc_cost

*&---------------------------------------------------------------------*
*&      Form  unit_conversion
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_WA_OPERATIONS_DURATION_NORMAL_  text
*      -->P_G_LEINH  text
*      <--P_P_VALUE  text
*----------------------------------------------------------------------*
FORM unit_conversion  USING    p_tounit
                               p_fromunit
                               p_pricequant
                      CHANGING p_quantity.

  CALL FUNCTION 'UNIT_CONVERSION_SIMPLE'
    EXPORTING
      input                      = p_pricequant
*     NO_TYPE_CHECK              = 'X'
*     ROUND_SIGN                 = ' '
      unit_in                    = p_fromunit
      unit_out                   = p_tounit
    IMPORTING
*     ADD_CONST                  =
*     DECIMALS                   =
*     DENOMINATOR                =
*     NUMERATOR                  =
      output                     = p_quantity
    EXCEPTIONS
      conversion_not_found       = 1
      division_by_zero           = 2
      input_invalid              = 3
      output_invalid             = 4
      overflow                   = 5
      type_invalid               = 6
      units_missing              = 7
      unit_in_not_found          = 8
      unit_out_not_found         = 9
      OTHERS                     = 10.

ENDFORM.                    " unit_conversion

*&---------------------------------------------------------------------*
*&      Form  GET_ARBID
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->ARBPL                                                        *
*      -->ARBID_TAB                                                    *
*      -->ARBID                                                        *
*      -->P_G_WERKI                                                    *
*----------------------------------------------------------------------*
FORM get_arbid TABLES arbid_tab STRUCTURE arbid_tab_c
                      arbpl STRUCTURE s_arbpl
                      arbid STRUCTURE arbid_c
               USING  p_g_werki.

* read internal number of a work center -------------------------------*
  DESCRIBE TABLE arbpl LINES sy-tabix.
  IF NOT sy-tabix IS INITIAL.
    SELECT objid arbpl FROM crhd INTO TABLE arbid_tab
      WHERE arbpl IN arbpl
      AND werks EQ p_g_werki.

*-- no work center found ----------------------------------------------*
    DESCRIBE TABLE arbid_tab LINES sy-tabix.
    IF sy-tabix IS INITIAL.
      MESSAGE s047(ih).
      STOP.
    ENDIF.

*-- too many work centers for select-----------------------------------*
    IF sy-tabix > 255.
      MESSAGE i103(ih).
      STOP.
    ENDIF.

*-- fill range for selection ------------------------------------------*
    arbid-option = 'EQ'.
    arbid-sign = 'I'.
    LOOP AT arbid_tab.
      arbid-low = arbid_tab-arbid.
      APPEND arbid.
    ENDLOOP.
    SORT arbid_tab.
  ENDIF.

ENDFORM.                               " GET_ARBID

*Text symbol text£º
*001:Selection screen input
*E01:Planning plant does not exist
*E02:No authorization for plant :
*E03:Planner group does not exist for planning plant :
*I01:No maintenance plans selected !
*I02:No variant table for Central Task Lists found (CU60)
*I03:Could not find caracteristic :
*I10:Equipment description
*I11:Division
*I12:Task List Group
*I13:Sold-to partner
*I14:Name Sold-to
*I15:Bill-to partner
*I16:Name Bill-to
*I17:Work Cost
*I18:Spare parts Cost
*I19:Distance

*I20:Distance Cost
*Selection text£º
*P_IWERK:D       Planning plant
*P_WPGRP:D       Planner group
*S_ARBPL:D       Main work center
*S_EQUNR:D       Equipment
*S_KDAUF:        Contract number
*S_NPLDA:D       Planned Date
*S_WARPL:D       Maintenance plan
