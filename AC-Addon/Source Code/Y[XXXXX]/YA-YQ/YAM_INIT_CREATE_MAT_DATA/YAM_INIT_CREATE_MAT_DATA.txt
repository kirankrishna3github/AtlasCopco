*----------------------------------------------------------------------
* PROGRAM ID           : YAM_INIT_CREATE_MAT_DATA                      *
* PROGRAM TITLE        : AM: Initial create for plant/sales area       *
*                            specific material data                    *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 26/04/2005                                    *
* DEVELOPMENT ID       :                                               *
* CHANGE REQUEST NUMBER: CD1K902116                                    *
* PROGRAM DESCRIPTION  : Program that initially creates plant / sales  *
*               area specific segments when a new country is loaded.   *
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |27.10.2005|Luc Mertens      |CD1K903742| GAP Use P041       *
*                for plant GMIX do not create any sales data           *
*----------------------------------------------------------------------*

REPORT  YAM_INIT_CREATE_MAT_DATA  no standard page heading
                                  line-size 120
                                  line-count 80.

*- Tables -------------------------------------------------------------*
tables: mara,                   "Material Client level
        marc,                   "Material Plant level
        mlan,                   "Tax Classification for Material
        mvke,                   "Sales Data for Material
        mbew,                   "Accounting data for Material
        t001w.                  "Plants


*- Internal tables
*----------------------------------------------------------------------*
* Control report data
data: begin of g_output occurs 0,
        matnr like mara-matnr,
        o_werks like marc-werks,
        o_vkorg like mvke-vkorg,
        o_vtweg like mvke-vtweg,
        errflg(1),
        text(100).
data: end of g_output.

data: begin of i_mara occurs 0.
        include structure mara.
data: end   of i_mara.

data: begin of i_mvke occurs 0.
        include structure mvke.
data: end   of i_mvke.

data: begin of i_mlan occurs 0.
        include structure mlan.
data: end   of i_mlan.

data: begin of i_marc occurs 0.
        include structure marc.
data: end   of i_marc.

data: begin of i_mbew occurs 0.
        include structure mbew.
data: end   of i_mbew.

* MATERIAL_MAINTAIN_DARK interface tables
* General Data for Material
data: begin of gt_mara occurs 0.
        include structure mara_ueb.
data: end   of gt_mara.

* Plant Data for Material
data: begin of gt_marc occurs 0.
        include structure marc_ueb.
data: end   of gt_marc.

* Valuation Data for Material
data: begin of gt_mbew occurs 0.
        include structure mbew_ueb.
data: end   of gt_mbew.

* Sales Data for Material
data: begin of gt_mvke occurs 0.
        include structure mvke_ueb.
data: end   of gt_mvke.

* Tax clasification for Material
data: begin of gt_mlan occurs 0.
        include structure steu_ueb.
data: end   of gt_mlan.

* Messages
data: begin of gt_messtab occurs 0.
        include structure merrdat.
data: end   of gt_messtab.


*- Variables ----------------------------------------------------------*
data: g_vkorgfr   type vkorg,
      g_vkorgto   type vkorg,
      g_landfr    type land1,
      g_landto    type land1,
      g_subrc     type subrc,
      g_errors type bierrnum,
      g_msg_no type msgnr,
      g_cnt_tranc  type i value 1,
      g_cnt_d_ind  type i value 1.


*- Constants ----------------------------------------------------------*
constants: c_11    like mvke-vkorg value '11',    " Distr. channel
           c_mwst  type tatyp      value 'MWST',  " Tax category
* begin of insert MOD-001
           c_gmix(4) type c        value 'GMIX',  " Centr.tasklist plant
* end of insert MOD-001
           c_mm01  like sy-tcode   value 'MM01'.  " Transaction code


*- Select-options / Parameters ----------------------------------------*
select-options: s_matnr for mara-matnr,
                s_mtart for mara-mtart.

parameters:     p_werkfr type werks obligatory,   " plant code FROM
                p_werkto type werks obligatory.   " plant code TO


*- Selection-screen ---------------------------------------------------*
at selection-screen.

  if p_werkfr eq p_werkto.
    message e001(00) with text-e05.    "Plant FROM and TO must be #
  endif.


at selection-screen on p_werkfr.

  select single werks into t001w-werks
     from t001w
     where  werks = p_werkfr.

  if sy-subrc ne 0.
    message e001(00) with text-e02.    "Plant FROM doesn't exist
  endif.


at selection-screen on p_werkto.

  select single werks into t001w-werks
     from t001w
     where  werks = p_werkto.

  if sy-subrc ne 0.
    message e001(00) with text-e03.    "Plant TO doesn't exist
  endif.


*- Start-of-selection -------------------------------------------------*
start-of-selection.

* Sales organisation = plant
  g_vkorgfr = p_werkfr.
  g_vkorgto = p_werkto.

* get country code based on 'plant from'
  select single land1 into g_landfr
      from T001W where werks = p_werkfr.

* get country code based on 'plant to'
  select single land1 into g_landto
      from T001W where werks = p_werkto.

* select data from company to copy from
  select * from mara
      into table i_mara
      where matnr in s_matnr
        and mtart in s_mtart.

  if i_mara[] is initial.
    write: / text-e10.                " no materials selected
  else.
* begin of insert MOD-001
* if plant is 'GMIX' then only select plant data
    if p_werkto eq c_gmix.
      select * from marc
          into table i_marc
          for all entries in i_mara
          where matnr eq i_mara-matnr
            and werks eq p_werkfr.
    else.
* end of insert MOD-001
      select * from mvke
          into table i_mvke
          for all entries in i_mara
          where vkorg eq g_vkorgfr
            and vtweg eq c_11
            and matnr eq i_mara-matnr.

      select * from mlan
          into table i_mlan
          for all entries in i_mvke
          where matnr eq i_mvke-matnr
            and aland eq g_landfr.

      select * from marc
          into table i_marc
          for all entries in i_mvke
          where matnr eq i_mvke-matnr
            and werks eq p_werkfr.

      select * from mbew
          into table i_mbew
          for all entries in i_marc
          where matnr eq i_marc-matnr
            and bwkey eq i_marc-werks.
* begin of insert MOD-001
    endif.
* end of insert MOD-001
  endif.


*- End-of-selection ---------------------------------------------------*
end-of-selection.

  sort: i_mvke, i_mlan, i_marc, i_mbew.

* begin of insert MOD-001
  if p_werkto eq c_gmix.

    loop at i_marc.

      select single * from marc
        where matnr eq i_marc-matnr
          and werks eq p_werkto.

      if sy-subrc eq 0.
        clear g_output.
        move 'X'      to g_output-errflg.
        move i_marc-matnr  to g_output-matnr.
        move p_werkto      to g_output-o_werks.
        move text-e07 to g_output-text.
        append g_output.
      endif.

      check not sy-subrc eq 0.

      perform copy_material_gmix.

    endloop.

  else.
* end of insert MOD-001

    loop at i_mvke.

      clear g_subrc.

*   check existence of plant/SA to be created
      select single * from mvke
        where vkorg eq g_vkorgto
          and vtweg eq i_mvke-vtweg
          and matnr eq i_mvke-matnr.

      if sy-subrc eq 0.
        clear g_output.
        move 'X'      to g_output-errflg.
        move i_mvke-matnr  to g_output-matnr.
        move g_vkorgto     to g_output-o_vkorg.
        move i_mvke-vtweg  to g_output-o_vtweg.
        move text-e06 to g_output-text.
        append g_output.
      endif.

      g_subrc = sy-subrc.

      select single * from marc
        where matnr eq i_mvke-matnr
          and werks eq p_werkto.

      if sy-subrc eq 0.
        clear g_output.
        move 'X'      to g_output-errflg.
        move i_mvke-matnr  to g_output-matnr.
        move p_werkto      to g_output-o_werks.
        move text-e07 to g_output-text.
        append g_output.
      endif.

      check not ( sy-subrc eq 0 and g_subrc eq 0 ).

      read table i_mlan with key matnr = i_mvke-matnr
                                 aland = g_landfr
                          binary search.
      read table i_marc with key matnr = i_mvke-matnr
                                 werks = i_mvke-vkorg
                          binary search.
      read table i_mbew with key matnr = i_mvke-matnr
                                 bwkey = i_mvke-vkorg
                          binary search.
      perform copy_material.

    endloop.
* begin of insert MOD-001
  endif.
* end of insert MOD-001

  perform write_output.


*- forms --------------------------------------------------------------*
*&---------------------------------------------------------------------
*&      Form  COPY_MATERIAL
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form copy_material.

* General Material Data
  gt_mara-mandt = sy-mandt.
  gt_mara-tcode = c_mm01.
  gt_mara-tranc = g_cnt_tranc.
  gt_mara-d_ind = g_cnt_d_ind.

  gt_mara-matnr = i_mvke-matnr.
  append gt_mara.

* Plant Data for Material
  gt_marc-mandt = sy-mandt.
  gt_marc-tranc = g_cnt_tranc.
  gt_marc-d_ind = g_cnt_d_ind.

  move-corresponding i_marc to gt_marc.
  gt_marc-werks = p_werkto.
  append gt_marc.

* Sales Data for Material
  gt_mvke-mandt = sy-mandt.
  gt_mvke-tranc = g_cnt_tranc.
  gt_mvke-d_ind = g_cnt_d_ind.

  move-corresponding i_mvke to gt_mvke.
  gt_mvke-vkorg = g_vkorgto.
  gt_mvke-vtweg = c_11.
  gt_mvke-dwerk = p_werkto.
  append gt_mvke.

* Tax classification for Material
  gt_mlan-mandt = sy-mandt.
  gt_mlan-tranc = g_cnt_tranc.
  gt_mlan-d_ind = g_cnt_d_ind.

  move-corresponding i_mlan to gt_mlan.
  gt_mlan-aland = g_landto.
  gt_mlan-tatyp = c_mwst.
  gt_mlan-taxkm = i_mlan-taxm1.
  append gt_mlan.

* Valuation Data for Material
  gt_mbew-mandt = sy-mandt.
  gt_mbew-tranc = g_cnt_tranc.
  gt_mbew-d_ind = g_cnt_d_ind.

  move-corresponding i_mbew to gt_mbew.
  gt_mbew-bwkey = p_werkto.

  clear: gt_mbew-stprs,                      "Standard price
         gt_mbew-verpr,                      "Moving average price
         gt_mbew-stprv,                      "Previous price
         gt_mbew-laepr,                      "Last Price change
         gt_mbew-zkprs,                      "Future price
         gt_mbew-zkdat,                      "Valid from
         gt_mbew-lbkum,                      "Total stock
         gt_mbew-salk3.                      "Total value

* Clear historical data ( Previous period)
  clear: gt_mbew-vmbkl,                      "Valuation Class
         gt_mbew-vmvpr,                      "Price Control
         gt_mbew-vmpei,                      "Price Unit
         gt_mbew-vmver,                      "Moving Average Price
         gt_mbew-vmstp,                      "Standard Price
         gt_mbew-vmkum,                      "Total stock
         gt_mbew-vmsal.                      "Total stock value

* Clear historical data ( Previous year)
  clear: gt_mbew-vjbkl,                      "Valuation Class
         gt_mbew-vjvpr,                      "Price Control
         gt_mbew-vjpei,                      "Price Unit
         gt_mbew-vjver,                      "Moving Average Price
         gt_mbew-vjstp,                      "Standard Price
         gt_mbew-vjkum,                      "Total stock
         gt_mbew-vjsal.                      "Total stock value
  append gt_mbew.

  perform change_material using gt_mvke-matnr gt_marc-werks
                          gt_mvke-vkorg gt_mvke-vtweg.

endform.                    "copy_material

*&---------------------------------------------------------------------*
*&      Form  CHANGE_MATERIAL
*&---------------------------------------------------------------------*
*       Call DI FM 'MATERIAL_MAINTAIN_DARK' to change the Material
*----------------------------------------------------------------------*
form change_material using r_matnr r_werks r_vkorg r_vtweg.

* Change Material
  call function 'MATERIAL_MAINTAIN_DARK'
      exporting
            p_kz_no_warn              = 'N'
            kz_prf                    = 'W'
      importing
            number_errors_transaction = g_errors
      tables
            amara_ueb                 = gt_mara
            amarc_ueb                 = gt_marc
            ambew_ueb                 = gt_mbew
            amvke_ueb                 = gt_mvke
            asteu_ueb                 = gt_mlan
            amerrdat                  = gt_messtab
*           amfieldres                =
      exceptions
            kstatus_empty             = 1
            tkstatus_empty            = 2
            t130m_error               = 3
            internal_error            = 4
            too_many_errors           = 5
            update_error              = 6
            others                    = 7.

  if sy-subrc eq 0.
    if g_errors ne 0.
* Add messages to output table
      loop at gt_messtab.
        clear g_output.
        if gt_messtab-msgty eq 'E' or gt_messtab-msgty eq 'A'.
          move 'X' to g_output-errflg.
        endif.
        move r_matnr to g_output-matnr.
        move r_werks to g_output-o_werks.
        move r_vkorg to g_output-o_vkorg.
        move r_vtweg to g_output-o_vtweg.

        perform prepare_message_text using    gt_messtab
                                     changing g_output-text.
        append g_output.
      endloop.
    else.
      commit work and wait.
      clear g_output.
      move r_matnr to g_output-matnr.
      move r_werks to g_output-o_werks.
      move r_vkorg to g_output-o_vkorg.
      move r_vtweg to g_output-o_vtweg.
      append g_output.
    endif.
  else.
    rollback work.
* Add message to output table
    clear g_output.
    move 'X'      to g_output-errflg.
    move r_matnr  to g_output-matnr.
    move r_werks  to g_output-o_werks.
    move r_vkorg  to g_output-o_vkorg.
    move r_vtweg  to g_output-o_vtweg.
    move text-e04 to g_output-text.
    append g_output.
  endif.

* Refresh interface tables
  clear: gt_mara, gt_marc, gt_mbew, gt_mvke, gt_mlan, gt_messtab,
                g_errors.
  refresh: gt_mara, gt_marc, gt_mbew, gt_mvke, gt_mlan, gt_messtab.

endform.                    "change_material

*&---------------------------------------------------------------------*
*&      Form  PREPARE_MESSAGE_TEXT
*&---------------------------------------------------------------------*
*       Format message with variables
*----------------------------------------------------------------------*
*  -->  P_MESSTAB  Messages table header line
*  <--  R_TEXT     Message text
*----------------------------------------------------------------------*
form prepare_message_text using    p_messtab like merrdat
                          changing r_text.

  clear r_text.
  g_msg_no = p_messtab-msgno.       "Type conflict!
  call function 'MESSAGE_PREPARE'
    EXPORTING
      language               = sy-langu
      msg_id                 = p_messtab-msgid
      msg_no                 = g_msg_no
      msg_var1               = p_messtab-msgv1
      msg_var2               = p_messtab-msgv2
      msg_var3               = p_messtab-msgv3
      msg_var4               = p_messtab-msgv4
    IMPORTING
      msg_text               = r_text
    EXCEPTIONS
      function_not_completed = 1
      message_not_found      = 2
      others                 = 3.

  if sy-subrc ne 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    concatenate p_messtab-msgty p_messtab-msgid p_messtab-msgno
                into r_text separated by space.
  else.
    concatenate p_messtab-msgty p_messtab-msgid p_messtab-msgno r_text
                into r_text separated by space.
  endif.

endform.                    " PREPARE_MESSAGE_TEXT

*&---------------------------------------------------------------------*
*&      Form  WRITE_OUTPUT
*&---------------------------------------------------------------------*
*       Output the control report
*----------------------------------------------------------------------*
form write_output.

  sort g_output by matnr.

* Output
  loop at g_output where errflg eq 'X'.

    at new matnr.
      write:/ text-002.          "Error occured during call transaction
    endat.

    write:/ g_output-matnr, g_output-o_werks, g_output-o_vkorg,
            g_output-o_vtweg, g_output-text.

    at end of matnr.
      uline.
    endat.

  endloop.

endform.                    " WRITE_OUTPUT

*&---------------------------------------------------------------------
*&      Form  COPY_MATERIAL_GMIX                       MOD-001
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form copy_material_gmix.

* General Material Data
  gt_mara-mandt = sy-mandt.
  gt_mara-tcode = c_mm01.
  gt_mara-tranc = g_cnt_tranc.
  gt_mara-d_ind = g_cnt_d_ind.

  gt_mara-matnr = i_marc-matnr.
  append gt_mara.

* Plant Data for Material
  gt_marc-mandt = sy-mandt.
  gt_marc-tranc = g_cnt_tranc.
  gt_marc-d_ind = g_cnt_d_ind.

  move-corresponding i_marc to gt_marc.
  gt_marc-werks = p_werkto.
  append gt_marc.

  perform change_material_gmix using gt_marc-matnr gt_marc-werks.

endform.                    "copy_material_gmix

*&---------------------------------------------------------------------*
*&      Form  change_material_gmix
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_GT_MARC_MATNR  text
*      -->P_GT_MARC_WERKS  text
*----------------------------------------------------------------------*
FORM change_material_gmix  USING    P_GT_MARC_MATNR
                                    P_GT_MARC_WERKS.

* Change Material
  call function 'MATERIAL_MAINTAIN_DARK'
      exporting
            p_kz_no_warn              = 'N'
            kz_prf                    = 'W'
      importing
            number_errors_transaction = g_errors
      tables
            amara_ueb                 = gt_mara
            amarc_ueb                 = gt_marc
            amerrdat                  = gt_messtab
*           amfieldres                =
      exceptions
            kstatus_empty             = 1
            tkstatus_empty            = 2
            t130m_error               = 3
            internal_error            = 4
            too_many_errors           = 5
            update_error              = 6
            others                    = 7.

  if sy-subrc eq 0.
    if g_errors ne 0.
* Add messages to output table
      loop at gt_messtab.
        clear g_output.
        if gt_messtab-msgty eq 'E' or gt_messtab-msgty eq 'A'.
          move 'X' to g_output-errflg.
        endif.
        move P_GT_MARC_MATNR to g_output-matnr.
        move P_GT_MARC_WERKS to g_output-o_werks.

        perform prepare_message_text using    gt_messtab
                                     changing g_output-text.
        append g_output.
      endloop.
    else.
      commit work and wait.
      clear g_output.
      move P_GT_MARC_MATNR to g_output-matnr.
      move P_GT_MARC_WERKS to g_output-o_werks.
      append g_output.
    endif.
  else.
    rollback work.
* Add message to output table
    clear g_output.
    move 'X'      to g_output-errflg.
    move P_GT_MARC_MATNR  to g_output-matnr.
    move P_GT_MARC_WERKS  to g_output-o_werks.
    move text-e04 to g_output-text.
    append g_output.
  endif.

* Refresh interface tables
  clear: gt_mara, gt_marc, gt_messtab,
                g_errors.
  refresh: gt_mara, gt_marc, gt_messtab.

ENDFORM.                    " change_material_gmix

*Text symbol text£º
*001:Copy is successful
*002:Error occured during call transaction
*E01:Enter a valid material number
*E02:Plant FROM doesn't exist
*E03:Plant TO doesn't exist
*E04:Update material failed !!!
*E05:Plant FROM and Plant TO must be different
*E06:Sales Area specific data already exists
*E07:Plant specific data already exists

*E10:No materials selected
*Selection text£º
*P_WERKFR:        Plant FROM
*P_WERKTO:        Plant TO
*S_MATNR:D       Material
*S_MTART:D       Material Type
