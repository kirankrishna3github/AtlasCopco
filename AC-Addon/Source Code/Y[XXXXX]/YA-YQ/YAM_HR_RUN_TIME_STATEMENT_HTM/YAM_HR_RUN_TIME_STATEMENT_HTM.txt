*&---------------------------------------------------------------------*
*& Report  YAM_HR_RUN_TIME_statement                                   *
*&---------------------------------------------------------------------*
*&  This program will execute the program RPTEDT00 with variant.       *
*&  The spool will be delivered via email.                             *
*&---------------------------------------------------------------------*
* Date     Name           Description                                  *
* 200810   DeBruyne Bart  started from YAM_HR_RUN_TIME_EVALUATION_HTM  *
* 201003   Maryns Els     Change of standard LOCL printer to HRTS
*----------------------------------------------------------------------*

REPORT  yam_hr_run_time_statement_htm.              .
*----------------------------------------------------------------------*
*  Declaration of SYSTEM TABLES                                        *
*----------------------------------------------------------------------*
TABLES: tsp01.        " Spool Requests

*----------------------------------------------------------------------*
*  TYPES DEFINITIONS                                                   *
*----------------------------------------------------------------------*
TYPES: BEGIN OF t_tsp01.
        INCLUDE STRUCTURE tsp01.               " Spool Requests
TYPES: END OF t_tsp01.

*----------------------------------------------------------------------*
*  Declaration of INTERNAL TABLES                                      *
*----------------------------------------------------------------------*
DATA: i_params TYPE STANDARD TABLE OF rsparams WITH HEADER LINE,
      i_variant TYPE STANDARD TABLE OF rsparams WITH HEADER LINE.

DATA: i_return TYPE STANDARD TABLE OF bapiret2 WITH HEADER LINE,
      i_smtp TYPE STANDARD TABLE OF bapiadsmtp WITH HEADER LINE,
      i_addr TYPE STANDARD TABLE OF bapiaddr3 WITH HEADER LINE.

* Management errors
DATA: messtab TYPE STANDARD TABLE OF bdcmsgcoll WITH HEADER LINE.
*----------------------------------------------------------------------*
*  Declaration of VARIABLES                                            *
*----------------------------------------------------------------------*
DATA: ws_pgm_1 LIKE rsvar-report,
      ws_pgm_2 LIKE rsvar-report.

DATA: rc LIKE sy-subrc.

DATA: params LIKE pri_params,
      valid    TYPE c,
      lv_paart(50),
      lv_linsz(10),
      lv_linct(10).

DATA: wa_tsp01 TYPE t_tsp01.

DATA: ws_spool_num  LIKE tsp01-rqident,
      ws_spool_name LIKE tsp01-rq2name,
      ws_mail       LIKE i_smtp-e_mail.

* Management errors
DATA: l_mstring TYPE string.
DATA: g_error(1) VALUE ' '.
*----------------------------------------------------------------------*
*  Selection screen                                                    *
*----------------------------------------------------------------------*
PARAMETERS: p_myvar(14),
            p_sub LIKE sodocchgi1-obj_descr,
            p_mail(40).

ws_pgm_1 = 'RPTEDT00'.
ws_pgm_2 = 'YAM_HR_EMAIL_SPOOL_IN_HTML'.

* the used Variant must exist for pgm RPTEDT00
PERFORM variant_exists USING ws_pgm_1.
IF g_error = 'X'.
  PERFORM error_management.
  EXIT.
ENDIF.

CONCATENATE 'TS' p_myvar INTO ws_spool_name.
CALL FUNCTION 'GET_PRINT_PARAMETERS'
  EXPORTING
    destination            = 'HRTS'
    immediately            = ' '
    release                = ' '
    new_list_id            = 'X'
    no_dialog              = 'X'
    line_count             = 65
    line_size              = 82
    list_name              = ws_spool_name
    expiration             = 1
  IMPORTING
    out_parameters         = params
    valid                  = valid
  EXCEPTIONS
    archive_info_not_found = 1
    invalid_print_params   = 2
    invalid_archive_params = 3
    OTHERS                 = 4.

IF sy-subrc <> 0.
  g_error = 'X'.
  messtab-msgspra = sy-langu.     " language
  messtab-msgid   = 'YAM_AFRU'.   " class
  messtab-msgnr   = '030'.        " number
  messtab-msgv1   = p_myvar.
  messtab-msgv2   = sy-subrc.
  APPEND messtab. CLEAR messtab.
ENDIF.

CHECK ( g_error IS INITIAL ).

IF valid <> space.
  lv_linsz = 82. " width of list
  lv_linct = 65. " page length of list

  CONCATENATE 'X_' lv_linct '_' lv_linsz INTO lv_paart.
  CONDENSE lv_paart NO-GAPS.
  SHIFT lv_paart LEFT DELETING LEADING space.
  params-paart = lv_paart+0(16).

*     RPTEDT00 is executed with the variant selected above
*     Results are stored into the spool
  SUBMIT rptedt00 USING SELECTION-SET p_myvar
                  TO SAP-SPOOL WITHOUT SPOOL DYNPRO
                  SPOOL PARAMETERS params
                  AND RETURN.

ENDIF.

IF sy-batch = 'X'.
  COMMIT WORK AND WAIT.
ENDIF.
*--------------------------------------------------------------
*   time evaluation is executed and report is created, now we
*   are going to send the report in HTML format as attachment
*   to an email.
*--------------------------------------------------------------
IF p_mail <> 'N' AND p_mail <> ' '.
*      get the spool-number.
  PERFORM get_spool_request.
  CHECK ( g_error IS INITIAL ).
*      Email is retrieved from parameter p_mail.
  ws_mail = p_mail.
*      call the program with correct parameters
  PERFORM fill_variant_2 TABLES i_params.
  CHECK ( g_error IS INITIAL ).

  SUBMIT YAM_HR_EMAIL_SPOOL_IN_HTML
     WITH  SELECTION-TABLE i_params
     TO SAP-SPOOL WITHOUT SPOOL DYNPRO
     SPOOL PARAMETERS params
     AND RETURN.
ENDIF.              "IF p_mail <> 'N'.

END-OF-SELECTION.
*****************
  PERFORM error_management.
  WRITE:/ 'Program ended'.




*&---------------------------------------------------------------------*
*&      Form  fill_variant2  (YAM_HR_EMAIL_SPOOL_IN_HTML)
*&---------------------------------------------------------------------*
FORM fill_variant_2  TABLES p_i_variant STRUCTURE i_params.
*  DATA: i_data TYPE STANDARD TABLE OF rsparams WITH HEADER LINE.
  DATA: tmp_spool_num(10).

  Refresh i_params.

  clear i_params.
  i_params-selname = 'S_SPOOL'.
  i_params-kind = 'S'.
  i_params-sign = 'I'.
  i_params-option = 'EQ'.
  MOVE ws_spool_num TO tmp_spool_num.
  i_params-low = tmp_spool_num.     " spoolnumber
  APPEND i_params.

  clear i_params.
  i_params-selname = 'P_DES'.
  i_params-kind = 'P'.
  i_params-low = p_sub.             " subject of email
  APPEND i_params.

  clear i_params.
  i_params-selname = 'S_EMAILS'.
  i_params-kind = 'S'.
  i_params-sign = 'I'.
  i_params-option = 'EQ'.
  i_params-low = ws_mail.            " email receiver 1
  APPEND i_params.

ENDFORM.                    " fill_variant2

*&---------------------------------------------------------------------*
*&      Form  variant_exists
*&---------------------------------------------------------------------*
*   Variant existence check
*----------------------------------------------------------------------*
FORM variant_exists USING ws_pgm LIKE rsvar-report.

  CALL FUNCTION 'RS_VARIANT_EXISTS'
    EXPORTING
      report              = ws_pgm
      variant             = p_myvar                         "BDB 200810
    IMPORTING
      r_c                 = rc
    EXCEPTIONS
      not_authorized      = 1
      no_report           = 2
      report_not_existent = 3
      report_not_supplied = 4
      OTHERS              = 5.
  IF rc <> 0.
    g_error = 'X'.
    messtab-msgspra = sy-langu.    " language
    messtab-msgid   = 'YAM_AFRU'.  " class
    messtab-msgnr   = '034'.
    messtab-msgv1   = p_myvar.
    APPEND messtab. CLEAR messtab.
  ENDIF.

ENDFORM.                    " variant_exists

*&---------------------------------------------------------------------*
*&      Form error_management
*&---------------------------------------------------------------------*
FORM error_management .
  LOOP AT messtab.
    SELECT SINGLE text FROM t100
                       INTO l_mstring
                       WHERE ( sprsl = messtab-msgspra ) AND
                             ( arbgb = messtab-msgid ) AND
                             ( msgnr = messtab-msgnr ).
    IF sy-subrc = 0.
      IF l_mstring CS '&1'.
        REPLACE '&1' WITH messtab-msgv1 INTO l_mstring.
        REPLACE '&2' WITH messtab-msgv2 INTO l_mstring.
        REPLACE '&3' WITH messtab-msgv3 INTO l_mstring.
        REPLACE '&4' WITH messtab-msgv4 INTO l_mstring.
      ELSE.
        REPLACE '&' WITH messtab-msgv1 INTO l_mstring.
        REPLACE '&' WITH messtab-msgv2 INTO l_mstring.
        REPLACE '&' WITH messtab-msgv3 INTO l_mstring.
        REPLACE '&' WITH messtab-msgv4 INTO l_mstring.
      ENDIF.

      CONDENSE l_mstring.
    ELSE.

    ENDIF.
    WRITE l_mstring.

  ENDLOOP.

ENDFORM.                    " management_errors

*&---------------------------------------------------------------------*
*& Form  get_spool_request
*&      Retrieves the correct spool-number. This is depending whether
*&      program is submitted in foreground or background.
*& returns: ws_spool_num
*&---------------------------------------------------------------------*
FORM get_spool_request .
  ws_spool_num = 0.

  IF sy-batch = 'X'.
*     We need the second register because in batch mode, it generates an
*     extra spool request that it's the first register.
    SELECT * UP TO 2 ROWS
           INTO wa_tsp01
           FROM tsp01
           WHERE rqclient = sy-mandt         AND
                 rq1name  = 'HRTS'           AND
                 rq2name  = ws_spool_name    AND
                 rqowner  = sy-uname
           ORDER BY rqident DESCENDING
                    rqcretime DESCENDING.
    ENDSELECT.
    IF sy-subrc <> 0.
      g_error = 'X'.
      messtab-msgspra = sy-langu.     " language
      messtab-msgid   = 'YAM_AFRU'.   " class
      messtab-msgnr   = '033'.        " number
      messtab-msgv1   = p_myvar.
      messtab-msgv2   = sy-subrc.
      APPEND messtab. CLEAR messtab.
      EXIT.
    ENDIF.

  ELSE.
    SELECT * UP TO 1 ROWS
      INTO wa_tsp01
      FROM tsp01
      WHERE rqclient = sy-mandt       AND
            rq1name  = 'HRTS'         AND
            rq2name = ws_spool_name   AND
            rqowner  = sy-uname
      ORDER BY rqident DESCENDING
               rqcretime DESCENDING.
    ENDSELECT.
    IF sy-subrc <> 0.
      g_error = 'X'.
      messtab-msgspra = sy-langu.     " language
      messtab-msgid   = 'YAM_AFRU'.   " class
      messtab-msgnr   = '033'.        " number
      messtab-msgv1   = p_myvar.
      messtab-msgv2   = sy-subrc.
      APPEND messtab. CLEAR messtab.
      EXIT.
    ENDIF.
  ENDIF.

  ws_spool_num = wa_tsp01-rqident.
ENDFORM.                    " get_spool_request

*Text symbol text£º
*001:Time Evaluation
*002:Weekly Timesheet
*003:Attachment
*004:Attached is the time-sheet for:
*005:Employee:
*006:Email:
*007:Spoolnr:

*008:Variant DE-TIMESHEETS for RPTEDT00 program does not exist
*Selection text£º
*P_MAIL:        Email address
*P_MYVAR:        Variant for RPTEDT00
*P_SUB:        Subject of email
