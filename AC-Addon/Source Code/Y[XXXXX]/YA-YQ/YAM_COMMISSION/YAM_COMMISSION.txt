*----------------------------------------------------------------------*
* PROGRAM ID           : YAM_COMMISSION                                *
* PROGRAM TITLE        : Commission report                             *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 12/06/2006                                    *
* DEVELOPMENT ID       : BE-CR025                                      *
* CHANGE REQUEST NUMBER: CD1K905781                                    *
* PROGRAM DESCRIPTION  : A report to calculate the commission for      *
*                        salesmen on aftermarket invoices.             *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |2006.08.09|L.Mertens          |CD1K906437|                  *
*           - change sign of values from G2- and S1-documents          *
* MOD-002 |2006.08.11|L.Mertens          |CD1K906442|                  *
* MOD-003 |2006.08.29|L.Mertens          |CD1K906550|                  *
*           - auth. check on company code                              *
* MOD-004 |2006.11.14|L. Mertens         |CD1K907549|BE-CR045          *
*           - add equipment + serial number                            *
* MOD-005 |2006.12.20|L. Mertens         |CD1K908820|BE-CR050          *
*           - add cost + correct error                                 *
* MOD-006 |2014.03.21|Anda Wu            |CD1K980554|CR3193            *
*           - Bugfix - yam_commission order nr not transferring        *
*----------------------------------------------------------------------*
REPORT YAM_COMMISSION no standard page heading
                                      line-count 65
                                      line-size 120.
TYPE-POOLS: SLIS.

TABLES: VBRK,                          "Billing Document: Header Data
        VBRP,                          "Billing Document: Item Data
        KNA1.                          "General Customer data

DATA: begin of gt_head occurs 0,
        vbeln like vbrk-vbeln,
        fkart like vbrk-fkart,
        fkdat like vbrk-fkdat,
        erdat like vbrk-erdat,
        kunrg like vbrk-kunrg,
        waerk like vbrk-waerk,
      end of gt_head.

DATA: begin of gt_item occurs 0,
        vbeln like vbrp-vbeln,
        posnr like vbrp-posnr,
        netwr like vbrp-netwr,
        vkbur like vbrp-vkbur,
        prctr like vbrp-prctr,
        vgbel like vbrp-vgbel,
        vgpos like vbrp-vgpos,
        autyp like vbrp-autyp,
        aufnr like vbrp-aufnr,
        shkzg like vbrp-shkzg,
* begin of insert MOD-002
        vbelv like vbrp-vbelv,
* end of insert MOD-002
* begin of insert MOD-005
        posnv like vbrp-posnv,
        wavwr like vbrp-wavwr,
* end of insert MOD-005
      end of gt_item.

DATA: begin of gt_sortl occurs 0,
        kunnr like kna1-kunnr,
        sortl like kna1-sortl,
        name1 like kna1-name1,
      end of gt_sortl.

DATA: begin of gt_equi occurs 0,
        sdaufnr like viser02-sdaufnr,
        posnr   like viser02-posnr,
        equnr   like equi-equnr,
* begin of insert MOD-004
        sernr   like equi-sernr,
* end of insert MOD-004
      end of gt_equi.

DATA: begin of gt_equi_ord occurs 0,
        aufnr    like vbrp-aufnr,
        equnr    like equi-equnr,
* begin of insert MOD-004
        serialnr like afih-serialnr,
* end of insert MOD-004
      end of gt_equi_ord.

DATA: begin of gt_vkgrp occurs 0,
        equnr like v_equi-equnr,
        tplnr like v_equi-tplnr,
        vkgrp like v_equi-vkgrp,
        adrnr like v_equi-adrnr,
      end of gt_vkgrp.

DATA: begin of gt_addr occurs 0,
        addrnumber like adrc-addrnumber,
        city1      like adrc-city1,
        post_code1 like adrc-post_code1,
      end of gt_addr.

DATA: begin of gt_slspers occurs 0,
        vkgrp like tvgrt-vkgrp,
        bezei like tvgrt-bezei,
      end of gt_slspers.

DATA: begin of gt_out occurs 0,
        vbeln like vbrp-vbeln,
        kunrg like vbrk-kunrg,
        sortl like kna1-sortl,
        fkart like vbrk-fkart,
        fkdat like vbrk-fkdat,
        erdat like vbrk-erdat,
        prctr like vbrp-prctr,
        plc(3) type c,
        gac(4) type c,
        pgc(4) type c,
        netwr like vbrp-netwr,
        tplnr like v_equi-tplnr,
        city1 like adrc-city1,
        post_code1 like adrc-post_code1,
        vkbur like vbrp-vkbur,
        vkgrp like tvgrt-vkgrp,
        slspers like tvgrt-bezei,
        name1 like kna1-name1,
        ctrnr like vbrp-vbeln,
        aufnr like vbrp-aufnr,
* begin of insert MOD-004
        equnr like equi-equnr,
        sernr like equi-sernr,
* end of insert MOD-004
* begin of insert MOD-005
        wavwr like vbrp-wavwr,
* end of insert MOD-005
        selected,
      end of gt_out.

DATA: gt_fieldcat     TYPE slis_t_fieldcat_alv,
      g_events_tab    TYPE slis_t_event,
      g_form_user_command TYPE slis_formname VALUE 'USER_COMMAND_L',
      g_ic1           LIKE sy-ucomm VALUE '&IC1',
      g_repid         like sy-repid,
      g_vbtyp         TYPE vbtyp,
      g_aufnr         type aufnr,
      wa_aufnr        type aufnr,
* begin of insert MOD-005
      g_vv200         type RKE2_VV200,
      g_vv300         type RKE2_VV300,
      g_vv400         type RKE2_VV400,
      g_vv500         type RKE2_VV500,
      g_vv600         type RKE2_VV600,
      g_act_cost      type RKE2_VV600,
      g_vbelv         like vbfa-vbelv,
      g_fkart         like vbrk-fkart,
* end of insert MOD-005
      wa_ctrnr        type vbeln_va,
      g_vgbel         TYPE vgbel,
      g_vgbel2        type vgbel,
      g_vgtyp         type vbtyp_v,
      l_index         TYPE sy-tabix.

CONSTANTS: c_sc1(3)             VALUE 'SC1',
           c_fv like vbrk-fkart value 'FV',   "Contract invoice
           c_g          TYPE c     VALUE 'G', "Contract
           c_l          TYPE c     VALUE 'L', "Debit memo
           c_k          TYPE c     VALUE 'K', "Credit memo
* begin of insert MOD-001
           c_g2         like vbrk-fkart value 'G2', "Credit memo
           c_s1         like vbrk-fkart value 'S1', "Cancellation
* end of insert MOD-001
* begin of insert MOD-005
           c_b(1) type c        value 'B',      "Quotation
           c_0(1) type c        value '0',
           c_02(2) type c       value '02',
           c_l2 like vbrk-fkart value 'L2',     "Debit memo
           c_s2 like vbrk-fkart value 'S2',     "Credit memo cancell.
* end of insert MOD-005
           c_input              VALUE '0',
           c_99991231(8)        value '99991231',
           c_s(1) type c        value 'S',
           c_x(1) type c        value 'X',
           c_e(1) type c        value 'E'.

SELECT-OPTIONS: s_fkart for vbrk-fkart,
                s_vkbur for vbrp-vkbur,
                s_erdat for vbrk-erdat obligatory.

PARAMETERS: p_bukrs type bukrs obligatory memory id BUK.

* begin of insert MOD-003
*.................. Selection screen validations...................... *
AT SELECTION-SCREEN ON p_bukrs.

  AUTHORITY-CHECK OBJECT 'YAM_BUKRS'
           ID 'BUKRS' FIELD p_bukrs.

  IF sy-subrc NE 0.
*.. No commission authorization for company code &1
    MESSAGE e101(yam_sls) WITH p_bukrs.
  ENDIF.
* end of insert MOD-003

*-----------------------------------------------------------------------
START-OF-SELECTION.

* select invoices: header data
  SELECT vbeln fkart fkdat erdat kunrg waerk
        INTO corresponding fields of table gt_head
        FROM VBRK
        WHERE fkart in s_fkart
          AND erdat in s_erdat
          AND bukrs eq p_bukrs.

* select invoice items
  if not gt_head[] is initial.
    SELECT vbeln posnr netwr prctr vkbur vgbel vgpos autyp aufnr shkzg
* begin of insert MOD-002
           vbelv
* end of insert MOD-002
* begin of insert MOD-005
           posnv wavwr
* end of insert MOD-005
          INTO corresponding fields of table gt_item
          FROM VBRP
          FOR all entries in gt_head
          WHERE vbeln eq gt_head-vbeln
            AND vkbur in s_vkbur.

*   preselect BPCS customer nr.
    SELECT kunnr sortl name1
          INTO corresponding fields of table gt_sortl
          FROM KNA1
          FOR ALL entries in gt_head
          WHERE kunnr = gt_head-kunrg.

    delete adjacent duplicates from gt_sortl comparing kunnr.

* begin of insert MOD-005
    loop at gt_item.
      if gt_item-vbelv is initial.
        SELECT SINGLE vgbel vgpos
            INTO (gt_item-vbelv, gt_item-posnv)
            FROM VBAP WHERE vbeln = gt_item-vgbel
                        AND vgpos = gt_item-vgpos.
        if sy-subrc = 0.
          modify gt_item transporting vbelv posnv.
        endif.
      endif.
    endloop.
* end of insert MOD-005

*   preselect equipment sales persons for contracts
    SELECT sdaufnr posnr equnr
* begin of insert MOD-004
           sernr
* end of insert MOD-004
          INTO corresponding fields of table gt_equi
          FROM VISER02
          FOR ALL entries in gt_item
* begin of change MOD-002
*         WHERE sdaufnr = gt_item-vgbel      "preceding document
          WHERE sdaufnr = gt_item-vbelv      "originating document
* end of change MOD-002
* begin of change MOD-005
*           AND posnr   = gt_item-vgpos.
            AND posnr   = gt_item-posnv.
* end of change MOD-005

*   preselect equipment sales persons for orders
    SELECT aufnr equnr
* begin of insert MOD-004
           serialnr
* end of insert MOD-004
          INTO corresponding fields of table gt_equi_ord
          FROM AFIH
          FOR ALL entries in gt_item
          WHERE aufnr = gt_item-aufnr.

    delete adjacent duplicates from gt_equi_ord comparing aufnr.

    if not gt_equi[] is initial.
      SELECT equnr vkgrp tplnr adrnr
            INTO corresponding fields of table gt_vkgrp
            FROM V_EQUI
            FOR ALL entries in gt_equi
            WHERE equnr = gt_equi-equnr
              AND datbi = c_99991231.
    endif.

    if not gt_equi_ord[] is initial.
      SELECT equnr vkgrp tplnr adrnr
            APPENDING corresponding fields of table gt_vkgrp
            FROM V_EQUI
            FOR ALL entries in gt_equi_ord
            WHERE equnr = gt_equi_ord-equnr
              AND datbi = c_99991231.
    endif.

    SELECT addrnumber city1 post_code1
          INTO corresponding fields of table gt_addr
          FROM ADRC
          FOR ALL entries in gt_vkgrp
          WHERE addrnumber = gt_vkgrp-adrnr.

    delete adjacent duplicates from gt_addr comparing addrnumber.

    SELECT vkgrp bezei
          INTO corresponding fields of table gt_slspers
          FROM TVGRT
          FOR ALL entries in gt_vkgrp
          WHERE spras = c_e
            AND vkgrp = gt_vkgrp-vkgrp.

    delete adjacent duplicates from gt_slspers comparing vkgrp.
  endif.

*-----------------------------------------------------------------------
END-OF-SELECTION.

  if gt_item[] is initial.
    skip 3.
    write: /15 'No Invoices selected'(015).
    stop.
  endif.

  sort gt_item by vbeln posnr.
  sort gt_head by vbeln.
  sort gt_sortl by kunnr.
  sort gt_equi by sdaufnr posnr.
  sort gt_equi_ord by aufnr.
  sort gt_vkgrp by equnr.
  sort gt_addr by addrnumber.
  sort gt_slspers by vkgrp.

* test on type of contract
  loop at gt_item.

    clear gt_head.
    read table gt_head with key vbeln = gt_item-vbeln
                         binary search.

*.. Check if the line item is generated from the Contract
    clear: wa_ctrnr, wa_aufnr.
    IF gt_item-autyp  = c_g.
      clear: g_vbtyp, g_aufnr.
      SELECT SINGLE vbtyp aufnr into (g_vbtyp, g_aufnr)
       FROM VBAK WHERE vbeln = gt_item-vgbel.

      if g_vbtyp = c_g.
        wa_ctrnr = gt_item-vgbel.
      else.
        wa_aufnr = g_aufnr.
      endif.

    elseif gt_item-autyp  = c_k.
      clear: g_vgbel.
      SELECT SINGLE vgbel into g_vgbel
       FROM VBAK WHERE vbeln = gt_item-vgbel.

      if sy-subrc = 0.
        clear: g_vgbel2, g_vgtyp.
        SELECT SINGLE vgbel vgtyp INTO (g_vgbel2, g_vgtyp)
         FROM vbrp WHERE vbeln = g_vgbel
* begin of change MOD-005
*                    AND posnr = gt_item-posnr.
                     AND posnr = gt_item-vgpos.
* end of change MOD-005

        if sy-subrc = 0.
          if g_vgtyp = c_g.
            wa_ctrnr = g_vgbel2.
          elseif g_vgtyp = c_l.
            if NOT gt_item-aufnr IS INITIAL.
              wa_aufnr = gt_item-aufnr.
            else.
              clear: g_aufnr.
              SELECT SINGLE aufnr into g_aufnr
               FROM VBAK WHERE vbeln = g_vgbel2.

              if sy-subrc = 0.
                if not g_aufnr is initial.
                  wa_aufnr = g_aufnr.
                endif.
              endif.
            endif.
          endif.
        else.
          clear: g_vbtyp.
          SELECT SINGLE vbtyp INTO (g_vbtyp)
           FROM vbak WHERE vbeln = g_vgbel.

          if sy-subrc = 0.
            if g_vbtyp = c_g.
              wa_ctrnr = g_vgbel.
            endif.
          endif.
        endif.
      endif.

    elseif gt_item-autyp  = c_l.
      if NOT gt_item-aufnr is initial.
        wa_aufnr = gt_item-aufnr.
      else.
        clear: g_vgbel.
        SELECT SINGLE vgbel into g_vgbel
         FROM VBAK WHERE vbeln = gt_item-vgbel.

        if sy-subrc = 0.
          clear: g_vbtyp.
          SELECT SINGLE vbtyp INTO (g_vbtyp)
           FROM vbak WHERE vbeln = g_vgbel.

          if sy-subrc = 0.
            if g_vbtyp = c_g.
              wa_ctrnr = g_vgbel.
            endif.
          endif.
        endif.
      endif.
    ENDIF.

* begin of insert MOD-005 : add actual cost; for non-contractinvoices
* this must come from service order in CO/PA (FP: check quotation)
*   if invoice contract --> cost = 0
    if gt_head-fkart = c_fv.

      clear gt_item-wavwr.

    elseif gt_head-fkart = c_s1 or
           gt_head-fkart = c_s2.

*     read document flow to check type of invoice
      select vbelv into g_vbelv
          from vbfa up to 1 rows
          where vbeln   = gt_item-vbeln
          and   vbtyp_n in ('N', 'S')       "Inv/Cr.memo cancellation
          and   vbtyp_v in ('M', 'P', 'K'). "Inv/Debmemo/Cr.memo request
      endselect.

      if sy-subrc = 0.
        select single fkart into g_fkart
            from vbrk
            where vbeln = g_vbelv.

        if g_fkart = c_fv.
          clear gt_item-wavwr.
        else.
          gt_item-wavwr = gt_item-wavwr * -1.
        endif.
      endif.
    elseif gt_head-fkart = c_g2.

      gt_item-wavwr = gt_item-wavwr * -1.

    elseif gt_head-fkart = c_l2.

*.... for Fixed Price orders: take actual cost from S.O. in CO/PA via
*.... document flow
      select vbelv into g_vbelv
          from vbfa
          where vbeln   = gt_item-vbeln
          and   posnn   = gt_item-posnr
          and   vbtyp_v = c_b.                   "Quotation
      endselect.

      if sy-subrc = 0.

*...... read sales document to get service ordernr.
        select single aufnr into g_aufnr
              from vbak
              where vbeln = g_vbelv.

          if sy-subrc = 0.
            clear g_act_cost.
            select vv200 vv300 vv400 vv500 vv600
                 into (g_vv200, g_vv300, g_vv400, g_vv500, g_vv600)
                 from ce11000
                 where paledger = c_02
                   and vrgar    = c_0
                   and rkaufnr  = g_aufnr.

              g_act_cost = g_act_cost + g_vv200 + g_vv300 + g_vv400
                         + g_vv500 + g_vv600.
            endselect.

            if sy-subrc = 0.
              gt_item-wavwr = g_act_cost.
            endif.
          endif.

      endif.
    endif.
* end of insert MOD-005

    clear gt_sortl.
    read table gt_sortl with key kunnr = gt_head-kunrg
                          binary search.

    clear gt_equi.
* begin of change MOD-002
*   read table gt_equi  with key sdaufnr = gt_item-vgbel
    read table gt_equi  with key sdaufnr = gt_item-vbelv
* end of change MOD-002
* begin of change MOD-005
*                                posnr   = gt_item-vgpos
                                 posnr   = gt_item-posnv
* end of change MOD-005
                          binary search.

    if sy-subrc = 0.
* begin of insert MOD-004
      move gt_equi-equnr to gt_out-equnr.
      move gt_equi-sernr to gt_out-sernr.
* end of insert MOD-004
      clear gt_vkgrp.
      read table gt_vkgrp with key equnr = gt_equi-equnr
                            binary search.
    else.
      clear gt_equi_ord.
      read table gt_equi_ord  with key aufnr = gt_item-aufnr
                              binary search.

      if sy-subrc = 0.
* begin of insert MOD-004
        move gt_equi_ord-equnr    to gt_out-equnr.
        move gt_equi_ord-serialnr to gt_out-sernr.
* end of insert MOD-004
        clear gt_vkgrp.
        read table gt_vkgrp with key equnr = gt_equi_ord-equnr
                              binary search.
      endif.
    endif.

    clear gt_addr.
    read table gt_addr with key addrnumber = gt_vkgrp-adrnr
                          binary search.

    clear gt_slspers.
    read table gt_slspers with key vkgrp = gt_vkgrp-vkgrp
                            binary search.

*   fill structure output
    move-corresponding gt_item to gt_out.
    move-corresponding gt_head to gt_out.
    move-corresponding gt_sortl to gt_out.
    move gt_vkgrp-tplnr to gt_out-tplnr.
    move-corresponding gt_addr to gt_out.
    move gt_slspers-vkgrp to gt_out-vkgrp.
    move gt_slspers-bezei to gt_out-slspers.
    move wa_aufnr to gt_out-aufnr.
    move wa_ctrnr to gt_out-ctrnr.

    clear gt_out-plc.
    case gt_item-prctr+1(1).
      when 1.
        move 'AIF'  to gt_out-plc.
      when 2.
        move 'AII'  to gt_out-plc.
      when 3.
        move 'AIP'  to gt_out-plc.
      when 4.
        move 'GAP'  to gt_out-plc.
    endcase.

    write gt_item-prctr+2(4)  to gt_out-gac no-zero.
    write gt_item-prctr+6(4)  to gt_out-pgc no-zero.

*.. begin of change MOD-001
*   if gt_item-shkzg = 'X'.
    if gt_out-fkart = c_g2 or
       gt_out-fkart = c_s1.
*.. end of change MOD-001
      gt_out-netwr = gt_out-netwr * -1.
    endif.

    append gt_out.
    clear gt_out.

  endloop.

* create ALV-GRID
  PERFORM build_field_catlog CHANGING gt_fieldcat.
  PERFORM fill_events_f14.
  PERFORM alv_display.

*-----------------------------------------------------------------------
TOP-OF-PAGE.


*&---------------------------------------------------------------------*
*&      Form  build_field_catlog
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      <--P_GT_FIELDCAT  text
*----------------------------------------------------------------------*
FORM build_field_catlog  CHANGING pt_fieldcat TYPE slis_t_fieldcat_alv.

  DATA : ls_fcat TYPE slis_fieldcat_alv.
*--------------------------Invoice Nr-----------------------*
  ls_fcat-fieldname = 'VBELN'.
  ls_fcat-rollname = 'VBELN_VF'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------SAP Customer Nr------------------*
  ls_fcat-fieldname = 'KUNRG'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'SAP Customer Nr'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Customer Name 1------------------*
  ls_fcat-fieldname = 'NAME1'.
  ls_fcat-outputlen = '14'.
  ls_fcat-seltext_l = 'Customer name1'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------BPCS Customer Nr-----------------*
  ls_fcat-fieldname = 'SORTL'.
  ls_fcat-outputlen = '16'.
  ls_fcat-seltext_l = 'BPCS Customer Nr'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Billing Type---------------------*
  ls_fcat-fieldname = 'FKART'.
  ls_fcat-rollname = 'FKART'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Contract number------------------*
  ls_fcat-fieldname = 'CTRNR'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Contract number'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------S.Order number------------------*
  ls_fcat-fieldname = 'AUFNR'.
  ls_fcat-rollname = 'AUFNR'.
*****MOD-006  Begin*******ADD*******************************
  ls_fcat-ref_fieldname =  'AUFNR'.
  ls_fcat-ref_tabname   =  'VBRP'.
*****MOD-006  End  *******ADD*******************************
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Billing Date---------------------*
  ls_fcat-fieldname = 'FKDAT'.
  ls_fcat-rollname = 'FKDAT'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Creation Date---------------------*
  ls_fcat-fieldname = 'ERDAT'.
  ls_fcat-rollname = 'ERDAT'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-------------------Profitcenter------------------------------*
  ls_fcat-fieldname = 'PRCTR'.
  ls_fcat-rollname = 'PRCTR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------PLC--------------------------------*
  ls_fcat-fieldname = 'PLC'.
  ls_fcat-rollname = 'RKEG_WW002'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------GAC--------------------------------*
  ls_fcat-fieldname = 'GAC'.
  ls_fcat-rollname = 'RKEG_WW006'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------PGC--------------------------------*
  ls_fcat-fieldname = 'PGC'.
  ls_fcat-rollname = 'RKEG_WW007'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------SALES Value------------------------*
  ls_fcat-fieldname = 'NETWR'.
  ls_fcat-rollname = 'NETWR_FP'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Funct. location---------------------*
  ls_fcat-fieldname = 'TPLNR'.
  ls_fcat-rollname = 'TPLNR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Funct. location City----------------*
  ls_fcat-fieldname = 'CITY1'.
  ls_fcat-rollname = 'AD_CITY1'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Funct. location Postal code---------*
  ls_fcat-fieldname = 'POST_CODE1'.
  ls_fcat-rollname = 'AD_PSTCD1'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Equipment sales group-------------*
  ls_fcat-fieldname = 'VKGRP'.
  ls_fcat-rollname = 'VKGRP'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Equipment sales person------------*
  ls_fcat-fieldname = 'SLSPERS'.
  ls_fcat-outputlen = '20'.
  ls_fcat-seltext_l = 'Equip.sales_person'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
* begin of insert MOD-004
*--------------------------Equipment number------------------*
  ls_fcat-fieldname = 'EQUNR'.
  ls_fcat-rollname = 'EQUNR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Serial number---------------------*
  ls_fcat-fieldname = 'SERNR'.
  ls_fcat-rollname = 'GERNR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
* end of insert MOD-004
* begin of insert MOD-005
*--------------------------Cost value---------------------*
  ls_fcat-fieldname = 'WAVWR'.
  ls_fcat-rollname = 'WAVWR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
* end of insert MOD-005

ENDFORM.                    " build_field_catlog

*&---------------------------------------------------------------------*
*&      Form  fill_events_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM fill_events_f14 .

  DATA h_event       TYPE slis_alv_event.

  CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
    EXPORTING
      i_list_type = 0
    IMPORTING
      et_events   = g_events_tab.

*--- allocate form for user-command ---------------------------------*
  READ TABLE g_events_tab WITH KEY name = slis_ev_user_command
                        INTO h_event.
  IF sy-subrc = 0.
    MOVE g_form_user_command TO h_event-form.
    MODIFY g_events_tab FROM h_event INDEX sy-tabix.
  ENDIF.

ENDFORM.                    " fill_events_f14

*&--------------------------------------------------------------------*
*&      Form  user_command_l
*&--------------------------------------------------------------------*
*       text
*---------------------------------------------------------------------*
*      -->P_UCOMM    text
*      -->P_SELFIELD text
*---------------------------------------------------------------------*
FORM user_command_l USING p_ucomm LIKE sy-ucomm
                          p_selfield TYPE slis_selfield.

  p_selfield-refresh = c_s.
  PERFORM check_pf2_with_object_f16 USING p_ucomm.
  PERFORM set_p_selfield_general_f16 USING p_selfield.

  CASE p_ucomm.
    WHEN 'ISEL'.
      p_ucomm = 'DISP'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
  ENDCASE.

ENDFORM.                    "user_command_l

*&---------------------------------------------------------------------*
*&      Form  check_pf2_with_object_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*----------------------------------------------------------------------*
FORM check_pf2_with_object_f16  USING    p_ucomm.

  CHECK p_ucomm = g_ic1.
  p_ucomm = 'ISEL'.

ENDFORM.                    " check_pf2_with_object_f16

*&---------------------------------------------------------------------*
*&      Form  set_p_selfield_general_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM set_p_selfield_general_f16  USING f_selfield TYPE slis_selfield.

  f_selfield-col_stable = c_x.
  f_selfield-row_stable = c_x.

ENDFORM.                    " set_p_selfield_general_f16

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*      -->P_ENDCASE  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_f16  USING p_ucomm LIKE sy-ucomm
                                p_selfield TYPE slis_selfield.

  PERFORM check_object_tab_marked_f14 USING p_ucomm p_selfield.

  LOOP AT gt_out WHERE selected = c_x .
    l_index = sy-tabix.
    PERFORM fcodes_with_mark_l USING p_ucomm p_selfield.
    gt_out-selected = ' '.
    MODIFY gt_out INDEX l_index.
  ENDLOOP.

  CLEAR p_ucomm.

ENDFORM.                    " fcodes_with_mark_f16

*&---------------------------------------------------------------------*
*&      Form  check_object_tab_marked_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM check_object_tab_marked_f14  USING    p_ucomm LIKE sy-ucomm
                                        p_selfield TYPE slis_selfield.

  READ TABLE gt_out WITH KEY selected = c_x.

  IF NOT sy-subrc IS INITIAL.
    IF NOT p_selfield-tabindex IS INITIAL.
      READ TABLE gt_out  INDEX p_selfield-tabindex.
      gt_out-selected = c_x.
      MODIFY gt_out  INDEX p_selfield-tabindex.
    ENDIF.
  ELSE.
*--- Checkbox markiert -----------------------------------------------*
    p_selfield-sel_tab_field = 'G_MARK'.
  ENDIF.

ENDFORM.                    " check_object_tab_marked_f14

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_l
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_F_UCOMM  text
*      -->P_F_SELFIELD  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_l  USING   p_ucomm LIKE sy-ucomm
                              p_selfield TYPE slis_selfield.

  DATA: h_ucomm LIKE sy-ucomm.

  CASE p_ucomm.
*   Display billing document
    WHEN 'DISP'.
      SET PARAMETER ID 'VF ' FIELD  gt_out-vbeln.
      CALL TRANSACTION 'VF03' AND SKIP FIRST SCREEN.
  ENDCASE.

ENDFORM.                    " fcodes_with_mark_l

*&---------------------------------------------------------------------*
*&      Form  alv_display
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM alv_display .

  g_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
     EXPORTING
       i_callback_program                =  g_repid
       i_save                            = 'A'
       it_events                         =  g_events_tab[]
*      I_GRID_TITLE                      =
*      I_GRID_SETTINGS                   =
*      is_layout                         =  g_layout
       it_fieldcat                       =  gt_fieldcat[]
     TABLES
        t_outtab                         =  gt_out.

ENDFORM.                    " alv_display

*Text symbol text£º
*001:Invoice_No
*002:SAP_Customer_No
*003:Inv_Date
*004:Sales_Val_excl_VAT
*005:Cost_Value
*006:User:
*007:BPCS_Customer_No
*008:Created_on
*010:From :
*011:To
*012:'X' = LIST w/o titles+totals, '  ' = NORMAL LIST
*015:No Invoices selected
*030:Order Number Total
*040:Acc.ind. Total
*050:Grand Total
*E01:Period/year From must be less than or equal To

*E03:Open dataset failed for :
*Selection text£º
*P_BUKRS:D       Company Code
*S_ERDAT:D       Created on
*S_FKART:D       Billing Type
*S_VKBUR:D       Sales office
