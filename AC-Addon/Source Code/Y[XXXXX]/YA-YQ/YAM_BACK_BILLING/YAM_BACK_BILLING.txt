*&---------------------------------------------------------------------*
*& Report  YAM_BACK_BILLING                                     *
*&                                                                     *
*&---------------------------------------------------------------------*
*& This report will allow the creation of debit/credit memo request    *
*& for contract items that have been flagged as Ready for Back-billing.*
*&---------------------------------------------------------------------*

REPORT  yam_back_billing MESSAGE-ID yam_sls                               .
*.................. Variable definitions.............................. *
TYPE-POOLS: slis.

TYPES: BEGIN OF ty_vbmtv.
        INCLUDE STRUCTURE yam_vbmtv.
TYPES:  exclu TYPE c,
       END   OF ty_vbmtv.

*.. Global variables
DATA: gs_bb_def TYPE yam_bb_def,
      gv_msg    TYPE c,
      gv_handle TYPE balloghndl,
      gv_log    TYPE c.

DATA: gt_vbmtv     TYPE STANDARD TABLE OF ty_vbmtv WITH HEADER LINE.
DATA: gt_vbmtv_loc TYPE STANDARD TABLE OF yam_vbmtv WITH HEADER LINE.

*.. Global constants
CONSTANTS: gc_charx    TYPE c VALUE 'X',
           gc_green(3) TYPE c VALUE 'C50',
           gc_red(3)   TYPE c VALUE 'C60',
           gc_orange(3) TYPE c VALUE 'C70'.

*.................. Layout selection screen........................... *
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-b01.
PARAMETER: p_vkorg TYPE vkorg OBLIGATORY MEMORY ID vko,
           p_vtweg TYPE vtweg OBLIGATORY MEMORY ID vtw,
           p_spart TYPE spart OBLIGATORY MEMORY ID spa.
SELECTION-SCREEN END   OF BLOCK b1.

SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-b02.
PARAMETER: p_auart TYPE vbak-auart,
           p_kunnr TYPE kunnr,
           p_vbeln TYPE vbak-vbeln matchcode object VMVA.
SELECTION-SCREEN END   OF BLOCK b2.

*.................. Selection screen validations...................... *
AT SELECTION-SCREEN ON BLOCK b1.
  AUTHORITY-CHECK OBJECT 'YAM_BB_VKO'
           ID 'VKORG' FIELD p_vkorg
           ID 'VTWEG' FIELD p_vtweg
           ID 'SPART' FIELD p_spart
           ID 'ACTVT' FIELD '16'.
  IF sy-subrc NE 0.
*.... No back-billing authorization for sales area &1 &2 &3
    MESSAGE e002 WITH p_vkorg p_vtweg p_spart.
  ELSE.
*.... SD document defaults for Back-billing process in the selected sales area
    SELECT SINGLE *
    INTO gs_bb_def
    FROM yam_bb_def
    WHERE vkorg EQ p_vkorg
      AND vtweg EQ p_vtweg
      AND spart EQ p_spart.
    IF sy-subrc NE 0.
*....... No Back-billing defaults maintained for Sales area &1 &2 &3. Please check
      MESSAGE e001 WITH p_vkorg p_vtweg p_spart.
    ENDIF.
  ENDIF.

*.................. Include programs.................................. *
  INCLUDE yam_back_billing_f01.

  INCLUDE yam_back_billing_f02.

  INCLUDE yam_back_billing_f03.

*.................. Main program...................................... *
START-OF-SELECTION.
*.. Ensure only one user at the time can access the back-billing data for a given sales area
  CALL FUNCTION 'ENQUEUE_EYAM_BACKBILL'
    EXPORTING
      mode_yam_backbill_lock = 'E'
      mandt                  = sy-mandt
      vkorg                  = p_vkorg
      vtweg                  = p_vtweg
      spart                  = p_spart
    EXCEPTIONS
      foreign_lock           = 1
      system_failure         = 2
      OTHERS                 = 3.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

*.. Initialization
  PERFORM initialization.

*.. Select the relevant contract items
  PERFORM select_contracts.

*.. Perform the debit/credit value calculations
  PERFORM calculate_dc_value.

*.. Output the ALV list
  PERFORM output_list.

*.. Save application log
  perform save_application_log.

*Text symbol text£º
*B01:Organizational data
*B02:Additional filtering
*H00:Back-billing
*H01:Selected Sales Area:
*H02:Credits allowed:
*H03:BB on current value:
*H04:Applied Percentage:
*P01:Create billing requests?
*P02:You are about to create billing requests for the selected contract items. Are you sure you want to continue?
*P03:Reject contract items?
*P04:You are about to reject the selected contract items. Are you sure you want to continue?

*T00:Usage:
*Selection text£º
*P_AUART:D       Sales document type
*P_KUNNR:        Sold-to party
*P_SPART:D       Division
*P_VBELN:D       Sales document
*P_VKORG:D       Sales Organization
*P_VTWEG:D       Distribution Channel
