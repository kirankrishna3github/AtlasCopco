*----------------------------------------------------------------------
* PROGRAM ID           : YAM_BACKLOG                                   *
* PROGRAM TITLE        : AM: Backlog report                            *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 21/11/2007                                    *
* DEVELOPMENT ID       : ALL-CR 375                                    *
* CHANGE REQUEST NUMBER: CD1K904896                                    *
* PROGRAM DESCRIPTION  : Create report for backlog                     *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*   1    |28.10.2008 |EXTPKA    |CD1K944028       |Adition of new field*
*----------------------------------------------------------------------*
* MOD-002 |16.02.2009|L. Mertens USG Innotiv|CD1K946   |CR0657         *
*      - change selection criteria                                     *
*----------------------------------------------------------------------*
* MOD-003 |05/08/2009|M.Jacobs  |CD1K949579 |CR0889                    *
*----------------------------------------------------------------------*
* MOD-004 |22/09/2009|Uzzawal V |CD1K950487 |CR0660                    *
*----------------------------------------------------------------------*
* MOD-005 |09/03/2010|Satyabrata Basu |CD1K954951 |T#7317              *
*      - Export data into Excel Spreadsheet                            *
*----------------------------------------------------------------------*
* MOD-006 |21/02/2014|Praveen   |CD1K980214 |CR3109                    *
*----------------------------------------------------------------------*

REPORT yam_backlog      NO STANDARD PAGE HEADING
                        LINE-SIZE 200.

*----------------------------------------------------------------------*
* type-pool ALV                                                        *
*----------------------------------------------------------------------*
TYPE-POOLS: slis.

*----------------------------------------------------------------------*
* DB tables                                                            *
*----------------------------------------------------------------------*
TABLES: t370a,                    " activity type of a transaction
        yam_backlog,              " Display structure reporting for QMEL
        yam_backlog_xxl,          "+Mod-005 - Satya (Excel download)
        crhd,                     " work centers
        kna1.                     " Customer Master
* begin of insertion MOD-003
TABLES : viqmelst.
* end of insertion MOD-003

*----------------------------------------------------------------------*
* global data                                                          *
*----------------------------------------------------------------------*
* display structure
DATA: BEGIN OF object_tab OCCURS 0.
*        INCLUDE STRUCTURE yam_backlog.                   "-Mod-005
        INCLUDE STRUCTURE yam_backlog_xxl.                "+Mod-005
DATA:   bukrs    LIKE viqmelst-bukrs,
        vkorg    LIKE viqmelst-vkorg,
        vtweg    LIKE viqmelst-vtweg,
        spart    LIKE viqmelst-spart.
DATA:   selected,
        pm_selected TYPE pm_selected.
DATA: END OF object_tab.

* selected notifications
DATA: BEGIN OF gt_not OCCURS 0,
        qmnum    LIKE viqmelst-qmnum,
        aufnr    LIKE viqmelst-aufnr,
        arbpl    LIKE viqmelst-arbpl,
        swerk    LIKE viqmelst-swerk,
        equnr    LIKE viqmelst-equnr,
        serialnr LIKE viqmelst-serialnr,
        strmn    LIKE viqmelst-strmn,
        qmart    LIKE viqmelst-qmart,
        qmtxt    LIKE viqmelst-qmtxt,
        kunum    LIKE viqmelst-kunum,
        name1    LIKE kna1-name1,
        tplnr    LIKE viqmelst-tplnr,
        wapos    LIKE viqmelst-wapos,
        arbei2   LIKE plpo-arbei,
        gstrs    LIKE afko-gstrs,
        eqktx    LIKE eqkt-eqktx,
        aufpl    LIKE afko-aufpl,
        arbei    LIKE afvv-arbei,
        arbeh    TYPE arbeite,"MOD-006++
        parts    LIKE yam_backlog-matcost,
        labour   LIKE yam_backlog-labourcost,
        others   LIKE yam_backlog-othercost,
        total    LIKE yam_backlog-totcost,
        waers    LIKE yam_backlog_xxl-waers,  "Currency Key    "+Mod-005
        bukrs    LIKE viqmelst-bukrs,
        vkorg    LIKE viqmelst-vkorg,
        vtweg    LIKE viqmelst-vtweg,
        spart    LIKE viqmelst-spart,
        adrnr_iloa    LIKE viqmelst-adrnr,
        list_name LIKE kna1-name1,
        arbpl_ord LIKE yam_backlog-arbpl_ord,
        zzcom     LIKE aufk-zzcom ,          "MOD-004
      END OF gt_not.

DATA: BEGIN OF gt_plas OCCURS 0,
        plnty     LIKE plas-plnty,
        plnnr     LIKE plas-plnnr,
        plnal     LIKE plas-plnal,
        plnkn     LIKE plas-plnkn,
      END OF gt_plas.

* work center of the order
DATA: BEGIN OF arbid_tab_n OCCURS 0,
        arbid LIKE crhd-objid,
        arbpl LIKE crhd-arbpl.
DATA: END OF arbid_tab_n.

DATA: wa_obj  LIKE object_tab,
      wa_not  LIKE gt_not.

* not used, needed for includes miolxtop, miolxf14, mioxf16
DATA: sel_tab LIKE yam_backlog OCCURS 0 WITH HEADER LINE.

DATA: go_on           VALUE '1',                            " 0 or 1
      gt_crhd         LIKE crhd OCCURS 0,
      gt_fieldcat     TYPE slis_t_fieldcat_alv,
      gs_crid         TYPE crid,
      gt_crid         TYPE crid_tab,
      gv_ustat(4)     TYPE c,
      gv_inact,
      gv_plnty        LIKE mpos-plnty,
      gv_plnnr        LIKE mpos-plnnr,
      gv_plnal        LIKE mpos-plnal,
      gv_arbei2       LIKE plpo-arbei,
      wa_order        TYPE bapi_alm_order_header_e-orderid.

DATA: i_costs_sum      TYPE bapi_alm_order_costs_sum_et,
      ls_costs_sum     TYPE bapi_alm_order_costs_sum_e.
DATA: i_costs_details  TYPE bapi_alm_order_costs_detail_et,
      ls_costs_details TYPE bapi_alm_order_costs_detail_e.

DATA: lt_messages     TYPE bal_t_msg.
DATA: ls_messages     TYPE bal_s_msg.

DATA g_adrnr_sel_tab LIKE addr1_sel OCCURS 50 WITH HEADER LINE.
DATA g_adrnr_val_tab LIKE addr1_val OCCURS 50 WITH HEADER LINE.

DATA: BEGIN OF tmp_equi  OCCURS 0,
          equnr    LIKE viqmelst-equnr,
      END OF tmp_equi,
      BEGIN OF equi_text OCCURS 0,
          equnr    LIKE viqmelst-equnr,
          eqktx    LIKE eqkt-eqktx,
      END OF equi_text.

*Begin of Insert.
DATA: gv_aufpl TYPE co_aufpl,
      gv_plnt TYPE cr_objty,
      gv_steus TYPE steus,
      gv_arbid TYPE cr_objid,
      gv_werks TYPE werks_d.
TYPES:BEGIN OF ty_afvc,
      aufpl TYPE co_aufpl,
      vornr TYPE vornr,
      arbid TYPE cr_objid,
      werks TYPE werks_d,
      objnr TYPE j_objnr,
      END   OF ty_afvc.
TYPES:BEGIN OF ty_jest .
        INCLUDE STRUCTURE jest.
TYPES:END OF ty_jest.
DATA:it_afvc TYPE STANDARD TABLE OF ty_afvc,
     it_jest TYPE STANDARD TABLE OF ty_jest,
     wa_jest TYPE ty_jest,
     wa_afvc TYPE ty_afvc.
*End   of Insert.
* begin of insertion MOD-003
DATA: g_directory(25) TYPE c VALUE '/var/load/xxx/UK/read/',
      g_ofile        LIKE /sapdmc/lsoinp-filename,
      p_logsys       LIKE tbdlst-logsys.
DATA: BEGIN OF object_tab2 OCCURS 0.
DATA : qmnum          TYPE qmnum,
       swerk          TYPE swerk,
       equnr          TYPE equnr,
       qmart          TYPE qmart,
       qmtxt          TYPE qmtxt,
       strmn(8)       TYPE c,
       aufnr          TYPE aufnr,
       arbpl          TYPE arbpl,
       eqktx          TYPE ktx01,
       serialnr       TYPE gernr,
       gstrs(8)       TYPE c,
       mandt(3)       TYPE c,
       arbei(10)      TYPE c,
       matcost(15)    TYPE c,
       labourcost(15) TYPE c,
       othercost(15)  TYPE c,
       totcost(15)    TYPE c,
       kunum          TYPE qkunum,
       tplnr          TYPE tplnr,
       arbei2(10)     TYPE c,
       name1          TYPE name1_gp,
       list_name      TYPE i_name_lst,
       arbpl_ord      TYPE arbpl,
       bukrs          LIKE viqmelst-bukrs,
       vkorg          LIKE viqmelst-vkorg,
       vtweg          LIKE viqmelst-vtweg,
       spart          LIKE viqmelst-spart,
       zzcom          LIKE aufk-zzcom.       "MOD-004
DATA: END OF object_tab2.
CONSTANTS : c_name(20) TYPE c VALUE 'YAM_BACKLOG',
            c_user TYPE uname VALUE 'BATCHUSER'.
* end of insertion MOD-003

*----------------------------------------------------------------------*
* constants                                                            *
*----------------------------------------------------------------------*
CONSTANTS: c_a        TYPE cr_objty VALUE 'A'.

*----------------------------------------------------------------------*
* ranges                                                               *
*----------------------------------------------------------------------*
RANGES: arbid_n FOR viqmelst-arbpl.  " id of work center(notif)
RANGES: gr_date FOR sy-datum.


*- SELECTION SCREEN---------------------------------------------------
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.
PARAMETERS:    p_swerk    TYPE swerk OBLIGATORY MEMORY ID wrk.
* begin of insertion MOD-003
SELECT-OPTIONS s_vkorg    FOR  viqmelst-vkorg.
* end of insertion MOD-003
SELECT-OPTIONS s_arbpl    FOR  yam_backlog-arbpl.
SELECT-OPTIONS s_equnr    FOR  yam_backlog-equnr.
SELECT-OPTIONS s_kunum    FOR  yam_backlog-kunum.
SELECT-OPTIONS s_tplnr    FOR  yam_backlog-tplnr.
PARAMETERS:    p_strmn    TYPE strmn OBLIGATORY DEFAULT sy-datum.
SELECTION-SCREEN END OF BLOCK b1.

* list variant
SELECTION-SCREEN BEGIN OF BLOCK b3 WITH FRAME TITLE text-003.
PARAMETERS: variant LIKE disvariant-variant.
SELECTION-SCREEN END OF BLOCK b3.

* Comment
SELECTION-SCREEN BEGIN OF BLOCK b4 WITH FRAME TITLE text-002.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(72) text-c01.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(72) text-c02.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(72) text-c03.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN BEGIN OF LINE.
SELECTION-SCREEN COMMENT 1(72) text-c04.
SELECTION-SCREEN END OF LINE.
SELECTION-SCREEN END OF BLOCK b4.

PARAMETERS:
  dy_selm DEFAULT '0' NO-DISPLAY,      " selection mode
  dy_tcode LIKE sy-tcode NO-DISPLAY,   " transaction
  p_ocall NO-DISPLAY.                  " 'X' => prog called from
" pm order

*----------------------------------------------------------------------*
* includes                                                             *
*----------------------------------------------------------------------*
INCLUDE miolxtop.
INCLUDE miolxf14.
INCLUDE miolxf16.

*----------------------------------------------------------------------*
* initialization                                                       *
*----------------------------------------------------------------------*
INITIALIZATION.

* get aktype (display or change mode)
  PERFORM get_acttype_afih_l.

* get variant for selection screen
  PERFORM variant_start_f16.

* initializes g_variant
  PERFORM variant_init_f14 USING '    ' '    ' '    '.

* fill variant with the default variant
  IF variant IS INITIAL.
    PERFORM get_default_variant_f14 USING variant.
  ENDIF.

  CALL METHOD ycl_statistics=>record_transaction
    .

* fill g_fieldcat_tab
* PERFORM create_fieldcat_f14 USING 'YAM_CONF_HR'.

*----------------------------------------------------------------------*
* F4 help for list variant                                             *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON VALUE-REQUEST FOR variant.

  PERFORM variant_inputhelp_f14 USING variant.

*----------------------------------------------------------------------*
* at selection-screen on plant                                         *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN ON p_swerk.

  AUTHORITY-CHECK OBJECT 'I_IWERK'
           ID 'TCD'   FIELD sy-tcode
           ID 'IWERK' FIELD p_swerk.

  IF sy-subrc NE 0.
*.... No authorization for plant
    MESSAGE e001(00) WITH text-e03 p_swerk.
  ENDIF.

*----------------------------------------------------------------------*
* at selection-screen                                                  *
*----------------------------------------------------------------------*
AT SELECTION-SCREEN.

  REFRESH arbid_tab_n.
  REFRESH arbid_n.

*--- take always default variant
  IF variant IS INITIAL.
    PERFORM get_default_variant_f14 USING variant.
  ENDIF.
*--- Correct Listvariant chosen ? -------------------------------------*
  PERFORM variant_existence_f14 USING variant.


*-START OF SELECTION----------------------------------------------------
START-OF-SELECTION.

  g_form_set_pf_stat = 'PF_STATUS_L'.

  PERFORM determine_g_tcode_f16.
  PERFORM get_acttype_afih_l.
  PERFORM prepare_display_list_f14.
  PERFORM update_fieldcat_variant_f14.
  PERFORM check_fieldcat_variant_l.

  PERFORM get_arbid TABLES arbid_tab_n s_arbpl arbid_n
                    USING p_swerk.

* Create Date-Range for Selection
  gr_date-sign   = 'I'.
  gr_date-option = 'BT'.
* begin of change MOD-002
* gr_date-low    = sy-datum - 365.
* gr_date-high   = sy-datum.
  gr_date-low    = p_strmn  - 365.
  gr_date-high   = p_strmn.
* end of change MOD-002
  APPEND gr_date.

* select notifications
  PERFORM select_not.

  IF NOT gt_not IS INITIAL.
    gs_crid-objty = c_a.
    LOOP AT gt_not INTO wa_not.
      gs_crid-objid = wa_not-arbpl.
      COLLECT gs_crid INTO gt_crid.
    ENDLOOP.
  ENDIF.

* preselect work centers
  PERFORM preselect_crhd TABLES gt_crid.

  REFRESH object_tab.

  LOOP AT gt_not INTO wa_not.
    PERFORM fill_object_tab USING wa_not.
  ENDLOOP.

  DESCRIBE TABLE object_tab LINES sy-tabix.
  IF sy-tabix = 0.
    go_on = '0'.
    MESSAGE s047(ih).
  ENDIF.

*-----------------------------------------------------------------------
END-OF-SELECTION.

* create ALV-GRID
  PERFORM build_field_catlog CHANGING gt_fieldcat.
  PERFORM alv_display.
* begin of insertion MOD-003
  PERFORM write_outputfile.
* end of insertion MOD-003


*- SUBROUTINES---------------------------------------------------------
*&---------------------------------------------------------------------*
*&      Form  SELECT_NOT
*&---------------------------------------------------------------------*
*       select data from the db
*----------------------------------------------------------------------*
FORM select_not.
*>>>> Begin of insert MOD-006++
  TYPES:BEGIN OF ty_afko,
        aufnr TYPE aufnr,
        gstrs TYPE co_gstrs,
        aufpl TYPE co_aufpl,
        END OF ty_afko.
  TYPES:BEGIN OF ty_afvc,
        aufpl TYPE co_aufpl,
        aplzl TYPE co_aplzl,
        vornr TYPE vornr,
        END OF ty_afvc.
  TYPES:BEGIN OF ty_afvv,
        aufpl TYPE co_aufpl,
        aplzl TYPE co_aplzl,
        arbei TYPE arbeit,
        arbeh TYPE arbeite,
        END OF ty_afvv.
  DATA:lt_afko TYPE STANDARD TABLE OF ty_afko,
       lt_afvc TYPE STANDARD TABLE OF ty_afvc,
       lt_afvv TYPE STANDARD TABLE OF ty_afvv,
       ls_afko TYPE ty_afko,
       ls_afvc TYPE ty_afvc,
       ls_afvv TYPE ty_afvv.
  CONSTANTS:c_vornr TYPE vornr VALUE '0010',
            c_std   TYPE arbeh VALUE 'STD'.
*>>>> Begin of insert MOD-006++
  DATA: BEGIN OF h_status_tab OCCURS 30.
          INCLUDE STRUCTURE jstat.
  DATA: END OF h_status_tab.

  DATA: h_stat_flag,
        lv_objnr_ord LIKE aufk-objnr.

  DATA: lv_addr1 TYPE addr1_sel.


* first select notifications
  SELECT arbpl aufnr swerk qmnum equnr serialnr strmn
         qmart qmtxt bukrs kunum name1
         tplnr wapos
         bukrs vkorg vtweg spart
         viqmelst~adrnr_iloa
    INTO CORRESPONDING FIELDS OF TABLE gt_not
    FROM viqmelst
    JOIN kna1 ON viqmelst~kunum EQ kna1~kunnr
      WHERE swerk EQ p_swerk
* begin of delete MOD-002
*       AND qmdat IN gr_date
* end of delete MOD-002
        AND qmart NE 'Z5'
        AND arbpl IN arbid_n
        AND equnr IN s_equnr
        AND kunum IN s_kunum
        AND tplnr IN s_tplnr
* begin of change MOD-002
*       AND strmn LT p_strmn
        AND strmn IN gr_date
* end of change MOD-002
* begin of insertion MOD-003
        AND vkorg IN s_vkorg
* end of insertion MOD-003
        AND stat  IN ('I0068','I0070').
*>>>> Begin of insert MOD-006++
  IF NOT gt_not[] IS INITIAL.
    SELECT aufnr
           gstrs
           aufpl
           FROM afko
           INTO TABLE lt_afko
           FOR ALL ENTRIES IN gt_not
           WHERE aufnr EQ gt_not-aufnr.
  ENDIF.
  IF NOT lt_afko[] IS INITIAL.
    SELECT aufpl
           aplzl
           vornr
           FROM afvc
           INTO TABLE lt_afvc
           FOR ALL ENTRIES IN lt_afko
           WHERE aufpl EQ lt_afko-aufpl
             AND vornr EQ c_vornr.
  ENDIF.
  IF NOT lt_afvc[] IS INITIAL.
    SELECT aufpl
           aplzl
           arbei
           arbeh
           FROM afvv
           INTO TABLE lt_afvv
           FOR ALL ENTRIES IN lt_afvc
           WHERE aufpl EQ lt_afvc-aufpl
             AND aplzl EQ lt_afvc-aplzl.
  ENDIF.
*>>>> End of insert MOD-006++
  LOOP AT gt_not.

    IF NOT gt_not-aufnr IS INITIAL.

*.... exclude order status CNF / QUCR without QUAC
      REFRESH: h_status_tab.
      CONCATENATE 'OR' gt_not-aufnr INTO lv_objnr_ord.

      CALL FUNCTION 'STATUS_READ'
        EXPORTING
          objnr            = lv_objnr_ord
          only_active      = 'X'
        TABLES
          status           = h_status_tab
        EXCEPTIONS
          object_not_found = 01.

*.... check status
      h_stat_flag = ' '.
      CLEAR gv_ustat.

      LOOP AT h_status_tab.
        IF h_status_tab-stat EQ 'I0009' OR
           h_status_tab-stat EQ 'I0395 '.
          h_stat_flag = 'X'.
          EXIT.
        ENDIF.
        IF h_status_tab-stat EQ 'E0010'.
          gv_ustat = 'APAV'.
        ENDIF.
      ENDLOOP.

      IF h_stat_flag = 'X'.       "delete from internal table
        DELETE gt_not.
        CONTINUE.
      ENDIF.
*>>>> Begin of insert MOD-006--
*.... read order header
*      SELECT SINGLE gstrs aufpl
*            INTO (gt_not-gstrs, gt_not-aufpl)
*            FROM afko WHERE aufnr = gt_not-aufnr.
*>>>> End of insert MOD-006--
      READ TABLE lt_afko INTO ls_afko WITH KEY aufnr = gt_not-aufnr."MOD-006++
      IF sy-subrc <> 0.
        DELETE gt_not.
        CONTINUE.
      ELSE.
        MOVE:ls_afko-aufpl TO  gt_not-aufpl,
             ls_afko-gstrs TO  gt_not-gstrs."MOD-006++
      ENDIF.

*>>>> Begin of insert MOD-004
*.... read internal comment
      IF NOT gt_not-aufnr IS INITIAL.
        SELECT SINGLE zzcom
              INTO gt_not-zzcom
              FROM aufk WHERE aufnr = gt_not-aufnr.
      ENDIF.
*>>>> End of insert MOD-004
*.... get planned hours
*>>>> Begin of insert MOD-006--
*      IF NOT gt_not-aufpl IS INITIAL.
*        SELECT SINGLE arbei
*                      INTO gt_not-arbei
*                      FROM afvv
*                      WHERE aufpl = gt_not-aufpl
*                        AND aplzl = '00000001'.
*      ENDIF.
*>>>> End of insert MOD-006--
*>>>> Begin of insert MOD-006++
      READ TABLE lt_afvv INTO ls_afvv WITH KEY aufpl = ls_afko-aufpl.
      IF sy-subrc EQ 0 AND
         ls_afvv-arbeh EQ c_std.
        MOVE:ls_afvv-arbei TO gt_not-arbei.
      ENDIF.
*>>>> End of insert MOD-006++
*.... read order costs
      CLEAR: i_costs_sum[],
             i_costs_details[].

      wa_order = gt_not-aufnr.

      CALL FUNCTION 'IBAPI_ALM_ORDERCOSTS_READ'
        EXPORTING
          iv_orderid       = wa_order
        IMPORTING
          et_costs_sum     = i_costs_sum
          et_costs_details = i_costs_details
        TABLES
          et_messages      = lt_messages.

*.... get costs
      CLEAR: gt_not-parts,
             gt_not-labour,
             gt_not-others.

      LOOP AT i_costs_details INTO ls_costs_details.
        CASE ls_costs_details-value_category.
          WHEN 'Z04'.
            IF gv_ustat = 'APAV'.
              gt_not-parts = gt_not-parts + ls_costs_details-costs_plan.
            ENDIF.
          WHEN 'Z03'.
            gt_not-labour = gt_not-labour + ls_costs_details-costs_act.
          WHEN OTHERS.
            gt_not-others = gt_not-others + ls_costs_details-costs_act.
        ENDCASE.
      ENDLOOP.

      gt_not-total = gt_not-parts + gt_not-labour +
                     gt_not-others.
    ELSE.
*.... if no order has been created,
*.... select planned hour 2 from tasklist in Z4 notifications
      IF gt_not-qmart = 'Z4'.
        REFRESH: gt_plas.

        SELECT SINGLE plnty plnnr plnal
           INTO (gv_plnty, gv_plnnr, gv_plnal)
           FROM mpos
           WHERE wapos = gt_not-wapos.

        IF sy-subrc = 0.
          SELECT plnkn plnty plnnr plnal
             INTO CORRESPONDING FIELDS OF TABLE gt_plas
             FROM plas
             WHERE plnty = gv_plnty
               AND plnnr = gv_plnnr
               AND plnal = gv_plnal
               AND loekz = space.
        ENDIF.

        IF NOT gt_plas[] IS INITIAL.
          LOOP AT gt_plas.
            SELECT arbei INTO gv_arbei2
              FROM plpo
              WHERE plnty = gt_plas-plnty
                AND plnnr = gt_plas-plnnr
                AND plnkn = gt_plas-plnkn
                AND loekz = space
                AND steus = 'ZCO3'.

              gt_not-arbei2 = gt_not-arbei2 + gv_arbei2.
            ENDSELECT.
          ENDLOOP.
        ENDIF.
      ENDIF.
    ENDIF.

*Begin of insert Changes.

    SELECT SINGLE aufpl
            INTO gv_aufpl
            FROM afko
            WHERE aufnr = gt_not-aufnr.

    SELECT  aufpl vornr steus
            arbid werks objnr
            INTO  CORRESPONDING FIELDS OF TABLE it_afvc
            FROM  afvc
            WHERE aufpl = gv_aufpl.

    IF sy-subrc EQ 0.
      SELECT objnr stat
        FROM jest
        INTO CORRESPONDING FIELDS OF TABLE it_jest
        FOR ALL ENTRIES IN it_afvc
        WHERE objnr = it_afvc-objnr.

      IF sy-subrc EQ 0.
        SORT it_afvc BY vornr .
        SORT it_jest BY objnr.
        LOOP AT it_jest INTO wa_jest.
          READ TABLE it_afvc INTO wa_afvc WITH KEY objnr = wa_jest-objnr.
          IF wa_jest-stat  = 'I0013'.
            DELETE it_afvc WHERE objnr = wa_jest-objnr.
          ENDIF.
        ENDLOOP.
      ENDIF.

      SORT it_afvc BY vornr.
      LOOP AT it_afvc INTO wa_afvc.
        IF sy-tabix = 2.
          EXIT.
        ELSE.
          SELECT SINGLE arbpl
                 INTO gt_not-arbpl_ord
                 FROM crhd
                 WHERE objid = wa_afvc-arbid
                  AND werks = wa_afvc-werks.
          IF sy-subrc = 0.
          ELSE.
            CONTINUE.
          ENDIF.
        ENDIF.

      ENDLOOP.

    ENDIF.
    CLEAR:gv_aufpl, gv_plnt, gv_steus, gv_arbid, gv_werks,ls_afko,ls_afvv.
*End   of insert Changes.

**.. get short text from equipment
*    SELECT SINGLE eqktx INTO gt_not-eqktx
*          FROM eqkt WHERE equnr = gt_not-equnr
*                      AND spras = sy-langu.

    MODIFY gt_not.
  ENDLOOP.

  PERFORM fill_list_name_addr .
  PERFORM fill_equi_descript.

ENDFORM.                               " SELECT_NOT

*&---------------------------------------------------------------------*
*&      Form  build_field_catlog
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      <--P_GT_FIELDCAT  text
*----------------------------------------------------------------------*
FORM build_field_catlog  CHANGING pt_fieldcat TYPE slis_t_fieldcat_alv.

  DATA : ls_fcat TYPE slis_fieldcat_alv,
         lv_idx(3)   TYPE n,
         lv_fname(9) TYPE c.

*--------------------------Notification----------------------*
  ls_fcat-fieldname = 'QMNUM'.
  ls_fcat-rollname = 'QMNUM'.
  ls_fcat-no_zero = 'X'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Work center-----------------------*
  ls_fcat-fieldname = 'ARBPL'.
  ls_fcat-rollname = 'ARBPL'.
  ls_fcat-outputlen = '8'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Order number----------------------*
  ls_fcat-fieldname = 'AUFNR'.
  ls_fcat-rollname = 'AUFNR'.
  ls_fcat-no_convext = 'X'.
  ls_fcat-no_zero = 'X'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Plant-----------------------------*
  ls_fcat-fieldname = 'SWERK'.
  ls_fcat-rollname = 'SWERK'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Equipment nr.---------------------*
  ls_fcat-fieldname = 'EQUNR'.
  ls_fcat-rollname = 'EQUNR'.
  ls_fcat-no_zero = 'X'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Notification type-----------------*
  ls_fcat-fieldname = 'QMART'.
  ls_fcat-rollname = 'QMART'.
  ls_fcat-outputlen = '2'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Notification description----------*
  ls_fcat-fieldname = 'QMTXT'.
  ls_fcat-outputlen = '20'.
  ls_fcat-seltext_m = text-i01.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Required Start Date---------------*
  ls_fcat-fieldname = 'STRMN'.
  ls_fcat-rollname = 'STRMN'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Description of technical object---*
  ls_fcat-fieldname = 'EQKTX'.
  ls_fcat-outputlen = '20'.
  ls_fcat-seltext_m = text-i02.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Serial number---------------------*
  ls_fcat-fieldname = 'SERIALNR'.
  ls_fcat-rollname = 'GERNR'.
  ls_fcat-no_zero = 'X'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Scheduled start-------------------*
  ls_fcat-fieldname = 'GSTRS'.
  ls_fcat-rollname = 'CO_GSTRS'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Customer number-------------------*
  ls_fcat-fieldname = 'KUNUM'.
  ls_fcat-rollname = 'KUNNR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.

* CVM.sn
*--------------------------List  name ------------------------*
  ls_fcat-fieldname = 'LIST_NAME'.
  ls_fcat-rollname = 'I_NAME_LST'.
  ls_fcat-ref_tabname = 'RIHAUFK_LIST'.

  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.


**--------------------------Customer number-------------------*
*  ls_fcat-fieldname = 'KUNNR'.
*  ls_fcat-ref_tabname = 'KNA1'.
** CVM.en
*  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.

*--------------------------Customer Name -------------------*
* CVM.sn
  ls_fcat-fieldname = 'NAME1'.
  ls_fcat-rollname = 'NAME1_GP'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
* CVM.en
*--------------------------Funct. location-------------------*
  ls_fcat-fieldname = 'TPLNR'.
  ls_fcat-rollname = 'TPLNR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Planned hours---------------------*
  ls_fcat-fieldname = 'ARBEI'.
  ls_fcat-outputlen = '20'.
  ls_fcat-seltext_m = text-i04.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Material cost---------------------*
  ls_fcat-fieldname = 'MATCOST'.
  ls_fcat-rollname = 'YMATCOST'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Labour cost-----------------------*
  ls_fcat-fieldname = 'LABOURCOST'.
  ls_fcat-rollname = 'YLABOURCOST'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Other cost------------------------*
  ls_fcat-fieldname = 'OTHERCOST'.
  ls_fcat-rollname = 'YOTHERCOST'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Total cost------------------------*
  ls_fcat-fieldname = 'TOTCOST'.
  ls_fcat-rollname = 'YTOTCOST'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Planned hours 2-------------------*
  ls_fcat-fieldname = 'ARBEI2'.
  ls_fcat-outputlen = '20'.
  ls_fcat-seltext_m = text-i05.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-------------------------Work Center from Order-------------*
  ls_fcat-fieldname = 'ARBPL_ORD'.
  ls_fcat-seltext_m = text-i06.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Internal Comment-------------------*
  ls_fcat-fieldname = 'ZZCOM'.
  ls_fcat-outputlen = '30'.
  ls_fcat-seltext_m = text-i09.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.

ENDFORM.                    " build_field_catlog

*&---------------------------------------------------------------------*
*&      Form  USER_COMMAND_L
*&---------------------------------------------------------------------*
*       event user_command, called from ALV
*----------------------------------------------------------------------*
FORM user_command_l USING p_ucomm LIKE sy-ucomm
                          p_selfield TYPE slis_selfield.

  p_selfield-refresh = g_s.

  PERFORM check_pf2_with_object_f16 USING p_ucomm.
  PERFORM set_p_selfield_general_f16 USING p_selfield.

  CASE p_ucomm.
*   show order
    WHEN 'AUFK'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
*   show notification
    WHEN 'QMEL'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
*   show equipment
    WHEN 'EQUI'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
*   show customer
    WHEN 'CUST'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
*   show functional location
    WHEN 'FLOC'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
*   show detail
    WHEN 'IOBJ'.
      p_ucomm = 'QMEL'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
    WHEN 'V-A '.
*--- #ndern PF-Status #ndern -> Anzeigen ----------------------------
      p_selfield-refresh = g_x.
      PERFORM change_aktyp_f16.
    WHEN 'A-V '.
*--- #ndern PF-Status Anezeigen -> #ndern ---------------------------
      p_selfield-refresh = g_x.
      PERFORM change_aktyp_f16.
*   Spreadsheet
    WHEN 'XXL '.
      p_selfield-refresh = space.
      t370a-tabname = 'YAM_BACKLOG_XXL'.  "+Mod-005 (Satya -> XXL Struct)
      PERFORM xxl_call_f16.
  ENDCASE.

*--- If list is empty now - leave ALV
  IF object_tab[] IS INITIAL AND g_variant_save NE g_x.
    p_selfield-exit = g_x.
    MESSAGE s047(ih).
  ENDIF.

ENDFORM.                               " USER_COMMAND_L

*&---------------------------------------------------------------------*
*&      Form  PF_STATUS_L
*&---------------------------------------------------------------------*
*       set pf-status, called from ALV
*----------------------------------------------------------------------*
*----------------------------------------------------------------------*
FORM pf_status_l USING extab TYPE slis_t_extab.

* APPEND 'V-A' TO extab.
  SET PF-STATUS 'MAIN' EXCLUDING extab.

ENDFORM.                               " PF_STATUS_L

*&---------------------------------------------------------------------*
*&      Form  GET_ARBPL
*&---------------------------------------------------------------------*
*       read working center
*----------------------------------------------------------------------*
*  -->  arbid
*  <--  arbpl
*----------------------------------------------------------------------*
FORM get_arbpl USING    arbid TYPE cr_objid
               CHANGING arbpl TYPE arbpl.

  DATA:
    l_crhd LIKE crhd.

  READ TABLE gt_crhd INTO l_crhd
    WITH KEY objid = arbid
    BINARY SEARCH.

  arbpl = l_crhd-arbpl.

ENDFORM.                               " GET_ARBPL

*&---------------------------------------------------------------------*
*&      Form  GET_ARBID
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->ARBPL                                                        *
*      -->ARBID_TAB                                                    *
*      -->ARBID                                                        *
*      -->P_G_WERKI                                                    *
*----------------------------------------------------------------------*
FORM get_arbid TABLES arbid_tab STRUCTURE arbid_tab_n
                      arbpl STRUCTURE s_arbpl
                      arbid STRUCTURE arbid_n
               USING  p_g_werki.

* read internal number of a work center -------------------------------*
  DESCRIBE TABLE arbpl LINES sy-tabix.
  IF NOT sy-tabix IS INITIAL.
    SELECT objid arbpl FROM crhd INTO TABLE arbid_tab
      WHERE arbpl IN arbpl
      AND werks EQ p_g_werki.

*-- no work center found ----------------------------------------------*
    DESCRIBE TABLE arbid_tab LINES sy-tabix.
    IF sy-tabix IS INITIAL.
      MESSAGE s047(ih).
      STOP.
    ENDIF.

*-- too many work centers for select-----------------------------------*
    IF sy-tabix > 255.
      MESSAGE i103(ih).
      STOP.
    ENDIF.

*-- fill range for selection ------------------------------------------*
    arbid-option = 'EQ'.
    arbid-sign = 'I'.
    LOOP AT arbid_tab.
      arbid-low = arbid_tab-arbid.
      APPEND arbid.
    ENDLOOP.
    SORT arbid_tab.
  ENDIF.

ENDFORM.                               " GET_ARBID

*&---------------------------------------------------------------------*
*&      Form  FILL_OBJECT_TAB
*&---------------------------------------------------------------------*
*       fill object_tab with selected data
*----------------------------------------------------------------------*
FORM fill_object_tab USING i_not STRUCTURE gt_not.

  CLEAR object_tab.

  MOVE-CORRESPONDING i_not TO object_tab.
*  move i_not-arbpl_ord to object_tab-arbpl_ord.
  MOVE i_not-parts   TO object_tab-matcost.
  MOVE i_not-labour  TO object_tab-labourcost.
  MOVE i_not-others  TO object_tab-othercost.
  MOVE i_not-total   TO object_tab-totcost.

  PERFORM get_arbpl USING i_not-arbpl object_tab-arbpl.

  COLLECT object_tab.

ENDFORM.                               " FILL_OBJECT_TAB

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_l
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_F_UCOMM  text
*      -->P_F_SELFIELD  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_l  USING   p_ucomm LIKE sy-ucomm
                              p_selfield TYPE slis_selfield.

  DATA: h_ucomm LIKE sy-ucomm.

  PERFORM mark_selected_f16 CHANGING object_tab-selected
                                     object_tab-pm_selected.

  CASE p_ucomm.
*   notification
    WHEN 'QMEL'.
      SET PARAMETER ID 'IQM' FIELD  object_tab-qmnum.
      SET PARAMETER ID 'ANR' FIELD  object_tab-aufnr.
      IF g_aktyp = 'A'.
        CALL TRANSACTION 'IW53' AND SKIP FIRST SCREEN.
      ELSE.
        CALL TRANSACTION 'IW52' AND SKIP FIRST SCREEN.
      ENDIF.
*   order document
    WHEN 'AUFK'.
      SET PARAMETER ID 'ANR' FIELD  object_tab-aufnr.
      IF g_aktyp = 'A'.
        CALL TRANSACTION 'IW33' AND SKIP FIRST SCREEN.
      ELSE.
        CALL TRANSACTION 'IW32' AND SKIP FIRST SCREEN.
      ENDIF.
*   equipment
    WHEN 'EQUI'.
      SET PARAMETER ID 'EQN' FIELD  object_tab-equnr.
      IF g_aktyp = 'A'.
        CALL TRANSACTION 'IE03' AND SKIP FIRST SCREEN.
      ELSE.
        CALL TRANSACTION 'IE02' AND SKIP FIRST SCREEN.
      ENDIF.
*   customer
    WHEN 'CUST'.
      SET PARAMETER ID 'KUN' FIELD  object_tab-kunum.
      SET PARAMETER ID 'BUK' FIELD  object_tab-bukrs.
      SET PARAMETER ID 'VKO' FIELD  object_tab-vkorg.
      SET PARAMETER ID 'VTW' FIELD  object_tab-vtweg.
      SET PARAMETER ID 'SPA' FIELD  object_tab-spart.
      IF g_aktyp = 'A'.
        CALL TRANSACTION 'XD03' AND SKIP FIRST SCREEN.
      ELSE.
        CALL TRANSACTION 'XD02' AND SKIP FIRST SCREEN.
      ENDIF.
*   functional location
    WHEN 'FLOC'.
      SET PARAMETER ID 'IFL' FIELD  object_tab-tplnr.
      IF g_aktyp = 'A'.
        CALL TRANSACTION 'IL03' AND SKIP FIRST SCREEN.
      ELSE.
        CALL TRANSACTION 'IL02' AND SKIP FIRST SCREEN.
      ENDIF.
  ENDCASE.

ENDFORM.                    " fcodes_with_mark_l

*&---------------------------------------------------------------------*
*&      Form  alv_display
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM alv_display .

* data: g_title TYPE  LVC_TITLE.

* g_title = p_reptp.
  g_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
     EXPORTING
       i_callback_program                =  g_repid
       i_save                            = 'A'
       it_events                         =  g_events_tab[]
*      I_GRID_TITLE                      =  g_title
*      I_GRID_SETTINGS                   =
       is_layout                         =  g_layout
       it_fieldcat                       =  gt_fieldcat[]
       is_variant                        =  g_variant
     TABLES
        t_outtab                         =  object_tab.

ENDFORM.                    " alv_display

*&---------------------------------------------------------------------*
*&      Form  PRESELECT_CRHD
*&---------------------------------------------------------------------*
FORM preselect_crhd TABLES it_crid TYPE crid_tab.

  SELECT * FROM crhd INTO TABLE gt_crhd
    FOR ALL ENTRIES IN it_crid
    WHERE objty = it_crid-objty
    AND   objid = it_crid-objid.

  SORT gt_crhd BY objid.

ENDFORM.                               " PRESELECT_CRHD

*&---------------------------------------------------------------------*
*&      Form  CHECK_FIELDCAT_VARIANT_L
*&---------------------------------------------------------------------*
*       set disp. fields if no list variant exists
*----------------------------------------------------------------------*
FORM check_fieldcat_variant_l.

  DATA fieldcat_wa TYPE slis_fieldcat_alv.
  DATA index       LIKE sy-tabix.

  index = 1.

  DESCRIBE TABLE g_selfields_tab LINES sy-tabix.
  IF sy-tabix IS INITIAL.
    LOOP AT g_fieldcat_tab INTO fieldcat_wa.
      CASE fieldcat_wa-fieldname.
        WHEN 'PM_SELECTED'.
          fieldcat_wa-no_out  = space.
          fieldcat_wa-col_pos = 1.
        WHEN 'ARBPL'.
          fieldcat_wa-no_out  = space.
          fieldcat_wa-col_pos = 2.
        WHEN 'AUFNR'.
          fieldcat_wa-no_out = space.
          fieldcat_wa-col_pos = 3.
        WHEN 'QMNUM'.
          fieldcat_wa-no_out = space.
          fieldcat_wa-col_pos = 4.
        WHEN OTHERS.
          fieldcat_wa-no_out = g_x.
      ENDCASE.
      MODIFY g_fieldcat_tab FROM fieldcat_wa.
    ENDLOOP.
  ENDIF.

  PERFORM create_g_selfields_tab_f14.

ENDFORM.                               " CHECK_FIELDCAT_VARIANT_L

*&---------------------------------------------------------------------*
*&      Form  GET_ACTTYPE_AFIH_L
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM get_acttype_afih_l.

  SELECT SINGLE * FROM t370a WHERE tcode = g_tcode.
  IF sy-subrc <> 0.
    SELECT SINGLE * FROM t370a WHERE tcode = 'IW59'.
    IF sy-subrc <> 0.
      RAISE iw29_not_in_t370a.
    ENDIF.
  ENDIF.
  g_aktyp = t370a-aktyp.

ENDFORM.                               " GET_ACTTYPE_AFIH_L
*&---------------------------------------------------------------------*
*&      Form  FILL_LIST_NAME_ADDR
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM fill_list_name_addr .

* Step 1 fill table g_adrnr_sel_tab
  CLEAR g_adrnr_sel_tab.
  LOOP AT gt_not
  WHERE NOT  adrnr_iloa IS INITIAL.
    MOVE gt_not-adrnr_iloa TO  g_adrnr_sel_tab-addrnumber.
    APPEND g_adrnr_sel_tab.
  ENDLOOP.


* Step 2 get data from adres table
  CHECK NOT g_adrnr_sel_tab[] IS INITIAL.

  SORT g_adrnr_sel_tab BY addrnumber.
  DELETE ADJACENT DUPLICATES FROM g_adrnr_sel_tab.

*---refresh buffer before new selection
  CALL FUNCTION 'ADDR_MEMORY_CLEAR'
    EXPORTING
      force              = ' '
    EXCEPTIONS
      unsaved_data_exist = 1
      internal_error     = 2
      OTHERS             = 3.

  CALL FUNCTION 'ADDR_GET_ARRAY'
*      IMPORTING
*            RETURNCODE              =
       TABLES
            address_selection       = g_adrnr_sel_tab
            address_value           = g_adrnr_val_tab
*           ADDRESS_ADDITIONAL_INFO =
*           ERROR_TABLE             =
*           ADDRESS_TEXT            =
       EXCEPTIONS
            parameter_error         = 1
            internal_error          = 2
            OTHERS                  = 3.

  IF sy-subrc IS INITIAL.
*--- sortieren f¨¹r sp#teren binary search ---------------------------
    SORT g_adrnr_val_tab BY addrnumber.
  ENDIF.

* STEP 3, fill the results into table  gt_not
  LOOP AT gt_not
  WHERE NOT adrnr_iloa IS INITIAL.
    READ TABLE g_adrnr_val_tab
    WITH KEY addrnumber = gt_not-adrnr_iloa.
    IF sy-subrc EQ 0.
      MOVE g_adrnr_val_tab-name1 TO gt_not-list_name.
      MODIFY gt_not.
    ENDIF.

  ENDLOOP.

ENDFORM.                    " FILL_LIST_NAME_ADDR
*&---------------------------------------------------------------------*
*&      Form  FILL_EQUI_DESCRIPT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM fill_equi_descript .

* Step 1 fill table g_adrnr_sel_tab
  CLEAR: tmp_equi,equi_text.
* Step 1, fill tmp_equi, with equnr
  LOOP AT gt_not
  WHERE NOT  equnr IS INITIAL.
    MOVE gt_not-equnr TO  tmp_equi-equnr.
    APPEND tmp_equi.
  ENDLOOP.


* Step 2 get data from adres table
  CHECK NOT tmp_equi[] IS INITIAL.

  SORT tmp_equi[] BY equnr.
  DELETE ADJACENT DUPLICATES FROM tmp_equi.

  SELECT equnr eqktx
  INTO TABLE equi_text
  FROM eqkt
  FOR ALL ENTRIES IN tmp_equi
  WHERE equnr = tmp_equi-equnr
  AND spras = sy-langu.
  IF sy-subrc EQ 0.
    LOOP AT gt_not
    WHERE NOT  equnr IS INITIAL.
      READ TABLE equi_text WITH KEY equnr = gt_not-equnr.
      IF sy-subrc EQ 0.
        MOVE equi_text-eqktx TO gt_not-eqktx.
        MODIFY gt_not.
      ENDIF.
    ENDLOOP.
  ENDIF.


ENDFORM.                    " FILL_EQUI_DESCRIPT

*&---------------------------------------------------------------------*
*&      Form  write_outputfile
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
* begin of insertion MOD-003
FORM write_outputfile .

  IF sy-uname = c_user.

    CALL FUNCTION 'OWN_LOGICAL_SYSTEM_GET'
      IMPORTING
        own_logical_system             = p_logsys
      EXCEPTIONS
        own_logical_system_not_defined = 1
        OTHERS                         = 2.

    REPLACE 'xxx' IN g_directory WITH p_logsys(3).
    CONCATENATE g_directory c_name '_' p_swerk INTO g_ofile.
    OPEN DATASET g_ofile FOR OUTPUT IN TEXT MODE ENCODING DEFAULT.
    IF sy-subrc <> 0.
      WRITE: / text-e03, g_ofile.
      EXIT.
    ENDIF.
    CLEAR object_tab2[].
    LOOP AT object_tab.
      MOVE-CORRESPONDING object_tab TO object_tab2.
      APPEND object_tab2.
    ENDLOOP.
    LOOP AT object_tab2.
      TRANSFER object_tab2 TO g_ofile.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " write_outputfile

* end of insertion MOD-003
*Text symbol text£º
*001:Selection screen input
*002:Limitations for this report
*003:Variant
*700:
*701:Wollen Sie die Verarbeitung f¨¹r
*702:
*703:selektierten Objekte abbrechen?
*704:
*705:
*706:Ja
*ANZ:Number
*C01:* This report only looks for the last 12 months
*C02:* Material cost = Ordered parts in status APAV (All Parts AVailable)
*C03:* All other cost is confirmed cost (labour, kilometer, other)
*C04:* Please be aware that runtime can take 10-15 minutes
*E03:No authorisation for plant
*I01:Notif.description
*I02:Equipm.description
*I03:Order description
*I04:Planned hours
*I05:Planned hours 2
*I06:Work_Ctr_Order
*I09:AM: Internal comment
*I10:Unit
*I11:ARBEH

*SON:
*Selection text£º
*P_STRMN:        Backlog date
*P_SWERK:D       .
*S_ARBPL:D       .
*S_EQUNR:D       .
*S_KUNUM:D       .
*S_TPLNR:D       .
*S_VKORG:        Sales Organisation
*VARIANT:        Layout
