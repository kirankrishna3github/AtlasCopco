*----------------------------------------------------------------------*
* PROGRAM ID           : YAM_CORR_MARC                                 *
* PROGRAM TITLE        : AM : Correction fields LADGR + EKGRP          *
* AUTHOR               : Marc Jacobs                                   *
* DATE                 : 07/01/2008                                    *
* DEVELOPMENT ID       :                                               *
* CHANGE REQUEST NUMBER:  CD1K924845                                   *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 | 09/10/05 | W. Deplancke | Desactivate program              *
*----------------------------------------------------------------------*

REPORT  yam_corr_marc               NO STANDARD PAGE HEADING
                                    LINE-SIZE 160
                                    LINE-COUNT 80.

*- Tables -------------------------------------------------------------*
TABLES: mara,                   "Material Client level
        marc.                   "Material Plant level
*- Internal tables ----------------------------------------------------*
* Control report data
DATA: BEGIN OF g_output OCCURS 0,
        matnr LIKE mara-matnr,
        werks LIKE marc-werks,
        ladgr LIKE marc-ladgr,
        ekgrp like marc-ekgrp,
        errflg(1),
        text(100).
DATA: END OF g_output.

DATA: BEGIN OF i_mara OCCURS 0,
        matnr  TYPE matnr,
      END   OF i_mara.

DATA: BEGIN OF i_marc OCCURS 0,
        matnr  TYPE matnr,
        werks  TYPE werks_d,
        ladgr  TYPE ladgr,
        ekgrp  TYPE ekgrp,
      END   OF i_marc.

DATA: BEGIN OF t_marc,
        matnr  TYPE matnr,
        werks  TYPE werks_d,
        ladgr  TYPE ladgr,
        ekgrp  TYPE ekgrp,
      END   OF t_marc.

* MATERIAL_MAINTAIN_DARK interface tables
* General Data for Material
DATA: BEGIN OF gt_mara OCCURS 0.
        INCLUDE STRUCTURE mara_ueb.
DATA: END   OF gt_mara.

* Plant Data for Material
DATA: BEGIN OF gt_marc OCCURS 0.
        INCLUDE STRUCTURE marc_ueb.
DATA: END   OF gt_marc.

* Messages
DATA: BEGIN OF gt_messtab OCCURS 0.
        INCLUDE STRUCTURE merrdat.
DATA: END   OF gt_messtab,
      t_messtab LIKE gt_messtab.


*- Variables ----------------------------------------------------------*
DATA: g_errors TYPE bierrnum,
      g_msg_no TYPE msgnr,
      g_cnt_tranc  TYPE i VALUE 1,
      g_cnt_d_ind  TYPE i VALUE 1.


*- Constants ----------------------------------------------------------*
CONSTANTS: c_mm02  LIKE sy-tcode   VALUE 'MM02'.  " Transaction code


*- Select-options / Parameters ----------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK block1 WITH FRAME TITLE text-010.
SELECT-OPTIONS: s_matnr FOR mara-matnr,
                s_mtart FOR mara-mtart OBLIGATORY,
                s_werks FOR marc-werks OBLIGATORY MEMORY ID wrk.
SELECTION-SCREEN END OF BLOCK block1.

*
* MOD-001 = Desactivated
*
DATA col TYPE i VALUE 0.
col = 6.
format color = col.
write 'Development desactivated - Prevent unauthorized use ' .
exit.

*
* MOD-001 = Desactivated
*




*- Start-of-selection -------------------------------------------------*
START-OF-SELECTION.

* select data from MARA
  SELECT matnr FROM mara
      INTO CORRESPONDING FIELDS OF TABLE i_mara
      WHERE matnr IN s_matnr
        AND mtart IN s_mtart.

  IF i_mara[] IS INITIAL.
    WRITE: / text-e10.                " no materials selected
    EXIT.
  ELSE.
    SELECT matnr werks ladgr ekgrp FROM marc
        INTO CORRESPONDING FIELDS OF TABLE i_marc
        FOR ALL ENTRIES IN i_mara
        WHERE matnr EQ i_mara-matnr
          AND werks IN s_werks.

    SORT i_marc BY matnr werks.

    LOOP AT i_marc.
      IF i_marc-ladgr IS INITIAL OR
         i_marc-ekgrp IS INITIAL.
        t_marc-matnr = i_marc-matnr.
        t_marc-werks = i_marc-werks.
        IF i_marc-ladgr IS INITIAL.
          t_marc-ladgr = 'Z001'.
        ENDIF.
        IF i_marc-ekgrp IS INITIAL.
          t_marc-ekgrp = '100'.
        ENDIF.
        PERFORM update_material.
      ENDIF.
      CLEAR t_marc.
    ENDLOOP.
  ENDIF.

*- End-of-selection ---------------------------------------------------*
END-OF-SELECTION.

  PERFORM write_output.

  SKIP.
  WRITE: / 'End of Proces'(001).

*- Top-of-page --------------------------------------------------------*
TOP-OF-PAGE.

  WRITE: sy-title, 85 sy-datum, 100 sy-uzeit, 115 sy-pagno.
  ULINE.
  SKIP.

*- forms --------------------------------------------------------------*
*&---------------------------------------------------------------------
*&      Form  UPDATE_MATERIAL
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM update_material.

* General Material Data
  gt_mara-mandt = sy-mandt.
  gt_mara-tcode = c_mm02.
  gt_mara-tranc = g_cnt_tranc.
  gt_mara-d_ind = g_cnt_d_ind.

  gt_mara-matnr = t_marc-matnr.
  APPEND gt_mara.

* Plant Data for Material
  gt_marc-mandt = sy-mandt.
  gt_marc-tranc = g_cnt_tranc.
  gt_marc-d_ind = g_cnt_d_ind.

  gt_marc-matnr = t_marc-matnr.
  gt_marc-werks = t_marc-werks.
  if not t_marc-ladgr is initial.
  gt_marc-ladgr = t_marc-ladgr.
  endif.
  if not t_marc-ekgrp is initial.
  gt_marc-ekgrp = t_marc-ekgrp.
  endif.
  APPEND gt_marc.

  PERFORM change_material USING t_marc-matnr t_marc-werks
                                t_marc-ladgr t_marc-ekgrp.

ENDFORM.                    "update_material

*&---------------------------------------------------------------------*
*&      Form  CHANGE_MATERIAL
*&---------------------------------------------------------------------*
*       Call DI FM 'MATERIAL_MAINTAIN_DARK' to change the Material
*----------------------------------------------------------------------*
FORM change_material USING r_matnr r_werks r_ladgr r_ekgrp.

* Change Material
  CALL FUNCTION 'MATERIAL_MAINTAIN_DARK'
    EXPORTING
      p_kz_no_warn              = 'N'
      kz_prf                    = 'W'
    IMPORTING
      number_errors_transaction = g_errors
    TABLES
      amara_ueb                 = gt_mara
      amarc_ueb                 = gt_marc
      amerrdat                  = gt_messtab
    EXCEPTIONS
      kstatus_empty             = 1
      tkstatus_empty            = 2
      t130m_error               = 3
      internal_error            = 4
      too_many_errors           = 5
      update_error              = 6
      OTHERS                    = 7.

  IF sy-subrc EQ 0.
    IF g_errors NE 0.
* Add messages to output table
      LOOP AT gt_messtab.
        t_messtab = gt_messtab.
        CLEAR g_output.
        IF gt_messtab-msgty EQ 'E' OR gt_messtab-msgty EQ 'A'.
          MOVE 'X' TO g_output-errflg.
        ENDIF.

        AT NEW msgno.
          MOVE r_matnr TO g_output-matnr.
          MOVE r_werks TO g_output-werks.
          MOVE r_ladgr TO g_output-ladgr.
          move r_ekgrp to g_output-ekgrp.

          PERFORM prepare_message_text USING    t_messtab
                                       CHANGING g_output-text.
          APPEND g_output.
        ENDAT.
      ENDLOOP.
    ELSE.
      COMMIT WORK AND WAIT.
      CLEAR g_output.
      MOVE r_matnr TO g_output-matnr.
      MOVE r_werks TO g_output-werks.
      MOVE r_ladgr TO g_output-ladgr.
          move r_ekgrp to g_output-ekgrp.
      APPEND g_output.
    ENDIF.
  ELSE.
    ROLLBACK WORK.
* Add message to output table
    CLEAR g_output.
    MOVE 'X'      TO g_output-errflg.
    MOVE r_matnr  TO g_output-matnr.
    MOVE r_werks  TO g_output-werks.
   MOVE r_ladgr TO g_output-ladgr.
          move r_ekgrp to g_output-ekgrp.
    MOVE text-e04 TO g_output-text.
    APPEND g_output.
  ENDIF.

* Refresh interface tables
  CLEAR: gt_mara, gt_marc, gt_messtab, g_errors.
  REFRESH: gt_mara, gt_marc, gt_messtab.

ENDFORM.                    "change_material

*&---------------------------------------------------------------------*
*&      Form  PREPARE_MESSAGE_TEXT
*&---------------------------------------------------------------------*
*       Format message with variables
*----------------------------------------------------------------------*
*  -->  P_MESSTAB  Messages table header line
*  <--  R_TEXT     Message text
*----------------------------------------------------------------------*
FORM prepare_message_text USING    p_messtab LIKE merrdat
                          CHANGING r_text.

  CLEAR r_text.
  g_msg_no = p_messtab-msgno.       "Type conflict!
  CALL FUNCTION 'MESSAGE_PREPARE'
    EXPORTING
      language               = sy-langu
      msg_id                 = p_messtab-msgid
      msg_no                 = g_msg_no
      msg_var1               = p_messtab-msgv1
      msg_var2               = p_messtab-msgv2
      msg_var3               = p_messtab-msgv3
      msg_var4               = p_messtab-msgv4
    IMPORTING
      msg_text               = r_text
    EXCEPTIONS
      function_not_completed = 1
      message_not_found      = 2
      OTHERS                 = 3.

  IF sy-subrc NE 0.
    CONCATENATE p_messtab-msgty p_messtab-msgid p_messtab-msgno
                INTO r_text SEPARATED BY space.
  ELSE.
    CONCATENATE p_messtab-msgty p_messtab-msgid p_messtab-msgno r_text
                INTO r_text SEPARATED BY space.
  ENDIF.

ENDFORM.                    " PREPARE_MESSAGE_TEXT

*&---------------------------------------------------------------------*
*&      Form  WRITE_OUTPUT
*&---------------------------------------------------------------------*
*       Output the control report
*----------------------------------------------------------------------*
FORM write_output.

  SORT g_output BY matnr werks.

* Output
  LOOP AT g_output WHERE errflg EQ 'X'.

    WRITE:/ g_output-matnr NO-ZERO, 14 g_output-werks, g_output-ladgr,
            g_output-ekgrp, g_output-text.
  ENDLOOP.

ENDFORM.                    " WRITE_OUTPUT

*Text symbol text£º
*001:End of Proces
*010:Selection
*E04:Update material failed !!!

*E10:No materials selected
*Selection text£º
*S_MATNR:D       Material
*S_MTART:        Material type
*S_WERKS:D       Plant
