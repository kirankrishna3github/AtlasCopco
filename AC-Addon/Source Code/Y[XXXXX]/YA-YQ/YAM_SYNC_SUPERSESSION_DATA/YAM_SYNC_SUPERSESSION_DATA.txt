*----------------------------------------------------------------------
* PROGRAM ID           : YAM_SYNC_SUPERSESSION_DATA                    *
* PROGRAM TITLE        : AM: Synchronise supersession data between     *
*                            plants                                    *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 03/08/2005                                    *
* DEVELOPMENT ID       :                                               *
* CHANGE REQUEST NUMBER: CD1K902914                                    *
* PROGRAM DESCRIPTION  : Program that synchronise the supersession data*
*               between the different plants.                          *
*                                                                      *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*

REPORT  YAM_SYNC_SUPERSESSION_DATA  no standard page heading
                                    line-size 120
                                    line-count 80.

*- Tables -------------------------------------------------------------*
tables: mara,                   "Material Client level
        marc,                   "Material Plant level
        t001w.                  "Plants

*- Internal tables ----------------------------------------------------*
* Control report data
data: begin of g_output occurs 0,
        matnr like mara-matnr,
        werks like marc-werks,
        errflg(1),
        text(100).
data: end of g_output.

data: begin of i_mara occurs 0.
        include structure mara.
data: end   of i_mara.

data: begin of i_marc occurs 0.
        include structure marc.
data: end   of i_marc.

data: begin of i_marc_fr occurs 0.
        include structure marc.
data: end of i_marc_fr.

* MATERIAL_MAINTAIN_DARK interface tables
* General Data for Material
data: begin of gt_mara occurs 0.
        include structure mara_ueb.
data: end   of gt_mara.

* Plant Data for Material
data: begin of gt_marc occurs 0.
        include structure marc_ueb.
data: end   of gt_marc.

* Messages
data: begin of gt_messtab occurs 0.
        include structure merrdat.
data: end   of gt_messtab.

* fields to be assigned initial value
data: begin of gt_mfieldres occurs 0.
        include structure mfieldres.
data: end of gt_mfieldres.

*- Variables ----------------------------------------------------------*
data: g_errors type bierrnum,
      g_msg_no type msgnr,
      g_cnt_tranc  type i value 1,
      g_cnt_d_ind  type i value 1.


*- Constants ----------------------------------------------------------*
constants: c_mm02  like sy-tcode   value 'MM02'.  " Transaction code


*- Select-options / Parameters ----------------------------------------*
SELECTION-SCREEN BEGIN OF BLOCK block1 WITH FRAME TITLE text-010.
select-options: s_matnr for mara-matnr,
                s_mstae for mara-mstae.

parameters:     p_werkfr type werks.              " plant code FROM
SELECTION-SCREEN END OF BLOCK block1.

SELECTION-SCREEN BEGIN OF BLOCK block2 WITH FRAME TITLE text-020.
parameters:     p_clear  radiobutton group 01,
                p_copy   radiobutton group 01.
SELECTION-SCREEN END OF BLOCK block2.


*- Selection-screen ---------------------------------------------------*
at selection-screen.

  if p_copy = 'X'.
    if p_werkfr is initial.
      message e001(00) with text-e03.    "Plant FROM must be filled
    else.
      select single werks into t001w-werks
         from t001w
         where  werks = p_werkfr.

      if sy-subrc ne 0.
        message e001(00) with text-e02.    "Plant FROM doesn't exist
      endif.
    endif.
  endif.

*- Start-of-selection -------------------------------------------------*
start-of-selection.

* select data from plant to copy from
  select * from mara
      into table i_mara
      where matnr in s_matnr
        and mstae in s_mstae.

  if i_mara[] is initial.
    write: / text-e10.                " no materials selected
    exit.
  else.
    if p_copy = 'X'.                  " copy fields

      select * from marc
          into table i_marc_fr
          for all entries in i_mara
          where matnr eq i_mara-matnr
            and werks eq p_werkfr.

      if sy-subrc = 0.
        sort i_marc_fr by matnr.

        select * from marc
            into table i_marc
            for all entries in i_mara
            where matnr eq i_mara-matnr
              and werks ne p_werkfr.
      else.
        write: / text-e11.
        exit.
      endif.

    else.                             " clear fields

      select * from marc
          into table i_marc
          for all entries in i_mara
          where matnr eq i_mara-matnr.

    endif.
  endif.


*- End-of-selection ---------------------------------------------------*
end-of-selection.

  loop at i_marc.

    if p_copy = 'X'.
      clear i_marc_fr.
      read table i_marc_fr with key matnr = i_marc-matnr
                          binary search.

      if sy-subrc ne 0.
        clear g_output.
        move 'X'      to g_output-errflg.
        move i_marc-matnr  to g_output-matnr.
        move i_marc_fr-werks  to g_output-werks.
        move text-e07 to g_output-text.
        append g_output.
        continue.
      else.
        if i_marc_fr-nfmat is initial.
          clear g_output.
          move 'X'      to g_output-errflg.
          move i_marc-matnr  to g_output-matnr.
          move i_marc_fr-werks  to g_output-werks.
          move text-e08 to g_output-text.
          append g_output.
          continue.
        endif.
      endif.
    endif.

    perform copy_material.

  endloop.

  perform write_output.

  skip.
  write: / 'End of Proces'.

*- Top-of-page --------------------------------------------------------*
top-of-page.

  write: sy-title, 85 sy-datum, 100 sy-uzeit, 115 sy-pagno.
  uline.
  skip.

*- forms --------------------------------------------------------------*
*&---------------------------------------------------------------------
*&      Form  COPY_MATERIAL
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
form copy_material.

* General Material Data
  gt_mara-mandt = sy-mandt.
  gt_mara-tcode = c_mm02.
  gt_mara-tranc = g_cnt_tranc.
  gt_mara-d_ind = g_cnt_d_ind.

  gt_mara-matnr = i_marc-matnr.
  append gt_mara.

* Plant Data for Material
  gt_marc-mandt = sy-mandt.
  gt_marc-tranc = g_cnt_tranc.
  gt_marc-d_ind = g_cnt_d_ind.

  if p_copy = 'X'.

    gt_marc-matnr = i_marc_fr-matnr.
    gt_marc-werks = i_marc-werks.
    gt_marc-kzaus = i_marc_fr-kzaus.
    gt_marc-ausdt = i_marc_fr-ausdt.
    gt_marc-nfmat = i_marc_fr-nfmat.
    append gt_marc.

  else.

    gt_marc-matnr = i_marc-matnr.
    gt_marc-werks = i_marc-werks.
    append gt_marc.

    gt_mfieldres-tranc = g_cnt_tranc.
    gt_mfieldres-d_ind = g_cnt_d_ind.

    move 'MARC-KZAUS' to gt_mfieldres-fname.
    append gt_mfieldres.
    move 'MARC-AUSDT' to gt_mfieldres-fname.
    append gt_mfieldres.
    move 'MARC-NFMAT' to gt_mfieldres-fname.
    append gt_mfieldres.

  endif.

  perform change_material using gt_marc-matnr gt_marc-werks.

endform.                    "copy_material

*&---------------------------------------------------------------------*
*&      Form  CHANGE_MATERIAL
*&---------------------------------------------------------------------*
*       Call DI FM 'MATERIAL_MAINTAIN_DARK' to change the Material
*----------------------------------------------------------------------*
form change_material using r_matnr r_werks.

* Change Material
  call function 'MATERIAL_MAINTAIN_DARK'
    EXPORTING
      p_kz_no_warn              = 'N'
      kz_prf                    = 'W'
    IMPORTING
      number_errors_transaction = g_errors
    TABLES
      amara_ueb                 = gt_mara
      amarc_ueb                 = gt_marc
      amerrdat                  = gt_messtab
      amfieldres                = gt_mfieldres
    EXCEPTIONS
      kstatus_empty             = 1
      tkstatus_empty            = 2
      t130m_error               = 3
      internal_error            = 4
      too_many_errors           = 5
      update_error              = 6
      others                    = 7.

  if sy-subrc eq 0.
    if g_errors ne 0.
* Add messages to output table
      loop at gt_messtab.
        clear g_output.
        if gt_messtab-msgty eq 'E' or gt_messtab-msgty eq 'A'.
          move 'X' to g_output-errflg.
        endif.
        move r_matnr to g_output-matnr.
        move r_werks to g_output-werks.

        perform prepare_message_text using    gt_messtab
                                     changing g_output-text.
        append g_output.
      endloop.
    else.
      commit work and wait.
      clear g_output.
      move r_matnr to g_output-matnr.
      move r_werks to g_output-werks.
      append g_output.
    endif.
  else.
    rollback work.
* Add message to output table
    clear g_output.
    move 'X'      to g_output-errflg.
    move r_matnr  to g_output-matnr.
    move r_werks  to g_output-werks.
    move text-e04 to g_output-text.
    append g_output.
  endif.

* Refresh interface tables
  clear: gt_mara, gt_marc, gt_messtab, gt_mfieldres, g_errors.
  refresh: gt_mara, gt_marc, gt_mfieldres, gt_messtab.

endform.                    "change_material

*&---------------------------------------------------------------------*
*&      Form  PREPARE_MESSAGE_TEXT
*&---------------------------------------------------------------------*
*       Format message with variables
*----------------------------------------------------------------------*
*  -->  P_MESSTAB  Messages table header line
*  <--  R_TEXT     Message text
*----------------------------------------------------------------------*
form prepare_message_text using    p_messtab like merrdat
                          changing r_text.

  clear r_text.
  g_msg_no = p_messtab-msgno.       "Type conflict!
  call function 'MESSAGE_PREPARE'
    EXPORTING
      language               = sy-langu
      msg_id                 = p_messtab-msgid
      msg_no                 = g_msg_no
      msg_var1               = p_messtab-msgv1
      msg_var2               = p_messtab-msgv2
      msg_var3               = p_messtab-msgv3
      msg_var4               = p_messtab-msgv4
    IMPORTING
      msg_text               = r_text
    EXCEPTIONS
      function_not_completed = 1
      message_not_found      = 2
      others                 = 3.

  if sy-subrc ne 0.
*   message id sy-msgid type sy-msgty number sy-msgno
*           with sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
    concatenate p_messtab-msgty p_messtab-msgid p_messtab-msgno
                into r_text separated by space.
  else.
    concatenate p_messtab-msgty p_messtab-msgid p_messtab-msgno r_text
                into r_text separated by space.
  endif.

endform.                    " PREPARE_MESSAGE_TEXT

*&---------------------------------------------------------------------*
*&      Form  WRITE_OUTPUT
*&---------------------------------------------------------------------*
*       Output the control report
*----------------------------------------------------------------------*
form write_output.

  sort g_output by matnr werks.

* Output
  loop at g_output where errflg eq 'X'.

    at new werks.
      write:/ text-002.          "Error occured during call transaction
    endat.

    write:/ g_output-matnr, g_output-werks, g_output-text.

    at end of werks.
      uline.
    endat.

  endloop.

endform.                    " WRITE_OUTPUT

*Text symbol text£º
*001:Copy is successful
*002:Error occured during call transaction
*010:Selection
*020:Action
*E01:Enter a valid material number
*E02:Plant FROM doesn't exist
*E03:Plant FROM must be filled
*E04:Update material failed !!!
*E07:Plant FROM does not exist to copy from
*E08:Supersession data is not filled in Plant-FROM
*E10:No materials selected

*E11:No materials/plants selected
*Selection text£º
*P_CLEAR:        Clear fields in all plants
*P_COPY:        Copy fields from Plant-FROM
*P_WERKFR:        Plant-FROM
*S_MATNR:D       Material
*S_MSTAE:D       X-plant matl status
