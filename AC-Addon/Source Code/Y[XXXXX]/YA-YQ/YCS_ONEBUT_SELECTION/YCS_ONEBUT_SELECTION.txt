*----------------------------------------------------------------------*
* PROGRAM ID           : YCS_ONEBUT_SELECTION                          *
* PROGRAM TITLE        : Selection of service orders                   *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 02/08/2005                                    *
* DEVELOPMENT ID       :                                               *
* CHANGE REQUEST NUMBER: CD1K963718                                    *
* PROGRAM DESCRIPTION  : Selection of service orders                   *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG
*
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME                 |CORRECTION NR| CHANGE REF #
*----------------------------------------------------------------------*
* MOD-001 |20110825  | Geert Rutten         |CD1K967421   | CR2117     *
* MOD-002 |20120328  | Geert Rutten         |CD1K971019   | CR2461     *
* MOD-003 |20120522  | Geert Rutten         |CD1K971846   | CR2306     *
*----------------------------------------------------------------------*
REPORT ycs_onebut_selection NO STANDARD PAGE HEADING
                                LINE-SIZE 100.

TABLES: aufk,                                " Order Master data
* Begin of insert MOD-001
        afko,
* End of insert MOD-001
* Begin of insert MOD-003
        crhd,
        alm_me_d997,
        knvv,
* End of insert MOD-003
        ycs_onebut_sel.

SELECT-OPTIONS: s_aufnr  FOR aufk-aufnr,
                s_bukrs  FOR aufk-bukrs,
                s_iwerk  FOR aufk-werks,
                s_erdat  FOR aufk-erdat,
* Begin of insert MOD-001
                s_gltrs  FOR afko-gltrs.
* End of insert MOD-001

* Begin of insert MOD-001

TYPES: BEGIN OF t_fplt,
         fplnr TYPE fplnr,
         fpltr TYPE fpltr,
         tetxt TYPE tetxt,
         fksaf TYPE fksaf.
TYPES: END OF t_fplt.

TYPES: BEGIN OF t_fpla,
        fplnr TYPE fplnr.
TYPES: END OF t_fpla.

* End of insert MOD-001

DATA: rp_mcfound TYPE boolean,
      rp_tcfound TYPE boolean.
DATA: rp_mcfound2 TYPE boolean,
      rp_tcfound2 TYPE boolean.

DATA: BEGIN OF gt_aufk OCCURS 0,
        aufnr LIKE aufk-aufnr,
        auart TYPE aufk-auart,
* Begin of insert MOD-003
        auart_c TYPE vbak-auart,
        bezei_c TYPE tvakt-bezei,
        eqktx  TYPE ktx01,
        zzrem  TYPE zzrem,
* End of insert MOD-003
        erdat TYPE aufk-erdat,
        bukrs TYPE bukrs,
        iwerk TYPE afih-iwerk,
        vkorg TYPE vkorg,
        sttxt TYPE co_sttxt,
        bemot TYPE bemot,
        objnr TYPE aufk-objnr,
        kdauf TYPE aufk-kdauf,
        kdpos TYPE aufk-kdpos,
        maufnr TYPE afko-maufnr,
        ingpr TYPE afih-ingpr,
        kunum TYPE afih-kunum,
        vaplz TYPE gewrk,
        ilart TYPE ila,
        ktext TYPE aufk-ktext,
        serialnr TYPE gernr,
        getri TYPE co_getri,
        zzcom TYPE zzcom,
        qmnum TYPE qmnum,
        gltrs TYPE co_gltrs,
        ctam,
        go,
      END OF gt_aufk,
      ls_aufk LIKE gt_aufk.

DATA: BEGIN OF gt_afko OCCURS 0,
        aufnr  TYPE aufnr,
        maufnr TYPE maufnr.
DATA: END OF gt_afko.

DATA: BEGIN OF gt_seo OCCURS 0,
        kdauf  TYPE aufk-kdauf,
        aufnr  TYPE aufnr,
        del(1) TYPE c.
DATA: END OF gt_seo.

DATA: BEGIN OF lt_ycs_onebut_sel OCCURS 0.
        INCLUDE STRUCTURE ycs_onebut_sel.
DATA: END OF lt_ycs_onebut_sel.

DATA: BEGIN OF h_status_tab OCCURS 20.
        INCLUDE STRUCTURE jstat.
DATA: END OF h_status_tab.

DATA: BEGIN OF h_status_text_tab OCCURS 20,
        txt04 LIKE tj02t-txt04.
DATA: END OF h_status_text_tab.

DATA: lt_yam_ctam_ccodes TYPE STANDARD TABLE OF yam_ctam_ccodes.

DATA: it_bal_fil          TYPE bal_s_lfil OCCURS 0 WITH HEADER LINE,
      it_bal_hdr          TYPE balhdr_t,
      ls_bal_hdr          LIKE LINE OF it_bal_hdr,
      l_aldate            LIKE bal_s_date,
      lv_lines            TYPE sy-index,
      it_objectentry      LIKE bal_s_obj OCCURS 0 WITH HEADER LINE,
      it_extnnumberentry  LIKE bal_s_extn OCCURS 0 WITH HEADER LINE.

DATA: gv_objnr        TYPE j_objnr,
      gv_ctam(1)      TYPE c,
      gv_datum        TYPE datum,
      gv_vbeln        TYPE vbak-vbeln,
      gv_netwr        TYPE vbak-netwr,
      gv_invoice(1)   TYPE c,
      gv_nok(1)       TYPE c,               " X = not OK.
      h_stat_flag_cnf(1)   TYPE c,
      h_stat_flag_pcnf(1)   TYPE c,
      h_stat_flag_teco(1)  TYPE c,
      gv_skip(1)      TYPE c,
      gv_sttxt(40)    TYPE c,
      gv_fksak_so     TYPE vbuk-fksak,
      gv_fksak_dmr    TYPE vbuk-fksak.

* Begin of insert MOD-001
DATA: i_fpla  TYPE STANDARD TABLE OF t_fpla.
DATA: i_fplt  TYPE STANDARD TABLE OF t_fplt.
DATA: wa_fpla  TYPE t_fpla.
DATA: wa_fplt  TYPE t_fplt.
DATA: gv_fplnr  TYPE fplnr,
      gv_counter TYPE i,
      gv_flag TYPE i.
* End of insert MOD-001
* Begin of insert MOD-003
DATA: lv_vbeln_c TYPE vbeln,
      lv_sdaufnr TYPE viser02-sdaufnr,
      lv_matkl TYPE matkl,
      lv_vgbel TYPE vgbel,
      lv_vgpos TYPE vgpos,
      lv_equnr   TYPE equnr,
      lv_go      TYPE c,
      lv_kdgrp   TYPE kdgrp,
      gt_operation TYPE TABLE OF bapi_alm_order_operation_e WITH HEADER LINE,
      gt_return TYPE TABLE OF bapiret2,
      lv_hours TYPE arbeit.
* End of insert MOD-003

*-----------------------------------------------------------------------
INITIALIZATION.

  SELECT * FROM yam_ctam_ccodes INTO TABLE lt_yam_ctam_ccodes.


*-----------------------------------------------------------------------
START-OF-SELECTION.

  SELECT aufnr auart erdat bukrs iwerk bemot objnr kdauf kdpos maufnr vkorg
         ingpr kunum vaplz ilart ktext serialnr getri zzcom qmnum gltrs
* Begin of insert MOD-003
         zzrem
* End of insert MOD-003
    INTO CORRESPONDING FIELDS OF TABLE gt_aufk
     FROM viaufkst
     WHERE aufnr IN s_aufnr
       AND bukrs IN s_bukrs
       AND werks IN s_iwerk
       AND auart NE 'ZSM5'
       AND erdat IN s_erdat
* Begin of insert MOD-001
       AND gltrs IN s_gltrs
* End of insert MOD-001
       AND loekz NE 'X'
       AND iphas NE '6'.


*-----------------------------------------------------------------------
END-OF-SELECTION.

  CLEAR: gt_aufk,
         gv_ctam.

  gv_datum = sy-datum - 1.


* Begin of insert MOD-003

  LOOP AT gt_aufk.


* Get equipment description
    CLEAR lv_equnr.
    SELECT SINGLE equnr
     FROM afih INTO lv_equnr
    WHERE aufnr = gt_aufk-aufnr.

    SELECT SINGLE eqktx  INTO gt_aufk-eqktx
    FROM eqkt WHERE equnr = lv_equnr
    AND spras = sy-langu.
    gt_aufk-go = 'X'.
    MODIFY gt_aufk TRANSPORTING eqktx
     go.

* Retrieve contract Type
*    READ TABLE lt_yam_ctam_ccodes WITH KEY bukrs = gt_aufk-bukrs
*         BINARY SEARCH TRANSPORTING NO FIELDS.
*
*    IF sy-subrc = 0.

    CLEAR: lv_vbeln_c, lv_sdaufnr.
*      IF NOT gt_aufk-kdauf IS INITIAL.
*        SELECT SINGLE auart FROM vbak INTO gt_aufk-auart_c
*        WHERE vbeln = gt_aufk-kdauf.
*        MODIFY gt_aufk TRANSPORTING auart_c.
*      ELSE.
    SELECT sdaufnr FROM viser02 INTO (lv_sdaufnr)
    WHERE equnr = lv_equnr.
      IF sy-subrc = 0.
        SELECT SINGLE auart FROM vbak INTO gt_aufk-auart_c
          WHERE vbeln = lv_sdaufnr
          AND   gueen > sy-datum
          AND   vbtyp = 'G'.
        IF sy-subrc = '0'.
          MODIFY gt_aufk TRANSPORTING auart_c.
          EXIT.
        ENDIF.
      ENDIF.
    ENDSELECT.
*      ENDIF.

    IF gt_aufk-auart_c IS NOT INITIAL.
      SELECT SINGLE bezei FROM tvakt INTO gt_aufk-bezei_c
        WHERE spras = sy-langu AND
              auart = gt_aufk-auart_c.
      MODIFY gt_aufk TRANSPORTING bezei_c.
    ENDIF.

*    ELSE.
*
*      CLEAR: lv_vbeln_c, lv_matkl, lv_vgbel, lv_vgpos.
*      SELECT SINGLE vgbel vgpos FROM vbap INTO (lv_vgbel, lv_vgpos)
*      WHERE vbeln = gt_aufk-kdauf AND posnr = gt_aufk-kdpos.
*
*
*      IF lv_vgbel IS NOT INITIAL.
*
*
*        SELECT SINGLE auart FROM vbak INTO gt_aufk-auart_c
*        WHERE vbeln = lv_vgbel AND vbtyp = 'G'.
*
*        IF sy-subrc = 0.
*
*          MODIFY gt_aufk TRANSPORTING auart_c.
*
*          SELECT SINGLE matkl INTO lv_matkl
*          FROM vbap
*          WHERE vbeln = lv_vgbel AND posnr = lv_vgpos.
*
*          SELECT SINGLE wgbez FROM t023t INTO gt_aufk-bezei_c
*           WHERE matkl = lv_matkl AND spras = sy-langu.
*
*          MODIFY gt_aufk TRANSPORTING bezei_c.
*        ENDIF.
*      ENDIF.
*    ENDIF.

  ENDLOOP.

* End of insert MOD-003

  LOOP AT gt_aufk.
*.. get statusses
    CALL FUNCTION 'STATUS_TEXT_EDIT'
           EXPORTING
                objnr           = gt_aufk-objnr
                spras           = sy-langu
*                flg_user_stat   = 'X'
                only_active     = 'X'
           IMPORTING
                line            = gt_aufk-sttxt
*                user_line       =
           EXCEPTIONS
                object_not_found.

*.. Determine CT-AM / SEED
    READ TABLE lt_yam_ctam_ccodes WITH KEY bukrs = gt_aufk-bukrs
          BINARY SEARCH TRANSPORTING NO FIELDS.

    IF sy-subrc = 0.
      gv_ctam = 'X'.
    ELSE.
      CLEAR gv_ctam.
    ENDIF.
* Begin of insert MOD-003
    CLEAR: lv_go, lv_kdgrp.
* Dealers
    IF lv_go <> 'X'.
      SELECT SINGLE kdgrp
        FROM yse_dealer INTO lv_kdgrp
        WHERE vkorg = gt_aufk-vkorg.
      IF sy-subrc = 0.

        SELECT SINGLE *
          FROM knvv
          WHERE kunnr = gt_aufk-kunum AND kdgrp = lv_kdgrp.
        IF sy-subrc = 0 AND ( gt_aufk-sttxt CS 'REL ' ).
          CALL FUNCTION 'STATUS_CHECK'
            EXPORTING
              objnr             = gt_aufk-objnr
              status            = 'E0010'
            EXCEPTIONS
              object_not_found  = 1
              status_not_active = 2
              OTHERS            = 3.

          IF sy-subrc EQ 0.  " apav status
            lv_go = 'X'.
          ENDIF.
        ENDIF.

      ENDIF.

    ENDIF.

*Orders for FSE's without MAM

    IF lv_go <> 'X'.
      SELECT SINGLE * FROM crhd
        WHERE arbpl = gt_aufk-vaplz AND
              stand = 'FSE'.
      IF sy-subrc = 0.
        SELECT SINGLE * FROM alm_me_d997
          WHERE arbpl = gt_aufk-vaplz.
        IF sy-subrc <> 0 AND ( gt_aufk-sttxt CS 'REL ' ).

          lv_go = 'X'.

        ENDIF.
      ENDIF.
    ENDIF.

* Subcontracting

    IF lv_go <> 'X' AND ( gt_aufk-sttxt CS 'REL ' ).
*      lv_go = 'X'.
      CALL FUNCTION 'BAPI_ALM_ORDER_GET_DETAIL'
        EXPORTING
          number                 = gt_aufk-aufnr
*     IMPORTING
*       es_header               = wa_header
*       es_srvdata              = wa_srvdata
        TABLES
*       et_partner              = gt_partner
         et_operations           = gt_operation
*       ET_COMPONENTS          =
*       ET_RELATIONS           =
*       ET_SRULES              =
*       ET_OLIST               =
*       ET_OPROL               =
*       ET_TEXTS               =
*       ET_TEXT_LINES          =
*       ET_PRTS                =
*       ET_COSTS_SUM           =
*       ET_COSTS_DETAILS       =
          return                 = gt_return.
*       EXTENSION_IN           =
*       EXTENSION_OUT          =        .
      CLEAR lv_hours.
      LOOP AT gt_operation.
        IF gt_operation-duration_normal_unit = 'STD'.
          lv_hours = lv_hours + gt_operation-work_activity.
        ENDIF.
      ENDLOOP.

      LOOP AT gt_operation.
        IF gt_operation-acttype = 'ZAM019' AND lv_hours <= 1.
          lv_go = 'X'.
          EXIT.
        ENDIF.
      ENDLOOP.
    ENDIF.

    IF lv_go <> 'X'.

* End of insert MOD-003
*.. Select on statusses
      CASE gv_ctam.
        WHEN 'X'.          " CT-AM.
          IF gt_aufk-sttxt CS ' CNF' OR gt_aufk-sttxt CS 'PCNF'.
            CLEAR: rp_tcfound, rp_mcfound.
            PERFORM check_conf USING gt_aufk-aufnr
                               CHANGING rp_tcfound
                                        rp_mcfound.
            CLEAR: rp_tcfound2, rp_mcfound2.
            PERFORM check_conf2 USING gt_aufk-aufnr
                               CHANGING rp_tcfound2
                                        rp_mcfound2.
            IF gt_aufk-sttxt CS 'TECO'.
              IF gt_aufk-bemot = 'FP' OR gt_aufk-bemot = 'CH' OR rp_tcfound = 'X' OR rp_mcfound = 'X'.
               IF ( gt_aufk-bemot = 'FP' OR gt_aufk-bemot = 'CH' ) AND  rp_tcfound2 = ' ' AND rp_mcfound2 = ' '.
                  DELETE gt_aufk.
                  CONTINUE.
                ENDIF.
                " test on invoice
                CLEAR gv_invoice.
                PERFORM check_invoice    USING gt_aufk-aufnr
                                               gt_aufk-bemot
                                      CHANGING gv_invoice.
                IF gv_invoice = 'X'.
                  DELETE gt_aufk.
                  CONTINUE.
                ENDIF.
              ELSE.
                DELETE gt_aufk.
                CONTINUE.
              ENDIF.
            ELSE.
              " test on invoice
              CLEAR gv_invoice.
              PERFORM check_invoice    USING gt_aufk-aufnr
                                             gt_aufk-bemot
                                    CHANGING gv_invoice.
              IF gv_invoice = 'X'.
                DELETE gt_aufk.
                CONTINUE.
              ENDIF.
            ENDIF.
          ELSE.
            DELETE gt_aufk.
            CONTINUE.
          ENDIF.
          CLEAR gt_aufk-kdauf.
        WHEN OTHERS.       " SEED.
          IF gt_aufk-sttxt CS ' CNF' OR gt_aufk-sttxt CS 'PCNF'.
            IF gt_aufk-sttxt CS 'TECO'.
              DELETE gt_aufk.
              CONTINUE.
            ELSE.
* Begin of insert MOD-001
              IF gt_aufk-bukrs <> 'MRUA' OR ( gt_aufk-bukrs = 'MRUA' AND gt_aufk-ilart <> 'FP' ) .
* End of insert MOD-001
                " check SO and DMR header billing status
                SELECT SINGLE fksak INTO gv_fksak_so
                  FROM vbuk WHERE vbeln = gt_aufk-kdauf.

                IF sy-subrc = 0.
                  SELECT SINGLE vbeln INTO gv_vbeln
                    FROM vbfa WHERE vbelv = gt_aufk-kdauf
                                AND vbtyp_n EQ 'L'             "debit memo request
                                AND vbtyp_v EQ 'C'.            "order

                  IF sy-subrc = 0.             " DMR found
                    SELECT SINGLE fksak INTO gv_fksak_dmr
                      FROM vbuk WHERE vbeln = gv_vbeln.

                    IF gv_fksak_dmr = 'C'.           " DMR completely invoiced
                      DELETE gt_aufk.
                      CONTINUE.
                    ENDIF.
                  ELSE.                        " no DMR found
                    IF gv_fksak_so = 'C'.            " SO completely invoiced
                      DELETE gt_aufk.
                      CONTINUE.
                    ENDIF.
                  ENDIF.
                ENDIF.
* Begin of insert MOD-001
              ELSE.
                " Russia the orders never reach status C due to the billing plan setup.  The
                " Z006 payment request line is often not used and remains on status A.

                CLEAR gv_fplnr.
                CLEAR gv_counter.
                SELECT SINGLE fplnr INTO gv_fplnr
                FROM vbkd WHERE vbeln = gt_aufk-kdauf
                          AND posnr = gt_aufk-kdpos.

                SELECT fplnr
                FROM fpla
                 INTO TABLE i_fpla
                 WHERE vbeln = gt_aufk-kdauf.

                IF sy-subrc EQ 0.  "Billing plan found
                  SELECT fplnr fpltr tetxt fksaf
                  FROM fplt
                   INTO TABLE i_fplt
                   FOR ALL ENTRIES IN i_fpla
                   WHERE fplnr = i_fpla-fplnr.
                  IF sy-subrc EQ 0.  "Billing plan dates found
***
                    IF sy-dbcnt > 1.
                      SORT i_fplt BY fplnr fpltr.
                      LOOP AT i_fplt INTO wa_fplt.
                        IF wa_fplt-fplnr NE gv_fplnr.
                          CONTINUE.
                        ENDIF.
                        IF wa_fplt-fksaf NE 'C' AND
                           wa_fplt-tetxt NE 'Z006'.   "Payment request
                          gv_counter = gv_counter + 1.
                        ENDIF.    "Not fully billed yet
                      ENDLOOP.
                    ELSE.
                      LOOP AT i_fplt INTO wa_fplt.
                        IF wa_fplt-fksaf NE 'C' AND
                           wa_fplt-tetxt NE 'Z006'.   "Payment request
                          gv_counter = gv_counter + 1.
                        ENDIF.    "Not fully billed yet
                      ENDLOOP.
                    ENDIF.
                    IF gv_counter = 0.
                      DELETE gt_aufk.
                      CONTINUE.
                    ENDIF.

***
                  ENDIF.
                ELSE. "no billing plan found
                ENDIF.

              ENDIF.
* End of insert MOD-001
            ENDIF.
          ELSE.
            DELETE gt_aufk.
            CONTINUE.
          ENDIF.
      ENDCASE.
* Begin of insert MOD-003
      gt_aufk-go = ' '.
      MODIFY gt_aufk TRANSPORTING go.
* End of insert MOD-003
    ENDIF.


    MOVE gv_ctam TO gt_aufk-ctam.
    MODIFY gt_aufk TRANSPORTING sttxt ctam kdauf.

*.. Put Suborders in separate table
    IF NOT gt_aufk-maufnr IS INITIAL.
      MOVE gt_aufk-aufnr  TO gt_afko-aufnr.
      MOVE gt_aufk-maufnr TO gt_afko-maufnr.
      APPEND gt_afko.
    ENDIF.
  ENDLOOP.



* Loop again in order to treat several SEO linked to one SO.
  LOOP AT gt_aufk WHERE NOT kdauf IS INITIAL
* Begin of insert MOD-003
  AND ctam = ' ' AND go = ' '.
* End of insert MOD-003


    REFRESH gt_seo.
    SELECT aufnr kdauf FROM aufk
      INTO CORRESPONDING FIELDS OF TABLE gt_seo
      WHERE kdauf = gt_aufk-kdauf
* Begin of insert MOD-003
      AND aufnr <> gt_aufk-aufnr.
* End of insert MOD-003

    IF NOT gt_seo[] IS INITIAL.
      LOOP AT gt_seo.
        CLEAR gt_seo-del.
        REFRESH: h_status_tab.
        CONCATENATE 'OR' gt_seo-aufnr INTO gv_objnr.

        CALL FUNCTION 'STATUS_READ'
          EXPORTING
            objnr            = gv_objnr
            only_active      = 'X'
          TABLES
            status           = h_status_tab
          EXCEPTIONS
            object_not_found = 01.

        CLEAR: h_stat_flag_cnf,
               h_stat_flag_pcnf,
               h_stat_flag_teco,
               gv_skip.

        LOOP AT h_status_tab.
          IF h_status_tab-stat EQ 'I0009'.
            h_stat_flag_cnf = 'X'.
          ENDIF.
          IF h_status_tab-stat EQ 'I0010'.
            h_stat_flag_pcnf = 'X'.
          ENDIF.
          IF h_status_tab-stat EQ 'I0045'.
            h_stat_flag_teco = 'X'.
          ENDIF.
        ENDLOOP.

        IF h_stat_flag_teco = 'X'.
          gv_skip = 'X'.
        ENDIF.

        IF h_stat_flag_cnf <> 'X' AND h_stat_flag_pcnf <> 'X'.
          gv_skip = 'X'.
        ENDIF.

        IF gv_skip = 'X'.
          gt_seo-del = 'X'.
          MODIFY gt_seo TRANSPORTING del.
        ENDIF.
      ENDLOOP.
    ENDIF.
  ENDLOOP.



* Delete evt. service orders
  SORT gt_aufk BY aufnr.
  SORT gt_seo BY kdauf aufnr.

  LOOP AT gt_seo WHERE del = 'X'.
    LOOP AT gt_aufk WHERE kdauf = gt_seo-kdauf.
      DELETE gt_aufk.
    ENDLOOP.
  ENDLOOP.


* Loop over Suborders
  LOOP AT gt_afko.
*.. get statusses
    CONCATENATE 'OR' gt_afko-maufnr INTO gv_objnr.

    CALL FUNCTION 'STATUS_TEXT_EDIT'
           EXPORTING
                objnr           = gv_objnr
                spras           = sy-langu
*                flg_user_stat   = 'X'
                only_active     = 'X'
           IMPORTING
                line            = gv_sttxt
*                user_line       =
           EXCEPTIONS
                object_not_found.

    IF gv_sttxt CS 'TECO'.
      CONTINUE.
    ELSE.
      READ TABLE gt_aufk WITH KEY aufnr = gt_afko-maufnr
          BINARY SEARCH.

      IF sy-subrc = 0.
        DELETE gt_aufk INDEX sy-tabix.
      ENDIF.
    ENDIF.
  ENDLOOP.

* Read only once all messages for this object/subobject in the application log
  IF NOT gt_aufk[] IS INITIAL.
    REFRESH: it_bal_fil,
             it_bal_hdr.

*    CLEAR it_extnnumberentry.
*    it_extnnumberentry-sign = 'I'.
*    it_extnnumberentry-option = 'EQ'.
*    it_extnnumberentry-low = ls_aufk-aufnr.
*    APPEND it_extnnumberentry TO it_bal_fil-extnumber.

    CLEAR it_extnnumberentry.
    l_aldate-low  = sy-datum - 14.
*    l_aldate-high = sy-datum.
    it_extnnumberentry-sign = 'I'.
    it_extnnumberentry-option = 'GT'.
    it_extnnumberentry-low  = l_aldate-low.
*    it_extnnumberentry-high = l_aldate-high.
    APPEND it_extnnumberentry TO it_bal_fil-aldate.

    CLEAR it_objectentry.
    it_objectentry-sign = 'I'.
    it_objectentry-option = 'EQ'.
    it_objectentry-low = 'YCS_ONE_BUTTON'.
    APPEND it_objectentry TO it_bal_fil-object.

    CLEAR it_objectentry.
    it_objectentry-sign = 'I'.
    it_objectentry-option = 'EQ'.
    it_objectentry-low = 'YCS_ONE_BUTTON'.
    APPEND it_objectentry TO it_bal_fil-subobject.

    CALL FUNCTION 'BAL_DB_SEARCH'
      EXPORTING
        i_client           = sy-mandt
        i_s_log_filter     = it_bal_fil
      IMPORTING
        e_t_log_header     = it_bal_hdr
      EXCEPTIONS
        log_not_found      = 1
        no_filter_criteria = 2
        OTHERS             = 3.
  ENDIF.

  LOOP AT gt_aufk INTO ls_aufk.
    CLEAR lt_ycs_onebut_sel.
    MOVE-CORRESPONDING ls_aufk TO lt_ycs_onebut_sel.

*.. Get existing error messages from closing job
    SELECT checklist INTO lt_ycs_onebut_sel-checklist
      FROM yse_close_log
      WHERE aufnr = ls_aufk-aufnr
*        AND datum = sy-datum.
        AND datum = gv_datum.
    ENDSELECT.

* Begin of insert MOD-001
    CLEAR: gv_flag.
    IF ls_aufk-ctam = 'X'.
      PERFORM check1_apav.
    ENDIF.
    PERFORM check2_cred_lim_exc.
    PERFORM check3_dam_cod.


* End of insert MOD-001

*.. Get userstatus
    PERFORM get_userstatus USING ls_aufk-objnr
                           CHANGING lt_ycs_onebut_sel-stat_subc
                                    lt_ycs_onebut_sel-stat_back.

*.. Get 'blocked for credit'
    PERFORM get_credit USING ls_aufk-kunum ls_aufk-bukrs
                       CHANGING lt_ycs_onebut_sel-crblb.

*.. Get bill-to name
    PERFORM get_billto_name USING ls_aufk-objnr
                            CHANGING lt_ycs_onebut_sel-billtoname1.

*.. Get sales data
    IF ls_aufk-ctam = ' '.
      SELECT SINGLE vkbur vkgrp
        INTO (lt_ycs_onebut_sel-vkbur, lt_ycs_onebut_sel-vkgrp)
        FROM vbak WHERE vbeln = ls_aufk-kdauf.

      IF sy-subrc = 0.
        SELECT SINGLE bstkd
          INTO lt_ycs_onebut_sel-bstkd
          FROM vbkd WHERE vbeln = ls_aufk-kdauf
                      AND posnr = '000000'.

        SELECT SINGLE netwr
          INTO lt_ycs_onebut_sel-netwr
          FROM vbap WHERE vbeln = ls_aufk-kdauf
                      AND posnr = ls_aufk-kdpos.
      ENDIF.
    ENDIF.
* Begin of MOD-002
* Get P.O. data for CTAM
    IF ls_aufk-ctam = 'X '.
      SELECT SINGLE bstkd INTO lt_ycs_onebut_sel-bstkd
        FROM pmsdo
        WHERE objnr = ls_aufk-objnr.
    ENDIF.
* End of MOD-002
*.. Get notification malfunction end date
    IF ls_aufk-ctam = ' '.
      SELECT SINGLE ausbs INTO lt_ycs_onebut_sel-ausbs
        FROM qmih WHERE qmnum = ls_aufk-qmnum.
    ENDIF.

*.. Get actual cost of SEO
    IF ls_aufk-ctam = 'X'.
      PERFORM get_act_cost USING ls_aufk-aufnr
                           CHANGING lt_ycs_onebut_sel-actcost.
    ENDIF.

*.. Get DMR data
    IF ls_aufk-ctam = 'X'.
      PERFORM get_dmr_am USING ls_aufk-aufnr ls_aufk-bemot
                         CHANGING lt_ycs_onebut_sel-dmr_erdat
                                  gv_netwr.
      IF ls_aufk-bemot = 'FP' OR
         ls_aufk-bemot = 'CH' OR
         rp_tcfound = 'X'     OR rp_mcfound = 'X' .
        lt_ycs_onebut_sel-dmr_netwr = gv_netwr.
      ENDIF.
    ELSE.
      PERFORM get_dmr_seed USING ls_aufk-kdauf
                           CHANGING lt_ycs_onebut_sel-dmr_erdat.
    ENDIF.

*.. Check for errormessages in the application log (SLG1)
*.. --> check last one
    IF NOT it_bal_hdr[] IS INITIAL.
      LOOP AT it_bal_hdr INTO ls_bal_hdr WHERE    object = 'YCS_ONE_BUTTON'
                                           AND subobject = 'YCS_ONE_BUTTON'
                                           AND extnumber = ls_aufk-aufnr.
      ENDLOOP.

      IF sy-subrc = 0 AND
        NOT ( ls_bal_hdr-msg_cnt_a IS INITIAL AND ls_bal_hdr-msg_cnt_e IS INITIAL ).
        lt_ycs_onebut_sel-applog_err = 'X'.
      ENDIF.
    ENDIF.

    APPEND lt_ycs_onebut_sel.
    CLEAR lt_ycs_onebut_sel.
  ENDLOOP.


  DELETE FROM ycs_onebut_sel WHERE aufnr IN s_aufnr
                               AND bukrs IN s_bukrs
                               AND iwerk IN s_iwerk
                               AND erdat IN s_erdat.
* Begin of insert MOD-001
*                               AND gltrs IN s_gltrs.
* End of insert MOD-001

  CHECK NOT lt_ycs_onebut_sel[] IS INITIAL.
  INSERT ycs_onebut_sel FROM TABLE lt_ycs_onebut_sel.


*-----------------------------------------------------------------------
TOP-OF-PAGE.

  WRITE: sy-title, 65 sy-datum, 80 sy-uzeit, 95 sy-pagno.
  ULINE.
  SKIP.


*&---------------------------------------------------------------------*
*&      Form  CHECK_INVOICE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->R_AUFNR       service order number
*      -->R_BEMOT       accounting indicator
*      <--P_GV_INVOICE  'X': invoice exist
*----------------------------------------------------------------------*
FORM check_invoice    USING r_aufnr
                            r_bemot
                   CHANGING p_gv_invoice.

  DATA: lv_vbeln TYPE vbak-vbeln,
        lv_docnr TYPE vbak-vbeln,
        lv_count_p TYPE i,
        lv_count_n TYPE i.

  CLEAR p_gv_invoice.

  IF r_bemot = 'FP'.              " Fixed price
    SELECT vbeln INTO lv_docnr
      FROM vbak WHERE aufnr = r_aufnr
                  AND vbtyp = 'B'.                 "quotation
    ENDSELECT.

    IF sy-subrc = 0.
      SELECT vbeln INTO lv_vbeln
        FROM vbfa WHERE vbelv = lv_docnr
                    AND vbtyp_n EQ 'P'             "debit memo
                    AND vbtyp_v EQ 'B'.            "quotation
      ENDSELECT.
      IF sy-subrc = 0.
        SELECT COUNT(*) INTO lv_count_p
        FROM vbfa WHERE vbelv = lv_docnr
                    AND vbtyp_n EQ 'P'             "debit memo
                    AND vbtyp_v EQ 'B'.
        SELECT COUNT(*) INTO lv_count_n
        FROM vbfa WHERE vbelv = lv_docnr
                    AND vbtyp_n EQ 'N'             "debit memo
                    AND vbtyp_v EQ 'B'.
        IF lv_count_p > lv_count_n.
          p_gv_invoice = 'X'.
        ENDIF.
      ENDIF.
    ENDIF.
  ELSE.                           " other than Fixed price
    SELECT vbeln INTO lv_docnr
      FROM vbak WHERE aufnr = r_aufnr
                  AND vbtyp = 'L'.                 "debit memo request
    ENDSELECT.

    IF sy-subrc = 0.
      SELECT vbeln INTO lv_vbeln
        FROM vbfa WHERE vbelv = lv_docnr
                    AND vbtyp_n EQ 'P'             "debit memo
                    AND vbtyp_v EQ 'L'.            "debit memo request
      ENDSELECT.
      IF sy-subrc = 0.
        SELECT COUNT(*) INTO lv_count_p
          FROM vbfa WHERE vbelv = lv_docnr
                      AND vbtyp_n EQ 'P'             "debit memo
                      AND vbtyp_v EQ 'L'.            "debit memo request
        SELECT COUNT(*) INTO lv_count_n
          FROM vbfa WHERE vbelv = lv_docnr
                      AND vbtyp_n EQ 'N'             "debit memo
                      AND vbtyp_v EQ 'L'.            "debit memo request
        IF lv_count_p > lv_count_n.
          p_gv_invoice = 'X'.
        ENDIF.

      ENDIF.
    ENDIF.
  ENDIF.


ENDFORM.                    " CHECK_INVOICE
*&---------------------------------------------------------------------*
*&      Form  GET_USERSTATUS
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  r_objnr   order object number
*  <--  r_ustat1  user status SUBC
*  <--  r_ustat2  user status BACK
*----------------------------------------------------------------------*
FORM get_userstatus USING r_objnr
                    CHANGING r_ustat1 r_ustat2.


  DATA: lt_status     TYPE STANDARD TABLE OF jstat,
        ls_status     TYPE jstat,
        lv_txt04      TYPE j_txt04,
        lv_prof(8)    TYPE c.

  CLEAR: r_ustat1,
         r_ustat2.

  CALL FUNCTION 'STATUS_READ'
    EXPORTING
      objnr            = r_objnr
      only_active      = 'X'
    TABLES
      status           = lt_status
    EXCEPTIONS
      object_not_found = 01.

  IF sy-subrc = 0.
* Begin of insert MOD-001
    IF ls_aufk-ctam = 'X'.
      lv_prof = 'ZAM00001'.
    ELSE.
      lv_prof = 'ZAM00006'.
    ENDIF.
* End opf insert MOD-001
    LOOP AT lt_status INTO ls_status.
      CLEAR: lv_txt04.
      SELECT SINGLE txt04 FROM tj30t INTO lv_txt04
* Begin of change MOD-001
*        WHERE stsma = 'ZAM00001'  AND
         WHERE stsma = lv_prof    AND
* End of change MOD-001
          estat = ls_status-stat  AND
          spras = sy-langu.

      IF lv_txt04 = 'SUBC'.
        r_ustat1 = lv_txt04.
      ELSEIF lv_txt04 = 'BACK'.
        r_ustat2 = lv_txt04.
      ENDIF.
    ENDLOOP.
  ENDIF.

ENDFORM.                    " GET_USERSTATUS
*&---------------------------------------------------------------------*
*&      Form  GET_CREDIT
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  r_kunum   customer number
*  -->  r_bukrs   company code
*  <--  r_crblb   blocked for credit
*----------------------------------------------------------------------*
FORM get_credit USING r_kunum r_bukrs
                CHANGING r_crblb.

  CLEAR r_crblb.

  SELECT SINGLE crblb
     FROM knkk INTO r_crblb
     WHERE kunnr = r_kunum AND
           kkber = r_bukrs.

ENDFORM.                    " GET_CREDIT
*&---------------------------------------------------------------------*
*&      Form  GET_BILLTO_NAME
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_GT_AUFK_OBJNR  order object number
*      <--P_BILLTONAME1    bill-to name1
*----------------------------------------------------------------------*
FORM get_billto_name  USING    p_gt_aufk_objnr
                      CHANGING p_billtoname1.

  DATA: lv_parnr      TYPE i_parnr,
        lv_parvw      TYPE ihpa-parvw,
        lv_adrnr      TYPE ihpa-adrnr.
  DATA: ls_partners   TYPE yscs_partnerseo,
        ls_addr1_val  TYPE addr1_val,
        sel           TYPE addr1_sel,
        lv_adrnr_kna1 TYPE kna1-adrnr.

  CLEAR p_billtoname1.

  SELECT SINGLE parnr parvw adrnr FROM ihpa
    INTO (lv_parnr, lv_parvw, lv_adrnr)
    WHERE objnr    EQ p_gt_aufk_objnr
      AND parvw    EQ 'RE'
      AND kzloesch NE 'X'.

  IF NOT lv_parnr IS INITIAL.
    ls_partners-customer = lv_parnr.
    ls_partners-parvw    = lv_parvw.

    IF lv_adrnr IS NOT INITIAL.
      sel-addrnumber = lv_adrnr.
      CALL FUNCTION 'ADDR_GET'
        EXPORTING
          address_selection = sel
        IMPORTING
          address_value     = ls_addr1_val
        EXCEPTIONS
          parameter_error   = 1
          address_not_exist = 2
          version_not_exist = 3
          internal_error    = 4
          OTHERS            = 5.

      IF sy-subrc = 0.
        p_billtoname1 = ls_addr1_val-name1.
      ENDIF.
    ELSE.           " ihpa-adrnr is initial
      CLEAR lv_adrnr_kna1.
      SELECT SINGLE adrnr FROM kna1
        INTO lv_adrnr_kna1
        WHERE kunnr EQ ls_partners-customer.

      SELECT SINGLE name1 INTO p_billtoname1
        FROM adrc
        WHERE addrnumber = lv_adrnr_kna1
          AND nation     = ' '.
    ENDIF.
  ENDIF.

ENDFORM.                    " GET_BILLTO_NAME
*&---------------------------------------------------------------------*
*&      Form  GET_ACT_COST
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_GT_AUFK_AUFNR  text
*      <--P_LT_YCS_ONEBUT_SEL_ACTCOST  text
*----------------------------------------------------------------------*
FORM get_act_cost  USING    p_gt_aufk_aufnr
                   CHANGING p_lt_ycs_onebut_sel_actcost.

  DATA: lt_total_costs  TYPE STANDARD TABLE OF bapi_alm_order_costs_sum_e,
        ls_total_costs  TYPE bapi_alm_order_costs_sum_e,
        lt_return       TYPE STANDARD TABLE OF bapiret2.

  CALL FUNCTION 'BAPI_ALM_ORDER_GET_DETAIL'
    EXPORTING
      number       = p_gt_aufk_aufnr
    TABLES
      et_costs_sum = lt_total_costs
      return       = lt_return.

  IF NOT lt_total_costs[] IS INITIAL.
*.. It is assumed there will only be one line in the total cost table
    READ TABLE lt_total_costs INTO ls_total_costs INDEX 1.

    p_lt_ycs_onebut_sel_actcost = ls_total_costs-total_costs_act.
  ENDIF.

ENDFORM.                    " GET_ACT_COST
*&---------------------------------------------------------------------*
*&      Form  GET_DMR_AM
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  r_aufnr        service order number
*  -->  r_bemot        accounting indicator
*  <--  r_dmr_erdat    DMR creation date
*  <--  r_dmr_netwr    DMR net value
*----------------------------------------------------------------------*
FORM get_dmr_am USING r_aufnr r_bemot
                CHANGING r_dmr_erdat r_dmr_netwr.

  CLEAR: r_dmr_erdat,
         r_dmr_netwr.

  DATA: lv_vbeln TYPE vbak-vbeln,
        lv_docnr TYPE vbak-vbeln.

  IF r_bemot = 'FP'.              " Fixed price
    SELECT vbeln INTO lv_docnr
      FROM vbak WHERE aufnr = r_aufnr
                  AND vbtyp = 'B'.                 "quotation
    ENDSELECT.

    IF sy-subrc = 0.
      SELECT vbeln INTO lv_vbeln
        FROM vbfa WHERE vbelv = lv_docnr
                    AND vbtyp_n EQ 'L'             "debit memo request
                    AND vbtyp_v EQ 'B'.            "quotation
      ENDSELECT.
    ENDIF.
  ELSE.                           " Chargeable
    SELECT vbeln INTO lv_vbeln
      FROM vbak WHERE aufnr = r_aufnr
                  AND vbtyp = 'L'.                 "debit memo request
    ENDSELECT.
  ENDIF.

  IF sy-subrc = 0.               " DMR found
    SELECT SINGLE erdat netwr
      INTO (r_dmr_erdat, r_dmr_netwr)
      FROM vbak WHERE vbeln = lv_vbeln.
  ENDIF.

ENDFORM.                    " GET_DMR_AM
*&---------------------------------------------------------------------*
*&      Form  GET_DMR_SEED
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  r_vbeln        sales order number
*  <--  r_dmr_erdat    DMR creation date
*----------------------------------------------------------------------*
FORM get_dmr_seed USING r_vbeln
                CHANGING r_dmr_erdat.

  DATA: lv_vbeln TYPE vbak-vbeln.

  SELECT SINGLE vbeln INTO lv_vbeln
    FROM vbfa WHERE vbelv = r_vbeln
                AND vbtyp_n EQ 'L'             "debit memo request
                AND vbtyp_v EQ 'C'.            "order

  IF sy-subrc = 0.             " DMR found
    SELECT SINGLE erdat INTO r_dmr_erdat
      FROM vbak WHERE vbeln = lv_vbeln.
  ENDIF.

ENDFORM.                    " GET_DMR_SEED

* Begin of insert MOD-001
FORM check1_apav.

  DATA: lt_resb TYPE STANDARD TABLE OF resbdget INITIAL SIZE 0
                                              WITH HEADER LINE.
  DATA: lv_error TYPE c,
       lv_menge TYPE menge_d.
*.. Retrieve the components for the service order (skip the deleted
*.. ones)
  CALL FUNCTION 'CO_BC_RESBD_OF_ORDER_GET'
    EXPORTING
      aufnr_act            = ls_aufk-aufnr
      check_deleted        = 'X'
*        no_read              = 'X'
    TABLES
      resbd_get            = lt_resb.

  LOOP AT lt_resb.
*.... Check the status of the component is 280
    IF lt_resb-wempf+0(3) NE 280.
      lv_error = 'X'.
    ELSE.
*...... Check the entire quantity has status 280
      lv_menge = lt_resb-wempf+4.
      IF lv_menge LT lt_resb-bdmng.
        lv_error = 'X'.
      ENDIF.
    ENDIF.

  ENDLOOP.

  IF lv_error = 'X'.
    gv_flag = gv_flag + 1.
    IF lt_ycs_onebut_sel-checklist IS NOT INITIAL.
      IF gv_flag = 1.
        CONCATENATE lt_ycs_onebut_sel-checklist '//' text-t01 INTO lt_ycs_onebut_sel-checklist.
      ELSE.
        CONCATENATE lt_ycs_onebut_sel-checklist '/' text-t01 INTO lt_ycs_onebut_sel-checklist.
      ENDIF.
    ELSE.
      lt_ycs_onebut_sel-checklist = text-t01.
    ENDIF.
  ENDIF.
ENDFORM.                    "CHECK1_APAV

*&---------------------------------------------------------------------*
*&      Form  CHECK2_CRED_LIM_EXC
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM check2_cred_lim_exc.
  DATA: lv_error TYPE c,
        lv_crblb TYPE knkk-crblb,
        lv_klimk TYPE knkk-klimk,
        lv_ctlpc TYPE knkk-ctlpc,
        lv_knkli TYPE knkk-knkli,
        lv_skfor TYPE knkk-skfor,
        lv_ssobl TYPE knkk-ssobl,
        lv_oblig TYPE rf02l-oblig,
        lv_sauft TYPE knkk-sauft,
        lv_kkber TYPE knkk-kkber.


  IF ls_aufk-ctam = 'X'.
    lv_kkber = ls_aufk-bukrs.
  ELSE.
    SELECT SINGLE kkber FROM vbak INTO lv_kkber
      WHERE vbeln = gt_aufk-kdauf.
  ENDIF.

  SELECT SINGLE crblb klimk ctlpc knkli skfor ssobl
    INTO (lv_crblb, lv_klimk, lv_ctlpc, lv_knkli, lv_skfor, lv_ssobl)
    FROM knkk WHERE kunnr = lv_kkber
                AND kkber = ls_aufk-bukrs.

  PERFORM credit_exposure USING lv_sauft lv_ctlpc lv_knkli lv_kkber.
  lv_oblig = lv_sauft + lv_skfor + lv_ssobl.

  IF lv_oblig > lv_klimk.
    lv_error = 'X'.
  ENDIF.


  IF lv_error = 'X'.
    gv_flag = gv_flag + 1.

    IF lt_ycs_onebut_sel-checklist IS NOT INITIAL.
      IF gv_flag = 1.
        CONCATENATE lt_ycs_onebut_sel-checklist '//' text-t02 INTO lt_ycs_onebut_sel-checklist.
      ELSE.
        CONCATENATE lt_ycs_onebut_sel-checklist '/' text-t02 INTO lt_ycs_onebut_sel-checklist.
      ENDIF.
    ELSE.
      lt_ycs_onebut_sel-checklist = text-t02.
    ENDIF.
  ENDIF.
ENDFORM.                    "CHECK2_CRED_LIM_EXC

*&---------------------------------------------------------------------*
*&      Form  CHECK3_DAM_COD
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM check3_dam_cod.

  DATA: lv_error TYPE c,
       i_wiqmfe          LIKE wqmfe   OCCURS 0 WITH HEADER LINE.
  TABLES afru.


  IF gv_ctam = 'X'.
* Time confirmations
    SELECT SINGLE * FROM afru
      WHERE aufnr = ls_aufk-aufnr AND bemot = 'PG'
      AND stokz = ' ' AND
      stzhl = ' '.
    IF sy-subrc = 0.
      lv_error = 'A'.
    ENDIF.
  ELSE.
    SELECT SINGLE * FROM afru
      WHERE aufnr = ls_aufk-aufnr AND bemot = '1W'
      AND stokz = ' ' AND
          stzhl = ' '.
    IF sy-subrc = 0.
      lv_error = 'A'.
    ENDIF.
  ENDIF.

  IF lv_error = 'A'. " warrenty time confirmations exist so we need to check
*      further if the necessary damage codes exist


    CALL FUNCTION 'IQS4_GET_NOTIFICATION'
      EXPORTING
        i_qmnum     = ls_aufk-qmnum
      TABLES
        e_iviqmfe_t = i_wiqmfe[].
    IF sy-subrc EQ 0.

      LOOP AT i_wiqmfe.
        IF i_wiqmfe-otgrp = 'ZACO-020' AND
           i_wiqmfe-oteil = '098'      AND
           i_wiqmfe-fegrp = 'ZACD-020' AND
           i_wiqmfe-fecod = '098'.
          CONTINUE.
        ENDIF.

        IF i_wiqmfe-otgrp = 'ZACO-038' AND
           i_wiqmfe-oteil = '098'      AND
           i_wiqmfe-fegrp = 'ZACD-038' AND
           i_wiqmfe-fecod = '098'.
          CONTINUE.
        ENDIF.

        IF i_wiqmfe-fetxt(10) CO '1234567890' AND
           i_wiqmfe-fetxt+10(30) CO ' '.
          IF i_wiqmfe-otgrp IS INITIAL OR
             i_wiqmfe-oteil IS INITIAL OR
             i_wiqmfe-fegrp IS INITIAL OR
             i_wiqmfe-fecod IS INITIAL.
          ELSE.
            lv_error = 'N'.
            EXIT.
          ENDIF.
        ENDIF.
      ENDLOOP.

      IF lv_error <> 'N'.
        gv_flag = gv_flag + 1.
        IF lt_ycs_onebut_sel-checklist IS NOT INITIAL.
          IF gv_flag = 1.
            CONCATENATE lt_ycs_onebut_sel-checklist '//' text-t03 INTO lt_ycs_onebut_sel-checklist.
          ELSE.
            CONCATENATE lt_ycs_onebut_sel-checklist '/' text-t03 INTO lt_ycs_onebut_sel-checklist.
          ENDIF.
        ELSE.
          lt_ycs_onebut_sel-checklist = text-t03.
        ENDIF.
      ENDIF.

    ENDIF.

  ENDIF.
ENDFORM.                    "CHECK3_DAM_COD

*&---------------------------------------------------------------------*
*&      Form  CREDIT_EXPOSURE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->R_SAUFT    text
*      -->R_CTLPC    text
*      -->R_KNKLI    text
*      -->R_KKBER    text
*----------------------------------------------------------------------*
FORM credit_exposure USING r_sauft r_ctlpc r_knkli r_kkber.

  DATA: lv_olikw LIKE s067-olikw,
        lv_ofakw LIKE s067-ofakw,
        lv_oeikw LIKE s066-oeikw,
        lv_horda TYPE horda_f02l.

  CLEAR: r_sauft.

  CALL FUNCTION 'SD_CREDIT_HORIZON_DATE'
    EXPORTING
      i_kkber         = r_kkber
      i_ctlpc         = r_ctlpc
      i_horizon_exist = 'X'
    IMPORTING
      e_horizon_date  = lv_horda.

  IF lv_horda IS INITIAL.
    lv_horda = '99991231'.
  ENDIF.

  CALL FUNCTION 'SD_CREDIT_EXPOSURE'
     EXPORTING
          flag_open_delivery = 'X'
          flag_open_invoice  = 'X'
          flag_open_order    = 'X'
          horizon_date       = lv_horda
*          HORIZON_DATE       = '99991231'
          kkber              = r_kkber
          knkli              = r_knkli
*          T014               = ' '
     IMPORTING
          open_delivery      = lv_olikw
          open_invoice       = lv_ofakw
          open_order         = lv_oeikw.

  r_sauft = lv_olikw + lv_ofakw + lv_oeikw.

ENDFORM.                    " CREDIT_EXPOSURE

* End of insert MOD-001

FORM check_conf USING ip_aufnr
                CHANGING rp_tcfound
                         rp_mcfound.
  DATA:   ls_order            TYPE viord,
          lt_confirmation     TYPE STANDARD TABLE OF bapi_conf_key,
          ls_confirmation     LIKE LINE OF lt_confirmation,
          ls_confdet          TYPE bapi_alm_confirmation,
          lt_order_range      TYPE STANDARD TABLE OF bapi_pp_orderrange,
          ls_order_range_line LIKE LINE OF lt_order_range,
          lv_accind           TYPE bemot,
          it_aufm             TYPE STANDARD TABLE OF aufm,
          is_aufm             LIKE LINE OF it_aufm,
          gv_bemot            TYPE bemot,
          gv_mblnr            TYPE aufm-mblnr.

* Get confirmations
  MOVE 'I'  TO ls_order_range_line-sign.
  MOVE 'EQ' TO ls_order_range_line-option.
  MOVE ip_aufnr TO ls_order_range_line-low.
  APPEND ls_order_range_line TO lt_order_range.

  CALL FUNCTION 'BAPI_ALM_CONF_GETLIST'
    TABLES
      order_range   = lt_order_range
      confirmations = lt_confirmation.


  LOOP AT lt_confirmation INTO ls_confirmation.
    CLEAR ls_confdet.
    CALL FUNCTION 'BAPI_ALM_CONF_GETDETAIL'
      EXPORTING
        confirmation        = ls_confirmation-conf_no
        confirmationcounter = ls_confirmation-conf_cnt
      IMPORTING
        conf_detail         = ls_confdet.


    lv_accind = 'CH'.


    IF ls_confdet-calc_motive = lv_accind AND
       ls_confdet-reversed     IS INITIAL AND
       ls_confdet-rev_conf_cnt IS INITIAL AND
       ls_confdet-act_work IS NOT INITIAL.
      rp_tcfound = 'X'.
      EXIT.
    ENDIF.
  ENDLOOP.

  IF rp_tcfound IS INITIAL.
*.... Check if one of the goods movements has acc.indicator 'CH'
    SELECT mblnr mjahr zeile matnr werks lgort menge aufnr
      INTO CORRESPONDING FIELDS OF TABLE it_aufm
      FROM aufm
      WHERE aufnr = ip_aufnr
        AND bwart IN ('261', '961').

    LOOP AT it_aufm INTO is_aufm.
      CLEAR gv_bemot.
      SELECT SINGLE bemot INTO gv_bemot
        FROM mseg
        WHERE mblnr = is_aufm-mblnr
          AND mjahr = is_aufm-mjahr
          AND zeile = is_aufm-zeile.

      IF gv_bemot = 'CH'.
*........ look for 262/962 with 'CH', if so then skip
        SELECT mblnr INTO gv_mblnr
          FROM mseg
          WHERE bwart IN ('262', '962')
            AND bemot = 'CH'
            AND matnr = is_aufm-matnr
            AND werks = is_aufm-werks
            AND lgort = is_aufm-lgort
            AND menge = is_aufm-menge
            AND aufnr = is_aufm-aufnr.
        ENDSELECT.

        IF sy-subrc = 0.
          CONTINUE.
        ENDIF.
*
        rp_mcfound = 'X'.
        EXIT.
      ENDIF.
    ENDLOOP.
  ENDIF.

ENDFORM.                    "CHECK_CONF

*&---------------------------------------------------------------------*
*&      Form  CHECK_CONF2
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->IP_AUFNR     text
*      -->RP_TCFOUND2  text
*      -->RP_MCFOUND2  text
*----------------------------------------------------------------------*
FORM check_conf2 USING ip_aufnr
                CHANGING rp_tcfound2
                         rp_mcfound2.
  DATA:   ls_order            TYPE viord,
          lt_confirmation     TYPE STANDARD TABLE OF bapi_conf_key,
          ls_confirmation     LIKE LINE OF lt_confirmation,
          ls_confdet          TYPE bapi_alm_confirmation,
          lt_order_range      TYPE STANDARD TABLE OF bapi_pp_orderrange,
          ls_order_range_line LIKE LINE OF lt_order_range,
          lv_accind           TYPE bemot,
          lv_accind2          TYPE bemot,
          it_aufm             TYPE STANDARD TABLE OF aufm,
          is_aufm             LIKE LINE OF it_aufm,
          gv_bemot            TYPE bemot,
          gv_mblnr            TYPE aufm-mblnr.

* Get confirmations
  MOVE 'I'  TO ls_order_range_line-sign.
  MOVE 'EQ' TO ls_order_range_line-option.
  MOVE ip_aufnr TO ls_order_range_line-low.
  APPEND ls_order_range_line TO lt_order_range.

  CALL FUNCTION 'BAPI_ALM_CONF_GETLIST'
    TABLES
      order_range   = lt_order_range
      confirmations = lt_confirmation.


  LOOP AT lt_confirmation INTO ls_confirmation.
    CLEAR ls_confdet.
    CALL FUNCTION 'BAPI_ALM_CONF_GETDETAIL'
      EXPORTING
        confirmation        = ls_confirmation-conf_no
        confirmationcounter = ls_confirmation-conf_cnt
      IMPORTING
        conf_detail         = ls_confdet.


    lv_accind = 'CH'.
    lv_accind2 = 'FP'.

    IF ( ls_confdet-calc_motive = lv_accind OR ls_confdet-calc_motive = lv_accind2 ) AND
       ls_confdet-reversed     IS INITIAL AND
       ls_confdet-rev_conf_cnt IS INITIAL AND
       ls_confdet-act_work IS NOT INITIAL.
      rp_tcfound2 = 'X'.
      EXIT.
    ENDIF.

  ENDLOOP.

  IF rp_tcfound2 IS INITIAL.
*.... Check if one of the goods movements has acc.indicator 'CH'
    SELECT mblnr mjahr zeile matnr werks lgort menge aufnr
      INTO CORRESPONDING FIELDS OF TABLE it_aufm
      FROM aufm
      WHERE aufnr = ip_aufnr
        AND bwart IN ('261', '961').

    LOOP AT it_aufm INTO is_aufm.
      CLEAR gv_bemot.
      SELECT SINGLE bemot INTO gv_bemot
        FROM mseg
        WHERE mblnr = is_aufm-mblnr
          AND mjahr = is_aufm-mjahr
          AND zeile = is_aufm-zeile.

      IF gv_bemot = 'CH' OR gv_bemot = 'FP'.
*........ look for 262/962 with 'CH', if so then skip
        SELECT mblnr INTO gv_mblnr
          FROM mseg
          WHERE bwart IN ('262', '962')
            AND ( bemot = 'CH' OR bemot = 'FP' )
            AND matnr = is_aufm-matnr
            AND werks = is_aufm-werks
            AND lgort = is_aufm-lgort
            AND menge = is_aufm-menge
            AND aufnr = is_aufm-aufnr.
        ENDSELECT.

        IF sy-subrc = 0.
          CONTINUE.
        ENDIF.
*
        rp_mcfound2 = 'X'.
        EXIT.
      ENDIF.
    ENDLOOP.
  ENDIF.

ENDFORM.                    "CHECK_CONF2

*Text symbol text£º
*E01:Error for :
*T01:No APAV status
*T02:Credit Limit Exceeded

*T03:Missing Damage Codes
*Selection text£º
*S_AUFNR:D       .
*S_BUKRS:D       .
*S_ERDAT:D       .
*S_GLTRS:D       .
*S_IWERK:D       .
