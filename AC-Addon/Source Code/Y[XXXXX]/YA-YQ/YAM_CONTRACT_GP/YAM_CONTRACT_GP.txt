*----------------------------------------------------------------------
* PROGRAM ID           : YAM_CONTRACT_GP                               *
* PROGRAM TITLE        : Contract life for cost, revenue and GP        *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 14/01/2008                                    *
* DEVELOPMENT ID       : All-CR350                                     *
* CHANGE REQUEST NUMBER: CD1K925177                                    *
* PROGRAM DESCRIPTION  : Create report for the contract, revenue and GP*
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |12/08/2008|M.Jacobs  |CD1K942660 no extra checks for '45'   *
*                                contracts + include debits and credits*
*                                + multiple contract and customers     *
*----------------------------------------------------------------------*

REPORT yam_contract_gp NO STANDARD PAGE HEADING
                                  LINE-SIZE 200.

TYPE-POOLS: slis.

TABLES: vbak,                          "Sales Document: Header Data
        crhd,
        plpo,
        plas,
        mpla,
        mpos.


*- VARIABLES----------------------------------------------------------
DATA: BEGIN OF gt_vbap OCCURS 0,
        vbeln     LIKE vbak-vbeln,
        posnr     LIKE vbap-posnr,
        guebg     LIKE vbak-guebg,
        gueen     LIKE vbak-gueen,
        kunnr     LIKE vbak-kunnr,
        vkgrp     LIKE vbak-vkgrp,
        fplnr     LIKE vbkd-fplnr,
      END OF gt_vbap.

DATA: BEGIN OF gt_copa OCCURS 0,
        ww004    TYPE rkeg_ww004,
        vrgar    TYPE rke_vrgar,
        kdpos    TYPE kdpos,
        equnr    TYPE equnr,
*       hzdat    type erdat,
        vv100    TYPE rke2_vv100,
        vv110    TYPE rke2_vv110,
        vv200    TYPE rke2_vv200,
        vv300    TYPE rke2_vv300,
        vv400    TYPE rke2_vv400,
        vv500    TYPE rke2_vv500,
        vv600    TYPE rke2_vv600,
        vv120    TYPE rke2_vv120,
      END OF gt_copa.

DATA: BEGIN OF gt_final OCCURS 0,
        vbeln         LIKE vbak-vbeln,
        posnr         LIKE vbap-posnr,
        act_rev(15)   TYPE p DECIMALS 2,
        act_cost(15)  TYPE p DECIMALS 2,
        act_gp(15)    TYPE p DECIMALS 2,
        act_gp%(3)    TYPE p DECIMALS 2,
        pl_w_cost(15) TYPE p DECIMALS 2,
        pl_p_cost(15) TYPE p DECIMALS 2,
        dist_cost(15) TYPE p DECIMALS 2,
        pl_t_cost(15) TYPE p DECIMALS 2,
        plan_rev(15)  TYPE p DECIMALS 2,
        plan_gp(15)   TYPE p DECIMALS 2,
        plan_gp%(3)   TYPE p DECIMALS 2,
        tot_gp(15)    TYPE p DECIMALS 2,
        tot_gp%(3)    TYPE p DECIMALS 2,
        guebg         LIKE vbak-guebg,
        gueen         LIKE vbak-gueen,
        vkgrp         LIKE vbak-vkgrp,
        equnr         TYPE equnr,
        fplnr         LIKE vbkd-fplnr,
        nplda         LIKE mhis-nplda,
        selected,
      END OF gt_final.

DATA: BEGIN OF gt_plas OCCURS 0,
        plnty     LIKE plas-plnty,
        plnnr     LIKE plas-plnnr,
        plnal     LIKE plas-plnal,
        plnfl     LIKE plas-plnfl,
        plnkn     LIKE plas-plnkn,
        zaehl     LIKE plas-zaehl,
      END OF gt_plas.

DATA: BEGIN OF gt_plpo OCCURS 0,
        plnty     LIKE plas-plnty,
        plnnr     LIKE plas-plnnr,
        plnkn     LIKE plas-plnkn,
        zaehl     LIKE plas-zaehl,
        steus     LIKE plpo-steus,
        larnt     LIKE plpo-larnt,
        arbei     LIKE plpo-arbei,
        arbeh     LIKE plpo-arbeh,
        werks     LIKE plpo-werks,
      END OF gt_plpo.

DATA: BEGIN OF gt_plmz OCCURS 0,
        plnty     LIKE plas-plnty,
        plnnr     LIKE plas-plnnr,
        plnkn     LIKE plas-plnkn,
        stlty     LIKE plmz-stlty,
        stlnr     LIKE plmz-stlnr,
        stlkn     LIKE plmz-stlkn,
        imeng     LIKE plmz-imeng,
        imein     LIKE plmz-imein,
      END OF gt_plmz.

DATA: BEGIN OF gt_stpo OCCURS 0,
        stlty     LIKE stpo-stlty,
        stlnr     LIKE stpo-stlnr,
        stlkn     LIKE stpo-stlkn,
        idnrk     LIKE stpo-idnrk,
      END OF gt_stpo.
* begin of insertion MOD-001
DATA: BEGIN OF gt_vbfa OCCURS 0,
        vbeln     LIKE vbfa-vbeln,
        posnn     LIKE vbfa-posnn,
      END OF gt_vbfa.
* end of insertion MOD-001

DATA: i_cosel    TYPE STANDARD TABLE OF
                 cosel   INITIAL SIZE 0,

      i_costa    TYPE STANDARD TABLE OF
                 costa   INITIAL SIZE 0.

DATA: gt_basic_data TYPE cuvtab OCCURS 0  WITH HEADER LINE,
      gt_value_n    TYPE cuvtab_valn OCCURS 0  WITH HEADER LINE,
      gt_value_c    TYPE cuvtab_valc OCCURS 0  WITH HEADER LINE.

DATA: g_repid             LIKE ceddb-program,
      gv_bukrs            TYPE bukrs,
      gv_kokrs            TYPE kokrs,
      g_exist(1)          TYPE c,
      wa_copa             LIKE LINE OF gt_copa,
      g_ww004_10(10)      TYPE c,
      gv_warpl            LIKE mpla-warpl,
      gv_objnr_warpl      LIKE jest-objnr,
      gv_atinn_country    TYPE atinn,
      gv_atinn_km         TYPE atinn,
      gv_atinn_exlab      TYPE atinn,
      gv_spp_cost         TYPE stprs,
      gv_spp_unit         TYPE peinh,
      gv_kostl            TYPE kostl,
      gv_hrs              TYPE arbeit,
      gv_dist             TYPE arbeit,
      gv_dist_unit        TYPE arbeh,
      gv_fakwr            LIKE fplt-fakwr,
      gv_tot_rev(15)      TYPE p DECIMALS 2,
      gv_iwerk            TYPE iwerk,
      gv_gewrk            TYPE lgwid,
      gv_save_werks       TYPE iwerk,
      gv_prctr            TYPE prctr,
      gv_equnr            TYPE equnr,
      gv_plnty            TYPE plnty,
      gv_plnnr            TYPE plnnr,
      gv_plnal            TYPE plnal,
      gv_nplda            LIKE mhis-nplda,
      gv_save_nplda       LIKE mhis-nplda,
      gv_hrs_unit         TYPE arbeh,
      gv_pl_w_cost(15)    TYPE p DECIMALS 2,
      gv_dist_cost(15)    TYPE p DECIMALS 2,
      gv_jahr             LIKE bkpf-gjahr,
      gv_period           TYPE cest1-perio,
      gv_monat            TYPE bkpf-monat,
      gv_monat1           TYPE cest1-perde,
      gv_objnr            TYPE cssl-objnr,
      gv_fldperiod        TYPE dd03l-fieldname,
      gv_leinh            LIKE cssl-leinh,
      wa_cosel            TYPE cosel,
      wa_costa            TYPE costa,
      l_index             TYPE sy-tabix,
      gv_ic1              LIKE sy-ucomm VALUE '&IC1',
      gt_fieldcat         TYPE slis_t_fieldcat_alv,
      g_events_tab        TYPE slis_t_event,
      g_form_user_command TYPE slis_formname VALUE 'USER_COMMAND_L'.

*- Constants------------------------------------------------------------
CONSTANTS: gc_x(1)        TYPE c       VALUE 'X',
           gc_zam001      TYPE lstar   VALUE 'ZAM001',
           gc_zam010      TYPE lstar   VALUE 'ZAM010',
           gc_zco3        TYPE steus   VALUE 'ZCO3',
           c_g(1)         TYPE c       VALUE 'G',        "Contracts
           c_00(2)        TYPE c       VALUE '00',
           gc_z_vc_table  TYPE vtnam   VALUE 'Z_VC_TABLE',
           gc_country(20) TYPE c       VALUE 'CH_EQUIPMENT_COUNTRY',
           gc_km(08)      TYPE c       VALUE 'CH_TL_KM',
           gc_exlab(17)   TYPE c       VALUE 'CH_TL_EXTRALABOUR',
           gc_gmix        TYPE werks_d VALUE 'GMIX',
           gc_kl(2)       TYPE c       VALUE 'KL',
           gc_tof0(4)     TYPE c       VALUE 'TOF0',
           gc_toe0(4)     TYPE c       VALUE 'TOE0',
           c_99999(3)     TYPE p DECIMALS 2 VALUE '999.99'.
* begin of insertion MOD-001
CONSTANTS: c_m(1)         TYPE c       VALUE 'M'.
* end of insertion MOD-001
*- SELECTION SCREEN---------------------------------------------------
SELECTION-SCREEN: BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.
PARAMETERS:     p_vkorg  TYPE vbak-vkorg OBLIGATORY MEMORY ID vko,
                p_vtweg  TYPE vbak-vtweg OBLIGATORY MEMORY ID vtw,
                p_spart  TYPE vbak-spart OBLIGATORY MEMORY ID spa.
* begin of change MOD-001
*                p_vbeln  TYPE vbeln_va MATCHCODE OBJECT vmva,
*                p_kunnr  TYPE vbak-kunnr.
SELECT-OPTIONS :s_vbeln FOR vbak-vbeln MATCHCODE OBJECT vmva,
                s_kunnr FOR vbak-kunnr.
* end of change MOD-001
SELECTION-SCREEN: END OF BLOCK b1.
SELECTION-SCREEN BEGIN OF BLOCK b2 WITH FRAME TITLE text-002.
PARAMETERS:     p_active RADIOBUTTON GROUP 01 DEFAULT 'X',
                p_all    RADIOBUTTON GROUP 01.
SELECTION-SCREEN END OF BLOCK b2.


*.................. Selection screen validations...................... *
AT SELECTION-SCREEN ON p_vkorg.

  AUTHORITY-CHECK OBJECT 'V_VBAK_VKO'
           ID 'VKORG' FIELD p_vkorg
*          ID 'VTWEG' FIELD p_vtweg
*          ID 'SPART' FIELD p_spart
           ID 'ACTVT' FIELD '03'.

  IF sy-subrc NE 0.
*.. No authorization for sales organisation:
    MESSAGE e001(00) WITH text-e03 p_vkorg.
  ENDIF.

*
AT SELECTION-SCREEN ON BLOCK b1.

* begin of change MOD-001
*  IF p_vbeln IS INITIAL AND
*     p_kunnr IS INITIAL.
  IF s_vbeln IS INITIAL AND
     s_kunnr IS INITIAL.
* end of change MOD-001
*.. Please fill in Sold-to party or Contract number
    MESSAGE e001(00) WITH text-e04.
  ENDIF.


*-START OF SELECTION----------------------------------------------------
START-OF-SELECTION.

* get company code
  SELECT SINGLE bukrs INTO gv_bukrs
      FROM tvko WHERE vkorg = p_vkorg.

  IF sy-subrc NE 0.
    WRITE:/ text-e05, p_vkorg.                       "SO does not exist
    EXIT.
  ENDIF.

* select contract items
  IF p_active = 'X'.                               "Only active ones
* begin of change MOD-001
*    IF NOT p_vbeln IS INITIAL.
    IF NOT s_vbeln IS INITIAL.
* end of change MOD-001
      SELECT a~vbeln a~kunnr a~vkgrp a~guebg a~gueen b~posnr
             c~fplnr
            INTO CORRESPONDING FIELDS OF TABLE gt_vbap
            FROM vbak AS a
            INNER JOIN vbap AS b
             ON a~vbeln EQ b~vbeln
            INNER JOIN vbkd AS c
             ON  a~vbeln EQ c~vbeln
             AND b~posnr EQ c~posnr
* begin of change MOD-001
*            WHERE a~vbeln EQ p_vbeln
             WHERE a~vbeln IN s_vbeln
* end of change MOD-001
              AND a~vkorg EQ p_vkorg
              AND a~vtweg EQ p_vtweg
              AND a~spart EQ p_spart
              AND a~vbtyp EQ c_g
              AND a~gueen GT sy-datum.
    ELSE.
      SELECT a~vbeln a~kunnr a~vkgrp a~guebg a~gueen b~posnr
             c~fplnr
            INTO CORRESPONDING FIELDS OF TABLE gt_vbap
            FROM vbak AS a
            INNER JOIN vbap AS b
             ON a~vbeln EQ b~vbeln
            INNER JOIN vbkd AS c
             ON  a~vbeln EQ c~vbeln
             AND b~posnr EQ c~posnr
            WHERE a~vkorg EQ p_vkorg
              AND a~vtweg EQ p_vtweg
              AND a~spart EQ p_spart
* begin of change MOD-001
*              AND a~kunnr EQ p_kunnr
              AND a~kunnr IN s_kunnr
* end of change MOD-001
              AND a~vbtyp EQ c_g
              AND a~gueen GT sy-datum.
    ENDIF.
  ELSE.                                        " All contract items
* begin of change MOD-001
*    IF NOT p_vbeln IS INITIAL.
    IF NOT s_vbeln IS INITIAL.
* end of change MOD-001
      SELECT a~vbeln a~kunnr a~vkgrp a~guebg a~gueen b~posnr
             c~fplnr
            INTO CORRESPONDING FIELDS OF TABLE gt_vbap
            FROM vbak AS a
            INNER JOIN vbap AS b
             ON a~vbeln EQ b~vbeln
            INNER JOIN vbkd AS c
             ON  a~vbeln EQ c~vbeln
             AND b~posnr EQ c~posnr
* begin of change MOD-001
*            WHERE a~vbeln EQ p_vbeln
            WHERE a~vbeln IN s_vbeln
* end of change MOD-001
              AND a~vkorg EQ p_vkorg
              AND a~vtweg EQ p_vtweg
              AND a~spart EQ p_spart
              AND a~vbtyp EQ c_g.
    ELSE.
      SELECT a~vbeln a~kunnr a~vkgrp a~guebg a~gueen b~posnr
             c~fplnr
            INTO CORRESPONDING FIELDS OF TABLE gt_vbap
            FROM vbak AS a
            INNER JOIN vbap AS b
             ON a~vbeln EQ b~vbeln
            INNER JOIN vbkd AS c
             ON  a~vbeln EQ c~vbeln
             AND b~posnr EQ c~posnr
            WHERE a~vkorg EQ p_vkorg
              AND a~vtweg EQ p_vtweg
              AND a~spart EQ p_spart
* begin of change MOD-001
*              AND a~kunnr EQ p_kunnr
              AND a~kunnr IN s_kunnr
* end of change MOD-001
              AND a~vbtyp EQ c_g.
    ENDIF.
  ENDIF.

  IF gt_vbap[] IS INITIAL.
    WRITE: / text-i01.              "No contracts selected
    EXIT.
  ENDIF.

* preselect variant table for central task lists
  CALL FUNCTION 'CUTX_INIT_STRUC_ENTRY_PLANNING'
    EXPORTING
      var_tab          = gc_z_vc_table
    TABLES
      et_basic_data    = gt_basic_data
      et_table_value_n = gt_value_n
      et_table_value_c = gt_value_c
    EXCEPTIONS
      table_not_found  = 1
      OTHERS           = 2.

  IF sy-subrc <> 0.
    WRITE: / text-i02.              "No variant table available
    EXIT.
  ENDIF.

* get internal numbers for the caracteristics
  SELECT SINGLE atinn INTO gv_atinn_country
      FROM cabn
      WHERE atnam = gc_country.

  IF sy-subrc <> 0.
    WRITE: / text-i03, 'CH_EQUIPMENT_COUNTRY'.
    EXIT.
  ENDIF.

  SELECT SINGLE atinn INTO gv_atinn_km
      FROM cabn
      WHERE atnam = gc_km.

  IF sy-subrc <> 0.
    WRITE: / text-i03, 'CH_TL_KM'.
    EXIT.
  ENDIF.

  SELECT SINGLE atinn INTO gv_atinn_exlab
      FROM cabn
      WHERE atnam = gc_exlab.

  IF sy-subrc <> 0.
    WRITE: / text-i03, 'CH_TL_EXTRALABOUR'.
    EXIT.
  ENDIF.

* put details from contract(s) into internal table
  SORT gt_vbap BY vbeln posnr.
  LOOP AT gt_vbap.

    SELECT ww004 kdpos equnr vrgar vv100 vv110 vv120 vv200
           vv300 vv400 vv500 vv600
         FROM ce11000
         APPENDING CORRESPONDING FIELDS OF TABLE gt_copa
         WHERE paledger EQ '02'
           AND vrgar    IN ('0', 'F', '9')
           AND bukrs    EQ gv_bukrs
           AND kaufn    EQ gt_vbap-vbeln
           AND kdpos    EQ gt_vbap-posnr.

    IF sy-subrc <> 0.
*.... Determine equipment number
      SELECT SINGLE equnr
        INTO gt_copa-equnr
        FROM viser02
        WHERE sdaufnr EQ gt_vbap-vbeln
          AND posnr   EQ gt_vbap-posnr.

      MOVE gt_vbap-vbeln+2(8) TO gt_copa-ww004.
      MOVE gt_vbap-posnr      TO gt_copa-kdpos.
      MOVE '9'                TO gt_copa-vrgar.

      APPEND gt_copa.
      CLEAR gt_copa.
    ENDIF.
* begin of insertion MOD-001
* add corresponding credits and debits
    REFRESH gt_vbfa.
    CLEAR gt_vbfa.
    SELECT vbeln posnn INTO CORRESPONDING FIELDS OF TABLE
        gt_vbfa FROM vbfa
        WHERE vbelv = gt_vbap-vbeln
          AND posnv = gt_vbap-posnr
          AND vbtyp_n <> c_m.
    SORT gt_vbfa BY vbeln posnn.
    DELETE ADJACENT DUPLICATES FROM gt_vbfa COMPARING ALL FIELDS.
    LOOP AT gt_vbfa.
      SELECT ww004 kdpos equnr vrgar vv100 vv110 vv120 vv200
           vv300 vv400 vv500 vv600
         FROM ce11000
         APPENDING CORRESPONDING FIELDS OF TABLE gt_copa
         WHERE paledger EQ '02'
           AND vrgar    IN ('0', 'F', '9')
           AND bukrs    EQ gv_bukrs
           AND kaufn    EQ gt_vbfa-vbeln
           AND kdpos    EQ gt_vbfa-posnn.
    ENDLOOP.
* end of insertion MOD-001
  ENDLOOP.

* Select also the contract history.
* begin of deletion MOD-001
*  loop at gt_vbap where vbeln+2(2) = '45'.
*
*    at new vbeln.
*      SELECT ww004 equnr vrgar vv100 vv110 vv120 vv200
*             vv300 vv400 vv500 vv600
*           FROM CE11000
*           appending corresponding fields of table gt_copa
*           WHERE paledger eq '02'
*             AND vrgar    eq '9'
*             AND BUKRS    eq gv_bukrs
*             and ww004    eq gt_vbap-vbeln+2(8).
*    endat.
*
*  endloop.
* end of deletion MOD-001

* get itemnumber for those where this is not filled in
  LOOP AT gt_copa.

    IF gt_copa-kdpos IS INITIAL.
      CONCATENATE c_00 gt_copa-ww004 INTO g_ww004_10.
      SELECT SINGLE posnr INTO gt_copa-kdpos
        FROM viser02
        WHERE sdaufnr EQ g_ww004_10
          AND equnr   EQ gt_copa-equnr.
      MODIFY gt_copa TRANSPORTING kdpos.
    ENDIF.

  ENDLOOP.

  SORT gt_copa BY ww004 kdpos.

*-----------------------------------------------------------------------
END-OF-SELECTION.

* calculate actual cost and revenue
  LOOP AT gt_copa.

    MOVE gt_copa TO wa_copa.

    AT NEW ww004.
      CONCATENATE c_00 wa_copa-ww004 INTO g_ww004_10.
    ENDAT.

    READ TABLE gt_vbap WITH KEY vbeln = g_ww004_10
                                posnr = wa_copa-kdpos
                 BINARY SEARCH.

    MOVE-CORRESPONDING gt_vbap TO gt_final.

    CASE wa_copa-vrgar.
      WHEN '0'.
        gt_final-act_cost = wa_copa-vv200 + wa_copa-vv300 +
               wa_copa-vv400 + wa_copa-vv500 + wa_copa-vv600.
      WHEN 'F'.
        gt_final-act_rev  = wa_copa-vv100 + wa_copa-vv120 +
               wa_copa-vv110.
      WHEN '9'.
        gt_final-act_cost = wa_copa-vv200 + wa_copa-vv300 +
               wa_copa-vv400 + wa_copa-vv500 + wa_copa-vv600.
        gt_final-act_rev  = wa_copa-vv100 + wa_copa-vv120 +
               wa_copa-vv110.
      WHEN OTHERS.
*       no action
    ENDCASE.

    MOVE wa_copa-equnr TO gt_final-equnr.

    COLLECT gt_final.
    CLEAR gt_final.

  ENDLOOP.


  LOOP AT gt_final.

*.. Actual GP in value
    gt_final-act_gp = gt_final-act_rev - gt_final-act_cost.

*.. Actual GP in %
    CATCH SYSTEM-EXCEPTIONS arithmetic_errors = 1.
      IF gt_final-act_gp <> 0 AND gt_final-act_rev <> 0.
        gt_final-act_gp% = 100 - ( gt_final-act_cost * 100 /
                                   gt_final-act_rev ).
      ENDIF.
    ENDCATCH.
    IF sy-subrc = 1.
      gt_final-act_gp% = c_99999.
    ENDIF.

*.. Get planned revenue via billing plan
    SELECT fakwr INTO gv_fakwr
      FROM fplt
      WHERE fplnr EQ gt_final-fplnr
        AND fksaf IN ('A', 'B').

      ADD gv_fakwr TO gt_final-plan_rev.
    ENDSELECT.

*.. Get planned cost via maintenance call
*..  Select active maintenance items/ planned calls
    CLEAR: gt_final-pl_w_cost,
           gt_final-pl_p_cost,
           gt_final-dist_cost,
           gt_final-nplda,
           gv_save_nplda.

    SELECT a~plnty a~plnnr a~plnal a~iwerk a~gewrk c~nplda a~warpl
       INTO (gv_plnty, gv_plnnr, gv_plnal, gv_iwerk, gv_gewrk,
             gv_nplda, gv_warpl)
       FROM mpos AS a INNER JOIN mhio AS b ON
            a~warpl = b~warpl AND
            a~wapos = b~wppos
                      INNER JOIN mhis AS c ON
            b~warpl = c~warpl AND
            b~abnum = c~abnum
       WHERE a~equnr  = gt_final-equnr
         AND a~inact  = ' '
         AND b~addat  = '00000000'
         AND c~nplda <= gt_final-gueen.

*.... only the active maintenance plans
*.... not deleted / not inactive
      CONCATENATE 'WO' gv_warpl INTO gv_objnr_warpl.

      CALL FUNCTION 'STATUS_CHECK'
        EXPORTING
          objnr             = gv_objnr_warpl
          status            = 'I0320'
        EXCEPTIONS
          object_not_found  = 1
          status_not_active = 2
          OTHERS            = 3.
      IF sy-subrc EQ 0.
        CONTINUE.
      ELSE.
        CALL FUNCTION 'STATUS_CHECK'
          EXPORTING
            objnr             = gv_objnr_warpl
            status            = 'I0076'
          EXCEPTIONS
            object_not_found  = 1
            status_not_active = 2
            OTHERS            = 3.
        IF sy-subrc EQ 0.
          CONTINUE.
        ENDIF.
      ENDIF.

      REFRESH gt_plas.
*.... Select tasklist info
      SELECT plnkn plnty plnnr plnal plnfl zaehl
         INTO CORRESPONDING FIELDS OF TABLE gt_plas
         FROM plas
         WHERE plnty = gv_plnty
           AND plnnr = gv_plnnr
           AND plnal = gv_plnal
           AND loekz = space.

      IF NOT gt_plas[] IS INITIAL.
        REFRESH gt_plpo.
        SELECT arbei arbeh steus werks
               plnty plnnr plnkn zaehl
           INTO CORRESPONDING FIELDS OF TABLE gt_plpo
           FROM plpo
           FOR ALL ENTRIES IN gt_plas
           WHERE plnty = gt_plas-plnty
             AND plnnr = gt_plas-plnnr
             AND plnkn = gt_plas-plnkn
             AND loekz = space
             AND ( steus = gc_zco3
              OR larnt = gc_zam010 ).
      ENDIF.

      IF NOT gt_plpo[] IS INITIAL.
        CLEAR: gv_save_werks,
               gv_hrs,
               gv_hrs_unit,
               gv_dist,
               gv_dist_unit.

        LOOP AT gt_plpo.
          IF gt_plpo-steus = gc_zco3.
            ADD gt_plpo-arbei TO gv_hrs.
            MOVE gt_plpo-arbeh TO gv_hrs_unit.
          ELSE.
            ADD gt_plpo-arbei TO gv_dist.
            MOVE gt_plpo-arbeh TO gv_dist_unit.
          ENDIF.
          MOVE gt_plpo-werks TO gv_save_werks.
        ENDLOOP.

*...... Select spare parts
        REFRESH: gt_plmz,
                 gt_stpo.

        SELECT stlty stlnr stlkn imeng imein
               plnnr plnty plnkn
           INTO CORRESPONDING FIELDS OF TABLE gt_plmz
           FROM plmz
           FOR ALL ENTRIES IN gt_plpo
           WHERE plnnr = gt_plpo-plnnr
             AND plnty = gt_plpo-plnty
             AND plnkn = gt_plpo-plnkn
             AND loekz = space.

        IF NOT gt_plmz[] IS INITIAL.
          SELECT idnrk stlty stlnr stlkn
             INTO CORRESPONDING FIELDS OF TABLE gt_stpo
             FROM stpo
             FOR ALL ENTRIES IN gt_plmz
             WHERE stlty = gt_plmz-stlty
               AND stlnr = gt_plmz-stlnr
               AND stlkn = gt_plmz-stlkn
               AND lkenz = space.
        ENDIF.

        IF NOT gt_stpo[] IS INITIAL.
          SORT gt_stpo BY stlty stlnr stlkn.

*........ Get cost of spare parts; always read with planning plant
          LOOP AT gt_plmz.

            READ TABLE gt_stpo WITH KEY stlty = gt_plmz-stlty
                                        stlnr = gt_plmz-stlnr
                                        stlkn = gt_plmz-stlkn
                               BINARY SEARCH.

            IF sy-subrc = 0.
              SELECT SINGLE stprs peinh
                 INTO (gv_spp_cost, gv_spp_unit)
                 FROM mbew
                 WHERE matnr = gt_stpo-idnrk
                   AND bwkey = gv_iwerk.

              IF sy-subrc = 0.
                gv_spp_cost =
                      gv_spp_cost * gt_plmz-imeng / gv_spp_unit.
                ADD gv_spp_cost TO gt_final-pl_p_cost.
              ENDIF.
            ENDIF.

          ENDLOOP.
        ENDIF.

*...... Only for GMIX: read Variant table for Central task lists
        IF gv_save_werks = gc_gmix.
          READ TABLE gt_basic_data WITH KEY mandt = sy-mandt
                                            vtnam = gc_z_vc_table.

          IF sy-subrc = 0.
            READ TABLE gt_value_c WITH KEY mandt = sy-mandt
                                           vtint = gt_basic_data-vtint
                                           atinn = gv_atinn_country
                                           valc  = gv_bukrs.
            IF sy-subrc = 0.
*............ get km
              READ TABLE gt_value_n WITH KEY mandt = sy-mandt
                                             vtint = gt_basic_data-vtint
                                             slnid = gt_value_c-slnid
                                             atinn = gv_atinn_km.
              IF sy-subrc = 0.
                ADD gt_value_n-val_from TO gv_dist.
              ENDIF.
*............ get extra labour
              READ TABLE gt_value_n WITH KEY mandt = sy-mandt
                                          vtint = gt_basic_data-vtint
                                          slnid = gt_value_c-slnid
                                          atinn = gv_atinn_exlab.
              IF sy-subrc = 0.
                ADD gt_value_n-val_from TO gv_hrs.
              ENDIF.
            ENDIF.
          ENDIF.
        ENDIF.

*...... look for the cost price per hour/distance
*......  read costcenter (and controlling area)
        PERFORM get_kostl USING gv_gewrk gv_kostl gv_kokrs.

*...... Get financial period
        PERFORM get_fi_period USING gv_nplda
                                    gv_iwerk
                           CHANGING gv_monat
                                    gv_jahr.

*...... get cost price hour
        CLEAR gv_pl_w_cost.
        PERFORM calc_cost USING gv_kokrs
                                gv_kostl
                                gc_zam001
                                gv_jahr
                                gv_monat
                                gv_hrs
                                gv_hrs_unit
                       CHANGING gv_pl_w_cost.
        ADD gv_pl_w_cost TO gt_final-pl_w_cost.

*...... get cost price distance
        CLEAR gv_dist_cost.
        PERFORM calc_cost USING gv_kokrs
                                gv_kostl
                                gc_zam010
                                gv_jahr
                                gv_monat
                                gv_dist
                                gv_dist_unit
                       CHANGING gv_dist_cost.
        ADD gv_dist_cost TO gt_final-dist_cost.

        IF gv_pl_w_cost IS INITIAL.
*........ calculate future cost with cost price of current period
*........ Get financial period
          PERFORM get_fi_period USING sy-datum
                                      gv_iwerk
                             CHANGING gv_monat
                                      gv_jahr.

*........ get cost price hour
          CLEAR gv_pl_w_cost.
          PERFORM calc_cost USING gv_kokrs
                                  gv_kostl
                                  gc_zam001
                                  gv_jahr
                                  gv_monat
                                  gv_hrs
                                  gv_hrs_unit
                         CHANGING gv_pl_w_cost.
          ADD gv_pl_w_cost TO gt_final-pl_w_cost.

*........ get cost price distance
          CLEAR gv_dist_cost.
          PERFORM calc_cost USING gv_kokrs
                                  gv_kostl
                                  gc_zam010
                                  gv_jahr
                                  gv_monat
                                  gv_dist
                                  gv_dist_unit
                         CHANGING gv_dist_cost.
          ADD gv_dist_cost TO gt_final-dist_cost.
        ENDIF.

      ENDIF.
      IF gv_nplda > gv_save_nplda.
        MOVE gv_nplda TO: gt_final-nplda, gv_save_nplda.
      ENDIF.
    ENDSELECT.

*.. Calculate planned GP and total GP(actual and planned)
    gt_final-pl_t_cost = gt_final-pl_w_cost + gt_final-pl_p_cost +
                         gt_final-dist_cost.
    gt_final-plan_gp = gt_final-plan_rev - gt_final-pl_t_cost.
* begin of insertion MOD-001
    CATCH SYSTEM-EXCEPTIONS arithmetic_errors = 1.
* end of insertion MOD-001
      IF gt_final-plan_gp <> 0 AND gt_final-plan_rev <> 0.
        gt_final-plan_gp% = 100 - ( gt_final-pl_t_cost * 100 /
                                   gt_final-plan_rev ).
      ENDIF.
* begin of insertion MOD-001
    ENDCATCH.
    IF sy-subrc = 1.
      gt_final-plan_gp% = c_99999.
    ENDIF.
* end of insertion MOD-001

    gt_final-tot_gp = gt_final-act_gp + gt_final-plan_gp.

    gv_tot_rev = gt_final-act_rev + gt_final-plan_rev.
    CATCH SYSTEM-EXCEPTIONS arithmetic_errors = 1.
      IF gv_tot_rev <> 0.
        gt_final-tot_gp% = 100 - ( ( gt_final-act_cost +
           gt_final-pl_t_cost ) * 100 / gv_tot_rev ).
      ENDIF.
    ENDCATCH.
    IF sy-subrc = 1.
      gt_final-tot_gp% = c_99999.
    ENDIF.

*.. add new fields to internal table
    MODIFY gt_final TRANSPORTING act_gp act_gp%
                                 pl_w_cost pl_p_cost
                                 pl_t_cost dist_cost
                                 plan_gp plan_gp%
                                 tot_gp tot_gp%
                                 plan_rev
                                 nplda.
  ENDLOOP.

* create ALV-GRID
  PERFORM build_field_catlog CHANGING gt_fieldcat.
  PERFORM fill_events_f14.
  PERFORM alv_display.


*- SUBROUTINES---------------------------------------------------------
*&---------------------------------------------------------------------*
*&      Form  build_field_catlog
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      <--P_GT_FIELDCAT  text
*----------------------------------------------------------------------*
FORM build_field_catlog  CHANGING pt_fieldcat TYPE slis_t_fieldcat_alv.

  DATA : ls_fcat TYPE slis_fieldcat_alv.

*-----------------------Sales Document--------------------*
  ls_fcat-fieldname = 'VBELN'.
  ls_fcat-rollname = 'VBELN_VA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Sales Document Item----------------*
  ls_fcat-fieldname = 'POSNR'.
  ls_fcat-rollname = 'POSNR_VA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Actual revenue----------------------*
  ls_fcat-fieldname = 'ACT_REV'.
  ls_fcat-outputlen = '12'.
  ls_fcat-seltext_l = 'Act_rev'(i10).
  ls_fcat-emphasize = 'C100'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Actual cost----------------------*
  ls_fcat-fieldname = 'ACT_COST'.
  ls_fcat-outputlen = '12'.
  ls_fcat-seltext_l = 'Act_cost'(i11).
  ls_fcat-emphasize = 'C100'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Actual GP----------------------*
  ls_fcat-fieldname = 'ACT_GP'.
  ls_fcat-outputlen = '12'.
  ls_fcat-seltext_l = 'Act_GP'(i12).
  ls_fcat-emphasize = 'C100'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Actual GP%---------------------*
  ls_fcat-fieldname = 'ACT_GP%'.
  ls_fcat-outputlen = '7'.
  ls_fcat-seltext_l = 'Act_GP%'(i13).
  ls_fcat-no_sum    = 'X'.
  ls_fcat-emphasize = 'C100'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*----------------Planned work Cost--------------------------*
  ls_fcat-fieldname = 'PL_W_COST'.
  ls_fcat-outputlen = '14'.
  ls_fcat-seltext_l = 'Plan_work_cost'(i14).
  ls_fcat-emphasize = 'C500'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*----------------Planned parts Cost--------------------------*
  ls_fcat-fieldname = 'PL_P_COST'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Plan_parts_cost'(i15).
  ls_fcat-emphasize = 'C500'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------Distance Cost--------------------------*
  ls_fcat-fieldname = 'DIST_COST'.
  ls_fcat-outputlen = '13'.
  ls_fcat-seltext_l = 'Distance_Cost'(i18).
  ls_fcat-emphasize = 'C500'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*----------------Planned total Cost--------------------------*
  ls_fcat-fieldname = 'PL_T_COST'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Plan_total_cost'(i17).
  ls_fcat-emphasize = 'C500'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*----------------Planned revenue----------------------------*
  ls_fcat-fieldname = 'PLAN_REV'.
  ls_fcat-outputlen = '12'.
  ls_fcat-seltext_l = 'Plan_rev'(i16).
  ls_fcat-emphasize = 'C500'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*----------------Planned GP---------------------------------*
  ls_fcat-fieldname = 'PLAN_GP'.
  ls_fcat-outputlen = '12'.
  ls_fcat-seltext_l = 'Plan_GP'.
  ls_fcat-emphasize = 'C500'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*----------------Planned GP%--------------------------------*
  ls_fcat-fieldname = 'PLAN_GP%'.
  ls_fcat-outputlen = '8'.
  ls_fcat-seltext_l = 'Plan_GP%'.
  ls_fcat-no_sum    = 'X'.
  ls_fcat-emphasize = 'C500'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*----------------Total GP-----------------------------------*
  ls_fcat-fieldname = 'TOT_GP'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Tot_GP'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*----------------Total GP%--------------------------------*
  ls_fcat-fieldname = 'TOT_GP%'.
  ls_fcat-outputlen = '7'.
  ls_fcat-seltext_l = 'Tot_GP%'.
  ls_fcat-no_sum    = 'X'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Start date------------------------*
  ls_fcat-fieldname = 'GUEBG'.
  ls_fcat-rollname = 'VBDAT_VEDA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------End date------------------------*
  ls_fcat-fieldname = 'GUEEN'.
  ls_fcat-rollname = 'VNDAT_VEDA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Sales group---------------------*
  ls_fcat-fieldname = 'VKGRP'.
  ls_fcat-rollname = 'VKGRP'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Last planned visit---------------*
  ls_fcat-fieldname = 'NPLDA'.
  ls_fcat-rollname = 'NPLDA'.
  ls_fcat-seltext_l = 'Last_planned_visit'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.

ENDFORM.                    " build_field_catlog

*&---------------------------------------------------------------------*
*&      Form  fill_events_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM fill_events_f14 .

  DATA h_event       TYPE slis_alv_event.

  CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
    EXPORTING
      i_list_type = 0
    IMPORTING
      et_events   = g_events_tab.

*--- allocate form for user-command ---------------------------------*
  READ TABLE g_events_tab WITH KEY name = slis_ev_user_command
                        INTO h_event.
  IF sy-subrc = 0.
    MOVE g_form_user_command TO h_event-form.
    MODIFY g_events_tab FROM h_event INDEX sy-tabix.
  ENDIF.

ENDFORM.                    " fill_events_f14

*&--------------------------------------------------------------------*
*&      Form  user_command_l
*&--------------------------------------------------------------------*
*       text
*---------------------------------------------------------------------*
*      -->P_UCOMM    text
*      -->P_SELFIELD text
*---------------------------------------------------------------------*
FORM user_command_l USING p_ucomm LIKE sy-ucomm
                          p_selfield TYPE slis_selfield.

  p_selfield-refresh = 'S'.
  PERFORM check_pf2_with_object_f16 USING p_ucomm.
  PERFORM set_p_selfield_general_f16 USING p_selfield.

  CASE p_ucomm.
    WHEN 'ISEL'.
      p_ucomm = 'DISP'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
  ENDCASE.

ENDFORM.                    "user_command_l

*&---------------------------------------------------------------------*
*&      Form  check_pf2_with_object_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*----------------------------------------------------------------------*
FORM check_pf2_with_object_f16  USING    p_ucomm.

  CHECK p_ucomm = gv_ic1.
  p_ucomm = 'ISEL'.

ENDFORM.                    " check_pf2_with_object_f16

*&---------------------------------------------------------------------*
*&      Form  set_p_selfield_general_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM set_p_selfield_general_f16  USING f_selfield TYPE slis_selfield.

  f_selfield-col_stable = gc_x.
  f_selfield-row_stable = gc_x.

ENDFORM.                    " set_p_selfield_general_f16

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*      -->P_ENDCASE  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_f16  USING p_ucomm LIKE sy-ucomm
                                p_selfield TYPE slis_selfield.

  PERFORM check_object_tab_marked_f14 USING p_ucomm p_selfield.

  LOOP AT gt_final WHERE selected = gc_x .
    l_index = sy-tabix.
    PERFORM fcodes_with_mark_l USING p_ucomm p_selfield.
    gt_final-selected = ' '.
    MODIFY gt_final INDEX l_index.
  ENDLOOP.

  CLEAR p_ucomm.

ENDFORM.                    " fcodes_with_mark_f16

*&---------------------------------------------------------------------*
*&      Form  check_object_tab_marked_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM check_object_tab_marked_f14  USING    p_ucomm LIKE sy-ucomm
                                        p_selfield TYPE slis_selfield.

  READ TABLE gt_final WITH KEY selected = gc_x.

  IF NOT sy-subrc IS INITIAL.
    IF NOT p_selfield-tabindex IS INITIAL.
      READ TABLE gt_final INDEX p_selfield-tabindex.
      gt_final-selected = gc_x.
      MODIFY gt_final INDEX p_selfield-tabindex.
    ENDIF.
  ELSE.
*--- Checkbox markiert -----------------------------------------------*
    p_selfield-sel_tab_field = 'G_MARK'.
  ENDIF.

ENDFORM.                    " check_object_tab_marked_f14

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_l
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_F_UCOMM  text
*      -->P_F_SELFIELD  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_l  USING   p_ucomm LIKE sy-ucomm
                              p_selfield TYPE slis_selfield.

  DATA: h_ucomm LIKE sy-ucomm.

  CASE p_ucomm.
*   Display Contract
    WHEN 'DISP'.
      SET PARAMETER ID 'KTN' FIELD gt_final-vbeln.
      CALL TRANSACTION 'VA43' AND SKIP FIRST SCREEN.
  ENDCASE.

ENDFORM.                    " fcodes_with_mark_l

*&---------------------------------------------------------------------*
*&      Form  alv_display
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM alv_display .

  g_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
     EXPORTING
       i_callback_program                =  g_repid
       i_save                            = 'A'
       it_events                         =  g_events_tab[]
*      I_GRID_TITLE                      =
*      I_GRID_SETTINGS                   =
*      is_layout                         =  g_layout
       it_fieldcat                       =  gt_fieldcat[]
     TABLES
        t_outtab                         =  gt_final.

ENDFORM.                    " alv_display

*&---------------------------------------------------------------------*
*&      Form  GET_KOSTL
*&---------------------------------------------------------------------*
*       read cost center and controlling area
*----------------------------------------------------------------------*
*  -->  arbid
*  <--  kostl
*  <--  kokrs
*----------------------------------------------------------------------*
FORM get_kostl USING    arbid TYPE cr_objid
               CHANGING kostl TYPE kostl
                        kokrs TYPE kokrs.

  DATA:
    i_rcrco_a LIKE rcrco_a.

  CALL FUNCTION 'CR_WORKCENTER_READ_COSTING'
    EXPORTING
      arbid                    = arbid
      date                     = sy-datum
*     ALL_SETS_FROM_DATE       = ' '
    IMPORTING
*     ECRHD                    =
      edata                    = i_rcrco_a
*   TABLES
*     ET_CRCO_A                =
    EXCEPTIONS
      not_found                = 1
      OTHERS                   = 2
      .

  IF sy-subrc = 0 AND NOT i_rcrco_a-kostl IS INITIAL.
    kostl = i_rcrco_a-kostl.
    kokrs = i_rcrco_a-kokrs.
  ENDIF.

ENDFORM.                               " GET_KOSTL

*&---------------------------------------------------------------------*
*&      Form  get_fi_period
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_fi_period USING    p_budat
                            p_bukrs
                   CHANGING p_monat
                            p_gjahr.

  CALL FUNCTION 'FI_PERIOD_DETERMINE'
    EXPORTING
      i_budat              = p_budat
      i_bukrs              = p_bukrs
*     I_PERIV              = ' '
*     I_GJAHR              = 0000
*     I_MONAT              = 00
*     X_XMO16              = ' '
    IMPORTING
      e_gjahr              = p_gjahr
      e_monat              = p_monat
*     E_POPER              =
    EXCEPTIONS
      fiscal_year          = 1
      period               = 2
      period_version       = 3
      posting_period       = 4
      special_period       = 5
      version              = 6
      posting_date         = 7
      OTHERS               = 8.

ENDFORM.                    " get_fi_period

*&---------------------------------------------------------------------*
*&      Form  calc_cost
*&---------------------------------------------------------------------*
*       Get costprice from the activity type/price planning maint. KP26
*----------------------------------------------------------------------*
FORM calc_cost USING p_kokrs
                     p_costc
                     p_acttype
                     p_jahr
                     p_monat
                     p_quant
                     p_unit
            CHANGING p_value.

  DATA: l_pricequant(7) TYPE p DECIMALS 2 VALUE 1,
        l_unit(10) TYPE c,
        l_messagetext(80) TYPE c.

  FIELD-SYMBOLS: <fs>  TYPE ANY,
                 <fs2> TYPE ANY.

  CLEAR: i_cosel[],
         i_costa[],
         wa_cosel.

  REFRESH: i_cosel[],
           i_costa[].

* Construct internal table for COSEL.
  wa_cosel-sign   = 'I'.
  wa_cosel-option = 'EQ'.

* Field Fiscal year
  wa_cosel-field  = 'GJAHR'.
  wa_cosel-low    = p_jahr.
  APPEND wa_cosel TO i_cosel.
  CLEAR: wa_cosel-field,
         wa_cosel-low.

* Field Ledger for Controlling objects
  wa_cosel-field  = 'LEDNR'.
  wa_cosel-low    = '00'.
  APPEND wa_cosel TO i_cosel.
  CLEAR: wa_cosel-field,
         wa_cosel-low.

* Field Object Number
* Construct object number
  CLEAR gv_objnr.

  CONCATENATE gc_kl
              p_kokrs
              p_costc
              p_acttype INTO gv_objnr.

  wa_cosel-field  = 'OBJNR'.
  wa_cosel-low    = gv_objnr.
  APPEND wa_cosel TO i_cosel.
  CLEAR: wa_cosel-field,
         wa_cosel-low.

* Field Version
  wa_cosel-field  = 'VERSN'.
  wa_cosel-low    = '000'.
  APPEND wa_cosel TO i_cosel.
  CLEAR: wa_cosel-field,
         wa_cosel-low.

* Field Value Type
  wa_cosel-field  = 'WRTTP'.
  wa_cosel-low    = '01'.
  APPEND wa_cosel TO i_cosel.
  CLEAR: wa_cosel-field,
         wa_cosel-low.

* Get totals table for prices of all periods
  CALL FUNCTION 'K_COSTA_READ_MULTI'
    EXPORTING
      von_periode       = '001'
      bis_periode       = '500'
      modus             = '1'
*     PROGNAME          =
*     FORMNAME          =
    TABLES
      t_cosel           = i_cosel
      t_costa           = i_costa
*     T_FGR             =
            .

  CONCATENATE gc_tof0 p_monat INTO gv_fldperiod.

* This table should only come back with one row
  LOOP AT i_costa INTO wa_costa.

*   Get Activity Unit
    CLEAR: gv_leinh.
    FREE: gv_leinh.

    SELECT SINGLE leinh FROM cssl INTO gv_leinh WHERE
                             kokrs = p_kokrs               AND
                             kostl = p_costc               AND
                             lstar = p_acttype             AND
                             gjahr = p_jahr.

    IF sy-subrc NE 0.
*     Activity Unit Not Found in Table CSSL For Key & & & &
    ELSE.

      ASSIGN COMPONENT gv_fldperiod
      OF STRUCTURE     wa_costa TO <fs>.

      ASSIGN <fs> TO <fs2>.

      IF p_unit NE gv_leinh AND
         NOT p_unit IS INITIAL.

        PERFORM unit_conversion USING  gv_leinh
                                       p_unit
                                       p_quant
                              CHANGING l_pricequant.
      ELSE.

        l_pricequant = p_quant.

      ENDIF.

*     Part calculation of field value (has to be divided still by price
*     unit)
      p_value = <fs> * l_pricequant.

*     Get price unit out of TOE0xx and recalculate correct value
      CLEAR gv_fldperiod.
      CONCATENATE gc_toe0 p_monat INTO gv_fldperiod.

      ASSIGN COMPONENT gv_fldperiod
      OF STRUCTURE     wa_costa TO <fs>.

      p_value = p_value / <fs>.

    ENDIF.

  ENDLOOP.

ENDFORM.                    " calc_cost

*&---------------------------------------------------------------------*
*&      Form  unit_conversion
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_WA_OPERATIONS_DURATION_NORMAL_  text
*      -->P_G_LEINH  text
*      <--P_P_VALUE  text
*----------------------------------------------------------------------*
FORM unit_conversion  USING    p_tounit
                               p_fromunit
                               p_pricequant
                      CHANGING p_quantity.

  CALL FUNCTION 'UNIT_CONVERSION_SIMPLE'
    EXPORTING
      input                      = p_pricequant
*     NO_TYPE_CHECK              = 'X'
*     ROUND_SIGN                 = ' '
      unit_in                    = p_fromunit
      unit_out                   = p_tounit
    IMPORTING
*     ADD_CONST                  =
*     DECIMALS                   =
*     DENOMINATOR                =
*     NUMERATOR                  =
      output                     = p_quantity
    EXCEPTIONS
      conversion_not_found       = 1
      division_by_zero           = 2
      input_invalid              = 3
      output_invalid             = 4
      overflow                   = 5
      type_invalid               = 6
      units_missing              = 7
      unit_in_not_found          = 8
      unit_out_not_found         = 9
      OTHERS                     = 10.

ENDFORM.                    " unit_conversion

*Text symbol text£º
*001:Selection screen input
*002:Which contracts ?
*E03:No authorization for sales organisation:
*E04:Please fill in Sold-to party or Contract number
*E05:No company code found for Sales Organization
*I01:No contracts selected !
*I02:No variant table for Central Task Lists found (CU60)
*I03:Could not find caracteristic :
*I10:Act_rev
*I11:Act_cost
*I12:Act_GP
*I13:Act_GP%
*I14:Plan_work_cost
*I15:Plan_parts_cost
*I16:Plan_rev
*I17:Plan_total_cost
*I18:Distance_Cost
*I20:no comment

*I21:Plan_labour_cost
*Selection text£º
*P_ACTIVE:        Only active contracts
*P_ALL:        Show all contracts
*P_SPART:D       .
*P_VKORG:D       .
*P_VTWEG:D       .
*S_KUNNR:        Customer Number
*S_VBELN:        Contract Number
