*&---------------------------------------------------------------------*
*& Report  YAM_EQUI_ADD_OILTYPE                                        *
*&                                                                     *
*&---------------------------------------------------------------------*
*& This report will add the value for characteristic ZAM_OILTYPE       *
*& to the selected equipment(s) based on PGC and company code.         *
*&---------------------------------------------------------------------*
*& Modifications                                                       *
*&---------------------------------------------------------------------*
* MOD-xxx| Date     | Author          | Request  | Development ID      *
*----------------------------------------------------------------------*
* MOD-001 | 2007.09.18 | Marc Jacobs       |CD1K920703 |ZAM_OILTYPE    *
*                       in stead of CH_TL_COMPRESSOR_OIL               *
*&---------------------------------------------------------------------*
REPORT  yam_equi_add_oiltype MESSAGE-ID yam_re.

*.................. Tables definitions................................ *
TABLES: v_equi,
        mara.

*.................. Variable definitions.............................. *
TYPES: BEGIN OF ty_equi,
        equnr TYPE equnr,
        matnr TYPE matnr,
        bukrs TYPE bukrs,
        prdha TYPE prodh_d,
        valc  TYPE atwrt,
       END   OF ty_equi.

*.. Global variables
DATA: gt_equi     TYPE STANDARD TABLE OF ty_equi WITH HEADER LINE.

DATA: i_bdcdata LIKE bdcdata OCCURS 0 WITH HEADER LINE,
      struct_bdcdata TYPE bdcdata.

DATA: gt_errmsg LIKE bdcmsgcoll OCCURS 0 WITH HEADER LINE,
      wa_errmsg LIKE LINE OF gt_errmsg.

DATA: gt_basic_data TYPE cuvtab      OCCURS 0  WITH HEADER LINE,
      gt_value_n    TYPE cuvtab_valn OCCURS 0  WITH HEADER LINE,
      gt_value_c    TYPE cuvtab_valc OCCURS 0  WITH HEADER LINE,
      gt_value_c2   TYPE cuvtab_valc OCCURS 0  WITH HEADER LINE.

DATA: gv_atinn_country    TYPE atinn,
      gv_atinn_pgc        TYPE atinn,
      gv_atinn_oiltype    TYPE atinn,
      gv_msgnr            LIKE t100-msgnr,
      gv_mestx            LIKE t100-text.

*.. Global constants
CONSTANTS: gc_eqart         LIKE equi-eqart VALUE 'EQUIPMENT',
           gc_z_vc_pgcoil   TYPE vtnam      VALUE 'Z_VC_PGCOIL',
           gc_country(20)   TYPE c         VALUE 'CH_EQUIPMENT_COUNTRY',
           gc_pgc(7)        TYPE c          VALUE 'ZAM_PGC',
           gc_oiltype(11)   TYPE c          VALUE 'ZAM_OILTYPE',
* begin of deletion MOD-001
*          gc_compr_oil(20) type c         value 'CH_TL_COMPRESSOR_OIL',
* end of deletion MOD-001
           gc_99991231      LIKE sy-datum   VALUE '99991231'.

*.................. Layout selection screen........................... *
SELECTION-SCREEN BEGIN OF BLOCK b1 WITH FRAME TITLE text-b01.
SELECT-OPTIONS:
  s_equnr      FOR       v_equi-equnr MATCHCODE OBJECT equi.
PARAMETERS:
  p_bukrs      TYPE      bukrs MEMORY ID buk.
SELECTION-SCREEN END   OF BLOCK b1.

*.................. Selection screen validations...................... *
AT SELECTION-SCREEN ON p_bukrs.

  AUTHORITY-CHECK OBJECT 'YAM_BUKRS'
           ID 'BUKRS' FIELD p_bukrs.

  IF sy-subrc NE 0.
*.. No authorization for company: &1
    MESSAGE s001(00) WITH text-e03 p_bukrs.
    EXIT.
  ENDIF.


*.................. Main program...................................... *
START-OF-SELECTION.

* Input checks
  IF p_bukrs IS INITIAL AND
     s_equnr IS INITIAL.
*.. Please fill in equipment and/or company code
    MESSAGE s001(00) WITH text-e01.
    EXIT.
  ENDIF.

* Select the relevant equipments
  PERFORM select_equipments.

  IF gt_equi[] IS INITIAL.
    MESSAGE s001(00) WITH text-i01.
    EXIT.
  ENDIF.

* Add additional fields to internal table
  LOOP AT gt_equi.
    SELECT SINGLE prdha
        INTO gt_equi-prdha
        FROM mara
        WHERE matnr = gt_equi-matnr.

    IF sy-subrc EQ 0.
      MODIFY gt_equi.
    ENDIF.
  ENDLOOP.

* preselect variant table for oiltype
  CALL FUNCTION 'CUTX_INIT_STRUC_ENTRY_PLANNING'
    EXPORTING
      var_tab                = gc_z_vc_pgcoil
    TABLES
      et_basic_data          = gt_basic_data
*     ET_TABLE_TEXTS         =
*     ET_TABLE_FIELDS        =
*     ET_TABLE_INDICES       =
*     ET_TABLE_LINE          =
      et_table_value_n       = gt_value_n
      et_table_value_c       = gt_value_c
    EXCEPTIONS
      table_not_found        = 1
      OTHERS                 = 2.

  IF sy-subrc <> 0.
    MESSAGE s001(00) WITH text-i02.    "No variant table available
    EXIT.
  ENDIF.

* get internal numbers for the characteristics
  SELECT SINGLE atinn INTO gv_atinn_country
      FROM cabn
      WHERE atnam = gc_country.

  IF sy-subrc <> 0.
    MESSAGE s001(00) WITH text-i03 'CH_EQUIPMENT_COUNTRY'.
    EXIT.
  ENDIF.

  SELECT SINGLE atinn INTO gv_atinn_pgc
      FROM cabn
      WHERE atnam = gc_pgc.

  IF sy-subrc <> 0.
    MESSAGE s001(00) WITH text-i03 'ZAM_PGC'.
    EXIT.
  ENDIF.

  SELECT SINGLE atinn INTO gv_atinn_oiltype
      FROM cabn
      WHERE atnam = gc_oiltype.

  IF sy-subrc <> 0.
    MESSAGE s001(00) WITH text-i03 'ZAM_OILTYPE'.
    EXIT.
  ENDIF.


*.................. End of selection.................................. *
END-OF-SELECTION.

  gt_value_c2[] = gt_value_c[].
  SORT gt_value_c2 BY mandt vtint slnid atinn.

  SORT gt_equi BY equnr.

  LOOP AT gt_equi.

    READ TABLE gt_basic_data WITH KEY mandt = sy-mandt
                                      vtnam = gc_z_vc_pgcoil.

    IF sy-subrc = 0.
*.... get oiltype
      LOOP AT gt_value_c WHERE mandt = sy-mandt
                           AND vtint = gt_basic_data-vtint
                           AND atinn = gv_atinn_pgc
                           AND valc  = gt_equi-prdha+4(4).

        READ TABLE gt_value_c2 WITH KEY mandt = sy-mandt
                                        vtint = gt_basic_data-vtint
                                        slnid = gt_value_c-slnid
                                        atinn = gv_atinn_country
                                        valc  = gt_equi-bukrs
                             BINARY SEARCH.

        IF sy-subrc = 0.
          READ TABLE gt_value_c2 WITH KEY mandt = sy-mandt
                                          vtint = gt_basic_data-vtint
                                          slnid = gt_value_c2-slnid
                                          atinn = gv_atinn_oiltype
                                BINARY SEARCH.

          IF sy-subrc = 0.
            MOVE gt_value_c2-valc TO gt_equi-valc.
            MODIFY gt_equi.
            EXIT.
          ENDIF.
        ENDIF.
      ENDLOOP.
    ENDIF.

  ENDLOOP.

* Update equipment (characteristic Compressor oil type)
  LOOP AT gt_equi.
    IF gt_equi-valc IS INITIAL.
      WRITE: / text-e04, gt_equi-equnr.
      CONTINUE.
    ELSE.
      REFRESH i_bdcdata.
      PERFORM bdc_filldata.
      CALL TRANSACTION 'IE02' USING i_bdcdata
                   MODE 'N' UPDATE 'S' MESSAGES INTO gt_errmsg.

      IF NOT sy-subrc IS INITIAL.
        WRITE: / gt_equi-equnr, text-e07.

        LOOP AT gt_errmsg.
          gv_msgnr = gt_errmsg-msgnr.
          CALL FUNCTION 'RH_MESSAGE_GET'
            EXPORTING
*             SPRSL                   = SY-LANGU
              arbgb                   = sy-msgid
              msgnr                   = gv_msgnr
              msgv1                   = sy-msgv1
              msgv2                   = sy-msgv2
              msgv3                   = sy-msgv3
              msgv4                   = sy-msgv4
            IMPORTING
              msgtext                 = gv_mestx
            EXCEPTIONS
              message_not_found       = 1
              OTHERS                  = 2.

          IF sy-subrc = 0.
            WRITE: / gv_mestx.
          ENDIF.
        ENDLOOP.
        SKIP.

      ENDIF.
    ENDIF.
  ENDLOOP.


*.................. Subroutines....................................... *
*
*---------------------------------------------------------------------
FORM select_equipments.

  IF p_bukrs IS INITIAL.
    SELECT equnr matnr bukrs
      INTO CORRESPONDING FIELDS OF TABLE gt_equi
      FROM v_equi
      WHERE eqart EQ gc_eqart
        AND datbi EQ gc_99991231
        AND equnr IN s_equnr.
  ELSE.
    SELECT equnr matnr bukrs
      INTO CORRESPONDING FIELDS OF TABLE gt_equi
      FROM v_equi
      WHERE eqart EQ gc_eqart
        AND datbi EQ gc_99991231
        AND equnr IN s_equnr
        AND bukrs EQ p_bukrs.
  ENDIF.

ENDFORM.                    "select_equipments

*----------------------------------------------------------------------
FORM bdc_filldata.

* first screen
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPMIEQ0' '0100' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: '' '' '' 'RM63E-EQUNR' gt_equi-equnr
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '/00'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

* second screen - general data
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPMIEQ0' '0101' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=T\09'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

* 3rd screen - configuration data
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPMIEQ0' '0101' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=COCH'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

* 4th screen - characteristic value assignment
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPLCEI0' '0109' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.
* begin of deletion MOD-001
*  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
*                    USING: '' '' '' 'RCTMS-MNAME(01)' gc_compr_oil
*                     CHANGING struct_bdcdata.
* end of deletion MOD-001
* begin of insertion MOD-001
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: '' '' '' 'RCTMS-MNAME(01)' gc_oiltype
                     CHANGING struct_bdcdata.
* end of insertion MOD001

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: '' '' '' 'RCTMS-MWERT(01)' gt_equi-valc
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=BACK'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

* save changes
  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                     USING: 'SAPMIEQ0' '0101' 'X' ' ' ' '
                     CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

  PERFORM fill_bdcdata IN PROGRAM yam_common_routines
                      USING: '' '' '' 'BDC_OKCODE' '=BU'
                      CHANGING struct_bdcdata.

  APPEND struct_bdcdata TO i_bdcdata.
  CLEAR  struct_bdcdata.

ENDFORM.                    "update_equip

*Text symbol text£º
*B01:Selection
*E01:Please fill in equipment and/or company code
*E03:No authorization for company :
*E04:No value found in Z_VC_PGCOIL for equipment :
*E07:Transaction failed !
*I01:No equipments selected !
*I02:No variant table for Oil types (CU60)

*I03:Could not find caracteristic :
*Selection text£º
*P_BUKRS:D       Company Code
*S_EQUNR:D       Equipment
