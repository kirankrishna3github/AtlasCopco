*----------------------------------------------------------------------*
* PROGRAM ID           : YAM_NAVIGATOR_FAVORITES                       *
* PROGRAM TITLE        : Copy obj. navigator favorites between systems *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 15/03/2005                                    *
* DEVELOPMENT ID       : DE-CR009                                      *
* CHANGE REQUEST NUMBER: CD1K901837                                    *
* PROGRAM DESCRIPTION  : Copy of favorites between systems             *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
*                                                                      *
*----------------------------------------------------------------------*
REPORT  YAM_NAVIGATOR_FAVORITES.

data:   gt_tmemo    type standard table of SMEN_BUFFC.

field-symbols: <tmemo> like line of gt_tmemo.

parameters: uname   type xubname default sy-uname,
            file(30) default 'c:\temp\tmemo.txt',
            rb_down type xfeld radiobutton group r1,
            rb_up   type xfeld radiobutton group r1.

start-of-selection.
  if rb_down is initial.
*   upload tmemo
    perform upload_tmemo.
  else.
*   download tmemo
    perform download_tmemo.
  endif.

*---------------------------------------------------------------------*
*  FORM upload_tmemo
*---------------------------------------------------------------------*
*  Upload object navigator favorites
*---------------------------------------------------------------------*
form upload_tmemo.
  data: lf_file type string.

  lf_file = file.

  call method cl_gui_frontend_services=>gui_upload
    exporting
      filename                = lf_file
    changing
      data_tab                = gt_tmemo
    exceptions
      file_open_error         = 1
      file_read_error         = 2
      no_batch                = 3
      gui_refuse_filetransfer = 4
      invalid_type            = 5
      no_authority            = 6
      unknown_error           = 7
      bad_data_format         = 8
      header_not_allowed      = 9
      separator_not_allowed   = 10
      header_too_long         = 11
      unknown_dp_error        = 12
      access_denied           = 13
      dp_out_of_memory        = 14
      disk_full               = 15
      dp_timeout              = 16
      not_supported_by_gui    = 17
      error_no_gui            = 18
      others                  = 19.

  check gt_tmemo is not initial.

  loop at gt_tmemo assigning <tmemo>.
    <tmemo>-uname = uname.
    modify gt_tmemo from <tmemo>.
  endloop.

  delete from SMEN_BUFFC
    where uname = uname.

  insert SMEN_BUFFC from table gt_tmemo
    accepting duplicate keys.

  if sy-subrc is initial.
    message 'Upload successful'(001) type 'S'.
  else.
    message 'Upload failed'(002) type 'S'.
  endif.

endform.                    "upload_tmemo

*---------------------------------------------------------------------*
*  FORM download_tmemo
*---------------------------------------------------------------------*
*  download object navigator favorites
*---------------------------------------------------------------------*
form download_tmemo.
  data: lf_filename type string.

  select * from SMEN_BUFFC
    into table gt_tmemo
    where uname = uname.

  check sy-subrc is initial.

  lf_filename = file.

  check lf_filename is not initial.

  call method cl_gui_frontend_services=>gui_download
    exporting
      filename                = lf_filename
    changing
      data_tab                = gt_tmemo
    exceptions
      file_write_error        = 1
      no_batch                = 2
      gui_refuse_filetransfer = 3
      invalid_type            = 4
      no_authority            = 5
      unknown_error           = 6
      header_not_allowed      = 7
      separator_not_allowed   = 8
      filesize_not_allowed    = 9
      header_too_long         = 10
      dp_error_create         = 11
      dp_error_send           = 12
      dp_error_write          = 13
      unknown_dp_error        = 14
      access_denied           = 15
      dp_out_of_memory        = 16
      disk_full               = 17
      dp_timeout              = 18
      file_not_found          = 19
      dataprovider_exception  = 20
      control_flush_error     = 21
      not_supported_by_gui    = 22
      error_no_gui            = 23
      others                  = 24.

  if sy-subrc is initial.
    message 'Download succesful'(003) type 'S'.
  else.
    message 'Download failed'(004) type 'S'.
  endif.

endform.                    "download_tmemo

*Selection text£º
*FILE:        PC File for Up/Download
*RB_DOWN:        Download Favorites
*RB_UP:        Upload Favorites
*UNAME:        Favorites for User
