REPORT yam_kpi_update .

*----------------------------------------------------------------------*
* Author: Tim Volkaert                                                 *
*                                                                      *
* Description: This program will delete entries from the KPI figure    *
*              with the company code and date parameter as filter      *
*              option. Once the program completes execution, the       *
*              KPI figures will no longer be available in the result   *
*              table.                                                  *
*----------------------------------------------------------------------*
* Modification log.                                                    *
*                                                                      *
*----------------------------------------------------------------------*
*.................. Includes.......................................... *

*.................. Tables............................................ *
TABLES: t001,  " Company Codes
        yam_kpi_res.  " Startup/rollout KPI's: KPI results

*.................. Global variables.................................. *
*.. Variables
DATA: v_answer    TYPE c,
      v_text(132) TYPE c,
      v_dbcnt(6)  TYPE n,
      v_log       TYPE bal_s_log.


*.. Internal tables

*.. Field symbols

*.................. Constants......................................... *
CONSTANTS: c_yam_kpi TYPE bal_s_log-object VALUE 'YAM_KPI',
           c_yam_kpi_del TYPE bal_s_log-subobject VALUE 'YAM_KPI_DEL'.

*.................. Selection screen.................................. *
SELECTION-SCREEN BEGIN OF BLOCK b01.
SELECT-OPTIONS: s_bukrs FOR t001-bukrs.
SELECTION-SCREEN END   OF BLOCK b01.

SELECTION-SCREEN BEGIN OF BLOCK b02.
SELECT-OPTIONS: s_datum FOR yam_kpi_res-datum obligatory.
SELECTION-SCREEN END   OF BLOCK b02.

*.................. Events............................................ *

*.................. Main program...................................... *
START-OF-SELECTION.
*.. Create application log
  v_log-extnumber = text-ext.
  v_log-object    = c_yam_kpi.
  v_log-subobject = c_yam_kpi_del.
  v_log-aldate    = sy-datlo.
  v_log-altime    = sy-timlo.
  v_log-aluser    = sy-uname.

  CALL FUNCTION 'BAL_LOG_CREATE'
    EXPORTING
      i_s_log                       = v_log
*   IMPORTING
*     E_LOG_HANDLE                  =
    EXCEPTIONS
      log_header_inconsistent       = 1
      OTHERS                        = 2
            .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.

*.. Sent confirmation pop up
  CALL FUNCTION 'POPUP_TO_CONFIRM_LOSS_OF_DATA'
    EXPORTING
      textline1           = text-t01
      textline2           = text-t02
      titel               = text-t03
*     START_COLUMN        = 25
*     START_ROW           = 6
*     DEFAULTOPTION       = 'N'
    IMPORTING
      answer              = v_answer
            .

*.. User &1 has confirmed the deletion of KPI results with &2
  v_text = text-t04.
  REPLACE '&1' WITH sy-uname INTO v_text.
  REPLACE '&2' WITH v_answer INTO v_text.
  PERFORM message_add_free_text USING v_text.

  IF v_answer = 'J'.
*.... KPI results for following Company ...
    PERFORM message_add_free_text USING text-t05.
    SELECT *
    FROM t001
    WHERE bukrs IN s_bukrs.
      PERFORM message_add_free_text USING t001-butxt.
    ENDSELECT.

*.... ... and for the following day selection ...
    PERFORM message_add_free_text USING text-t06.
    LOOP AT s_datum.
      v_text = text-t07.
      REPLACE '&1' WITH s_datum-sign   INTO v_text.
      REPLACE '&2' WITH s_datum-option INTO v_text.
      REPLACE '&3' WITH s_datum-low    INTO v_text.
      REPLACE '&4' WITH s_datum-high   INTO v_text.
      PERFORM message_add_free_text USING v_text.
    ENDLOOP.

*.... ... will be deleted
    PERFORM message_add_free_text USING text-t08.

*.... Delete records
    DELETE FROM yam_kpi_res
    WHERE bukrs IN s_bukrs
      AND datum IN s_datum.
    IF sy-subrc EQ 0.
      v_text = text-t09.
      v_dbcnt = sy-dbcnt.
*...... &1 number of KPI records deleted from database
      REPLACE '&1' WITH v_dbcnt INTO v_text.
      PERFORM message_add_free_text USING v_text.
      COMMIT WORK AND WAIT.
      IF sy-subrc EQ 0.
*........ Commit work happened for deletion
        PERFORM message_add_free_text USING text-t10.
      ENDIF.
    ELSE.
      ROLLBACK WORK.
*...... Rollback work happened for deletion
      PERFORM message_add_free_text USING text-t11.
    ENDIF.
  ENDIF.

*.. Save application log to database
  CALL FUNCTION 'BAL_DB_SAVE'
    EXPORTING
*     I_CLIENT               = SY-MANDT
*     I_IN_UPDATE_TASK       = ' '
      i_save_all             = 'X'
*     I_T_LOG_HANDLE         =
*   IMPORTING
*     E_NEW_LOGNUMBERS       =
    EXCEPTIONS
      log_not_found          = 1
      save_not_allowed       = 2
      numbering_error        = 3
      OTHERS                 = 4
            .
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


*eject
*&---------------------------------------------------------------------*
*&      Form  message_add_free_Text
*&---------------------------------------------------------------------*
*       text: Add free message text to application log
*----------------------------------------------------------------------*
*  -->  ip_text   text: Free text
*----------------------------------------------------------------------*
FORM message_add_free_text USING value(ip_text) TYPE c.
  CALL FUNCTION 'BAL_LOG_MSG_ADD_FREE_TEXT'
       EXPORTING
*           I_LOG_HANDLE  =
            i_msgty       = 'I'
            i_text        = ip_text
       EXCEPTIONS
            log_not_found = 0
            OTHERS        = 1.
  IF sy-subrc <> 0.
    MESSAGE ID sy-msgid TYPE sy-msgty NUMBER sy-msgno
            WITH sy-msgv1 sy-msgv2 sy-msgv3 sy-msgv4.
  ENDIF.


ENDFORM.                    " message_add_free_Text

*Text symbol text£º
*T01:You are about to delete KPI result records!
*T02:Are you sure you want to continue?
*T03:Delete KPI result records?
*T04:User &1 has confirmed the deletion of KPI results with &2
*T05:KPI results for following  Company  ...
*T06:... and for the following day selection ...
*T08:... will be deleted
*T09:&1 number of KPI records deleted from database
*T10:Commit work happened for deletion

*T11:Rollback work happened for deletion
*Selection text£º
*S_BUKRS:D       Company Code
*S_DATUM:        Reporting date
