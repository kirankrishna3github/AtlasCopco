*----------------------------------------------------------------------
* PROGRAM ID           : yam_billing_due_list                          *
* PROGRAM TITLE        : List of billing due items                     *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 20/12/2005                                    *
* DEVELOPMENT ID       : CR220                                         *
* CHANGE REQUEST NUMBER: CD1K901827                                    *
* PROGRAM DESCRIPTION  : List all billing due items                    *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |2006.04.07|Luc Mertens      |CD1K905218| correction FKDAT   *
* MOD-002 |2007.05.23|Luc Mertens      |CD1K915245| correction: lines  *
*      - which are already billed are not shown anymore                *
* MOD-003 |2008.01.02|Luc Mertens      |CD1K924727| All-CR378          *
*      - differences SAP-Excel (VBELN)                                 *
*----------------------------------------------------------------------*

REPORT yam_billing_due_list no standard page heading
                             line-size 450.

TYPE-POOLS: SLIS.

TABLES: VKDFS,                         "SD Index: Billing Initiator
        VBPA.                          "Sales Document: Partner

*-----------------------------------------------------------------------
DATA: BEGIN OF i_VKDFS OCCURS 0.
        INCLUDE STRUCTURE VKDFS.
DATA: END   OF i_VKDFS.

DATA: BEGIN OF i_VKDFI OCCURS 0.
        INCLUDE STRUCTURE VKDFI.
data:   posnr like vbap-posnr,
        prctr like vbap-prctr,
        selected.
DATA: END   OF i_VKDFI.

DATA: BEGIN OF i_tvkbt OCCURS 0.
        INCLUDE STRUCTURE tvkbt.
DATA: END   OF i_tvkbt.

DATA: BEGIN OF gt_vbadr OCCURS 0.
        INCLUDE STRUCTURE vbadr.
DATA: END   OF gt_vbadr.

DATA: BEGIN OF i_tvrgt OCCURS 0.
        INCLUDE STRUCTURE tvrgt.
DATA: END   OF i_tvrgt.

DATA: begin of gt_vbak occurs 0,
        vbeln like vbak-vbeln,
        objnr like vbak-objnr,
        vkbur like vbak-vkbur,
        submi like vbak-submi,
      end of gt_vbak.

DATA: begin of gt_vbap occurs 0,
        vbeln like vbap-vbeln,
        posnr like vbap-posnr,
        prctr like vbap-prctr,
      end of gt_vbap.

DATA: begin of gt_vbkd occurs 0,
        vbeln  like vbkd-vbeln,
        posnr  like vbkd-posnr,
        fplnr  like vbkd-fplnr,
      end of gt_vbkd.

DATA: begin of gt_fpla occurs 0,
        fplnr like fpla-fplnr,
        perio like fpla-perio,
      end of gt_fpla.

DATA: begin of gt_fplt occurs 0,
        fplnr  like fplt-fplnr,
* begin of change MOD-001
*       fkdat  like fplt-fkdat,
        afdat  like fplt-afdat,
* end of change MOD-001
        fakwr  like fplt-fakwr,
* begin of insert MOD-002
        fksaf  like fplt-fksaf,
* end of insert MOD-002
      end of gt_fplt.

DATA: begin of gt_veda occurs 0,
        vbeln   like veda-vbeln,
        vbegdat like veda-vbegdat,
        venddat like veda-venddat,
        vasda   like veda-vasda,
      end of gt_veda.

DATA: g_pltxt     like riwol-pltxt,
      g_objnr     like jest-objnr,
      g_repid         like sy-repid,
      l_index         TYPE sy-tabix,
      g_ic1           LIKE sy-ucomm VALUE '&IC1',
      gt_fieldcat     TYPE slis_t_fieldcat_alv,
      g_events_tab    TYPE slis_t_event,
      g_form_user_command TYPE slis_formname VALUE 'USER_COMMAND_L'.

CONSTANTS: c_a(1)     type c value 'A',           "Order-related
           c_x(1)     type c value 'X',
           c_s(1)     type c value 'S'.


*-----------------------------------------------------------------------
SELECT-OPTIONS: S_FKDAT     for vkdfs-fkdat,
                s_fkart     for vkdfs-fkart.

PARAMETERS:     P_VKORG     like VKDFS-VKORG obligatory memory id VKO.

SELECT-OPTIONS: S_VTWEG     for VKDFS-VTWEG obligatory memory id VTW,
                S_SPART     for VKDFS-SPART obligatory memory id SPA.


*-----------------------------------------------------------------------
INITIALIZATION.

  MOVE: 'BT'       TO s_fkdat-OPTION,
        'I'        TO s_fkdat-SIGN,
        sy-datum   TO s_fkdat-HIGH.
  APPEND s_fkdat.


*-----------------------------------------------------------------------
START-OF-SELECTION.

  SELECT * INTO TABLE i_vkdfs from vkdfs
        WHERE vkorg eq p_vkorg
          AND vtweg in s_vtweg
          AND spart in s_spart
          AND fkdat in s_fkdat
          AND fkart in s_fkart
          AND fktyp eq c_a.

  if i_vkdfs[] is initial.
    message E001(38) with text-e02.               "No documents selected
  endif.

  SELECT vbeln vkbur objnr submi
        INTO corresponding fields of table gt_vbak
        FROM VBAK
        FOR all entries in i_vkdfs
        WHERE vbeln eq i_vkdfs-vbeln.

  SELECT vbeln posnr prctr
        INTO corresponding fields of table gt_vbap
        FROM VBAP
        FOR all entries in i_vkdfs
        WHERE vbeln eq i_vkdfs-vbeln.

  SELECT vbeln posnr fplnr
        INTO corresponding fields of table gt_vbkd
        FROM VBKD
        FOR all entries in i_vkdfs
        WHERE vbeln eq i_vkdfs-vbeln
          AND posnr ne '000000'.

  SELECT fplnr perio
        INTO corresponding fields of table gt_fpla
        FROM FPLA
        FOR all entries in gt_vbkd
        WHERE fplnr eq gt_vbkd-fplnr.

* begin of change MOD-001
* SELECT fplnr fkdat fakwr
  SELECT fplnr afdat fakwr
* end of change MOD-001
* begin of insert MOD-002
         fksaf
* end of insert MOD-002
        INTO corresponding fields of table gt_fplt
        FROM FPLT
        FOR all entries in gt_fpla
* begin of change MOD-002
*       WHERE fplnr eq gt_fpla-fplnr.
        WHERE fplnr eq gt_fpla-fplnr
          and fksaf ne 'C'.
* end of change MOD-002

  SELECT vbeln vbegdat venddat vasda
        INTO corresponding fields of table gt_veda
        FROM VEDA
        FOR all entries in i_vkdfs
        WHERE vbeln eq i_vkdfs-vbeln.

  SELECT * into table i_tvkbt from tvkbt
        where spras eq sy-langu.

  SELECT * into table i_tvrgt from tvrgt
        where spras eq sy-langu.

*-----------------------------------------------------------------------
END-OF-SELECTION.

  sort gt_vbak by vbeln.
  sort gt_vbap by vbeln posnr.
  sort i_tvkbt by spras vkbur.
  sort i_tvrgt by spras regel.
  sort gt_vbkd by vbeln posnr.
  sort gt_fpla by fplnr.
* begin of change MOD-001
* sort gt_fplt by fplnr fkdat.
  sort gt_fplt by fplnr afdat.
* end of change
  sort gt_veda by vbeln.

  loop at i_vkdfs.

    clear i_vkdfi.
    move-corresponding i_vkdfs to i_vkdfi.

    CLEAR: i_vkdfi-zzstats,
           i_vkdfi-zzstatl,
           i_vkdfi-zzsubmi,
           i_vkdfi-zzperio,
           i_vkdfi-zzpetxt,
           i_vkdfi-zzbedat,
           i_vkdfi-zzbidat,
           i_vkdfi-zzvasda,
           i_vkdfi-zzvkbur_bez.

    CASE i_vkdfs-vbtyp.
      WHEN 'C' OR
           'K' OR
           'L' OR
           'G'.

*...... Select sales office description for sales order, debit/credit
*...... memo request or contract
        read table gt_vbak with key vbeln = i_vkdfs-vbeln
                             binary search.

        IF sy-subrc EQ 0.
          read table i_tvkbt with key spras = sy-langu
                                      vkbur = gt_vbak-vkbur
                              binary search.

          if sy-subrc = 0.
            move i_tvkbt-bezei to i_vkdfi-zzvkbur_bez.
          endif.
        ENDIF.

*...... Select status and collective number in case of contract
*...... reference
        IF i_vkdfs-vbtyp EQ 'G'.
          move gt_vbak-submi to i_vkdfi-zzsubmi.
          move gt_vbak-objnr to g_objnr.

          CALL FUNCTION 'STATUS_TEXT_EDIT_LONG'
            EXPORTING
              flg_user_stat    = 'X'
              objnr            = g_objnr
              spras            = sy-langu
            IMPORTING
              user_line        = i_vkdfi-zzstats
              user_line_long   = i_vkdfi-zzstatl
            EXCEPTIONS
              object_not_found = 1
              OTHERS           = 2.

          IF sy-subrc <> 0.
*             MESSAGE ID SY-MSGID TYPE SY-MSGTY NUMBER SY-MSGNO
*                   WITH SY-MSGV1 SY-MSGV2 SY-MSGV3 SY-MSGV4.
          ENDIF.

*........ Select contract validity + action date
          read table gt_veda with key vbeln = i_vkdfs-vbeln
                              binary search.

          if sy-subrc = 0.
            move gt_veda-vbegdat to i_vkdfi-zzbedat.
            move gt_veda-venddat to i_vkdfi-zzbidat.
            move gt_veda-vasda   to i_vkdfi-zzvasda.
          endif.
        ENDIF.
      WHEN OTHERS.
        CLEAR: i_vkdfi-zzstats,
               i_vkdfi-zzstatl,
               i_vkdfi-zzsubmi,
               i_vkdfi-zzperio,
               i_vkdfi-zzpetxt,
               i_vkdfi-zzbedat,
               i_vkdfi-zzbidat,
               i_vkdfi-zzvasda,
               i_vkdfi-zzvkbur_bez.
    ENDCASE.

*...Get name and location of sold-to party
    clear vbpa.
    VBPA-KUNNR = i_VKDFI-KUNNR.
    VBPA-ADRNR = i_VKDFI-ADRNR.

    CALL FUNCTION 'VIEW_VBADR'
      EXPORTING
        INPUT   = VBPA
        NRART   = 'KU'
      IMPORTING
        ADRESSE = gt_vbadr
      EXCEPTIONS
        ERROR.

    if sy-subrc = 0.
      i_VKDFI-NAME1 = gt_vbadr-NAME1.
      i_VKDFI-ORT01 = gt_vbadr-ORT01.
    endif.

*   Select item data
    loop at gt_vbap where vbeln = i_vkdfs-vbeln.

      move gt_vbap-posnr to i_vkdfi-posnr.
      move gt_vbap-prctr to i_vkdfi-prctr.

*..   Select billing plan number for contract item
      read table gt_vbkd with key vbeln = gt_vbap-vbeln
                                  posnr = gt_vbap-posnr
                           binary search.

      IF sy-subrc EQ 0.

*.....  Select billing plan frequency
        read table gt_fpla with key fplnr = gt_vbkd-fplnr
                             binary search.

        IF sy-subrc EQ 0.
          move gt_fpla-perio to i_vkdfi-zzperio.
          read table i_tvrgt with key spras = sy-langu
                                      regel = i_vkdfi-zzperio
                               binary search.

          if sy-subrc = 0.
            move i_tvrgt-bezeich to i_vkdfi-zzpetxt.
          endif.
        ENDIF.

*.....  Select billing plan bill.value
        read table gt_fplt with key fplnr = gt_vbkd-fplnr
* begin of change MOD-001
*                                   fkdat = i_vkdfs-fkdat
                                    afdat = i_vkdfs-fkdat
* end of change MOD-001
                             binary search.

        if sy-subrc eq 0.
          move gt_fplt-fakwr to i_vkdfi-netwr.
* begin of mod-002
        else.
          continue.
* end of mod-002
        endif.

      ENDIF.

      append i_vkdfi.

    endloop.

  endloop.

* create ALV-GRID
  PERFORM build_field_catlog CHANGING gt_fieldcat.
  PERFORM fill_events_f14.
  PERFORM alv_display.

*&---------------------------------------------------------------------*
*&      Form  build_field_catlog
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      <--P_GT_FIELDCAT  text
*----------------------------------------------------------------------*
FORM build_field_catlog  CHANGING pt_fieldcat TYPE slis_t_fieldcat_alv.

  DATA : ls_fcat TYPE slis_fieldcat_alv.
*--------------------------Bill.Cat.------------------------*
  ls_fcat-fieldname = 'FKTYP'.
  ls_fcat-rollname = 'FKTYP'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Sales Organization------------------*
  ls_fcat-fieldname = 'VKORG'.
  ls_fcat-rollname = 'VKORG'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Billing date---------------------*
  ls_fcat-fieldname = 'FKDAT'.
  ls_fcat-rollname = 'FKDAT'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Sold-to party--------------------*
  ls_fcat-fieldname = 'KUNNR'.
  ls_fcat-rollname = 'KUNAG'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Billing Type---------------------*
  ls_fcat-fieldname = 'FKART'.
  ls_fcat-rollname = 'FKART'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Destination country---------------------*
  ls_fcat-fieldname = 'LLAND'.
  ls_fcat-rollname = 'LLAND'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-------------------Doc. number------------------------------*
  ls_fcat-fieldname = 'VBELN'.
  ls_fcat-rollname = 'VBELN'.
* begin of change MOD-003
  ls_fcat-no_convext = 'X'.
* end of change MOD-003
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Doc. item number-------------------*
  ls_fcat-fieldname = 'POSNR'.
  ls_fcat-rollname = 'POSNR_VA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Profitcenter-----------------------*
  ls_fcat-fieldname = 'PRCTR'.
  ls_fcat-rollname = 'PRCTR'.
* begin of change MOD-003
  ls_fcat-no_convext = 'X'.
* end of change MOD-003
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Net. Value------------------------*
  ls_fcat-fieldname = 'NETWR'.
  ls_fcat-rollname = 'NETWR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Currency---------------------------*
  ls_fcat-fieldname = 'WAERK'.
  ls_fcat-rollname = 'WAERK'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Distr.Channel-------------------*
  ls_fcat-fieldname = 'VTWEG'.
  ls_fcat-rollname = 'VTWEG'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Division---------------------------*
  ls_fcat-fieldname = 'SPART'.
  ls_fcat-rollname = 'SPART'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Doc. Cat--------------------------*
  ls_fcat-fieldname = 'VBTYP'.
  ls_fcat-rollname = 'VBTYP'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Address----------------------------*
  ls_fcat-fieldname = 'ADRNR'.
  ls_fcat-rollname = 'ADRNR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Name of the sold-to party-------------------*
  ls_fcat-fieldname = 'NAME1'.
  ls_fcat-rollname = 'NAME_AG'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------Location of the sold-to party--------------------------*
  ls_fcat-fieldname = 'ORT01'.
  ls_fcat-rollname = 'ORT01_AG'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*------------Shipping Point/Receiving Point----------------------------*
  ls_fcat-fieldname = 'VSTEL'.
  ls_fcat-rollname = 'VSTEL'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------POD status----------------------------------*
  ls_fcat-fieldname = 'PDSTK'.
  ls_fcat-rollname = 'PDSTK'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------System status line----------------------------*
  ls_fcat-fieldname = 'ZZSTATS'.
  ls_fcat-rollname = 'J_STEXT'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------System status line----------------------------*
  ls_fcat-fieldname = 'ZZSTATL'.
  ls_fcat-rollname = 'J_STEXT'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Collective number (SD)-------------------------*
  ls_fcat-fieldname = 'ZZSUBMI'.
  ls_fcat-rollname = 'SUBMI_SD'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*------------Rule for determining the next billing/invoice date--------*
  ls_fcat-fieldname = 'ZZPERIO'.
  ls_fcat-rollname = 'PERIO_FP'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Description----------------------------*
  ls_fcat-fieldname = 'ZZPETXT'.
  ls_fcat-rollname = 'BEZEI40'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Contract start date----------------------------*
  ls_fcat-fieldname = 'ZZBEDAT'.
  ls_fcat-rollname = 'VBDAT_VEDA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Contract end date----------------------------*
  ls_fcat-fieldname = 'ZZBIDAT'.
  ls_fcat-rollname = 'VNDAT_VEDA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Date for action----------------------------*
  ls_fcat-fieldname = 'ZZVASDA'.
  ls_fcat-rollname = 'VASDA'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------------------Sales office----------------------------------*
  ls_fcat-fieldname = 'ZZVKBUR_BEZ'.
  ls_fcat-rollname = 'VKBUR_BEZ'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.

ENDFORM.                    " build_field_catlog

*&---------------------------------------------------------------------*
*&      Form  fill_events_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM fill_events_f14 .

  DATA h_event       TYPE slis_alv_event.

  CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
    EXPORTING
      i_list_type = 0
    IMPORTING
      et_events   = g_events_tab.

*--- allocate form for user-command ---------------------------------*
  READ TABLE g_events_tab WITH KEY name = slis_ev_user_command
                        INTO h_event.
  IF sy-subrc = 0.
    MOVE g_form_user_command TO h_event-form.
    MODIFY g_events_tab FROM h_event INDEX sy-tabix.
  ENDIF.

ENDFORM.                    " fill_events_f14

*&--------------------------------------------------------------------*
*&      Form  user_command_l
*&--------------------------------------------------------------------*
*       text
*---------------------------------------------------------------------*
*      -->P_UCOMM    text
*      -->P_SELFIELD text
*---------------------------------------------------------------------*
FORM user_command_l USING p_ucomm LIKE sy-ucomm
                          p_selfield TYPE slis_selfield.

  p_selfield-refresh = c_s.
  PERFORM check_pf2_with_object_f16 USING p_ucomm.
  PERFORM set_p_selfield_general_f16 USING p_selfield.

  CASE p_ucomm.
    WHEN 'ISEL'.
      p_ucomm = 'DISP'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
  ENDCASE.

ENDFORM.

*&---------------------------------------------------------------------*
*&      Form  check_pf2_with_object_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*----------------------------------------------------------------------*
FORM check_pf2_with_object_f16  USING    p_ucomm.

  CHECK p_ucomm = g_ic1.
  p_ucomm = 'ISEL'.

ENDFORM.                    " check_pf2_with_object_f16

*&---------------------------------------------------------------------*
*&      Form  set_p_selfield_general_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM set_p_selfield_general_f16  USING f_selfield TYPE slis_selfield.

  f_selfield-col_stable = c_x.
  f_selfield-row_stable = c_x.

ENDFORM.                    " set_p_selfield_general_f16

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*      -->P_ENDCASE  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_f16  USING p_ucomm LIKE sy-ucomm
                                p_selfield TYPE slis_selfield.

  PERFORM check_object_tab_marked_f14 USING p_ucomm p_selfield.

  LOOP AT i_vkdfi WHERE selected = c_x .
    l_index = sy-tabix.
    PERFORM fcodes_with_mark_l USING p_ucomm p_selfield.
    i_vkdfi-selected = ' '.
    MODIFY i_vkdfi INDEX l_index.
  ENDLOOP.

  CLEAR p_ucomm.

ENDFORM.                    " fcodes_with_mark_f16

*&---------------------------------------------------------------------*
*&      Form  check_object_tab_marked_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM check_object_tab_marked_f14  USING    p_ucomm LIKE sy-ucomm
                                        p_selfield TYPE slis_selfield.

  READ TABLE i_vkdfi WITH KEY selected = c_x.

  IF NOT sy-subrc IS INITIAL.
    IF NOT p_selfield-tabindex IS INITIAL.
      READ TABLE i_vkdfi INDEX p_selfield-tabindex.
      i_vkdfi-selected = c_x.
      MODIFY i_vkdfi INDEX p_selfield-tabindex.
    ENDIF.
  ELSE.
*--- Checkbox markiert -----------------------------------------------*
    p_selfield-sel_tab_field = 'G_MARK'.
  ENDIF.

ENDFORM.                    " check_object_tab_marked_f14

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_l
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_F_UCOMM  text
*      -->P_F_SELFIELD  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_l  USING   p_ucomm LIKE sy-ucomm
                              p_selfield TYPE slis_selfield.

  DATA: h_ucomm LIKE sy-ucomm.

  CASE p_ucomm.
*   Display contract document
    WHEN 'DISP'.
      SET PARAMETER ID 'KTN' FIELD  i_vkdfi-vbeln.
      CALL TRANSACTION 'VA43' AND SKIP FIRST SCREEN.
  ENDCASE.

ENDFORM.                    " fcodes_with_mark_l

*&---------------------------------------------------------------------*
*&      Form  alv_display
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM alv_display .

  g_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
     EXPORTING
       i_callback_program                =  g_repid
       i_save                            = 'A'
       it_events                         =  g_events_tab[]
*      I_GRID_TITLE                      =
*      I_GRID_SETTINGS                   =
*      is_layout                         =  g_layout
       it_fieldcat                       =  gt_fieldcat[]
     TABLES
        t_outtab                         =  i_vkdfi.

ENDFORM.                    " alv_display

*Text symbol text£º

*E02:No documents selected !
*Selection text£º
*P_VKORG:D       Sales Organization
*S_FKART:D       Billing Type
*S_FKDAT:D       Billing date
*S_SPART:D       Division
*S_VTWEG:D       Distribution Channel
