*----------------------------------------------------------------------
* PROGRAM ID           : YAM_CONTRACT_PROFITABILITY                    *
* PROGRAM TITLE        : Contract profitability                        *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 30/03/2006                                    *
* DEVELOPMENT ID       : GAP00053: Rep P053                            *
* CHANGE REQUEST NUMBER: CD1K905129                                    *
* PROGRAM DESCRIPTION  : Create report to see the profitability of     *
*                        ended/closed contracts.                       *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* MOD-001 |2006.10.31|Luc Mertens      |CD1K907188|                    *
*                     prevent BCD-overflow                             *
* MOD-002 |2006.11.10|Luc Mertens      |CD1K907459|All-CR311           *
*                     add GAC to output and selection screen           *
*----------------------------------------------------------------------*

REPORT YAM_CONTRACT_PROFITABILITY no standard page heading
                                  line-size 200.

TYPE-POOLS: SLIS.

TABLES: vbak,                          "Sales Document: Header Data
* begin of insert MOD-002
        ce11000,
* end of insert MOD-002
        veda.                          "Contract Data


*- SELECTION SCREEN---------------------------------------------------
SELECTION-SCREEN : BEGIN OF BLOCK B1 WITH FRAME TITLE TEXT-001.
PARAMETERS:     p_VKORG  like vbak-vkorg obligatory memory id VKO.
SELECT-OPTIONS: S_VKBUR  for  vbak-vkbur,
                s_vkgrp  for  vbak-vkgrp,
                s_enddt  for  vbak-gueen obligatory.
* begin of insert MOD-002
SELECT-OPTIONS: s_ww006  for ce11000-ww006.
* end of insert MOD-002
SELECTION-SCREEN: END OF BLOCK B1.

*- VARIABLES----------------------------------------------------------
DATA: begin of gt_vbak occurs 0,
        vbeln like vbak-vbeln,
        vkbur like vbak-vkbur,
        vkgrp like vbak-vkgrp,
        auart like vbak-auart,
        kunnr like vbak-kunnr,
      end of gt_vbak.

DATA: begin of gt_copa occurs 0,
        ww004    type RKEG_WW004,
* begin of insert MOD-002
        ww006    type RKEG_WW006,
* end of insert MOD-002
        vrgar    type RKE_VRGAR,
        vv100    type RKE2_VV100,
        vv110    type RKE2_VV110,
        vv200    type RKE2_VV200,
        vv300    type RKE2_VV300,
        vv400    type RKE2_VV400,
        vv500    type RKE2_VV500,
        vv600    type RKE2_VV600,
        vv120    type RKE2_VV120,
       end of gt_copa.

** begin of insert MOD-04
DATA: begin of gt_cname occurs 0,
        name1 type AD_NAME1,
      end of gt_cname.
** end of insert MOD-04



DATA: begin of gt_final occurs 0,
        ww004         type RKEG_WW004,
* begin of insert MOD-002
        ww006    type RKEG_WW006,
* end of insert MOD-002

* begin of insert MOD-04
        name1 type AD_NAME1,
* end of insert MOD-04

        vkbur         type vkbur,
        vkgrp         type vkgrp,
        auart         type auart,
        kunnr         type kunag,
        act_rev(15)   type p decimals 2,
        plan_rev(15)  type p decimals 2,
        var_rev(15)   type p decimals 2,
        act_cost(15)  type p decimals 2,
        plan_cost(15) type p decimals 2,
        var_cost(15)  type p decimals 2,
        act_marg(15)  type p decimals 2,
        act_marg%(3)  type p decimals 2,
        plan_marg(15) type p decimals 2,
        plan_marg%(3) type p decimals 2,
        selected,
      end of gt_final.

DATA: g_TABNAME       LIKE TKETR-TABNAME   VALUE 'CE1xxxx',
      g_REPID         LIKE CEDDB-PROGRAM,
      gt_SELTAB       LIKE CEDDBSEL OCCURS 1 WITH HEADER LINE,
      g_TIMESTMP      LIKE TKETRLG-TIMESTMP,
      g_trcatnr       LIKE TKETRLT-TRCATNR,
      l_index         TYPE sy-tabix,
      g_exist(1)      type c,
      g_ic1           LIKE sy-ucomm VALUE '&IC1',
      gt_fieldcat     TYPE slis_t_fieldcat_alv,
      g_events_tab    TYPE slis_t_event,
      g_form_user_command TYPE slis_formname VALUE 'USER_COMMAND_L',
      wa_copa         like line of gt_copa,
      g_ww004_10(10)  type c,
      g_bukrs         type bukrs,
      g_kokrs         type kokrs,
      g_erkrs         type erkrs.

*- Constants------------------------------------------------------------
CONSTANTS: c_g(1)          type c value 'G',               "Contracts
           c_x(1)          type c value 'X',
           c_s(1)          type c value 'S',
           c_00(2)         type c value '00',
           c_01(2)         type c value '01',
* begin of insert MOD-001
           c_99999(3)      type p decimals 2 value '999.99',
* end of insert MOD-001
           c_APPLCLASS     LIKE TKETR-APPLCLASS VALUE 'KE',
           c_SUBCLASS      LIKE TKETR-SUBCLASS  VALUE '01'.

*-INITIALIZATION -------------------------------------------------------
INITIALIZATION.

  s_enddt-sign = 'I'.
  s_enddt-option = 'BT'.
  s_enddt-low(6) = sy-datum(6).
  s_enddt-low+6(2) = c_01.
  s_enddt-high = sy-datum.
  APPEND s_enddt.

*-START OF SELECTION----------------------------------------------------
START-OF-SELECTION.

* get company code
  select single bukrs into g_bukrs
      from TVKO where vkorg = p_vkorg.

  if sy-subrc ne 0.
    write:/ text-e01, p_vkorg.                       "SO does not exist
    exit.
  endif.

* get controlling area
  CALL FUNCTION 'COPA_ERKRS_FIND'
    EXPORTING
      BUKRS              = g_bukrs
      GSBER              = ' '
      KOKRS              = ' '
    IMPORTING
      ERKRS              = g_erkrs
      KOKRS              = g_kokrs
    EXCEPTIONS
      ERROR_KOKRS_FIND   = 1
      KOKRS_WRONG        = 2
      NO_ERKRS_DEFINED   = 3
      NO_ERKRS_FOR_KOKRS = 4
      OTHERS             = 5.

  IF SY-SUBRC <> 0.
    write: / text-e02, sy-subrc.
    exit.
  ENDIF.

* get summarization level from custom table
  select single trcatnr into g_trcatnr
      from yam_summ_levels
      where bukrs = g_bukrs.

  if sy-subrc ne 0.
    write:/ text-e04, 'YAM_SUMM_LEVELS'.
    exit.
  endif.

* select all ended/closed contracts
  SELECT vbeln vkbur auart kunnr vkgrp
        INTO corresponding fields of table gt_vbak
        FROM vbak
        WHERE vkorg eq p_vkorg
          AND vkbur in s_vkbur
          AND vkgrp in s_vkgrp
          AND vbtyp eq c_g
          AND gueen in s_enddt.

  if gt_vbak[] is initial.
    write: / text-i01.              "No contracts selected
    exit.
  endif.



*-----------------------------------------------------------------------
END-OF-SELECTION.

  sort gt_vbak by vbeln.

  loop at gt_vbak.

*   Build SELTAB
    refresh gt_seltab.

    CLEAR gt_SELTAB.
    gt_SELTAB-FNAM = 'WW004'.
    gt_seltab-sign = 'I'.
    gt_seltab-option = 'EQ'.
    gt_seltab-low = gt_vbak-vbeln+2(8).
    APPEND gt_SELTAB.

* begin of insert MOD-002
    if not s_ww006[] is initial.
      loop at s_ww006.
        clear gt_seltab.
        gt_SELTAB-FNAM = 'WW006'.
        gt_seltab-sign = s_ww006-sign.
        gt_seltab-option = s_ww006-option.
        gt_seltab-low = s_ww006-low.
        gt_seltab-high = s_ww006-high.
        APPEND gt_SELTAB.
      endloop.
    endif.
* end of insert MOD-002

    CLEAR gt_SELTAB.
    gt_SELTAB-FNAM = 'VRGAR'.
    gt_seltab-sign = 'I'.
    gt_seltab-option = 'EQ'.
    gt_seltab-low = '0'.
    APPEND gt_SELTAB.

    CLEAR gt_SELTAB.
    gt_SELTAB-FNAM = 'VRGAR'.
    gt_seltab-sign = 'I'.
    gt_seltab-option = 'EQ'.
    gt_seltab-low = '1'.
    APPEND gt_SELTAB.

    CLEAR gt_SELTAB.
    gt_SELTAB-FNAM = 'VRGAR'.
    gt_seltab-sign = 'I'.
    gt_seltab-option = 'EQ'.
    gt_seltab-low = 'F'.
    APPEND gt_SELTAB.

    CLEAR gt_SELTAB.
    gt_SELTAB-FNAM = 'VRGAR'.
    gt_seltab-sign = 'I'.
    gt_seltab-option = 'EQ'.
    gt_seltab-low = 'A'.
    APPEND gt_SELTAB.

    CLEAR gt_SELTAB.
    gt_SELTAB-FNAM = 'VRGAR'.
    gt_seltab-sign = 'I'.
    gt_seltab-option = 'EQ'.
    gt_seltab-low = '9'.
    APPEND gt_SELTAB.

    g_TABNAME+3(4) = g_ERKRS.
    g_repid = sy-repid.

    clear g_exist.

    CALL FUNCTION 'RKD_TR_LEVEL_READ'
      EXPORTING
        APPLCLASS         = c_APPLCLASS
        SUBCLASS          = c_SUBCLASS
        TABNAME           = g_TABNAME
        TRCATNR           = g_TRCATNR
        S_FORM_NAME       = 'GET_DATA'
        S_PROGRAM_NAME    = g_REPID
        READ_DIRTY        = ' '
      IMPORTING
        TIMESTMP          = g_TIMESTMP
      TABLES
        SELECTION_TABLE   = gt_SELTAB
      EXCEPTIONS
        CATALOG_NOT_FOUND = 1
        TRCATNR_NOT_FOUND = 2.

    IF SY-SUBRC <> 0.
      WRITE: /(79) 'Summarization level does not exist.'(E03)
             COLOR COL_NEGATIVE.
      STOP.
    ENDIF.

*.. show contract also when no summarization data was found
    if g_exist is initial.
      move gt_vbak-vbeln+2(8) to gt_copa-ww004.
      append gt_copa.
      clear gt_copa.
    endif.

  endloop.

* begin of change MOD-002
* sort gt_copa by ww004.
  sort gt_copa by ww004 ww006.
* end of change MOD-002

* calculate actual/planned costs and revenues
  loop at gt_copa into wa_copa.

    at new ww004.
      concatenate c_00 wa_copa-ww004 into g_ww004_10.
      read table gt_vbak with key vbeln = g_ww004_10
                   binary search.
    endat.

    move g_ww004_10 to gt_final-ww004.
* begin of insert MOD-002
    move wa_copa-ww006 to gt_final-ww006.
* end of insert MOD-002
    move-corresponding gt_vbak to gt_final.

    case wa_copa-vrgar.
      when '0'.
        gt_final-act_cost = wa_copa-vv200 + wa_copa-vv300 +
               wa_copa-vv400 + wa_copa-vv500 + wa_copa-vv600.
      when 'F'.
        gt_final-act_rev  = wa_copa-vv100 + wa_copa-vv120 +
               wa_copa-vv110.
      when '1'.
        gt_final-plan_cost = wa_copa-vv200 + wa_copa-vv300 +
               wa_copa-vv400 + wa_copa-vv500 + wa_copa-vv600.
      when 'A'.
        gt_final-plan_rev  = wa_copa-vv100.
      when '9'.
        gt_final-act_cost = wa_copa-vv200 + wa_copa-vv300 +
               wa_copa-vv400 + wa_copa-vv500 + wa_copa-vv600.
        gt_final-act_rev  = wa_copa-vv100 + wa_copa-vv120 +
               wa_copa-vv110.
      when others.
*       no action
    endcase.

    collect gt_final.
    clear gt_final.

  endloop.

* calculate variances and margins
  loop at gt_final.

    gt_final-var_rev    = gt_final-act_rev - gt_final-plan_rev.
    gt_final-var_cost   = gt_final-act_cost - gt_final-plan_cost.
    gt_final-act_marg   = gt_final-act_rev - gt_final-act_cost.

* begin of insert MOD-001
    CATCH SYSTEM-EXCEPTIONS ARITHMETIC_ERRORS = 1.
* end of insert MOD-001
      if gt_final-act_rev ne 0.
        gt_final-act_marg%  =
                  ( gt_final-act_marg / gt_final-act_rev ) * 100.
      endif.
* begin of insert MOD-001
    ENDCATCH.
    if sy-subrc = 1.
      gt_final-act_marg% = c_99999.
    endif.
* end of insert MOD-001

    gt_final-plan_marg  = gt_final-plan_rev - gt_final-plan_cost.

* begin of insert MOD-001
    CATCH SYSTEM-EXCEPTIONS ARITHMETIC_ERRORS = 1.
* end of insert MOD-001
      if gt_final-plan_rev ne 0.
        gt_final-plan_marg% =
                  ( gt_final-plan_marg / gt_final-plan_rev ) * 100.
      endif.
* begin of insert MOD-001
    ENDCATCH.
    if sy-subrc = 1.
      gt_final-plan_marg% = c_99999.
    endif.
* end of insert MOD-001


**********************************************************************
"Add Customer Name
Data:  Lv_adrnr Type Kna1-adrnr.
Clear Lv_adrnr.
Select Single Adrnr Into Lv_adrnr From Kna1 Where Kunnr Eq Gt_final-kunnr.
 If Sy-subrc Eq 0.
 Select Single Name1 Into Gt_final-name1 From Adrc Where Addrnumber Eq Lv_adrnr .
 Endif.
**********************************************************************
    modify gt_final transporting var_rev var_cost act_marg act_marg%
                                 plan_marg plan_marg% name1.

  endloop.

* create ALV-GRID
  PERFORM build_field_catlog CHANGING gt_fieldcat.
  PERFORM fill_events_f14.
  PERFORM alv_display.


*- SUBROUTINES---------------------------------------------------------
*----------------------------------------------------------------------
*       FORM GET_DATA
*----------------------------------------------------------------------
*       routine which processes the read data called by
*       RKD_TR_LEVEL_READ.
*----------------------------------------------------------------------
*  -->  CE1_TAB   line item table with structure CE1xxxx
*  <->  p2        flag, cancel selection
*----------------------------------------------------------------------
FORM GET_DATA
     TABLES CE1_TAB
     USING  EXIT_FLAG LIKE CEDDB-FLAG.

  LOOP AT CE1_TAB.
    move-corresponding CE1_TAB to gt_copa.
    append gt_copa.
    clear gt_copa.
    g_exist = c_x.
  ENDLOOP.

ENDFORM.                    "GET_DATA

*&---------------------------------------------------------------------*
*&      Form  build_field_catlog
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      <--P_GT_FIELDCAT  text
*----------------------------------------------------------------------*
FORM build_field_catlog  CHANGING pt_fieldcat TYPE slis_t_fieldcat_alv.

  DATA : ls_fcat TYPE slis_fieldcat_alv.

*--------------------------Contract-------------------------*
  ls_fcat-fieldname = 'WW004'.
  ls_fcat-rollname = 'RKEG_WW004'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------GAC------------------------------*
  ls_fcat-fieldname = 'WW006'.
  ls_fcat-rollname = 'RKEG_WW006'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Contract type-----------------------*
  ls_fcat-fieldname = 'AUART'.
  ls_fcat-rollname = 'AUART'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Sales office-----------------------*
  ls_fcat-fieldname = 'VKBUR'.
  ls_fcat-rollname = 'VKBUR'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Sales group------------------------*
  ls_fcat-fieldname = 'VKGRP'.
  ls_fcat-rollname = 'VKGRP'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Sold-to party---------------------*
  ls_fcat-fieldname = 'KUNNR'.
  ls_fcat-rollname = 'KUNAG'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Actual Revenues------------------*
  ls_fcat-fieldname = 'ACT_REV'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Actual Revenues'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Planned Revenues---------------------*
  ls_fcat-fieldname = 'PLAN_REV'.
  ls_fcat-outputlen = '16'.
  ls_fcat-seltext_l = 'Planned Revenues'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Variance on Revenues-----------------*
  ls_fcat-fieldname = 'VAR_REV'.
  ls_fcat-outputlen = '20'.
  ls_fcat-seltext_l = 'Variance on Revenues'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Actual cost------------------------*
  ls_fcat-fieldname = 'ACT_COST'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Actual Cost'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Planned cost--------------------*
  ls_fcat-fieldname = 'PLAN_COST'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Planned Cost'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Variance on Cost-----------------*
  ls_fcat-fieldname = 'VAR_COST'.
  ls_fcat-outputlen = '16'.
  ls_fcat-seltext_l = 'Variance on Cost'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Actual Margin-------------------*
  ls_fcat-fieldname = 'ACT_MARG'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Actual Margin'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Actual Margin in %---------------*
  ls_fcat-fieldname = 'ACT_MARG%'.
  ls_fcat-outputlen = '18'.
  ls_fcat-seltext_l = 'Actual Margin in %'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Plan. Margin-------------------*
  ls_fcat-fieldname = 'PLAN_MARG'.
  ls_fcat-outputlen = '15'.
  ls_fcat-seltext_l = 'Planned Margin'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------Plan. Margin in %---------------*
  ls_fcat-fieldname = 'PLAN_MARG%'.
  ls_fcat-outputlen = '19'.
  ls_fcat-seltext_l = 'Planned Margin in %'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*------------------------Customer  name   *----------------------*
  ls_fcat-fieldname = 'NAME1'.
  ls_fcat-outputlen = '30'.
  ls_fcat-seltext_l = 'Customer'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.


ENDFORM.                    " build_field_catlog

*&---------------------------------------------------------------------*
*&      Form  fill_events_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM fill_events_f14 .

  DATA h_event       TYPE slis_alv_event.

  CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
    EXPORTING
      i_list_type = 0
    IMPORTING
      et_events   = g_events_tab.

*--- allocate form for user-command ---------------------------------*
  READ TABLE g_events_tab WITH KEY name = slis_ev_user_command
                        INTO h_event.
  IF sy-subrc = 0.
    MOVE g_form_user_command TO h_event-form.
    MODIFY g_events_tab FROM h_event INDEX sy-tabix.
  ENDIF.

ENDFORM.                    " fill_events_f14

*&--------------------------------------------------------------------*
*&      Form  user_command_l
*&--------------------------------------------------------------------*
*       text
*---------------------------------------------------------------------*
*      -->P_UCOMM    text
*      -->P_SELFIELD text
*---------------------------------------------------------------------*
FORM user_command_l USING p_ucomm LIKE sy-ucomm
                          p_selfield TYPE slis_selfield.

  p_selfield-refresh = c_s.
  PERFORM check_pf2_with_object_f16 USING p_ucomm.
  PERFORM set_p_selfield_general_f16 USING p_selfield.

  CASE p_ucomm.
    WHEN 'ISEL'.
      p_ucomm = 'DISP'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
  ENDCASE.

ENDFORM.                    "user_command_l

*&---------------------------------------------------------------------*
*&      Form  check_pf2_with_object_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*----------------------------------------------------------------------*
FORM check_pf2_with_object_f16  USING    p_ucomm.

  CHECK p_ucomm = g_ic1.
  p_ucomm = 'ISEL'.

ENDFORM.                    " check_pf2_with_object_f16

*&---------------------------------------------------------------------*
*&      Form  set_p_selfield_general_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM set_p_selfield_general_f16  USING f_selfield TYPE slis_selfield.

  f_selfield-col_stable = c_x.
  f_selfield-row_stable = c_x.

ENDFORM.                    " set_p_selfield_general_f16

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*      -->P_ENDCASE  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_f16  USING p_ucomm LIKE sy-ucomm
                                p_selfield TYPE slis_selfield.

  PERFORM check_object_tab_marked_f14 USING p_ucomm p_selfield.

  LOOP AT gt_final WHERE selected = c_x .
    l_index = sy-tabix.
    PERFORM fcodes_with_mark_l USING p_ucomm p_selfield.
    gt_final-selected = ' '.
    MODIFY gt_final INDEX l_index.
  ENDLOOP.

  CLEAR p_ucomm.

ENDFORM.                    " fcodes_with_mark_f16

*&---------------------------------------------------------------------*
*&      Form  check_object_tab_marked_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM check_object_tab_marked_f14  USING    p_ucomm LIKE sy-ucomm
                                        p_selfield TYPE slis_selfield.

  READ TABLE gt_final WITH KEY selected = c_x.

  IF NOT sy-subrc IS INITIAL.
    IF NOT p_selfield-tabindex IS INITIAL.
      READ TABLE gt_final INDEX p_selfield-tabindex.
      gt_final-selected = c_x.
      MODIFY gt_final INDEX p_selfield-tabindex.
    ENDIF.
  ELSE.
*--- Checkbox markiert -----------------------------------------------*
    p_selfield-sel_tab_field = 'G_MARK'.
  ENDIF.

ENDFORM.                    " check_object_tab_marked_f14

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_l
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_F_UCOMM  text
*      -->P_F_SELFIELD  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_l  USING   p_ucomm LIKE sy-ucomm
                              p_selfield TYPE slis_selfield.

  DATA: h_ucomm LIKE sy-ucomm.

  CASE p_ucomm.
*   Display contract document
    WHEN 'DISP'.
      SET PARAMETER ID 'KTN' FIELD  gt_final-ww004.
      CALL TRANSACTION 'VA43' AND SKIP FIRST SCREEN.
  ENDCASE.

ENDFORM.                    " fcodes_with_mark_l

*&---------------------------------------------------------------------*
*&      Form  alv_display
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM alv_display .

  g_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
     EXPORTING
       i_callback_program                =  g_repid
       i_save                            = 'A'
       it_events                         =  g_events_tab[]
*      I_GRID_TITLE                      =
*      I_GRID_SETTINGS                   =
*      is_layout                         =  g_layout
       it_fieldcat                       =  gt_fieldcat[]
     TABLES
        t_outtab                         =  gt_final.

ENDFORM.                    " alv_display

*Text symbol text£º
*001:Selection screen input
*E01:No company code found for Sales Organization
*E02:Controlling area could not be derived, returncode
*E03:Summarization level does not exist.
*E04:Summarization level not found in custom table

*I01:No contracts selected !
*Selection text£º
*P_VKORG:D       Sales Organization
*S_ENDDT:        Contract End Date
*S_VKBUR:D       Sales office
*S_VKGRP:D       Sales group
*S_WW006:D       GAC
