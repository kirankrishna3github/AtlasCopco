*----------------------------------------------------------------------*
* PROGRAM ID           : YAM_DEL_FILES_FROM_APPL_SERVER                *
* PROGRAM TITLE        : AM: Delete files from application server      *
* AUTHOR               : Luc Mertens                                   *
* DATE                 : 28/11/2005                                    *
* DEVELOPMENT ID       :                                               *
*                                                                      *
* CHANGE REQUEST NUMBER: CD1K904032                                    *
*                                                                      *
* Program Description: This program deletes files from the appl.server *
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
*                                                                      *
************************************************************************
REPORT YAM_DEL_FILES_FROM_APPL_SERVER MESSAGE-ID yam_re.


************************************************************************
*       V A R I A B L E S                                              *
************************************************************************
TYPES: NAME_OF_DIR(1024)        TYPE C,
       NAME_OF_FILE(260)        TYPE C.

DATA: BEGIN OF searchpoints OCCURS 1,
        dirname     TYPE NAME_OF_DIR,  " name of directory.
        sp_name     TYPE NAME_OF_FILE, " name of entry.
        sp_cs(40)   TYPE c,         " ContainsString pattern for name.
      END OF searchpoints.

DATA: BEGIN OF file,
        dirname     TYPE NAME_OF_DIR,  " name of directory. (possibly
                                       " truncated.)
        name        TYPE NAME_OF_FILE, " name of entry. (possibly
                                       " truncated.)
        errno(3)    TYPE c,
        errmsg(40)  TYPE c,
      END OF file.

DATA: BEGIN OF file_list OCCURS 100,
        dirname     TYPE NAME_OF_DIR,  " name of directory. (possibly
                                       " truncated.)
        name        TYPE NAME_OF_FILE, " name of entry. (possibly
                                       " truncated.)
        errno(3)    TYPE c,
        errmsg(40)  TYPE c,
      END OF file_list.

DATA: g_delfile    TYPE epspath,
      g_lin        type i,
      g_numlin(3)  type c,
      g_answer,
      g_text_quest(72) type c,
      g_subrc      LIKE sy-subrc.

CONSTANTS:
*     c_no_cs   TYPE C VALUE ' ',      " no MUST_ContainString
      c_all     TYPE C value '*'.    "generic filename shall select all

************************************************************************
*       S E L E C T - O P T I O N S / P A R A M E T E R S              *
************************************************************************
SELECTION-SCREEN : BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.

PARAMETERS: p_serv type msname,
            p_dir  like EPSF-EPSDIRNAM default 'DIR_HOME',
            p_file like EPSF-EPSFILNAM default 'YAM*' lower case
                                           obligatory.

SELECTION-SCREEN: END OF BLOCK b1.

************************************************************************
INITIALIZATION.

* the following is to read the own application server for default value
  CALL 'C_SAPGPARAM' ID 'NAME' FIELD 'rdisp/myname'
                     ID 'VALUE' FIELD p_serv.

************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.

  if p_file(1) eq 'Y' or
     p_file(1) eq 'y' or
     p_file(1) eq 'Z' or
     p_file(1) eq 'z'.
*   OK - delete only files beginning with Y/Z
  else.
*   do not delete
    message e001(00) with text-e02.
  endif.

  if p_file(1) eq '*'.
    message e001(00) with text-e04.
  endif.

* get name of directory
  CALL 'C_SAPGPARAM' ID 'NAME'  FIELD p_dir
                     ID 'VALUE' FIELD searchpoints-dirname.

* get directory
  g_subrc = 0.
  REFRESH file_list.
  REFRESH searchpoints.

  searchpoints-sp_name = c_all.
  searchpoints-sp_cs   = p_file.
  APPEND searchpoints.

  LOOP AT searchpoints.
    PERFORM fill_file_list USING searchpoints-dirname
                                 searchpoints-sp_name
                                 searchpoints-sp_cs.
    g_subrc = sy-subrc.
  ENDLOOP.

  if g_subrc <> 0 or
     file_list[] is initial.
    write: / text-e03.                "No files were selected
    exit.
  endif.

* popup with warning before deleting selected files
  describe table file_list lines g_lin.
  g_numlin = g_lin.
  g_text_quest = text-t02.
  replace '&' in g_text_quest with g_numlin.

  CALL FUNCTION 'POPUP_TO_CONFIRM'
    EXPORTING
      titlebar              = text-t01
      text_question         = g_text_quest
      text_button_1         = 'Yes'
*     icon_button_1         = 'ICON_OKAY'
      text_button_2         = 'No'
*     icon_button_2         =
      default_button        = '2'
      display_cancel_button = space
    IMPORTING
      answer                = g_answer
    EXCEPTIONS
      text_not_found        = 1
      OTHERS                = 2.

  IF g_answer EQ '2'.
    write: / text-i02.                 "No files were deleted
    exit.
  ENDIF.

  LOOP AT file_list.
    concatenate file_list-dirname '/' file_list-name into g_delfile.
    delete dataset g_delfile.

    if sy-subrc = 0.
      write: / text-i01, g_delfile.   "File deleted
    else.
      write: / text-e01, g_delfile.   "Could not delete file
    endif.
  ENDLOOP.


*--------------------------------------------------------------------*
* S U B R O U T I N E S                                              *
*--------------------------------------------------------------------*
FORM fill_file_list USING a_dir_name a_generic_name a_must_cs.

  CALL 'C_DIR_READ_FINISH'             " just to be sure
      ID 'ERRNO'  FIELD file_list-errno
      ID 'ERRMSG' FIELD file_list-errmsg.

  CALL 'C_DIR_READ_START' ID 'DIR'    FIELD a_dir_name
                          ID 'FILE'   FIELD a_generic_name
                          ID 'ERRNO'  FIELD file-errno
                          ID 'ERRMSG' FIELD file-errmsg.
  IF sy-subrc <> 0.
    sy-subrc = 4.
    EXIT.
  ENDIF.

  DO.
    CLEAR file.
    CALL 'C_DIR_READ_NEXT'
      ID 'NAME'   FIELD file-name
      ID 'ERRNO'  FIELD file-errno
      ID 'ERRMSG' FIELD file-errmsg.
    file-dirname = a_dir_name.

    if sy-subrc <> 0.
      EXIT.
    endif.

*   Does the filename contains the requested pattern?
*   Then store it, else forget it.
    IF a_must_cs CS '*'.
      if file-name CP a_must_cs.
        MOVE-CORRESPONDING file TO file_list.
        APPEND file_list.
      endif.
    ELSE.
      IF file-name EQ a_must_cs.
        MOVE-CORRESPONDING file TO file_list.
        APPEND file_list.
      ENDIF.
    ENDIF.
  ENDDO.

  CALL 'C_DIR_READ_FINISH'
      ID 'ERRNO'  FIELD file_list-errno
      ID 'ERRMSG' FIELD file_list-errmsg.
  IF sy-subrc <> 0.
    WRITE: / 'C_DIR_READ_FINISH', 'SUBRC', sy-subrc.
  ENDIF.

  sy-subrc = 0.

ENDFORM.                    "fill_file_list

*Text symbol text£º
*001:Selection Screen Input
*E01:Could not delete file :
*E02:Delete only files beginning with Y or Z
*E03:No files were selected
*E04:No asterisk in first position allowed !
*I01:File deleted :
*I02:No files were deleted
*T01:Delete file processing

*T02:Do you want the selected & file(s) to be deleted?
*Selection text£º
*P_DIR:        Directory
*P_FILE:        File name
*P_SERV:        Server name
