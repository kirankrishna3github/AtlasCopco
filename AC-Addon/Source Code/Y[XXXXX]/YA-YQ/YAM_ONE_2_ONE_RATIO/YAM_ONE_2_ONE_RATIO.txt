*----------------------------------------------------------------------
* PROGRAM ID           : YAM_ONE_2_ONE_RATIO                           *
* PROGRAM TITLE        : List of equipment in contract or without      *
*                        service since a date                          *
* AUTHOR               : Marc Jacobs                                   *
* DATE                 : 19/11/2007                                    *
* DEVELOPMENT ID       : CR365                                         *
* CHANGE REQUEST NUMBER: CD1K923544                                    *
* PROGRAM DESCRIPTION  : List all equipment in contract or not         *
*                        List of equipment without service since a date*
*----------------------------------------------------------------------*
* CHANGE HISTORY LOG                                                   *
*----------------------------------------------------------------------*
* MOD. NO.|  DATE    | NAME     |CORRECTION NUMBER| CHANGE REFERENCE # *
*----------------------------------------------------------------------*
* X0001   |12/03/2008|S. Basu   |                 |DB2 Hint Performance*
*         |          |          |                 |                    *
*         |          |          |                 |                    *
*----------------------------------------------------------------------*
* X0002   |08/04/2009|Uzzawal   |CD1K947540       |CR565- Add multiple *
*                                                          selection   *
*         |          |          |                 |                    *
*         |          |          |                 |                    *
*----------------------------------------------------------------------*
* X0003   |12/06/2009|Uzzawal   |CD1K948700       |INCIDENT 4664       *
*         |          |          |                 |                    *
*         |          |          |                 |                    *
*----------------------------------------------------------------------*


REPORT yam_one_2_one_ratio .
************************************************************************
*                   T A B L E S                                        *
************************************************************************
TABLES : rihea, vbak.
************************************************************************
*                   C O N S T A N T S                                  *
************************************************************************
CONSTANTS : c_equipment(10) TYPE c  VALUE 'EQUIPMENT',
            c99991231 LIKE sy-datum VALUE '99991231',
            c_x(1) TYPE c           VALUE 'X',
            c_s(1) TYPE c           VALUE 'S',
            c_6(1) TYPE c           VALUE '6',
            c_4(1) TYPE c           VALUE '4',
            c_3(1) TYPE c           VALUE '3',
            c_2(1) TYPE c           VALUE '2',
            c_0(1) TYPE c           VALUE '0'.

TYPE-POOLS : fibs,stree.
TYPE-POOLS: slis.

************************************************************************
*                   V A R I A B L E S                                  *
************************************************************************
DATA : BEGIN OF gt_equ OCCURS 0,
       equnr LIKE equi-equnr,
       matnr LIKE equi-matnr.
DATA : END OF gt_equ.
DATA : BEGIN OF i_tot_equi OCCURS 0,
        pgc TYPE yam_pgcid ,
        number(4) TYPE p ,
        service(4) TYPE p .
DATA : END OF i_tot_equi.

DATA : BEGIN OF i_cont_equi OCCURS 0,
        auart LIKE vbak-auart,
        pgc TYPE yam_pgcid ,
        service(4) TYPE p .
DATA : END OF i_cont_equi.

DATA : BEGIN OF i_no_cont_equi OCCURS 0,
        pgc TYPE yam_pgcid ,
        service(4) TYPE p .
DATA : END OF i_no_cont_equi.
DATA : BEGIN OF i_tot_cont_equi OCCURS 0,
        pgc TYPE yam_pgcid ,
        service(4) TYPE p .
DATA : END OF i_tot_cont_equi.

DATA:  BEGIN OF lt_ord_header OCCURS 0,
       aufnr LIKE viaufkst-aufnr,
       addat LIKE viaufkst-addat.
DATA:  END   OF lt_ord_header.

DATA : BEGIN OF i_121 OCCURS 0,
       text(30)    TYPE c,
       pgcdesc(45) TYPE c,
       number(4)   TYPE p,
       service(4)  TYPE p,
       ratio       TYPE fm_proz32,
END OF i_121.

DATA : BEGIN OF i_equi_no_serv OCCURS 0,
        equnr LIKE v_equi-equnr,
        sernr LIKE v_equi-sernr,
        matnr LIKE v_equi-matnr,
        eqktx LIKE v_equi-eqktx,
        tplnr LIKE v_equi-tplnr,
        parnr LIKE ihpa-parnr,
        name1 LIKE kna1-name1,
        erdat LIKE v_equi-erdat,
        sttxt(40) TYPE c ,
        sttxu(40) TYPE c ,
        aufnr LIKE afih-aufnr,
        gstrp LIKE caufv-gstrp,
        pgc(4) TYPE c ,
        cuswar LIKE sy-datum ,
        venwar LIKE sy-datum ,
        vkbur LIKE v_equi-vkbur,
        vkgrp LIKE v_equi-vkgrp,
        gewrk LIKE v_equi-gewrk,
        arbpl LIKE crhd-arbpl,
        selected,
END OF i_equi_no_serv.

DATA :  BEGIN OF gt_stai1 OCCURS 0,
      low(4) TYPE c,
     END OF gt_stai1.
DATA: BEGIN OF h_status_tab OCCURS 20.
        INCLUDE STRUCTURE jstat.
DATA: END OF h_status_tab.

DATA: BEGIN OF h_status_text_tab OCCURS 20,
        txt04 LIKE tj02t-txt04.
DATA: END OF h_status_text_tab.
DATA: h_stat_flag.

* declaration for events table where user comand or set PF status will
*be defined
DATA: v_events TYPE slis_t_event,
     wa_event TYPE slis_alv_event.

DATA : t_equi_warranties LIKE alm_me_tob_warranty OCCURS 0.
DATA : wa_equi_warranties LIKE alm_me_tob_warranty.
DATA : return LIKE bapiret2 OCCURS 0.
DATA: g_stai1_lines LIKE sy-tabix.
DATA: g_stae1_lines LIKE sy-tabix.

DATA : g_incl(1) TYPE c,
       g_excl(1) TYPE c,
       g_actcont(1) TYPE c,
       g_service(1) TYPE c.
DATA : g_objnr LIKE jest-objnr.
DATA : g_vbeln LIKE vbak-vbeln.
DATA : g_matnr LIKE viser02-matnr.
DATA : g_prdha LIKE mara-prdha.
DATA : g_pgc(4) TYPE c.
DATA : g_auart LIKE vbak-auart.
DATA : g_aufnr LIKE aufk-aufnr.
DATA : g_svaufnr LIKE aufk-aufnr.
DATA : g_gstrp LIKE caufv-gstrp.
DATA : g_svgstrp LIKE caufv-gstrp.
DATA : g_number(7) TYPE c.
DATA : g_serviced(7) TYPE c.
DATA : g_ratioc TYPE fm_proz32.
DATA : g_ratio(7) TYPE c.
DATA : g_deleted(1) TYPE c.
DATA :  BEGIN OF gt_stae1 OCCURS 0,
 low(4) TYPE c,
END OF gt_stae1.
DATA : g_equnr LIKE equi-equnr.
DATA : t_node TYPE snodetext.
DATA : gt_fieldcat     TYPE slis_t_fieldcat_alv,
       g_events_tab    TYPE slis_t_event,
       g_form_user_command TYPE slis_formname VALUE 'USER_COMMAND_L',
       g_ic1           LIKE sy-ucomm VALUE '&IC1',
       g_repid         LIKE sy-repid,
       l_index         TYPE sy-tabix.
DATA : it_t179t TYPE t179t               OCCURS 0,
       wa_t179t TYPE t179t.
DATA : g_pgc_desc(15) TYPE c.


DATA : node_tab LIKE t_node OCCURS 0 WITH HEADER LINE.
************************************************************************
*       S E L E C T - O P T I O N S / P A R A M E T E R S              *
************************************************************************
SELECTION-SCREEN : BEGIN OF BLOCK b1 WITH FRAME TITLE text-001.

PARAMETERS :
p_werks TYPE werks_d OBLIGATORY ,
p_vkorg TYPE vkorg OBLIGATORY ,
p_vtweg TYPE vtweg DEFAULT '11' OBLIGATORY ,
p_spart TYPE spart DEFAULT '01' OBLIGATORY .
SELECTION-SCREEN SKIP.
* sales office / sales group
SELECTION-SCREEN : BEGIN OF BLOCK b2.
*>>>>> Begin of Change X0002
SELECT-OPTIONS:
  p_vkbur FOR vbak-vkbur  NO INTERVALS,
  p_vkgrp FOR vbak-vkgrp  NO INTERVALS.

*PARAMETERS : p_vkbur LIKE vbak-vkbur,
*             p_vkgrp LIKE vbak-vkgrp.
*>>>>> End of Change X0002
SELECTION-SCREEN: END OF BLOCK b2.
SELECTION-SCREEN SKIP.
* Order status included / excluded
SELECT-OPTIONS:
  so_stai1 FOR rihea-i_estatin MATCHCODE OBJECT i_status NO INTERVALS,
  so_stae1 FOR rihea-i_estatex MATCHCODE OBJECT i_status NO INTERVALS.
SELECTION-SCREEN SKIP.
* date
PARAMETER : p_date LIKE sy-datum.
SELECTION-SCREEN SKIP.
* type of list
PARAMETERS: p_1two1  TYPE c RADIOBUTTON GROUP radi DEFAULT 'X',
            p_eqnos  TYPE c RADIOBUTTON GROUP radi .
SELECTION-SCREEN: END OF BLOCK b1.

AT SELECTION-SCREEN ON BLOCK b2.
*>>>>> Begin of Change X0002
*  IF ( NOT p_vkbur IS INITIAL AND p_vkgrp IS INITIAL ) OR
*   ( p_vkbur IS INITIAL AND p_vkgrp IS NOT INITIAL ).
*    MESSAGE e001(00) WITH text-e02.
*  ENDIF.
*>>>>> End of Change X0002

* authority check
  AUTHORITY-CHECK OBJECT 'V_VBAK_VKO'
         ID 'VKORG' FIELD p_vkorg
         ID 'VTWEG' FIELD p_vtweg
         ID 'SPART' FIELD p_spart
         ID 'ACTVT' FIELD '01'.

  IF sy-subrc <> 0.
    MESSAGE e001(00) WITH text-e01 p_vkorg.
  ENDIF.

*&---------------------------------------------------------------------*
INITIALIZATION.
*&---------------------------------------------------------------------*
* to become equipment statusses at F4
  EXPORT p_obtyp = 'IEQ' TO MEMORY ID 'PM_OBTYP'.

  p_date = sy-datum - 365.

************************************************************************
*       S T A R T - O F - S E L E C T I O N    E V E N T               *
************************************************************************
START-OF-SELECTION.
* fill status tables
  REFRESH : gt_stai1,
            gt_stae1.

  LOOP AT so_stai1.
    CLEAR gt_stai1.
    MOVE so_stai1-low TO gt_stai1-low.
    APPEND gt_stai1.
  ENDLOOP.
  LOOP AT so_stae1.
    CLEAR gt_stae1.
    MOVE so_stae1-low TO gt_stae1-low.
    APPEND gt_stae1.
  ENDLOOP.
* number of records in the status-tables
  DESCRIBE TABLE gt_stai1 LINES g_stai1_lines.
  DESCRIBE TABLE gt_stae1 LINES g_stae1_lines.

*>>>>> Begin of Change X0002
*   IF  p_vkbur IS INITIAL or p_vkgrp IS INITIAL.
*    MESSAGE e001(00) WITH text-e02.
*   endif.
*>>>>> End of Change X0002

* One-2-One
  IF p_1two1 = 'X'.
    REFRESH: i_tot_equi,
             i_cont_equi,
             i_no_cont_equi.
    PERFORM select_equi_one_2_one.
  ELSE.
* Equipment without service
    REFRESH: i_equi_no_serv.
    PERFORM select_equi_without_service.
    PERFORM add_info_to_equi.
  ENDIF.
************************************************************************
*       E N D - O F - S E L E C T I O N    E V E N T                   *
************************************************************************
END-OF-SELECTION .

* one-2-one
  IF p_1two1 IS NOT INITIAL.
    IF i_tot_equi[] IS INITIAL.
      WRITE: /.
      WRITE: / 'No equipments selected for One-2-One'(i01).
    ELSE.
      PERFORM get_t179t.
      PERFORM fill_i_one2one.
      PERFORM bld_fld_catlog_121 CHANGING gt_fieldcat.
      PERFORM fill_events_f14.
      PERFORM populate_event.
      PERFORM display_alv_121.
    ENDIF.
  ELSE.
* equipments without service.
    IF i_equi_no_serv[] IS INITIAL.
      WRITE: /.
      WRITE: / 'No equipments without service selected'(i02).
    ELSE.
      PERFORM build_field_catlog CHANGING gt_fieldcat.
      PERFORM fill_events_f14.
      PERFORM display_alv.
    ENDIF.
  ENDIF.
*&---------------------------------------------------------------------*
*&      Form  check_statusses
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM check_statusses USING g_equnr.

  CLEAR: g_excl,
         g_incl.

  REFRESH: h_status_tab,
           h_status_text_tab.

  CONCATENATE 'IE' g_equnr INTO g_objnr.

*--- get active order statusses
  CALL FUNCTION 'STATUS_READ'
    EXPORTING
      objnr       = g_objnr
      only_active = 'X'
    TABLES
      status      = h_status_tab
    EXCEPTIONS
      OTHERS      = 01.
  CHECK sy-subrc = 0.
*--- get order status descriptions
  LOOP AT h_status_tab.
    CALL FUNCTION 'STATUS_NUMBER_CONVERSION'
      EXPORTING
        language      = sy-langu
        objnr         = g_objnr
        status_number = h_status_tab-stat
      IMPORTING
        txt04         = h_status_text_tab-txt04
      EXCEPTIONS
        OTHERS        = 01.
    IF sy-subrc = 0.
      APPEND h_status_text_tab.
    ENDIF.
  ENDLOOP.

*--- check status inclusive
  IF NOT g_stai1_lines IS INITIAL.
    LOOP AT h_status_text_tab.
      LOOP AT gt_stai1.
        IF gt_stai1-low = h_status_text_tab-txt04.
          g_incl = 'X'.
          EXIT.
        ENDIF.
      ENDLOOP.
      IF g_incl = 'X'.
        EXIT.
      ENDIF.
    ENDLOOP.
  ENDIF.

*--- check status exclusive
  IF NOT g_stae1_lines IS INITIAL.
    LOOP AT h_status_text_tab.
      LOOP AT gt_stae1.
        IF gt_stae1-low = h_status_text_tab-txt04.
          g_excl = 'X'.
          EXIT.
        ENDIF.
      ENDLOOP.
      IF g_excl = 'X'.
        EXIT.
      ENDIF.
    ENDLOOP.
  ENDIF.

ENDFORM.                    "check_statusses
*&---------------------------------------------------------------------*
*&      Form  select_equi_without_service
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM select_equi_without_service.

* select all the equipments for the selection
  IF p_vkbur IS INITIAL.
    SELECT equnr sernr matnr eqktx tplnr erdat vkbur vkgrp gewrk
    FROM v_equi
    INTO CORRESPONDING FIELDS OF TABLE i_equi_no_serv
    WHERE eqart = c_equipment
      AND   datbi = c99991231
      AND   swerk = p_werks
      AND   vkorg = p_vkorg
      AND   vtweg = p_vtweg
      AND   spart = p_spart
      AND   spras = sy-langu
    %_HINTS DB6 'USE_OPTLEVEL 0'.                  "+x0001
  ELSE.
    SELECT equnr sernr matnr eqktx tplnr erdat vkbur vkgrp gewrk
    FROM v_equi
    INTO CORRESPONDING FIELDS OF TABLE i_equi_no_serv
    WHERE eqart = c_equipment
          AND   datbi = c99991231
          AND   swerk = p_werks
          AND   vkorg = p_vkorg
          AND   vtweg = p_vtweg
          AND   vkbur IN p_vkbur                   "+x0003
          AND   vkgrp in p_vkgrp                   "+x0002
          AND   spart = p_spart
          AND   spras = sy-langu
    %_HINTS DB6 'USE_OPTLEVEL 0'.                  "+x0001
  ENDIF.
* extra selection on equipments
  LOOP AT i_equi_no_serv.
* check status inclusive / exlusive
    IF NOT ( g_stai1_lines IS INITIAL
        AND g_stae1_lines IS INITIAL ) .
      PERFORM check_statusses USING i_equi_no_serv-equnr.
      IF NOT g_stai1_lines IS INITIAL AND g_incl = ' '.
        DELETE i_equi_no_serv.
        CONTINUE.
      ELSEIF NOT g_stae1_lines IS INITIAL AND g_excl = 'X'.
        DELETE i_equi_no_serv.
        CONTINUE.
      ENDIF.
    ENDIF.
* check equipment in active contract --> delete from internal table
    CLEAR: g_vbeln,
           g_auart,
           g_deleted.
    SELECT sdaufnr INTO g_vbeln FROM viser02
      WHERE equnr = i_equi_no_serv-equnr.
      SELECT SINGLE auart INTO g_auart FROM vbak
        WHERE vbeln = g_vbeln
        AND   gueen > sy-datum
        AND   vbtyp = 'G'.
*active contract found
      IF sy-subrc = '0'.
        DELETE i_equi_no_serv.
        g_deleted = 'X'.
        EXIT.
      ENDIF.
    ENDSELECT.
* serviced last year ? --> delete from internal table
* also select on iphas for performance reasons
    IF g_deleted IS INITIAL.
      REFRESH lt_ord_header.
      SELECT aufnr addat FROM afih
          INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
          UP TO 1 ROWS
          WHERE iphas = c_6
          AND   equnr EQ i_equi_no_serv-equnr
          AND   addat GT p_date.
      IF NOT lt_ord_header[] IS INITIAL.
        DELETE i_equi_no_serv.
      ELSE.
        SELECT aufnr addat FROM afih
          INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
          UP TO 1 ROWS
          WHERE iphas = c_4
          AND   equnr EQ i_equi_no_serv-equnr
          AND   addat GT p_date.
        IF NOT lt_ord_header[] IS INITIAL.
          DELETE i_equi_no_serv.
        ELSE.
          SELECT aufnr addat FROM afih
            INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
            UP TO 1 ROWS
            WHERE iphas = c_3
            AND   equnr EQ i_equi_no_serv-equnr
            AND   addat GT p_date.
          IF NOT lt_ord_header[] IS INITIAL.
            DELETE i_equi_no_serv.
          ELSE.
            SELECT aufnr addat FROM afih
              INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
              UP TO 1 ROWS
              WHERE iphas = c_2
              AND   equnr EQ i_equi_no_serv-equnr
              AND   addat GT p_date.
            IF NOT lt_ord_header[] IS INITIAL.
              DELETE i_equi_no_serv.
            ELSE.
              SELECT aufnr addat FROM afih
                INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
                UP TO 1 ROWS
                WHERE iphas = c_0
                AND   equnr EQ i_equi_no_serv-equnr
                AND   addat GT p_date.
              IF NOT lt_ord_header[] IS INITIAL.
                DELETE i_equi_no_serv.
              ENDIF.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.

  ENDLOOP.

ENDFORM.                    "select_equi_without_service
*&---------------------------------------------------------------------*
*&      Form  add_info_to_equi
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM add_info_to_equi.

* get all extra info for selected equipments
  LOOP AT i_equi_no_serv.
* sold to partner and name
    CONCATENATE 'IE' i_equi_no_serv-equnr INTO g_objnr.
    SELECT SINGLE parnr INTO i_equi_no_serv-parnr
       FROM ihpa WHERE objnr = g_objnr
       AND parvw = 'AG'.
    SELECT SINGLE name1 INTO i_equi_no_serv-name1
       FROM kna1 WHERE kunnr = i_equi_no_serv-parnr.
* system and user status
    CALL FUNCTION 'STATUS_TEXT_EDIT'
               EXPORTING
                    objnr           = g_objnr
                    spras           = sy-langu
                    flg_user_stat   = 'X'
               IMPORTING
                    line            = i_equi_no_serv-sttxt
                    user_line       = i_equi_no_serv-sttxu
               EXCEPTIONS
                    object_not_found.
* last order number
* also select on iphas for performance reasons
    REFRESH lt_ord_header.
    SELECT aufnr addat FROM afih
        INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
        WHERE iphas = c_6
        AND   equnr EQ i_equi_no_serv-equnr
        AND   addat LE p_date.
    SELECT aufnr addat FROM afih
        INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
        WHERE iphas = c_4
        AND   equnr EQ i_equi_no_serv-equnr
        AND   addat LE p_date.
    SELECT aufnr addat FROM afih
        INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
        WHERE iphas = c_3
        AND   equnr EQ i_equi_no_serv-equnr
        AND   addat LE p_date.
    SELECT aufnr addat FROM afih
        INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
        WHERE iphas = c_2
        AND   equnr EQ i_equi_no_serv-equnr
        AND   addat LE p_date.
    SELECT aufnr addat FROM afih
        INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
        WHERE iphas = c_0
        AND   equnr EQ i_equi_no_serv-equnr
        AND   addat LE p_date.
    SORT lt_ord_header BY addat DESCENDING.
    READ TABLE lt_ord_header INDEX 1.
    IF sy-subrc = '0'.
      i_equi_no_serv-aufnr = lt_ord_header-aufnr.
      i_equi_no_serv-gstrp = lt_ord_header-addat.
    ENDIF.
* pgc
    CLEAR g_prdha.
    SELECT SINGLE prdha INTO g_prdha
       FROM mara WHERE matnr = i_equi_no_serv-matnr.
    i_equi_no_serv-pgc = g_prdha+4(4).
* customer and vendor warranty
    CLEAR t_equi_warranties.
    CALL FUNCTION 'ALM_ME_TOB_WARRANTY'
      EXPORTING
        i_objnr           = g_objnr
      TABLES
        t_object_warranty = t_equi_warranties
        return            = return.
    LOOP AT t_equi_warranties INTO wa_equi_warranties.
      IF wa_equi_warranties-warranty_type = '1'.
        i_equi_no_serv-cuswar = wa_equi_warranties-warranty_end.
        CONTINUE.
      ENDIF.
      IF wa_equi_warranties-warranty_type = '2'.
        i_equi_no_serv-venwar = wa_equi_warranties-warranty_end.
        CONTINUE.
      ENDIF.
    ENDLOOP.
* workcenter
    SELECT SINGLE arbpl INTO i_equi_no_serv-arbpl
       FROM crhd WHERE objty = 'A'
       AND objid = i_equi_no_serv-gewrk.
* zero suppression
    WRITE i_equi_no_serv-equnr TO i_equi_no_serv-equnr NO-ZERO.
    WRITE i_equi_no_serv-matnr TO i_equi_no_serv-matnr NO-ZERO.
    WRITE i_equi_no_serv-parnr TO i_equi_no_serv-parnr NO-ZERO.
    WRITE i_equi_no_serv-aufnr TO i_equi_no_serv-aufnr NO-ZERO.
* update
    MODIFY i_equi_no_serv.
  ENDLOOP.
ENDFORM.                    "add_info_to_equi

*&---------------------------------------------------------------------*
*&      Form  select_equi_one_2_one
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM select_equi_one_2_one.

* select all the equipments for the selection
  IF p_vkbur IS INITIAL.
    SELECT equnr matnr FROM v_equi INTO TABLE gt_equ
      WHERE eqart = c_equipment
      AND   datbi = c99991231
      AND   swerk = p_werks
      AND   vkorg = p_vkorg
      AND   vtweg = p_vtweg
      AND   spart = p_spart
      AND   spras = sy-langu
    %_HINTS DB6 'USE_OPTLEVEL 0'.                    "+x0001
  ELSE.
    SELECT equnr matnr FROM v_equi INTO TABLE gt_equ
          WHERE eqart = c_equipment
          AND   datbi = c99991231
          AND   swerk = p_werks
          AND   vkorg = p_vkorg
          AND   vtweg = p_vtweg
          AND   vkbur in p_vkbur                    "+x0002
          AND   vkgrp in p_vkgrp                    "+x0002
          AND   spart = p_spart
          AND   spras = sy-langu
    %_HINTS DB6 'USE_OPTLEVEL 0'.                    "+x0001
  ENDIF.
* check status inclusive / exlusive
  LOOP AT gt_equ.
    IF NOT ( g_stai1_lines IS INITIAL
        AND g_stae1_lines IS INITIAL ) .
      PERFORM check_statusses USING gt_equ-equnr.
      IF NOT g_stai1_lines IS INITIAL AND g_incl = ' '.
        DELETE gt_equ.
        CONTINUE.
      ELSEIF NOT g_stae1_lines IS INITIAL AND g_excl = 'X'.
        DELETE gt_equ.
        CONTINUE.
      ENDIF.
    ENDIF.
* check equipment in active contract
    CLEAR: g_vbeln,
            g_matnr,
            g_auart.
    g_actcont = 'N'.
    SELECT sdaufnr  INTO g_vbeln
    FROM viser02 WHERE equnr = gt_equ-equnr.
      SELECT SINGLE auart INTO g_auart FROM vbak
        WHERE vbeln = g_vbeln
        AND   gueen > sy-datum
        AND   vbtyp = 'G'.
      IF sy-subrc = '0'.
        g_actcont = 'Y'.
        EXIT.
      ENDIF.
    ENDSELECT.
* get PGC
    CLEAR: g_pgc,
           g_prdha.
    SELECT SINGLE prdha INTO g_prdha
      FROM mara WHERE matnr = gt_equ-matnr.
    IF sy-subrc = 0.
      g_pgc = g_prdha+4(4).
    ENDIF.
* service since the one2one-date
* also select on iphas for performance reasons
    g_service = 'N'.
    REFRESH lt_ord_header.
    SELECT aufnr addat FROM afih
        INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
        UP TO 1 ROWS
        WHERE iphas = c_6
        AND   equnr EQ gt_equ-equnr
        AND   addat GT p_date.
    IF NOT lt_ord_header[] IS INITIAL.
      g_service = 'Y'.
    ELSE.
      SELECT aufnr addat FROM afih
        INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
        UP TO 1 ROWS
        WHERE iphas = c_4
        AND   equnr EQ gt_equ-equnr
        AND   addat GT p_date.
      IF NOT lt_ord_header[] IS INITIAL.
        g_service = 'Y'.
      ELSE.
        SELECT aufnr addat FROM afih
          INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
          UP TO 1 ROWS
          WHERE iphas = c_3
          AND   equnr EQ gt_equ-equnr
          AND   addat GT p_date.
        IF NOT lt_ord_header[] IS INITIAL.
          g_service = 'Y'.
        ELSE.
          SELECT aufnr addat FROM afih
          INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
          UP TO 1 ROWS
          WHERE iphas = c_2
          AND   equnr EQ gt_equ-equnr
          AND   addat GT p_date.
          IF NOT lt_ord_header[] IS INITIAL.
            g_service = 'Y'.
          ELSE.
            SELECT aufnr addat FROM afih
              INTO CORRESPONDING FIELDS OF TABLE lt_ord_header
              UP TO 1 ROWS
              WHERE iphas = c_0
              AND   equnr EQ gt_equ-equnr
              AND   addat GT p_date.
            IF NOT lt_ord_header[] IS INITIAL.
              g_service = 'Y'.
            ENDIF.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.
* update internal tables
* update table  gt_tot_equi (total populations)
* record without pgc
    CLEAR i_tot_equi.
    i_tot_equi-number = 1.
    IF g_service = 'Y' OR g_actcont = 'Y'.
      i_tot_equi-service = 1.
    ENDIF.
    COLLECT i_tot_equi.
* record with PGC
    IF NOT g_pgc IS INITIAL.
      i_tot_equi-pgc = g_pgc.
      COLLECT i_tot_equi.
    ENDIF.
* update table i_cont_equi (totals per contract type)
    IF g_actcont = 'Y'.
* record without pgc
      CLEAR i_cont_equi.
      i_cont_equi-auart = g_auart.
      IF g_actcont = 'Y'.
        i_cont_equi-service = 1.
      ENDIF.
      COLLECT i_cont_equi.
* record with PGC
      IF NOT g_pgc IS INITIAL.
        i_cont_equi-pgc = g_pgc.
        COLLECT i_cont_equi.
      ENDIF.
    ENDIF.

* update table i_tot_cont_equi
    IF g_actcont = 'Y' .
      CLEAR i_tot_cont_equi.
      i_tot_cont_equi-service = 1.
      COLLECT i_tot_cont_equi.
* record with PGC
      IF NOT g_pgc IS INITIAL.
        i_tot_cont_equi-pgc = g_pgc.
        COLLECT i_tot_cont_equi.
      ENDIF.
    ENDIF.

* update table i_no_cont_equi
    IF g_actcont = 'N' AND g_service = 'Y'.
      CLEAR i_no_cont_equi.
      i_no_cont_equi-service = 1.
      COLLECT i_no_cont_equi.
* record with PGC
      IF NOT g_pgc IS INITIAL.
        i_no_cont_equi-pgc = g_pgc.
        COLLECT i_no_cont_equi.
      ENDIF.
    ENDIF.

  ENDLOOP.

ENDFORM.                    "select_equi_one_2_one
*&---------------------------------------------------------------------*
*&      Form  build_field_catlog
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->PT_FIELDCAT  text
*----------------------------------------------------------------------*
FORM build_field_catlog  CHANGING pt_fieldcat TYPE slis_t_fieldcat_alv.

  DATA : ls_fcat TYPE slis_fieldcat_alv.
*---------------------Plant-----------------------*
  ls_fcat-fieldname = 'EQUNR'.
  ls_fcat-rollname = 'EQUNR'.
  ls_fcat-outputlen = '12'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-------------Serial number ---------------*
  ls_fcat-fieldname = 'SERNR'.
  ls_fcat-seltext_l = 'Serial Number'.
  ls_fcat-outputlen = '15'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------------Material--------------------*
  ls_fcat-fieldname = 'MATNR'.
  ls_fcat-rollname = 'MATNR'.
  ls_fcat-outputlen = '11'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------------Equipment description-----------*
  ls_fcat-fieldname = 'EQKTX'.
  ls_fcat-seltext_l = 'Equipment Description'.
  ls_fcat-outputlen = '25'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-------------Functional location-------------*
  ls_fcat-fieldname = 'TPLNR'.
  ls_fcat-rollname = 'TPLNR'.
  ls_fcat-outputlen = '20'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------Customer ----------*
  ls_fcat-fieldname = 'PARNR'.
  ls_fcat-seltext_l = 'Customer Nr'.
  ls_fcat-outputlen = '12'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------Customer Name ----------*
  ls_fcat-fieldname = 'NAME1'.
  ls_fcat-seltext_l = 'Customer Name'.
  ls_fcat-outputlen = '40'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------Created on-----*
  ls_fcat-fieldname = 'ERDAT'.
  ls_fcat-seltext_l = 'Created On'.
  ls_fcat-outputlen = '10'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------System status----------*
  ls_fcat-fieldname = 'STTXT'.
  ls_fcat-seltext_l = 'System Status'.
  ls_fcat-outputlen = '40'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*------------Customer status----------*
  ls_fcat-fieldname = 'STTXU'.
  ls_fcat-seltext_l = 'User Status'.
  ls_fcat-outputlen = '40'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-----------Last service order ----------*
  ls_fcat-fieldname = 'AUFNR'.
  ls_fcat-seltext_l = 'Order Nr'.
  ls_fcat-outputlen = '10'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*-------Date last service order ----------*
  ls_fcat-fieldname = 'GSTRP'.
  ls_fcat-seltext_l = 'Order Date'.
  ls_fcat-outputlen = '10'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*--------------------------PGC--------------------*
  ls_fcat-fieldname = 'PGC'.
  ls_fcat-seltext_l = 'PGC'.
  ls_fcat-outputlen = '4'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------- Customer Warranty  ----------*
  ls_fcat-fieldname = 'CUSWAR'.
  ls_fcat-seltext_l = 'Cust Warr.'.
  ls_fcat-outputlen = '10'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------- Vendor Warranty  ----------*
  ls_fcat-fieldname = 'VENWAR'.
  ls_fcat-seltext_l = 'Vendor Warr.'.
  ls_fcat-outputlen = '10'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------- Sales Office  ----------*
  ls_fcat-fieldname = 'VKBUR'.
  ls_fcat-seltext_l = 'Sales Off'.
  ls_fcat-outputlen = '9'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------- Sales Group  ----------*
  ls_fcat-fieldname = 'VKGRP'.
  ls_fcat-seltext_l = 'Sales Gr'.
  ls_fcat-outputlen = '8'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------- Workcenter ----------*
  ls_fcat-fieldname = 'ARBPL'.
  ls_fcat-seltext_l = 'Workcenter'.
  ls_fcat-outputlen = '10'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.

ENDFORM.                    " build_field_catlog

*&---------------------------------------------------------------------*
*&      Form  fill_events_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM fill_events_f14 .

  DATA h_event       TYPE slis_alv_event.

  CALL FUNCTION 'REUSE_ALV_EVENTS_GET'
    EXPORTING
      i_list_type = 0
    IMPORTING
      et_events   = g_events_tab.

*--- allocate form for user-command ---------------------------------*
  READ TABLE g_events_tab WITH KEY name = slis_ev_user_command
                        INTO h_event.
  IF sy-subrc = 0.
    MOVE g_form_user_command TO h_event-form.
    MODIFY g_events_tab FROM h_event INDEX sy-tabix.
  ENDIF.

ENDFORM.                    " fill_events_f14

*&--------------------------------------------------------------------*
*&      Form  user_command_l
*&--------------------------------------------------------------------*
*       text
*---------------------------------------------------------------------*
*      -->P_UCOMM    text
*      -->P_SELFIELD text
*---------------------------------------------------------------------*
FORM user_command_l USING p_ucomm LIKE sy-ucomm
                          p_selfield TYPE slis_selfield.

  p_selfield-refresh = c_s.
  PERFORM check_pf2_with_object_f16 USING p_ucomm.
  PERFORM set_p_selfield_general_f16 USING p_selfield.

  CASE p_ucomm.
    WHEN 'ISEL'.
      p_ucomm = 'DISP'.
      PERFORM fcodes_with_mark_f16 USING p_ucomm p_selfield.
  ENDCASE.

ENDFORM.                    "user_command_l

*&---------------------------------------------------------------------*
*&      Form  check_pf2_with_object_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*----------------------------------------------------------------------*
FORM check_pf2_with_object_f16  USING    p_ucomm.

  CHECK p_ucomm = g_ic1.
  p_ucomm = 'ISEL'.

ENDFORM.                    " check_pf2_with_object_f16

*&---------------------------------------------------------------------*
*&      Form  set_p_selfield_general_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM set_p_selfield_general_f16  USING f_selfield TYPE slis_selfield.

  f_selfield-col_stable = c_x.
  f_selfield-row_stable = c_x.

ENDFORM.                    " set_p_selfield_general_f16

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_f16
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*      -->P_ENDCASE  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_f16  USING p_ucomm LIKE sy-ucomm
                                p_selfield TYPE slis_selfield.

  PERFORM check_object_tab_marked_f14 USING p_ucomm p_selfield.

  LOOP AT i_equi_no_serv WHERE selected = c_x .
    l_index = sy-tabix.
    PERFORM fcodes_with_mark_l USING p_ucomm p_selfield.
    i_equi_no_serv-selected = ' '.
    MODIFY i_equi_no_serv INDEX l_index.
  ENDLOOP.

  CLEAR p_ucomm.

ENDFORM.                    " fcodes_with_mark_f16

*&---------------------------------------------------------------------*
*&      Form  check_object_tab_marked_f14
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_P_UCOMM  text
*      -->P_P_SELFIELD  text
*----------------------------------------------------------------------*
FORM check_object_tab_marked_f14  USING    p_ucomm LIKE sy-ucomm
                                        p_selfield TYPE slis_selfield.

  READ TABLE i_equi_no_serv WITH KEY selected = c_x.

  IF NOT sy-subrc IS INITIAL.
    IF NOT p_selfield-tabindex IS INITIAL.
      READ TABLE i_equi_no_serv  INDEX p_selfield-tabindex.
      i_equi_no_serv-selected = c_x.
      MODIFY i_equi_no_serv INDEX p_selfield-tabindex.
    ENDIF.
  ELSE.
*--- Checkbox markiert -----------------------------------------------*
    p_selfield-sel_tab_field = 'G_MARK'.
  ENDIF.

ENDFORM.                    " check_object_tab_marked_f14

*&---------------------------------------------------------------------*
*&      Form  fcodes_with_mark_l
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->P_F_UCOMM  text
*      -->P_F_SELFIELD  text
*----------------------------------------------------------------------*
FORM fcodes_with_mark_l  USING   p_ucomm LIKE sy-ucomm
                              p_selfield TYPE slis_selfield.

  DATA: h_ucomm LIKE sy-ucomm.

  CASE p_ucomm.
*   Display equipment
    WHEN 'DISP'.
      SET PARAMETER ID 'EQN' FIELD  i_equi_no_serv-equnr.
      CALL TRANSACTION 'IE03' AND SKIP FIRST SCREEN.
  ENDCASE.

ENDFORM.                    " fcodes_with_mark_l

*&---------------------------------------------------------------------*
*&      Form  display_alv
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM display_alv .

  g_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
     EXPORTING
       i_callback_program                =  g_repid
       i_save                            = 'A'
       it_events                         =  g_events_tab[]
*      I_GRID_TITLE                      =
*      I_GRID_SETTINGS                   =
*      is_layout                         =  g_layout
       it_fieldcat                       =  gt_fieldcat[]
     TABLES
        t_outtab                         =  i_equi_no_serv.

ENDFORM.                    "display_alv
*&---------------------------------------------------------------------*
*&      Form  get_t179
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM get_t179t.

  SELECT *
           FROM t179t
           INTO TABLE it_t179t
          WHERE spras = sy-langu.

ENDFORM.                                                    "get_t179


*&---------------------------------------------------------------------*
*&      Form  get_pgc_desc
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->G_PGC      text
*----------------------------------------------------------------------*
FORM get_pgc_desc USING g_pgc.

  CLEAR g_pgc_desc.

  IF NOT g_pgc IS INITIAL.
    LOOP AT it_t179t INTO wa_t179t WHERE prodh+4(4) = g_pgc.
      g_pgc_desc = wa_t179t-vtext.
      EXIT.
    ENDLOOP.
  ENDIF.

ENDFORM.                    "get_pgc_desc
*&---------------------------------------------------------------------*
*&      Form  fill_i_one2one
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*  -->  p1        text
*  <--  p2        text
*----------------------------------------------------------------------*
FORM fill_i_one2one.

  REFRESH i_121.
* total number of equipments
  SORT i_tot_equi BY pgc.
  LOOP AT i_tot_equi.
    i_121-number = i_tot_equi-number.
    i_121-service = i_tot_equi-service.
    i_121-ratio = i_tot_equi-service / i_tot_equi-number * 100 .
    IF i_tot_equi-pgc IS INITIAL.
      i_121-text = 'Total equipments'.
      APPEND i_121.
      CLEAR i_121.
    ELSE.
      i_121-pgcdesc+0(4) = i_tot_equi-pgc.
      PERFORM get_pgc_desc USING i_tot_equi-pgc.
      i_121-pgcdesc+5(40) = g_pgc_desc.
      APPEND i_121.
      CLEAR i_121.
    ENDIF.
  ENDLOOP.
* list the contract types
  SORT i_cont_equi BY auart pgc.
  LOOP AT i_cont_equi.
    i_121-service = i_cont_equi-service.
    CLEAR g_number.
    READ TABLE i_tot_equi WITH KEY i_cont_equi-pgc.
    IF sy-subrc = '0'.
      i_121-number = i_tot_equi-number.
      i_121-ratio = i_cont_equi-service / i_tot_equi-number * 100 .
    ENDIF.
    IF i_cont_equi-pgc IS INITIAL.
      i_121-text = i_cont_equi-auart .
      APPEND i_121.
      CLEAR i_121.
    ELSE.
      i_121-pgcdesc+0(4) = i_cont_equi-pgc.
      PERFORM get_pgc_desc USING i_cont_equi-pgc.
      i_121-pgcdesc+5(40) = g_pgc_desc.
      APPEND i_121.
      CLEAR i_121.
    ENDIF.
  ENDLOOP.
* contract total
  SORT i_tot_cont_equi BY pgc.
  LOOP AT i_tot_cont_equi.
    i_121-service = i_tot_cont_equi-service.
    CLEAR g_number.
    READ TABLE i_tot_equi WITH KEY i_tot_cont_equi-pgc.
    IF sy-subrc = '0'.
      i_121-number = i_tot_equi-number.
      i_121-ratio = i_tot_cont_equi-service / i_tot_equi-number * 100 .
    ENDIF.
    IF i_tot_cont_equi-pgc IS INITIAL.
      i_121-text = 'Contract total'.
      APPEND i_121.
      CLEAR i_121.
    ELSE.
      i_121-pgcdesc+0(4) = i_tot_cont_equi-pgc.
      PERFORM get_pgc_desc USING i_tot_cont_equi-pgc.
      i_121-pgcdesc+5(40) = g_pgc_desc.
      APPEND i_121.
      CLEAR i_121.
    ENDIF.
  ENDLOOP.
* service without contract
  SORT i_no_cont_equi BY pgc.
  LOOP AT i_no_cont_equi.
    i_121-service = i_no_cont_equi-service.
    CLEAR g_number.
    READ TABLE i_tot_equi WITH KEY i_no_cont_equi-pgc.
    IF sy-subrc = '0'.
      i_121-number = i_tot_equi-number.
      i_121-ratio = i_no_cont_equi-service / i_tot_equi-number * 100 .
    ENDIF.
    IF i_no_cont_equi-pgc IS INITIAL.
      i_121-text = 'Service without contract'.
      APPEND i_121.
      CLEAR i_121.
    ELSE.
      i_121-pgcdesc+0(4) = i_no_cont_equi-pgc.
      PERFORM get_pgc_desc USING i_no_cont_equi-pgc.
      i_121-pgcdesc+5(40) = g_pgc_desc.
      APPEND i_121.
      CLEAR i_121.
    ENDIF.
  ENDLOOP.

ENDFORM.                    " fill_i_one2one
*&---------------------------------------------------------------------*
*&      Form  bld_fld_catlog_121
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
*      -->PT_FIELDCAT  text
*----------------------------------------------------------------------*
FORM bld_fld_catlog_121  CHANGING pt_fieldcat TYPE slis_t_fieldcat_alv.

  DATA : ls_fcat TYPE slis_fieldcat_alv.


*---------- TEXT ----------*
  ls_fcat-fieldname = 'TEXT'.
  ls_fcat-seltext_l = 'Description'.
  ls_fcat-outputlen = '30'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------- PGC Desc ----------*
  ls_fcat-fieldname = 'PGCDESC'.
  ls_fcat-seltext_l = 'PGC + Description'.
  ls_fcat-outputlen = '45'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------- number ----------*
  ls_fcat-fieldname = 'NUMBER'.
  ls_fcat-seltext_l = 'Number'.
  ls_fcat-outputlen = '10'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------- Service ----------*
  ls_fcat-fieldname = 'SERVICE'.
  ls_fcat-seltext_l = 'Service'.
  ls_fcat-outputlen = '10'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.
*---------- Ratio ----------*
  ls_fcat-fieldname = 'RATIO'.
  ls_fcat-seltext_l = 'One-2-One %'.
  ls_fcat-outputlen = '12'.
  APPEND ls_fcat TO pt_fieldcat.
  CLEAR ls_fcat.

ENDFORM.                    " build_field_catlog
*&---------------------------------------------------------------------*
*&      Form  display_alv_121
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM display_alv_121 .

  g_repid = sy-repid.

  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
     EXPORTING
       i_callback_program                =  g_repid
       i_save                            = 'A'
       it_events                         =  g_events_tab[]
       i_callback_top_of_page            = 'TOP_OF_PAGE'
*      I_GRID_TITLE                      = 'YAM: One-2-One Ratio'
*      I_GRID_SETTINGS                   =
*      is_layout                         =  g_layout
       it_fieldcat                       =  gt_fieldcat[]
*      is_sel_hide                       = i_selinfo

     TABLES
        t_outtab                         =  i_121.

ENDFORM.                    "display_alv_121
*&--------------------------------------------------------------------*
*&      Form  POPULATE_EVENT
*&--------------------------------------------------------------------*
*      Events populated for TOP OF PAGE & USER COMAND
*---------------------------------------------------------------------*
FORM populate_event.
  READ TABLE v_events INTO wa_event WITH KEY name = 'TOP_OF_PAGE'.
  IF sy-subrc EQ 0.
    wa_event-form = 'TOP_OF_PAGE'.

    MODIFY v_events FROM wa_event TRANSPORTING form WHERE name =
 wa_event-form.
  ENDIF.

ENDFORM.                    "POPULATE_EVENT
*&---------------------------------------------------------------------*
*&      Form  TOP_OF_PAGE
*&---------------------------------------------------------------------*
*       text
*----------------------------------------------------------------------*
FORM top_of_page.

*ALV Header declarations
  DATA: t_header TYPE slis_t_listheader,
        wa_header TYPE slis_listheader,
        t_line TYPE string,
        ld_lines TYPE i,
        ld_linesc(10) TYPE c.

* Title
  wa_header-typ  = 'H'.
  wa_header-info = 'YAM: One-2-One Ratio or Equipment without service'.
  APPEND wa_header TO t_header.
  CLEAR wa_header.

* Date
  wa_header-typ  = 'S'.
  wa_header-key = 'Date : '.
  CONCATENATE  sy-datum+6(2) '.'
               sy-datum+4(2) '.'
               sy-datum(4) INTO wa_header-info.   "todays date
  APPEND wa_header TO t_header.
  CLEAR: wa_header.

* Run By
  wa_header-typ  = 'S'.
  wa_header-key = 'Run By'.
  CONCATENATE sy-uname ' ' INTO wa_header-info.  "user name

  APPEND wa_header TO t_header.
  CLEAR: wa_header.

* plant
  IF p_werks IS NOT INITIAL.
      t_line = p_werks.
      wa_header-key = 'Plant'.
      wa_header-typ  = 'S'.
      wa_header-info =  t_line.
      APPEND wa_header TO t_header.
      CLEAR: wa_header, t_line.
  ENDIF.

* sales org
  IF p_vkorg IS NOT INITIAL.
    t_line = p_vkorg.
    wa_header-key = 'Sales Organisation'  .
    wa_header-typ = 'S'.
    wa_header-info = t_line.
    APPEND wa_header TO t_header.
    CLEAR: wa_header, t_line.
  ENDIF.

* distribution channel
  IF p_vtweg IS NOT INITIAL.
    t_line = p_vtweg.
    wa_header-key = 'Distribution Channel'  .
    wa_header-typ = 'S'.
    wa_header-info = t_line.
    APPEND wa_header TO t_header.
    CLEAR: wa_header, t_line.
  ENDIF.

* division
  IF p_spart IS NOT INITIAL.
    t_line = p_spart.
    wa_header-key = 'Division'  .
    wa_header-typ = 'S'.
    wa_header-info = t_line.
    APPEND wa_header TO t_header.
    CLEAR: wa_header, t_line.
  ENDIF.

* sales office
  IF p_vkbur IS NOT INITIAL.
      t_line = p_vkbur.
      wa_header-key = 'Sales Office'.
      wa_header-typ  = 'S'.
      wa_header-info =  t_line.
      APPEND wa_header TO t_header.
      CLEAR: wa_header, t_line.
  ENDIF.

* sales group
  IF p_vkgrp IS NOT INITIAL.
      t_line = p_vkgrp.
      wa_header-key = 'Sales Group '.
      wa_header-typ  = 'S'.
      wa_header-info =  t_line.
      APPEND wa_header TO t_header.
      CLEAR: wa_header, t_line.
  ENDIF.

* status incl
  IF so_stai1[] IS NOT INITIAL.
    LOOP AT so_stai1.
      CONCATENATE  so_stai1-low '..to..' so_stai1-high INTO t_line.
      wa_header-key = 'Equipment Status Included '.
      wa_header-typ  = 'S'.
      wa_header-info =  t_line.
      APPEND wa_header TO t_header.
      CLEAR: wa_header, t_line.
    ENDLOOP.
  ENDIF.

* status excl
  IF so_stae1[] IS NOT INITIAL.
    LOOP AT so_stae1.
      CONCATENATE  so_stae1-low '..to..' so_stae1-high INTO t_line.
      wa_header-key = 'Equipment Status Exlcuded '.
      wa_header-typ  = 'S'.
      wa_header-info =  t_line.
      APPEND wa_header TO t_header.
      CLEAR: wa_header, t_line.
    ENDLOOP.
  ENDIF.

* one to one date
  IF p_date IS NOT INITIAL.
    CONCATENATE  p_date+6(2) '.'
                 p_date+4(2) '.'
                 p_date(4) INTO wa_header-info.
    wa_header-key = 'One_2_One Date '.
    wa_header-typ  = 'S'.
    APPEND wa_header TO t_header.
    CLEAR: wa_header, t_line.
  ENDIF.

  CALL FUNCTION 'REUSE_ALV_COMMENTARY_WRITE'
    EXPORTING
      it_list_commentary = t_header.

ENDFORM.                    "TOP_OF_PAGE

*Text symbol text£º
*001:Selection Screen Input
*E01:You have no authorisation for sales organisation :
*E02:Fill in Sales Office and Sales Group
*I01:No equipments selected for One-2-One

*I02:No equipments without service selected
*Selection text£º
*P_1TWO1:        One-2-One
*P_DATE:        One-2-One date
*P_EQNOS:        Equipment without Service
*P_SPART:        Division
*P_VKBUR:        Sales Office
*P_VKGRP:        Sales Group
*P_VKORG:        Sales Organisation
*P_VTWEG:        Distribution Channel
*P_WERKS:D       .
*SO_STAE1:        Status Exclusive
*SO_STAI1:        Status Inclusive
